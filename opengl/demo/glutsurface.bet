ORIGIN '~beta/opengl/glut';
INCLUDE '~beta/sysutils/cstring';
INCLUDE '~beta/containers/list';

-- LIB: Attributes --
GetTickCount: external
  (** The GetTickCount function should be in the time library.
   * Only works on windows
   *)
  (# milli: @integer;
  do callStd
  exit milli
  #);




GLUTSurface:
  (# windowID: @integer;
     
     timers: @list
       (# Element:: Timer;
          check:
            (# now: @int32u;
            enter now
            do scan
               (#
               do (if (current.before + current.interval) < now then
                      now -> current.fire;
                  if);
               #);
            #);
       #);
     Timer:
       (# once:< BooleanValue;
          action:<
            (#
            do INNER;
            #);
          start:
            (#
            enter interval
            do GetTickCount -> before;
               THIS(Timer)[] -> timers.append;
            #);
          stop:
            (#
            do THIS(Timer)[] -> timers.at -> timers.delete;
            #);
          fire:
            (# now: @integer;
            enter now
            do action;
               now -> before;
               (if once then
                   stop;
               if);
            #);
          interval: @integer;
          before: @int32u;
       #);
     
     reshape: reshapeFunc
       (#
       do (0, 0, w, h) -> glViewport;
          (w, h) -> onUpdateCooordinateSystem;
       #);
     display: displayFunc
       (#
       do onDisplay;
          glutSwapBuffers;
       #);
     idle: idleFunc
       (# now: @int32u;
       do GetTickCount -> now;
          now -> timers.check;
       #);
     mouse: mouseFunc
       (#
       do (if state
           //GLUT_UP then
              (1, button + 1) -> onMouseUp;
           //GLUT_DOWN then
              (1, button + 1) -> onMouseDown;
          if);
       #);
     motion: motionFunc
       (# 
       do (1, x, y) -> onMousePosition;
       #);
     keyboard: keyboardFunc
       (#
       do (key, false, false) -> onKeyDown;
       #);
     
     onMouseDown:<
       (# ID: @integer;
          button: @integer;
          
       enter (ID, button)
       do INNER;
       #);
     onMousePosition:<
       (# ID: @integer;
          x, y: @integer;
       enter (ID, x, y)
       do INNER;
       #);
     onMouseUp:<
       (# ID: @integer;
          button: @integer;
       enter (ID, button)
       do INNER;
       #);
     onKeyDown:<
       (# ch: @char;
          controlKey: @boolean;
          shiftKey: @boolean;
       enter (ch, controlKey, shiftKey)
       do INNER;
       #);
     run:
       (#
       do glutMainLoop;
       #);
     init:<
       (# argc: @integer;
          argv: @cstring;
          
       do 1 -> argc;
          'main'  -> argv.set;
          (@@argc, @@argv.charptr) -> glutInit;
          GLUT_RGBA %bor GLUT_DOUBLE -> glutInitDisplayMode;
          'main' -> glutCreateWindow -> windowID;
          (640, 480) -> glutReshapeWindow;
          display## -> glutDisplayFunc;
          reshape## -> glutReshapeFunc;
          idle## -> glutIdleFunc;
          mouse## -> glutMouseFunc;
          motion## -> glutMotionFunc;
          motion## -> glutPassiveMotionFunc;
          keyboard## -> glutKeyboardFunc;
          INNER;
       #);
     onDisplay:<
       (#
       do INNER;
       #);
     onUpdateCooordinateSystem:<
       (# width, height: @integer;
       enter (width, height)
       do INNER;
       #);
  #);
