ORIGIN '~beta/opengl/glut';
INCLUDE '~beta/sysutils/cstring';

-- LIB: Attributes --
GetTickCount: external
  (** The GetTickCount function should be in the time library.
   * Only works on window
   *)
  (# milli: @integer;
  do callStd
  exit milli
  #);

GLUTSurface:
  (# windowID: @integer;
     
     ellapsed: @int32u;
     before: @int32u;
     reshape: reshapeFunc
       (#
       do (0, 0, w, h) -> glViewport;
          (w, h) -> onUpdateCooordinateSystem;
       #);
     display: displayFunc
       (#
       do onDisplay;
          glutSwapBuffers;
       #);
     idle: idleFunc
       (# now: @int32u;
          delta: @int32u;
       do GetTickCount -> now;
          (if before = 0 then
              now -> before;
           else
              now - before -> delta;
              (if ellapsed = 0 then
                  delta -> ellapsed;
               else
                  (ellapsed + delta) div 2 -> ellapsed;
              if);
              now -> before;
          if);
       #);
     
     run:
       (#
       do glutMainLoop;
       #);
     init:<
       (# argc: @integer;
          argv: @cstring;
          
       do 1 -> argc;
          'main'  -> argv.set;
          (@@argc, @@argv.charptr) -> glutInit;
          GLUT_RGBA %bor GLUT_DOUBLE -> glutInitDisplayMode;
          'main' -> glutCreateWindow -> windowID;
          (640, 480) -> glutReshapeWindow;
          display## -> glutDisplayFunc;
          reshape## -> glutReshapeFunc;
          idle## -> glutIdleFunc;
          INNER;
       #);
     onDisplay:<
       (#
       do 'ellapsed: ' -> puttext;
          ellapsed -> putint;
          newline;
          (*** 0 -> ellapsed; ***)
          INNER;
       #);
     onUpdateCooordinateSystem:<
       (# width, height: @integer;
       enter (width, height)
       do INNER;
       #);
  #);
