ORIGIN '~beta/opengl/glut';
INCLUDE '~beta/sysutils/cstring';
INCLUDE '~beta/containers/list';
INCLUDE 'abstractsurface';

-- LIB: Attributes --




GLUTSurface: Surface
  (# timers: @list
       (# Element:: Timer;
          check:
            (# now: @int32u;
            enter now
            do scan
               (#
               do (if (current.before + current.interval) < now then
                      now -> current.fire;
                  if);
               #);
            #);
       #);
     
     registerTimer::
       (#
       do theTimer[] -> timers.append;
       #);
     deregisterTimer::
       (#
       do theTimer[] -> timers.at -> timers.delete;
       #);
     
     reshape: reshapeFunc
       (#
       do (0, 0, w, h) -> glViewport;
          (w, h) -> onUpdateCooordinateSystem;
       #);
     display: displayFunc
       (#
       do onDisplay;
          glutSwapBuffers;
       #);
     idle: idleFunc
       (# now: @int32u;
       do GetTickCount -> now;
          now -> timers.check;
       #);
     mouse: mouseFunc
       (#
       do (if state
           //GLUT_UP then
              (1, button + 1) -> onMouseUp;
           //GLUT_DOWN then
              (1, button + 1) -> onMouseDown;
          if);
       #);
     motion: motionFunc
       (# 
       do (1, x, y) -> onMousePosition;
       #);
     keyboard: keyboardFunc
       (#
       do (key, false, false) -> onKeyDown;
       #);
     
     
     run:
       (#
       do glutMainLoop;
       #);
     init:<
       (# argc: @integer;
          argv: @cstring;
          
       do 1 -> argc;
          'main'  -> argv.set;
          (@@argc, @@argv.charptr) -> glutInit;
          GLUT_RGBA %bor GLUT_DOUBLE -> glutInitDisplayMode;
          'main' -> glutCreateWindow -> systemID;
          (640, 480) -> glutReshapeWindow;
          display## -> glutDisplayFunc;
          reshape## -> glutReshapeFunc;
          idle## -> glutIdleFunc;
          mouse## -> glutMouseFunc;
          motion## -> glutMotionFunc;
          motion## -> glutPassiveMotionFunc;
          keyboard## -> glutKeyboardFunc;
          INNER;
       #);
     
  #);
