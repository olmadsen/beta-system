ORIGIN 'glutsurface';

-- program: descriptor --
(# theSurface: @GLUTSurface
     (# width, height: @integer;
        updater: @timer
          (# count: @integer;
             action::
               (#
               do count + 1 -> count;
                  'update: ' -> puttext;
                  count -> putint;
                  newline;
               #);
          #);
        checker: @timer
          (# count: @integer;
             action::
               (#
               do count + 1 -> count;
                  'checker: ' -> puttext;
                  count -> putint;
                  newline;
               #);
          #);
        signal1: @timer
          (# once:: (# do true -> value #);
             action::
               (#
               do 'signal 1' -> putline;
                  3000 -> signal2.start;
                  updater.stop;
               #);
          #);
        signal2: @timer
          (# once:: (# do true -> value #);
             action::
               (#
               do 'signal 2' -> putline;
                  2000 -> updater.start;
               #);
          #);
        init::
          (#
          do GLUT_CURSOR_LEFT_ARROW -> glutSetCursor;
             1000 -> updater.start;
             300 -> checker.start;
             3000 -> signal1.start;
          #);
        onDisplay::
          (#
          do GL_PROJECTION -> glMatrixMode;
             glLoadIdentity;
             (0, width, height, 0, -10, 10) -> glOrtho;
             GL_MODELVIEW -> glMatrixMode;
             glLoadIdentity;
             (0.1, 0.2, 0.5, 1) -> glClearColor;
             GL_COLOR_BUFFER_BIT -> glClear;
             (0.0, 0.7, 0.5) -> glColor3d;
             (13, 13, width - 13, height - 13) -> glRectd;
          #);
        onUpdateCooordinateSystem::
          (#
          do width -> theSurface.width;
             height -> theSurface.height;
          #);
        onMouseDown::
          (#
          do 'down: ' -> putline;
          #);
        onMousePosition::
          (#
          do 'position: ' -> puttext;
             x -> putint;
             ' ' -> put;
             y -> putint;
             newline;
          #);
        onMouseUp::
          (#
          do 'up: ' -> putline;
          #);
        onKeyDown::
          (#
          do 'ch: [' -> puttext;
             ch -> putint;
             '[ "' -> puttext;
             ch -> put;
             '"' -> putline;
          #);
     #);
do theSurface.init;
   theSurface.run;
#)
