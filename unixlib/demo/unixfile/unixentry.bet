ORIGIN '~beta/unixlib/v1.5/unixfile';
INCLUDE '~beta/sysutils/v1.5/time';
---- PROGRAM: descriptor ----

(* An example of using UnixEntry: First the user is asked if symbolic links should
 * be followed or not. If the UnixEntry specified on the command
 * line exists, it is then various other unix specific attributes are examined.
 * It is examined whether the entry is a symbolic link or not, only if links should
 * not be followed.
 * Finally it is attempted to change the owner of it to 675.
 *)

(# e: @unixentry; follow: @boolean;
do (if noOfArguments <> 2 //true then
       'Usage: ' -> puttext; 1->arguments->puttext; ' path' -> putline;
       stop;
   if);
   2 -> arguments -> e.path;
   
   'Examine symbolic links themselves? (y/n) ' -> puttext;
   (Keyboard.get='n') -> e.followlinks;
   
   e.path -> puttext;
   (if e.exists
    // true then
       ' exists. It' -> putline;
       (if e.followlinks // false then
           (if e.isSymbolicLink // true then
               '  is a symbolic link' -> putline;;
           if);    
       if);
       '  was last modified ' -> puttext;
       e.modtime -> formattime -> putline;
       '  has inode: '->puttext; e.inode -> putint; newline;
       '  has owner id: '->puttext; e.owner -> putint; newline;
       '  the owner '-> puttext;
       (if ('u','w') -> e.permission.has
        // true then 'has write permission to the disk entry' -> putline;
        // false then 'does not have write permission to the disk entry' 
           -> putline;
       if);
       'Now lets try to change the owner id to 675.' -> puttext;
       675 -> e.owner;
       ' That succeeded!' -> putline;   
    // false then
       ' does not exist.' -> putline;
   if);
#)
