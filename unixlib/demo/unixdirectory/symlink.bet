ORIGIN '~beta/unixlib/v1.5/unixdirectory';
-- PROGRAM: descriptor --

(* Program showing how to manipulate symbolic links using UnixEntry:
 * If the entry given as argument exists, the user is offered to make a symbolic 
 * link to it. If the entry given as argument does not exist, the user is
 * offered to make it a symbolic link to another entry.
 *)

(# e: @UnixEntry;
   usage:
     (# do 'Usage: ' -> puttext; 
        1->arguments->puttext; 
        ' path' -> putline;
        stop;
     #);
do (if noOfArguments<>2//true then Usage if);
   
   2 -> arguments -> e.path -> puttext;
   false -> e.followlinks;
   (if e.exists
    // true then
       ' exists.' -> putline;
       'Do you want to make a symbolic link to it? (y/n) ' ->puttext;
       (if keyboard.get // 'y' then
           'Link name: ' -> puttext;
           keyboard.getAtom -> e.linkFrom;
       if);
    // false then
       ' does not exist.' -> putline;
       'Do you want to make it a symbolic link to another entry? (y/n) ' -> puttext;
       (if keyboard.get // 'y' then
           'Other entry: ' -> puttext;
           keyboard.getAtom -> e.linkTo;
       if);
   if);
#)
