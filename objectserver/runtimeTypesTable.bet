ORIGIN '~beta/basiclib/v1.5/betaenv';
(* 
 * $RCSfile: runtimeTypesTable.bet,v $ $Revision: 1.6 $ $Date: 1996-06-04 14:36:45 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
INCLUDE '~beta/sysutils/v1.5/objinterface';
INCLUDE 'ObjectSerializerTables';

--- lib:attributes ---

(* RUNTIMETYPESTABLE
 * =================
 * 
 * Intended to be used in specializations of location as a table of 
 * object types that should be "cut" when saving an object graph. 
 * Cutting a reference is done by assigning the OID noneOID to the object.
 * 
 * register inserts an object type in the form of a pattern variable.
 * 
 * lookup checks whether the object entered matches one of the 
 * registered types, i.e. is an instance of a subtype. *)

runtimeTypesTable: IntegerHashTable
  (# 
     register: @
       (# type: ##Object;
          gps: @GetPrototypeForStruc;
       enter type##
       do (register[] (*dummy!!*), type## -> gps) -> insert;
       #);
     
     lookup: @
       (# o: ^Object;
          value: @Boolean;
          gp: @getPrototype; tmp: @Integer;
          pt: @Prototype; pref: @pt.prefix; 
       enter o[]
       do 
          (* Check the prefix chain of o[] to make sure none of its 
           * prefixes are a runtime type. *)
          false -> value;
          o[] -> gp -> pt;
          dofind:
            (if pt -> find //NONE then
                (if pref -> tmp //pt//0 then else
                    tmp -> pt; 
                    restart dofind;
                if);
             else
                true -> value;
            if);
       exit value
       #);
  #);

