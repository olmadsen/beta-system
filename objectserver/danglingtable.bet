ORIGIN '~beta/basiclib/v1.4/betaenv';
(* 
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- include 'betaOID'

--- lib:attributes ---



(* DANGLINGTABLE
 * =============
 * Used by ObjectSerializer *)

danglingData:
  (# OID: @OIDtype;         (* Referred object.             *)
     offsets: [1]@Integer;  (* Offsets into referred object. *)
     danglers: [1]@Integer; (* Negative values used as dangling references. *)
     fromOID: @OIDtype;     (* The OID of some object referring this one. *)
     count: @Integer;
     add: 
       (# offset, dangling: @Integer;
       enter (offset, dangling)
       do (if (count+1 -> count) > offsets.range //true then
              offsets.range -> offsets.extend;
              danglers.range -> danglers.extend;
          if);
          (offset, dangling) -> (offsets[count], danglers[count])
       #);
  #);

danglingTable:
  (* Used to keep track of the relationship between negative numbers used
   * as dangling references and the objects referred by these. *)
  (# OIDtoDangling:< 
       (* Should return a (negative!) value used to represent a dangling
        * reference to the object denoted by (OID,offset). *)
       (# OID, fromOID: @OIDtype;
          offset: @Integer;
          dangling: @Integer;
       enter (OID, offset, fromOID)
       do INNER
       exit dangling
       #);
     otd: @OIDtoDangling;
     
     danglingToOID:<
       (# dangling: @Integer;
          OID, fromOID: @OIDtype;
          offset: @Integer;
       enter dangling
       do INNER
       exit (OID, offset, fromOID)
       #);
     dto: @danglingToOID;
     
     delete:<
       (* When dangling reference to an object has been swizzled, it may 
        * be forgotten, as all dangling references to the same object are 
        * always swizzled at the same time. *)
       (# e: ^danglingData;
       enter e[]
       do INNER
       #);
     del: @delete;
     
     init:< (# do INNER #);
  #);
