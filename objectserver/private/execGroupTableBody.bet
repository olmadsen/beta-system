ORIGIN '../execGroupTable';
LIB_ITEM 'execgrouptable';
(* 
 * $RCSfile: execGroupTableBody.bet,v $ $Revision: 1.16 $ $Date: 2003-07-01 09:47:39 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
INCLUDE '~beta/sysutils/objinterface';
INCLUDE '~beta/basiclib/external';
INCLUDE '../ObjectSerializerTables';
INCLUDE '../diradds';

MDBODY ppcmac 'execGroupTableMac'
       default 'execGroupTableOther';
	   
--- initGroupTable:descriptor ---
(#
   strippath:
     (# tmp: @Integer;
        fname,stripped: ^Text;
     enter fname[]
     do
        0 -> tmp;
        '/' (* The paths used here always use '/' regardles 
             * of the local directoryChar.
             *)
          -> fname.findAll (# do inx -> tmp #);
        (if tmp=0 then
            fname.copy -> stripped[]
         else
            (tmp+1,fname.lgth) -> fname.sub -> stripped[]
        if)
     exit stripped[]
     #);
   
   getGroups:
     (# elm: ^standardEGTE;
        current: ^group_header;
        name: @Text;
     do 
        loop:
          (if (current[] -> NextGroup -> current[]) <> NONE then
              &standardEGTE[] -> elm[];
              (* &Text[] -> elm.groupName[]; *)
              
              current[] -> NameOfGroup -> elm.address 
                -> extGetCstring -> name; (* elm.groupName; *)
              name[] -> strippath -> elm.groupName[];
              
              nextInx -> elm.inx;
              
              (if (elm.groupName[] -> findGroupByName) = NONE then
                  elm[] -> nameHash.insert;
                  (elm[], elm.address) -> adrHash.insert;
                  
                  (%getLongAt(current.protoTableAdr)) -> elm.len;
                  (* Above line tos_converted from: current.protoTableAdr -> tos'%adrGetLong' -> elm.len; *)
                  current.protoTableAdr + 4 -> elm.ptListAdr;
               else
                  'ERROR!! This executable contains two source files named "' 
                    -> screen.putText; elm.groupName[] -> screen.putText;
                  '".\nThis is currenly not allowed when using persistence or distribution libraries.' -> screen.putLine;
                  (failure,'') -> stop;
              if);
              restart loop;
          if);
     #);
do 
   nameHash.init; 
   adrHash.init;
   getGroups;
#)


--- execGroupTableInsert:dopart ---
do egte[] -> nameHash.insert;
   (egte[], address) -> adrHash.insert;
   
--- findGroupByAdr:dopart ---
do (address,0) -> GroupName -> adrHash.find -> found[];
   
--- lib:attributes ---
GroupName: external (* in outpattern.c *)
  (# address: @Integer;
     isCode: @Integer;
     groupNameAdr: @Integer;
  enter (address, isCode)
  exit groupNameAdr
  #);

--- segteInxToPt:dopart ---
do (%getLongAt(ptListAdr+4*inx)) -> ptAdr;
(* Above line tos_converted from: do ptListAdr + 4*inx -> tos'%adrGetLong' -> ptAdr; *)

(* --- segtePTtoInx:dopart --- 
 * Moved to a machine dependent body to allow linary search on MAC
 *)
 
