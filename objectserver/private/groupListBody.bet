ORIGIN '../groupList';
(* 
 * $RCSfile: groupListBody.bet,v $ $Revision: 1.8 $ $Date: 1998-04-23 12:51:38 $
 *
 * COPYRIGHT
 *       Copyright (C) Aarhus University
 *       All rights reserved.
 *)
INCLUDE '~beta/containers/hashTable';
INCLUDE '../ObjectSerializerTables';

--- groupListInitBody:descriptor ---
(# 
do glPrivate.ih.init;
   glPrivate.nh.init;
#)

--- groupListfindInxBody:dopart ---
do group -> glPrivate.ih.find -> elm[];

--- groupListfindGroupBody:descriptor ---
(# e: @groupElement;
   hash: @glPrivate.nh.hashfunction;
   dofind: @glPrivate.nh.findIndexed
     (# predicate::< (# do current.groupName[]->groupName.equalNCS->value #);
        notFound::< (# do NONE -> elm[] #);
     do current[] -> elm[] 
     #)
do
   groupName[] -> e.groupName[];
   e[] -> hash -> doFind;
#)

--- groupListSizeBody:dopart ---
do glPrivate.ih.size -> value;

---  groupListScanBody:descriptor ---
(# 
do glPrivate.ih.scan  (# do current[] -> cur[]; INNER scan #);
#)

--- groupListInsertBody:descriptor ---
(# 
do (elm[], elm.inx) -> glPrivate.ih.insert;
   elm[] -> glPrivate.nh.insert;
#)

--- groupListClearBody:descriptor ---
(# 
do glPrivate.ih.init;
   glPrivate.nh.clear;
#)

--- groupListPrivate:descriptor ---
(# ih: @integerHashTable (# element::< groupElement #);
   nh: @HashTable (# element::< groupElement;
                     hashFunction::< 
                       (# len: @Integer;
                       do e.groupName.lgth->len;
                          (for i: len repeat
                               (e.groupName.T[i] ->ascii.lowcase)
                               + value -> value;
                          for)
                       #);
                  #);
#)
