ORIGIN '../groupList';
(* 
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- include '~beta/containers/v1.4/hashTable'
--- include '../ObjectSerializerTables'

--- groupListInitBody:descriptor ---
(# 
do glPrivate.ih.init;
   glPrivate.nh.init;
#)

--- groupListfindInxBody:dopart ---
do group -> glPrivate.ih.find -> elm[];

--- groupListfindGroupBody:descriptor ---
(# e: @groupElement;
   hash: @glPrivate.nh.hashfunction;
   dofind: @glPrivate.nh.findIndexed
     (# predicate::< (# do current.groupName[] -> groupName.equal -> value #);
        notFound::< (# do NONE -> elm[] #);
     do current[] -> elm[] 
     #)
do
   groupName[] -> e.groupName[];
   e[] -> hash -> doFind;
#)

--- groupListSizeBody:dopart ---
do glPrivate.ih.size -> value;

---  groupListScanBody:descriptor ---
(# 
do glPrivate.ih.scan  (# do current[] -> cur[]; INNER scan #);
#)

--- groupListInsertBody:descriptor ---
(# 
do (elm[], elm.inx) -> glPrivate.ih.insert;
   elm[] -> glPrivate.nh.insert;
#)

--- groupListClearBody:descriptor ---
(# 
do glPrivate.ih.init;
   glPrivate.nh.clear;
#)

--- groupListPrivate:descriptor ---
(# ih: @integerHashTable (# element::< groupElement #);
   nh: @HashTable (# element::< groupElement;
                     hashFunction::< 
                       (# inx: @Integer;
                       do L: (for i: 7 repeat
                                  e.groupName.lgth-i+1 -> inx;
                                  (if inx < 1 // true then leave L if);
                                  e.groupName.T[inx]+value->value;
                             for)
                       #);
                  #);
#)
