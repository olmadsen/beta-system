ORIGIN '../groupList';
(* 
 * $RCSfile: groupListBody.bet,v $ $Revision: 1.6 $ $Date: 1997-08-07 09:06:14 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
INCLUDE '~beta/containers/v1.6/hashTable';
INCLUDE '../ObjectSerializerTables';

--- groupListInitBody:descriptor ---
(# 
do glPrivate.ih.init;
   glPrivate.nh.init;
#)

--- groupListfindInxBody:dopart ---
do group -> glPrivate.ih.find -> elm[];

--- groupListfindGroupBody:descriptor ---
(# e: @groupElement;
   hash: @glPrivate.nh.hashfunction;
   dofind: @glPrivate.nh.findIndexed
     (# predicate::< (# do current.groupName[]->groupName.equalNCS->value #);
        notFound::< (# do NONE -> elm[] #);
     do current[] -> elm[] 
     #)
do
   groupName[] -> e.groupName[];
   e[] -> hash -> doFind;
#)

--- groupListSizeBody:dopart ---
do glPrivate.ih.size -> value;

---  groupListScanBody:descriptor ---
(# 
do glPrivate.ih.scan  (# do current[] -> cur[]; INNER scan #);
#)

--- groupListInsertBody:descriptor ---
(# 
do (elm[], elm.inx) -> glPrivate.ih.insert;
   elm[] -> glPrivate.nh.insert;
#)

--- groupListClearBody:descriptor ---
(# 
do glPrivate.ih.init;
   glPrivate.nh.clear;
#)

--- groupListPrivate:descriptor ---
(# ih: @integerHashTable (# element::< groupElement #);
   nh: @HashTable (# element::< groupElement;
                     hashFunction::< 
                       (# first,len: @Integer;
                       do (if ((e.groupName.lgth->len)-8->first)<0 then
                              0->first 
                          if);
                          len-first->len;
                          (for i: len repeat
                               (* Case-insensitive hashfunction:
                                * In ascii, 'a' - 'A' is 32.
                                * Hashing only on the 5 lowest bits
                                * makes it insensitive to case, at least for
                                * 7-bit ascii-chars used in filenames...
                                *)
                               (e.groupName.T[first+i] %Band 31)
                               + value -> value;
                          for)
                       #);
                  #);
#)
