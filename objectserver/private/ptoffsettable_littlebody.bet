ORIGIN '../ptOffsetTable';

-- ptOTscanSimpleTypesBody:doPart --
do pt.scanSimples
   (# 
   do simplePos+1 -> simplePos;
      (if simplePos > simpleTypes.Range then
          simpleTypes.Range -> simpleTypes.extend;
          simpleOfs.Range -> simpleOfs.extend;
      if);
      simpleType -> simpleTypes[simplePos];
      (inx * 4) + thisOffset -> simpleOfs[simplePos];
   #);
   
-- ptOTcopySimpleTypesBody:doPart --  
do (if 0<simplepos then
       (* Reserve space for the most probable need (All longs) *)
       simplepos -> elm.simpleVals.new;
       simplepos -> elm.simpleTypes.new;
       1 -> i; 
       simpleloop:
         (#
         do (* safety checks. Remove once it works. *)
            (if ofs[simpleOfs[i] div 4]
             //simple then else 
                'simpleOfs points at a nonsimple:' -> putText;
                ofs[simpleOfs[i] div 4] -> putInt; newLine;
                Stop;
            if);
            (if (simpleOfs[i] mod 4)<> 0 then
                'datatype found at non-long boundery!' -> putLine;
                Stop;
            if);
            
            (* Find out what type the subwords have. Also mark
             * that it has been handled. The rest are BIN32 *)
            (if simpleTypes[i]
             // SIMPLETYPE_INT32 then
                SIMPLETYPE_INT32 -> ofs[simpleOfs[i] div 4];
                i+1->i;
             // SIMPLETYPE_REAL64 then
                SIMPLETYPE_REAL64  -> ofs[(simpleOfs[i] div 4) + 0];
                SIMPLETYPE_HANDLED -> ofs[(simpleOfs[i] div 4) + 1];
                i+1->i
             // SIMPLETYPE_INT16 then
                (if i<simplepos then
                    (if simpleTypes[i+1]
                     // SIMPLETYPE_INT16 then
                        SIMPLETYPE_INT16INT16
                          -> ofs[simpleOfs[i] div 4];
                        i+2->i;
                     else
                        SIMPLETYPE_INT16BIN16
                          -> ofs[simpleOfs[i] div 4];
                        i+1->i;
                    if)
                 else
                    SIMPLETYPE_INT16BIN16
                      -> ofs[simpleOfs[i] div 4];
                    i+1->i;
                if)
            if);
            (if i<=simplepos then restart simpleloop if)
         #)
   if);
