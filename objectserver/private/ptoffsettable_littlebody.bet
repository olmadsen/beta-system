ORIGIN '../ptOffsetTable';

-- ptOTscanSimpleTypesBody:descriptor --
(# i: @Integer;
   
   traceSimples: (# exit false #);
   debugSimples: (# exit true #);

do (if traceSimples then
       '---- Building elm for proto: '->putText;
       pt.labId->putLine;
       '---- Size='->putText;pt.size->putInt;newLine;
       (for i:pt.size - 1 repeat
            'ofs['->putText; i+inx ->putInt; ']='->putText;
            ofs[i+inx]->putInt;newLine;
       for);
   if);
   
   pt.scanSimples
   (# 
      simpleError:
        (# 
        do (if debugSimples then
               (if ofs[i]<>simpleType then
                   pt.labId->putText; ':'->put;
                   'ofs[' -> putText;i -> putInt; 
                   ']='->putText; ofs[i]->putInt;
                   '. New simpleType='->putText; 
                   simpleType->putInt; newLine;
                else
                   '[Found same type of simple ('->putText;
                   simpleType->putInt; ') at index '->putText;
                   i->putInt;']'->put;newLine;
               if);
           if);
        #);
   do (* simpleType is the type of the entry; i is the longindex *)
      (thisOffset div 4) + inx -> i;
      (if ofs[i]=SIMPLETYPE_BIN32 then
          (if simpleType
           // SIMPLETYPE_INT32 then
              SIMPLETYPE_INT32 -> ofs[i];
           // SIMPLETYPE_INT16 then
              (if (thisOffset mod 4) = 0 then
                  SIMPLETYPE_INT16BIN16 -> ofs[i];
               else
                  SIMPLETYPE_BIN16INT16 -> ofs[i];
              if);
           // SIMPLETYPE_REAL64 then
              SIMPLETYPE_REAL64  -> ofs[i + 0];
              SIMPLETYPE_HANDLED -> ofs[i + 1];
           else
              (if debugSimples then
                  'Unknown simpletype found in ptoffsettable:'->putText;
                  simpleType->putInt;newLine;
                  Stop;
              if);
          if)
       else
          (if simpleType=SIMPLETYPE_INT16 then
              (if ofs[i] 
               // SIMPLETYPE_INT16BIN16 then
                  (if (thisOffset mod 4) = 2 then
                      SIMPLETYPE_INT16INT16 -> ofs[i];
                   else
                      simpleError;
                  if);
               // SIMPLETYPE_BIN16INT16 then
                  (if (thisOffset mod 4) = 0 then
                      SIMPLETYPE_INT16INT16 -> ofs[i];
                   else
                      simpleError;
                  if);

               else
                  simpleError;
              if)
           else
              simpleError;
          if);
      if);
   #);
#)
