ORIGIN 'browserEnv';

INCLUDE '~beta/persistentstore/v1.4/persistentstore';
INCLUDE '~beta/persistentstore/v1.4/psObjectServer';
INCLUDE '~beta/sysutils/v1.4/objinterface';

--- objectStorePrivatePart:descriptor --- 
(# ps: @persistentStore
     (# allowLazyFetch::< (# do true -> value #);
     #);
   open: @Boolean;
#)

--- objectStoreOpenBody:descriptor ---
(# 
do
   (if objectStorePrivate.open //true then
       close;
   if);
   
   doOpen: name[] -> objectStorePrivate.ps.openRead
   (# notFound::< (# do couldNotOpen; leave doOpen #);
      accessError::< (# do couldNotOpen; leave doOpen #);
   #);
   true -> objectStorePrivate.open;
   
   opened;
#)

--- objectStoreCloseBody:descriptor ---
(# 
do 
   (if objectStorePrivate.open //true then
       false ->objectStorePrivate.open;
       objectStorePrivate.ps.close 
       (# danglersExists::< (# do ignore -> toDo #)#);
   if);
#)

--- objectStoreScanRootNamesBody:descriptor ---
(# 
do
   (if objectStorePrivate.open //true then
       objectStorePrivate.ps.scanRootNames
       (# do current[] -> currentName[]; INNER scanRootNames #);
   if);
#)

--- objectStoreGetBody:descriptor ---
(# 
do
   (if objectStorePrivate.open //true then
       (name[], Object##) -> objectStorePrivate.ps.get -> obj[];
   if);
#)

--- objectStoreHandleDanglerBody:descriptor ---
(# os: ^PSObjectServer;
   OID,fromOID: @OIDtype;
   offset: @Integer;
   pt: @protoType;
   theObject: ^Object;
do 
   (if os[] //NONE then getPSObjectServer -> os[] if);
   dangler -> os.danglers.dto -> (OID,offset,fromOID);
   (30,OID,offset,fromOID)  
     -> os.singleObjectGetPartialClosure 
     -> theObject[];
#)
