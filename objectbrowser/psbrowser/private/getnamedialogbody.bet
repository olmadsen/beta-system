ORIGIN  '../getnamedialog';
INCLUDE '~beta/guienv/v1.6/figureitems';
INCLUDE '~beta/guienv/v1.6/guienvactions';

--- GetNameDialogAttributes: Descriptor ---
(#
   onOk: 
     (#
     do FALSE -> cancelled;
        iName.contents -> value[]; 
        this (GetNameDialog).close;
     #);
   
   iCancel: @pushButton
     (#	eventHandler::
          (# onMouseup::  
               (# 
               do TRUE -> cancelled;
                  this (GetNameDialog).close; 
               #);
          #);
        
        open::
          (# 
          do (60,70) -> position; 
             (50,20) -> size; 
             'Cancel' -> label;
          #);
     #);
   iOk: @pushButton
     (#	eventHandler::
          (# onMouseup:: (# do onOk #)#);
        
        open::
          (# 
          do (10,70)-> position; 
             (30,20) -> size; 
             'Ok' -> label;
          #);
     #);
   
   iStatTextButton: @staticText
     (# 
        open::
          (# 
          do (10,8) ->position; 
             (250,20) -> size; 
             'Name:'->label;
          #);
     #);
   
   iName: @editText
     (# 
        open::
          (# 
          do (10,27) -> position; (175,29) -> size;
             ''->contents;
          #);
        
        eventHandler::
          (# onKeyDown::
               (#
               do (* This line will close the object twice, since
                   * the okbutton is defaultbutton and therefore closed
                   * the window upon "return"
                   * 
                   * (if (ch=ascii.cr) or (ch=ascii.etx) then onOk if);
                   *)
               #)
          #);
     #); 
   
#)  

--- GetNameDialogOpen: Descriptor ---
(# pos: @Point;
do hide;
   'Open Persistent Store'->title;
   (195,100)->size;
   (*
    * (195,100)->minSize;
    * (195,100)->maxSize;
    *)
   owner.position->pos; (50,100)->pos.add; pos->position;
   THIS(getNameDialog).contents->private.iCancel.open;
   THIS(getNameDialog).contents->private.iOK.open; 
   private.iOk[] -> DefaultButton;
   THIS(getNameDialog).contents->private.iStatTextButton.open;
   THIS(getNameDialog).contents->private.iName.open; 
   Private.iName[]-> target;
#) 

-- getNameDialogRun:descriptor ---
(# 
do open; showModal;
#)
