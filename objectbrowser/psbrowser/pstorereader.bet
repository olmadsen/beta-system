ORIGIN '../mpsinterface';

INCLUDE '~beta/guienv/v1.3.1/guienv';
INCLUDE '~beta/basiclib/v1.4/formatio';

INCLUDE '~beta/betaast/v5.0.1/betasematt';
INCLUDE '~beta/mps/v5.0.1/private/astparser';
INCLUDE '../betagrammarinit';

INCLUDE '~beta/editor/v5.0.1/codeviewer';

INCLUDE '../UI/objectview';

INCLUDE 'psobjdumper';
INCLUDE 'odOffsetTable';
INCLUDE 'psfullnamepatch';

INCLUDE '~beta/persistentstore/v1.4.2/psLocation';
INCLUDE '~beta/persistentstore/v1.4.2/private/psLocationBody'; 
(* psLocationBody is needed to implement scanAll. *)

--- program:descriptor ---
mpsinterface
(# fromLoc: @psLocation
     (# locationObjectsImpl::
          (# scanAll:
               (# curproto,curgroup,curOIDb: @Integer;
               do (for i: pslopriv.last repeat
                       pslopriv.protos[i]->curproto;
                       pslopriv.groups[i]->curgroup;
                       i->curOIDb;
                       INNER;
                  for);
               #);
          #);
     #);
   dumper: @psdumper;
   
   onOpenGroup::
     (# 
     do 'Opening %s ...' -> putformat (# do groupName[]->s #);
     #);
   onOpenGroupDone::
     (# 
     do 'Done'->putLine;
     #);
   
   groupsInfo: @groupsInfoTable
     (# element:: 
          (# seen: [30]@Boolean;
             append:: (# onExtend:: (# do seen.range->seen.extend #)#)
          #);
        onScan::
          (# 
          do 'Scanning %s for ObjectDescriptors...'->putformat
             (# 
             do groupName[]->s;
             #);
          #);
        onScanDone::
          (# 
          do 'Done'->putLine;
          #);
     #);
   
   gui: @guienv
     (#
        editwin: @window
          (# editor: @SifTextEditor
               (# open::
                    (# 
                    do father.size->size;
                    #);
               #);
             open::
               (# 
               do (500,400)->size; editor.open;
               #);
          #);
        
        win: @window
          (# 
             showObject:
               (# obj: ^object;
                  bo: ^PSBetaObject;
                  OID: @OIDtype;
                  offset: @Integer;
               enter (OID,offset)
               do 
                  (OID,offset,dumper[])->newPSobject->bo[];
                  (bo[],FALSE) -> OpenMoveableObjectView
                  (# getFather:: (# do contents->father[] #)#)
               #);
             
             menuBarType::
               (# filemenu: @menu
                    (# openDescriptorItem: @menuitem
                         (# open:: (# do 'Next Descriptor'->name; 'N'->key #);
                            eventHandler::
                              (# onSelect:: 
                                   (# 
                                   do (if not nextPattern then
                                          'Done'->putLine;
                                          terminate;
                                      if);
                                   #);
                              #);
                         #);
                       openObjectItem: @menuitem
                         (# open:: (# do 'Next Object'->name; 'O'->key #);
                            eventHandler::
                              (# onSelect:: 
                                   (# 
                                   do (if not nextObject then
                                          'Done'->putLine;
                                      if);
                                   #);
                              #);
                         #);
                       quitItem: @menuitem
                         (# open:: (# do 'Quit'->name; 'Q'->key #);
                            eventHandler::
                              (# onSelect:: 
                                   (# 
                                   do terminate;
                                   #);
                              #);
                         #);
                       open::
                         (# 
                         do openDescriptorItem.open; 
                            openDescriptorItem[]->append;
                            openObjectItem.open; 
                            openObjectItem[]->append;
                            quitItem.open;
                            quitItem[]->append;
                         #);
                    #);
                  open::
                    (# 
                    do (400,200)->size;
                       filemenu.open; filemenu[]->append;
                    #);
               #);
          #);
        
        nextObject: @|
          (# more: @Boolean;
          do true->more;
             fromLoc.locationObjects.scanAll
             (# 
             do ((fromLoc.creationTime,curOIDb),0)->win.showObject;
                suspend;
             #);
             false->more;
          exit more
          #);
        
        nextPattern: @|
          (# od: ^BETACFL.ObjectDescriptor;
             groupName: ^Text;
             more: @Boolean;
             seen: @Boolean;
          do 
             true->more;
             fromLoc.locationObjects.scanAll
             (# 
             do
                (curproto,
                (curgroup->fromLoc.locationGroups.inxToElm).groupName[])
                  ->groupsInfo.protoGroupToOD
                (#
                do (if not (elm.seen[proto+1]->seen) then
                       true->elm.seen[proto+1]
                   if);
                   elm.groupName[]->nextPattern.groupName[];
                #)->od[];
                
                (if not seen then
                    (AST[],BETACFL[],od.frag[],od[])
                      ->editwin.editor.newfragment;
                    suspend;
                if);
             #);
             false->more;
          exit more
          #);

     do win.open;
        editwin.open;
     #);
   
do persistentstore_fullnamepatch;
   (if noOfArguments = 2 then
       (2->Arguments,read)->fromLoc.open;
    else
       'Usage: %s <oldstore>\n' -> putFormat
       (#
       do 1 -> Arguments -> s;
       #);
       stop;
   if);
   init; (* Altsaa mpsinterface.init *)
   groupsInfo.init; groupsInfo[]->objectPool.put;
   (AST[],BETACFL[])->dumper.init;
   betaGrammarInit;
   gui; 
#)
