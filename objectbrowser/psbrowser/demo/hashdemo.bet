ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/persistentstore/v1.6/persistentstore';
INCLUDE '~beta/containers/v1.6/hashTable';
INCLUDE '~beta/objectbrowser/v2.2/psbrowser/psfullnamepatch';

---lib: attributes---
TextHashTable: hashTable
  (# element::< Text;
     hashfunction::< (# do e.scanAll (# do ch*26+value -> value #)#);
  #);

--- program : descriptor ---
(# PS: @persistentstore;
   H: ^TextHashTable;
   exists: @Boolean;
   psName: ^Text;
   T: ^Text;
do
   persistentstore_fullnamepatch;
   
   'Enter name of persistent store: ' -> putText;
   getLine -> psName[];
   
   true -> exists;
   open: psName[] -> PS.openWrite
   (# notFound::< 
        (# do psName[] -> PS.create; false -> exists; leave open #)
   #);
   
   (if exists //false then
       'created' -> putLine;
       &TextHashTable[] -> H[];
       H.init;
       (H[],'TextTable') -> PS.put;
    else
       'opened' -> putLine;
       ('TextTable', TextHashTable##) -> PS.get -> H[];
   if);
   
   &Text[] -> T[]; H.size + 1 -> T.putInt; T[] -> H.insert;
   
   H.scan  (# do current[] -> putLine #);
   'scan done\n' -> putLine;
   
   PS.close; 'closed' -> putLine;
#)



