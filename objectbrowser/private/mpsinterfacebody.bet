ORIGIN '../mpsinterface';
INCLUDE '~beta/basiclib/v1.4/file';

--- mpsinterfaceInitBody:dopart ---
do AST;
   BETACFL.init;
   fragmentGroupTable.init;
   INNER;
   
--- ptToObjectDescriptorBody:dopart ---
do find: fg.fragmentList.scan
   (# 
   do (if current.type //AST.FormType then
          (if formIndex-1->formIndex //0 then
              current.open->ff[];
              astIndex->ff.indexToNode->a[];
              leave find;
          if)
      if);
   #);

--- getFragmentGroupBody: descriptor ---
(# e: ^element;
   fgf: ^AST.fragmentGroup;
do     
   &element[]->e[];
   groupName.copy->e.groupName[];
   
   find: e[]->hashFunction->findIndexed
   (# predicate:: (# do current.groupName[]->groupName.equal->value #);
      notFound::< 
        (# de: @diskEntry; donecheckOk: @Boolean;
        do    
           groupName[]->onOpenGroup;
           groupName[]->AST.expandToFullPath->groupName[];
           
           (groupName[],screen[])->AST.top.open 
           (# startingParsing::
                (# 
                do groupName[]->onOldGroup;
                   NONE->AST.theCatcher[];
                   leave find;
                #);
              FragmentNotExisting::
                (# 
                do groupName[]->onGroupNotFound;
                   leave find;
                #);
           #)->e.fg[]->foundGroup[];
           
           checkcheck: foundGroup.prop.scanProp
             (# doProp::
                  (#
                  do (if ('donecheck'->prop.equalNCS) then
                         TRUE->doneCheckOk;
                         leave checkcheck;
                     if);
                  #);
             #);
           
           (if not doneCheckOk then
               (groupName[],foundGroup[])->onUncheckedGroup;
               NONE->foundGroup[];
               leave find;
           if);
           
           foundGroup.diskFileName->de.path;
           (if not ((foundGroup[],de.modtime)->modtimeOk) then
               groupName[]->onNewGroup; 
               NONE->foundGroup[];
               leave find
           if);
           
           e[]->insert;
           
           (groupName[],e.fg[])->onOpenGroupDone;
           
           e.fg[]->fgf[];
           (* Setup origin chain. *)
           loop:
             (if fgf[]<>NONE then
                 (if fgf.origin=NONE then
                     fgf[]->mySetupOrigin;
                     fgf.origin->fgf[];
                     restart loop;
                 if)
             if);
               
           leave find;
        #);
   do current.fg[]->foundGroup[];
   #);
   
#)

--- mpsintMySetupOrigin:dopart ---
do fg.prop.ScanProp
   (# doProp:: 
        (# 
        do prop.makelc;
           (if 'origin'->prop.equal then
               ScanParameters
               (# doString:: 
                    (# t: ^Text;
                    do fg.fullName->AST.stripPathName->t[];
                       (if t[]//NONE then 
                           (s.copy,AST.thePathHandler.currentDirectory) 
                             ->AST.thePathHandler.convertFilePath
                             ->fragmentGroupTable.getFragmentGroup
                             ->fg.origin
                        else 
                           (s.copy,t[])
                             ->AST.thePathHandler.convertFilePath
                             ->fragmentGroupTable.getFragmentGroup
                             ->fg.origin
                       if)
                    #)
               #)
           if)
        #)
   #)
   
