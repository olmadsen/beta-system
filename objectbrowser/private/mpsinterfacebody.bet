ORIGIN '../mpsinterface';

--- mpsinterfaceInitBody:dopart ---
do AST;
   BETACFL.init;
   fragmentGroupTable.init;
   INNER;
   
--- ptToObjectDescriptorBody:dopart ---
do find: fg.fragmentList.scan
   (# 
   do (if current.type //AST.FormType then
          (if formIndex-1->formIndex //0 then
              current.open -> ff[];
              astIndex -> ff.indexToNode -> a[];
              leave find;
          if)
      if);
   #);

--- getFragmentGroupBody: descriptor ---
(# e: ^element;
   fgf: ^AST.fragmentGroup;
do     
   &element[] -> e[];
   groupName.copy -> e.groupName[];
   
   find: e[] -> hashFunction -> findIndexed
   (# predicate:: (# do current.groupName[] -> groupName.equal -> value #);
      notFound::< 
        (#
        do    
           groupName[]->onOpenGroup;
           groupName[]->AST.expandToFullPath->groupName[];
           
           (groupName[], screen[]) -> AST.top.open 
           (# startingParsing::
                (# 
                do groupName[] -> onOldGroup;
                   (failure, '') -> stop;
                #);
              FragmentNotExisting::
                (# 
                do false -> continue;
                   groupName[] -> onGroupNotFound;
                #);
           #) -> e.fg[] -> foundGroup[];
           e[] -> insert;
           
           e.fg[] -> fgf[];
           (* Setup origin chain. *)
           loop:
             (if fgf[]<>NONE then
                 (if fgf.origin=NONE then
                     fgf.setupOrigin;
                     fgf.origin -> fgf[];
                     restart loop;
                 if)
             if);
           
           (groupName[],e.fg[])->onOpenGroupDone;
           
           leave find;
        #);
   do current.fg[] -> foundGroup[];
   #);
   
#)
