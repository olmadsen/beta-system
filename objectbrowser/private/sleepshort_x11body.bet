ORIGIN '../sleepshort';
INCLUDE '~beta/basiclib/v1.5/private/basicsystemenvbody'
        '~beta/guienv/v1.4/private/X11/guienvxsystemenvbody';
-- systemlib: Attributes --
XtAppAddTimeOut: external
  (#
     appContext: @integer;
     interval: @integer;
     proc: @Integer;
     (* We only install callbacks once. *)
     closure: @integer;
     intervalId: @integer;
     
  enter (appContext,interval,proc,closure)
  do callC
  exit intervalId
  #);
makeCBF: External
  (# pat: ##external; cb: @integer;  enter pat## exit cb #);
  

-- sleepshortsleep: DoPart --
do
   &sleepers.element[]->new[];
   ssprivate.nextid->new.id;
   (private.mdpriv.pioPrivate.appContext,(ticks*1000) div 60,
    ssprivate.shortSleepTimeoutPtr,new.id)->XtAppAddTimeOut;
   new[]->sleepers.append;
   new.sem.P;
     

-- sleepshortinit: DoPart --
do
   ssprivate.shortSleepTimeout##->makeCBF->ssprivate.shortSleepTimeoutPtr;
   sleepers.init;
     

-- sleepshortprivate: Descriptor --
(#
   shortSleepTimeoutPtr: @Integer;
   shortSleepTimeout: external
     (# data,id: @Integer; 
     enter (data,id)
     do
        cExternalEntry;
        iter: sleepers.iterate
          (# 
          do
             (if current.elm.id
              // data then
                 current.elm.sem.V;
                 current[]->sleepers.delete;
                 leave iter;
                 private.mdpriv.pioPrivate.ensure;
                 
             if);
             
          #);
        
     #);
   nextid: @IntegerValue (#  do value+1->value #);
   
#)  

