ORIGIN '../objectbrowserinterface';
-- objworldOpen: DoPart --
do
   initialBrowserSize->sz;
   theInfo.size->tmp;
   sz.v-tmp.v->sz.v;
   sz->size;
   TRUE->bindLeft->bindRight->bindBottom->bindTop;
   INNER ;
     

-- objWorldOnMouseDown: DoPart --
do
   (if buttonState
    // 1 then
       (if clearInProgress then clr->browserDrawRect if);
       localPosition->SimpleDefineRect->clr;
       (if not clr.isEmpty then
           true->clearInProgress; clr->browserDrawRect; 
        else
           false->clearInProgress; 
       if);
       
   if);
   INNER ;
     

-- infoOpen: DoPart --
do
   style->ts[];
   initialBrowserSize->sz;
   ts.lineHeight+6->sz.v;
   sz->size;
   initialBrowserSize->pos;
   0->pos.h;
   pos.v-sz.v->pos.v;
   pos->Position;
   TRUE->bindLeft->bindRight->bindBottom;
   FALSE->bindTop;
   TRUE->border.visible;
   borderStyles.shadowIn->border.style;
   INNER ;
     

-- browserInit: DoPart --
do
   getOptionDB->options[];
   (charRepOnALine,TRUE)->options.setBooleanOption;
   (invisibleOrigins,TRUE)->options.setBooleanOption;
   (useShortObjectTitles,TRUE)->options.setBooleanOption;
   (numberObjects,TRUE)->options.setBooleanOption;
     

-- browserOnOpenGroup: DoPart --
do
   'opening '->t[];
   groupName[]->t.append;
   t[]->theInfo.label;
   true->theInfo.update;
     

-- browserOnOpenGroupDone: DoPart --
do ''->theInfo.label; true->theInfo.update;   

-- browserOnOldGroup: DoPart --
do
   'ERROR: "%s.%s" is older than "%s.%s". Recompile!!!\n'
     ->putFormat
       (# 
       do groupName[]->s; AST.astFileExtension->s; groupName[]->s; 'bet'->s; 
       #)  

-- browserOnGroupNotFound: DoPart --
do
   'ERROR: "%s.%s" not found !!! \n'
     ->putFormat (#  do groupName[]->s; AST.astFileExtension->s;  #);
     

-- iconsGetIcon: DoPart --
do
   (if TRUE
    // ObjectViewIcon->name.equalNCS then
       (if movIcon[]
        // none then &pixmap[]->movIcon[]; 'ObjectViewIcon'->movIcon.read; 
       if);
       movIcon[]->theIcon[];
       
   if);
   INNER ;
     

-- OBIcloseItem: Descriptor --
menuitem
  (#
     open::  (#  do 'Close'->name; 'W'->key #);
     eventHandler:: 
       (#
          onSelect:: 
            (# m: ^moveableList; 
            do
               getMoveableList->m[];
               m.scan
                 (# 
                 do (if clr->current.inRect then current.close if); 
                 #);
               clr->theObjworld.browserDrawRect;
               false->clearInProgress;
               
            #);
          onStatus::  (#  do clearInProgress->value #);
          
       #)
  #)  

-- OBIquitItem: Descriptor --
menuitem
  (#
     open::  (#  do 'Quit'->name; 'Q'->key #);
     eventHandler::  (# onSelect::<  (#  do Terminate #) #)
  #)  

-- OBIfileMenuOpen: DoPart --
do
   'File'->name;
   closeItem.open;
   closeItem[]->append;
   quitItem.open;
   quitItem[]->append;
   INNER ;
     

-- OBImenubarOpen: DoPart --
do theFileMenu.open; theFileMenu[]->append; INNER  

-- OBIopen: DoPart --
do
   initialBrowserSize->size;
   myIcons[]->objectPool.put;
   'Embedded Object Browser'->title;
   contents->theInfo.open;
   contents->theObjworld.open;
   theObjworld[]->theBrowser.init;
   INNER ;
     

