ORIGIN '~beta/guienv/guienv';
INCLUDE 'windowedbrowser'
        '~beta/basiclib/formatio'
        '~beta/toollibs/utils/optionlist'
        '~beta/toollibs/UI/icons'
        '~beta/toollibs/UI/iconlist';
-- lib: Attributes --
initialBrowserSize: (#  exit (600,400) #)  

-- guienvLib: Attributes --
objectBrowserInterface: window
  (#
     clr: @rectangle;
     clearInProgress: @Boolean;
     objworld: @Canvas
       (#
          open:: 
            (# sz: @Point; tmp: @Point; 
            do
               initialBrowserSize->sz;
               info.size->tmp;
               sz.v-tmp.v->sz.v;
               sz->size;
               TRUE->bindLeft->bindRight->bindBottom->bindTop;
               
            #);
          eventHandler:: 
            (#
               onMouseDown:: 
                 (# 
                 do
                    (if buttonState = 1 then
                        (if clearInProgress then clr->browserDrawRect if);
                        localPosition->SimpleDefineRect->clr;
                        (if not clr.isEmpty then
                            true->clearInProgress; clr->browserDrawRect; 
                         else
                            false->clearInProgress; 
                        if);
                        
                    if);
                    
                 #);
               
            #);
          
       #);
     info: @staticText
       (#
          open::< 
            (# sz: @Point; ts: ^TextStyle; pos: @Point; 
            do
               style->ts[];
               initialBrowserSize->sz;
               ts.lineHeight+6->sz.v;
               sz->size;
               initialBrowserSize->pos;
               0->pos.h;
               pos.v-sz.v->pos.v;
               pos->Position;
               TRUE->bindLeft->bindRight->bindBottom;
               FALSE->bindTop;
               TRUE->border.visible;
               borderStyles.shadowIn->border.style;
               
            #);
          
       #);
     browser:< windowedbrowser
       (#
          init::< 
            (# options: ^optionDB; 
            do
               getOptionDB->options[];
               (charRepOnALine,TRUE)->options.setBooleanOption;
               (invisibleOrigins,TRUE)->options.setBooleanOption;
               (useShortObjectTitles,TRUE)->options.setBooleanOption;
               (numberObjects,TRUE)->options.setBooleanOption;
               
            #);
          mpsinterfaceType::< 
            (#
               onOpenGroup:: 
                 (# t: ^Text; 
                 do
                    'opening '->t[];
                    groupName[]->t.append;
                    t[]->info.label;
                    true->info.update;
                    
                 #);
               onOpenGroupDone:: 
                 (#  do ''->info.label; true->info.update;  #);
               onOldGroup:: 
                 (# 
                 do
                    'ERROR: "%s.%s" is older than "%s.%s". Recompile!!!\n'
                      ->putFormat
                        (# 
                        do
                           groupName[]->s;
                           AST.astFileExtension->s;
                           groupName[]->s;
                           'bet'->s;
                           
                        #)
                 #);
               onGroupNotFound:: 
                 (# 
                 do
                    'ERROR: "%s.%s" not found !!! \n'
                      ->putFormat
                        (#  do groupName[]->s; AST.astFileExtension->s;  #);
                    
                 #);
               
            #);
          
       #);
     aBrowser: @browser;
     myIcons: @iconDB
       (#
          movIcon: ^pixmap;
          getIcon:: 
            (# 
            do
               (if ObjectViewIcon->name.equalNCS then
                   (if movIcon[] = none then
                       &pixmap[]->movIcon[]; 'ObjectViewIcon'->movIcon.read; 
                   if);
                   movIcon[]->theIcon[];
                   
               if);
               
            #);
          
       #);
     close::<  (#  do Terminate #);
     menuBarType::< 
       (#
          fileMenu: @Menu
            (#
               closeItem: @menuitem
                 (#
                    open::  (#  do 'Close'->name; 'W'->key #);
                    eventHandler:: 
                      (#
                         onSelect:: 
                           (# m: ^moveableList; 
                           do
                              getMoveableList->m[];
                              m.scan
                                (# 
                                do
                                   (if clr->current.inRect then
                                       current.close
                                   if);
                                   
                                #);
                              clr->objworld.browserDrawRect;
                              false->clearInProgress;
                              
                           #);
                         onStatus::  (#  do clearInProgress->value #);
                         
                      #)
                 #);
               quitItem: @menuitem
                 (#
                    open::  (#  do 'Quit'->name; 'Q'->key #);
                    eventHandler:: 
                      (# onSelect::<  (#  do Terminate #) #)
                 #);
               open:: 
                 (# 
                 do
                    'File'->name;
                    closeItem.open;
                    closeItem[]->append;
                    quitItem.open;
                    quitItem[]->append;
                    
                 #);
               
            #);
          open::<  (#  do fileMenu.open; fileMenu[]->append; INNER #)
       #);
     open::< 
       (# 
       do
          initialBrowserSize->size;
          myIcons[]->objectPool.put;
          'Embedded Object Browser'->title;
          contents->info.open;
          contents->objworld.open;
          objworld[]->aBrowser.init;
          
       #)
  #)  

