ORIGIN '~beta/guienv/guienv';
INCLUDE 'windowedbrowser'
        '~beta/basiclib/formatio'
        '~beta/toollibs/utils/optionlist'
        '~beta/toollibs/UI/icons'
        '~beta/toollibs/UI/iconlist';
BODY 'private/objectbrowserinterfacebody';
-- lib: Attributes --
initialBrowserSize:
  (# 
  exit (600,400)
  #)
  (* When making an instance of the objectBrowserInterface pattern, the
   * user can furtherbind the following virtuals to customise the behaviour
   * of the objectbrowser
   * 
   * objworld 
   *    is the canvas wherein the graphical representaion of objects are 
   *    displayed
   * info 
   *    is an info field at the bottom of the window
   * browser 
   *    contains the main functionality such as mps interface and
   *    a virtual for determining which type of objectview should be used
   * icons
   *    defines which icons are used in the objworld
   * 
   * For examples of usage see:
   * 
   * demo/browser.bet
   * demo/dynamicCompilation.bet
   *)  

-- guienvLib: Attributes --
objectBrowserInterface: window
  (#
     clr: @rectangle;
     clearInProgress: @Boolean;
     objworld:< Canvas
       (#
          open::< 
            (# sz: @Point; tmp: @Point;  <<SLOT objworldOpen:DoPart>> #);
          eventHandler::< 
            (#
               onMouseDown::< 
                 (# 
                 <<SLOT objWorldOnMouseDown:DoPart>>
                 #);
               
            #);
          
       #);
     theObjworld: @objworld;
     objectViewType:< moveableObjectView;
     info:< staticText
       (#
          open::< 
            (# sz: @Point; ts: ^TextStyle; pos: @Point; 
            <<SLOT infoOpen:DoPart>>
            #);
          
       #);
     theInfo: @info;
     myIcons: @icons;
     icons:< iconDB
       (#
          movIcon: ^pixmap;
          getIcon::< 
            (#  <<SLOT iconsGetIcon:DoPart>> #);
          
       #);
     menuBarType::< 
       (#
          theFileMenu:
            @fileMenu;
          fileMenu:< Menu
            (#
               closeItem: @<<SLOT OBIcloseItem:Descriptor>>;
               quitItem:
                 @<<SLOT OBIquitItem:Descriptor>>;
               open::<  (#  <<SLOT OBIfileMenuOpen:DoPart>> #);
               
            #);
          open::< 
            (# 
            <<SLOT OBImenubarOpen:DoPart>>
            #)
       #);
     open::< 
       (#
          mpsInter:
            ^mpsInterface
       enter mpsInter[]
       <<SLOT OBIopen:DoPart>>
       #);
     mpsInterfaceType:< mpsInterface;
     mpsInt: ^mpsInterfaceType;
     objectdumperType:< objectDumper;
     od: ^objectDumperType;
     showObject:<
       (#
          betaObjectType:< betaObject;
          bo: ^betaObjectType;
          
       do
          INNER ;
          (bo[],FALSE)
            ->OpenMoveableObjectView
              (#
                 moveableObjectViewType::
                  THIS(objectBrowserInterface).
                    ObjectViewType;
                 getFather::  (#  do theObjWorld[]->father[] #)
              #)
       #)
  #);
standaloneBrowser: objectBrowserInterface
  (#
     objectdumperType::< mpsInt.localdumper;
     mpsInterfaceType::< 
       (#
          onOpenGroup::< 
            (# t: ^Text; 
            do
               'opening '->t[];
               groupName[]->t.append;
               t[]->theInfo.label;
               true->theInfo.update;
               
            #);
          onOpenGroupDone::< 
            (#  do ''->theInfo.label; true->theInfo.update;  #);
          onOldGroup::< 
            (# 
            do
               'ERROR: "%s.%s" is older than "%s.%s". Recompile!!!\n'
                 ->putFormat
                   (# 
                   do
                      groupName[]->s;
                      AST.astFileExtension->s;
                      groupName[]->s;
                      'bet'->s;
                      
                   #)
            #);
          onGroupNotFound::< 
            (# 
            do
               'ERROR: "%s.%s" not found !!! \n'
                 ->putFormat
                   (#  do groupName[]->s; AST.astFileExtension->s;  #);
               
            #)
       #);
     showObject::< 
       (# obj: ^object
       enter obj[]
       do (od[],obj[])->mpsInt.newLocalObject->bo[]; INNER
       #);
     open::< 
       (# prefs:^preferences;
       do
          getObjectPrefs->prefs[];
          TRUE->prefs.getBoolean(# do 'OneLineCharRep'->name[] #);
          TRUE->prefs.getBoolean(# do 'HideOrigin'->name[] #);
          TRUE->prefs.getBoolean(# do 'ShortObjectTitles'->name[] #);
          TRUE->prefs.getBoolean(# do 'ObjectSerialNumber'->name[] #);
          
          &mpsInterfaceType[]->mpsInt[];
          mpsInt.init;
          INNER
       #)
  #)  

