ORIGIN '~beta/basiclib/v1.4/betaenv';

(* A coroutinespawner is an abstract interface to systemenv like
 * functionality. It is used in order to avoid unnecessary 
 * dependency on systemenv. *)

--- lib:attributes ---

coroutinespawner:
  (# spawn:
       (# newprocess: @Boolean;
          theProc: ^|Object;
       do (if newprocess then
              INNER;
           else
              TRUE->newprocess;
              THIS(spawn)[]->spawnimpl;
          if);
       #);
     spawnimpl:<
       (# thespawn: ^spawn;
       enter thespawn[]
       do INNER
       #);
     pause:< (# do INNER #);
     sleep:< 
       (# ticks: @Integer; 
       enter ticks
       do INNER
       #);
     kill:< (# do INNER #);
     semaphore:<
       (# V:< (# do INNER #);
          P:< (# do INNER #);
       #);
  #);

getCoroutineSpawner:
  (# cs: ^coroutineSpawner;
  do find: objectPool.scan
       (# type:: coroutinespawner;
       do current[]->cs[]; leave find
       #);
  exit cs[]   
  #);
