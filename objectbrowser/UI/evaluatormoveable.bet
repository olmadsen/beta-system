ORIGIN 'objectview';
INCLUDE '~beta/compiler/DYN/UI/evaluator';
-- windowLib: Attributes --
moveableObjectViewEvaluator: moveableObjectView
  (#
     onEvaluateButton:< (#  do INNER #);
     onCloseEvaluator:< (#  do INNER #);
     moveableMenu::< 
       (#
          open::< 
            (# openEvaluatorMenuEntry:< booleanValue (# do true->value #);
               evaluatorAction: actionedMenuItemAction
                 (# evalSize,listSize: @point
                 do
                    (if contents.evaluator.evaluatorVisible then
                        hideEvaluatorAction
                     else
                        true->contents.evaluator.evaluatorVisible;
                        true->contents.evaluator.bindBottom;
                        THIS(moveableMenu).scan
                          (# 
                          do
                             (if openEvaluatorMenuName->(current.name).equal
                              then
                                 closeEvaluatorMenuName->current.name
                             if)
                          #);
                        contents.evaluator.calculateSize;
                        contents.evaluator.size->evalSize;
                        contents.list.size->listSize;
                        (listSize.h+2*bw,listSize.v+evalSize.v)
                          ->newPreferredContentsSize;
                        contents.evaluator.calculateSize;
                        contents.evaluator.show
                    if)
                 #)
            do (if openEvaluatorMenuEntry then
                   &actionedMenuItem[]->mi[];
                   (evaluatorAction##,none ,none ,openEvaluatorMenuName)->mi.open;
               if)
            #)
       #);
     hideEvaluatorAction:
       (# evalSize,listSize: @point
       do
          false->contents.evaluator.evaluatorVisible;
          false->contents.evaluator.bindBottom;
          movMenu.scan
            (# 
            do
               (if closeEvaluatorMenuName->(current.name).equal then
                   openEvaluatorMenuName->current.name
               if)
            #);
          contents.list.size->listSize;
          (2*bw,0)->listSize.add;
          listSize->newPreferredContentsSize;
          true->contents.list.bindBottom;
          contents.evaluator.hide
       #);
     contentsType::< 
       (#
          evaluator: @evaluatorCanvas
            (#
               onEvaluateButton::< 
                 (# 
                 do THIS(moveableObjectViewEvaluator).onEvaluateButton
                 #);
               onCloseButton::< 
                 (#  do hideEvaluatorAction; onCloseEvaluator #);
               calculateSize::< 
                 (# listPosition: @point
                 do
                    list.size->listSize;
                    list.position->listPosition;
                    (listPosition.h,listSize.v)->position;
                    false->list.bindBottom
                 #)
            #);
          open::<  (#  do contents[]->evaluator.open; evaluator.hide #)
       #);
     listEncloserType::< 
       (#
          newObjectView::< 
            (# moveableObjectViewType:: moveableObjectViewEvaluator #);
          
       #)
  #)  

-- lib: Attributes --
openEvaluatorMenuName: (#  exit 'Open Evaluator' #);
closeEvaluatorMenuName: (#  exit 'Close Evaluator' #)  

