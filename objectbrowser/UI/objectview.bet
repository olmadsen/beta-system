ORIGIN '~beta/toollibs/UI/moveable';
INCLUDE '../objectdumper'
        '../attribute'
        '~beta/guienv/scrolllists'
        '~beta/toollibs/UI/listmoveable'
        '~beta/toollibs/UI/icons';
LIB_DEF 'objview' '../../lib';
BODY 'private/objectviewbody';
-- guienvlib: Attributes --
objectViewList: List
  (# (*element:: window.objectView;*)
     onInsert:<
       (# theObjectView: ^window.objectView; 
       enter theObjectView[]
       do INNER
       #);
     onDelete:<
       (# theObjectView: ^window.objectView; 
       enter theObjectView[]
       do INNER
       #);
     newscan: scan
       (# cur: ^window.objectView;  do current[]->cur[]; INNER ;  #);
     
  #);
getobjectViewList: objectPool.get
  (# type::< objectViewList; init::  (#  do obj.init #) #);
  

-- windowLib: Attributes --
OpenObjectView:
  (#
     bo: ^betaObject;
     force: @Boolean;
     (* If TRUE, a new view is opened in any case. *)
     
  enter (bo[],force)
  do INNER
  #);
objectView: listMoveableList
  (#
     <<SLOT objectViewLib:Attributes>>;
     (* Open this objectView, but do not set any bindings. *)
     open::<  (#  do <<SLOT objectViewOpen:Descriptor>> #);
     close::< 
       (# 
       <<SLOT ObjectViewClose:DoPart>>
       #);
     onIconify::  (#  <<SLOT objectViewOnIconify:DoPart>> #);
     onDeIconify:: 
       (# 
       <<SLOT objectViewOnDeIconify:DoPart>>
       #);
     doUpdate::<  (#  <<SLOT objectViewDoUpdate:DoPart>> #);
     (* writeObj writes the representation of the object returned from
      * getObject to the scrolllist. Is called by open, and may later be
      * called to update the view. *)
     writeObj: <<SLOT objectViewWriteObject:Descriptor>>;
     eventHandler:: 
       (#
          onmouseDown:: 
            (# 
            do
               <<SLOT objectViewOnMouseDown:Descriptor>>
            #);
          onSelect::  (#  <<SLOT objectViewOnSelect:DoPart>> #);
          
       #);
     bo: ^betaObject;
     (* Fetched initially from encl.getObject. *)
     rootType: rootAttribute
       (#
          onComplexCreate:: 
            (#  <<SLOT objectViewOnComplexCreate:DoPart>> #);
          
       #);
     root: ^rootType;
     itemMenu: @menu
       (#
          <<SLOT itemMenuAttributes:Attributes>>;
          targetatt: ^attribute;
          (* When itemMenu is popped up, targetatt is the attribute
           * that was selected on popup. *)
          open::<  (#  do <<SLOT itemMenuOpen:Descriptor>> #);
          close::< 
            (# 
            <<SLOT itemMenuClose:DoPart>>
            #);
          imprivate: @<<SLOT itemMenuPrivate:Descriptor>>;
          (* Lists of actions to be performed when this menu is opened
           * or popped up: *)
          onOpen: @ActionList;
          onPopup: @ActionList;
          
       #);
     getencloser:<
       (# theEncloser: ^objectViewEncloser; 
       do INNER ; theEncloser[]->encl[]; 
       exit theEncloser[]
       #);
     encl: ^objectViewEncloser;
     ovprivate: @<<SLOT ovprivate:Descriptor>>;
     
  #);
objectViewEncloser:
  (#
     onHighLight:< Object;
     (* Called when the objectView should attract attention. *)
     getObject:< (# bo: ^betaObject do INNER exit bo[] #);
     (* Further bind to open a new object view. Called by objectView when user
      * clicks on dynamic reference. *)
     newObjectView:< OpenObjectView;
     onObjectChange:<
     (* Called when the object displayed changes. If isGarbage is TRUE,
      * the object displayed has become garbage. *)
       (# root: ^rootAttribute; isGarbage: @Boolean; 
       enter (root[],isGarbage)
       do INNER
       #);
     onLowLevelObjectChange:<
     (* Called when the object displayed changes to an object whose
      * type is unknown, or simply if we want to view low-level
      * information on the object entered. If isGarbage is TRUE,
      * the object displayed has become garbage. *)
       (# bo: ^betaObject; isGarbage: @Boolean; 
       enter (bo[],isGarbage)
       do INNER
       #);
     getMoveable:<
     (* If the objectView is enclosed by a moveable, getMoveable should
      * return it. *) (# mov: ^moveable;  do INNER exit mov[] #);
     tryclose:< Object;
     
  #);
OpenMoveableObjectView: openObjectView
  (#
     moveableObjectViewType:< moveableObjectView;
     getFather:< (# father: ^Canvas do INNER exit father[] #);
     theObjectView: ^moveableObjectViewType
  do <<SLOT openMoveableObjectView:Descriptor>>; INNER
  #);
MoveableObjectView: listmoveable
  (#
     <<SLOT moveableObjectViewLib:Attributes>>;
     theBo: ^betaObject;
     open::<  (#  enter theBo[] do INNER #);
     contentsType::< 
       (#
          listType::< ObjectView
            (# getencloser::<  (#  do listencloser[]->theEncloser[] #)
            #);
          
       #);
     listEncloserType:< objectViewEncloser
       (#
          newObjectView::< OpenMoveableObjectView
            (# getFather::<  (#  <<SLOT movGetFather:DoPart>> #) 
            do INNER 
            #);
          onHighlight::  (#  do wriggle #);
          getObject::  (#  do theBo[]->bo[] #);
          onObjectChange:: 
            (# 
            do
               <<SLOT moveableObjectViewonObjectChange:Descriptor>>
            #);
          onLowLevelObjectChange:: 
            (# 
            do <<SLOT moveableObjectViewonLowlewelObjectChange:Descriptor>>
            #);
          getMoveable::  (#  do THIS(moveableObjectView)[]->mov[] #);
          tryclose::  (#  do THIS(moveableObjectView).close #);
          
       #);
     listencloser: @listEncloserType;
     icon:: 
       (# 
       do 'MoveableObjectView_Icon'->(getIconDB).getIcon->theIcon[]; 
       #);
     moveableMenu::< 
       (#
          <<SLOT moveableObjectViewMenuLib:Attributes>>;
          open::< 
            (# 
            do
               <<SLOT moveableObjectViewMenuOpen:Descriptor>>
            #);
          close::<  (#  <<SLOT moveableObjectViewMenuClose:DoPart>> #);
          movmPrivate:
            @<<SLOT moveableObjectViewMenuPrivate:Descriptor>>
       #);
     
  #);
  

