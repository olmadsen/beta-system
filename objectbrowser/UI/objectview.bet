ORIGIN 'moveable';
BODY 'private/objectviewbody';

INCLUDE '../objectdumper';
INCLUDE '../attribute';
INCLUDE 'icons';
INCLUDE '~beta/guienv/v1.2/scrolllists';

--- windowLib: attributes ---

OpenObjectView: 
  (# bo: ^betaObject; 
     force: @Boolean; (* If TRUE, a new view is opened in any case. *)
  enter (bo[],force)
  do INNER   
  #);

objectView: TextScrollList
  (# <<SLOT objectViewLib:attributes>>;
     
     (* Open this objectView, but do not set any bindings. *)
     open::< (# do <<SLOT objectViewOpen:descriptor>> #);
     close::< (# <<SLOT ObjectViewClose:dopart>> #);
     
     (* Do the bindings that was omitted by open: *)
     dobindings: (# <<SLOT objectViewDoBinding:dopart>> #);
     
     (* Further bind to return the betaObject shown by this objectView: *)
     getObject:< (# bo: ^betaObject do INNER exit bo[] #);
     
     (* Further bind to open a new object view on the betaObject given
      * to openNewView. Called by objectView when user clicks on dynamic
      * reference. *)
     openNewView:< OpenObjectView;
     
     onObjectChange:< 
       (* Called when the object displayed changes. If isGarbage is TRUE,
        * the object displayed has become garbage. *)
       (# root: ^rootType; isGarbage: @Boolean;
       enter (root[],isGarbage)
       do INNER
       #);
     
     onHighlight:< Object;
     (* Called when this objectView should attract the users attention.
      * Use it to wriggle the surrounding moveable or the like. *)
     
     onIconify: (# <<SLOT objectViewOnIconify:dopart>> #);
     onDeIconify: (# <<SLOT objectViewOnDeIconify:dopart>> #);
     
     doUpdate:
       (* Called when doUpdate in the surrounding moveable is called.
        * The "iconified" parameter is the "iconified" attribute of the
        * moveable. If TRUE, writeObj is deferred until the moveable is
        * deiconified. *)
       (# iconified: @Boolean;
       enter iconified
       <<SLOT objectViewDoUpdate:dopart>>
       #);
     
     (* writeObj writes the representation of the object returned from
      * getObject to the scrolllist. Is called by open, and may later be
      * called to update the view. *)
     writeObj: <<SLOT objectViewWriteObject:descriptor>>;

     (* fit returns the preferred size of this objectView. It is calculated
      * by scanning the texts currently in the list. *)
     fit:
       (# newsize: @Point;
       do <<SLOT objectViewFit:descriptor>>
       exit newsize   
       #);
     
     eventHandler:: 
       (# onmouseDown:: (# do <<SLOT objectViewOnMouseDown:descriptor>> #)#);
     
     bo: ^betaObject;
     
     rootType: rootAttribute
       (# onComplexCreate:: (# <<SLOT objectViewOnComplexCreate:dopart>> #);
       #);
     root: ^rootType;
     
     itemMenu: @menu
       (# <<SLOT itemMenuAttributes: attributes>>;
          open::< (# do <<SLOT itemMenuOpen:descriptor>> #);  
          close::< (# <<SLOT itemMenuClose:dopart>> #);
          imprivate: @<<SLOT itemMenuPrivate:descriptor>>;
       #);     
     
     ovprivate: @<<SLOT ovprivate:descriptor>>;
  #);

OpenMoveableObjectView: openObjectView 
  (# getFather:< (# father: ^Canvas do INNER exit father[] #);
  do <<SLOT openMoveableObjectView:descriptor>>
  #);

MoveableObjectView: moveable
  (# <<SLOT moveableObjectViewLib:attributes>>;
     
     theBo: ^betaObject;
     open::<
       (# 
       enter theBo[]
       do INNER;
       #);
     
     doUpdate::<
       (# 
       do iconified->contents.view.doUpdate;
       #);
     
     onIconify:: (# do contents.view.onIconify #);
     onDeIconify:: (# do contents.view.onDeIconify #);
     
     contentsType::<
       (# view: @ObjectView
            (# getObject:: (# do theBo[] -> bo[] #);
               openNewview::< OpenMoveableObjectView
                 (# getFather::< (# <<SLOT movGetFather: dopart>> #)
                 #);
               onHighlight:: (# do wriggle #);
               onObjectChange:: 
                 (# 
                 do <<SLOT moveableObjectViewonObjectChange:descriptor>>
                 #);
            #);
          open::<
            (# 
            do view.open;
               view.size -> size; 
            #);
          dobindings::< 
            (# 
            do view.dobindings;
            #);
       #);
     
     icon::
       (#
       do 'MoveableObjectView_Icon' -> (getIconDB).getIcon -> theIcon[];
       #);
     
     moveableMenu::<
       (# <<SLOT moveableObjectViewMenuLib: attributes>>;
          open::< (# do <<SLOT moveableObjectViewMenuOpen: descriptor>> #);
          close::< (# <<SLOT moveableObjectViewMenuClose: dopart>> #);
          onPopup::< (# <<SLOT moveableObjectViewMenuOnPopup:dopart>> #);
          movmPrivate: @<<SLOT moveableObjectViewMenuPrivate: descriptor>>
       #);
  #);

