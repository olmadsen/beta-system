ORIGIN 'moveable';
BODY 'private/objectviewbody';

INCLUDE '../objectdumper';
INCLUDE '../attribute';
INCLUDE 'icons';
INCLUDE '~beta/guienv/v1.2/scrolllists';
INCLUDE 'listmoveable';

--- windowLib: attributes ---

OpenObjectView: 
  (# bo: ^betaObject; 
     force: @Boolean; (* If TRUE, a new view is opened in any case. *)
  enter (bo[],force)
  do INNER   
  #);

objectView: listMoveableList
  (# <<SLOT objectViewLib:attributes>>;
     
     (* Open this objectView, but do not set any bindings. *)
     open::< 
       (# 
       do <<SLOT objectViewOpen:descriptor>> 
       #);
     close::< (# <<SLOT ObjectViewClose:dopart>> #);
     
     (* Further bind to return the betaObject shown by this objectView: *)
     getObject:< (# bo: ^betaObject do INNER exit bo[] #);
     
     (* Further bind to open a new object view on the betaObject given
      * to openNewView. Called by objectView when user clicks on dynamic
      * reference. *)
     openNewView:< OpenObjectView;
     
     onObjectChange:< 
       (* Called when the object displayed changes. If isGarbage is TRUE,
        * the object displayed has become garbage. *)
       (# root: ^rootType; isGarbage: @Boolean;
       enter (root[],isGarbage)
       do INNER
       #);
     
     onHighlight:< Object;
     (* Called when this objectView should attract the users attention.
      * Use it to wriggle the surrounding moveable or the like. *)
     
     onIconify:: (# <<SLOT objectViewOnIconify:dopart>> #);
     onDeIconify:: (# <<SLOT objectViewOnDeIconify:dopart>> #);
     
     doUpdate::< (# <<SLOT objectViewDoUpdate:dopart>> #);
     
     (* writeObj writes the representation of the object returned from
      * getObject to the scrolllist. Is called by open, and may later be
      * called to update the view. *)
     writeObj: <<SLOT objectViewWriteObject:descriptor>>;

     eventHandler:: 
       (# onmouseDown:: (# do <<SLOT objectViewOnMouseDown:descriptor>> #)#);
     
     bo: ^betaObject;
     
     rootType: rootAttribute
       (# onComplexCreate:: (# <<SLOT objectViewOnComplexCreate:dopart>> #);
       #);
     root: ^rootType;
     
     itemMenu: @menu
       (# <<SLOT itemMenuAttributes: attributes>>;
          targetatt: ^attribute; 
          (* When itemMenu is popped up, targetatt is the attribute
           * that was selected on popup. *)
          open::< (# do <<SLOT itemMenuOpen:descriptor>> #);  
          close::< (# <<SLOT itemMenuClose:dopart>> #);
          imprivate: @<<SLOT itemMenuPrivate:descriptor>>;
          (* Lists of actions to be performed when this menu is opened
           * or popped up: *)
          onOpen: @ActionList;
          onPopup: @ActionList;
       #);     
     
     ovprivate: @<<SLOT ovprivate:descriptor>>;
  #);

OpenMoveableObjectView: openObjectView 
  (# getFather:< (# father: ^Canvas do INNER exit father[] #);
  do <<SLOT openMoveableObjectView:descriptor>>
  #);

MoveableObjectView: listmoveable
  (# <<SLOT moveableObjectViewLib:attributes>>;
     
     theBo: ^betaObject;
     open::< (# enter theBo[] do INNER #);
     
     contentsType::<
       (# listType::< ObjectView
            (# openNewview::< OpenMoveableObjectView
                 (# getFather::< (# <<SLOT movGetFather: dopart>> #)#);
               onHighlight:: (# do wriggle #);
               getObject:: (# do theBo[]->bo[] #);
               onObjectChange:: 
                 (# 
                 do <<SLOT moveableObjectViewonObjectChange:descriptor>>
                 #);
            #);
       #);
     
     icon::
       (#
       do 'MoveableObjectView_Icon' -> (getIconDB).getIcon -> theIcon[];
       #);
     
     moveableMenu::<
       (# <<SLOT moveableObjectViewMenuLib: attributes>>;
          open::< (# do <<SLOT moveableObjectViewMenuOpen: descriptor>> #);
          close::< (# <<SLOT moveableObjectViewMenuClose: dopart>> #);
          movmPrivate: @<<SLOT moveableObjectViewMenuPrivate: descriptor>>
       #);
  #);

