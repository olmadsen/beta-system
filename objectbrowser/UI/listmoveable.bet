ORIGIN '~beta/guienv/v1.3.1/scrolllists';
INCLUDE '~beta/guienv/v1.3.1/utils/guienvadds';
(* windowItem.{PreferredSize,BringToFront} *)
INCLUDE 'moveable';

--- lib:attributes ---
scrollBarWidth: (# exit 25 #);

--- textScrollListLib: attributes ---

(* fit returns the preferred size of this textScrollList  *)
fit:
  (# newsize: @Point; ts: ^textStyle;
  do 
     preferredSize->newsize; 
     (* Due to a guienv bug, only the width is correct. Make a guess for
      * the preferred heigth: *)
     style -> ts[];
     (newsize.h,((numberOfItems+3)*ts.lineHeight)+scrollBarWidth)
       ->newsize;
  exit newsize   
  #);

--- windowLib: attributes ---

listmoveableList: TextScrollList
  (# open::<
       (# defaultSize: @Point;
       enter defaultSize
       do defaultSize->size;
          INNER;
       #);
     dobindings:<
       (# 
       do TRUE->bindBottom->bindRight->bindLeft->bindTop;
          INNER;
       #);
     doUpdate:<
       (* Called when doUpdate in the surrounding moveable is called.
        * The "iconified" parameter is the "iconified" attribute of the
        * moveable. If TRUE, writeObj is deferred until the moveable is
        * deiconified. *)
       (# iconified: @Boolean
       enter iconified
       do INNER
       #);
     onDeIconify:< Object;
     onIconify:< Object;
     setNumberOfItems:
       (# lc: @Integer
       enter lc
       do (if true
           //(numberOfItems>lc) then
              (lc+1,numberOfItems-lc)->delete;
           //(numberOfItems<lc) then
              lc-numberOfItems->append;
          if);
       #);
  #);

listmoveable: moveable
  (# contentstype::<
       (# list: @listType;
          listType:< listmoveableList;
          open::<
            (#
            do (NONE,defaultSize)->list.open; 
               (if not (list.size->defaultSize.isEqual) then
                   list.size->size; 
               if);
               INNER;
            #);
          dobindings::<
            (# 
            do list.dobindings;
            #);
       #);
     doUpdate::< (# do iconified->contents.list.doupdate; INNER #);
     onIconify::< (# do contents.list.onIconify; INNER #);
     onDeIconify::< (# do contents.list.onDeIconify; INNER #);
     
     moveableMenu::<
       (# open::<
            (# mi: ^actionedMenuItem;
            do &actionedMenuItem[]->mi[];
               (listMoveableFitAction##,NONE,NONE,'Fit to contents')->mi.open;
               INNER;
            #);
       #);
     
     listMoveableFitAction: actionedMenuItemAction
       (# 
       do contents.list.fit->newPreferredContentsSize;
       #);
  #);
