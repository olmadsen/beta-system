ORIGIN '../obguienvadds';

INCLUDE '../../options';
INCLUDE '~beta/guienv/v1.6/controls';
INCLUDE '~beta/guienv/v1.6/utils/guienvadds';
INCLUDE '~beta/guienv/v1.6/utils/graphicsadds';
INCLUDE '~beta/guienv/v1.6/utils/walkingants';
INCLUDE '../icons';

--- VEUresizeRelative:descriptor ---
(# tmpFrame: @rectangle;
   ofw,ofh,nfw,nfh: @Integer;
do
   theEvent.oldFrame.size->(ofw,ofh);
   (* OldFatherWidth, OldFatherHeight *)

   theEvent.newFrame.size->(nfw,nfh);
   (* NewFatherWidth, NewFatherHeight *)

   frame->tmpFrame;

   ((tmpFrame.left*nfw) div ofw)->tmpFrame.left;
   ((tmpFrame.top*nfh) div ofh)-> tmpFrame.top;

   ((tmpFrame.right*nfw) div ofw)->tmpFrame.right;
   ((tmpFrame.bottom*nfh) div ofh)->tmpFrame.bottom;

   INNER resizeRelative;

   tmpFrame->frame;
#)

--- VEUitemPos:dopart ---
do (# cur: ^windowItem;
   do THIS(windowItem)[]->cur[];
      loop:
	(if cur[]<>NONE then
	    cur.position->pos.add;
	    cur.father[]->cur[];
	    restart loop;
	if);
   #);

--- FrameDrawerLib:attributes ---

drawOutline: graphics
  (# pat: ^pixmap;
     h,v: @Integer;
     overrideChildren:: (# do true -> value #);
     (* Draw on top of children of THIS(canvas) *)
  enter pat[]
  do pat[]->Pen.stipple;
     3->pen.size;
     size->(h,v);
     ((0,0),(h-1,v-1))->drawRect;
  #);

frameRefreshAction: action
  (# eventType:: theEventHandler.refresh;
  do patterns.black[]->drawOutline;
  #);

--- FrameDrawerKill:dopart ---
do (if fdprivate.wa[]<>NONE then
       fdprivate.wa.stop;
   if);
   (if fdprivate.fra[]<>NONE then
       fdprivate.fra[]->deleteAction;
   if);
   (if doupdate then update if)

--- FrameDrawerPrivate:descriptor ---
(# wa: ^walkingants;
   fra: ^frameRefreshAction;
   outline: @rectangle;
#)

-- FrameDrawerDo:dopart ---
do
   (if UseRunningOutline then
       (if fdprivate.wa[]=NONE then
	   (# h,v: @integer;
	      outline: ^rectangle;
	   do &walkingants[] -> fdprivate.wa[];
	      fdprivate.wa.init;
	      50-> fdprivate.wa.interval;
	      2->fdprivate.wa.pensize;
	      size -> (h,v);
	      &rectangle[]-> outline[];
	      ((0,0),(h-1,v-1))->outline;
	      (THIS(windowitem)[],outline[]) -> fdprivate.wa.add;
	   #)
       if);
       fdprivate.wa.start;
    else
       patterns.black[]->drawOutLine;
       (&frameRefreshAction[]->fdprivate.fra[])->appendAction;
   if);

--- canvasGetOutlineBody:descriptor ---
(# wa: ^walkingants;
   r: @statictext(*should be 'rect', but rect.bringToFront does not work*)
     (# inside: @rectangle;
	eventhandler::
	  (# onFrameChanged::
	       (#
	       do ((0,0),newframe.size)->inside;
		  (15,15)->inside.inset
	       #);
	     onmousedown::
	       (#
	       do wa.stop;
		  (if doubleclick then
		      NONE->mouse.busyCursor;
		      position->default.offset;
		      default->done; close
		   else
		      (if localPosition->inside.containsPoint then
			  (* Drag the rectangle *)
			  drag; size->default.size
		       else
			  (frame,FALSE)->this(Canvas).defineRect->frame;
			  size->default.size;
		      if);
		      wa.start
		  if)
	       #)
	  #);
	open::
	  (# do default->frame; ''->label #)
     #)

do THIS(window).bringToFront;
   (if wa[]=NONE then
       &walkingants[]->wa[];
       wa.init;
       50-> wa.interval;
       2->wa.pensize;
       this(Canvas)[]->r.open; r.bringToFront;
       ((0,0),r.size)->default;
       (r[],default[]) -> wa.add;
   if);
   wa.start;
   cursors.cross[]->mouse.busyCursor
#)
