ORIGIN '../obguienvadds';

INCLUDE '~beta/guienv/v1.3.1/private/X11/guienv_unixprivate';
INCLUDE '~beta/guienv/v1.3.1/private/X11/widget';
INCLUDE '~beta/guienv/v1.3.1/private/X11/raster_unixbody';

--- guienvlib:attributes ---
stripeslist:
  (# stripes: [8] ^raster;
  #);

-- graphicalEditorGetStripes: doPart --
do  (# loadRaster:
         (# path, name: ^text;
            theRaster: ^bitmap;
         enter (path[], name[])
         do path[]->name.copyPrepend->name[];
            &bitmap[]->theRaster[];
            name[]->theRaster.readFromX11File;
         exit theRaster[]
         #);
       sl: ^stripeslist;
    do 
       objectPool.get
       (# type:: stripeslist;
          init::
            (# ph: @pathhandler; path: ^text;
            do ph.init;
               ('~beta/bitmap/objectbrowser/', ph.currentDirectory)
                 ->ph.convertFilePath->path[];
               (path[],'stripes1.x11')->loadRaster->obj.stripes[1][];
               (path[],'stripes2.x11')->loadRaster->obj.stripes[2][];
               (path[],'stripes3.x11')->loadRaster->obj.stripes[3][];
               (path[],'stripes4.x11')->loadRaster->obj.stripes[4][];
               (path[],'stripes5.x11')->loadRaster->obj.stripes[5][];
               (path[],'stripes6.x11')->loadRaster->obj.stripes[6][];
               (path[],'stripes7.x11')->loadRaster->obj.stripes[7][];
               (path[],'stripes8.x11')->loadRaster->obj.stripes[8][];
            #);
       #)->sl[];
       sl.stripes->stripes;
    #)
   
--- obGrabPointer:descriptor ---
(# status: @Integer;
   mask: @Integer;
do
   XButtonReleaseMask + XButtonPressMask + XPointerMotionMask -> mask;
   (if widgetID>0 then
       (widgetID,
       0,
       mask,
       grabModeASync,
       grabModeAsync,
       0,
       0,
       currentTime)->XtGrabPointer->status;
       (if status
        // 0 then 
        // 1 then
           'Failed to grab X Pointer: allready grabbed' -> putline;
        // 2 then
           'Failed to grab X Pointer: invalid time' -> putline;
        // 3 then
           'Failed to grab X Pointer: not viewable' -> putline;
        // 4 then
           'Failed to grab X Pointer: frozen' -> putline;
       if);
    if);
#)

--- obUnGrabPointer: descriptor ---
(# 
do (widgetID,currentTime) -> XtUnGrabPointer;
#)
