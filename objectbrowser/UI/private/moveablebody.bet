ORIGIN '../moveable';

(* MOVEABLEBODY (Created by Soren Brandt, 16/8-1994)
 * ============
 * 
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1994
 *       All rights reserved.
 *)

INCLUDE '~beta/interfacebuilder/v1.0d/guienvadds/guienvadds';
(* windowItem.{PreferredSize,BringToFront} *)


--- moveableLib:attributes ---

popupMenu:
  (# menupos,itempos,mousepos: @Point;
  enter (itempos,mousepos)
  do 
     (if not moveablePrivate.menuOpened then 
         movMenu.open; TRUE->moveablePrivate.menuOpened;
     if);
     (if iconified then
         'Deiconify'->moveablePrivate.theIconifyItem.name
      else
         'Iconify'->moveablePrivate.theIconifyItem.name
     if);
     mousepos->menupos; itempos -> menupos.add;
     movMenu.onPopup;
     (1,menupos,THIS(window)[]) -> movMenu.popup;
  #);
     

moveableIcon: iconButton
  (# 
     doshow: (# do show; name.show #);
     
     name: @staticText
       (# mw,iw,ih: @Integer;
          open::
            (# title: ^Text; ts: ^textStyle
            do THIS(moveable).title -> title[] -> label;
               style->ts[];
               (title[] -> ts.widthOfText) + borderWidth -> mw;
               (mw,THIS(moveable).hh) -> size;
               THIS(moveableIcon).size -> (iw,ih);
               setpos;
               (* borderStyles.ShadowOut -> border.style;
                * true -> border.visible; *)
               true -> bindBottom -> bindLeft;
               false -> bindTop -> bindRight;
            #);
          setpos:
            (# x,y: @Integer;
            do THIS(moveableIcon).position -> (x,y);
               (x - ((mw - iw) div 2), y+ih-1) -> position;
            #);
          eventHandler::
            (# onMouseDown::
                 (# 
                 do (if doubleClick //true then
                        THIS(moveableIcon).hide; hide; 
                        THIS(moveable).show;
                     else
                        (if buttonState 
                         //1 then 
                            THIS(moveableIcon).drag; setpos;
                         //3 then
                            (name.itemPos,localPosition) -> popupMenu;
                        if);
                    if);
                 #);
            #);
       #);
     
     open:: 
       (# ic: ^raster;
       do hide;
          THIS(moveable).position -> position;
          THIS(moveable).icon -> ic[] -> icon;
          borderStyles.ShadowOut -> border.style;
          true -> border.visible;
          (ic.width+borderWidth,ic.height+borderWidth) -> size;
          false -> showLabel;
          father[] -> name.open;
          true -> bindBottom -> bindLeft;
          false -> bindTop -> bindRight;
          show;
       #);
     
     close:: (# do name.close #);
     
     eventHandler::
       (# onMouseDown::
            (# 
            do (if doubleClick then
                   deiconify;
                else
                   (if buttonState 
                    //1 then
                       THIS(moveableIcon).drag; name.setpos;
                    //3 then
                       (THIS(moveableIcon).position,localPosition)
                         -> popupMenu;
                   if);
               if);
            #);
       #);
     
     deiconify:
       (# 
       do THIS(moveableIcon).hide; name.hide; THIS(moveable).show;
          FALSE -> iconified;
          THIS(moveable).onDeIconify;
       #);
  #);

--- VEUresizeRelative:descriptor ---
(# tmpFrame: @rectangle;
   ofw,ofh,nfw,nfh: @Integer;
do
   action.oldFrame.size -> (ofw,ofh);
   (* OldFatherWidth, OldFatherHeight *)
   
   action.newFrame.size -> (nfw,nfh);
   (* NewFatherWidth, NewFatherHeight *)
   
   frame -> tmpFrame;
   
   ((tmpFrame.left*nfw) div ofw) -> tmpFrame.left;
   ((tmpFrame.top*nfh) div ofh) ->  tmpFrame.top;
   
   ((tmpFrame.right*nfw) div ofw) -> tmpFrame.right;
   ((tmpFrame.bottom*nfh) div ofh) -> tmpFrame.bottom;
   
   INNER resizeRelative;
   
   tmpFrame -> frame;
#)

--- VEUitemPos:dopart ---
do (# cur: ^windowItem;
   do THIS(windowItem)[]->cur[];
      loop:
        (if cur[]<>NONE then
            cur.position->pos.add;
            cur.father[]->cur[];
            restart loop;
        if);
   #);

(* MOVEABLE
 * ======== *)

--- VEUmoveableTitleEnter:dopart ---
do (if moveablePrivate.headerInitialized then
       t[] -> moveableHeader.label;
       (if moveablePrivate.theIcon[]<>NONE then
           t[]->moveablePrivate.theIcon.name.label
       if);
   if);
   t[] -> moveablePrivate.title[];
   
--- VEUmoveableTitleExit:dopart ---
do moveablePrivate.title[] -> t[]
   
--- VEUmoveableIconifyOpen:descriptor ---
(# 
do (hh-2*bw,hh-2*bw) -> size; (2*bw,2*bw) -> position;
   false -> showLabel;
   true -> border.visible;
   borderStyles.etchedIn -> border.style;
#)

--- VEUmoveableIconifyBindings:dopart ---
do true -> bindleft -> bindtop;
   

--- VEUmoveableIconifyOnMouseDown:descriptor ---
(# 
do trackMouse
   (# br: @rectangle;
      isDown: @Boolean;
      mousePress::
        (# 
        do borderStyles.shadowIn -> border.style;
           ((0,0),size) -> br.setFromPoints;
           TRUE->isDown;
        #);
      mouseMove::
        (# 
        do (if curPt -> br.containsPoint then
               (if not isDown then
                   borderStyles.shadowIn->border.style;
                   TRUE->isDown;
               if);
            else
               (if isDown then
                   borderStyles.etchedIn->border.style;
                   FALSE->isDown;
               if);
           if);
        #);
      mouseRelease::
        (# 
        do (if isDown then
               borderStyles.etchedIn -> border.style;
               iconify;
           if);
        #)
   #);
#)

--- VEUmoveableIconifyOnBorderStyleChanged:dopart ---
do (if moveablePrivate.openDone then true -> update; if)
   
--- VEUmoveableIconifyOnMouseUp:descriptor ---
(# 
#)
   
--- VEUmoveableIconify:dopart --- 
do hide;
   (if moveablePrivate.theIcon[] //NONE then
       &moveableIcon[] -> moveablePrivate.theIcon[];
       father[] -> moveablePrivate.theIcon.open;
    else
       moveablePrivate.theIcon.doshow;
   if);
   TRUE -> iconified;
   OnIconify;
   
--- VEUmoveableHeaderOpen:dopart ---
do (# ts: ^textStyle;
   do style->ts[];
      ts.lineHeight -> hh; 
   #)
   
--- VEUmoveableHeaderBindings:descriptor ---
(# fw,fh: @Integer;
do (if moveablePrivate.title[] //NONE then else
       moveablePrivate.title[] -> label;
   if);
   THIS(moveable).size -> (fw,fh);
   (fw-2*hh-2*bw,hh) -> size;
   (hh+bw,bw) -> position;
   borderStyles.shadowIn -> border.style;
   true -> moveablePrivate.headerInitialized; 
   true -> bindRight -> bindTop -> bindLeft; 
#)
   
--- VEUmoveableHeaderOnBorderVisibleChanged:dopart ---
do (if moveablePrivate.openDone then true -> update; if)
   
--- VEUmoveableHeaderOnMouseDown:descriptor ---
(# menupos: @Point;
do (if buttonState
    //1 then
       true -> border.visible;
       THIS(moveable).bringToFront;
       THIS(moveable).drag 
    //2 then (*THIS(moveable).bringBack;*)
    //3 then 
       (moveableHeader.itemPos,localPosition)->popupMenu;
   if);
#)

--- VEUmoveableHeaderOnMouseUp:dopart ---
do false -> border.visible;

--- VEUmoveableResizeopen:dopart ---
do

--- VEUmoveableResizeBindings:descriptor ---
(# w,h: @Integer;
do 
   (hh-2*bw,hh-2*bw) -> size;
   moveableHeader.size -> (w,h);
   (hh+w+2*bw,2*bw) -> position;
   false -> showLabel;
   true -> border.visible;
   borderStyles.etchedIn -> border.style;
   true -> bindright -> bindtop; false -> bindleft;
#)
   
   
--- VEUmoveableResizeOnMouseDown:descriptor ---
(# newFrame,oldFrame: @rectangle;
   w,h: @Integer;
do borderStyles.shadowIn -> border.style;
   THIS(moveable).frame -> oldFrame 
     -> THIS(moveable).father.defineRect 
     -> newFrame;
   (if not (oldFrame->newFrame.isEqual) then
       newFrame.size -> (w,h);
       (if (w < 3*hh) or (h < hh) then
           ((3*hh,w)->max,(hh,h)->max) -> newFrame.size;
       if);
       newFrame -> THIS(moveable).frame;
       onResized;
   if);
   borderStyles.etchedIn -> border.style;
#)

--- VEUmoveableResizeOnBorderStyleChanged:dopart ---
do (if moveablePrivate.openDone then true -> update; if)
   
   
--- VEUmoveableOpen:descriptor---
(# sz: @point; ml: ^moveableList;
do hide;
   true -> border.visible;
   borderStyles.shadowOut -> border.style;
   borderWidth -> bw;
   INNER open;
   moveableHeader.open; (* Sets hh *)
   moveableIconify.open; 
   moveableResize.open;
   contents.open;
   contents.size -> sz; (2*bw,hh+2*bw) -> sz.add; sz -> size;
   moveableHeader.dobindings;
   moveableIconify.dobindings;
   moveableResize.dobindings;
   contents.dobindings;
   true -> moveablePrivate.openDone;
   show;
   getMoveableList->ml[];
   THIS(moveable)[] -> ml.onInsert;
   THIS(moveable)[] -> ml.append;
#)

--- newPrefContentsSize:dopart ---
do (2*bw,hh+2*bw) -> sz.add; sz -> size;

--- moveableOnClose:dopart ---
do 
   TRUE->wasClosed;
   (# ml: ^moveableList;
   do getMoveableList -> ml[]; 
      THIS(moveable)[] -> ml.at -> ml.delete;
      THIS(moveable)[] -> ml.onDelete;
   #);
   (if moveablePrivate.theIcon[] //NONE then else
       moveablePrivate.theIcon.close;
   if);
   INNER close;
   
--- moveableWriggle:dopart ---
do (# w: ^windowItem;
   do 
      (if iconified then
          moveablePrivate.theIcon.deiconify;
      if);
      THIS(moveable)[] -> w[];
      w.bringToFront;
      (-10,0) -> w.move;
      (for i:10 repeat
           (if i mod 2 = 1 then
               (20-(2*i),0) -> w.move;
            else
               (-20+(2*i),0) -> w.move;
           if); 
      for);
      true -> w.update;
   #);
        
--- moveableInRect:dopart ---
do (# inside: 
        (# r2: @rectangle;
           value: @Boolean;
        enter r2
        do false -> value;
           (if r2.topLeft -> r.containsPoint then
               (if r2.bottomRight -> r.containsPoint then
                   true -> value
               if);
           if);
        exit value
        #);
   do
      (if iconified then
          (moveablePrivate.theIcon.frame -> inside) and
          (moveablePrivate.theIcon.name.frame -> inside) -> value;
       else
          THIS(moveable).frame -> inside -> value;
      if);
   #);
   
   
   
--- VEUmoveablePrivate:descriptor ---
(# title: ^Text;
   theIcon: ^moveableIcon;
   iconified: @Boolean;
   menuOpened: @Boolean;
   headerInitialized: @Boolean;
   openDone: @Boolean;
   theIconifyItem: ^movMenu.iconifyItem;
#)

--- moveableMenuOpen:descriptor ---
(# mi: ^menuItem;
do &closeItem[] -> mi[]; mi.open; mi[] -> append;
   &iconifyItem[] -> moveablePrivate.theIconifyItem[] -> mi[]; 
   mi.open; mi[] -> append;
   INNER open;
   (if onMovMenuOpen##<>NONE then onMovMenuOpen if);
#)
   
--- iconifyItemOpen:dopart ---
do INNER
   
--- iconifyItemOnSelect:dopart ---
do (if iconified then
       THIS(moveable).moveablePrivate.theIcon.deiconify;
    else
       THIS(moveable).iconify; 
   if);
   INNER;
   
