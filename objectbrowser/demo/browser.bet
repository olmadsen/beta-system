ORIGIN '~beta/guienv/v1.6/guienv';

INCLUDE '~beta/objectbrowser/v2.2/windowedbrowser';
INCLUDE '~beta/objectbrowser/v2.2/options';
INCLUDE '~beta/objectbrowser/v2.2/optionlist';
INCLUDE '~beta/objectbrowser/v2.2/UI/icons';
INCLUDE '~beta/objectbrowser/v2.2/UI/iconlist';
INCLUDE '~beta/guienv/v1.6/fields';
INCLUDE '~beta/basiclib/v1.6/formatio';
INCLUDE '~beta/sysutils/v1.6/envstring';

--- lib: attributes ---

initialMAINsize: (# exit (600,400) #);

breakpoint: external
     (# objAdr: @Integer
     enter objAdr
     #);

--- program:descriptor ---
guienv
(# clr: @rectangle;
   clearInProgress: @Boolean;
   
   main: @window
     (# menuBarType::<
          (# fileMenu: @Menu
               (# newItem: @menuitem
                    (# open:: (# do 'New' -> name; 'N' -> key #);;
                       eventHandler::
                         (# onSelect::<
                              (# h: ^humle;
                              do 
                                 &humle[]->h[];
                                 (10,'a',5,TRUE,4.5,main[]) -> h;
                                 h[]->browser.showObject;
                              #)
                         #)
                    #);
                  closeItem: @menuitem
                    (# open:: (# do 'Close' -> name; 'W' -> key #);
                       eventHandler:: 
                         (# onSelect:: 
                              (# m: ^moveableList;
                              do getMoveableList -> m[];
                                 m.scan
                                 (#
                                 do (if clr->current.inRect then 
                                        current.close 
                                    if);
                                 #);
                                 clr -> objworld.browserDrawRect;
                                 false -> clearInProgress;
                              #);
                            onStatus:: (# do clearInProgress -> value #);
                         #)
                    #);
                  quitItem: @menuitem
                    (# open:: (# do 'Quit' -> name; 'Q' -> key #);
                       eventHandler:: (# onSelect::< (# do Terminate #)#)
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       newItem.open; newItem[] -> append;
                       closeItem.open; closeItem[] -> append;
                       quitItem.open; quitItem[] -> append;
                    #);
               #);
             open::< (# do fileMenu.open; fileMenu[] -> append #)
          #);
        
        open::
          (# 
          do initialMAINsize -> size; 'Embedded Object Browser' -> title;
             contents->info.open; contents->objworld.open;
             objworld[]->browser.init;
          #);
        
        objworld: @Canvas
          (# open::
               (# sz: @Point; tmp: @Point;
               do initialMAINsize->sz;
                  info.size->tmp;
                  sz.v-tmp.v->sz.v;
                  sz->size;
                  TRUE->bindLeft->bindRight->bindBottom->bindTop;
               #);
             eventHandler::
               (# onMouseDown::
                    (#
                    do (if buttonState
                        //1 then
                           (if clearInProgress then clr->browserDrawRect if);
                           localPosition->SimpleDefineRect->clr;
                           (if not clr.isEmpty then
                               true -> clearInProgress;
                               clr->browserDrawRect;
                            else 
                               false -> clearInProgress;
                           if);
                       if);
                    #);
               #);
          #);
        
        info: @staticText
          (# open::<
               (# sz: @Point; ts: ^TextStyle;
                  pos: @Point;
               do 
                  style->ts[]; 
                  
                  InitialMAINsize->sz;
                  ts.lineHeight+6->sz.v;
                  sz->size;
                  
                  InitialMAINsize->pos;
                  0->pos.h;
                  pos.v-sz.v->pos.v;
                  pos -> Position;
                  
                  TRUE->bindLeft->bindRight->bindBottom; FALSE->bindTop;
                  
                  TRUE->border.visible;
                  borderStyles.shadowIn->border.style;
               #);
          #);

        browser: @windowedbrowser
          (# init:: 
               (# options: ^optionDB;
               do getOptionDB -> options[];
                  (charRepOnALine,TRUE) -> options.setBooleanOption;
                  (invisibleOrigins,TRUE) -> options.setBooleanOption;
                  (useShortObjectTitles,TRUE)->options.setBooleanOption;
                  (numberObjects,TRUE)->options.setBooleanOption;
               #);
             mpsinterfaceType::
               (# 
                  onOpenGroup::
                    (# t: ^Text;
                    do 'opening '->t[];
                       groupName[]->t.append;
                       t[]->main.info.label;
                       true->main.info.update;
                    #);
                  onOpenGroupDone::
                    (# 
                    do ''->main.info.label;
                       true->main.info.update;
                    #);
                  onOldGroup::
                    (#
                    do 'ERROR: "%s.%s" is older than "%s.%s". Recompile!!!\n'
                         -> putFormat
                       (# 
                       do groupName[]->s; AST.astFileExtension->s;
                          groupName[]->s; 'bet'->s;
                       #)
                    #);
                  onGroupNotFound::
                    (#
                    do 'ERROR: "%s.%s" not found !!! \n' -> putFormat
                       (# 
                       do groupName[]->s; AST.astFileExtension->s;
                       #);
                    #);
               #);
          #);
     #);
   
   mycomp:
     (# index: @Integer;
     do suspend;
     #);
   myvirt:< IntegerObject;
   myslot: <<SLOT myslot:descriptor>>;
   
   humle: IntegerObject
     (# c: @Char;
        s: @ShortInt;
        obj: @IntegerObject;
        b: @Boolean;
        r: @Real;
        ref: ^Object;
        t: @Text;
        p: ##Object;
        pNone: ##Object;
        orep: [10]^Object;
        prep: [10]##Object;
        stat: [10]@Text;
        cstat: [10]@|mycomp;
        slotobj: @<<SLOT myslot4:descriptor>>;
        slotstat: [10]@myslot;
        virtstat: [10]@myvirt;
        slot2stat: [10]@<<SLOT myslot2:descriptor>>;
        cslotstat: [10]@|myslot;
        csirtstat: [10]@|myvirt;
        cslot2stat: [10]@|<<SLOT myslot3:descriptor>>;
        rr: [50]@Real;
        ir: [50]@Integer;
        dims: ^Object;
     enter (c,s,b,r,ref[])
     do 'dette er en humle ' -> t;
        (for i:50 repeat i->ir[i] for);
        (for i:50 repeat i->rr[i] for);
        (for i:10 repeat THIS(humle)[]-> orep[i][] for);
        (for i:5 repeat main##-> prep[i]## for);
        humle##->p##;
        
        &(# #)[]->dims[];
     #);
   
   myIcons: @iconDB
     (# movIcon: ^bitmap;
        getIcon::
          (# 
          do (if TRUE
              //ObjectViewIcon -> name.equalNCS then
                 (if movIcon[]//NONE then
                     &bitmap[] -> movIcon[];
                     '$(BETALIB)/bitmap/objectbrowser/ObjectViewIcon.xbm' 
                       -> expandEnvVar
                     (# defaultvalue::
                          (# do '/usr/local/lib/beta/'->envvarvalue[] #) 
                     #) -> movIcon.readFromX11File;
                 if);
                 movIcon[] -> theIcon[];
             if);
          #);       
     #);

do main.open; myIcons[]->objectPool.put;
#)

--- myslot:descriptor ---
(# dyt: @integer; 
do 'myslot' ->putline;
#)
--- myslot2:descriptor ---
(# dyt2: @integer; 
do 'myslot 2' ->putline;
#)
--- myslot3:descriptor ---
(# baat: @integer; 
do 'myslot 3' ->putline;
#)
--- myslot4:descriptor ---
(# honk: @integer; 
do 'myslot 4' ->putline;
#)
