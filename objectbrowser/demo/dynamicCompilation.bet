ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/compiler/DYN/UI/evaluator'
        '~beta/objectbrowser/UI/evaluatormoveable'
        '~beta/objectbrowser/objectbrowserinterface'
        '~beta/sysutils/objinterface'
        '~beta/compiler/DYN/dynlib'
        '~beta/compiler/DYN/AstOfObject'
        '~beta/compiler/DYN/treelib'
        '~beta/guienv/fields'
        '~beta/sysutils/envstring'
        '~beta/sysutils/time';
-- lib: Attributes --
initialObjectBrowsersize: (#  exit (600,400) #);
breakpoint: external (# objAdr: @Integer enter objAdr #)  

-- program: Descriptor --
GUIenv
  (#
     a: @ (# b: @text; c: @real #);
     theObjectBrowserWindow: @standaloneBrowser
       (#
          objectViewType:: moveableObjectViewEvaluator
            (#
               onEvaluateButton:: 
                 (# 
                 do
                    (if theBO##
                     // mpsInt.localBetaObject## then
                          (#
                             originFGname,evaluatorCode,dynamicFGname: ^text;
                             aLocalBetaObject: ^mpsInt.localBetaObject;
                             FF: ^mpsInt.AST.fragmentForm;
                             FG: ^mpsInt.AST.fragmentGroup;
                             im: ^imagePair;
                             error: @boolean;
                             aMoveableList: ^moveableList;
                             constructUniqueID:
                               (#
                                  T: @Text;
                                  timeSec,timeMiliSec: @integer;
                                  timeInMiliSec: @real
                               do
                                  preciseTime->(timeSec,timeMiliSec);
                                  timeSec->timeInMiliSec;
                                  timeInMiliSec*1000000->timeInMiliSec;
                                  timeInMiliSec+timeMiliSec->timeInMiliSec;
                                  timeInMiliSec
                                    ->t.putReal (#  do 0->precision #)
                               exit t.copy
                               #)
                          do
                             theBO[]->aLocalBetaObject[];
                             aLocalBetaObject.o[]
                               ->mpsInt.AST.AstOfObject
                                 (#  do FG.name->originFGname[] #);
                             contents.evaluator.editor.contents.contents
                               ->evaluatorCode[];
                             'dynamicDescriptor'
                               ->mpsInt.BETACFL.makeBETAfragmentForm->FF[];
                             (mpsInt.BETA.objectDescriptor,evaluatorCode[],FF[])
                               ->mpsInt.BETACFL.parseText;
                             constructUniqueID->dynamicFGname[];
                             dynamicFGname[]
                               ->mpsInt.BETACFL.openBETAfragmentGroup->FG[];
                             FF.name->FG.fragmentList.deleteLocalName;
                             FF[]->FG.fragmentList.addFragment;
                             (if (FG.prop[] <> none ) then
                                 'ORIGIN'
                                   ->FG.prop.addProp
                                     (#  do originFGname[]->addString #);
                                 'doneCheck'
                                   ->FG.prop.addProp (#  do 0->addConst #);
                                 
                              else
                                 'Prop[] is none'->putLine
                             if);
                             FG.markAsChanged;
                             (FG.name,aLocalBetaObject.o[])
                               ->mpsInt.dc.activateCompiler->error;
                             (if not error then
                                 mpsInt.dc.getImagePair->IM[];
                                 (aLocalBetaObject.o[],IM[])
                                   ->mpsInt.dc.executeObject->showObject;
                                 getMoveableList->aMoveableList[];
                                 aMoveableList.scan
                                   (#  do current.doUpdate #)
                              else
                                 'An error happend during compilation'->putLine
                             if);
                             
                          #)
                     // betaObject## then
                        'betaObject'->putLine
                     else
                        'Unknown type of object in theBO##'->putLine
                    if);
                    INNER
                 #)
            #);
          mpsinterfaceType::< 
            (#
               DC: @AST.dynamicCompiler;
               init::< 
                 (# error: @boolean
                 do
                    dc.initDyn;
                    'Initialising the dynamic compiler... '->putText;
                    (1->Arguments, false)->DC.NT.init;
                    (1->Arguments,none )->DC.activateCompiler->error;
                    (if error then
                        'The initialisation of the dynamic compiler failed'
                          ->putLine;
                        stop
                     else
                        'done'->putLine
                    if);
                    INNER
                 #);
               
            #)
       #)
  do theObjectBrowserWindow.open; a[]->theObjectBrowserWindow.showObject
  #)  

