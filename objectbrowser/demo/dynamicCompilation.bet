ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/compiler/DYN/UI/evaluator'
        '~beta/objectbrowser/UI/evaluatormoveable'
        '~beta/objectbrowser/objectbrowserinterface'
        '~beta/sysutils/objinterface'
        '~beta/objectbrowser/windowedbrowser'
        '~beta/objectbrowser/options'
        '~beta/objectbrowser/optionlist'
        '~beta/compiler/DYN/dynlib'
        '~beta/compiler/DYN/AstOfObject'
        '~beta/compiler/DYN/treelib'
        '~beta/objectbrowser/UI/icons'
        '~beta/objectbrowser/UI/iconlist'
        '~beta/guienv/fields'
        '~beta/basiclib/formatio'
        '~beta/sysutils/envstring'
        '~beta/sysutils/time';
-- lib: Attributes --
initialObjectBrowsersize: (#  exit (600,400) #);
breakpoint: external (# objAdr: @Integer enter objAdr #)  

-- program: Descriptor --
GUIenv
  (#
     a: @ (# b: @text; c: @real #);
     theObjectBrowserWindow: @objectbrowserinterface
       (#
          browser::< 
            (#
               objectViewType::< moveableObjectViewEvaluator
                 (#
                    onEvaluateButton::< 
                      (# 
                      do
                         (if theBO##
                          // be.localBetaObject## then
                               (#
                                  originFGname,evaluatorCode,dynamicFGname:
                                    ^text;
                                  aLocalBetaObject: ^be.localBetaObject;
                                  FF: ^be.AST.fragmentForm;
                                  FG: ^be.AST.fragmentGroup;
                                  im: ^imagePair;
                                  error: @boolean;
                                  aMoveableList: ^moveableList;
                                  constructUniqueID:
                                    (#
                                       T: @Text;
                                       timeSec,timeMiliSec: @integer;
                                       timeInMiliSec: @real
                                    do
                                       preciseTime->(timeSec,timeMiliSec);
                                       timeSec->timeInMiliSec;
                                       timeInMiliSec*1000000->timeInMiliSec;
                                       timeInMiliSec+timeMiliSec->timeInMiliSec;
                                       timeInMiliSec
                                         ->t.putReal (#  do 0->precision #)
                                    exit t.copy
                                    #)
                               do
                                  theBO[]->aLocalBetaObject[];
                                  aLocalBetaObject.o[]
                                    ->be.AST.AstOfObject
                                      (#  do FG.name->originFGname[] #);
                                  contents.evaluator.editor.contents.contents
                                    ->evaluatorCode[];
                                  'dynamicDescriptor'
                                    ->be.BETACFL.makeBETAfragmentForm->FF[];
                                  (be.BETA.objectDescriptor,evaluatorCode[],
                                   FF[])->be.BETACFL.parseText;
                                  constructUniqueID->dynamicFGname[];
                                  dynamicFGname[]
                                    ->be.BETACFL.openBETAfragmentGroup->FG[];
                                  FF.name->FG.fragmentList.deleteLocalName;
                                  FF[]->FG.fragmentList.addFragment;
                                  (if (FG.prop[] <> none ) then
                                      'ORIGIN'
                                        ->FG.prop.addProp
                                          (# 
                                          do originFGname[]->addString
                                          #);
                                      'doneCheck'
                                        ->FG.prop.addProp
                                          (#  do 0->addConst #);
                                      
                                   else
                                      'Prop[] is none'->putLine
                                  if);
                                  FG.markAsChanged;
                                  (FG.name,aLocalBetaObject.o[])
                                    ->be.dc.activateCompiler->error;
                                  (if not error then
                                      be.dc.getImagePair->IM[];
                                      (aLocalBetaObject.o[],IM[])
                                        ->be.dc.executeObject->showObject;
                                      getMoveableList->aMoveableList[];
                                      aMoveableList.scan
                                        (#  do current.doUpdate #)
                                   else
                                      'An error happend during compilation'
                                        ->putLine
                                  if);
                                  
                               #)
                          // betaObject## then
                             'betaObject'->putLine
                          else
                             'Unknown type of object in theBO##'->putLine
                         if);
                         INNER
                      #)
                 #);
               mpsinterfaceType::< 
                 (#
                    DC: @AST.dynamicCompiler;
                    init::< 
                      (# error: @boolean
                      do
                         dc.initDyn;
                         'Initialising the dynamic compiler... '->putText;
                         1->Arguments->DC.NT.init;
                         (1->Arguments,none )->dc.activateCompiler->error;
                         (if error then
                             'The initialisation of the dynamic compiler failed'
                               ->putLine;
                             stop
                          else
                             'done'->putLine
                         if);
                         INNER
                      #);
                    
                 #);
               
            #)
       #)
  do
     theObjectBrowserWindow.open;
     a[]->theObjectBrowserWindow.aBrowser.showObject
  #)  

