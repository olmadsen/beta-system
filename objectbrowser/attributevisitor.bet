ORIGIN 'attribute';

--- lib:attributes ---
sevenTypeVisitor: attributeVisitor
  (* Collapses the currently 22 different attribute types into 7 different
   * attribute kinds: simple, complex, repetition or
   * repetitionElm and hidden repetition elements. Repetition and hidden 
   * repetition  elements are called directly. Other
   * attribute kinds are redirected to either onSimple, onComplex or 
   * onRepRefElm. *)
  (# 
     onSimple:< (# att: ^attribute enter att[] do INNER #);
     onComplex:< (# att: ^complexAttribute enter att[] do INNER #);
     (* onRepRef called directly. *)
     onRepRefElm:<
       (# att: ^repetitionAttribute;
          index: @Integer;
       enter (att[],index)
       do INNER
       #);
     (* onRefRep, onRepRepHiddenBefore, onRepRefHiddenAfter, and onPatRef
      * are not further bound. *)
          
     (* Simple attributes: *)
     
     onChar:: (# do att[]->onSimple #);
     onShortInt:: (# do att[]->onSimple #);
     onInt::  (# do att[]->onSimple #);
     onBool:: (# do att[]->onSimple #);
     onReal:: (# do att[]->onSimple #);
     onDynRef:: (# do att[]->onSimple #);
     onOriRef:: (# do att[]->onSimple #);
     onDynCompRef:: (# do att[]->onSimple #);
     
     (* Complex attributes: *)
     
     onStatRef:: (# do att[]->onComplex #);
     onStatCompRef:: (# do att[]->onComplex #);
     
     (* Repetition elements: *)
     onRepChar:: (# do (att[],index)->onRepRefElm #);
     onRepShortInt:: (# do (att[],index)->onRepRefElm #);
     onRepInt:: (# do (att[],index)->onRepRefElm #);
     onRepBool:: (# do (att[],index)->onRepRefElm #);
     onRepReal:: (# do (att[],index)->onRepRefElm #);
     onRepDynRef:: (# do (att[],index)->onRepRefElm #);
     onRepDynCompRef:: (# do (att[],index)->onRepRefElm #);
     onRepPatRef:: (# do (att[],index)->onRepRefElm #);
  #);

oneTypeVisitor: attributeVisitor
  (# onAll:<
       (# att: ^attribute;
       enter att[]
       do INNER
       #);
     onChar:: (# do att[]->onAll #);
     onShortInt::  (# do att[]->onAll #);
     onInt::  (# do att[]->onAll #);
     onBool:: (# do att[]->onAll #);
     onReal:: (# do att[]->onAll #);
     onDynRef:: (# do att[]->onAll #);
     onOriRef:: (# do att[]->onAll #);
     onStatRef:: (# do att[]->onAll #);
     onPatRef:: (# do att[]->onAll #);
     onDynCompRef:: (# do att[]->onAll #);
     onStatCompRef:: (# do att[]->onAll #);
     onRepRef::  (# do att[]->onAll #);
     onRepRefHiddenBefore:: (# do att[]->onAll #);
     onRepRefHiddenAfter:: (# do att[]->onAll #);
     onRepChar:: (# do att[]->onAll #);
     onRepShortInt:: (# do att[]->onAll #);
     onRepInt:: (# do att[]->onAll #);
     onRepBool:: (# do att[]->onAll #);
     onRepReal:: (# do att[]->onAll #);
     onRepDynRef:: (# do att[]->onAll #);
     onRepDynCompRef:: (# do att[]->onAll #);
     onRepPatRef:: (# do att[]->onAll #);
  #);

