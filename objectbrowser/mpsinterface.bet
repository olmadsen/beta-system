ORIGIN '~beta/basiclib/v1.4/betaenv';
BODY 'private/mpsinterfacebody';

INCLUDE '~beta/betaast/v4.9/betasematt';
INCLUDE '~beta/betaast/v4.9/gram';
INCLUDE '~beta/mps/v4.9/astlevel';
INCLUDE '~beta/containers/v1.4/hashTable';
INCLUDE '~beta/sysutils/v1.4/objinterface';

--- lib:attributes ---

mpsinterface: 
  (# 
     <<SLOT mpsinterfacelib: Attributes>>;
     
     AST: @astInterface;
     BETACFL: @AST.beta;
     BETA: @grammar; (* From '~beta/betaast/v4.9/gram'    *)
     
     astRefToAst:
       (* Exits the Ast.ast identified by:
        *    fg: Fragment group.
        *    formIndex: Form number in fragment group.
        *    astIndex: ast index in the form. *)
       (# astType:< AST.ast;
          fg: ^AST.fragmentGroup; formIndex,astIndex: @Integer;
          a: ^astType;
          (*private:*) ff: ^AST.fragmentForm;
       enter (fg[],formIndex,astIndex)
       <<SLOT ptToObjectDescriptorBody:dopart>>
       exit a[]
       #);
     
     fragmentGroupTable: @HashTable
       (* Table of fragmentGroups that have already been opened. *)
       (#
          <<SLOT fragmentGroupTable: attributes>>;
          
          element::
            (# groupName: ^Text;
               fg: ^AST.fragmentGroup;
            #);
          
          hashFunction::
            (# 
            do e.groupName.scanAll (# do ch*26 + value -> value #)
            #);
          
          getFragmentGroup:
            (* If the requested group is not already in the table, 
             * it is opened and inserted into the table.
             *)
            (# groupName: ^Text;
               foundGroup: ^AST.fragmentGroup;
            enter groupName[]
            do <<SLOT getFragmentGroupBody: descriptor>>
            exit foundGroup[]
            #)
       #);
     
     onOpenGroup:< 
       (* Called when a fragmentgroup is about to be opened. *)
       (# groupName: ^Text;
       enter groupName[]
       do INNER
       #);
     onOpenGroupDone:<
       (* Called when a fragmentgroup has been opened. *)
       (# groupName: ^Text; fg: ^AST.fragmentGroup;
       enter (groupName[],fg[])
       do INNER
       #);
     
     onOldGroup:< Exception
       (* Called when a fragmentgroup needs to be parsed. 
        * It is not possible to continue after this error has occurred. *)
       (# groupName: ^Text;
       enter groupName[]
       do INNER
       #);
     onGroupNotFound:< Exception
       (* Caled when a fragmentgroup could not be found. *)
       (# groupName: ^Text;
       enter groupName[]
       do INNER
       #);
       
     
     init:< (# <<SLOT mpsinterfaceInitBody:dopart>> #);
  do INNER
  #);
