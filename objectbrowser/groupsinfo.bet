ORIGIN 'mpsinterface';
BODY 'private/groupsinfobody';

INCLUDE '~beta/containers/v1.4/hashTable';

--- mpsinterfacelib:attributes ---

getGroupsInfoTable: objectPool.get
  (# type::< groupsInfoTable; init::< (# do obj.init; INNER #)#);

groupsInfoTable: HashTable
  (# <<SLOT groupsInfoTableLib:attributes>>;
     
     protoGroupToOD:
       (* Call protoGroupToOD to convert a (proto,groupName) pair into the
        * corresponding ObjectDescriptor.
        * 
        * When the od has been found, INNER is called with elm set to the
        * element corresponding to group, and od set to the ObjectDescriptor 
        * about to be returned. *)
       (# proto: @Integer; groupName: ^Text;
          elm: ^element;
          od: ^BETACFL.ObjectDescriptor;
       enter (proto,groupName[])
       <<SLOT groupsInfoProtoGroupToOD:dopart>>
       exit od[]
       #);
     
     ODtoProtoGroup:
       (* The inverse of protoGroupToOD *)
       (# proto: @Integer; groupName: ^Text;
          elm: ^element;
          od: ^BETACFL.ObjectDescriptor;
       enter od[]
       <<SLOT groupsInfoODtoProtoGroup:dopart>>
       exit (proto,groupName[])
       #);
     
     element::<
       (# fg: ^AST.fragmentGroup;
          ods: [30]^BETACFL.ObjectDescriptor;
          last: @Integer;
          groupName: ^Text;
          append:<
            (# od: ^BETACFL.ObjectDescriptor;
               onExtend:< Object;
            enter od[]
            do last+1->last;
               (if last>ods.range then
                   ods.range->ods.extend;
                   onExtend;
               if);
               od[]->ods[last][];
            #);
       #);
     hashfunction:: (# <<SLOT groupsinfohashfunction:dopart>> #);
     
     (* onScan and onScanDone are called before and after the scanning
      * of a fragment group to generate an element mapping proto numbers
      * to ObjectDescriptors. *)
     onScan:< (# groupName: ^Text enter groupName[] do INNER #);
     onScanDone:< (# groupName: ^Text enter groupName[] do INNER #);
     
     init::<
       (# 
       do INNER;
       #);
  #);

