ORIGIN '~beta/designenv/v1.5/designenv';
-- DesignEnvAttributes: Attributes --
IndexIDListDesc:
  (#
     impl: [64] @Integer;
     last: @Integer;
     insert:<
       (# a,i: @Integer;
       enter (a,i)
       do
          (if last+2 > impl.range // true then impl.range->impl.extend if);
          (if a <> 0
           // true then a->impl[last+1]; i->impl[last+2]; last+2->last;
          if);
          INNER ;
       #);
     remove:<
       (# a,i: @Integer
       enter a
       do
          1->i;
          loop:
            (#
            do
               (if impl[i]
                // a then 0->impl[i+1];
                else
                   2+i->i; (if i < last // true then restart loop if);
               if)
            #);
       #);
     indexToObject:<
       (# a,i,o: @Integer; ido: ^IDObject
       enter a
       do
          1->i;
          loop:
            (#
            do
               (if impl[i]
                // a then impl[i+1]->o;
                else
                   2+i->i; (if i < last // true then restart loop if);
               if)
            #);
          (if (o <> 0)
           // true then o->theDocument.theObjectList.Find->ido[]
          if);
       exit ido[]
       #);
     scan:
       (# a,i,o: @Integer; currentNode: ^IDObject
       enter a
       do
          1->i;
          loop1:
            (#
            do
               loop2:
                 (#
                 do
                    (if impl[i]
                     // a then impl[i+1]->o;
                     else
                        2+i->i;
                        (if i < last // true then restart loop2 if);
                    if)
                 #);
               (if (o <> 0)
                // true then o->theDocument.theObjectList.Find->currentNode[]
               if);
               (if currentNode[] <> none then INNER scan; if);
               2+i->i;
               (if i < last // true then restart loop1 if);
            #)
       #);
     update:<
       (# oldAstIndex,newAstIndex,i,o: @Integer; ido: ^IDObject
       enter (oldAstIndex,newAstIndex)
       do
          1->i;
          loop:
            (#
            do
               (if impl[i]
                // oldAstIndex then impl[i+1]->o; newAstIndex->impl[i];
                else
                   2+i->i; (if i < last // true then restart loop if);
               if)
            #);
          (if (o <> 0)
           // true then o->theDocument.theObjectList.Find->ido[]
          if);
       exit ido[]
       #);
  #);  

