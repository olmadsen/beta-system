ORIGIN 'diagramattributes';
-- PatternDiagramConcludeDisplay: DoPart --
do
     (#
        p: ^localNodes.theCellType;
        e: ^LocalNodes.element;
        connectorList: ^ObjectList;
        thisListDiagram,theParentDiagram: ^ListDiagram;
        thePatternNode: ^theParentDiagram.PatternNode;
        theAbstractNode: ^theParentDiagram.AbstractNode
     do
        (if switch[30] then
            '******** PatternDeclDiagramConcludeDisplay calling new with ********'
              ->putline;
            'x: '->putText;
            dist.x->putint;
            newLine;
            'y: '->putText;
            dist.y->putint;
            newLine;
            'w: '->putText;
            gppProp.titlewidth->putint;
            newLine;
            'h: '->putText;
            gppProp.height*2->putint;
            newLine;
            
        if);
        (if gppProp.DiagramPositioning
         // gppProp.AllManual then
            (dist.x,dist.y,gppProp.titlewidth*2,gppProp.height,defaultShape)
              ->new
        if);
        theDiagram->thisListDiagram[];
        (if theParentNode <> none then
            (theParentNode).theDiagram->theParentDiagram[];
            (if (theParentNode).struc <= theParentDiagram.AbstractNode## then
                theParentNode->theAbstractNode[];
                thisListDiagram[]->theAbstractNode.currentDecomposDiagram
            if);
            (theParentNode).getConnectors->connectorList[];
            (if connectorList[] <> none then
                connectorList.scan
                  (#
                     theConn: ^DeletableConnector;
                     OtherEnd: ^PatternDiagramNode;
                     theOtherListDiagram: ^ListDiagram;
                     theOtherSimpleNode: ^theOtherListDiagram.SimpleNode
                  do
                  (*                      (if (current## <= DynamicItemConnector##) or
                   *                      (current## <= DynamicComponentConnector##) then
                   *                          current[]->theConn[];
                   *                          theParentNode->theConn.getOtherEnd->OtherEnd[];
                   *                          OtherEnd.theDiagram->theOtherListDiagram[];
                   *                          OtherEnd[]->theOtherSimpleNode[];
                   *                          (if theOtherListDiagram[] <> thisListDiagram[] then
                   *                              (OtherEnd.getTopParent,OtherEnd[],getTopParent,THIS
                   *                               (titleNode)[])->theConn.setEnds
                   *                           else
                   *                              none ->theOtherSimpleNode.theReferenceConnector;
                   *                              theConn.delete
                   *                          if)
                   *                      if)
                   *) 
                  #)
            if)
        if);
        (if localNodes.empty then
            (dist,titleNode.Center)->UpdateNextFree; 
         else
            localNodes.scan
              (# thisSimpleNode: ^thisListDiagram.SimpleAttributeDecl
              do
                 (0,0)->current.ConcludeDisplay;
                 (if current## <= thisListDiagram.SimpleAttributeDecl## then
                     current[]->thisSimpleNode[];
                     thisSimpleNode.DisplayReference;
                     
                 if)
              #);
            localNodes.Last->p[];
            p.elm[]->e[];
            (dist,e.Center)->UpdateNextFree
        if);
        (if theAbstractNode[] <> none then
            (if theAbstractNode## <= theParentDiagram.PatternNode## then
                theAbstractNode[]->thePatternNode[];
                thePatternNode.DisplayAssociations
            if)
        if);
        INNER concludeDisplay
     #)  

-- PatternDiagramOnInit: Descriptor --
(# pos: @Point; w1,h1: @Integer; x2,y2,w2,h2: @integer; 
do
   UDPrivate.UDPatternDiagram->UserDataInit;
   (if not initialisingSaved then
       titleText->theText.set;
       titleNode[]->updateMaxWH;
       (if gppProp.ShowLocals
        // true then
           THIS(patternDiagram).titleNode[]->THIS(OADPage).scrollIntoView;
           (x-(w+1) div 2,y+(h+1) div 2)->pos;
           pos->DisplayDeclarations;
           
       if);
       (if localNodes.empty then
           (x-(w div 2)+(MaxWidth div 2),y,MaxWidth,GppProp.Height)->geometry
       if)
   if);
   
#)
(****************** PatternAttDiagram *******************)  

-- PatternDiagramNodeOnSelect: DoPart --
do
   (if theSifEditor <> none then
       (if (theSifEditor).isReadOnly then setReadOnly else unsetReadOnly if)
    else
       setReadOnly
   if);
   INNER onSelect  

-- PatternDiagramNodeDump: Descriptor --
(# x,y,w,h: @integer; connectors: ^ObjectList
do
   '********connectors********'->putline;
   getConnectors->connectors[];
   (if connectors[] <> none then
       connectors.scan (#  do current.ID->putint; ' '->puttext #); newline
    else
       'none'->putline
   if);
   '********theDiagram********'->putline;
   (if theDiagram <> none then
       (theDiagram).titleText[]->putline
    else
       'is NONE!'->putline
   if);
   '********astIndex********'->putline;
   astIndex->putint;
   newline;
   '********pos and size********'->putline;
   geometry->(x,y,w,h);
   'x: '->putText;
   x->putint;
   newLine;
   'y: '->putText;
   y->putint;
   newLine;
   'w: '->putText;
   w->putint;
   newLine;
   'h: '->putText;
   h->putint;
   newLine
#)  

-- OADDiagramOnInit: Descriptor --
(#  do UDPrivate.UDOADDiagram->UserDataInit; UDtheDescriptor.Init #)  

-- OADDiagramDump: Descriptor --
(# 
do
   '********theDescriptor********'->putline;
   (if theDescriptor <> none then
       (theDescriptor).dump; newline
    else
       'is NONE!'->putline
   if)
#)  

-- PatternAttDiagramDump: Descriptor --
(# 
do
   '********theAST********'->putline;
   (if theAST <> none then
       (theAST).dump; newline
    else
       'is NONE!'->putline
   if)
#)  

-- PatternDiagramNodeOnInit: Descriptor --
(# 
do
   UDPrivate.UDPatternDiagramNode->UserDataInit;
   theDiagram.Init;
   invisibleText.init;
   theMarkNode.Init;
   defaultShape.Init;
   astIndex.Init
#)  

-- PatternAttDiagramDisplayDeclarations: Descriptor --
(# anAST: ^MPS.AST do theAST->anAST[]; anAST[]->theAttributes[];  #)  

-- PatternAttDiagramNew: Descriptor --
(# 
do
   (if anAST.symbol
    // betaGram.Attributes then
       anAST[]->theAST;
       shape.RndRect->Display;
       (anAST.index,anAST.frag.fullname,THIS(PatternAttDiagram)[])
         ->PatternDiagrams.theList.insertPD;
       
    else
       'PatternAttDiagram New: unknown AST symbol'->putline; 
   if);
   
#)
(****************** PatternDeclDiagram *******************)  

-- PatternAttDiagramOnInit: Descriptor --
(# 
do UDPrivate.UDPatternAttDiagram->UserDataInit; UdtheAST.Init; 2->lineThickness
#)  

-- PatternDeclDiagramDump: Descriptor --
(# 
do
   '********thePrefix********'->putline;
   (if thePrefix <> none then
       (thePrefix).dump; newline
    else
       'is NONE!'->putline
   if)
#)  

-- PatternDeclDiagramDisplayDeclarations: Descriptor --
(# main: ^betaGram.MainPart; theDummyDesc: ^betaGram.ObjectDescriptor
do
   (if theDescriptor
    // none then none ->theAttributes[]; 
    else
       theDescriptor->theDummyDesc[];
       theDummyDesc.getMainPart->main[];
       main.getAttributes->theAttributes[];
       
   if);
   
#)  

-- PatternDeclDiagramDisplay: Descriptor --
(#
   x,y: @Integer;
   subList: ^DiagramList;
   prefixDiagram: ^PatternDiagram;
   e: ^localNodes.element;
   theDummyDesc: ^betaGram.ObjectDescriptor;
   theDiagramNode: ^DiagramNode;
   anAST,theFather: ^MPS.AST
do
   (if theDescriptor
    // none then (* I must be generated from a non-terminal *)
       none ->thePrefix; 
    else
       theDescriptor->theDummyDesc[];
       (if theDummyDesc.frag.father->isChecked
        // true then
           theDescriptor->getPrefix->thePrefix;
           (if thePrefix <> none then
               theDummyDesc.father->theFather[];
               (if theFather## <= betaGram.AttributeDecl## then
                   (theFather[],(thePrefix).father)
                     ->patternDiagrams.InheritanceList.insert
                else
               (*could be a singular*)
                   theFather.father->theFather[];
                   (if theFather[] <> none then
                       (if theFather## <= betaGram.AttributeDecl## then
                           ((theDummyDesc.father).father,(thePrefix).father)
                             ->patternDiagrams.InheritanceList.insert
                       if)
                   if)
               if)
           if)
        else
           theParentNode->theDiagramNode[];
           (if theDiagramNode[] <> none then
               (if theDiagramNode.thePrefix <> none then
                   theDiagramNode.thePrefix->thePrefix
               if)
           if)
       if);
       
   if);
   (if thePrefix <> none
    // true then (* find diagram *)
       ((thePrefix).index,(thePrefix).frag.fullname)
         ->PatternDiagrams.theList.findPD->prefixDiagram[];
       (if prefixDiagram[]
        // none then
           (PatternDiagrams.NextFreeColumn,PatternDiagrams.NextFreeLine)
             ->theCenter;
           
        else
           prefixDiagram[]->PatternDiagrams.theList.GetSubPatternDiagrams
             ->subList[];
           (if subList.size
            // 0 then (* I am the first subpat of perfixDiagram[] *)
               (if prefixDiagram.localNodes.size > 0
                // true then
                   prefixDiagram.localNodes.Last
                     (#  do position.elm[]->e[]; e.Center->(x,y) #);
                   
                else
                   prefixDiagram.titleNode.Center->(x,y); 
               if);
               prefixDiagram.titleNode.Center->theCenter;
               (if x-gppProp.width > theCenter.x
                // true then
                (* the last box is not in the same column as the
                 * title:  use center of the last box *)
                   x->theCenter.x; 
               if);
               y+gppProp.DownFromPrefix->theCenter.y;
               
            else
               (- 10000,- 10000)->theCenter;
               subList.scanPatternDiagrams
                 (# p1: @Point; 
                 do
                    (if thisDiagram[]
                     // THIS(PatternDeclDiagram)[] then 
                     else
                        thisDiagram.titleNode.Center->p1;
                        (if p1.x > theCenter.x
                         // true then p1->theCenter
                        if);
                        
                    if);
                    
                 #);
               theCenter.x+gppProp.width+gppProp.RightFromPrevious->theCenter.x;
               
           if)
       if)
   if);
   
#)  

-- PatternDeclDiagramConcludeDisplay: DoPart --
do
     (#
        prefixDiagram,superDiagram: ^PatternDiagram;
        subList: ^DiagramList;
        x1,y1,x2,y2: @integer
     do
        (if gppProp.DiagramPositioning = gppProp.AllAuto then
            (if thePrefix <> none then
                ((thePrefix).index,(thePrefix).frag.fullname)
                  ->PatternDiagrams.theList.findPD->prefixDiagram[];
                (if prefixDiagram[] <> none then
                    prefixDiagram[]->prettyprintSubPatterns
                if);
                (* getTopMostSuper:
                 (if prefixDiagram[] <> none then
                 prefixDiagram[]
                 ->patternDiagrams.inheritanceList.getSuperPatternDiagram
                 ->superDiagram[];
                 (if superDiagram[] <> none then
                 superDiagram[]->prefixDiagram[]; restart getTopMostSuper
                 else
                 prefixDiagram[]->prettyprintSubPatterns
                 if)
                 if)*)
                
             else
            (* I am the top-most super-pattern *)
                THIS(PatternDeclDiagram)[]
                  ->PatternDiagrams.inheritanceList.GetSubPatternDiagrams
                  ->subList[];
                (if not subList.empty then
                    subList.scan
                      (#  do current.e.titleNode.unMakeRegion #);
                    (subList.head).elm.e.titleNode.center->(x1,y1);
                    (subList.last).elm.e.titleNode.center->(x2,y2);
                    ((x1+x2) div 2,((y1,y2)->min)-
                     (gppProp.DownFromPrefix+localNodes.size*gppProp.height))
                      ->titleNode.center;
                    subList.scan
                      (# 
                      do titleNode[]->current.e.titleNode.createRegion
                      #)
                if)
            if)
        if)
     #)  

-- PatternDiagramDisplayPrefix: Descriptor --
(# prefixDiagram: ^PatternDiagram; con: ^PrefixConnector; 
do
   (if switch[50]
    // true then 'PatternDiagramDisplayPrefix'->screen.putline; 
   if);
   (if theDescriptor <> none then
       (if thePrefix <> none then (* is the prefix displayed on this page *)
           ((thePrefix).index,(thePrefix).frag.fullname)
             ->PatternDiagrams.theList.findPD->prefixDiagram[];
           (if prefixDiagram[] <> none then
           (* Yes, go make a connector between the diagrams *)
               &PrefixConnector[]->con[];
               (titleNode[],prefixDiagram.titleNode[])->con.new;
               prefixDiagram.titleNode[]->titleNode.CreateRegion;
               (* and make THIS to a region of prefix *)
               con[]->titleNode.thePrefixConn;
               
           if)
        else
       (* No prefix *)
           
       if);
       (* now check for any shown subpatterns of this descriptor *)
       PatternDiagrams.theList.scanPatternDiagrams
         (# con: ^PrefixConnector; theDummyDesc: ^betaGram.ObjectDescriptor
         do
            (if thisDiagram.thePrefix <> none then
                theDescriptor->theDummyDesc[];
                (if thisDiagram.thePrefix->theDummyDesc.equal then
                (* yes this diagram *)
                    &PrefixConnector[]->con[];
                    (thisDiagram.titleNode[],titleNode[])->con.new;
                    titleNode[]->thisDiagram.titleNode.CreateRegion;
                    con[]->thisDiagram.titleNode.thePrefixConn;
                    
                if)
            if)
         #);
       
    else
       (if switch[50]
        // true then 'DisplayPrefix: No Descriptor!!!'->putline
       if);
       
   if);
   
#)  

-- PatternDeclDiagramDisplayInheritance: Descriptor --
(#
   prefixDiagram,subDiagram: ^PatternDeclDiagram;
   con: ^PrefixConnector;
   theParent,theNode: ^PatternDiagramNode;
   superNode,subNode: ^theListDiagram.AbstractNode;
   theListDiagram: ^ListDiagram;
   theDiagramNode: ^theListDiagram.DiagramNode;
   theDecl,theSuperDecl: ^betaGram.AttributeDecl
do
   (if switch[50] then 'DisplayInheritance: '->putline if);
   (if not patternDiagrams.InheritanceList.empty then
       (if theParentNode <> none then
           theParentNode->theParent[];
           theParent.theDiagram->theListDiagram[];
           (if theParent## <= theListDiagram.DiagramNode## then
               theParent[]->theDiagramNode[];
               theDiagramNode.theDeclaration->theDecl[];
               patternDiagrams.InheritanceList.scan
                 (# ff: ^mps.fragmentform; t: @Text
                 do
                    (if (theDecl.index = current.sub) and
                    (theDecl.frag.fullname->current.subFrag.equal) then
                        current.superFrag[]->topDotOpen->ff[];
                        current.super->ff.indexToNode->theSuperDecl[]->AstToNode
                          ->theNode[];
                        theSuperDecl.getSon2->theDiagramNode.thePrefix
                          ->thePrefix;
                        (if theNode[] <> none then
                            theNode.theDiagram->theListDiagram[];
                            theNode[]->superNode[];
                            (if superNode.CurrentDecomposDiagram <> none then
                                superNode.CurrentDecomposDiagram
                                  ->prefixDiagram[];
                                &PrefixConnector[]->con[];
                                (* to prevent onInit from generating code *)
                                true->con.initialisingSaved;
                                (if titleNode.thePrefixConn = none then
                                    (theSurroundBox,
                                     prefixDiagram.theSurroundBox)->con.new;
                                    (if not gppProp.specializations then
                                        false->con.borderVisible;
                                        false->con.selectable
                                    if);
                                    prefixDiagram.titleNode[]
                                      ->titleNode.CreateRegion;
                                    con[]->titleNode.thePrefixConn
                                if)
                            if)
                        if)
                    if);
                    (if (theDecl.index = current.super) and
                    (theDecl.frag.fullname->current.superFrag.equal) then
                        current.subFrag[]->topDotOpen->ff[];
                        current.sub->ff.indexToNode->AstToNode->theNode[];
                        (if theNode[] <> none then
                            theNode.theDiagram->theListDiagram[];
                            theNode[]->subNode[];
                            (if subNode.CurrentDecomposDiagram <> none then
                                subNode.CurrentDecomposDiagram->subDiagram[];
                                &PrefixConnector[]->con[];
                                (* to prevent onInit from generating code *)
                                true->con.initialisingSaved;
                                (if subDiagram.titleNode.thePrefixConn = none
                                 then
                                    (subDiagram.theSurroundBox,theSurroundBox)
                                      ->con.new;
                                    titleNode[]
                                      ->subDiagram.titleNode.CreateRegion;
                                    con[]->subDiagram.titleNode.thePrefixConn
                                if)
                            if)
                        if)
                    if)
                 #)
           if);
           
       if)
   if);
   
#)  

-- PatternDeclDiagramOnInit: Descriptor --
(#
   fd: ^FragmentDiagram;
   index: @integer;
   t,tt: ^text;
   theDummyFragNode: ^PatternDiagramNode;
   theDummyGroup: ^mps.fragmentgroup
do
   UDPrivate.UDPatternDeclDiagram->UserDataInit;
   UDthePrefix.Init;
   (if not initialisingSaved then
       theFragmentNode->theDummyFragNode[];
       theDummyFragNode.theDiagram->fd[];
       DisplayInheritance;
       
   if);
   
#)  

-- PatternDeclDiagramNew: Descriptor --
(# 
do
   (if anAST.symbol
    // betaGram.ObjectDescriptor then
       anAST[]->theDescriptor; titleShape->Display; 
    else
       'PatternDeclDiagram New: unknown AST symbol'->putline; 
   if);
   
#)
(****************** Pattern Classification Diagram *******************)  

-- PatternClassificationDiagramDisplay: Descriptor --
(#
   root: ^mps.expanded;
   LookForInheritance:
     (# root: ^mps.expanded; 
     enter root[]
     do
        betaGram.ObjectDescriptor
          ->root.suffixWalkforProd
            (#
               scanCat::< betaGram.ObjectDescriptor;
               tempAst: ^mps.ast;
               pref: ^betaGram.prefix;
               ad: ^betaGram.attributeDenotation;
               thisInheritor: ^betaGram.ObjectDescriptor;
               inheritorDiagram: ^PatternDeclDiagram;
               con: ^PrefixConnector;
               
            do
               (if switch[50]
                // true then
                   'PatternClassificationDiagramDisplay'->screen.putline; 
               if);
               current.getPrefixOpt->tempAst[];
               (if tempAst.kind
                // mps.kinds.interior then
                   (if switch[50] // true then '.'->screen.put;  if);
                   tempAst[]->pref[];
                   pref.getAttributeDenotation->tempAst[];
                   (if tempAst.kind
                    // mps.kinds.interior then
                       tempAst[]->ad[];
                       ad.findDescriptor->thisInheritor[];
                       (if thisInheritor[]
                        // theDescriptor then
                           (if switch[50]
                            // true then 'Found inheritor.'->screen.putline; 
                           if);
                           (thisInheritor.index,thisInheritor.frag.fullname)
                             ->PatternDiagrams.theList.findPD
                             ->inheritorDiagram[];
                           (if inheritorDiagram[]
                            // none then
                               (if switch[50]
                                // true then 'Not displayed'->screen.putline; 
                               if);
                               
                            else
                               (if switch[50]
                                // true then
                                   'Already displayed, create perfix connector'
                                     ->screen.putline;
                                   
                               if);
                               &PrefixConnector[]->con[];
                               (inheritorDiagram.titleNode[],titleNode[])
                                 ->con.new;
                               titleNode[]
                                 ->inheritorDiagram.titleNode.CreateRegion;
                               con[]->inheritorDiagram.titleNode.thePrefixConn;
                               
                           if)
                       if)
                   if)
               if)
            #)
     #);
   theDiagram: ^theGroupPage.FragmentDiagram;
   theDummyGroup: ^mps.fragmentgroup
do
   theGroupPage.FragmentDiagram##
     ->theGroupPage.ScanDiagrams
       (# 
       do
          current[]->theDiagram[];
          (if switch[50]
           // true then theDiagram.fullname->screen.putline; 
          if);
          (if (theDiagram.theGroup->isChecked)
           // true then
              theDiagram.theGroup->theDummyGroup[];
              theDummyGroup.fragmentlist.scan
                (# ff: ^mps.fragmentform; 
                do
                   (if current.type
                    // mps.FormType then
                       current.f[]->ff[];
                       (if ff.root[]
                        // none then 
                        else
                           ff.root[]->LookForInheritance; 
                       if)
                   if)
                #)
          if)
       #)
#)
(****************** Pattern NonTerminal Diagram *******************)  

-- PatternClassificationDiagramOnInit: Descriptor --
(#  do UDPrivate.UDPatternClassificationDiagram->UserDataInit #)  

-- PatternNonTerminalDiagramNew: Descriptor --
(# 
do
   'PatternNonTerminalDiagramNew: '->screen.puttext;
   titletext[]->screen.putline;
   (if anAst[]
    // none then 'PatternNonTerminalDiagramNew anAST is NONE'->putline; 
    else
       (if anAST.symbol
        // betaGram.ObjectDescriptor then
           anAST[]->theDescriptor; titleshape->Display; 
        else
           'PatternNonTerminalDiagram New: unknown AST symbol'->putline; 
       if)
   if);
   
#)  

-- PatternNonTerminalDisplayDeclarations: Descriptor --
(#
   nt: ^NonTerminalNode;
   unExp: ^mps.unexpanded;
   x,y: @Integer;
   main: ^betaGram.MainPart;
   theDummyDesc: ^betaGram.ObjectDescriptor
do
   (if theDescriptor
    // none then
       none ->theAttributes[];
       'NonTerminal: theDescriptor is NONE'->putline;
       (*&NonTerminalNode[] -> nt[];
        (gram.attributedecl,theDescriptor.frag[]) -> betagram.newUnexpanded -> unExp[];
        (startpos,unExp[]) -> nt.display;
        THIS(PatternNonTerminalDiagram)[] -> nt.theDiagram[];*)
       
    else
       theDescriptor->theDummyDesc[];
       theDummyDesc.getMainPart->main[];
       main.getAttributes->theAttributes[];
       
   if);
   
#)  

-- PatternNonTerminalDiagramOnInit: Descriptor --
(# 
do UDPrivate.UDPatternNonTerminalDiagram->UserDataInit; UDtheDesc.Init
#)  

-- CommentNodeOnInit: Descriptor --
(# 
do
   UDPrivate.UDCommentNode->UserDataInit;
   textJust.leftJustification->theText.just
#)  

-- UserDataNodeOninit: Descriptor --
(# 
do
   LocalNodesUDAttributes.init;
   oldLocalNodesUDAttributes.init;
   LocalNodesUDAttributes->oldLocalNodesUDAttributes;
   LocalNodesUserDataStart->LocalNodesUDAttributes;
   decomposDiagramsUDAttributes.init;
   oldDecomposDiagramsUDAttributes.init;
   decomposDiagramsUDAttributes->oldDecomposDiagramsUDAttributes;
   decomposDiagramsUserDataStart->decomposDiagramsUDAttributes;
   INNER onInit
#)  

