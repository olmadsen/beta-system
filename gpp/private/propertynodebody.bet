ORIGIN '../propertydiagrams';
INCLUDE 'diagramattributes';
-- DisplayPropertyFiles: Descriptor --
(#
   x,y,w,h: @Integer;
   lastOrigin,lastInclude,lastBody: ^SimplePropertyDiagram;
   fg: ^mps.fragmentgroup;
   
do
   false->mps.doRealOpen;
   (* only open the properties of the group *)
   localNodes.scan
     (#
        pn: ^PropertyNode;
        pd: ^thePage.SimplePropertyDiagram;
        aCon: ^thePage.Connector;
        connode: ^thePage.RectNode;
        
     do
        current[]->pn[];
        (pn.fullName,screen[])->mps.top.open->fg[];
        (if fg[]
         // none then
            'Fragment error'->putline;
            'Cannot open fragment group: '->puttext;
            pn.fullname->putline
         else
            (if fg[]->isChecked
             // false then
                'FragmentGroup '''->puttext;
                pn.fullname->puttext;
                ''' not checked.'->putline;
                'Must be checked by compiler...'->putline;
                
             else
                &thePage.SimplePropertyDiagram[]->pd[];
                (fg[],false,pn.fullName)->pd.new;
                (if pn.type
                 // pd.originProp then
                    (if lastOrigin[] <> none
                     // true then
                        lastOrigin.titlenode.Center->(x,y);
                        (x,y-gppProp.OriginOffset)->pd.titlenode.Center;
                        
                     else
                        mainProperty.titlenode.Center->(x,y);
                        y-
                        ((pd.localNodes.size*gppProp.height)+
                         gppProp.OriginOffset)->y;
                        (x,y)->pd.titlenode.Center;
                        
                    if);
                    pd[]->lastOrigin[];
                    &thePage.Connector[]->aCon[];
                    (mainProperty.titlenode[],pd.titlenode[])->aCon.new;
                    1->aCon.FillType;
                    
                 // pd.includeProp then
                    (if lastInclude[] <> none
                     // true then
                        lastInclude.titlenode.Center->(x,y);
                        (x,y+gppProp.OriginOffset)->pd.titlenode.Center;
                        
                     else
                        mainProperty.titlenode.geometry->(x,y,w,h);
                        (x+w+gppProp.RightOnPage,y-gppProp.OriginOffset)
                          ->pd.titlenode.Center;
                        
                    if);
                    pd[]->lastInclude[];
                    pd.titlenode.geometry->(x,y,w,h);
                    &thePage.RectNode[]->connode[];
                    (if switch[70] then
                        '******** propertynodebody-displayPropertyFiles: calling new with ********'
                          ->putline;
                        'x: '->putText;
                        x-(w div 2)->putint;
                        newLine;
                        'y: '->putText;
                        y->putint;
                        newLine;
                        'w: '->putText;
                        0->putint;
                        newLine;
                        'h: '->putText;
                        0->putint;
                        newLine;
                        
                    if);
                    (x-(w div 2),y,0,0)->connode.new;
                    false->connode.Bordervisible;
                    pd.titlenode[]->connode.createRegion;
                    &thePage.Connector[]->aCon[];
                    (mainProperty.titlenode[],connode[])->aCon.new;
                    
                 // pd.bodyProp then
                    (if lastBody[] <> none
                     // true then
                        lastBody.titleNode.Center->(x,y);
                        y+gppProp.height->y;
                        (x,y)->pd.titlenode.Center;
                        
                     else
                    (* first body diagram *)
                        mainProperty.titlenode.Center->(x,y);
                        (x,y+gppProp.OriginOffset)->pd.titlenode.Center;
                        &thePage.Connector[]->aCon[];
                        (mainProperty.titlenode[],pd.titlenode[])->aCon.new;
                        1->aCon.lineType;
                        
                    if);
                    pd[]->lastBody[];
                    
                if);
                
            if)
        if)
     #);
   true->mps.doRealOpen;
   
#)  

-- PropertyTitleDetail: Descriptor --
(#
   aPage: ^OADPage;
   mainProperty: ^aPage.SimplePropertyDiagram;
   x,y,w,h: @Integer;
   
do
   (if detailed
    // false then
       true->detailed;
       &OADPage[]->aPage[];
       aPage.new;
       &aPage.SimplePropertyDiagram[]->mainProperty[];
       (theGroup,false,fullName)->mainProperty.new;
       aPage.geometry->(x,y,w,h);
       (x-(w div 2)+gppProp.RightOnPage+20,y)->mainProperty.titlenode.Center;
       (mainProperty[],aPage[])->displayPropertyFiles;
       true->realOpen;
       
   if);
   
#)  

-- SimplePropertyTitleDetail: Descriptor --
(#
   DiagramExists:
     (# fullname: @Text; foundDiagram: ^OADDiagram; 
     enter fullname
     do
        (if switch[70]
         // true then
            'DiagramExists: scanning for '->puttext; fullname[]->puttext; 
        if);
        scanner: PatternDiagrams.theList.scanPropertyDiagrams
          (# 
          do
             (if thisDiagram.fullname->fullname.equal
              // true then thisDiagram[]->foundDiagram[]; leave scanner; 
             if);
             
          #);
        (if switch[70] // true then ' done'->putline if);
        
     exit foundDiagram[]
     #);
   
do
   (if switch[70] // true then 'SimplePropertyNodeDetail'->putline if);
   (if switch[70] // true then 'Working...'->putLine (*statusbar.set*) if);
   cursor.wait->cursor.set;
   false->mps.doRealOpen;
   ScanProperties
     (#
        con: ^Connector;
        mainProperty,lastInclude,lastBody: ^SimplePropertyDiagram;
        f: ^OADDiagram;
        x,y,w,h: @Integer;
        fg: ^mps.fragmentgroup;
        thisOrigin::< 
          (# 
          do
             (if fullName->DiagramExists->f[]
              // none then
                 (fullName[],screen[])->mps.top.open->fg[];
                 (if fg[]
                  // none then
                     'Fragment error'->putline;
                     'Cannot open origin fragment: '->puttext;
                     fullname[]->putline;
                     
                  else
                     &SimplePropertyDiagram[]->mainProperty[];
                     (fg[],false,fullName[])->mainProperty.new;
                     titlenode.geometry->(x,y,w,h);
                     y-(gppProp.height+gppProp.OriginOffset)->y;
                     (x,y)->mainProperty.titlenode.Center;
                     &Connector[]->con[];
                     (titlenode[],mainProperty.titleNode[])->con.new;
                     1->con.FillType;
                     
                 if);
                 
              else
                 &Connector[]->con[];
                 (titlenode[],f.titleNode[])->con.new;
                 1->con.FillType;
                 
             if);
             
          #);
        thisInclude::< 
          (# 
          do
             (if fullName->DiagramExists->f[]
              // none then
                 (fullName[],screen[])->mps.top.open->fg[];
                 (if fg[]
                  // none then
                     'Fragment error'->putline;
                     'Cannot open include fragment: '->puttext;
                     fullname[]->putline;
                     
                  else
                     &SimplePropertyDiagram[]->mainProperty[];
                     (fg[],false,fullName[])->mainProperty.new;
                     (if lastInclude[]
                      // none then
                         titlenode.geometry->(x,y,w,h);
                         (x+w+gppProp.RightOnPage,y-gppProp.OriginOffset)
                           ->mainProperty.titlenode.Center;
                         
                      else
                         lastInclude.titlenode.Center->(x,y);
                         (x,y+gppProp.OriginOffset)
                           ->mainProperty.titlenode.Center;
                         
                     if);
                     &Connector[]->con[];
                     (titlenode[],mainProperty.titleNode[])->con.new;
                     mainProperty[]->lastInclude[];
                     
                 if);
                 
              else
                 &Connector[]->con[]; (f.titleNode[],titlenode[])->con.new; 
             if);
             
          #);
        thisBody::< 
          (# 
          do
             (if fullName->DiagramExists->f[]
              // none then
                 (fullName[],screen[])->mps.top.open->fg[];
                 (if fg[]
                  // none then
                     'Fragment error'->putline;
                     'Cannot open body fragment: '->puttext;
                     fullname[]->putline;
                     
                  else
                     &SimplePropertyDiagram[]->mainProperty[];
                     (fg[],false,fullName[])->mainProperty.new;
                     (if lastBody[]
                      // none then
                         titlenode.Center->(x,y);
                         (x,y+gppProp.OriginOffset)
                           ->mainProperty.titlenode.Center;
                         &Connector[]->con[];
                         (titlenode[],mainProperty.titleNode[])->con.new;
                         titlenode[]->mainproperty.titlenode.createRegion;
                         1->con.lineType;
                         
                      else
                         lastBody.titlenode.Center->(x,y);
                         (x,y+gppProp.height)->mainProperty.titlenode.Center;
                         lastBody.titlenode[]
                           ->mainproperty.titlenode.createRegion;
                         
                     if);
                     mainProperty[]->lastBody[];
                     
                 if);
                 
              else
                 &Connector[]->con[]; (f.titleNode[],titlenode[])->con.new; 
             if);
             
          #);
        thisOther::< 
          (# 
          do
             (if fullName->DiagramExists->f[]
              // none then
                 (if switch[70]
                  // true then 'Other property: ignore'->putline
                 if);
                 
              else
                 &Connector[]->con[]; (titlenode[],f.titleNode[])->con.new; 
             if);
             
          #);
        
     #);
   cursor.reset;
   (*   statusbar.reset;*)
   true->mps.doRealOpen;
   
#)  

-- SimplePropertyTitleAbstract: Descriptor --
(# 
do
   THIS(SimplePropertyDiagram)[]->PatternDiagrams.theList.deleteProp;
   titleNode.delete;
   
#)  

-- SimplePropertyDiagramRedisplay: DoPart --
do
     (#
        x,y,fx,fy,tx,ty,sx,sy,sw,sh,noOfProp,oldNoOfProp,propDiff: @integer;
        thePos: @Point
     do
        localNodes.scan
          (# 
          do
             oldNoOfProp+1->oldNoOfProp;
             current.delete;
             current[]->localnodes.at->localnodes.delete
          #);
        titleNode.center->(x,y);
        pos->thePos;
        (thePos.x,y+gppProp.height)->pos;
        ScanProperties
        (* show the properties below the titlebox *)
          (#
             thisOrigin::< 
               (# o: ^OriginPropertyNode; 
               do
                  noOfProp+1->noOfProp;
                  &OriginPropertyNode[]->o[];
                  (nodeText,fullname[])->o.display;
                  
               #);
             thisInclude::< 
               (# i: ^IncludePropertyNode; 
               do
                  noOfProp+1->noOfProp;
                  &IncludePropertyNode[]->i[];
                  (nodeText,fullname[])->i.display;
                  
               #);
             thisBody::< 
               (# b: ^BodyPropertyNode; 
               do
                  noOfProp+1->noOfProp;
                  &BodyPropertyNode[]->b[];
                  (nodeText,fullname[])->b.display;
                  
               #);
             thisOther::< 
               (# p: ^PassivePropertyNode; 
               do
                  noOfProp+1->noOfProp;
                  &PassivePropertyNode[]->p[];
                  (nodeText,fullname[])->p.display;
                  
               #);
             
          #);
        (theSurroundBox).geometry->(sx,sy,sw,sh);
        noOfProp-oldNoOfProp->propDiff;
        (if propDiff <> 0 then
            (sx,sy+propDiff*gppProp.Height div 2,sw,sh+propDiff*gppProp.Height)
              ->(theSurroundBox).geometry;
            FragmentDiagram##
              ->scanDiagrams
                (# thisFragmentDiagram: ^FragmentDiagram
                do
                   current[]->thisFragmentDiagram[];
                   (if thisFragmentDiagram.theGroup = theGroup then
                       current.titleNode.center->(tx,ty);
                       (tx,ty+propDiff*gppProp.Height)->current.titleNode.center
                   if)
                #)
        if)
     #)  

-- SimplePropertyDiagramNew: Descriptor --
(#
   x,y,w,h: @Integer;
   thePos: @Point;
   theDummyGroup: ^mps.fragmentgroup;
   theBox: ^SurroundBox
do
   (0,none ,THIS(SimplePropertyDiagram)[])->PatternDiagrams.theList.insertPD;
   (if singleProp then
       'Group: '->titleText;
         (# index: @Integer; name: ^text; 
         do
            theGroup->theDummyGroup[];
            theDummyGroup.name->name[];
            '/'->name.findAll (#  do inx->index;  #);
            (index+1,name.length)->name.sub->name[];
            name[]->titletext.append;
            
         #);
       (if PatternDiagrams.theList.size > 1
        // true then
           (PatternDiagrams.NextFreeColumn,PatternDiagrams.NextFreeLine)->(x,y);
           
        else
           THIS(OADPage).Geometry->(x,y,w,h);
           x-(w div 2)+gppProp.RightOnPage->x;
           y-(h div 2)+gppProp.DownOnPage->y;
           
       if);
       titletext->titleNode.theText.set;
       (if switch[70] then
           '******** propertynodebody-SimplePropertyDiagramNew: 1 calling new with ********'
             ->putline;
           'x: '->putText;
           x+((gppProp.width-gppProp.titlewidth+1) div 2)->putint;
           newLine;
           'y: '->putText;
           y->putint;
           newLine;
           'w: '->putText;
           gppProp.width->putint;
           newLine;
           'h: '->putText;
           gppProp.height->putint;
           newLine;
           
       if);
       (x+((gppProp.width-gppProp.titlewidth+1) div 2),y,gppProp.width,
        gppProp.height,titleNode.defaultshape)->titleNode.new;
       (x-(gppProp.width-gppProp.titlewidth+1) div 2,y+(gppProp.height+1))->pos;
       titleNode.geometry->(x,y,w,h);
       (if switch[70] then
           '******** propertynodebody-SimplePropertyDiagramNew: 2 calling new with ********'
             ->putline;
           'x: '->putText;
           x->putint;
           newLine;
           'y: '->putText;
           y->putint;
           newLine;
           'w: '->putText;
           w->putint;
           newLine;
           'h: '->putText;
           h->putint;
           newLine;
           
       if);
       &SurroundBox[]->theBox[];
       (x,y,w,h,rectangle)->theBox.new;
       theBox[]->theSurroundBox;
       
    else
         (# index: @Integer; name: ^text; 
         do
            theGroup->theDummyGroup[];
            theDummyGroup.name->name[];
            '/'->name.findAll (#  do inx->index;  #);
            (index+1,name.length)->name.sub->name[];
            name->titletext;
            
         #);
       titletext->titleNode.theText.set;
       (0,0)->(x,y);
       (if switch[70] then
           '******** propertynodebody-SimplePropertyDiagramNew: 3 calling new with ********'
             ->putline;
           'x: '->putText;
           x->putint;
           newLine;
           'y: '->putText;
           y->putint;
           newLine;
           'w: '->putText;
           gppProp.titlewidth*2->putint;
           newLine;
           'h: '->putText;
           gppProp.height->putint;
           newLine;
           
       if);
       (x,y,gppProp.titlewidth*2,gppProp.height,titleNode.defaultshape)
         ->titleNode.new;
       (x-(gppProp.titlewidth*2+1) div 2,y+(gppProp.height+1) div 2)->pos;
       
   if);
   INNER new;
   (if PatternDiagrams.theList.size
    // 1 then (* property diagram was the first to show *)
       pos->thePos;
       thePos.y+gppProp.DownFromPrefix->PatternDiagrams.NextFreeLine;
       THIS(OADPage).Geometry->(x,y,w,h);
       x-(w div 2)+gppProp.RightOnPage->PatternDiagrams.NextFreeColumn;
       PatternDiagrams.NextFreeLine->PatternDiagrams.SecondFreeLine;
       PatternDiagrams.NextFreeColumn+gppProp.width+gppProp.RightFromPrevious
         ->PatternDiagrams.SecondFreeColumn;
       
   if);
   
#)  

-- SimplePropertyDiagramScanProperties: Descriptor --
(#
   includeList: @
     (#
        rep: [10] ^text;
        top: @integer;
        append:
          (# name: ^text; 
          enter name[]
          do
             top+1->top;
             (if top // rep.range then top div 2->rep.extend if);
             name[]->rep[top][]
          #);
        exists:
          (# name: ^text; found: @boolean
          enter name[]
          do
             loop:
             (for i: top repeat
               (if rep[i][]->name.equal then true->found; leave loop;  if)
             for);
             
          exit found
          #)
     #);
   theDummyGroup: ^mps.fragmentgroup;
   MakeName:
     (#
        nodeText: @text;
        theNode: ^PropertyNode;
        fullname:
          (# local,n: ^Text; name: @Text; i: @Integer; 
          enter local[]
          do
             theGroup->theDummyGroup[];
             theDummyGroup.name->n[];
             n->name;
             '/'->name.findAll (#  do inx->i #);
             (if i > 0 // true then (i,name.length)->name.delete;  if);
             (local[],name[])->mps.thePathHandler.convertFilePath->n[];
             
          exit n[]
          #);
        name: @Text;
        action: ##doThis;
        
     enter (name,action##)
     do
        theGroup->theDummyGroup[];
        (if name[]->theDummyGroup.prop.findProp
         // none then 
         else
            name[]
              ->theDummyGroup.prop.getprop
                (#
                   doprop::< 
                     (# 
                     do
                        scanParameters
                          (#
                             doString::< 
                               (# 
                               do
                                  name->nodeText;
                                  ' '''->nodetext.append;
                                  s[]->nodeText.append;
                                  ''''->nodetext.append;
                                  (nodeText[],s[]->fullName)->&action;
                                  (if 'include'->prop.equalNCS then
                                      s[]->includeList.append; 
                                  if);
                                  
                               #)
                          #)
                     #)
                #);
            
        if);
        
     #);
   
do
   ('origin',thisOrigin##)->MakeName;
   ('include',thisInclude##)->MakeName;
   ('body',thisBody##)->MakeName;
   ('mdbody',thisBody##)->MakeName;
   ('make',thisOther##)->MakeName;
   ('objfile',thisOther##)->MakeName;
   ('libfile',thisOther##)->MakeName;
   theGroup->theDummyGroup[];
   theDummyGroup.fragmentList.scan
     (#
        in: ^IncludePropertyNode;
        nodetext: @text;
        fullname:
          (# local,n: ^Text; name: @Text; i: @Integer; 
          enter local[]
          do
             theGroup->theDummyGroup[];
             theDummyGroup.name->n[];
             n->name;
             '/'->name.findAll (#  do inx->i #);
             (if i > 0 // true then (i,name.length)->name.delete;  if);
             (local[],name[])->mps.thePathHandler.convertFilePath->n[];
             
          exit n[]
          #);
        
     do
        (if current.type
         // mps.linkType then
            (if current.name[]->includeList.exists then
            (* already prettyprinted *)
                
             else
                &IncludePropertyNode[]->in[];
                'include'->nodeText;
                ' '''->nodetext.append;
                current.localName[]->nodeText.append;
                ''''->nodetext.append;
                (nodeText[],current.Name[]->fullName)->thisInclude;
                
            if);
            
        if)
     #);
   
#)  

-- PropertyDiagramNew: Descriptor --
(# (* only for PropertyDiagram on PatternDiagram pages *) 
do
   ScanProperties
   (* show the properties below the titlebox *)
     (#
        thisOrigin::< 
          (# o: ^OriginPropertyNode; 
          do &OriginPropertyNode[]->o[]; (nodeText,fullname[])->o.display; 
          #);
        thisInclude::< 
          (# i: ^IncludePropertyNode; 
          do &IncludePropertyNode[]->i[]; (nodeText,fullname[])->i.display; 
          #);
        thisBody::< 
          (# b: ^BodyPropertyNode; 
          do &BodyPropertyNode[]->b[]; (nodeText,fullname[])->b.display; 
          #);
        thisOther::< 
          (# p: ^PassivePropertyNode; 
          do &PassivePropertyNode[]->p[]; (nodeText,fullname[])->p.display; 
          #);
        
     #);
   
#)  

-- PropertyNodeDisplay: Descriptor --
(# thePos: @Point
do
   pos->thePos;
   (thePos.x+(gppProp.propertywidth+1) div 2,thePos.y+(gppProp.height+1) div 2,
    gppProp.propertywidth,gppProp.height,defaultshape)->new;
   (thePos.x,thePos.y+gppProp.height)->pos;
   (* update y-coord for the next node *)
   nodeText->theText.set;
   leftJustification->theText.just;
   gppProp.fontSize->theText.size;
   false->moveable;
   (* we don't like this Node to be moved alone *)
   false->sizeable;
   titleNode[]->CreateRegion;
   THIS(PropertyNode)[]->localNodes.append;
   
#)  

-- PropertyNodeOnInit: Descriptor --
(# 
do
   UDPrivate.UDPropertyNode->UserDataInit;
   fullname.Init;
   (if not initialisingSaved then
       leftJustification->theText.just; gppProp.fontSize->theText.size
   if);
   false->moveable;
   false->sizeable
#)  

-- ActivePropertyDoubleClick: Descriptor --
(# t: ^Text
do
   (if switch[30]
    // true then 'Doubleclick: active property node'->putline; 
   if);
   (if expanded
    // false then
       (if switch[30]
        // true then 'opening: '->puttext; fullname->putline; 
       if);
       (if detailPage
        // none then
           fullName->t[];
           (if t->openFragmentGroup
            // none then 
            else
           (*gppProp.DetailedStyle -> theText.style;*)
               CurrentPage->detailPage; 
           if);
           
        else
           detailPage->CurrentPage; 
       if);
       
    else
       (if switch[30]
        // true then 'closing: '->puttext; fullname->putline; 
       if);
       (if gppProp.DetailedOnSamePage // false then (detailPage).close if);
       
   if);
   
#)
(* -- markRelatedFragments: DescriptorForm --
 * (# {* get bindings the "beta" way; search origin and include fragments *}
 * do
 * #)  
 * 
 *)  

-- SimplePropertyDiagramReadFragUserData: DoPart --
do
   (if dataElement[] = none then
       (fullname,error[])->mps.top.open->dataElement[]
   if)  

