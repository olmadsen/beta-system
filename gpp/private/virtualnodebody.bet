ORIGIN 'diagramattributes';
-- virtualNodeDoDisplay: DoPart --
do
   (if theDeclaration## <= betaGram.VirtualDecl##
    // true then
       theDeclaration[]->vd[];
       vd.getObjectSpecification->anAST[];
       '{virtual}'->PropertyString[]
    else
       INNER ppAST
   if);
   (if anAST.symbol
    // betaGram.NameApl then
       anAst[]->aName[];
       aName.getNameAppl->a[];
       ':'->t.append;
       (if a.kind
        // mps.kinds.unExpanded then ' <<NameAppl>>'->t.append
        else
       (* is a real name *)
           a[]->theAppl[]; theAppl.getText->t.append
       if);
       (if attributeType = 0 then
           gppProp.localClass->attributeType;
           THIS(VirtualNode)[]->localClasses.append
       if)
    // betaGram.Remote then
       ':'->t.append;
       anAST[]->MakeRemote->t.append;
       (if attributeType = 0 then
           gppProp.localClass->attributeType;
           THIS(VirtualNode)[]->localClasses.append
       if)
    else
       (if anAST.kind = mps.kinds.unExpanded then
           ':'->t.append;
           anAST[]->theUnExpObjSpec[];
           theUnExpObjSpec.nonTerminalSymbol->betaGram.symbolToName
             ->makeNonTerminalSymbol->t.append;
           (if attributeType = 0 then
               gppProp.operation->attributeType;
               THIS(VirtualNode)[]->operations.append
           if)
       if)
   if)  

-- virtualNodeOnInit: DoPart --
do UDPrivate.UDVirtualNode->UserDataInit; INNER  

-- virtualNodeDump: DoPart --
do 'VirtualNode'->nodetype[]; INNER dump  

