ORIGIN 'diagramattributes';
-- FragmentNodeDisplay: Descriptor --
(# dummy: ^Text
do
   (pos.x+(gppProp.width+1) div 2+
    ((gppProp.fragmentwidth-gppProp.width+1) div 2),pos.y+(gppProp.height+1) div
    2,gppProp.fragmentwidth,gppProp.height,defaultshape)->new;
   (theFragment).name->theName->dummy[];
   dummy->t;
   (theFragment).root[]->theForm;
   INNER Display;
   (theForm).index->astIndex;
   (if switch[1] then
       'FragmentNodeDisplay: '->putText; astIndex->putInt; '->astIndex'->putLine
   if);
   (if switch[1]
    // true then
       'Freja[[Creating FragmentNode with AST root index: '->screen.puttext;
       astIndex->screen.putint;
       ']]'->screen.putline;
       
   if);
   (if CheckForAttributes
    // true then (*gppProp.AbstractedStyle -> theText.style*)
       ' ...'->t.puttext; 
   if);
   LeftJustification->theText.Just;
   
#)  

-- AttributesNodeDetail: Descriptor --
(#
   theGroup: ^FragmentGroup;
   t: ^Text;
   index: @Integer;
   dummyDetailPage: ^OADPage;
   DD: ^dummyDetailPage.PatternAttDiagram;
   
do
   (if switch[1] // true then 'Detail: AttributesNode'->putline if);
   detailPage->dummyDetailPage[];
   &dummyDetailPage.PatternAttDiagram[]->DD[];
   THIS(AttributesNode)[]->DD.theFragmentNode;
   (theFragment).father->theGroup[];
   theGroup.name->t[];
   '/'->t.findCh (#  do inx->index #);
   (* get local name of group *)
   (index+1,t.length)->t.sub->t[];
   '-'->t.put;
   theName->t.puttext;
   ((theForm).getson1,t,Rectangle)->DD.new;
   DD[]->currentDecomposDiagram;
   
#)  

-- DescriptorNodeDetail: Descriptor --
(#
   theGroup: ^FragmentGroup;
   t: ^Text;
   index: @Integer;
   dummyDetailPage: ^OADPage;
   DD: ^dummyDetailPage.PatternDeclDiagram;
   
do
   (if switch[1] // true then 'Detail: DescriptorNode'->putline if);
   detailPage->dummyDetailPage[];
   &dummyDetailPage.PatternDeclDiagram[]->DD[];
   THIS(DescriptorNode)[]->DD.theFragmentNode;
   (theFragment).father->theGroup[];
   theGroup.name->t[];
   '/'->t.findCh (#  do inx->index #);
   (* get local name of group *)
   (index+1,t.length)->t.sub->t[];
   '-'->t.put;
   theName->t.puttext;
   ((theForm).getson1,t,Rectangle)->DD.new;
   DD[]->currentDecomposDiagram;
   
#)  

-- FragmentReReadAST: Descriptor --
(# fd: ^FragmentDiagram; fg: ^FragmentGroup; 
do
   (if switch[1] // true then 'ReReadAST'->screen.putline if);
   (* read in the FragmentGroup and update all Forms *)
   theDiagram->fd[];
   fd.theGroup->fg[];
   fg.fragmentList.scan
     (# f: ^FragmentForm; theNode: ^FragmentNode; fname: ^Text; 
     do
        (if current.type
         // linkType then (* ignore *)
            
         // groupType then
            'Groups in groups not suported'->putline; 
         // formType then
            current.f[]->f[];
            f.name->fname[];
            (if switch[1]
             // true then 'Form: '->screen.puttext; fname[]->screen.putline; 
            if);
            scanner: localNodes.scan
              (# fn: ^FragmentNode; 
              do
                 current[]->fn[];
                 (if fname[]->(fn.theName).equal
                  // true then
                     f.root[]->fn.theForm;
                     f[]->fn.theFragment;
                     f[]->updateFragmentForm;
                     (if switch[1]
                      // true then
                         'Form: '->screen.puttext;
                         fname[]->screen.puttext;
                         ' found and updated'->screen.putline;
                         
                     if);
                     leave scanner;
                     
                 if)
              #)
        if)
     #);
   theFragment->newff[];
   (if switch[1] // true then 'End ReReadAST'->screen.putline if);
   
#)  

-- FragmentNodeDetail: Descriptor --
(#  do (theFragment,THIS(FragmentNode)[])->openFormInSif #)  

-- FragmentNodeDump: Descriptor --
(# 
do
   '********theFragment********'->putline;
   (if theFragment <> none then
       (theFragment).fullname->putline
    else
       'is NONE!'->putline
   if);
   '********theForm********'->putline;
   (if theForm <> none then
       (theForm).dump; newline
    else
       'is NONE!'->putline
   if)
#)  

