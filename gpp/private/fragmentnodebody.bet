ORIGIN 'diagramattributes';
-- FragmentNodeDisplay: Descriptor --
(# dummy: ^Text
do
   (pos.x+(gppProp.width+1) div 2+
    ((gppProp.fragmentwidth-gppProp.width+1) div 2),pos.y+(gppProp.height+1) div
    2,gppProp.fragmentwidth,gppProp.height,defaultshape)->new;
   (theFragment).name->theName->dummy[];
   dummy->t;
   (theFragment).root[]->theRoot;
   INNER Display;
   (theRoot).index->astIndex;
   (if switch[1] then
       'FragmentNodeDisplay: '->putText; astIndex->putInt; '->astIndex'->putLine
   if);
   (if switch[1]
    // true then
       'Freja[[Creating FragmentNode with AST root index: '->screen.puttext;
       astIndex->screen.putint;
       ']]'->screen.putline;
       
   if);
   (if CheckForAttributes
    // true then (*gppProp.AbstractedStyle -> theText.style*)
       ' ...'->t.puttext; 
   if);
   LeftJustification->theText.Just;
   
#)  

-- AttributesNodeDetail: Descriptor --
(#
   theGroup: ^mps.fragmentgroup;
   t: ^Text;
   index: @Integer;
   dummyDetailPage: ^OADPage;
   DD: ^dummyDetailPage.PatternAttDiagram;
   theForm: ^betaGram.betaForm
do
   (if switch[1] // true then 'Detail: AttributesNode'->putline if);
   detailPage->dummyDetailPage[];
   &dummyDetailPage.PatternAttDiagram[]->DD[];
   THIS(AttributesNode)[]->DD.theFragmentNode;
   (theFragment).father->theGroup[];
   theGroup.name->t[];
   '/'->t.findCh (#  do inx->index #);
   (* get local name of group *)
   (index+1,t.length)->t.sub->t[];
   '-'->t.put;
   theName->t.puttext;
   theRoot->theForm[];
   (theForm.getson1,t,Rectangle)->DD.new;
   DD[]->currentDecomposDiagram;
   
#)  

-- DescriptorNodeDetail: Descriptor --
(#
   theGroup: ^mps.fragmentgroup;
   t: ^Text;
   index: @Integer;
   dummyDetailPage: ^OADPage;
   DD: ^dummyDetailPage.PatternDeclDiagram;
   theForm: ^betaGram.betaForm
do
   (if switch[1] // true then 'Detail: DescriptorNode'->putline if);
   detailPage->dummyDetailPage[];
   &dummyDetailPage.PatternDeclDiagram[]->DD[];
   THIS(DescriptorNode)[]->DD.theFragmentNode;
   (theFragment).father->theGroup[];
   theGroup.name->t[];
   '/'->t.findCh (#  do inx->index #);
   (* get local name of group *)
   (index+1,t.length)->t.sub->t[];
   '-'->t.put;
   theName->t.puttext;
   theRoot->theForm[];
   (theForm.getson1,t,Rectangle)->DD.new;
   DD[]->currentDecomposDiagram;
   
#)  

-- FragmentNodeDetail: Descriptor --
(#  do (theFragment,THIS(FragmentNode)[])->openFormInSif #)  

-- FragmentNodeDump: Descriptor --
(# 
do
   '********theFragment********'->putline;
   (if theFragment <> none then
       (theFragment).fullname->putline
    else
       'is NONE!'->putline
   if);
   '********theRoot********'->putline;
   (if theRoot <> none then
       (theRoot).dump; newline
    else
       'is NONE!'->putline
   if)
#)  

