ORIGIN '../gppinterface';
INCLUDE 'diagramattributes';
-- WriteASTReferenceUserData: Descriptor --
(#
   theDiagram: ^ListDiagram;
   thisPatternDiagramNode: ^PatternDiagramNode;
   thisMarkNode: ^MarkNode;
   fn: ^theDiagram.FragmentNode
do
   (if dataType## <= AST## then
       dataElement[]->theAST[];
       (if THIS(myNode)## <= MarkNode## then
           THIS(myNode)[]->thisMarkNode[];
           thisMarkNode.thePatternDiagramNode->thisPatternDiagramNode[];
           (if thisPatternDiagramNode[] = none then
               thisMarkNode.theNode[]->thisPatternDiagramNode[]
           if)
        else
           THIS(myNode)[]->thisPatternDiagramNode[]
       if);
       thisPatternDiagramNode.theDiagram->theDiagram[];
       (if thisPatternDiagramNode## <= theDiagram.FragmentNode## then
           thisPatternDiagramNode[]->fn[]; 
        else
           theDiagram.theFragmentNode->fn[]; 
       if);
       (if (fn.theFragment).fullname->(theAST.frag.fullname).equal then
           (Id,attributeId,@@ theAST.index,4)->UDWriteType->ok;
           (if ok then
               (if userDataVerbose then
                   'Write AST data succeed (index): '->puttext;
                   theAST.index->putint;
                   'for id: '->puttext;
                   id->putint;
                   newline
               if)
            else
               (if userDataVerbose then 'Write AST data fail'->putline if)
           if)
        else
           
       if);
       (if not ok then true->isNone if)
   if);
   INNER
#)  

-- ReadASTReferenceUserData: Descriptor --
(#
   theDiagram: ^ListDiagram;
   thisPatternDiagramNode: ^PatternDiagramNode;
   thisMarkNode: ^MarkNode;
   fn: ^theDiagram.FragmentNode;
   anAST: ^AST;
do
     (*(# anAST: ^AST
     do*)
        (if dataType## <= AST## then
            (Id,attributeId,@@ ASTIndex)->UDReadType->ok;
            (if ok then
                (if userDataVerbose then
                    'Read AST data succeed (index): '->puttext;
                    ASTIndex->putint;
                    'for id: '->puttext;
                    id->putint;
                    newline
                if);
                (if THIS(myNode)## <= MarkNode## then
                    THIS(myNode)[]->thisMarkNode[];
                    thisMarkNode.thePatternDiagramNode
                      ->thisPatternDiagramNode[];
                    (if thisPatternDiagramNode[] = none then
                        thisMarkNode.theNode[]->thisPatternDiagramNode[]
                    if)
                 else
                    THIS(myNode)[]->thisPatternDiagramNode[]
                if);
                thisPatternDiagramNode.theDiagram->theDiagram[];
                (if thisPatternDiagramNode## <= theDiagram.FragmentNode## then
                    thisPatternDiagramNode[]->fn[]
                 else
                    theDiagram.theFragmentNode->fn[]
                if);
                (fn.theFragment,fn[])->theGroupPage.openFormInSif;
                fn.theFragment->theFragmentForm[];
                (if theFragmentForm[] <> none then
                    ASTIndex
                      ->theFragmentForm.indexToNode
                        (#
                           indexOutOfRange::< 
                             (# 
                             do 'indexOutOfRange'->putLine; true->continue; 
                             #);
                           noSuchSymbol::< 
                             (# 
                             do 'noSuchSymbol'->putLine; true->continue; 
                             #);
                           grammarGenRefArrayError::< 
                             (# 
                             do
                                'grammarGenRefArrayError'->putLine;
                                true->continue;
                                
                             #)
                        #)->anAST[];
                    (if anAST[] = none then
                        'Gpp ReadASTReferenceUserData: node not found'->putLine
                     else
                        (if userDataVerbose then
                            'indexToNode ok (index): '->puttext;
                            anAST.index->putint;
                            newline
                        if);
                        anAST[]->dataElement[]
                    if);
                    
                if)
             else
                (if userDataVerbose then 'Read AST data fail'->putline if)
            if)
         else
            'ReadASTReferenceUserData: dataType## is not in AST##'->putline;
        if);
        (*INNER
     #)*)
#)  

-- WritePointUserData: Descriptor --
(#  do thePoint->(x,y) #)  

-- ReadPointUserData: Descriptor --
(#  do (x,y)->thePoint #)  

-- InitPointUserData: Descriptor --
(#  do x.Init; y.Init #)  

-- ReadFragReferenceUserData: Descriptor --
(#
   theFrag: ^FragmentForm;
   fg: ^FragmentGroup;
   error: ^Stream;
   theDiagram: ^ListDiagram;
   thisPatternDiagramNode: ^PatternDiagramNode
do
   (if dataType## <= FragmentForm## then
       (if dataElement[] = none then
           (theGroupName,error[])->top.open->fg[];
           (if fg[] <> none then
               (theFragName,error[])->fg.open->theFrag[]->dataElement[];
               (if theFrag[] <> none then
                   THIS(myNode)[]->thisPatternDiagramNode[];
                   thisPatternDiagramNode.theDiagram->theDiagram[];
                   (if thisPatternDiagramNode## <= theDiagram.FragmentNode##
                    then
                       (theFrag[],thisPatternDiagramNode[])
                         ->theGroupPage.openFormInSif
                    else
                       (theFrag[],theDiagram.theFragmentNode)
                         ->theGroupPage.openFormInSif
                   if);
                   (if userDataVerbose then
                       'Read fragmentform user data succeed: '->puttext;
                       fg.fullname->puttext;
                       '-'->puttext;
                       theFrag.name->putline
                   if)
                else
                   (if userDataVerbose then
                       'Read fragmentform user data fail: '->puttext;
                       theGroupName->puttext;
                       '-'->puttext;
                       theFragName->putline
                   if)
               if)
            else
               (if userDataVerbose then
                   'Read fragmentform user data: open group fail!'->putline
               if)
           if)
        else
           dataElement[]->theFrag[];
           (if userDataVerbose then
               'Read fragmentform <> none: '->puttext; theFrag.name->putline
           if)
       if)
   if)
#)  

-- ReadDiagramReferenceUserData: Descriptor --
(# 
do
   (if dataType## <= Diagram## then
       (if dataElement[] = none then
           (Id,attributeId,@@ IDdata)->UDReadType->ok;
           (if ok then
               (if userDataVerbose then
                   'Read Diagram reference data for id: '->puttext;
                   id->putint;
                   ' with attributeId: '->puttext;
                   attributeId->putint;
                   ' succeed: '->puttext;
                   IDdata->putint;
                   newline
               if);
               IDdata->theObjectList.find->theNode[];
               theNode.thisDiagram->dataElement[]
            else
               (if userDataVerbose then
                   'Read Diagram reference data for id: '->puttext;
                   id->putint;
                   ' with attributeId: '->puttext;
                   attributeId->putint;
                   ' fail'->putline
               if)
           if)
       if)
   if)
#)  

-- WriteDiagramReferenceUserData: Descriptor --
(# 
do
   (if dataType## <= Diagram## then
       dataElement[]->theDiagram[];
       theDiagram.titleNode.ID->IDData;
       (ID,attributeID,@@ IDData,4)->UDWriteType->ok;
       (if ok then
           (if userDataVerbose then
               'Write Diagram reference data for id: '->puttext;
               id->putint;
               ' with attributeId: '->puttext;
               attributeId->putint;
               ' succeed:'->puttext;
               IDData->putint;
               newline
           if)
        else
           (if userDataVerbose then
               'Write Diagram reference data for id: '->puttext;
               id->putint;
               ' with attributeId: '->puttext;
               attributeId->putint;
               ' fail'->putline
           if)
       if)
   if)
#)  

-- InitDiagramReferenceUserData: Descriptor --
(# 
do
   (if dataType## <= Diagram## then
       IDUserDataAttributes+1->IDUserDataAttributes->attributeId;
       (if userDataVerbose then
           'Initialising Diagram reference user data for id: '->puttext;
           id->putint;
           ' ;attributeID: '->puttext;
           attributeID->putint;
           newline
       if);
       true->attrIDalreadyInit
   if)
#)  

-- WriteFragReferenceUserData: Descriptor --
(# theFrag: ^FragmentForm; theGroup: ^FragmentGroup
do
   (if dataType## <= FragmentForm## then
       dataElement[]->theFrag[];
       theFrag.name->theFragName;
       theFrag.Father->theGroup[];
       theGroup.fullname->theGroupName
   if)
#)  

-- ReadFragGroupReferenceUserData: Descriptor --
(# error: ^Text
do
   (if dataElement[] = none then
       (fullname,error[])->top.open->dataElement[]
   if)
#)  

-- InitFragReferenceUserData: Descriptor --
(#  do theFragName.Init; theGroupName.Init #)  

-- InitASTReferenceUserData: Descriptor --
(#  do INNER init #)  

-- InitLocalNode: Descriptor --
(# 
do
   &Type[]->DataType[];
   LocalNodesUDAttributes+1->LocalNodesUDAttributes->attributeId;
   (if userDataVerbose then
       'Initialising element in localNodes list for id='->puttext;
       id->putint;
       ' ;attributeID: '->puttext;
       attributeID->putint;
       newline
   if);
   (if dataElement[] <> none then
       (if userDataVerbose then
           'dataElement was none. Writing user data!'->putline
       if);
       dataElement[]->WriteUserData
   if);
   INNER InitLocalNode
#)  

-- InitDecomposDiagram: Descriptor --
(# 
do
   &Type[]->DataType[];
   DecomposDiagramsUDAttributes+1->DecomposDiagramsUDAttributes->attributeId;
   (if userDataVerbose then
       'Initialising element in decomposDiagrams list for id='->puttext;
       id->putint;
       ' ;attributeID: '->puttext;
       attributeID->putint;
       newline
   if);
   (if dataElement[] <> none then
       (if userDataVerbose then
           'dataElement was none. Writing user data!'->putline
       if);
       dataElement[]->WriteUserData
   if);
   INNER InitDecomposDiagram
#)  

-- OADDocumentUpdateUserDataIDs: Descriptor --
(#
   oldLocalNodesUDAttributes,oldDecomposDiagramsUDAttributes: @integer;
   ok: @Boolean
do
   LocalNodesUserDataStart->rangeStart;
   LocalNodesUserDataEnd->rangeEnd;
   (myCurrent.Id,LocalNodesUserDataEnd-1,@@ oldLocalNodesUDAttributes)
     ->UDReadType->ok;
   (LocalNodesUserDataStart,oldLocalNodesUDAttributes,myCurrent.ID)->update;
   decomposDiagramsUserDataStart->rangeStart;
   decomposDiagramsUserDataEnd->rangeEnd;
   (myCurrent.Id,LocalNodesUserDataEnd-1,@@ oldDecomposDiagramsUDAttributes)
     ->UDReadType->ok;
   (decomposDiagramsUserDataStart,oldDecomposDiagramsUDAttributes,myCurrent.ID)
     ->update
#)  

-- resetLocalNodesUD: Descriptor --
(# range: @integer; ok: @Boolean
do
   LocalNodesUDAttributes-LocalNodesUserDataStart->range;
   (for i: range repeat
     (ID,LocalNodesUserDataStart+i)->UDdeleteType->ok;
     (if ok then
         (if userDataVerbose then
             'For id '->puttext;
             id->putint;
             ' and attributeID '->puttext;
             LocalNodesUserDataStart+i->putint;
             ' delete LocalNodes userdata succeed'->putline
         if)
      else
         (if userDataVerbose then
             'For id '->puttext;
             id->putint;
             ' and attributeID '->puttext;
             LocalNodesUserDataStart+i->putint;
             ' delete LocalNodes userdata fail'->putline
         if)
     if)
   for);
   LocalNodesUserDataStart->LocalNodesUDAttributes
#)  

