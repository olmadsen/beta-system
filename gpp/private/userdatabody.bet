ORIGIN '../gppinterface';
INCLUDE 'diagramattributes';
-- WriteASTReferenceUserData: Descriptor --
(#
   theDiagram: ^ListDiagram;
   thisPatternDiagramNode: ^PatternDiagramNode;
   thisMarkNode: ^MarkNode;
   fn: ^theDiagram.FragmentNode
do
   (if dataType## <= mps.AST## then
       dataElement[]->theAST[];
       (if THIS(UserDataNode)## <= MarkNode## then
           THIS(UserDataNode)[]->thisMarkNode[];
           thisMarkNode.thePatternDiagramNode->thisPatternDiagramNode[];
           (if thisPatternDiagramNode[] = none then
               thisMarkNode.theNode[]->thisPatternDiagramNode[]
           if)
        else
           THIS(UserDataNode)[]->thisPatternDiagramNode[]
       if);
       thisPatternDiagramNode.theDiagram->theDiagram[];
       (if thisPatternDiagramNode## <= theDiagram.FragmentNode## then
           thisPatternDiagramNode[]->fn[]; 
        else
           theDiagram.theFragmentNode->fn[]; 
       if);
       (if (fn.theFragment).fullname->(theAST.frag.fullname).equal then
           (Id,attributeId,theAST.index)->writeIntegerUserData
        else
           
       if)
   if);
   INNER writeUserData
#)  

-- ReadASTReferenceUserData: Descriptor --
(#
   theDiagram: ^ListDiagram;
   thisPatternDiagramNode: ^PatternDiagramNode;
   thisMarkNode: ^MarkNode;
   fn: ^theDiagram.FragmentNode;
   anAST: ^MPS.AST;
   
do (*(# anAST: ^MPS.AST
    do*)
   (if dataType## <= mps.AST## then
       (Id,attributeId)->readIntegerUserData->ASTIndex;
       (if ASTIndex <> 0 then
           (if THIS(UserDataNode)## <= MarkNode## then
               THIS(UserDataNode)[]->thisMarkNode[];
               thisMarkNode.thePatternDiagramNode->thisPatternDiagramNode[];
               (if thisPatternDiagramNode[] = none then
                   thisMarkNode.theNode[]->thisPatternDiagramNode[]
               if)
            else
               THIS(UserDataNode)[]->thisPatternDiagramNode[]
           if);
           thisPatternDiagramNode.theDiagram->theDiagram[];
           (if thisPatternDiagramNode## <= theDiagram.FragmentNode## then
               thisPatternDiagramNode[]->fn[]
            else
               theDiagram.theFragmentNode->fn[]
           if);
           (fn.theFragment,fn[])->theGroupPage.openFormInSif;
           fn.theFragment->theFragmentForm[];
           (if theFragmentForm[] <> none then
               ASTIndex
                 ->theFragmentForm.indexToNode
                   (#
                      indexOutOfRange::< 
                        (# 
                        do 'indexOutOfRange'->putLine; true->continue; 
                        #);
                      noSuchSymbol::< 
                        (#  do 'noSuchSymbol'->putLine; true->continue;  #);
                      grammarGenRefArrayError::< 
                        (# 
                        do 'grammarGenRefArrayError'->putLine; true->continue; 
                        #)
                   #)->anAST[];
               (if anAST[] = none then
                   'Gpp ReadASTReferenceUserData: node not found'->putLine
                else
                   (if userDataVerbose then
                       'indexToNode ok (index): '->puttext;
                       anAST.index->putint;
                       newline
                   if);
                   anAST[]->dataElement[]
               if);
               
           if)
       if)
    else
       'ReadASTReferenceUserData: dataType## is not in AST##'->putline; 
   if);
   (*INNER
    #)*)
   
#)  

-- WritePointUserData: Descriptor --
(#  do thePoint->(x,y) #)  

-- ReadPointUserData: Descriptor --
(#  do (x,y)->thePoint #)  

-- InitPointUserData: Descriptor --
(#  do x.Init; y.Init #)  

-- ReadFragReferenceUserData: Descriptor --
(#
   theFrag: ^mps.fragmentform;
   fg: ^mps.fragmentgroup;
   error: ^Stream;
   theDiagram: ^ListDiagram;
   thisPatternDiagramNode: ^PatternDiagramNode;
   mess: @text
do
   (if dataType## <= mps.fragment## then
       (if dataElement[] = none then
           theGroupName->topDotOpen->fg[];
           (if dataType## <= mps.FragmentForm## then
               (if fg[] <> none then
                   thisOperation:
                   (theFragName,error[])
                     ->fg.open
                       (#
                          astOverflow::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'AST overflow for '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          fragmentNotExisting::< 
                            (# t: ^text
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'Fragment file does not exist '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          grammarNotFound::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'Grammar was not found'->mess;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          badFormat::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'Bad format: '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          fatalParseError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'Fatal parse error: '->mess;
                               errNo->mess.putInt;
                               ' for '->mess.puttext;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          doubleFormDeclaration::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'Double form declaration in '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          readAccessError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'No read access to '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          EOSError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'End Of Stream error in '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          noSuchFileError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'No such file: '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          fileExistsError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'File exists error'->mess;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          noSpaceLeftError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'No space left on device'->mess;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          otherFileError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'File error for: '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                            #);
                          WriteAccessOnLstFileError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'Access error on .lst file for '->mess;
                               theFragName->mess.puttext;
                               mess[]->AlertUser;
                               leave thisOperation
                               (*'Access error on .lst file'->myException*)
                            #);
                          writeAccessError::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               'No write access'->mess;
                               mess[]->AlertUser;
                               leave thisOperation
                               (*'Write access error'->myException
                                * *)
                            #);
                          startingParsing::< 
                            (# 
                            do
                               'Parsing '->mess;
                               theFragName->mess.putText;
                               mess[]->alertUser;
                               
                            #);
                          ParseErrors::< 
                            (# 
                            do
                               none ->f[];
                               none ->mps.theCatcher[];
                               mess[]->AlertUser;
                               leave thisOperation
                            #)
                       #)->theFrag[]->dataElement[];
                   (if theFrag[] <> none then
                       THIS(UserDataNode)[]->thisPatternDiagramNode[];
                       thisPatternDiagramNode.theDiagram->theDiagram[];
                       (if thisPatternDiagramNode## <= theDiagram.FragmentNode##
                        then
                           (theFrag[],thisPatternDiagramNode[])
                             ->theGroupPage.openFormInSif
                        else
                           (theFrag[],theDiagram.theFragmentNode)
                             ->theGroupPage.openFormInSif
                       if);
                       (if userDataVerbose then
                           'Read fragmentform user data succeed: '->puttext;
                           fg.fullname->puttext;
                           '-'->puttext;
                           theFrag.name->putline
                       if)
                    else
                       (if userDataVerbose then
                           'Read fragmentform user data fail: '->puttext;
                           theGroupName->puttext;
                           '-'->puttext;
                           theFragName->putline
                       if)
                   if)
                else
                   (if userDataVerbose then
                       'Read fragmentform user data: open group fail!'->putline
                   if)
               if)
            else
               fg[]->dataElement[]
           if)
       if)
   if)
#)  

-- ReadDiagramReferenceUserData: Descriptor --
(# 
do
   (if dataType## <= Diagram## then
       (if dataElement[] = none then
           (Id,attributeId)->readIntegerUserData->IDData;
           (if IDData <> 0 then
               IDdata->theObjectList.find->theNode[];
               (if theNode[] <> none then
                   theNode.thisDiagram->dataElement[]
               if)
           if)
       if)
   if)
#)  

-- WriteDiagramReferenceUserData: Descriptor --
(# 
do
   (if dataType## <= Diagram## then
       dataElement[]->theDiagram[];
       theDiagram.titleNode.ID->IDData;
       (ID,attributeID,IDData)->writeIntegeruserData
   if)
#)  

-- InitDiagramReferenceUserData: Descriptor --
(# 
do
   (if dataType## <= Diagram## then
       IDUserDataAttributes+1->IDUserDataAttributes->attributeId;
       (if userDataVerbose then
           'Initialising Diagram reference user data for id: '->puttext;
           id->putint;
           ' ;attributeID: '->puttext;
           attributeID->putint;
           newline
       if);
       true->attrIDalreadyInit
   if)
#)  

-- WriteFragReferenceUserData: Descriptor --
(# theFrag: ^mps.fragment; theGroup: ^mps.fragmentgroup
do
   (if dataType## <= mps.fragment## then
       dataElement[]->theFrag[];
       (if dataType## <= mps.FragmentForm## then
           theFrag.name->theFragName; (theFrag.Father).fullname->theGroupName
        else
           (if dataType## <= mps.FragmentGroup## then
               theFrag.fullname->theGroupName
           if)
       if)
   if)
#)  

-- ReadFragGroupReferenceUserData: Descriptor --
(# error: ^Text
do (if dataElement[] = none then fullname->topDotOpen->dataElement[] if)
#)  

-- InitFragReferenceUserData: Descriptor --
(#  do theFragName.Init; theGroupName.Init #)  

-- InitASTReferenceUserData: Descriptor --
(#  do INNER init #)  

-- InitLocalNode: Descriptor --
(# 
do
   &Type[]->DataType[];
   LocalNodesUDAttributes+1->LocalNodesUDAttributes->attributeId;
   (if userDataVerbose then
       'Initialising element in localNodes list for id='->puttext;
       id->putint;
       ' ;attributeID: '->puttext;
       attributeID->putint;
       newline
   if);
   (if dataElement[] <> none then
       (if userDataVerbose then
           'dataElement was none. Writing user data!'->putline
       if);
       dataElement[]->WriteUserData
   if);
   INNER InitLocalNode
#)  

-- InitDecomposDiagram: Descriptor --
(# 
do
   &Type[]->DataType[];
   DecomposDiagramsUDAttributes+1->DecomposDiagramsUDAttributes->attributeId;
   (if userDataVerbose then
       'Initialising element in decomposDiagrams list for id='->puttext;
       id->putint;
       ' ;attributeID: '->puttext;
       attributeID->putint;
       newline
   if);
   (if dataElement[] <> none then
       (if userDataVerbose then
           'dataElement was none. Writing user data!'->putline
       if);
       dataElement[]->WriteUserData
   if);
   INNER InitDecomposDiagram
#)  

-- resetLocalNodesUD: Descriptor --
(# range: @integer; ok: @Boolean
do
   oldLocalNodesUDAttributes-LocalNodesUserDataStart->range;
   (for i: range repeat
     (ID,LocalNodesUserDataStart+i)->deleteUserDataType
   for);
   LocalNodesUserDataStart->LocalNodesUDAttributes
#)  

