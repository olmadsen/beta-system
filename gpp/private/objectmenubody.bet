ORIGIN '../gppinterface';
INCLUDE 'objectgppproperties'
        'objectprivate';
-- ObjectMenuShowObjectdiagram: Descriptor --
(#
   p: ^Page;
   OADp: ^OADPage;
   object: ^p.DesignObject;
   diag: ^OADp.Listdiagram;
   patterndiag: ^OADp.Patterndiagram;
   patterndiagnode: ^OADp.patterndiagramNode;
   node: ^theObjectPage.AbstractedObjectNode;
   names: ^betaGram.Names;
   attDecl: ^betaGram.AttributeDecl;
   nd: ^betaGram.NameDcl;
   anAST: ^MPS.AST
do
   CurrentPage->p[];
   (if p.struc = OADPage##
    // true then
       p[]->OADp[];
       OADp.CurrentObject->object[];
       (if object.struc <= OADp.PatterndiagramNode##
        // true then
           object[]->PatterndiagNode[];
           PatterndiagNode.theDiagram->diag[];
           (if diag.struc <= OADp.PatternDiagram##
            // true then
               diag[]->patterndiag[];
               (if object.struc
                // patterndiag.title## then
                   (if patterndiag.theDescriptor <> none then
                       (if (patterndiag.theDescriptor).frag.father->isChecked
                        then
                           &ObjectPage[]->theObjectPage[];
                           theObjectPage.new;
                           'Object Page'->theObjectPage.PageTitle;
                           patterndiag.theDescriptor
                             ->ObjectPrivate.getEnclosingPattern->attDecl[];
                           attDecl.getSon1->names[];
                           names.newScan
                             (#  do current[]->nd[] #);
                           (if attDecl.symbol
                            // betaGram.SimpleDecl then
                               attDecl.getSon2->anAST[];
                               (if anAST.symbol
                                // betaGram.StaticComponent then
                                   &theObjectPage.AbstrPartComponentNode[]
                                     ->node[]
                                else
                                   &theObjectPage.AbstrPartObjectNode[]->node[]
                               if)
                            else
                               &theObjectPage.AbstrPartObjectNode[]->node[]
                           if);
                           (nd[],patterndiag.theDescriptor,false)->node.display;
                           node[]->theObjectPage.CurrentObject
                        else
                           'Group must be checked to show object diagram'
                             ->alertUser
                       if)
                    else
                       'Can''t show object diagram for selected node'->alertUser
                   if)
               if)
           if)
       if)
   if)
#)  

-- ObjectMenuGlobalConfirmedScan: Descriptor --
(# 
do
   false->OGppProp.LocalIncludeScan->LocalIncludeScan.check;
   true->OGppProp.GlobalConfirmedScan->check;
   false->OGppProp.GlobalIncludeScan->GlobalIncludeScan.check
#)  

-- ObjectMenuLocalIncludeScan: Descriptor --
(# 
do
   true->OGppProp.LocalIncludeScan->check;
   false->OGppProp.GlobalConfirmedScan->GlobalConfirmedScan.check;
   false->OGppProp.GlobalIncludeScan->GlobalIncludeScan.check
#)  

-- ObjectMenuGlobalIncludeScan: Descriptor --
(# 
do
   false->OGppProp.LocalIncludeScan->LocalIncludeScan.check;
   false->OGppProp.GlobalConfirmedScan->GlobalConfirmedScan.check;
   true->OGppProp.GlobalIncludeScan->check
#)  

-- ObjectMenuAbstract: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   object: ^p.DesignObject;
   detailnode: ^oPage.DetailedObjectNode;
   x,y,w,h,dummy: @integer
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->object[];
       (if object.struc <= oPage.DetailedObjectNode##
        // true then object[]->detailnode[]; detailnode.abstract
       if)
   if)
#)  

-- ObjectMenuAbstractRec: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   object: ^p.DesignObject;
   detailnode: ^oPage.DetailedObjectNode;
   x,y,w,h,dummy: @integer
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->object[];
       (if object.struc <= oPage.DetailedObjectNode##
        // true then object[]->detailnode[]; detailnode.abstractRecursive
       if)
   if)
#)  

-- ObjectMenuDetail: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   object: ^p.DesignObject;
   abstrNode: ^oPage.AbstractedObjectNode;
   x,y,w,h,dummy: @integer
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->object[];
       (if object.struc <= oPage.AbstractedObjectNode##
        // true then
           object[]->abstrNode[];
           (if abstrnode.pagelink[] = none then
               abstrnode.detail
            else
               abstrnode.pagelink[]->CurrentPage
           if)
       if)
   if)
#)  

-- ObjectMenuDetailRec: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   object: ^p.DesignObject;
   abstrNode: ^oPage.AbstractedObjectNode;
   x,y,w,h,dummy: @integer
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->object[];
       (if object.struc <= oPage.AbstractedObjectNode##
        // true then
           object[]->abstrNode[];
           (if abstrnode.pagelink[] = none then
               abstrnode.detailRecursive
            else
               abstrnode.pagelink[]->CurrentPage
           if)
       if)
   if)
#)  

-- ObjectMenuDetailOnDiffPage: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   objectnode: ^oPage.ObjectDiagramNode;
   abstrNode,node: ^oPage.AbstractedObjectNode
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->objectnode[];
       (if objectnode.struc <= oPage.AbstractedObjectNode##
        // true then
           objectnode[]->abstrNode[];
           3->AbstrNode.linethickness;
           (if objectNode.struc
            // theObjectPage.AbstrPartObjectNode## then
               &ObjectPage[]->theObjectPage[]->AbstrNode.pagelink[];
               theObjectPage.new;
               theObjectPage[]->CurrentPage;
               &theObjectPage.AbstrPartObjectNode[]->node[]
            // theObjectPage.AbstrPartComponentNode## then
               &ObjectPage[]->theObjectPage[]->AbstrNode.pagelink[];
               theObjectPage.new;
               theObjectPage[]->CurrentPage;
               &theObjectPage.AbstrPartComponentNode[]->node[]
            // theObjectPage.AbstrDynamicObjectNode## then
               &ObjectPage[]->theObjectPage[]->AbstrNode.pagelink[];
               theObjectPage.new;
               theObjectPage[]->CurrentPage;
               &theObjectPage.AbstrDynamicObjectNode[]->node[]
            // theObjectPage.AbstrDynamicComponentNode## then
               &ObjectPage[]->theObjectPage[]->AbstrNode.pagelink[];
               theObjectPage.new;
               theObjectPage[]->CurrentPage;
               &theObjectPage.AbstrDynamicComponentNode[]->node[]
            // theObjectPage.AbstrOperationObjectNode## then
               &ObjectPage[]->theObjectPage[]->AbstrNode.pagelink[];
               theObjectPage.new;
               theObjectPage[]->CurrentPage;
               &theObjectPage.AbstrOperationObjectNode[]->node[]
           if);
           'Object Page'->theObjectPage.PageTitle;
           (objectnode.theDecl[],objectnode.theObjectDescriptor[],false)
             ->node.display;
           node[]->theObjectPage.CurrentObject;
           node.detail
       if)
   if)
#)  

-- ObjectMenuOverview: Descriptor --
(# p: ^Page; oPage: ^ObjectPage; object: ^oPage.ObjectDiagramNode
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then p[]->oPage[]; oPage.CurrentObject->object[]; object.overview
   if)
#)  

-- ObjectMenuToggleDynamicCreations: Descriptor --
(#  do ObjectPrivate.toggleDynamicCreations #)  

-- ObjectMenuToggleProcedureCalls: Descriptor --
(#  do ObjectPrivate.toggleProc #)  

-- ObjectMenuShowSimpleObjects: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   object: ^p.DesignObject;
   detailnode: ^oPage.DetailedObjectNode
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->object[];
       (if object.struc <= oPage.DetailedObjectNode##
        // true then
           object[]->detailnode[];
           (if detailnode.showSimpleObjects = false then
               true->detailnode.showSimpleObjects; detailnode.showSimple
           if)
       if)
   if)
#)  

-- ObjectMenuHideSimpleObjects: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   object: ^p.DesignObject;
   detailnode: ^oPage.DetailedObjectNode
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->object[];
       (if object.struc <= oPage.DetailedObjectNode##
        // true then
           object[]->detailnode[];
           (if detailnode.showSimpleObjects = true then
               false->detailnode.showSimpleObjects; detailnode.hideSimple
           if)
       if)
   if)
#)  

-- ObjectMenuCompact: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   object: ^p.DesignObject;
   detailnode: ^oPage.DetailedObjectNode;
   x,y,w,h,dummy: @integer
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->object[];
       (if object.struc <= oPage.DetailedObjectNode##
        // true then object[]->detailnode[]; detailnode.compact
       if)
   if)
#)  

-- ObjectMenuBlowup: Descriptor --
(#
   p: ^Page;
   oPage: ^ObjectPage;
   object: ^p.DesignObject;
   detailnode: ^oPage.DetailedObjectNode;
   x,y,w,h,dummy: @integer
do
   CurrentPage->p[];
   (if p.struc = ObjectPage##
    // true then
       p[]->oPage[];
       oPage.CurrentObject->object[];
       (if object.struc <= oPage.DetailedObjectNode##
        // true then object[]->detailnode[]; detailnode.blowup
       if)
   if)
#)  

-- ObjectMenuDumpProcedurecalls: Descriptor --
(#  do ObjectPrivate.ProcedureCalls.dump #)  

-- ObjectMenuDumpDynamicGenerations: Descriptor --
(#  do ObjectPrivate.DynamicGenerations.dump #)  

-- ObjectMenuPrintNodeID: Descriptor --
(# thePage: ^Page; theObject: ^thePage.DesignObject
do
   CurrentPage->thePage[];
   (if thePage[] <> none
    // true then
       thePage.CurrentObject->theObject[];
       (if theObject[] <> none
        // true then 'ID: '->puttext; theObject.ID->putint; newline; 
       if)
   if)
#)  

-- ObjectMenuDumpAst: Descriptor --
(# thePage: ^ObjectPage; theObject: ^thePage.ObjectDiagramNode
do
   CurrentPage->thePage[];
   (if thePage[] <> none
    // true then
       thePage.CurrentObject->theObject[];
       (if theObject[] <> none
        // true then
           'Ast Dump: '->puttext; theObject.theObjectDescriptor.dump; newline; 
       if)
   if)
#)  

-- ObjectMenuInit: Descriptor --
(# 
do
   (UserMenu5+1,'Show objectdiagram')->ShowObjectdiagram.init;
   (* (UserMenu5+2,'-')->sep1.init; *)
   (UserMenu5+2,'Local search')->LocalIncludeScan.init;
   (UserMenu5+3,'Global search')->GlobalIncludeScan.init;
   (UserMenu5+4,'Global search with confirmation')->GlobalConfirmedScan.init;
   (UserMenu5+5,'-')->sep2.init;
   (UserMenu5+6,'Abstract')->Abstract.init;
   (UserMenu5+7,'Abstract recursively')->AbstractRec.init;
   (UserMenu5+8,'Detail')->Detail.init;
   (UserMenu5+9,'Detail recursively')->DetailRec.init;
   (UserMenu5+10,'Detail on different page')->DetailOnDiffPage.init;
   (UserMenu5+11,'Overview')->Overview.init;
   (UserMenu5+12,'-')->sep3.init;
   (UserMenu5+13,'Toggle dynamic creations')->ToggleDynamicCreations.init;
   (UserMenu5+14,'Toggle procedure calls')->ToggleProcedureCalls.init;
   (UserMenu5+15,'-')->sep4.init;
   (UserMenu5+16,'Show simple objects')->ShowSimpleObjects.init;
   (UserMenu5+17,'Hide simple objects')->HideSimpleObjects.init;
   (UserMenu5+18,'-')->sep5.init;
   (UserMenu5+19,'Compact')->Compact.init;
   (UserMenu5+20,'Blowup')->Blowup.init;
   (UserMenu5+21,'-')->sep6.init;
   (UserMenu5+22,'Dump ProcedureCalls')->DumpProcedurecalls.init;
   (UserMenu5+23,'Dump DynamicGenerations')->DumpDynamicGenerations.init;
   (UserMenu5+24,'Print node ID')->PrintNodeID.init;
   (UserMenu5+25,'Dump Ast')->DumpAst.init
#)  

