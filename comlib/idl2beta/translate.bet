ORIGIN 'control';
BODY 'preprocess';
BODY 'interface_header';
BODY 'interface_body';
BODY 'type_dcl';
BODY 'const_exp';
BODY 'declarator';
BODY 'attributes';
BODY 'typelib';

(* The following two includes are in order to
 * report byte position at astoverflow errors.
 *)
INCLUDE '~beta/mps/v5.2/private/astparser';
INCLUDE '~beta/bobs/v6.11/private/lex';

--Translate: Descriptor -----
(#
   <<SLOT TranslateLib: attributes>>;
   
   optionalPresent: 
     (# opt: ^astinterface.ast; 
     enter opt[]
     exit opt.kind<>ast.kinds.optional
     #);
   
   ExpandFileName:
     (# Name, ExpandedName: ^text
     enter Name[]
     do (Name[], '') 
          -> ast.thePathHandler.convertFilePath -> ExpandedName[];
     exit ExpandedName[]
     #);
   
   Preprocess:
     (# 
     do <<SLOT PreprocessBody:descriptor>>
     #);
   
   Postprocess:
     (# 
     do <<SLOT Postprocess:descriptor>>
     #);
      
   EmitComments: FetchComments
     (# 
     do (if currentSubcomment.lgth<>0 then
            currentSubcomment[]->outfile.CommentLine;
         else
            (* empty line comment *)
            outfile.newline;
        if)
     #);
   
   IdentifierToText:
     (# i: ^idl.identifier;
     enter i[]
     exit i.getnameAppl -> NameApplToText -> CheckForReservedWord
     #);
   
   EmitHolders:
     (# comment_done: @boolean;
     do (if not holders.empty then
            2 -> outfile.indent.inc;
            holders.scan
            (# 
            do (if not comment_done then
                   outfile.newline;
                   outfile.indent;
                   'Holders' -> outfile.CommentLine;
                   outfile.newline;
                   true -> comment_done;
               if);
               outfile.indent;
               current[] -> outfile.puttext;
               'Holder: Holder' -> outfile.putline;
               2 -> outfile.indent.inc;
               outfile.indent;
               outfile.PatternBegin;
               'value: @' ->outfile.puttext;
               current[] -> outfile.puttext;
               '; ' -> outfile.puttext;
               outfile.PatternEnd;
               2 -> outfile.indent.dec;
               ';' -> outfile.putline;
            #);
            2 -> outfile.indent.dec;
            outfile.newline;
        if);
     #);
   EmitRefholders:
     (# 
     do (if not refholders.empty then
            2 -> outfile.indent.inc;
            outfile.newline;
            outfile.indent;
            'Reference Holders' -> outfile.CommentLine;
            outfile.newline;
            refholders.scan
            (# 
            do outfile.indent;
               current[] -> outfile.puttext;
               'Holder: refHolder' -> outfile.puttext;
               outfile.PatternBegin;
               'type:: ' ->outfile.puttext;
               current[] -> outfile.puttext;
               ' '->outfile.put;
               outfile.PatternEnd;
               ';' -> outfile.putline;
            #);
            outfile.newline;
            2 -> outfile.indent.dec;
        if);
     #);
   
   DoFile:
     (# ifile: @file;
        ok: @boolean;
        ff: ^ast.fragmentForm;
        info: ^declarator_info;
     do
        (* Defines not cleared - should be used for all files *)
        holders.clear;
        refholders.clear;
        imports.clear;
        (* global_types should not be cleared - global to all files.*)
        (* local_types, propputs, propgets not cleared either: 
         * The types from imported files should be kept.
         *)
        
        (if not quiet then '  Preprocessing...' -> screen.putline; if);
        PreProcess;
        Ifilename[] -> ifile.name;
        ifile.openread;
        idl[] -> ast.newFragmentForm -> ff[];
        (if not quiet then 
            '  Parsing...' -> screen.putline; 
            '    [' -> screen.puttext;
            BobsTab -> ast.expandToFullPath -> screen.puttext;
            ' '->screen.put;
            ifilename[] -> screen.puttext;
            ']' -> screen.putline;
        if);
        (0,fromBeginning) -> ifile.setpos;
        (1,ifile[],screen[],ff[]) -> 
        idl.parser.doParse
        (# astOverflow::
             (# 
             do '\n*** AST overflow:' -> screen.putline;
                'An overflow occurred in the internal representation'
                  -> screen.putline;
                'of the parsed IDL file. Sorry!'
                  -> screen.putline;
                'Try splitting up ' 
                  -> screen.puttext;
                IdlFileName[]
                  -> screen.putline;
                'and run bidl again.' -> screen.putline;
                (*'File pos: ' -> screen.puttext;*)
                (*idl.parser.privatepart.b.lex.pos -> screen.putint;*)
                screen.newline;
                stop;
             #)
        #) -> ok;
	(if ok then
            (if not quiet then '  Translating...' -> screen.putline; if);
            Outfile.OpenWrite;
            IdlFileName[] -> Outfile.Identification;
            ff.root[] -> do_specification;
            ifile.close;
            Outfile.Epilog;
            PostProcess;
            (*'closing ' -> screen.puttext; Outfile.Name -> screen.putline;*)
            Outfile.Close;
            (if not quiet then 
                '  Translated BETA file in: \'' -> screen.puttext;
                outfilename[] -> screen.puttext;
                '\'' -> screen.putline;
            if);
         else
            (0,fromBeginning) -> ifile.setpos;
            'Parse errors:' -> screen.putline;
            (ifile[],screen[]) -> idl.parser.ErrorReport;
            ifile.close;
            PostProcess;
        if);
     #);
      
   do_specification:
     (* Translate the top node of the IDL *)
     (# s: ^idl.specification;
     enter s[]
     do s[] -> EmitComments;
        s.getDefinitions -> do_definitions;
        EmitHolders;
        EmitRefHolders;
     #);
   
   do_declarator: 
     (* Find the NAME and POINTER STATUS
      * of the parameter. 
      *)
     (# d: ^idl.declarator;
        info: ^declarator_info;
     enter (info[],d[])
     do <<SLOT declarator_body: descriptor>>
     #);
   
   do_attributes_spec:
     (# a: ^idl.attributes_spec;
        att: ^idl_attributes;
     enter (att[], a[])
     do (att[],a.getattributes) -> do_attributes;
     #);
   
   do_attributes:
     (# a: ^idl.attributes;
        att: ^idl_attributes;
     enter (att[], a[])
     do a.newscan
        (# 
        do (if debug then ':' -> screen.put; if);
           (current[], att[]) -> do_attribute_spec; (* no 's' (:-) *)
        #);
     #);
   
   do_attribute_spec:
     (# y: ^idl.attribute_spec;
        att: ^idl_attributes (* attributes read are put into this one *);
     enter (y[], att[])
     do <<SLOT attribute_spec_body: descriptor>>
     #);
   
   do_cpp_quote:
     (# c: ^idl.cpp_quote;
     enter c[]
     do outfile.indent;
        c.getString -> StringToText -> outfile.commentline;
     #);
   
   do_definitions:
     (# d: ^idl.definitions;
     enter d[]
     do (chain, PreviousFile[]) -> Outfile.Origin;
        outfile.Lib;
        d.newscan
	(# t: ^text;
           q: ^idl.def_cpp_quote;
	do (if current.symbol
            // idl.import then current[] -> do_import
            // idl.def_type_dcl then current[] -> do_def_type_dcl 
            // idl.const_dcl then current[] -> do_const_dcl;
            // idl.except_dcl then current[] -> do_except_dcl;
            // idl.interface_dcl then current[] -> do_interface_dcl;
            // idl.dispinterface_dcl then current[] -> do_dispinterface_dcl;
            // idl.forward_dcl then current[] -> do_forward_dcl;
            // idl.module then current[] -> do_module;
            // idl.def_cpp_quote then 
               (current[]->q[]).getcpp_quote -> do_cpp_quote
            // idl.library then
               current[] -> do_library;
            // idl.coclass then
               current[] -> do_coclass;
	    else
               (# msg: ^text
               do 'do_specification: illegal current.symbol: ' -> msg[];
	          current.symbol -> msg.putint;
                  msg[] -> error;
               #);
	   if);
	#)
     #);
   
   do_def_type_dcl: 
     (# d: ^idl.def_type_dcl;
     enter d[]
     do (* 2 -> outfile.indent.inc; *)
        d.gettype_dcl -> do_type_dcl;
        (* 2 -> outfile.indent.dec; *)
     #);
   
   do_base_type_spec:
     (# s: ^idl.base_type_spec;
        dcl_info: ^declarator_info;
     enter (dcl_info[], s[])
     do <<SLOT do_base_type_spec: descriptor>>
     #);
   
   do_type_dcl: 
     (# <<SLOT type_dcl_lib: attributes>>;
        d: ^idl.type_dcl;
     enter d[]
     do <<SLOT type_dcl_body: descriptor>>
     #);
   
   do_const_dcl: 
     (# c: ^idl.const_dcl;
     enter c[]
     do outfile.indent;
        'do_const_dcl NYI' -> outfile.commentline;
     #);
   
   do_except_dcl: 
     (# e: ^idl.except_dcl;
     enter e[]
     do outfile.indent;
        'do_except_dcl NYI' -> outfile.commentline;
     #);
   
   do_scoped_name:
     (# n: ^idl.scoped_name;
        s1: ^idl.scoped_name1;
        s2: ^idl.scoped_name2;
        s3: ^idl.scoped_name3;
        id: ^text;
     enter n[]
     do (if n.symbol
         // idl.scoped_name1 then
            n[] -> s1[];
            s1.getIdentifier -> IdentifierToText 
              -> id[] 
         // idl.scoped_name2 then
            n[] -> s2[];
            s2.getIdentifier -> IdentifierToText 
              -> id[] 
         // idl.scoped_name3 then
            n[] -> s3[];
            s3.getscoped_name -> do_scoped_name -> id[];
            '.' -> id.put;
            s3.getIdentifier -> IdentifierToText 
              -> id.puttext;
        if);
     exit id[]
     #);
   
   do_const_exp:
     (# e: ^idl.const_exp;
        exp: ^text;
        <<SLOT const_exp_lib: attributes>>;
        it: @<<SLOT const_exp_body:descriptor>>;
     enter e[]
     do it
     exit exp[]
     #);
   
   do_interface_body:
     (# <<SLOT interface_body_lib: attributes>>;
        b: ^idl.interface_body;
        e: ^idl.exports;
     enter b[]
     <<SLOT interface_body_body: dopart>>
     #);
   do_interface_dcl:
     (# i: ^idl.interface_dcl;
        do_interface_header:
          (# h: ^idl.interface_header;
          enter h[]
          do <<SLOT interface_header_body: descriptor>>
          #);
     enter i[]
     do 2 -> outfile.indent.inc;
        i.getinterface_header -> do_interface_header;
        i.getinterface_body -> do_interface_body;
        2 -> outfile.indent.dec;
     #);
   do_dispinterface_dcl:
     (# i: ^idl.dispinterface_dcl;
        do_dispinterface_header:
          (# h: ^idl.dispinterface_header;
          enter h[]
          do <<SLOT dispinterface_header_body: descriptor>>
          #);
     enter i[]
     do 2 -> outfile.indent.inc;
        i.getdispinterface_header -> do_dispinterface_header;
        i.getinterface_body -> do_interface_body;
        2 -> outfile.indent.dec;
     #);
   
   do_library:
     (# lib: ^idl.library;
     enter lib[]
     do <<SLOT do_library: descriptor>>
     #);
   
   do_forward_dcl:
     (# f: ^idl.forward_dcl;
        i: ^text;
     enter f[]
     do f.getIdentifier -> IdentifierToText -> i[];
        (i[],i[]) -> local_types.add_interface_type;
     #);
   
   do_module: 
     (# m: ^idl.module;
     enter m[]
     do outfile.indent;
        'do_module NYI' -> outfile.commentline;
     #);
   
   do_import: 
     (# m: ^idl.import;
     enter m[]
     do m.getstring -> StringToText -> imports.append;
     #);
   
   do_coclass:
     (# cc: ^idl.coclass
     enter cc[]
     do <<SLOT do_coclass: descriptor>>
     #);
   
   do_definition_error: 
     (# d: ^idl.definition_error
     enter d[]
     do outfile.indent;
        'do_definition_error NYI' -> outfile.commentline;
     #);
   
do DoFile;
#)
