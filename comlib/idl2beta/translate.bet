ORIGIN 'control';
BODY 'preprocess';
BODY 'interface_header';
BODY 'interface_body';
BODY 'type_dcl';
BODY 'const_exp';
BODY 'declarator';
BODY 'attributes';
BODY 'typelib';

--Translate: Descriptor -----
(#
   <<SLOT TranslateLib: attributes>>;
   
   optionalPresent: 
     (# opt: ^astinterface.ast; 
     enter opt[]
     exit opt.kind<>ast.kinds.optional
     #);
   
   ExpandFileName:
     (# Name, ExpandedName: ^text
     enter Name[]
     do (Name[], '') 
          -> ast.thePathHandler.convertFilePath -> ExpandedName[];
     exit ExpandedName[]
     #);
   
   Preprocess:
     (# 
     do <<SLOT PreprocessBody:descriptor>>
     #);
   
   Postprocess:
     (# 
     do <<SLOT Postprocess:descriptor>>
     #);
      
   EmitComments: FetchComments
     (# 
     do (if currentSubcomment.lgth<>0 then
            currentSubcomment[]->outfile.CommentLine;
         else
            (* empty line comment *)
            outfile.newline;
        if)
     #);
   
   IdentifierToText:
     (# i: ^idl.identifier;
     enter i[]
     exit i.getnameAppl -> NameApplToText
     #);
   
   EmitHolders:
     (# comment_done: @boolean;
     do (if not holders.empty then
            2 -> outfile.indent.inc;
            holders.scan
            (# 
            do (if true
                // 'integer' -> current.equalNCS 
                // 'shortInt' -> current.equalNCS 
                // 'char' -> current.equalNCS 
                // 'boolean' -> current.equalNCS 
                // 'real' -> current.equalNCS 
                // 'int8' -> current.equalNCS 
                // 'int8u' -> current.equalNCS 
                // 'int16' -> current.equalNCS 
                // 'int16u' -> current.equalNCS 
                // 'int32' -> current.equalNCS 
                // 'int32u' -> current.equalNCS 
                // 'int64' -> current.equalNCS 
                // 'int64u' -> current.equalNCS
                // 'BSTR' -> current.equalNCS
                // 'VARIANT' -> current.equalNCS
                // 'IUnknown' -> current.equalNCS
                // 'IDispatch' -> current.equalNCS
                   then
                   (* Holders for these are already defined in comlib *)
                else
                   (if not comment_done then
                       outfile.newline;
                       outfile.indent;
                       'Holders' -> outfile.CommentLine;
                       outfile.newline;
                       true -> comment_done;
                   if);
                   outfile.indent;
                   current[] -> outfile.puttext;
                   'Holder: Holder' -> outfile.putline;
                   2 -> outfile.indent.inc;
                   outfile.indent;
                   outfile.PatternBegin;
                   'value: @' ->outfile.puttext;
                   current[] -> outfile.puttext;
                   '; ' -> outfile.puttext;
                   outfile.PatternEnd;
                   2 -> outfile.indent.dec;
                   ';' -> outfile.putline;
               if);
               2 -> outfile.indent.dec;
               outfile.newline;
            #);
        if);
     #);
   EmitRefholders:
     (# 
     do (if not refholders.empty then
            2 -> outfile.indent.inc;
            outfile.newline;
            outfile.indent;
            'Reference Holders' -> outfile.CommentLine;
            outfile.newline;refholders.scan
            (# 
            do outfile.indent;
               current[] -> outfile.puttext;
               'Holder: refHolder' -> outfile.putline;
               2 -> outfile.indent.inc;
               outfile.indent;
               outfile.PatternBegin;
               outfile.newline;
               3 -> outfile.indent.inc;
               outfile.indent;
               'type:: ' ->outfile.puttext;
               current[] -> outfile.puttext;
               ';' -> outfile.putline;
               3 -> outfile.indent.dec;
               outfile.indent;
               outfile.PatternEnd;
               2 -> outfile.indent.dec;
               ';' -> outfile.putline;
            #);
            2 -> outfile.indent.dec;
        if);
     #);
   
   DoFile:
     (# ifile: @file;
        ok: @boolean;
        ff: ^ast.fragmentForm;
        info: ^declarator_info;
     do
        (* Defines not cleared - should be used for all files *)
        holders.clear;
        refholders.clear;
        imports.clear;
        (* global_types should not be cleared. *)
        local_types.clear;
        propputs.clear;
        propgets.clear;
        
        (if not quiet then '  Preprocessing...' -> screen.putline; if);
        PreProcess;
        Ifilename[] -> ifile.name;
        ifile.openread;
        idl[] -> ast.newFragmentForm -> ff[];
        (if not quiet then 
            '  Parsing...' -> screen.putline; 
            '    [' -> screen.puttext;
            BobsTab -> ast.expandToFullPath -> screen.puttext;
            ' '->screen.put;
            ifilename[] -> screen.puttext;
            ']' -> screen.putline;
        if);
        (0,fromBeginning) -> ifile.setpos;
        (1,ifile[],screen[],ff[]) -> idl.parser -> ok;
	(if ok then
            (if not quiet then '  Translating...' -> screen.putline; if);
            Outfile.OpenWrite;
            IdlFileName[] -> Outfile.Identification;
            ff.root[] -> do_specification;
            ifile.close;
            Outfile.Epilog;
            PostProcess;
            (*'closing ' -> screen.puttext; Outfile.Name -> screen.putline;*)
            Outfile.Close;
            (if not quiet then 
                '  Translated BETA file in: \'' -> screen.puttext;
                outfilename[] -> screen.puttext;
                '\'' -> screen.putline;
            if);
         else
            (0,fromBeginning) -> ifile.setpos;
            'Parse errors:' -> screen.putline;
            (ifile[],screen[]) -> idl.parser.ErrorReport;
            ifile.close;
            PostProcess;
        if);
     #);
      
   do_specification:
     (* Translate the top node of the IDL *)
     (# s: ^idl.specification;
     enter s[]
     do s[] -> EmitComments;
        outfile.newline;
        s.getDefinitions -> do_definitions;
        EmitHolders;
        EmitRefHolders;
     #);
   
   do_declarator: 
     (* Find the NAME and POINTER STATUS
      * of the parameter. 
      *)
     (# d: ^idl.declarator;
        info: ^declarator_info;
     enter (info[],d[])
     do <<SLOT declarator_body: descriptor>>
     #);
   
   do_attributeX:
     (# x: ^idl.attributeX;
        a: ^idl.attributes;
        att: ^idl_attributes;
     enter (att[], x[])
     do x.getattributes -> a[];
        a.newscan
        (# 
        do (current[], att[]) -> do_attributeY;
        #);
     #);
   
   do_attributeY:
     (# y: ^idl.attributeY;
        att: ^idl_attributes (* attributes read are put into this one *);
     enter (y[], att[])
     do <<SLOT attributeY_body: descriptor>>
     #);
   
   do_helpstring:
     (# s: ^idl.helpstring;
        help: ^text;
     enter s[]
     do s.getstring 
          -> StringToText
          -> help[];
     exit help[]
     #);
   
   do_definitions:
     (# d: ^idl.definitions;
     enter d[]
     do Outfile.Origin;
        outfile.Lib;
        d.newscan
	(# t: ^text;
	do (if current.symbol
            // idl.import then current[] -> do_import
            // idl.def_type_dcl then current[] -> do_def_type_dcl 
            // idl.const_dcl then current[] -> do_const_dcl;
            // idl.except_dcl then current[] -> do_except_dcl;
            // idl.interface_dcl then current[] -> do_interface_dcl;
            // idl.dispinterface_dcl then current[] -> do_dispinterface_dcl;
            // idl.forward_dcl then current[] -> do_forward_dcl;
            // idl.module then current[] -> do_module;
            // idl.library then
               current[] -> do_library;
	    else
	       '\nERROR: do_specification: illegal current.symbol: ' 
                 -> puttext;
	       current.symbol -> putint;
	       newline;
	   if);
	#)
     #);
   
   do_def_type_dcl: 
     (# d: ^idl.def_type_dcl;
     enter d[]
     do (* 2 -> outfile.indent.inc; *)
        d.gettype_dcl -> do_type_dcl;
        (* 2 -> outfile.indent.dec; *)
     #);
   
   do_base_type_spec:
     (# s: ^idl.base_type_spec;
        dcl_info: ^declarator_info;
     enter (dcl_info[], s[])
     do <<SLOT do_base_type_spec: descriptor>>
     #);
   
   do_type_dcl: 
     (# <<SLOT type_dcl_lib: attributes>>;
        d: ^idl.type_dcl;
     enter d[]
     do <<SLOT type_dcl_body: descriptor>>
     #);
   
   do_const_dcl: 
     (# c: ^idl.const_dcl;
     enter c[]
     do outfile.indent;
        'do_const_dcl NYI' -> outfile.commentline;
     #);
   
   do_except_dcl: 
     (# e: ^idl.except_dcl;
     enter e[]
     do outfile.indent;
        'do_except_dcl NYI' -> outfile.commentline;
     #);
   
   do_scoped_name:
     (# n: ^idl.scoped_name;
        s1: ^idl.scoped_name1;
        s2: ^idl.scoped_name2;
        s3: ^idl.scoped_name3;
        id: ^text;
     enter n[]
     do (if n.symbol
         // idl.scoped_name1 then
            n[] -> s1[];
            s1.getIdentifier -> IdentifierToText 
              -> id[] 
         // idl.scoped_name2 then
            n[] -> s2[];
            s2.getIdentifier -> IdentifierToText 
              -> id[] 
         // idl.scoped_name3 then
            n[] -> s3[];
            s3.getscoped_name -> do_scoped_name -> id[];
            '.' -> id.put;
            s3.getIdentifier -> IdentifierToText 
              -> id.puttext;
        if);
     exit id[]
     #);
   
   do_const_exp:
     (# e: ^idl.const_exp;
        exp: ^text;
        <<SLOT const_exp_lib: attributes>>
     enter e[]
     <<SLOT const_exp_body:dopart>>
     exit exp[]
     #);
   
   do_interface_body:
     (# <<SLOT interface_body_lib: attributes>>;
        b: ^idl.interface_body;
        e: ^idl.exports;
     enter b[]
     <<SLOT interface_body_body: dopart>>
     #);
   do_interface_dcl:
     (# i: ^idl.interface_dcl;
        do_interface_header:
          (# h: ^idl.interface_header;
          enter h[]
          do <<SLOT interface_header_body: descriptor>>
          #);
     enter i[]
     do 2 -> outfile.indent.inc;
        i.getinterface_header -> do_interface_header;
        i.getinterface_body -> do_interface_body;
        2 -> outfile.indent.dec;
     #);
   do_dispinterface_dcl:
     (# i: ^idl.dispinterface_dcl;
        do_dispinterface_header:
          (# h: ^idl.dispinterface_header;
          enter h[]
          do <<SLOT dispinterface_header_body: descriptor>>
          #);
     enter i[]
     do 2 -> outfile.indent.inc;
        i.getdispinterface_header -> do_dispinterface_header;
        i.getinterface_body -> do_interface_body;
        2 -> outfile.indent.dec;
     #);
   
   do_library:
     (# lib: ^idl.library;
     enter lib[]
     do <<SLOT do_library: descriptor>>
     #);
   
   do_forward_dcl:
     (# f: ^idl.forward_dcl;
     enter f[]
     do (* 2 -> outfile.indent.inc;
         * outfile.indent;
         * outfile.CommentBegin;
         * 'forward_dcl ignored: ' -> outfile.puttext;
         * f.getIdentifier -> IdentifierToText -> outfile.puttext;
         * outfile.CommentEnd;
         * outfile.newline;
         * 2 -> outfile.indent.dec;
         *)
     #);
   
   do_module: 
     (# m: ^idl.module;
     enter m[]
     do outfile.indent;
        'do_module NYI' -> outfile.commentline;
     #);
   
   do_import: 
     (# m: ^idl.import;
     enter m[]
     do m.getstring -> StringToText -> imports.append;
     #);
   
   do_definition_error: 
     (# d: ^idl.definition_error
     enter d[]
     do outfile.indent;
        'do_definition_error NYI' -> outfile.commentline;
     #);
   
do DoFile;
#)
