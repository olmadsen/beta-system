ORIGIN '~beta/mps/astlevel';

--LIB: attributes--

(* Operations needed because exit parameters of getNameAppl,
 * getNameDecl etc are not quilified enough to say gettext etc.
 * on them!
 *)
NameApplToText: 
  (# na: ^astinterface.NameAppl enter na[]  exit na.getText #);
NameDeclToText: 
  (# nd: ^astinterface.NameDecl enter nd[]  exit nd.getText #);
StringToText:   
  (# s: ^astinterface.String    enter s[]   exit s.getText #);
ConstToInteger:
  (# con: ^astinterface.Const   enter con[] exit con.getValue #);
ConstToText:
  (# con: ^astinterface.Const   enter con[] exit con.getText #);

FetchComments:
  (* Adapted from code in prettyprinter *)
  (# currentSubcomment: ^text;
     n: @integer;
     a: ^astinterface.ast;
  enter a[]
  do a.getNextComment->(currentSubcomment[],n);
     (if false then
         'FetchComments: '->screen.puttext; 
         currentSubcomment[] -> screen.puttext;
         ', n='->screen.puttext; n->screen.putint; newline;
     if);
     (if n
      // -2 then (* empty comment *)
      // -1 then (* empty subcomment *)
      // 0 then
         (if currentSubcomment[] <> none then
             INNER FetchComments;
         if)
      // 1 then
         (if currentSubcomment[] <> none then
             INNER FetchComments;
         if);
         loop:
           (# 
           do a.getNextComment->(currentSubcomment[],n);
              (if currentSubcomment[] <> none then
                  INNER FetchComments;
              if);
              (if n = 1 then restart loop if);
           #)         
      // 2 then (* finished *)
         (if currentSubcomment[] <> none then
             INNER FetchComments;
         if)
     if);
  #);
