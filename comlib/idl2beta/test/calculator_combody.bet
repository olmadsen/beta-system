ORIGIN 'calculator';
INCLUDE '~beta/guienv/stddialogs';
INCLUDE '~beta/comlib/comlib';
INCLUDE 'Deep_Thought';
INCLUDE 'calculator_server'; (* To get to fake CoCreateInstance *)

--LIB: attributes--
usage: 
  (# 
  do 'usage: calculator [--trace|-t] <server-kind>'->putline;
     '  where <server-kind> is one of'->putline;
     '       -i'->putline;
     '      --inproc   Use in-proc (DLL) COM server'->putline;
     '       -l'->putline;
     '      --local    Use out-of-proc local (EXE) COM server'->putline;
     '       -r'->putline;
     '      --remote   Use out-of-proc remote (EXE) DCOM server'->putline;
     '       -b'->putline;
     '      --beta     Use linked-on BETA server'->putline;
     '  Other options: ' -> putline;
     '       -t'->putline;
     '      --trace    Trace COM flow.'->putline;
     stop;
  #);

      

--ProgramStart: dopart--
do (# UseBetaServer, UseActiveObject, DoTrace: @Boolean;
      arg: ^text;
      
      MakeActiveObjects:
        (* Create the instances and register them using RegisterActiveObject *)
        (# ih: @int32uHolder;
           result: @HRESULT;
           pICalc: @IUnknownHolder;
        do 
           'RegisterActiveObject:'->screen.putLine;

           (main.calculator_private.CLSID_Calc[], NONE, 0,
           main.calculator_private.IID_ICalc[]) 
             -> BetaCoCreateInstance -> main.calculator_private.pICalc[];
           
           (main.calculator_private.pICalc[],
           main.calculator_private.CLSID_Calc[], 0, ih[])
             -> RegisterActiveObject -> result;
           (if result.succeeded then
               'RegisterActiveObject succeeded'->screen.putLine;
            else
               ('RegisterActiveObject failed', result.value)
               -> ComErrorMessage;
           if);
           NONE -> main.calculator_private.pICalc[];
           
           (* Retrieve the object: *)
           (main.calculator_private.CLSID_Calc[], 0, pICalc[])
             -> GetActiveObject -> result;
           (if result.succeeded then
               'GetActiveObject succeeded'->screen.putLine;
               pICalc -> main.calculator_private.pICalc[];
            else
               ('GetActiveObject failed', result.value)
               -> ComErrorMessage;
           if);
        #);
      
   do (if NoOfArguments<2 then usage if);
      (for i:NoOfArguments-1 repeat
           i+1 -> Arguments -> arg[];
           (if true
            //'-i'->arg.equal
            //'--inproc'->arg.equal then
               CLSCTX_INPROC_SERVER->main.calculator_private.clsctx;
            //'-l'->arg.equal
            //'--local'->arg.equal then
               CLSCTX_LOCAL_SERVER->main.calculator_private.clsctx;
            //'-r'->arg.equal
            //'--remote'->arg.equal then
               CLSCTX_REMOTE_SERVER->main.calculator_private.clsctx;
            //'-a'->arg.equal
            //'-active'->arg.equal then
               true -> UseActiveObject;
            //'-b'->arg.equal 
            //'--beta'->arg.equal then
               true -> UseBetaServer;
            //'-t'->arg.equal 
            //'--trace'->arg.equal then
               true -> DoTrace;
            else
               usage;
           if);
      for);
      (if not DoTrace then &stream[] -> screen[] if);
      
      0->CoInitialize;
      Conv_CLSID->main.calculator_private.CLSID_Conv;
      Calc_CLSID->main.calculator_private.CLSID_Calc;
      IConv_IID->main.calculator_private.IID_IConv;
      ICalc_IID->main.calculator_private.IID_ICalc;
      IHello_IID->main.calculator_private.IID_IHello;
      
      (if UseActiveObject then
          &MakeActiveObjects;
      if);
      
      (if UseBetaServer then
          (* Fake CoCreateInstance for the linked-in betaserver *)
          'Doing fake CoCreateInstance...'->screen.putLine;
          (main.calculator_private.CLSID_Calc[], NONE, 0,
          main.calculator_private.IID_ICalc[]) 
            -> BetaCoCreateInstance -> main.calculator_private.pICalc[];
          'Fake CoCreateInstance for Calc done'->screen.putLine;
          
          (main.calculator_private.CLSID_Conv[], NONE, 0,
          main.calculator_private.IID_IConv[])
            -> BetaCoCreateInstance -> main.calculator_private.pIConv[];
          'Fake CoCreateInstance for Conv done'->screen.putLine;
       else
          (main.calculator_private.CLSID_Calc[], NONE, 
          main.calculator_private.clsctx, main.calculator_private.IID_ICalc[])
            -> CoCreateInstance->main.calculator_private.pICalc[];
          
          (main.calculator_private.CLSID_Conv[], NONE, 
          main.calculator_private.clsctx, main.calculator_private.IID_IConv[])
            -> CoCreateInstance->main.calculator_private.pIConv[];
      if);

      'Querying IHello:'->screen.putLine;
      (main.calculator_private.pICalc[], main.calculator_private.IID_IHello[])
        -> query
        -> main.calculator_private.pIHello[];
      
      (if main.calculator_private.pIHello[]=NONE then
          'Error: pIHello[]=NONE' -> putline;
       else
          'Calling IHello:'->screen.putLine;
          (if main.calculator_private.pIHello[]<>NONE then
              main.calculator_private.bh[] 
                -> main.calculator_private.pIHello.World 
                -> main.calculator_private.hr;
              (if main.calculator_private.hr.succeeded then
                  (NONE, main.calculator_private.bh.gettext, 'IHello Message')
                    -> NoteUser;
              if);
              'Releasing IHello' -> screen.putLine;
              main.calculator_private.pIHello.Release;
              NONE->main.calculator_private.pIHello[];
          if);
      if);
   #);
      
   
--ProgramQuit: dopart--
do (if main.calculator_private.pICalc[]<>NONE then
       'Releasing ICalc' -> screen.putLine;
       main.calculator_private.pICalc.Release;
   if);
   (if main.calculator_private.pIConv[]<>NONE then 
       'Releasing IConv' -> screen.putLine;
       main.calculator_private.pIConv.Release; 
   if);
   CoUninitialize;
   
--CalculatorAdd: descriptor--
(# 
do (op1,op2,calculator_private.i[]) 
     -> calculator_private.pICalc.add 
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.i->op1;
    else
       (NONE, 'pICalc.add failed!', 'Error')->AlertUser;
       0->op1;
   if)
#)
--CalculatorSub: descriptor--
(# 
do (op1,op2,calculator_private.i[])
     -> calculator_private.pICalc.subtract 
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.i->op1;
    else
       (NONE, 'pICalc.subtract failed!', 'Error')->AlertUser;
       0->op1;
   if)
#)
--CalculatorDiv: descriptor--
(# 
do (op1,op2,calculator_private.i[]) 
     -> calculator_private.pICalc.divide
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.i->op1;
    else
       (NONE, 'pICalc.divide failed!', 'Error')->AlertUser;
       0->op1;
   if)
#)
--CalculatorMul: descriptor--
(# 
do (op1,op2,calculator_private.i[]) 
     -> calculator_private.pICalc.multiply 
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.i->op1;
    else
       (NONE, 'pICalc.multiply failed!', 'Error')->AlertUser;
       0->op1;
   if)
#)

--CalculatorHex2Dec: dopart--
do hex[]->calculator_private.b.setText;
   (calculator_private.b, calculator_private.i[])
     -> calculator_private.pIConv.hex2dec
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       ''->dec[];
       calculator_private.i->dec.putInt
    else
       (NONE, 'pIConv.hex2dec failed!', 'Error')->AlertUser;
       '0'->dec[];
   if);
   calculator_private.b.free;

--CalculatorDec2Hex: dopart--
do dec.reset;
   (dec.getint,calculator_private.bh[])
     -> calculator_private.pIConv.dec2hex
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.bh.getText->hex[];
    else
       (NONE, 'pIConv.dec2hex failed!', 'Error')->AlertUser;
       '0'->hex[];
   if);
--CalculatorPrivate: descriptor--
(# CLSID_Conv: @CLSID;
   CLSID_Calc: @CLSID;
   IID_IConv: @IID;
   IID_ICalc: @IID;
   IID_IHello: @IID;
   pIConv: ^IConv;
   pICalc: ^ICalc;
   pIHello: ^IHello;
   b: @BSTR;
   bh: @BSTRHolder;
   i: @int32Holder;
   hr: @HRESULT;
   clsctx: @integer;
#)
