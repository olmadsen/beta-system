ORIGIN 'calculator';
INCLUDE '~beta/comlib/comlib';
INCLUDE 'Deep_Thought';

--LIB: attributes--
usage: 
  (# 
  do 'usage: calculator <server-kind>'->putline;
     '  where <server-kind> is one of'->putline;
     '      -inproc'->putline;
     '      -local'->putline;
     '      -remote'->putline;
     stop;
  #);


--ProgramStart: dopart--
do (* Must specify either a inproc or local server *)
   (if NoOfArguments<>2 then usage if);
   (if true
    //'-inproc'->(2->Arguments).equal then
       CLSCTX_INPROC_SERVER->main.calculator_private.clsctx;
    //'-local'->(2->Arguments).equal then
       CLSCTX_LOCAL_SERVER->main.calculator_private.clsctx;
    //'-remote'->(2->Arguments).equal then
       CLSCTX_REMOTE_SERVER->main.calculator_private.clsctx;
    else
       usage;
   if);
   0->CoInitialize;
   Conv_CLSID->main.calculator_private.CLSID_Conv;
   Calc_CLSID->main.calculator_private.CLSID_Calc;
   IConv_IID->main.calculator_private.IID_IConv;
   ICalc_IID->main.calculator_private.IID_ICalc;
   IHello_IID->main.calculator_private.IID_IHello;
   
   (main.calculator_private.CLSID_Calc[], NONE, 
   main.calculator_private.clsctx, main.calculator_private.IID_ICalc[])
     -> CoCreateInstance->main.calculator_private.pICalc[];
   (main.calculator_private.CLSID_Conv[], NONE, 
   main.calculator_private.clsctx, main.calculator_private.IID_IConv[])
     -> CoCreateInstance->main.calculator_private.pIConv[];
   
   'Querying IHello:'->screen.putLine;
   main.calculator_private.IID_IHello[]
     -> main.calculator_private.pICalc.query
     -> main.calculator_private.pIHello[];
   
   'Calling IHello:'->screen.putLine;
   (* FIXME: gives adreg overflow *)
   (if main.calculator_private.pIHello[]<>NONE then
          main.calculator_private.bh[] 
            -> main.calculator_private.pIHello.World 
            -> main.calculator_private.hr;
          (if main.calculator_private.hr.succeeded then
              main.calculator_private.bh.gettext->screen.putline;
          if);
          main.calculator_private.pIHello.Release;
          NONE->main.calculator_private.pIHello[];
      if);
   
   
--ProgramQuit: dopart--
do (if main.calculator_private.pICalc[]<>NONE then
       'Releasing ICalc' -> screen.putLine;
       main.calculator_private.pICalc.Release;
   if);
   (if main.calculator_private.pIConv[]<>NONE then 
       'Releasing IConv' -> screen.putLine;
       main.calculator_private.pIConv.Release; 
   if);
   CoUninitialize;
   
--CalculatorAdd: descriptor--
(# 
do (op1,op2,calculator_private.i[]) 
     -> calculator_private.pICalc.add 
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.i->op1;
    else
       'pICalc.add failed!'->screen.putline;
       0->op1;
   if)
#)
--CalculatorSub: descriptor--
(# 
do (op1,op2,calculator_private.i[])
     -> calculator_private.pICalc.subtract 
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.i->op1;
    else
       'pICalc.subtract failed!'->screen.putline;
       0->op1;
   if)
#)
--CalculatorDiv: descriptor--
(# 
do (op1,op2,calculator_private.i[]) 
     -> calculator_private.pICalc.divide
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.i->op1;
    else
       'pICalc.divide failed!'->screen.putline;
       0->op1;
   if)
#)
--CalculatorMul: descriptor--
(# 
do (op1,op2,calculator_private.i[]) 
     -> calculator_private.pICalc.multiply 
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.i->op1;
    else
       'pICalc.multiply failed!'->screen.putline;
       0->op1;
   if)
#)

--CalculatorHex2Dec: dopart--
do hex[]->calculator_private.b.setText;
   (calculator_private.b, calculator_private.i[])
     -> calculator_private.pIConv.hex2dec
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       ''->dec[];
       calculator_private.i->dec.putInt
    else
       'pIConv.hex2dec failed!'->screen.putline;
       '0'->dec[];
   if);
   calculator_private.b.free;

--CalculatorDec2Hex: dopart--
do dec.reset;
   (dec.getint,calculator_private.bh[])
     -> calculator_private.pIConv.dec2hex
     -> calculator_private.hr;
   (if calculator_private.hr.succeeded then
       calculator_private.bh.getText->hex[];
    else
       'pIConv.dec2hex failed!'->screen.putline;
       '0'->hex[];
   if);
   
--CalculatorPrivate: descriptor--
(# CLSID_Conv: @CLSID;
   CLSID_Calc: @CLSID;
   IID_IConv: @IID;
   IID_ICalc: @IID;
   IID_IHello: @IID;
   pIConv: ^IConv;
   pICalc: ^ICalc;
   pIHello: ^IHello;
   b: @BSTR;
   bh: @BSTRHolder;
   i: @int32Holder;
   hr: @HRESULT;
   clsctx: @integer;
#)
