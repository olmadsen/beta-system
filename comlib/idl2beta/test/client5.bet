ORIGIN '~beta/comlib/comlib';
INCLUDE 'deep5';

--PROGRAM: descriptor--
(# CLSID_Conv: @CLSID;
   IID_IConv: @IID;
   pIConv: ^IConv;
   b: @BSTR;
   bh: @BSTRHolder;
   i: @int32Holder;
   hr: @HRESULT;
   
do 0->CoInitialize;
   
   IConv_IID -> IID_IConv; (* not pretty (:-) *)
   Calc_CLSID -> CLSID_Conv; (* not pretty (:-) *)
   
   'Create component and get interface IConv'->putLine;
   
   (CLSID_Conv[], NONE, CLSCTX_INPROC_SERVER, IID_IConv[])
     -> CoCreateInstance->pIConv[];
   
   'CoCreateInstance done'->putLine;

   (if pIConv[]<>NONE then
       'Succeeded creating component.'->putLine;
       '(17,bh[])->pIConv.dec2hex '->putText;
       (17,bh[])->pIConv.dec2hex->hr;
       (if hr.succeeded then
           '= '->putText; bh.getText->putLine;
        else
           'FAILED' -> putLine;
       if);
       '(\'ABBA\',i[])->pIConv.hex2dec '->putText;
       'ABBA' -> b.setText;
       (b, i[])->pIConv.hex2dec->hr;
       (if hr.succeeded then
           '= '->putText; i->putInt; newLine;
        else
           'FAILED' -> putLine;
       if);
       b.free;
       
       'Trying to release interface IConv'->putline;
       pIConv.Release;
       'Succeeded releasing interface IConv'->putline;
    else
       'Could not create component'->putline;
   if); 
   'CoUninitialize'->putline;
   CoUninitialize;
   'CoUninitialize done'->putline;
#)
