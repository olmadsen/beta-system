ORIGIN '~beta/comlib/comlib';
INCLUDE 'IConvTLB';

--PROGRAM: descriptor--
(# CLSID_Conv: @CLSID;
   IID_IConv: @IID;
   pIConv: ^IConv;
   t: @textHolder;
   i: @int32Holder;
   hr: @HRESULT;
   
do 0->CoInitialize;
   
   IConv_IID -> IID_IConv; (* not pretty (:-) *)
   Conv_CLSID -> CLSID_Conv; (* not pretty (:-) *)
   
   'Create component and get interface IConv'->putLine;
   
   (CLSID_Conv[], NONE, CLSCTX_INPROC_SERVER, IID_IConv[])
     -> CoCreateInstance->pIConv[];
   
   'CoCreateInstance done'->putLine;

   (if pIConv[]<>NONE then
       'Succeeded creating component.'->putLine;
       '(17,t[])->pIConv.dec2hex '->putText;
       (17,t[])->pIConv.dec2hex->hr;
       (if hr.succeeded then
           '= '->putText; t.get->putLine;
        else
           'FAILED' -> putLine;
       if);
       '(\'ABBA\',i[])->pIConv.hex2dec '->putText;
       ('ABBA', i[])->pIConv.hex2dec->hr;
       (if hr.succeeded then
           '= '->putText; i->putInt; newLine;
        else
           'FAILED' -> putLine;
       if);
       
       'Trying to release interface IConv'->putline;
       pIConv.Release;
       'Succeeded releasing interface IConv'->putline;
    else
       'Could not create component'->putline;
   if); 
   'CoUninitialize'->putline;
   CoUninitialize;
   'CoUninitialize done'->putline;
#)
