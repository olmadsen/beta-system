ORIGIN 'translate'

--TranslateLib: attributes--

do_inheritance_spec:
  (# i: ^idl.inheritance_spec;
     n: ^idl.scoped_names;
     
     numPrefixes: @integer;
  enter i[]
  do 0->numPrefixes;
     i.getscoped_names -> n[];
     n.newscan
     (# 
     do (if numPrefixes>0 then
            'Multiple inheritance_specs not supported!'
              -> error;
         else
            current[] -> do_scoped_name -> outfile.puttext;
        if);
     #)
  #);

--interface_header_body: descriptor--
(# interface_name: ^text;
   att: @idl_attributes;
   l: ^idl.attributes;
do propputs.clear;
   propgets.clear;
   (att[], h.getattributes) -> do_attributes;
   outfile.newline;
   (* Declare the IID *)
   outfile.indent;
   h.getIdentifier -> IdentifierToText -> interface_name[] -> outfile.puttext;
   '_IID: (\# exit \'' -> outfile.puttext;
   att.interface_header.uuid[] -> outfile.puttext;
   '\' #\);\n' -> outfile.putline;
   
   (if not quiet then
       '    [interface ' -> screen.puttext;
       interface_name[] -> screen.puttext;
       ']' -> screen.putline;
   if);
   
   (* Add the interface name to known local types *)
   (interface_name[],interface_name[]) -> local_types.add_interface_type;

   (* Declare the interface *)
   outfile.indent;
   interface_name[] -> refholders.add;
   interface_name[] -> outfile.puttext;
   ': ' -> outfile.puttext;
   (if h.getinheritance_specOpt -> optionalPresent then
       h.getinheritance_specOpt -> do_inheritance_spec;
    else
       (* prefix of IUnknown *)
       'COM' -> outfile.puttext;
   if);
   outfile.newline;
   (* The following will be matched with corresponding lines
    * in end of do_interface_body.
    *)
   2 -> outfile.indent.inc;
   outfile.indent;
   (if att.typelib.helpstring[]<>NONE then
       att.typelib.helpstring[] -> outfile.commentline;
       outfile.indent;
   if);
   outfile.PatternBegin;
   outfile.newline;
   3 -> outfile.indent.inc;
#)   
   
   
--dispinterface_header_body: descriptor--
(# interface_name: ^text;
   att: @idl_attributes;
do propputs.clear;
   propgets.clear;
   (att[], h.getattributes) -> do_attributes;
   outfile.newline;
   (* Declare the IID *)
   outfile.indent;
   h.getIdentifier -> IdentifierToText -> interface_name[] -> outfile.puttext;
   '_IID: (\# exit \'' -> outfile.puttext;
   att.interface_header.uuid[] -> outfile.puttext;
   '\' #\);\n' -> outfile.putline;
   
   (if not quiet then
       '    [dispinterface ' -> screen.puttext;
       interface_name[] -> screen.puttext;
       ']' -> screen.putline;
   if);
   
   (* Add the interface name to known local types *)
   (interface_name[],interface_name[]) -> local_types.add_interface_type;

   (* Declare the interface *)
   outfile.indent;
   interface_name[] -> outfile.puttext;
   interface_name[] -> refholders.add;
   ': ' -> outfile.puttext;
   (* Always IDispatch prefix *)
   'IDispatch' -> outfile.puttext;
   outfile.newline;
   (* The following will be matched with corresponding lines
    * in end of do_interface_body.
    *)
   2 -> outfile.indent.inc;
   outfile.indent;
   (if att.typelib.helpstring[]<>NONE then
       att.typelib.helpstring[] -> outfile.commentline;
       outfile.indent;
   if);
   outfile.PatternBegin;
   outfile.newline;
   3 -> outfile.indent.inc;
#)   
