ORIGIN 'translate'

--interface_header_body: descriptor--
(# interface_name: ^text;
   att: @idl_attributes;
   l: ^idl.attributes;
   do_inheritance_spec:
     (# i: ^idl.inheritance_spec;
        n: ^idl.scoped_names;
        numPrefixes: @integer;
     enter i[]
     do 0->numPrefixes;
        i.getscoped_names -> n[];
        n.newscan
        (# 
        do (if numPrefixes>0 then
               'Multiple inheritance_specs not supported!'
                 -> error;
            else
               current[] -> do_scoped_name -> output.puttext;
           if);
        #)
     #);
do propputs.clear;
   propgets.clear;
   (att[], h.getattributes_spec) -> do_attributes_spec;
   (if not compact then output.newline; if);
   h.getIdentifier -> IdentifierToText -> interface_name[];
   (if att.interface_header.uuid[]<>NONE then
       (* Declare the IID *)
       output.indent;
       interface_name[] -> output.puttext;
       '_IID: (\# exit \'' -> output.puttext;
       att.interface_header.uuid[] -> output.puttext;
       '\' #\);\n' -> output.puttext;
       (if not compact then output.newline if);
   if);
   
   (if not quiet then
       '    [interface ' -> screen.puttext;
       interface_name[] -> screen.puttext;
       ']' -> screen.putline;
   if);
   
   (* Add the interface name to known local types *)
   (interface_name[],interface_name[]) -> local_types.add_interface_type;

   (* Declare the interface *)
   interface_name[] -> refholders.add (* EmitRefHolder *);
   output.indent;
   interface_name[] -> output.puttext;
   ': ' -> output.puttext;
   (if h.getinheritance_spec_opt -> optionalPresent then
       h.getinheritance_spec_opt -> do_inheritance_spec;
    else
       (* prefix of IUnknown *)
       'COM' -> output.puttext;
   if);
   (if att.typelib.dual then
       'dual' -> output.spacecomment;
   if);
   output.newline;
   2 -> output.indent.inc;
   output.indent;
   (if att.typelib.helpstring[]<>NONE then
       att.typelib.helpstring[] -> output.commentline;
       (if not compact then output.indent; if);
   if);
   (* The following PatternBegin will be matched with corresponding lines
    * in end of do_interface_body.
    *)
   output.PatternBegin;
   3 -> output.indent.inc;
   '<<SLOT ' -> output.puttext;
   interface_name[] -> output.puttext;
   'Lib: attributes>>;' -> output.putline;
   (if att.typelib.version[]<>NONE then
       output.indent;
       'version: (\# exit ' -> output.puttext;
       att.typelib.version[] -> output.puttext;
       ' #\);' -> output.puttext;
       output.newline;
   if);
#)   
   
