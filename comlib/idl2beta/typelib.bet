ORIGIN 'translate';

--do_library: descriptor--
(# get_stuff_from_library:
     (# body: ^idl.library_body;
        lh: ^idl.library_header;
        att: ^idl_attributes;
        u: ^idl.uuid;
     do lib.getlibrary_header -> lh[];
        lh.getidentifier -> IdentifierToText -> libname[];
        (&idl_attributes[]->att[],lh.getattributes_spec) -> do_attributes_spec;
        att.typelib.helpstring[] -> libhelp[];
        att.interface_header.uuid[] -> libid[];
        lib.getlibrary_body -> body[];
        body.getdefinitions -> defs[];
     #);
   do_defs:
     (# 
     do defs.newscan
        (# 
        do (if current.symbol
            // idl.import then (* handled by scanForImports *)
            // idl.def_type_dcl then current[] -> do_def_type_dcl 
            // idl.const_dcl then current[] -> do_const_dcl;
            // idl.except_dcl then current[] -> do_except_dcl;
            // idl.interface_dcl then current[] -> do_interface_dcl;
            // idl.dispinterface_dcl then current[] -> do_dispinterface_dcl;
            // idl.forward_dcl then current[] -> do_forward_dcl;
            // idl.module then current[] -> do_module;
            // idl.coclass then
               current[] -> do_coclass;
           if);
        #)
     #);
   defs: ^idl.definitions;
   libhelp, libname, libid: ^text;
   help, name, clsid: [1]^text;
   
do get_stuff_from_library;
   2 -> output.indent.inc;
   output.indent;
   (if (not compact) and (libhelp[]<>NONE) then
       libhelp[] -> output.commentline;
       output.indent;
   if);
   2 -> output.indent.dec;
   do_defs;
   2 -> output.indent.inc;
   output.indent;
   libname[] -> output.puttext;
   '_LIBID: (\# exit \'' -> output.puttext;
   libid[] -> output.puttext;
   '\' #\);' -> output.putline;
   2 -> output.indent.dec;
#)


--do_coclass:descriptor--
(# ch: ^idl.coclass_header;
   att: ^idl_attributes;
do 2 -> output.indent.inc;
   output.indent;
   cc.getcoclass_header -> ch[];
   (&idl_attributes[]->att[],ch.getattributes_spec) -> do_attributes_spec;
   (if (not compact) and (att.typelib.helpstring[]<>NONE) then 
       'CoClass: ' -> (att.typelib.helpstring[]).prepend -> output.commentline;
       output.indent;
   if);
   ch.getidentifier -> IdentifierToText -> output.puttext;
   '_CLSID: (\# exit \'' -> output.puttext;
   att.interface_header.uuid[] -> output.puttext;
   '\' #\);' -> output.putline;
   2 -> output.indent.dec;
#)
