ORIGIN 'translate';

--do_library: descriptor--
(# get_stuff_from_library:
     (# body: ^idl.library_body;
        defs: ^idl.definitions;
        lh: ^idl.library_header;
        att: ^idl_attributes;
        u: ^idl.uuid;
     do lib.getlibrary_header -> lh[];
        lh.getidentifier -> IdentifierToText -> libname[];
        (&idl_attributes[]->att[],lh.getattributes_spec) -> do_attributes_spec;
        att.typelib.helpstring[] -> libhelp[];
        att.interface_header.uuid[] -> libid[];
        lib.getlibrary_body -> body[];
        body.getdefinitions -> defs[];
        0->inx;
        defs.newscan
        (# 
        do (if current.symbol
            // idl.import then current[] -> do_import
            // idl.def_type_dcl then current[] -> do_def_type_dcl 
            // idl.const_dcl then current[] -> do_const_dcl;
            // idl.except_dcl then current[] -> do_except_dcl;
            // idl.interface_dcl then current[] -> do_interface_dcl;
            // idl.dispinterface_dcl then current[] -> do_dispinterface_dcl;
            // idl.forward_dcl then current[] -> do_forward_dcl;
            // idl.module then current[] -> do_module;
            // idl.coclass then
               current[] -> do_coclass;
           if);
        #)
     #);
   libhelp, libname, libid: ^text;
   inx: @integer;
   help, name, clsid: [1]^text;
   
do get_stuff_from_library;
   2 -> outfile.indent.inc;
   outfile.indent;
   (if (not compact) and (libhelp[]<>NONE) then
       libhelp[] -> outfile.commentline;
       outfile.indent;
   if);
   libname[] -> outfile.puttext;
   '_LIBID: (\# exit \'' -> outfile.puttext;
   libid[] -> outfile.puttext;
   '\' #\);' -> outfile.putline;
   2 -> outfile.indent.dec;
#)


--do_coclass:descriptor--
(# ch: ^idl.coclass_header;
   att: ^idl_attributes;
do 2 -> outfile.indent.inc;
   outfile.indent;
   cc.getcoclass_header -> ch[];
   (&idl_attributes[]->att[],ch.getattributes_spec) -> do_attributes_spec;
   (if (not compact) and (att.typelib.helpstring[]<>NONE) then 
       att.typelib.helpstring[] -> outfile.commentline;
       outfile.indent;
   if);
   ch.getidentifier -> IdentifierToText -> outfile.puttext;
   '_CLSID: (\# exit \'' -> outfile.puttext;
   att.interface_header.uuid[] -> outfile.puttext;
   '\' #\);' -> outfile.putline;
   2 -> outfile.indent.dec;
#)
