ORIGIN 'interface_header';

--get_what_we_need_from_library: descriptor--
(# get_stuff_from_library:
     (# body: ^idl.library_body;
        defs: ^idl.definitions;
        lh: ^idl.library_header;
        cc: ^idl.coclass;
        ch: ^idl.coclass_header;
        att: ^idl.interface_attributes;
        u: ^idl.uuid;
     do lib.getlibrary_header -> lh[];
        lh.getidentifier -> IdentifierToText -> libname[];
        lh.getinterface_attributes 
          -> att[]
          -> do_interface_attributes_helpstring
          -> libhelp[];
        att.newscan
        (# 
        do (if current.symbol
            // idl.uuid then
               (current[]->u[]).getuuid_rep -> do_uuid_rep -> libid[];
           if);
        #);
        lib.getlibrary_body -> body[];
        body.getdefinitions -> defs[];
        0->inx;
        defs.newscan
        (# 
        do (if current.symbol
            // idl.import then
               (* add to imports *)
               current[] -> do_import;
            // idl.coclass then
               (* get helpstring, name, clsid *)
               inx+1 -> inx;
               (if inx>help.range then
                   help.range -> help.extend;
                   name.range -> name.extend;
                   clsid.range -> clsid.extend;
               if);
               (current[]->cc[]).getcoclass_header -> ch[];
               ch.getidentifier -> IdentifierToText -> name[inx][];
               ch.getinterface_attributes 
                 -> att[]
                 -> do_interface_attributes_helpstring
                 -> help[inx][];
               att.newscan
               (# 
               do (if current.symbol
                   // idl.uuid then
                      (current[]->u[]).getuuid_rep -> do_uuid_rep -> clsid[inx][];
                  if);
               #);
           if);
        #)
     #);
   libhelp, libname, libid: ^text;
   inx: @integer;
   help, name, clsid: [1]^text;
   
do outfile.indent;
   get_stuff_from_library;
   outfile.newline;
   libhelp[] -> outfile.commentline;
   libname[] -> outfile.puttext;
   '_LIBID: (\# exit \'' -> outfile.puttext;
   libid[] -> outfile.puttext;;
   '\' #\);\n' -> outfile.putline;
   outfile.newline;
   (for i:inx repeat
        help[i][] -> outfile.commentline;
        name[i][] -> outfile.puttext;
        '_CLSID: (\# exit \'' -> outfile.puttext;
        clsid[i][] -> outfile.puttext;;
        '\' #\);\n' -> outfile.putline;
   for);
#)
