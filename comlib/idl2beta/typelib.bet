ORIGIN 'interface_header';

--do_library: descriptor--
(# get_stuff_from_library:
     (# body: ^idl.library_body;
        defs: ^idl.definitions;
        lh: ^idl.library_header;
        cc: ^idl.coclass;
        ch: ^idl.coclass_header;
        att: ^idl_attributes;
        u: ^idl.uuid;
     do lib.getlibrary_header -> lh[];
        lh.getidentifier -> IdentifierToText -> libname[];
        (&idl_attributes[]->att[],lh.getattributes) -> do_attributes;
        att.typelib.helpstring[] -> libhelp[];
        att.interface_header.uuid[] -> libid[];
        lib.getlibrary_body -> body[];
        body.getdefinitions -> defs[];
        0->inx;
        defs.newscan
        (# 
        do (if current.symbol
            // idl.import then current[] -> do_import
            // idl.def_type_dcl then current[] -> do_def_type_dcl 
            // idl.const_dcl then current[] -> do_const_dcl;
            // idl.except_dcl then current[] -> do_except_dcl;
            // idl.interface_dcl then current[] -> do_interface_dcl;
            // idl.dispinterface_dcl then current[] -> do_dispinterface_dcl;
            // idl.forward_dcl then current[] -> do_forward_dcl;
            // idl.module then current[] -> do_module;
            // idl.coclass then
               (* get helpstring, name, clsid *)
               inx+1 -> inx;
               (if inx>help.range then
                   help.range -> help.extend;
                   name.range -> name.extend;
                   clsid.range -> clsid.extend;
               if);
               (current[]->cc[]).getcoclass_header -> ch[];
               ch.getidentifier -> IdentifierToText -> name[inx][];
               (&idl_attributes[]->att[],ch.getattributes) -> do_attributes;
               att.typelib.helpstring[] -> help[inx][];
               att.interface_header.uuid[] -> clsid[inx][];
           if);
        #)
     #);
   libhelp, libname, libid: ^text;
   inx: @integer;
   help, name, clsid: [1]^text;
   
do outfile.indent;
   get_stuff_from_library;
   outfile.newline;
   libhelp[] -> outfile.commentline;
   libname[] -> outfile.puttext;
   '_LIBID: (\# exit \'' -> outfile.puttext;
   libid[] -> outfile.puttext;;
   '\' #\);\n' -> outfile.putline;
   outfile.newline;
   (for i:inx repeat
        help[i][] -> outfile.commentline;
        name[i][] -> outfile.puttext;
        '_CLSID: (\# exit \'' -> outfile.puttext;
        clsid[i][] -> outfile.puttext;;
        '\' #\);\n' -> outfile.putline;
   for);
#)
