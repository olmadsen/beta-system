ORIGIN 'interface_header';

--get_what_we_need_from_library: descriptor--
(# get_stuff_from_library:
     (# libhelp, libname, libid, help, name, clsid: ^text;
        body: ^idl.library_body;
        defs: ^idl.definitions;
        lh: ^idl.library_header;
        cc: ^idl.coclass;
        ch: ^idl.coclass_header;
        att: ^idl.interface_attributes;
        u: ^idl.uuid;
     do lib.getlibrary_header -> lh[];
        lh.getidentifier -> IdentifierToText -> libname[];
        lh.getinterface_attributes 
          -> att[]
          -> do_interface_attributes_helpstring
          -> libhelp[];
        att.newscan
        (# 
        do (if current.symbol
            // idl.uuid then
               (current[]->u[]).getuuid_rep -> do_uuid_rep -> libid[];
           if);
        #);
        lib.getlibrary_body -> body[];
        body.getdefinitions -> defs[];
        defs.newscan
        (# 
        do (if current.symbol
            // idl.import then
               (* add to imports *)
               current[] -> do_import;
            // idl.coclass then
               (* get name, clsid, libid *)
               (current[]->cc[]).getcoclass_header -> ch[];
               ch.getidentifier -> IdentifierToText -> name[];
               ch.getinterface_attributes 
                 -> att[]
                 -> do_interface_attributes_helpstring
                 -> help[];
               att.newscan
               (# 
               do (if current.symbol
                   // idl.uuid then
                      (current[]->u[]).getuuid_rep -> do_uuid_rep -> clsid[];
                  if);
               #);
           if);
        #)
     exit (libhelp[], libname[], libid[], help[], name[], clsid[])
     #);
   libhelp, libname, libid, help, name, clsid: ^text;
do outfile.indent;
   get_stuff_from_library 
     -> (libhelp[], libname[], libid[], help[], name[], clsid[]);
   outfile.newline;
   libhelp[] -> outfile.commentline;
   libname[] -> outfile.puttext;
   '_LIBID: (\# exit \'' -> outfile.puttext;
   libid[] -> outfile.puttext;;
   '\' #\);\n' -> outfile.putline;
   outfile.newline;
   help[] -> outfile.commentline;
   name[] -> outfile.puttext;
   '_CLSID: (\# exit \'' -> outfile.puttext;
   clsid[] -> outfile.puttext;;
   '\' #\);\n' -> outfile.putline;
#)
