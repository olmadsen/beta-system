ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/toollibs/utils/v1.0/fileExtensions';
INCLUDE '~beta/containers/v1.6/list';
INCLUDE 'idlsematt';
INCLUDE 'mpsadditions';
BODY   'translate';
BODY   'symboltable';
INCLUDE 'outfile';
INCLUDE 'idl_attributes';
--StreamLib: attributes--
Line:
  (# 
  do '------------------------------------------------------------------'
       ->putline
  #);
--Lib: Attributes--
Control:
  (#
     BobsTab: (# exit '$(BETALIB)/grammars/idl/v2.0/idl-parser.btab' #);
     
     ast: @astInterface
       (* An instance of the AST interface *)
       (# defaultGrammarFinder:: findGrammar #);
     
     idl: @ast.idl; (* The MPS interface to the BETA grammar/ASTs *)
   
     Outfile: @OutputFile;
     Debug, trace, mps_trace, preserve, quiet, force: @boolean;
   
     StartOfFilenames: @integer; 
     aText: ^text;
     OutFileName, IdlFileName, Ifilename, Cfilename: ^text;
     
     textlist: list
       (# element::text;
          add: 
            (# name: ^text;
            enter name[]
            do find(# predicate:: (# do current[]->name.equalNCS->value #);
                      notfound:: (# do name[]->append #)
                   #);
            #);
       #);
     defines: @textlist;
     holders: @textlist;
     refholders: @textlist;
     imports: @textlist;
     files_remaining: @textlist;
     
     type_cat_basic:  (# exit 1 #);
     type_cat_struct: (# exit 2 #);
     type_cat_enum:   (# exit 3 #);
     type_cat_union:  (# exit 4 #);
     type_cat_user:   (# exit 5 #);
     
     type_info:
       (# as_text:
            (# t: ^text;
            do &text[] -> t[];
               (for pointer_level repeat '*' -> t.put for);
               (if name[]<>NONE then
                   name[] -> t.append;
                else
                   '<NONE>' -> t.append;
               if);
            exit t[]
            #);
          name: ^text;
          pointer_level: @integer;
          representation: ^text;
          comment: ^text;
          category: @integer;
          attributes: ^idl_attributes;
       #);
     type_list: list
       (# element::type_info;
          outer_scope: ^type_list;
          findtype: 
            (# name: ^text;
               found: @boolean;
               current: ^element;
            enter name[]
            <<SLOT type_list_findtype: dopart>>
            #);
          add_basic_type:
            (# name: ^text;
               representation: ^text;
               type: ^type_info;
            enter (name[], representation[])
            <<SLOT type_list_add_basic_type: dopart>>
            #);
          print:
            (# 
            <<SLOT type_list_print: dopart>>
            #);
       #);
     global_types: @type_list (* globally known types *);
     local_types: @type_list (* types local to current file *);
     
     declarator_info:
       (# as_text:
            (# t: ^text;
            do &text[] -> t[];
               (for pointer_level repeat '*' -> t.put for);
               (if type[]<>NONE then
                   type.name[] -> t.append;
                else
                   '<NONE>' -> t.append;
               if);
            exit t[]
            #);
          name: ^text;
          type: ^type_info;
          type_comment: ^text;
          attributes: ^idl_attributes;
          pointer_level: @integer;
       #);
     
     declarator_list: list
       (# element::declarator_info;
          findsym: find 
            (# name: ^text;
               predicate::(# do (current.name[]->name.equal)->value #);
               notfound::
                 (# 
                 do '***declarator_list: not found: '->puttext; 
                    name[] -> putline;
                 #);
            enter name[]
            do INNER
            #);
          print:
            (# 
            <<SLOT declarator_list_print: dopart>>
            #);
       #);
     
     symbols: @ 
       (* symbols (name,type) local to current function *)
       declarator_list;
     
  do global_types[] -> local_types.outer_scope[];
     block:
       (#
          Usage:(# 
                do 'Usage: ' -> putText; 1 -> Arguments -> putText;
                   ' [Options] [-o <name>] <filenames>' -> putLine;
                   '\nOptions:' -> putline;
                   '  -f' -> putline; 		   
                   '  --force\tForce generation of BETA files.' -> putline;
                   '  -q' -> putline; 		   
                   '  --quiet\tBe quiet - emit only error messages' -> putline;
                   '  -p' -> putline; 		   
                   '  --preserve\tPreserve temporary files' -> putline;
                   '  -D name' -> putline;
                   '  --define name\tDefine preprocessor name' -> putline;
                   '\nDebug options:' -> putline;
                   '  -d' -> putline; 
                   '  --debug\tEnable debug of bidl' -> putline;
                   '  -t' -> putline; 
                   '  --trace\tEnable tracing of bidl' -> putline;
                   '  -T' -> putline; 
                   '  --mps_trace\tEnable tracing of mps' -> putline;
                #);
          
          InitAstEnv:
            
            (* Initialization of the meta programming system *)
            (# parser_entry, bidl_entry: @diskentry;
            do 
	       (if debug and mps_trace then
                   (ast.trace.topOpen,true) -> ast.trace.set;
                   (ast.trace.fragmentOpen,true) -> ast.trace.set;
                   (ast.trace.grammars,true) -> ast.trace.set;
                   (ast.trace.compactOpen,true) -> ast.trace.set;
                   (ast.trace.onParse,true) -> ast.trace.set;
                   (ast.trace.parser,true) -> ast.trace.set;
                   (ast.trace.getBinding,true) -> ast.trace.set;
                   (ast.trace.getBindingMark,true) -> ast.trace.set;
               if);
               BobsTab -> ast.expandToFullPath -> parser_entry.path;
               1 -> Arguments -> bidl_entry.path;
               (* FIXME: the stuff below fails on NTI *)
               (if false then
                   (if bidl_entry.modtime < parser_entry.modtime then
                       'Parser is newer than bidl executable - recompile bidl!'
                         -> screen.putline;
                       stop;
                   if);
               if);
	       ast.astLevelInit;
               idl.init; 
	       parser_entry.path
                 -> idl.parser.initialize(# EOLasComEnd::(#do true->value#) #);
            #);
          
       do
          (if (NoOfArguments <= 1) then Usage; leave block if);
          2 -> StartOfFilenames;
          
          args: 
            (# 
            do StartOfFilenames -> Arguments -> aText[];    
               (if true 
                // ('-o' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1) then 
                       Usage; leave block
                   if);      
                   StartOfFilenames+1 -> Arguments -> OutFileName[]; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-D' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1) then 
                       Usage; leave block
                   if);      
                   StartOfFilenames+1 -> Arguments -> defines.append; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-p' -> aText.equal)
                // ('--preserve' -> aText.equal) then 
                   true -> preserve;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-f' -> aText.equal)
                // ('--force' -> aText.equal) then 
                   true -> force;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-q' -> aText.equal)
                // ('--quiet' -> aText.equal) then 
                   true -> quiet;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-d' -> aText.equal)  
                // ('--debug' -> aText.equal) then 
                   true -> debug;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-t' -> aText.equal)  
                // ('--trace' -> aText.equal) then 
                   true -> trace;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-T' -> aText.equal)  
                // ('--mps_trace' -> aText.equal) then 
                   true -> mps_trace;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
               if);
            #);
          
          (if StartOfFilenames>NoOfArguments then
              Usage; leave block;
          if);
          
          (if not quiet then 'IDL to BETA translator.' -> putLine; if);
          
          <<SLOT add_basic_types: descriptor>>;
          
          collect_arguments:
            (# 
            do (if StartOfFilenames<=NoOfArguments then
                   StartOfFilenames -> Arguments -> files_remaining.add;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart collect_arguments;
               if)
            #);
          
          process_files:
            (# idl_entry, beta_entry: @diskentry;
            do 
               (if files_remaining.empty then leave process_files if);
               (files_remaining.head -> files_remaining.delete).elm[]
                 -> IdlFileName[];
               
               InitAstEnv;
               (if debug then 'With DEBUG.' -> screen.putline; 
                   false -> quiet;
                   'Using parser: ' -> screen.puttext;
                   BobsTab -> screen.putline;
               if);
               (if trace then 'With TRACE.' -> screen.putline; 
                   false -> quiet;
               if);
               
               '.idl' -> IdlFileName.appendExtension;
          
               (if OutFileName[]=NONE then
                   IdlFileName.copy -> OutFileName[];
                   '.idl' -> OutFileName.stripExtension;
                   (* Prevent identically named ast files: 
                    * NOT needed anymore, since the ast file for
                    * the IDL is not saved.
                    *)
                   (* '-beta' -> OutFileName.append; *)
                   '.bet' -> OutFileName.append;
               if);
               IdlFileName.copy -> IFilename[];
               '.idl' -> IFileName.stripExtension;
               '.i' -> IFileName.appendExtension;
               IdlFileName.copy -> CFilename[];
               '.idl' -> CFileName.stripExtension;
               '.c' -> CFileName.appendExtension;
               
               
               OutFileName[] -> Outfile.Name;
               (*'opening ' -> screen.puttext; Outfile.Name -> screen.putline;*)
	       (if not quiet then
                   'Processing '''->screen.puttext; 
	           (IdlFileName[], '') -> ast.thePathhandler.localpath 
                     -> screen.puttext; 
	           '''...'->screen.putline;
               if);
               (* First check that it is there *)
               IdlFileName[] -> idl_entry.path;
               (if not idl_entry.exists then
                   (if not quiet then
                       '    [IDL file does not exist; skipped]' -> screen.putline;
                   if);
                   restart process_files;
               if);
               (* We do not use top.open, since this enforces fragment
                * syntax on the source files. And we would like to be able to
                * use the IDL files "as-is", without modification.
                * This means, that we will have to do the following ourselves:
                *    dependency analysis
                *    include handling 
                *)
               (if not force then
                   (* Check if .bet file needs to be generated *)
                   OutFileName[] -> beta_entry.path;
                   (if beta_entry.exists then
                       (if beta_entry.modtime>idl_entry.modtime then
                           (if not quiet then
                               '    [BETA file is up-to-date; skipped]' 
                                 -> screen.putline
                           if);
                           restart process_files;
                       if)
                   if);
               if);
               Outfile.OpenWrite;
               IdlFileName[] -> Outfile.Identification;
               
               <<SLOT Translate:descriptor>>;
               
               Outfile.Epilog;
               (*'closing ' -> screen.puttext; Outfile.Name -> screen.putline;*)
               Outfile.Close;
               (if debug then
                   newline;
                   'Trace: Global types:'->putline;
                   screen.line;
                   global_types.print;
                   'Trace: Local types for file '->puttext;
                   IdlFileName[] -> puttext;
                   ':' -> putline;
                   screen.line;
                   local_types.print;
               if);
               
               NONE -> OutFileName[];
               restart process_files;
            #);
       #)
  #);
