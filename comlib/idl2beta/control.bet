ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/toollibs/utils/v1.0/fileExtensions';
INCLUDE '~beta/containers/v1.6/list';
BODY   'translate';
INCLUDE 'outfile';
INCLUDE 'idl_attributes';
--Lib: Attributes--
Control:
  (#
     Outfile: @OutputFile;
     Debug, trace, preserve, quiet: @boolean;
   
     StartOfFilenames: @integer; 
     aText: ^text;
     OutFileName, IdlFileName, Ifilename, Cfilename: ^text;
     
     textlist: list(# element::text #);
     defines: @textlist;
     holders: @textlist;
     refholders: @textlist;
     imports: @textlist;
     
     declarator_info:
       (* id is the name of a type, which is either a basic_type,
        * or an alias (with possible pointer indirection) to another 
        * type.
        *)
       (# id: ^text;
          pointer_level: @integer;
          comment: ^text;
          attributes: ^idl_attributes;
          basic_type: ^text;
          is_alias_for: ^declarator_info;
       #);
     
     (* FIXME: are types global, or can they be local
      * to interfaces? For now we assume that all types
      * are global.
      *)
     symbollist: list
       (# element::declarator_info;
          findsym: find 
            (# name: ^text;
               predicate::(# do (current.id[]->name.equal)->value #);
            enter name[]
            do INNER
            #);
       #);
     types: @symbollist;
     
     print_types:
       (# 
       do 'Registered types: ' -> putline;
          'id      pointer comment basic   alias   attributes'
            -> putline;
          '---------------------------------------------------------'
            -> putline;
          types.scan
          (# 
          do current.id[] -> puttext;
             '\t' -> puttext;
             (for current.pointer_level repeat
                 '*' -> put;
             for);
             '\t' -> puttext;
             (if current.comment[]<>NONE then
                 current.comment[] -> puttext;
             if);
             '\t' -> puttext;
             (if current.basic_type[]<>NONE then
                 current.basic_type[] -> puttext;
             if);
             '\t' -> puttext;
             (if current.is_alias_for[] <> NONE then
                 current.is_alias_for.id[] -> puttext;
             if);
             '\t' -> puttext;
             (if current.attributes[]<>NONE then
                 screen[] -> current.attributes.print;
             if);
             newline;
          #)
       #);
  do
     block:
       (#
          Usage:(# 
                do 'Usage: ' -> putText; 1 -> Arguments -> putText;
                   ' [Options] [-o <name>] <filenames>' -> putLine;
                   '\nOptions:' -> putline;
                   '  -q' -> putline; 		   
                   '  --quiet\tBe quiet - emit only error messages' -> putline;
                   '  -p' -> putline; 		   
                   '  --preserve\tPreserve temporary files' -> putline;
                   '  -D name' -> putline;
                   '  --define name\tDefine preprocessor name' -> putline;
                   '\nDebug options:' -> putline;
                   '  -d' -> putline; 
                   '  --debug\tEnable debug of bidl' -> putline;
                   '  -t' -> putline; 
                   '  --trace\tEnable tracing of bidl' -> putline;
                #);
       do
          (if (NoOfArguments <= 1) then Usage; leave block if);
          2 -> StartOfFilenames;
          
          args: 
            (# 
            do StartOfFilenames -> Arguments -> aText[];    
               (if true 
                // ('-o' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1) then 
                       Usage; leave block
                   if);      
                   StartOfFilenames+1 -> Arguments -> OutFileName[]; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-D' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1) then 
                       Usage; leave block
                   if);      
                   StartOfFilenames+1 -> Arguments -> defines.append; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-p' -> aText.equal)
                // ('--preserve' -> aText.equal) then 
                   true -> preserve;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-q' -> aText.equal)
                // ('--quiet' -> aText.equal) then 
                   true -> quiet;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-d' -> aText.equal)  
                // ('--debug' -> aText.equal) then 
                   true -> debug;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-t' -> aText.equal)  
                // ('--trace' -> aText.equal) then 
                   true -> trace;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
               if);
            #);
          
          (if StartOfFilenames>NoOfArguments then
              Usage; leave block;
          if);
          
          (if not quiet then 'IDL to BETA translator.' -> putLine; if);
          
          process_files:
            (# 
            do 
               StartOfFilenames -> Arguments -> IdlFileName[];
               '.idl' -> IdlFileName.appendExtension;
               '.idl' -> IdlFileName.appendExtension;
          
               (if OutFileName[]=NONE then
                   IdlFileName.copy -> OutFileName[];
                   '.idl' -> OutFileName.stripExtension;
                   (* Prevent identically named ast files: 
                    * NOT needed anymore, since the ast file for
                    * the IDL is not saved.
                    *)
                   (* '-beta' -> OutFileName.append; *)
                   '.bet' -> OutFileName.append;
               if);
               IdlFileName.copy -> IFilename[];
               '.idl' -> IFileName.stripExtension;
               '.i' -> IFileName.appendExtension;
               IdlFileName.copy -> CFilename[];
               '.idl' -> CFileName.stripExtension;
               '.c' -> CFileName.appendExtension;

               
               OutFileName[] -> Outfile.Name;
               Outfile.OpenWrite;
               IdlFileName[] -> Outfile.Identification; 
               <<SLOT Translate:descriptor>>;
               Outfile.Epilog;
               Outfile.Close;
               (if debug then
                   print_types;
               if);
               
               StartOfFilenames+1 -> StartOfFilenames;
               (if StartOfFilenames<=NoOfArguments then
                   NONE -> OutFileName[];
                   restart process_files;
               if);
            #);
       #)
  #);
