ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/toollibs/utils/v1.0/fileExtensions';
INCLUDE '~beta/containers/v1.6/list';
BODY   'translate';
INCLUDE 'outfile';
--Lib: Attributes--
Control:
  (#
     Outfile: @OutputFile;
     Debug, trace, preserve, quiet: @boolean;
     defines: @list(# element::text #);
     StartOfFilenames: @integer; 
     aText, OutFileName, IdlFileName: ^text;
  do
     block:
       (#
          Usage:(# 
                do 'Usage: ' -> putText; 1 -> Arguments -> putText;
                   ' [options] [-o <name>] <filenames>' -> putLine;
                   'Options:' -> putline;
                   '  -q' -> putline; 		   
                   '  --quiet\tBe quiet - emit only error messages' -> putline;
                   '  -p' -> putline; 		   
                   '  --preserve\tPreserve temporary files' -> putline;
                   '  -D name' -> putline;
                   '  --define name\tDefine preprocessor name' -> putline;
                   'Debug options:' -> putline;
                   '  -d' -> putline; 
                   '  --debug\tEnable debug of bidl' -> putline;
                   '  -t' -> putline; 
                   '  --trace\tEnable tracing of bidl' -> putline;
                #);
       do
          (if (NoOfArguments <= 1) then Usage; leave block if);
          2 -> StartOfFilenames;
          
          args: 
            (# 
            do StartOfFilenames -> Arguments -> aText[];    
               (if true 
                // ('-o' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1) then 
                       Usage; leave block
                   if);      
                   StartOfFilenames+1 -> Arguments -> OutFileName[]; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-D' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1) then 
                       Usage; leave block
                   if);      
                   StartOfFilenames+1 -> Arguments -> defines.append; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-p' -> aText.equal)
                // ('--preserve' -> aText.equal) then 
                   true -> preserve;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-q' -> aText.equal)
                // ('--quiet' -> aText.equal) then 
                   true -> quiet;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-d' -> aText.equal) then 
                // ('--debug' -> aText.equal) then 
                   true -> debug;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-t' -> aText.equal) then 
                // ('--trace' -> aText.equal) then 
                   true -> trace;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
               if);
            #);
          
          (if StartOfFilenames>NoOfArguments then
              Usage; leave block;
          if);
          
          (if not quiet then 'IDL to BETA translator.' -> putLine; if);
          
          process_files:
            (# 
            do 
               StartOfFilenames -> Arguments -> IdlFileName[];
               '.idl' -> IdlFileName.appendExtension;
          
               (if OutFileName[]=NONE then
                   IdlFileName.copy -> OutFileName[];
                   '.idl' -> OutFileName.stripExtension;
                   (* Prevent identically named ast files: 
                    * NOT needed anymore, since the ast file for
                    * the IDL is not saved.
                    *)
                   (* '-beta' -> OutFileName.append; *)
                   '.bet' -> OutFileName.append;
               if);
               
               OutFileName[] -> Outfile.Name;
               Outfile.OpenWrite;
               IdlFileName[] -> Outfile.Identification; 
               <<SLOT Translate:descriptor>>;
               Outfile.Epilog;
               Outfile.Close;
               
               StartOfFilenames+1 -> StartOfFilenames;
               (if StartOfFilenames<=NoOfArguments then
                   NONE -> OutFileName[];
                   restart process_files;
               if);
            #);
       #)
  #);
