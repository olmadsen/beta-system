ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/toollibs/utils/v1.0/fileExtensions';
BODY   './translate';
INCLUDE './outfile';
--Lib: Attributes--
Control:
  (#
     Outfile: @OutputFile;
     Debug: @boolean;
     StartOfFilenames: @integer; 
     aText, OutFileName, IdlFileName: ^text;
  do
     block:
       (#
          Usage:(# 
                do 'USAGE: ' -> putText; 1 -> Arguments -> putText;
                   ' [-debug] [-o <name> ] <filenames>' -> putLine 
                #);
          
       do
          (if (NoOfArguments <= 1) then Usage; leave block if);
          2 -> StartOfFilenames;
          
          args: 
            (# 
            do StartOfFilenames -> Arguments -> aText[];    
               (if true 
                // ('-o' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1) then 
                       Usage; leave block
                   if);      
                   StartOfFilenames+1 -> Arguments -> OutFileName[]; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-debug' -> aText.equal) then 
                   true -> debug;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
               if);
            #);
          
          (if StartOfFilenames>NoOfArguments then
              Usage; leave block;
          if);
          
          process_files:
            (# 
            do 
               StartOfFilenames -> Arguments -> IdlFileName[];
               '.idl' -> IdlFileName.appendExtension;
          
               (if OutFileName[]=NONE then
                   IdlFileName.copy -> OutFileName[];
                   '.idl' -> OutFileName.stripExtension;
                   (* Prevent identically named ast files: *)
                   '-beta' -> OutFileName.append;
                   '.bet' -> OutFileName.append;
               if);
               
               OutFileName[] -> Outfile.Name;
               Outfile.OpenWrite;
               IdlFileName[] -> Outfile.Prolog; 
          
               <<SLOT Translate:descriptor>>;
               
               Outfile.Epilog;
               Outfile.Close;
               
               StartOfFilenames+1 -> StartOfFilenames;
               (if StartOfFilenames<=NoOfArguments then
                   NONE -> OutFileName[];
                   restart process_files;
               if);
            #);
       #)
  #);
