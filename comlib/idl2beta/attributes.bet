ORIGIN 'translate';

--attributeY_body: descriptor--
(# ca: ^idl.call_as;
   aii: ^idl.att_iid_is;
   ii: ^idl.iid_is;
   ta: ^idl.transmit_as;
   st: ^idl.switch_type;
   si: ^idl.switch_is;
   ac: ^idl.att_case;
   zi: ^idl.size_is;
   li: ^idl.length_is;
   ah: ^idl.att_helpstring;
   hs: ^idl.helpstring;
   dcl_info: @declarator_info;
do (if y.symbol
    // idl.att_unique then
       true -> att.pointer_type.unique;
    // idl.att_ref then
       true -> att.pointer_type.ref;
    // idl.att_in then
       true->att.directional.in;
    // idl.att_out then
       true->att.directional.out;
    // idl.att_in_out then
       true->att.directional.in;
       true->att.directional.out;
    // idl.att_local then
       (* FIXME: don't know context! *)
       true->att.function_call.local;
       true->att.interface_header.local;
       true->att.performance.local;
    // idl.att_retval then
       true->att.typelib.retval;
    // idl.att_default then
       true->att.typelib.default;
    // idl.call_as then
       (* FIXME: don't know context! *)
       (y[]->ca[]).getIdentifier
         -> IdentifierToText
         -> att.alias_marshal.call_as[]
         -> att.function_call.call_as[];
    // idl.att_iid_is then
       (* FIXME: don't know context! *)
       (y[]->aii[]).getiid_is->ii[];
       ii.getIdentifier
         -> IdentifierToText
         -> att.alias_marshal.iid_is[]
         -> att.pointer_type.iid_is[];
    // idl.size_is then
       (y[]->zi[]).getconst_exp
         -> do_const_exp
         -> att.array_size.size_is[];
    // idl.length_is then
       (dcl_info[], (y[]->li[]).getdeclarator)
         -> do_declarator;
       dcl_info.name[] -> att.array_size.size_is[];
    // idl.v1_enum then
       true -> att.data_type.v1_enum;
    // idl.transmit_as then
       (* FIXME: don't know context! *)
       (y[]->ta[]).getIdentifier
         -> IdentifierToText
         -> att.alias_marshal.transmit_as[]
         -> att.data_type.transmit_as[];
    // idl.att_string then
       (* FIXME: don't know context! *)
       true 
         -> att.array_size.string
         -> att.pointer_type.string
         -> att.typelib.string;
    // idl.switch_type then
       (y[]->st[]).getIdentifier
         -> IdentifierToText
         -> att.struct_union.switch_type[];
    // idl.switch_is then
       (y[]->si[]).getId1
         -> IdentifierToText
         -> att.struct_union.switch_is.expression[];
       si.getId2
         -> IdentifierToText
         -> att.struct_union.switch_is.declarator[];
    // idl.att_case then
       (y[]->ac[]).getIdentifier
         -> IdentifierToText
         -> att.struct_union.switch[];
    // idl.async then
       true -> att.asynchronous.async;
    // idl.att_id then
       (* FIXME *)
    // idl.att_optional then
       true -> att.typelib.optional;
    // idl.att_bindable then
       true -> att.typelib.bindable;
    // idl.att_propput then
       true -> att.typelib.propput;
    // idl.att_propget then
       true -> att.typelib.propget;
    // idl.att_helpstring then
       ((y[]->ah[]).gethelpstring->hs[]).getstring 
         -> StringToText
         -> att.typelib.helpstring[];
   if);
#)
