ORIGIN 'translate';

--attributeY_body: descriptor--
(# do_uuid_rep:
     (# r: ^idl.uuid_rep;
        uuid: ^text;
     enter r[]
     do &text[] -> uuid[];
        r.newscan
        (# k: ^idl.keys;
           first_done: @boolean;
        do current[] -> k[];
           (if first_done then
               '-' -> uuid.put;
            else
               true->first_done
           if);
           k.newscan
           (# n: ^idl.namekey;
              c: ^idl.constkey;
           do (if current.symbol
               // idl.namekey then
                  current[] -> n[];
                  n.getnameappl -> NameApplToText -> uuid.puttext;
               // idl.constkey then
                  current[] -> c[];
                  c.getconst -> ConstToText -> uuid.puttext;
              if)
           #);
        #);
     exit uuid[]
     #);
   
   do_iversion:
     (# v: ^idl.iversion;
        major: ^idl.major_version;
        minor: ^idl.minor_version;
        version: ^text;
     enter v[]
     do &text[] -> version[];
        'version: (\# exit ' -> version.puttext;
        v.getmajor_version -> major[];
        major.getconst -> ConstToText -> version.puttext;
        (if v.getminor_version_opt -> optionalPresent then
            '.' -> version.put;
            v.getminor_version_opt -> minor[];
            minor.getconst -> ConstToText -> version.puttext;
        if);
        ' #\);' -> version.putline;
     exit version[]
     #);
   
   ca: ^idl.call_as;
   ii: ^idl.iid_is;
   ta: ^idl.transmit_as;
   st: ^idl.switch_type;
   si: ^idl.switch_is;
   ac: ^idl.case;
   zi: ^idl.size_is;
   li: ^idl.length_is;
   hs: ^idl.helpstring;
   u: ^idl.uuid;
   dcl_info: @declarator_info;
do (if y.symbol
    // idl.unique then
       true -> att.pointer_type.unique;
    // idl.ref then
       true -> att.pointer_type.ref;
    // idl.in then
       true->att.directional.in;
    // idl.out then
       true->att.directional.out;
    // idl.local then
       (* FIXME: don't know context! *)
       true->att.function_call.local;
       true->att.interface_header.local;
       true->att.performance.local;
    // idl.retval then
       true->att.typelib.retval;
    // idl.default then
       true->att.typelib.default;
    // idl.call_as then
       (* FIXME: don't know context! *)
       (y[]->ca[]).getIdentifier
         -> IdentifierToText
         -> att.alias_marshal.call_as[]
         -> att.function_call.call_as[];
    // idl.iid_is then
       (* FIXME: don't know context! *)
       (y[]->ii[]).getIdentifier
         -> IdentifierToText
         -> att.alias_marshal.iid_is[]
         -> att.pointer_type.iid_is[];
    // idl.size_is then
       (y[]->zi[]).getconst_exp
         -> do_const_exp
         -> att.array_size.size_is[];
    // idl.length_is then
       (dcl_info[], (y[]->li[]).getdeclarator)
         -> do_declarator;
       dcl_info.name[] -> att.array_size.size_is[];
    // idl.v1_enum then
       true -> att.data_type.v1_enum;
    // idl.transmit_as then
       (* FIXME: don't know context! *)
       (y[]->ta[]).getIdentifier
         -> IdentifierToText
         -> att.alias_marshal.transmit_as[]
         -> att.data_type.transmit_as[];
    // idl.istring then
       (* FIXME: don't know context! *)
       true 
         -> att.array_size.string
         -> att.pointer_type.string
         -> att.typelib.string;
    // idl.switch_type then
       (y[]->st[]).getIdentifier
         -> IdentifierToText
         -> att.struct_union.switch_type[];
    // idl.switch_is then
       (y[]->si[]).getId1
         -> IdentifierToText
         -> att.struct_union.switch_is.expression[];
       si.getId2
         -> IdentifierToText
         -> att.struct_union.switch_is.declarator[];
    // idl.case then
       (* FIXME *)
    // idl.async then
       true -> att.asynchronous.async;
    // idl.id then
       (* FIXME *)
    // idl.optional then
       true -> att.typelib.optional;
    // idl.bindable then
       true -> att.typelib.bindable;
    // idl.propput then
       true -> att.typelib.propput;
    // idl.propget then
       true -> att.typelib.propget;
    // idl.helpstring then
       (y[]->hs[]).getstring 
         -> StringToText
         -> att.typelib.helpstring[];
    // idl.uuid then
       (y[]->u[]).getuuid_rep 
         -> do_uuid_rep 
         -> att.interface_header.uuid[]
         -> att.typelib.uuid[];
    // idl.iversion then
       y[] -> do_iversion  
         -> att.interface_header.version[]
         -> att.typelib.version[];
   if);
#)
