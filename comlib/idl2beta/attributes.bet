ORIGIN 'translate';

--attribute_spec_body: descriptor--
(# do_uuid_rep:
     (# r: ^idl.uuid_rep;
        uuid: ^text;
     enter r[]
     do &text[] -> uuid[];
        r.newscan
        (# k: ^idl.keys;
           first_done: @boolean;
        do current[] -> k[];
           (if first_done then
               '-' -> uuid.put;
            else
               true->first_done
           if);
           k.newscan
           (# n: ^idl.namekey;
              c: ^idl.constkey;
           do (if current.symbol
               // idl.namekey then
                  current[] -> n[];
                  n.getnameappl -> NameApplToText -> uuid.puttext;
               // idl.constkey then
                  current[] -> c[];
                  c.getconst -> ConstToText -> uuid.puttext;
              if)
           #);
        #);
     exit uuid[]
     #);
   
   do_iversion:
     (# v: ^idl.iversion;
        major: ^idl.major_version;
        minor: ^idl.minor_version;
        version: ^text;
     enter v[]
     do &text[] -> version[];
        'version: (\# exit ' -> version.puttext;
        v.getmajor_version -> major[];
        major.getconst -> ConstToText -> version.puttext;
        (if v.getminor_version_opt -> optionalPresent then
            '.' -> version.put;
            v.getminor_version_opt -> minor[];
            minor.getconst -> ConstToText -> version.puttext;
        if);
        ' #\);' -> version.putline;
     exit version[]
     #);
   
   u:  ^idl.uuid;
   g:  ^idl.attribute_general;
   av: ^idl.attribute_value;
   as: ^idl.att_val_spec;
   astr: ^idl.att_val_string;
   att_name: ^text;
   dcl_info: @declarator_info;
do (if y.symbol
    // idl.uuid then
       (if debug then 'uuid' -> screen.puttext; if);
       (y[]->u[]).getuuid_rep 
         -> do_uuid_rep 
         -> att.interface_header.uuid[]
         -> att.typelib.uuid[];
    // idl.iversion then
       (if debug then 'version' -> screen.puttext; if);
       y[] -> do_iversion  
         -> att.interface_header.version[]
         -> att.typelib.version[];
    // idl.attribute_general then
       y[] -> g[];
       g.getnameappl -> NameApplToText -> att_name[];
       (if debug then
           (if (att_name[]=NONE) or (att_name.length=0) then
               '??' -> screen.puttext;
            else
               att_name[] -> screen.puttext; 
           if);
       if);
       (if true
        // 'propput' -> att_name.equal then
           (if debug then 'propput' -> screen.putline; if);
           true -> att.typelib.propput;
        // 'propget' -> att_name.equal then
           (if debug then 'propput' -> screen.putline; if);
           true -> att.typelib.propget;
        // 'string' -> att_name.equal then
           (* FIXME: don't know context! *)
           true 
             -> att.array_size.string
             -> att.pointer_type.string
             -> att.typelib.string;
        // 'helpstring' -> att_name.equal then
           (if g.getattribute_value_opt -> optionalPresent then
               g.getattribute_value_opt -> av[];
               av.getatt_val_spec -> as[];
               (if as.symbol
                // idl.att_val_string then
                   as[] -> astr[];
                   astr.getstring 
                     -> StringToText
                     -> att.typelib.helpstring[];
                
               if)
           if);
           (if att.typelib.helpstring[]=NONE then
               'helpstring: no string specified?'-> warning
           if);
        // 'aggregatable' -> att_name.equal
        // 'appobject' -> att_name.equal
        // 'async' -> att_name.equal
        // 'bindable' -> att_name.equal
        // 'broadcast' -> att_name.equal
        // 'call_as' -> att_name.equal
        // 'callback' -> att_name.equal
        // 'coclass' -> att_name.equal
        // 'control' -> att_name.equal
        // 'custom' -> att_name.equal
        // 'default' -> att_name.equal
        // 'defaultbind' -> att_name.equal
        // 'defaultcollelem' -> att_name.equal
        // 'defaultvalue' -> att_name.equal
        // 'defaultvtbl' -> att_name.equal
        // 'dispinterface' -> att_name.equal
        // 'displaybind' -> att_name.equal
        // 'dllname' -> att_name.equal
        // 'dual' -> att_name.equal
        // 'endpoint' -> att_name.equal
        // 'entry' -> att_name.equal
        // 'first_is' -> att_name.equal
        // 'handle' -> att_name.equal
        // 'helpcontext' -> att_name.equal
        // 'helpfile' -> att_name.equal
        // 'helpstringdll' -> att_name.equal
        // 'hidden' -> att_name.equal
        // 'id' -> att_name.equal
        // 'idempotent' -> att_name.equal
        // 'ignore' -> att_name.equal
        // 'iid_is' -> att_name.equal
        // 'immediatebind' -> att_name.equal
        // 'in' -> att_name.equal
        // 'last_is' -> att_name.equal
        // 'lcid' -> att_name.equal
        // 'library' -> att_name.equal
        // 'licensed' -> att_name.equal
        // 'local' -> att_name.equal
        // 'max_is' -> att_name.equal
        // 'maybe' -> att_name.equal
        // 'message' -> att_name.equal
        // 'ms_union' -> att_name.equal
        // 'nonbrowsable' -> att_name.equal
        // 'noncreatable' -> att_name.equal
        // 'nonextensible' -> att_name.equal
        // 'odl' -> att_name.equal
        // 'object' -> att_name.equal
        // 'oleautomation' -> att_name.equal
        // 'optional' -> att_name.equal
        // 'out' -> att_name.equal
        // 'pipe' -> att_name.equal
        // 'pointer_default' -> att_name.equal
        // 'propputref' -> att_name.equal
        // 'ptr' -> att_name.equal
        // 'public' -> att_name.equal
        // 'readonly' -> att_name.equal
        // 'ref' -> att_name.equal
        // 'replaceable' -> att_name.equal
        // 'requestedit' -> att_name.equal
        // 'restricted' -> att_name.equal
        // 'retval' -> att_name.equal
        // 'source' -> att_name.equal
        // 'switch' -> att_name.equal
        // 'switch_type' -> att_name.equal
        // 'transmit_as' -> att_name.equal
        // 'transmit_as ' -> att_name.equal
        // 'uidefault' -> att_name.equal
        // 'ull_asynch' -> att_name.equal
        // 'unique' -> att_name.equal
        // 'usesgetlasterror' -> att_name.equal
        // 'v1_enum' -> att_name.equal
        // 'vararg' -> att_name.equal
        // 'version' -> att_name.equal
        // 'wire_marshal' -> att_name.equal
           then (* FIXME *)
        else
           (# msg: ^text;
           do 'Unhandled attribute: ' -> msg[];
              att_name[] -> msg.append;
              msg[] -> notice;
           #)
       if);
    else
       (# msg: ^text;
       do 'Unhandled attribute symbol: ' -> msg[];
          y.symbol -> msg.putint;
          msg[] -> error;
       #);
   if);
#)
