ORIGIN '../outfile';
INCLUDE '~beta/sysutils/time';

--OutfileIdentification:dopart--
do '(\* BETA interface generated from "' -> puttext;
   IdlFileName[] -> puttext; 
   '"' -> putline;
   (if false then
       ' * at ' -> puttext;
       systemtime -> formattime -> putline;
   if);
   (# options, arg: ^text;
   do &text[] -> options[];
      (for i:NoOfArguments repeat
           (if i>1 then
               i->Arguments->arg[];
               (if (arg.length>1) and (arg.T[1]='-') then
                   arg[] -> options.puttext;
                   ' '->options.put;
               if);
           if)
      for);
      (if options.length>0 then
          ' * with options: '->puttext;
          options[]->puttext;
      if);
   #);
   '\n *\)' -> putline;
   
--OutfileOrigin:dopart--
do (if chain and (PreviousFile[]<>NONE) then
       'ORIGIN \''->puttext;
       PreviousFile[] -> puttext;
       '\';' -> putline;
    else
       'ORIGIN \'~beta/comlib/comtypes\';' -> putline;
   if);
   
--OutfileLib:dopart--
do '--LIB: attributes-- ;' -> putline;
   (* The semicolon is there in case there is nothing generated in the LIB *)
--OutfileEpilog:dopart--
do
   
--OutFilePatternBegin: dopart--
do '(# ' -> puttext;
     
--OutFilePatternEnd: dopart--
do '#)' -> puttext;
   
--OutFileCommentBegin: dopart--
do '(* ' -> puttext;
     
--OutFileCommentEnd: dopart--
do ' *)' -> puttext;
   
--OutFileAttributes: attributes--
putcomment:
  (* be sure not to emit commentbegin or -end in comment body *)
  (# T: ^text;
     thisCh, prevCh: @char;
  enter T[]
  do (for i:t.lgth repeat
          (if (i=1) then
              T.T[1]->thisCh;
           else
              thisCh->prevCh;
              T.T[i] -> thisCh;
              (if (prevCh='(') and (thisCh='*') then ' '->put; if);
              (if (prevCh='*') and (thisCh=')') then ' '->put; if);
          if);
          thisCh -> put;
     for);
  #)
   
--OutFileComment: dopart--
do (if (not compact) and (c[]<>NONE) then
       '(* ' -> puttext;
        c[] -> putcomment;
        ' *)' -> puttext;
   if);
--OutFileSpaceComment: dopart--
do (if (not compact)  and (c[]<>NONE) then
       ' (\* ' -> puttext;
       c[] -> putcomment;
       ' *\)' -> puttext;
   if);
   
--OutFileCommentLine: dopart--
do (if (not compact) and (c[]<>NONE) then
       '(\* ' -> puttext;
       c[] -> putcomment;
       ' *\)' -> putline;
   if)
   
