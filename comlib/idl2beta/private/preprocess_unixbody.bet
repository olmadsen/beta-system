ORIGIN 'translatebody';
INCLUDE '~beta/sysutils/envstring';

--PreprocessBody:descriptor--
(# cmd: ^text;
do (* FIXME: should use process!!! *)
   
   'echo "#define SAFEARRAY(type) SAFEARRAY*" > '->cmd[];
   cfilename[] -> cmd.append;
   cmd[] -> report;
   cmd -> system;
      
   'grep -v "#pragma" '->cmd[];
   idlfilename[] -> cmd.append;
   ' >> ' -> cmd.append;
   cfilename[] -> cmd.append;
   cmd[] -> report;
   cmd -> system;
   
   '$(BIDL_CPP)' -> expandEnvVar
   (# defaultvalue:: 
        (# 
        do 'gcc -E -P' -> envvarvalue[];
           locate: scanSearchPath
             (# e: @diskentry;
             do '/gcc' -> (path.copy).append -> e.path;
                (if e.exists then
                    ' -E -P' -> ((e.path).copy).append ->envvarvalue[];
                    leave locate;
                if);
             #);
        #);
   #) -> cmd[];
   ' -I' -> cmd.append;
   (if HeadOfIdlFileName.length=0 then
       '. ' -> cmd.append;
    else
       HeadOfIdlFileName[] -> cmd.append;
       ' '->cmd.append;
   if);
   defines.scan
   (# 
   do '-D ' -> cmd.append;
      current[] -> cmd.append;
      ' ' -> cmd.append;
   #);
   includepaths.scan
   (# 
   do '-I ' -> cmd.append;
      current[] -> cmd.append;
      ' ' -> cmd.append;
   #);
   '-o ' -> cmd.append;
   ifilename[] -> cmd.append;
   ' ' -> cmd.append;
   cfilename[] -> cmd.append;
   cmd[] -> report;
   cmd -> system;
   
   (if not preserve then
       'rm -f '->cmd[];
       cfilename[] -> cmd.append;
       cmd[] -> report;
       cmd -> system;
   if);
#)

-- DeleleIFile: descriptor --
(# 
do (if not preserve then
       'rm -f ' -> cmd[];
       ifilename[] -> cmd.append;
       cmd[] -> report;
       cmd -> system;
   if);
#)
