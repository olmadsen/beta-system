ORIGIN 'translate';

--type_dcl_lib: attributes--
do_typedef:
  (# t: ^idl.typedef;
     x: ^idl.attributeX;
  enter (t[])
  do outfile.newline;
     outfile.indent;
     'typedef:' -> outfile.commentline;
     outfile.indent;
     (t.gettype_declarator) -> do_type_declarator;
     (if t.getattribute_opt -> optionalPresent then
         t.getattribute_opt -> do_attributeX;
     if);
     ';' -> outfile.putline;
  #);
  
do_type_spec:
  (# t: ^idl.type_spec;
     info: ^declarator_info;
  enter (info[],t[])
  do (if t.symbol
      // idl.float then
         'real (* float *)' -> outfile.puttext;
      // idl.double then
         'real (* double *)' -> outfile.puttext;
      // idl.signed_long_int then
         'integer' -> outfile.puttext;
      // idl.signed_short_int then
         'shortint' -> outfile.puttext;
      // idl.unsigned_long_int then
         'uint32' -> outfile.puttext;
      // idl.unsigned_short_int then
         'uint16' -> outfile.puttext;
      // idl.char_type then
         'char' -> outfile.puttext;
      // idl.boolean_type then
         'boolean' -> outfile.puttext;
      // idl.octet_type then
         'octet_type NYI' -> outfile.puttext;
      // idl.any_type then
         'any_type NYI' -> outfile.puttext;
      // idl.sequence1 then
         'sequence1 NYI' -> outfile.puttext;
      // idl.sequence2 then
         'sequence2 NYI' -> outfile.puttext;
      // idl.string1 then
         'string1 NYI' -> outfile.puttext;
      // idl.string2 then
         'string2 NYI' -> outfile.puttext;
      // idl.scoped_name1
      // idl.scoped_name2
      // idl.scoped_name3 then
         t[] -> do_scoped_name;
         (if true
          // info.is_pointer then
             '_Ref (* FIXME *)' -> outfile.puttext;
          // info.is_pointer_pointer then
             '_RefRef (* FIXME *)' -> outfile.puttext;
         if);
         '(# #)' -> outfile.puttext;
      // idl.constr_struct_type then
         t[] -> do_constr_struct_type;
      // idl.constr_union_type then
         'constr_union_type NYI' -> outfile.puttext;
      // idl.constr_enum_type then
         (info[],t[]) -> do_constr_enum_type;
      // idl.void_type_spec then
         'Object(# (* void *) #)' -> outfile.puttext;
      else
         'Oops, must have forgotten a type!' -> outfile.comment;
     if);
  #);

do_type_declarator:
  (# t: ^idl.type_declarator;
     ;
     info: @declarator_info;
     d: ^idl.declarators;
  enter t[]
  do t.getdeclarators -> d[];
     d.newscan
     (# d: ^idl.declarator;
     do (info[], current[]) -> do_declarator;
        info.id[] -> outfile.puttext;
        ': '->outfile.puttext;
        (info[], t.gettype_spec) -> do_type_spec;
     #);
  #);

do_struct_type:
  (# t: ^idl.struct_type;
     l: ^idl.member_list;
     do_members:
       (# m: ^idl.members
       enter m[]
       do m.newscan
          (# d: ^idl.declarators;
             mem: ^idl.member;
          do current[] -> mem[];
             mem.getdeclarators -> d[];
             d.newscan
             (# d: ^idl.declarator;
                info: @declarator_info;
             do outfile.indent;
                (info[], current[]) -> do_declarator;
                info.id[] -> outfile.puttext;
                ': '->outfile.puttext;
                (if info.is_pointer or info.is_pointer_pointer then
                    '^' -> outfile.put;
                 else
                    '@' -> outfile.put;
                if);
                (info[], mem.gettype_spec) -> do_type_spec;
                (if info.is_pointer_pointer then
                    'Holder' -> outfile.puttext;
                if);
                (if mem.getattribute_opt -> optionalPresent then
                    mem.getattribute_opt -> do_attributeX;
                if);
                ';' -> outfile.putline;
             #);
          #);
       #);
  enter t[]
  do 'DATA' -> outfile.putline;
     2 -> outfile.indent.inc;
     outfile.indent;
     outfile.CommentBegin;
     'struct' -> outfile.puttext;
     (if t.getidentifierOpt -> optionalPresent then
         ' ' -> outfile.put;
         t.getidentifierOpt 
           -> IdentifierToText
           -> outfile.puttext;
     if);
     outfile.CommentEnd;
     outfile.newline;
     outfile.indent;
     outfile.PatternBegin;
     outfile.newline;
     3 -> outfile.indent.inc;
     t.getmember_list -> l[];
     l.getmembers -> do_members;
     3 -> outfile.indent.dec;
     outfile.indent;
     outfile.PatternEnd;
     2 -> outfile.indent.dec;
  #);
do_enum_type:
  (# t: ^idl.enum_type;
     es: ^idl.enumerators;
     val: @integer;
     info: ^declarator_info;
     id, previous_id: ^text;
     initializer_met: @boolean;
  enter (info[],t[])
  do (* Define "type" pattern *)
     'IntegerObject(\# ' -> outfile.puttext;
     outfile.CommentBegin;
     'enum ' -> outfile.puttext;
     t.getidentifier -> IdentifierToText -> outfile.puttext;
     outfile.CommentEnd;
     outfile.PatternEnd;
     (* Process each enumerator *) 
     t.getenumerators -> es[];
     0->val;
     NONE -> id[];
     NONE -> previous_id[];
     false -> initializer_met;
     es.newscan
     (# v: ^idl.value;
     do ';' -> outfile.putline;
        outfile.indent;
        id[] -> previous_id[];
        current.getidentifier -> IdentifierToText -> id[] ->outfile.puttext;
        ': ' -> outfile.puttext;
        outfile.CommentBegin;
        info.id[] -> outfile.puttext;
        outfile.CommentEnd;
        outfile.PatternBegin;
        'exit ' -> outfile.puttext;
        (if current.getvalue_opt -> optionalPresent then
            (current.getvalue_opt -> v[]).getconst_exp -> do_const_exp;
            true -> initializer_met;
         else
            (if initializer_met then
                (* emit expression - let beta compiler do the work (:-) *)
                previous_id[] -> outfile.puttext;
                '+1' -> outfile.puttext;
             else
                (* emit value *)
                val -> outfile.putint;
                val+1 -> val;
            if);
        if);
        outfile.PatternEnd;
     #);
  #);

do_constr_enum_type:
  (# t: ^idl.constr_enum_type;
     info: ^declarator_info;
  enter (info[],t[])
  do (info[],t.getenum_type) -> do_enum_type;
  #);

do_constr_struct_type:
  (# t: ^idl.constr_struct_type;
  enter t[]
  do t.getstruct_type -> do_struct_type;
  #);


do_type_dcl_error:
  (# t: ^idl.type_dcl_error;
  enter t[]
  do outfile.indent;
     'do_type_dcl_error NYI' -> outfile.commentline;
  #);

--type_dcl_body: descriptor--
(# dcl_info: @declarator_info;          
do (if d.symbol
    // idl.typedef then
       d[] -> do_typedef;
    // idl.struct_type then
       d[] -> do_struct_type;
    // idl.enum_type then
       (dcl_info[],d[]) -> do_enum_type;
    // idl.type_dcl_error then
       d[] -> do_type_dcl_error;
   if);
#)
