ORIGIN 'translate';

--type_dcl_lib: attributes--
do_typedef:
  (# t: ^idl.typedef;
     x: ^idl.attributeX;
     a: ^idl.attributes;
     info: ^declarator_info;
  enter (info[],t[])
  do outfile.indent;
     'typedef:' -> outfile.commentline;
     outfile.indent;
     (info[],t.gettype_declarator) -> do_type_declarator;
     (if t.getattribute_opt -> optionalPresent then
         t.getattribute_opt -> x[];
         x.getattributes -> a[];
         outfile.CommentBegin;
         a.newscan
         (# first_done: @boolean;
         do (if first_done then
                ',' -> outfile.put;
             else
                true->first_done
            if);
            current[] -> do_attributeY;
         #);
         outfile.CommentEnd;
     if);
     ';' -> outfile.putline;
  #);
do_type_declarator:
  (# t: ^idl.type_declarator;
     do_type_spec:
       (# t: ^idl.type_spec;
          info: ^declarator_info;
       enter (info[],t[])
       do (if t.symbol
           // idl.float then
              'float NYI' -> outfile.puttext;
           // idl.double then
              'double NYI' -> outfile.puttext;
           // idl.signed_long_int then
              'signed_long_int NYI' -> outfile.puttext;
           // idl.signed_short_int then
              'signed_short_int NYI' -> outfile.puttext;
           // idl.unsigned_long_int then
              'unsigned_long_int NYI' -> outfile.puttext;
           // idl.unsigned_short_int then
              'unsigned_short_int NYI' -> outfile.puttext;
           // idl.char_type then
              'char_type NYI' -> outfile.puttext;
           // idl.boolean_type then
              'boolean_type NYI' -> outfile.puttext;
           // idl.octet_type then
              'octet_type NYI' -> outfile.puttext;
           // idl.any_type then
              'any_type NYI' -> outfile.puttext;
           // idl.sequence1 then
              'sequence1 NYI' -> outfile.puttext;
           // idl.sequence2 then
              'sequence2 NYI' -> outfile.puttext;
           // idl.string1 then
              'string1 NYI' -> outfile.puttext;
           // idl.string2 then
              'string2 NYI' -> outfile.puttext;
           // idl.scoped_name1
           // idl.scoped_name2
           // idl.scoped_name3 then
              t[] -> do_scoped_name;
              (if true
               // info.is_pointer then
                  '_Ref' -> outfile.puttext;
               // info.is_pointer_pointer then
                  '_RefRef' -> outfile.puttext;
              if);
              '(# #)' -> outfile.puttext;
           // idl.constr_struct_type then
              'constr_struct_type NYI' -> outfile.puttext;
           // idl.constr_union_type then
              'constr_union_type NYI' -> outfile.puttext;
           // idl.constr_enum_type then
              (info[],t[]) -> do_constr_enum_type;
           // idl.void_type_spec then
              'Object(# (* void *) #)' -> outfile.puttext;
           else
              'Oops, must have forgotten a type!' -> outfile.comment;
          if);
       #);
     do_declarators:
       (# d: ^idl.declarators;
       enter d[]
       do outfile[] -> info.out[];
          d.newscan
          (# d: ^idl.declarator;
             first_done: @boolean;
          do (if first_done then
                 ', ' -> outfile.puttext;
              else
                 true->first_done
             if);
             (info[], current[]) -> do_declarator;
          #);
          ': '->outfile.puttext;
       #);
     info: ^declarator_info;
  enter (info[],t[])
  do t.getdeclarators -> do_declarators;
     (info[],t.gettype_spec) -> do_type_spec;
  #);

do_struct_type:
  (# t: ^idl.struct_type;
     info: ^declarator_info;
  enter (info[],t[])
  do 'do_struct_type NYI' -> outfile.comment;
  #);
do_enum_type:
  (# t: ^idl.enum_type;
     es: ^idl.enumerators;
     val: @integer;
     info: ^declarator_info;
     id, previous_id: ^text;
     initializer_met: @boolean;
  enter (info[],t[])
  do (* Define "type" pattern *)
     'IntegerObject(\# ' -> outfile.puttext;
     outfile.CommentBegin;
     'enum ' -> outfile.puttext;
     t.getidentifier -> IdentifierToText -> outfile.puttext;
     outfile.CommentEnd;
     '#\)' -> outfile.puttext;
     (* Process each enumerator *) 
     t.getenumerators -> es[];
     0->val;
     NONE -> id[];
     NONE -> previous_id[];
     false -> initializer_met;
     es.newscan
     (# v: ^idl.value;
     do ';' -> outfile.putline;
        outfile.indent;
        id[] -> previous_id[];
        current.getidentifier -> IdentifierToText -> id[] ->outfile.puttext;
        ': ' -> outfile.puttext;
        outfile.CommentBegin;
        info.id[] -> outfile.puttext;
        outfile.CommentEnd;
        outfile.PatternBegin;
        'exit ' -> outfile.puttext;
        (if current.getvalue_opt -> optionalPresent then
            (current.getvalue_opt -> v[]).getconst_exp -> do_const_exp;
            true -> initializer_met;
         else
            (if initializer_met then
                (* emit expression - let beta compiler do the work (:-) *)
                previous_id[] -> outfile.puttext;
                '+1' -> outfile.puttext;
             else
                (* emit value *)
                val -> outfile.putint;
                val+1 -> val;
            if);
        if);
        ' #)'->outfile.puttext;
     #);
  #);

do_constr_enum_type:
  (# t: ^idl.constr_enum_type;
     info: ^declarator_info;
  enter (info[],t[])
  do (info[],t.getenum_type) -> do_enum_type;
  #);


do_type_dcl_error:
  (# t: ^idl.type_dcl_error;
     info: ^declarator_info;
  enter (info[],t[])
  do outfile.indent;
     'do_type_dcl_error NYI' -> outfile.commentline;
  #);

--type_dcl_body: descriptor--
(# dcl_info: @declarator_info;          
do (if d.symbol
    // idl.typedef then
       (dcl_info[],d[]) -> do_typedef;
    // idl.struct_type then
       (dcl_info[],d[]) -> do_struct_type;
    // idl.enum_type then
       (dcl_info[],d[]) -> do_enum_type;
    // idl.type_dcl_error then
       (dcl_info[],d[]) -> do_type_dcl_error;
   if);
#)
