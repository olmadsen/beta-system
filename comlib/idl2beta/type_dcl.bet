ORIGIN 'translate';

--do_base_type_spec: descriptor--
(# name: ^text;
do (if s.symbol
    // idl.float then 'float' -> name[];
    // idl.double then 'double' -> name[]; 
    // idl.int then 'int' -> name[];
    // idl.signed_long_int then 'signed_long_int' -> name[];
    // idl.signed_short_int then 'signed_short_int' -> name[];
    // idl.unsigned_long_int then 'unsigned_long_int' -> name[];
    // idl.unsigned_short_int then 'unsigned_short_int' -> name[];
    // idl.unsigned_char then 'unsigned_char' -> name[];
    // idl.signed_char then 'signed_char' -> name[];
    // idl.char then 'char' -> name[];
    // idl.boolean_type then 'boolean' -> name[];
    // idl.octet_type then 'octet' -> name[]; 
    // idl.any_type then 'any' -> name[];
   if);
   (* The type should be in global_types *)
   name[] -> global_types.findtype(# do current[] -> dcl_info.type[] #);
#)

--type_dcl_lib: attributes--
do_type_spec:
  (# t: ^idl.type_spec;
     info: ^type_info;
     types: ^type_list;
     alias: ^text;
     found: @boolean;
  enter (info[],types[],t[])
  do (if t.symbol
      // idl.float then
         'real' -> info.representation[];
         'float' -> info.comment[];
      // idl.double then
         'real' -> info.representation[];
         'double' -> info.comment[];
      // idl.signed_long_int then
         'int32' -> info.representation[];
      // idl.int then
         'int32' -> info.representation[];
      // idl.signed_short_int then
         'int16' -> info.representation[];
      // idl.unsigned_long_int then
         'int32u' -> info.representation[];
      // idl.unsigned_short_int then
         'int16u' -> info.representation[];
      // idl.unsigned_char then
         'int8u' -> info.representation[];
      // idl.signed_char then
         'int8' -> info.representation[];
      // idl.char then
         'char' -> info.representation[];
      // idl.boolean_type then
         'boolean' -> info.representation[];
      // idl.octet_type then
         'int8u' -> info.representation[];
         'octet' -> info.comment[];
      // idl.any_type then
         'any_type' -> info.representation[];
         'NYI' -> info.comment[];
      // idl.sequence1 then
         'sequence1' -> info.representation[];
         'NYI' -> info.comment[];
      // idl.sequence2 then
         'sequence2' -> info.representation[];
         'NYI' -> info.comment[];
      // idl.string1 then
         'string1' -> info.representation[];
         'NYI' -> info.comment[];
      // idl.string2 then
         'string2' -> info.representation[];
         'NYI' -> info.comment[];
      // idl.scoped_name1
      // idl.scoped_name2
      // idl.scoped_name3 then
         (* We have an alias *)
         t[] -> do_scoped_name -> alias[];
         alias[] -> types.findtype
         (# 
         do current[] -> info.is_alias_for[];
            true -> found;
         #);
         (if not found then
             'Warning: ' -> puttext;
             alias[] -> puttext;
             ' not found in symbol tables!' -> putline;
         if);
      // idl.constr_struct_type then
         (* Both emit the definition of the construction mapping, and
          * add the type to types.
          *)
         info.name[] -> outfile.puttext; ': ' -> outfile.puttext;
         (t[]) -> do_constr_struct_type;
         'data' -> info.representation[];
      // idl.constr_union_type then
         'constr_union_type NYI' -> outfile.puttext;
      // idl.constr_enum_type then
         (* Both emit the definition of the enum mapping, and
          * add the type to types.
          *)
         info.name[] -> outfile.puttext; ': ' -> outfile.puttext;
         (t[]) -> do_constr_enum_type;
         'integer' -> info.representation[];
      // idl.void_type_spec then
         'void' -> info.representation[];
      else
         'Oops, must have forgotten a type!' -> outfile.comment;
     if);
  #);

do_type_declarator:
  (# t: ^idl.type_declarator;
     info: ^type_info;
     att: ^idl_attributes;
     dcl_info: ^declarator_info;
     d: ^idl.declarators;
     types: ^type_list;
  enter (types[], att[], t[])
  do t.getdeclarators -> d[];
     d.newscan
     (# d: ^idl.declarator;
     do &type_info[] -> info[] -> types.append;
        att[] -> info.attributes[];
        &declarator_info[] -> dcl_info[];
        info[] -> dcl_info.type[];
        (dcl_info[], current[]) -> do_declarator;
        dcl_info.name.copy -> info.name[];
        dcl_info.pointer_level -> info.pointer_level;
        (info[], types[], t.gettype_spec) -> do_type_spec;
     #);
  #);

do_typedef:
  (# t: ^idl.typedef;
     x: ^idl.attributeX;
     att: ^idl_attributes;
  enter (t[])
  do (if t.getattribute_opt -> optionalPresent then
         (&idl_attributes[]->att[], t.getattribute_opt)
           -> do_attributeX;
     if);
     (local_types[], att[], t.gettype_declarator) -> do_type_declarator;
  #);
  
do_struct_type:
  (# t: ^idl.struct_type;
     l: ^idl.member_list;
     do_members:
       (# m: ^idl.members
       enter m[]
       do m.newscan
          (# d: ^idl.declarators;
             mem: ^idl.member;
          do current[] -> mem[];
             mem.getdeclarators -> d[];
             d.newscan
             (# d: ^idl.declarator;
                dcl_info: ^declarator_info;
                info: ^type_info;
             do outfile.indent;
                &type_info[] -> info[];
                &declarator_info[] -> dcl_info[];
                (dcl_info[], current[]) -> do_declarator;
                dcl_info.name[] -> outfile.puttext;
                ': '->outfile.puttext;
                (if dcl_info.pointer_level>0 then
                    '^' -> outfile.put;
                 else
                    '@' -> outfile.put;
                if);
                (info[], local_types[], mem.gettype_spec) -> do_type_spec;
                (if info.pointer_level>1 then
                    'Holder' -> outfile.puttext;
                    info.name.copy -> holders.add;
                if);
                (if mem.getattribute_opt -> optionalPresent then
                    (&idl_attributes[]->info.attributes[],mem.getattribute_opt)
                      -> do_attributeX;
                if);
                ';' -> outfile.putline;
             #);
          #);
       #);
  enter (t[])
  do 'DATA' -> outfile.putline;
     2 -> outfile.indent.inc;
     outfile.indent;
     outfile.CommentBegin;
     'struct' -> outfile.puttext;
     (if t.getidentifierOpt -> optionalPresent then
         ' ' -> outfile.put;
         t.getidentifierOpt 
           -> IdentifierToText
           -> outfile.puttext;
     if);
     outfile.CommentEnd;
     outfile.newline;
     outfile.indent;
     outfile.PatternBegin;
     outfile.newline;
     3 -> outfile.indent.inc;
     t.getmember_list -> l[];
     l.getmembers -> do_members;
     3 -> outfile.indent.dec;
     outfile.indent;
     outfile.PatternEnd;
     2 -> outfile.indent.dec;
  #);
do_enum_type:
  (# t: ^idl.enum_type;
     es: ^idl.enumerators;
     val: @integer;
     id, previous_id: ^text;
     initializer_met: @boolean;
  enter (t[])
  do (* Define "type" pattern *)
     'IntegerObject(\# ' -> outfile.puttext;
     outfile.CommentBegin;
     'enum ' -> outfile.puttext;
     t.getidentifier -> IdentifierToText -> outfile.puttext;
     outfile.CommentEnd;
     outfile.PatternEnd;
     (* Process each enumerator *) 
     t.getenumerators -> es[];
     0->val;
     NONE -> id[];
     NONE -> previous_id[];
     false -> initializer_met;
     es.newscan
     (# v: ^idl.value;
     do ';' -> outfile.putline;
        outfile.indent;
        id[] -> previous_id[];
        current.getidentifier -> IdentifierToText -> id[] ->outfile.puttext;
        ': ' -> outfile.puttext;
        outfile.CommentBegin;
        (* FIXME: info.name[] -> outfile.puttext; *)
        outfile.CommentEnd;
        outfile.PatternBegin;
        'exit ' -> outfile.puttext;
        (if current.getvalue_opt -> optionalPresent then
            (current.getvalue_opt -> v[]).getconst_exp -> do_const_exp;
            true -> initializer_met;
         else
            (if initializer_met then
                (* emit expression - let beta compiler do the work (:-) *)
                previous_id[] -> outfile.puttext;
                '+1' -> outfile.puttext;
             else
                (* emit value *)
                val -> outfile.putint;
                val+1 -> val;
            if);
        if);
        outfile.PatternEnd;
     #);
  #);

do_union_type:
  (# t: ^idl.union_type;
  enter (t[])
  do 'do_union_type NYI' -> outfile.commentline;
  #);

do_constr_enum_type:
  (# t: ^idl.constr_enum_type;
  enter (t[])
  do (t.getenum_type) -> do_enum_type;
  #);

do_constr_struct_type:
  (# t: ^idl.constr_struct_type;
  enter (t[])
  do (t.getstruct_type) -> do_struct_type;
  #);


do_type_dcl_error:
  (# t: ^idl.type_dcl_error;
  enter t[]
  do outfile.indent;
     'do_type_dcl_error NYI' -> outfile.commentline;
  #);

--type_dcl_body: descriptor--
(#          
do (if d.symbol
    // idl.typedef then
       (d[]) -> do_typedef;
    // idl.union_type then
       (d[]) -> do_union_type;
    // idl.struct_type then
       (d[]) -> do_struct_type;
    // idl.enum_type then
       (d[]) -> do_enum_type;
    // idl.type_dcl_error then
       d[] -> do_type_dcl_error;
   if);
#)
