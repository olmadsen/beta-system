ORIGIN 'translate';

--Preprocess:descriptor--
(# ifilename: ^text;
   cfilename: ^text;
   system: external
     (# cmd: [0]@char;
     enter cmd
     #);
   cmd: ^text;
   report:
     (# cmd: ^text
     enter cmd[]
     do (if not quiet then
            '    [' -> screen.puttext; 
            cmd[] -> screen.puttext; 
            ']'->screen.putline;
        if);
     #);
do idlfilename.copy -> ifilename[];
   (ifilename[], '') -> ast.thePathhandler.localpath -> ifilename[];
   '.idl' -> ifilename.stripExtension;
   ifilename.copy -> cfilename[];
   '.i' -> ifilename.append;
   '.c' -> cfilename.append;
   (* FIXME: should use process!!! *)
   (* FIXME: should be able to set defines from bidl command line *)
   
   'cp '->cmd[];
   (idlfilename[],'') -> ast.thePathhandler.localpath -> cmd.append;
   ' ' -> cmd.append;
   cfilename[] -> cmd.append;
   cmd[] -> report;
   cmd -> system;
   
   '/usr/local/bin/gcc -E -P ' -> cmd[];
   defines.scan
   (# 
   do '-D ' -> cmd.append;
      current[] -> cmd.append;
      ' ' -> cmd.append;
   #);
   '-o ' -> cmd.append;
   ifilename[] -> cmd.append;
   ' ' -> cmd.append;
   cfilename[] -> cmd.append;
   cmd[] -> report;
   cmd -> system;
   
   (if not preserve then
       'rm '->cmd[];
       cfilename[] -> cmd.append;
       cmd[] -> report;
       cmd -> system;
   if);
#)
