ORIGIN 'translate';

--TranslateLib: attributes--
report:
  (# cmd: ^text
  enter cmd[]
  do (if not quiet then
         '    [' -> screen.puttext; 
         cmd[] -> screen.puttext; 
         ']'->screen.putline;
     if);
  #);
system: external
  (# cmd: [0]@char;
  enter cmd
  #);
--Preprocess:descriptor--
(# ifilename: ^text;
   cfilename: ^text;
   cmd: ^text;
do idlfilename.copy -> ifilename[];
   (ifilename[], '') -> ast.thePathhandler.localpath -> ifilename[];
   '.idl' -> ifilename.stripExtension;
   ifilename.copy -> cfilename[];
   '.i' -> ifilename.append;
   '.c' -> cfilename.append;
   (* FIXME: should use process!!! *)
   
   'cp '->cmd[];
   (idlfilename[],'') -> ast.thePathhandler.localpath -> cmd.append;
   ' ' -> cmd.append;
   cfilename[] -> cmd.append;
   cmd[] -> report;
   cmd -> system;
   
   '/usr/local/bin/gcc -E -P ' -> cmd[];
   defines.scan
   (# 
   do '-D ' -> cmd.append;
      current[] -> cmd.append;
      ' ' -> cmd.append;
   #);
   '-o ' -> cmd.append;
   ifilename[] -> cmd.append;
   ' ' -> cmd.append;
   cfilename[] -> cmd.append;
   cmd[] -> report;
   cmd -> system;
   
   (if not preserve then
       'rm -f '->cmd[];
       cfilename[] -> cmd.append;
       cmd[] -> report;
       cmd -> system;
   if);
#)

--PostProcess:descriptor--
(# ifilename, cmd: ^text;
do (if not preserve then
       idlfilename.copy -> ifilename[];
       (ifilename[], '') -> ast.thePathhandler.localpath -> ifilename[];
       '.idl' -> ifilename.stripExtension;
       '.i' -> ifilename.append;
       'rm -f ' -> cmd[];
       ifilename[] -> cmd.append;
       cmd[] -> report;
       cmd -> system;
   if);
#)
