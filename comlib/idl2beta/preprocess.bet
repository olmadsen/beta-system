ORIGIN 'translate';
INCLUDE '~beta/basiclib/v1.6/file';

--TranslateLib: attributes--

report:
  (# cmd: ^text
  enter cmd[]
  do (if not quiet then
         '    [' -> screen.puttext; 
         cmd[] -> screen.puttext; 
         ']'->screen.putline;
     if);
  #);
system: external
  (# cmd: [0]@char;
  enter cmd
  #);
--PreprocessBody:descriptor--
(# cmd: ^text;
do (* FIXME: should use process!!! *)
   
   'cp '->cmd[];
   (idlfilename[],'') -> ast.thePathhandler.localpath -> cmd.append;
   ' ' -> cmd.append;
   (cfilename[],'') -> ast.thePathhandler.localpath -> cmd.append;
   cmd[] -> report;
   cmd -> system;
   
   '/usr/local/bin/gcc -E -P ' -> cmd[];
   defines.scan
   (# 
   do '-D ' -> cmd.append;
      current[] -> cmd.append;
      ' ' -> cmd.append;
   #);
   '-o ' -> cmd.append;
   (ifilename[],'') -> ast.thePathhandler.localpath -> cmd.append;
   ' ' -> cmd.append;
   cfilename[] -> cmd.append;
   cmd[] -> report;
   cmd -> system;
   
   (if not preserve then
       'rm -f '->cmd[];
       cfilename[] -> cmd.append;
       cmd[] -> report;
       cmd -> system;
   if);
#)

--PostProcess:descriptor--
(# cmd: ^text;
   e: @diskentry;
do (if not preserve then
       'rm -f ' -> cmd[];
       ifilename[] -> cmd.append;
       cmd[] -> report;
       cmd -> system;
   if);
   
   (* Emit imports *)
   imports.scan
   (# lib: ^text
   do current.copy -> lib[];
      '.IDL' -> lib.stripExtension;
      '.idl' -> lib.stripExtension;
      (if true 
       // 'unknwn' -> lib.equal then 
          '--INCLUDE \'~beta/comlib/unknwn\'--' -> outfile.putline;
       else
          '--INCLUDE \'' -> outfile.puttext;
          (* add path to the predefined interfaces *)
          lib[] -> outfile.puttext;
          '\'--' -> outfile.putline;
          'Add BUILD to top of file for autogeneration of beta file'
            -> outfile.commentline;
          outfile.CommentBegin;
          'BUILD default \'' -> outfile.puttext;
          lib[] -> outfile.puttext;
          '.bet\' \'' -> outfile.puttext;
          lib[] -> outfile.puttext;
          idlfilename[] -> e.path;
          e.path.name.suffix -> outfile.puttext;
          '\' \'bidl $1\';' -> outfile.puttext;
          outfile.CommentEnd;
      if);
      
   #);
#)

