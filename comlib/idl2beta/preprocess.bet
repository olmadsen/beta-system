ORIGIN 'translate';
INCLUDE '~beta/basiclib/file';
MDBODY nti 'private/preprocess_ntibody'
default 'private/preprocess_unixbody';

--TranslateLib: attributes--

report:
  (# cmd: ^text
  enter cmd[]
  do (if not quiet then
         '    [' -> screen.puttext; 
         cmd[] -> screen.puttext; 
         ']'->screen.putline;
     if);
  #);
system: external
  (# cmd: [0]@char;
  enter cmd
  #);

--PostProcess:descriptor--
(# cmd: ^text;
   e: @diskentry;
   remaining: ^text;
   exists: @boolean;
   last_include: ^text;
   oldfilename, newfilename: ^text;
do <<SLOT DeleleIFile: descriptor>>;
   IdlFileName.copy -> e.path;
   (* Emit imports *)
   imports.scan
   (# lib: ^text
   do current.copy -> lib[];
      '.idl' -> lib.stripExtension;
      (* add path to the predefined interfaces *)
      (if true 
       // 'unknwn' -> lib.equal then 
          '--INCLUDE \'~beta/comlib/unknwn\'--' -> outfile.putline;
       else
          HeadOfIdlFileName.copy -> remaining[];
          current[] -> remaining.append;
          remaining[] -> e.path;
          e.exists -> exists;
          (if chain then
              (if exists then lib.copy -> last_include[]; if);
           else
              (if not exists then outfile.commentbegin if);
              '--INCLUDE \'' -> outfile.puttext;
              lib[] -> outfile.puttext;
              '\'' -> outfile.put;
              (if not exists then outfile.commentend if);
              outfile.newline;
          if);
          
          (* Add imported library to list of files remaining,
           * disregarding if it exists or not. The user still needs
           * to get a warning if it does not exists. This is done
           * elsewhere.
           *)
          remaining[] -> files_remaining.add;
      if);
   #);
   (if chain and (last_include[]<>NONE) then
       (* We are chaining ORIGINS and IDL file has imports.
        * So we need to rename to an environment and create extra file
        * which ties it all up:
        *   foo_env.bet
        *           |___ foo1.bet
        *           |___ foo2.bet
        *           |___ ...
        *           |___ fooN.bet
        *                 |
        *   foo.bet_______|
        *)
       outfile.close;
       (if IdlFileName[]<>NONE then 
           '.idl' -> IdlFileName.stripextension;
           '_env.idl' -> IdlFileName.append;
       if);
       outfile.name -> newfilename[];
       newfilename.copy -> oldfilename[];
       '.bet' -> newfilename.stripExtension;
       '_env.bet' -> newfilename.append;
       (if not quiet then
           '    [rename ' -> screen.puttext;
           outfile.name -> screen.puttext;
           ' ' -> screen.put;
           newfilename[] -> screen.puttext;
           ']' -> screen.putline;
       if);
       newfilename[] -> outfile.entry.rename;
       outfilename[] -> outfile.name;
       (if not quiet then
           '    [reopen ' -> screen.puttext;
           outfile.name -> screen.puttext;
           ']' -> screen.putline;
       if);
       outfile.openWrite;
       'ORIGIN \'' -> outfile.puttext;
       last_include[] -> outfile.puttext;
       '\';' -> outfile.putline;
   if);
#)

