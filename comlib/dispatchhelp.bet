ORIGIN 'OAIdl';
INCLUDE '~beta/basiclib/external';
INCLUDE '~beta/sysutils/wstring';
INCLUDE '~beta/win32lib/winnt';
INCLUDE '~beta/comlib/comlib';
INCLUDE '~beta/comlib/oleauto';
INCLUDE '~beta/containers/list';
BODY 'private/dispatchhelpbody';

--LIB: attributes--
TextList: list
  (# element::<Text #);
Int16List: list
  (# element::<IntegerObject;
     add:
       (# v: @int16;
          i: ^IntegerObject;
       enter v
       do &IntegerObject[] -> i[];
          v->i;
          i[] -> append;
       #);
  #);

DispatchParams: 
  (# private: @<<SLOT DispatchParamsPrivate: Descriptor>>;
     theinteger: @integer;
     release: <<SLOT DispatchParamsRelease: Descriptor>>;
     
     check: 
       (# name: ^text;
          add: @integer;
       enter (name[])
       <<SLOT DispatchParamsCheck: dopart>>
       #);
     put: (*check<<SLOT DispatchParamsPut: MainPart>>;*)
       (# name: ^text
       enter name[]
       do name[] -> check(# do <<SLOT DispatchParamsPut: Descriptor>> #);
          (if false then INNER if);
       #);
     putDispatch: put
       (# value: ^IDispatch;
       enter value[]
       <<SLOT DispatchParamsPutDispatch: dopart>>
       #);
     putInt16: put
       (# value: @int16
       enter (value)
       <<SLOT DispatchParamsPutInt16: dopart>>
       #);
     putBoolean: put
       (# value: @boolean
       enter (value)
       <<SLOT DispatchParamsPutBoolean: dopart>>
       #);
     putReal: put
       (# value: @real
       enter (value)
       <<SLOT DispatchParamsPutReal: dopart>>
       #);
     putString: put
       (# value: ^text; 
          wstr: @wstring;
          bstr: @integer;
       enter value[]
       <<SLOT DispatchParamsPutString: dopart>>
       #);
     putSafeArray: put
       (* Put array with cDims dimensions, each with same number of
        * elements 
        *)
       (# listtype:<List;
          Dimensions: [0]@integer; (* Number of elements in each dimension *)
          R: ^listtype;
          private: @<<SLOT DispatchParamsPutSafeArrayPrivate: descriptor>>;
       enter (Dimensions, R[])
       <<SLOT DispatchParamsPutSafeArray: dopart>>
       #);
     putStringArray: putSafeArray
       (# listType::TextList;
       <<SLOT DispatchParamsPutStringArray: dopart>>
       #);
     putInt16Array: putSafeArray
       (# listType::Int16List;
       <<SLOT DispatchParamsPutInt16Array: dopart>>
       #);
  #);

InvokeByName:
  (# pdisp: ^IDispatch;
     member: ^text;
     params: ^DispatchParams;
     dParms: @DISPPARAMS;
     wAction: @integer;
     vReturn: ^VARIANT;
     status: @boolean;
  enter (pdisp[], member[], params[], wAction, vReturn[])
  <<SLOT InvokeByName: dopart>>
  exit status
  #);

PropertyPutByName:
  (# pdisp: ^IDispatch;
     member: ^text;
     dparms: ^DispatchParams;
     status: @boolean;
  enter (pdisp[], member[], dparms[])
  <<SLOT PropertyPutByName: dopart>>
  exit status
  #);

PropertyGetByName:
  (# pdisp: ^IDispatch;
     member: ^text;
     dparms: ^DispatchParams;
     vResult: ^VARIANT;
     status: @boolean;
  enter (pdisp[], member[], dparms[], vResult[])
  <<SLOT PropertyGetByName: dopart>>
  exit status
  #);

