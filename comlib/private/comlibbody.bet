ORIGIN '../comlib';
BUILD nti '$$/errormessage.obj' 'external/errormessage.cpp' 'betacc $0 $1' 
      default '$$/com_unix.o' 'external/com_unix.c' '$CC -c -o $0 $1'; 
BUILD nti     '$$/betainvoke.obj'  'external/betainvoke.cpp' 'betacc $0 $1'
      default '$$/betainvoke.o' 'external/betainvoke.c' '$CC -c -o $0 $1'; 
--CLSIDFromProgID: descriptor--
(# hr: @HRESULT;
   CLSIDFromProgID: external
     (# lpszProgID, hr: @Int32;
        rclsid: ^GUIDdata;
     enter (lpszProgID, rclsid[])
     do callStd
     exit hr
     #);
do &CLSID[]->theclsid[]; theclsid.init;
   (progid.wcharptr, theclsid.Binary[]) -> CLSIDFromProgID -> hr;
   (if hr.failed then
       ('CLSIDFromProgID failed', hr.value) -> ComErrorMessage;
       NONE->theclsid[];
   if);
#)

--CoCreateInstance: descriptor--
(# hr: @HRESULT; ih: @IUnknownHolder;
   CoCreateInstance: external
     (# pUnkOuter: ^IUnknown;
        rclsid, riid: ^GUIDdata;
        ppv: ^IUnknownHolder;
        dwClsContext, hr: @Int32;        
     enter (rclsid[], pUnkOuter[], dwClsContext, riid[], ppv[])
     do 'CoCreateInstance' -> callStd
     exit hr
     #);
do (theclsid.binary[], outer[], Flags, theiid.binary[], ih[]) -> CoCreateInstance -> hr;
   (if hr.failed then
       ('CoCreateInstance failed', hr.value) -> ComErrorMessage;
       NONE->Iptr[];
    else
       ih->Iptr[];
   if);
#)

--IUnknownQuery: descriptor--
(# ih: @IUnknownHolder;
do (if ((theIID.binary[], ih[]) -> theUnknown.QueryInterface -> hr).failed then
       ('IUnknownQuery failed', hr.value) -> ComErrorMessage;
       NONE->Iptr[];
    else
       (*'IUnknownQuery: succes' -> screen.putLine;*)
       ih->Iptr[];
   if);
#)
  
-- RegisterActiveObject: descriptor --
(# RegisterActiveObject: external
     (# punk: ^IUnknown; (* IUnknown FAR* *)
        rclsid: ^GUIDdata; (* REFCLSID *)
        dwFlags: @Int32; (* DWORD *)
        pdwRegister: ^int32uHolder; (* unsigned long FAR* *)
        result: @Int32;
     enter (punk[], rclsid[], dwFlags, pdwRegister[])
     do 'RegisterActiveObject' -> callStd;
     exit result
     #);
   tempReg: @integer;
do (punk[], rclsid.binary[], dwFlags, pdwRegister[]) ->
   RegisterActiveObject -> result;
#)
