ORIGIN '../comlib';
INCLUDE '~beta/basiclib/v1.6/formatio';
--- GUIDSetFromTextDP: dopart ---
do 1->p;
   (for i:16 repeat
        (if T.T[p]='-' then p+1->p if);
        ((T.T[p]->hextoint)*16)+(T.T[p+1]->hextoint)->Binary[i];
        p+2->p;
   for);
   binary[1].%byteswaplong;
   binary[5].%byteswapshort;
   binary[7].%byteswapshort;
   (if traceGUID then 
       'GUID.setfromtext: Parsed'->putline;
       T[] -> putline; 'into the following:' -> putline;
       getAsText -> putLine;
   if);
   
--- GUIDGetAsTextDP: dopart ---
do &Text[]->T[]; 
   '%08.08x-%04x-%04x-%02x%02x%02x%02x%02x%02x%02x%02x' -> 
   T.putFormat
   (# 
   do %getLongAt @@binary[1] -> x;
      %getShortAt @@binary[5] -> x;
      %getShortAt @@binary[7] -> x;
      (for i:16-8 repeat
           binary[i+8] -> x
      for)
   #);          
   
--- GUIDsetFromTextLib: attributes ---
hextoint: 
  (# c: @Char;
  enter c
  do (if c>'Z' then c-32->c (* Upcase *) if);
     c-'0'->c (* normalize to 0-9 *);
     (if c>10 then c-7->c (* normalize to 10-15 *) if);
     (if (c<0) or (c>15) then
         'hextoint error'->putLine;
     if);
  exit c
  #);
