ORIGIN '../comlib';
BUILD nti '$$/errormessage.obj' 'external/errormessage.cpp' 'betacc $0 $1'; 

--CLSIDFromProgID: descriptor--
(# hr: @HRESULT;
   CLSIDFromProgID: external
     (# lpszProgID, rclsid, hr: @Int32;
     enter (lpszProgID, rclsid)
     do callStd
     exit hr
     #);
do &CLSID[] -> theclsid[];
   (progid.wcharptr, @@theclsid.Binary) -> CLSIDFromProgID -> hr;
   (if hr.failed then
       ('CLSIDFromProgID failed', hr.value) -> ComErrorMessage;
       NONE->theclsid[];
   if);
#)

--CoCreateInstance: descriptor--
(# hr: @HRESULT;
   CoCreateInstance: external
     (# pUnkOuter: ^IUnknown;
        rclsid, dwClsContext, riid, ppv, hr: @Int32;
     enter (rclsid, pUnkOuter[], dwClsContext, riid, ppv)
     do callStd
     exit hr
     #);
do (@@theclsid.Binary, outer[], Flags, @@theiid.Binary, @@Iptr)
     -> CoCreateInstance->hr;
   (if hr.failed then
       ('CoCreateInstance failed', hr.value) -> ComErrorMessage;
       NONE->Iptr[];
   if);
#)

--IUnknownQuery: dopart--
do (if ((@@theIID.Binary, @@Iptr)->QueryInterface->hr).failed then
       (* ('IUnknownQuery failed', hr.value) -> ComErrorMessage; *)
       NONE->Iptr[];
   if);
  
