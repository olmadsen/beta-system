ORIGIN '../comlib';
BUILD nti '$$/errormessage.obj' 'external/errormessage.cpp' 'betacc $0 $1' 
      default '$$/com_unix.o' 'external/com_unix.c' '$CC -c -o $0 $1'; 

--CLSIDFromProgID: descriptor--
(# hr: @HRESULT;
   CLSIDFromProgID: external
     (# lpszProgID, hr: @Int32;
        rclsid: ^GUIDdata;
     enter (lpszProgID, rclsid[])
     do callStd
     exit hr
     #);
do &CLSID[]->theclsid[]; theclsid.init;
   (progid.wcharptr, theclsid.Binary[]) -> CLSIDFromProgID -> hr;
   (if hr.failed then
       ('CLSIDFromProgID failed', hr.value) -> ComErrorMessage;
       NONE->theclsid[];
   if);
#)

--CoCreateInstance: descriptor--
(# hr: @HRESULT; ih: @IUnknownHolder;
   CoCreateInstance: external
     (# pUnkOuter: ^IUnknown;
        rclsid, riid: ^GUID;
        ppv: ^IUnknownHolder;
        dwClsContext, hr: @Int32;        
     enter (rclsid[], pUnkOuter[], dwClsContext, riid[], ppv[])
     do callStd
     exit hr
     #);
do (theclsid[], outer[], Flags, theiid[], ih[])
     -> CoCreateInstance -> hr;
   (if hr.failed then
       ('CoCreateInstance failed', hr.value) -> ComErrorMessage;
       NONE->Iptr[];
    else
       ih->Iptr[];
   if);
#)

--IUnknownQuery: descriptor--
(# ih: @IUnknownHolder;
do (theIID[], ih[]) -> QueryInterface -> hr;
   (if hr.failed then
       ('IUnknownQuery failed', hr.value) -> ComErrorMessage;
       NONE->Iptr[];
    else
       ih->Iptr[];
   if);
#)
  
