ORIGIN '../comtypes';
INCLUDE '~beta/basiclib/formatio';
   
--- GUIDsetFromTextLib: attributes ---
traceGUID: (# exit false #);
(* FIXME: there are standard COM functions we could call.*)

hextoint: 
  (# c: @Char;
  enter c
  do (if c>'Z' then c-32->c (* Upcase *) if);
     c-'0'->c (* normalize to 0-9 *);
     (if c>10 then c-7->c (* normalize to 10-15 *) if);
     (if (c<0) or (c>15) then
         'hextoint error'->putLine;
     if);
  exit c
  #);

--- GUIDSetFromText: descriptor ---
(# v: @integer;
do 1->p;
   (for i:16 repeat
        (if T.T[p]='-' then p+1->p if);
        ((T.T[p]->hextoint)*16)+(T.T[p+1]->hextoint) -> v;
        (v) %putByteAt ((@@Binary)+i-1);
        p+2->p;
   for);
   binary.data1.%byteswaplong;
   binary.data2.%byteswapshort;
   binary.data3.%byteswapshort;
   (if traceGUID then 
       'GUID.setfromtext: Parsed'->putline;
       T[] -> putline; 'into the following:' -> putline;
       getAsText -> putLine;
   if);
#)

--- GUIDGetAsText: descriptor ---
(# 
do &Text[]->T[]; 
   '%08.08x-%04x-%04x-%02x%02x%02x%02x%02x%02x%02x%02x' -> 
   T.putFormat
   (# 
   do binary.data1 -> x;
      binary.data2 -> x;
      binary.data3 -> x;
      (for i:16-8 repeat
           (%getByteAt ((@@binary.data4_0)+i-1)) -> x
      for)
   #); 
#)
