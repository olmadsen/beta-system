ORIGIN '../holders';

--LIB: attributes--

stralloc: External
  (# size: @integer;
     ptr: @integer;
  enter size
     do 'malloc' -> callC
  exit ptr
  #);
strfree: External
  (# ptr: @integer
  enter ptr
  do 'free' -> callC;
  #);
strlen: External
  (# charptr: @integer;
     result: @integer;
  enter charptr
  exit result
  #);

--TextHolderAllocate: descriptor--
(# 
do (if length=0 then 256 -> stralloc -> charptr
    else length -> stralloc -> charptr
   if)
#)

--TextHolderSet: descriptor--
(# addr:@integer; 
do (if t[]=NONE then 
       (failuretrace, 'Enter parameter to TextHolder.set is NONE')->stop;
   if);
   t.length+1 -> allocate;
   charptr -> addr;
   t.scanall(# do (ch) %putByteAt (addr); addr + 1 -> addr #);
   (* NULL termination *)
   0 %putByteAt (addr);
#)

--TextHolderGet: descriptor--
(# len: @integer;
do &text[] -> t[];
   (if charptr<>0 then 
       length -> len;
       len-t.T.range -> t.extend;
       (for i:len repeat 
            %getByteAt(charptr + i-1) -> t.T[i];
       for);
       len -> t.lgth -> t.pos;
   if);
#)

--TextHolderGetChars: descriptor--
(# len: @integer;
do (if charptr<>0 then 
       length -> len;
       len -> R.extend;
       (for i:len repeat 
            %getByteAt (charptr + i-1) -> R[i];
       for);
   if)
#)

--TextHolderLength: dopart--
do (if charptr<>0 then charptr->strlen->value if) 

--TextHolderInxPut: dopart--
do (ch) %putByteAt (charptr+inx); 

--TextHolderInxGet: dopart--
do %getByteAt (charptr + inx) -> ch 

--TextHolderFree: dopart--
do charptr -> strfree
