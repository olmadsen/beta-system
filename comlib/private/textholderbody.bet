ORIGIN '../holders';

--LIB: attributes--

stralloc: External
  (# size: @integer;
     ptr: @integer;
  enter size
     do 'malloc' -> callC
  exit ptr
  #);
strfree: External
  (# ptr: @integer
  enter ptr
  do 'free' -> callC;
  #);
strlen: External
  (# charptr: @integer;
     result: @integer;
  enter charptr
  exit result
  #);

--TextHolderAllocate: descriptor--
(# 
do (if length=0 then 256 -> stralloc -> charptr
    else length -> stralloc -> charptr
   if)
#)

--TextHolderSet: descriptor--
(# addr:@integer; 
do (if t[]=NONE then 
       (failuretrace, 'Enter parameter to C string is NONE')->stop;
   if);
   t.length+1 -> allocate;
   charptr -> addr;
   t.scanall(# do (addr,ch) -> TOS'%putByte[0]'; addr + 1 -> addr #);
   (addr,0) -> tos'%putByte[0]';
#)

--TextHolderGet: descriptor--
(# len: @integer;
do &text[] -> t[];
   (if charptr<>0 then 
       length -> len;
       len-t.T.range -> t.extend;
       (for i:len repeat 
            charptr + i-1 -> TOS'%adrGetByte' -> t.T[i];
       for);
       len -> t.lgth -> t.pos;
   if);
#)

--TextHolderGetChars: descriptor--
(# len: @integer;
do (if charptr<>0 then 
       length -> len;
       len -> R.extend;
       (for i:len repeat 
            charptr + i-1 -> TOS'%adrGetByte' -> R[i];
       for);
   if)
#)

--TextHolderLength: dopart--
do (if charptr<>0 then charptr->strlen->value if) 

--TextHolderInxPut: dopart--
do (charptr+inx,ch) -> TOS'%putByte[0]' 

--TextHolderInxGet: dopart--
do charptr + inx -> TOS'%adrGetByte' -> ch 

--TextHolderFree: dopart--
do charptr -> strfree
