ORIGIN '../holders';

--LIB: attributes--

wstralloc: External
  (# size: @integer;
     ptr: @integer;
  enter size
     do 'malloc' -> callC
  exit ptr
  #);
wstrfree: External
  (# ptr: @integer
  enter ptr
  do 'free' -> callC;
  #);

--wTextHolderAllocate: descriptor--
(# 
do (if length=0 then 256->length if);
   (if BSTRoff<>0 then
       (BSTRoff+2*length+1 -> wstralloc) + BSTRoff -> wcharptr;
       (* Initialize length *)
       0 %putLongAt (wcharptr-BSTRoff);
    else
       2*length+1 -> wstralloc -> wcharptr;
   if);
   (* NULL terminate *)
   0 %putByteAt (wcharptr+length+1);
#)

--wTextHolderFree: dopart--
do wcharptr-BSTRoff -> wstrfree

--wTextHolderSet: descriptor--
(# addr:@integer; 
do (if T[]=NONE then 
       (failuretrace, 'Enter parameter to wTextHolder.set is NONE')->stop;
   if);
   T.length -> allocate;
   wcharptr -> addr;
   T.scanall(# do (ch) %putShortAt (addr); addr + 2 -> addr #);
   (* NULL termination *)
   0 %putByteAt (addr);
   (if BSTRoff<>0 then 
       (* set BSTR length *)
       T.lgth %putLongAt (wcharptr-BSTRoff);
   if);
#)

--wTextHolderGet: descriptor--
(# len: @integer;
do &wText[] -> t[];
   (if wcharptr<>0 then 
       length -> len;
       len-t.T.range -> t.extend;
       (for i:len repeat 
            %getShortAt(wcharptr + 2*(i-1)) -> t.T[i];
       for);
       len -> t.lgth -> t.pos;
   if);
#)
--wTextHolderSetText: descriptor--
(# addr:@integer; 
do (if T[]=NONE then 
       (failuretrace, 'Enter parameter to wTextHolder.set is NONE')->stop;
   if);
   T.length -> allocate;
   wcharptr -> addr;
   T.scanall(# do (ch) %putShortAt (addr); addr + 2 -> addr #);
   (* NULL termination *)
   0 %putByteAt (addr);
   (if BSTRoff<>0 then 
       (* set BSTR length *)
       T.lgth %putLongAt (wcharptr-BSTRoff);
   if);
#)


--wTextHolderGetText: descriptor--
(# len: @integer;
do &Text[] -> t[];
   (if wcharptr<>0 then 
       length -> len;
       len-t.T.range -> t.extend;
       (for i:len repeat 
            %getByteAt(wcharptr + 2*(i-1)) -> t.T[i];
       for);
       len -> t.lgth -> t.pos;
   if);
#)

--wTextHolderGetChars: descriptor--
(# len: @integer;
do (if wcharptr<>0 then 
       length -> len;
       len -> R.extend;
       (for i:len repeat 
            %getShortAt(wcharptr + 2*(i-1)) -> R[i];
       for);
   if)
#)

--wTextHolderLength: dopart--
do (if BSTRoff<>0 then
       (* The length field is number of bytes excluding last NULL termination *)
       (%getLongAt (wcharptr-BSTRoff)) div 2 -> value;
    else
       (* Search for NULL termination *)
       0 -> value;
       loop:
         (# 
         do (if %getShortAt(wcharptr + 2*value)<>0 then
                value+1 -> value;
                restart loop;
            if);
         #)
   if);

--wTextHolderInxPut: dopart--
do (ch) %putShortAt (wcharptr+2*inx);

--wTextHolderInxGet: dopart--
do %getShortAt(wcharptr + 2*inx) -> ch 
