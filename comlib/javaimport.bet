ORIGIN 'comlib';

---lib:attributes---
(* 
 * Beta Interface to simple component implemented in MS-Java.
 *)

ICalc_CLSID: 
  (# myID: ^CLSID;
  do &CLSID[]->myID[];
     '8f4e859c-761b-11d1-a2dd-00c04fb954fb' -> myID.setFromText;
  exit myID[]
  #);
        
ICalc: IDispatch
  (# 
     (* public int add(int a, int b)
      * 	{
      * 		return a+b;
      * 	}
      * 
      * and likewise for sub,mul,div in that order.
      *)
     F_add:<
       (# a,b,c_ptr,hresult: @Integer;
       enter (a,b,c_ptr)
       exit hresult
       #);
     F_sub:<
       (# a,b,c_ptr,hresult: @Integer;
       enter (a,b,c_ptr)
       exit hresult
       #);
     F_mul:<
       (# a,b,c_ptr,hresult: @Integer;
       enter (a,b,c_ptr)
       exit hresult
       #);
     F_div:<
       (# a,b,c_ptr,hresult: @Integer;
       enter (a,b,c_ptr)
       exit hresult
       #);
  #);
--PROGRAM: descriptor--
(# CLSID_Calculator: ^CLSID;
   IID_ICalc: @IID;
   pICalc: ^ICalc;
   i: @Integer;
   hres: @HRESULT;
   
do 0->CoInitialize;
   
(*   '2F9FBD42-64AE-11D1-B039-0020AF3BC782' -> IID_ICalc;
 * 'be0975f0-bbdd-11cf-97df-00aa001f73c1' -> IID_ICalc;
 * '009541a4-3b81-101c-92f3-040224009c02' -> IID_ICalc;
 *)
   'f4e859d7-861b-11d1-a2dd-00c04fb954fb' -> IID_ICalc;
   ICalc_CLSID -> CLSID_Calculator[];
   
   IID_ICalc.getAsText->putLine;
   CLSID_Calculator.getAsText->putLine;
   
   'Create component and get interface ICalc'->putLine;
   
   (CLSID_Calculator[], NONE, CLSCTX_INPROC_SERVER, IID_ICalc[])
     -> CoCreateInstance->pICalc[];
   
   'CoCreateInstance done'->putLine;
   (if pICalc[]<>NONE then
       'Succeeded creating component.'->putLine;
       '(17,4)->pICalc.F_add '->putText;
       (# c:@char; do (17,4,@@i)->pICalc.F_add->hres; #);
       (if hres.succeeded then
           '= '->putText; i->putInt;newLine;
        else
           'FAILED' -> putLine;
       if);
       '(17,4)->pICalc.F_sub '->putText;
       (# c:@char; do (17,4,@@i)->pICalc.F_sub->hres; #);
       (if hres.succeeded then
           '= '->putText; i->putInt;newLine;
        else
           'FAILED' -> putLine;
       if);
       
       'Trying to release interface ICalc'->putline;
       (#c:@char;  do pICalc.Release; #);
       'Succeeded releasing interface ICalc'->putline;
    else
       'Could not create component'->putline;
   if);	
   'CoUninitialize'->putline;
   CoUninitialize;
   'CoUninitialize done'->putline;
#)
