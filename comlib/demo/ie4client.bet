ORIGIN '~beta/comlib/comlib';
INCLUDE '~beta/comlib/ExDisp';
INCLUDE '~beta/comlib/MsHTML';

(* Small program showing how to use the WebBrowser2 interface
 * in ExDisp to start Internet Explorer and navigate it to a certain page.
 *)

--PROGRAM: descriptor--
(# CLSID_IE: ^CLSID;
   clsstr: @BSTR;
   IID_Web: @IID;
   pIWebBrowser2: ^IWebBrowser2;
   str: @BSTR;
   url: @VARIANT;
   empty: @VARIANT;
   hr: @HRESULT;
   ppDisp: @IDispatchHolder;
   pHTML: ^IHTMLDocument2;
   bstrh: @BSTRHolder;
   IIDHTML: @IID;
   pBool: @int16Holder;
   
   ie_progid: (# exit 'InternetExplorer.Application.1' #);
   
do 0->CoInitialize;
   
   ie_progid -> clsstr.setText;
   clsstr[] -> CLSIDFromProgID -> CLSID_IE[];
   IWebBrowser2_IID -> IID_Web; 
   
   'Create component and get interface IWebBrowser2'->putLine;
   
   (CLSID_IE[], NONE, CLSCTX_LOCAL_SERVER, IID_Web[])
     -> CoCreateInstance->pIWebBrowser2[];
   
   'CoCreateInstance done'->putLine;

   (if pIWebBrowser2[]<>NONE then
       'Succeeded creating component.'->putLine;
       'Initializing BSTR' -> putline;
       'http://www.mjolner.com/' -> str.setText;
       'Initializing URL VARIANT' -> putline;
       VT_BSTR -> url.vt;
       str -> url.bstrVal;
       'Initializing empty VARIANT' -> putline;
       VT_EMPTY -> empty.vt;
       'Calling pIWebBrowser2.visible' -> putline;
       1 -> pIWebBrowser2.putvisible; (* pIWebBrowser2.visible gives fatal error in compiler *)
       'Calling pIWebBrowser2.Navigate2' -> putline;
       (url[], empty[], empty[], empty[], empty[])->pIWebBrowser2.Navigate2;
       
       'Getting HTML document'->putLine;
       ppDisp[]->pIWebBrowser2.getdocument->hr;
       L: (# 
          do pBool[]->pIWebBrowser2.getBusy->hr;
             (if hr.failed then leave L; if);
             (if (pBool<>0) then 'busy'->putText;restart L if)
          #);
       (if hr.succeeded then
           IHTMLDocument2_IID-> IIDHTML;
           IIDHTML[]-> ppDisp.ref.Query->pHTML[];
           (if pHTML[]<>NONE then
               bstrh[]->pHTML.getTitle->hr;
               (if hr.succeeded then
                   'Title of doc is: '->putText;
                   bstrh.getText->putLine;
               if)
            else
               'query failed on HTML  interface'->putLine;
           if)
        else
           ('failed to get document interface',  hr.value)-> ComErrorMessage;
       if);
       
       'Releasing interfaces' -> putline;
       pIWebBrowser2.Release;
       'Succeeded releasing interface IWebBrowser2'->putline;
    else
       'Could not create component'->putline;
   if); 
   'CoUninitialize'->putline;
   CoUninitialize;
   'CoUninitialize done'->putline;
#)



