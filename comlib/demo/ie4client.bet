ORIGIN '~beta/comlib/comlib';
INCLUDE '~beta/comlib/ExDisp';
INCLUDE '~beta/comlib/MsApplication/MsHTML';

(* Small program showing how to use the WebBrowser2 interface
 * in ExDisp to start Internet Explorer and navigate it to a certain page.
 *)

--PROGRAM: descriptor--
(# CLSID_IE: ^CLSID;
   clsstr: @BSTR;
   IID_Web, IID_HTML: @IID;
   pIWebBrowser2: ^IWebBrowser2;
   pHTML: ^IHTMLDocument2;
   str: @BSTR;
   mydata: @COM(# empty, url: @VARIANT (* specialization of DATA *); #);
   hr: @HRESULT;
   dh: @IDispatchHolder;
   bh: @BSTRHolder;
   ih: @int16Holder;
   
   ie_progid: (# exit 'InternetExplorer.Application.1' #);
   
   Retries: @Integer;
   maxRetries: (# exit 100 #);
   
do 0->CoInitialize;
   
   ie_progid -> clsstr.setText;
   clsstr[] -> CLSIDFromProgID -> CLSID_IE[];
   IWebBrowser2_IID -> IID_Web; 
   
   'Create component and get interface IWebBrowser2'->putLine;
   
   (CLSID_IE[], NONE, CLSCTX_LOCAL_SERVER, IID_Web[])
     -> CoCreateInstance->pIWebBrowser2[];
   
   'CoCreateInstance done'->putLine;

   (if pIWebBrowser2[]<>NONE then
       'Succeeded creating component.'->putLine;
       'Initializing BSTR' -> putline;
       'http://www.mjolner.com/' -> str.setText;
       'Initializing URL VARIANT' -> putline;
       mydata.url[]->VARIANT_init
       (# 
       do VT_BSTR -> vt;
          str -> bstrVal;
       #);
       'Initializing empty VARIANT' -> putline;
       mydata.empty[]->VARIANT_init
       (# 
       do VT_EMPTY -> vt;
       #);
       'Calling pIWebBrowser2.putVisible' -> putline;
       1 -> pIWebBrowser2.putvisible; (* pIWebBrowser2.visible gives fatal error in compiler *)
       'Calling pIWebBrowser2.Navigate2' -> putline;
       (mydata.url[], mydata.empty[], mydata.empty[], mydata.empty[], mydata.empty[])
         ->pIWebBrowser2.Navigate2;
       'Waiting while page loads' -> putline;
       0->Retries;
       wait:
         (# 
         do ih[]->pIWebBrowser2.getBusy->hr;
            (if hr.failed then
                ('Failed to ask if browser is busy', hr.value) -> ComErrorMessage;
                leave wait; 
             else
                (* GetBusy succeeded *)
                (if ih=0 then 
                    (* Browser not busy *)
                    leave wait;
                 else
                    'Browser busy - waiting'->putline;
                    (5*(Retries+10), 0) -> SleepEx;
                    Retries+1->Retries;
                    (if Retries<Maxretries then restart wait if);
                if)
            if)
         #);
       0->Retries;

       L:(# 
         do 'Getting HTML document'->putLine;
            dh[]->pIWebBrowser2.getdocument->hr;
            (if hr.succeeded then
                IHTMLDocument2_IID -> IID_HTML;
                'Querying for IHTMLDocument2_IID'->putLine;
                (dh, IID_HTML[]) -> Query -> pHTML[];
                'Got IHTMLDocument2_IID'->putLine;
                (if pHTML[]<>NONE then
                    'Got IHTMLDocument2 interface'->putLine;
                    'Asking for document title' -> putline;
                    bh[]->pHTML.getTitle->hr;
                    (if hr.failed then
                        ('Failed to get title of document', hr.value) -> ComErrorMessage;
                     else
                        'Title of doc is: '->putText;
                        bh.getText->putLine;
                    if)
                 else
                    'Query for IHTMLDocument2 interface failed'->putLine;
                if)
             else
                ('failed to get document interface', hr.value)
                  -> ComErrorMessage;
                (5*(Retries+10), 0) -> SleepEx;
                Retries+1->Retries;
                (if Retries<Maxretries then restart L if);
            if);
         #);
       
       'Releasing interfaces' -> putline;
       (if pIWebBrowser2[]<>NONE then 
           pIWebBrowser2.Release; 
           'Succeeded releasing interface IWebBrowser2'->putline;
       if);
       (if dh.ref[]<>NONE then 
           dh.ref.Release; 
           'Succeeded releasing interface IDispatch'->putline;
       if);
       (if pHTML[]<>NONE then 
           pHTML.Release; 
           'Succeeded releasing interface IHTMLDocument2'->putline;
       if);
    else
       'Could not create component'->putline;
   if); 
   'CoUninitialize'->putline;
   CoUninitialize;
   'CoUninitialize done'->putline;
#)



