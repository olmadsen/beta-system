ORIGIN '~beta/comlib/comlib';
INCLUDE '~beta/comlib/ExDisp';
INCLUDE '~beta/comlib/MsHTML';

(* Small program showing how to use the WebBrowser2 interface
 * in ExDisp to start Internet Explorer and navigate it to a certain page.
 *)

--PROGRAM: descriptor--
(# CLSID_IE: ^CLSID;
   clsstr: @BSTR;
   IID_Web, IID_HTML: @IID;
   pIWebBrowser2: ^IWebBrowser2;
   pHTML: ^IHTMLDocument2;
   str: @BSTR;
   empty, url: @VARIANT;
   hr: @HRESULT;
   dh: @IDispatchHolder;
   bh: @BSTRHolder;
   ih: @int16Holder;
   
   ie_progid: (# exit 'InternetExplorer.Application.1' #);
   
do 0->CoInitialize;
   
   ie_progid -> clsstr.setText;
   clsstr[] -> CLSIDFromProgID -> CLSID_IE[];
   IWebBrowser2_IID -> IID_Web; 
   
   'Create component and get interface IWebBrowser2'->putLine;
   
   (CLSID_IE[], NONE, CLSCTX_LOCAL_SERVER, IID_Web[])
     -> CoCreateInstance->pIWebBrowser2[];
   
   'CoCreateInstance done'->putLine;

   (if pIWebBrowser2[]<>NONE then
       'Succeeded creating component.'->putLine;
       'Initializing BSTR' -> putline;
       'http://www.mjolner.com/' -> str.setText;
       'Initializing URL VARIANT' -> putline;
       VT_BSTR -> url.vt;
       str -> url.bstrVal;
       'Initializing empty VARIANT' -> putline;
       VT_EMPTY -> empty.vt;
       'Calling pIWebBrowser2.putVisible' -> putline;
       1 -> pIWebBrowser2.putvisible; (* pIWebBrowser2.visible gives fatal error in compiler *)
       'Calling pIWebBrowser2.Navigate2' -> putline;
       (url[], empty[], empty[], empty[], empty[])->pIWebBrowser2.Navigate2;
       'Waiting while page loads' -> putline;
       wait:
         (# 
         do ih[]->pIWebBrowser2.getBusy->hr;
            (if hr.failed then
                ('Failed to ask if browser is busy', hr.value) -> ComErrorMessage;
                leave wait; 
            if);
            (if (ih<>0) then 
                'Browser busy - waiting'->putline;
                (for 100000 repeat for) (* FIXME: a sleep function would be better *);
                restart wait
            if)
         #);
       'Getting HTML document'->putLine;
       dh[]->pIWebBrowser2.getdocument->hr;
       (if hr.succeeded then
           IHTMLDocument2_IID -> IID_HTML;
           'Querying for IHTMLDocument2_IID'->putLine;
           IID_HTML[]-> dh.ref.Query->pHTML[];
           (if pHTML[]<>NONE then
               'Got IHTMLDocument2 interface'->putLine;
               'Asking for document title' -> putline;
               bh[]->pHTML.getTitle->hr;
               (if hr.failed then
                   ('Failed to get title of document', hr.value) -> ComErrorMessage;
                else
                   'Title of doc is: '->putText;
                   bh.getText->putLine;
               if)
            else
               'Query for IHTMLDocument2 interface failed'->putLine;
           if)
        else
           ('failed to get document interface', hr.value)-> ComErrorMessage;
       if);
       
       'Releasing interfaces' -> putline;
       (if pIWebBrowser2[]<>NONE then 
           pIWebBrowser2.Release; 
           'Succeeded releasing interface IWebBrowser2'->putline;
       if);
       (if dh.ref[]<>NONE then 
           dh.ref.Release; 
           'Succeeded releasing interface IDispatch'->putline;
       if);
       (if pHTML[]<>NONE then 
           pHTML.Release; 
           'Succeeded releasing interface IHTMLDocument2'->putline;
       if);
    else
       'Could not create component'->putline;
   if); 
   'CoUninitialize'->putline;
   CoUninitialize;
   'CoUninitialize done'->putline;
#)



