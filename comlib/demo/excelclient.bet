ORIGIN '~beta/comlib/comlib';
INCLUDE '~beta/basiclib/external';
INCLUDE '~beta/sysutils/wstring';
INCLUDE '~beta/win32lib/winnt';
INCLUDE '~beta/comlib/oleauto';
INCLUDE '~beta/comlib/MsApplication/Excel8';

--PROGRAM: descriptor--
(# CLSID_XL: ^CLSID;
   clsstr: @BSTR;
   IID_IDispatch: @IID;
   empty: @VARIANT;
   hr: @HRESULT;
   dispid: @int32holder;
   wstr: @wstring;
   rgszNames: @ExternalRepetition(# elementSize::(# do 4->value #)#);
   params: @DISPPARAMS;
   varg: @VARIANT;
   
   pDisp: ^IDispatch;
   
   excel_progid: (# exit 'Excel.Application' #);
   
do 0->CoInitialize;
   
   excel_progid -> clsstr.setText;
   clsstr[] -> CLSIDFromProgID -> CLSID_XL[];
   IDispatch_IID -> IID_IDispatch; 
   
   'Create component and get dispinterface'->putLine;
   
   (CLSID_XL[], NONE, CLSCTX_LOCAL_SERVER, IID_IDispatch[])
     -> CoCreateInstance->pDisp[];
   
   'CoCreateInstance done'->putLine;

   (if pDisp[]<>NONE then
       'Succeeded creating component.'->putLine;
       
       'Invoking pDisp.Visible: Getting DispId' -> putline;
       wstr.init;
       'Visible' -> wstr.set;
       1 -> rgszNames.new;
       wstr %putLongAt(rgszNames.ptr+0); (* inxPut *)
       (IID_NULL, rgszNames, 1, LOCALE_SYSTEM_DEFAULT, dispid[])
         -> pDisp.GetIdsOfNames
         -> hr;
       (if hr.succeeded then
           'Got Id for \'Visible\': ' -> puttext;
           dispid.value -> putint;
           newline;
        else
           ('GetIdsOfNames Failed', hr.value) -> ComErrorMessage;
       if);
       wstr.free;
       rgszNames.free;
       'Invoking pDisp.Visible: Calling Invoke' -> putline;
       
       (* Set up parameters for Invoke *)
       1 -> rgszNames.new;
       DISPID_PROPERTYPUT %putLongAt(rgszNames.ptr+0); (* inxPut *)
       varg[]->VARIANT_init
       (# 
       do VT_BOOL -> vt;
          VARIANT_TRUE -> bool;
       #);
       @@varg -> params.rgvarg; (* Now we're getting insecure! *)
       rgszNames->params.rgdispidNamedArgs;
       1->params.cArgs;
       1->params.cNamedArgs;
       (dispid.value, IID_NULL, LOCALE_USER_DEFAULT, DISPATCH_PROPERTYPUT, params[], NONE, NONE, NONE)
         -> pDisp.Invoke
         -> hr;
       (if hr.succeeded then
           'Invoke succeeded' -> putline;
        else
           ('Invoke Failed', hr.value) -> ComErrorMessage;
       if);
       rgszNames.free;
       
       'Awaiting key-press before releasing interface: ' -> puttext;
       getline;
       
       'Releasing interfaces' -> putline;
       (if pDisp[]<>NONE then 
           pDisp.Release; 
           'Succeeded releasing dispinterface'->putline;
       if);
    else
       'Could not create component'->putline;
   if); 
   'CoUninitialize'->putline;
   CoUninitialize;
   'CoUninitialize done'->putline;
#)



