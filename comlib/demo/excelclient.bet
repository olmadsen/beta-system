ORIGIN '~beta/comlib/comlib';
INCLUDE '~beta/comlib/dispatchhelp';
INCLUDE '~beta/comlib/MsApplication/Excel8';

--LIB: attributes--
trace_com: (# exit false #);
trace_vb: (# exit true #);

VB:
  (# stmt: ^text
  enter stmt[]
  do (if trace_vb then
         'VB: ' -> puttext;
         stmt[] -> putline;
     if);
  #);

--PROGRAM: descriptor--
(# CLSID_XL: ^CLSID;
   clsstr: @BSTR;
   IID_IDispatch: @IID;
   hr: @HRESULT;
   mydata: @COM
     (# varg, varg1, varg2: @VARIANT (* specialization of DATA *);
        vResult: @VARIANT (* specialization of DATA *);
     #);
   status: @boolean;
   
   pDispExcelApp, pdispWorkBooks, pdispWorkBook, pdispWorksheet, 
   pdispRange, pdispCharts, pDispChart: ^IDispatch;
   
   excel_progid: (# exit 'Excel.Application' #);
   
   use_dual: (# exit false #);
   
   CleanUp:
     (# 
     do dparms.release;
        (if trace_com then
            '\nClean up:' -> putline;
            'Releasing interfaces' -> putline;
        if);
        
        (if pDispExcelApp[]<>NONE then 
            pDispExcelApp.Release; 
        if);
        (if pdispWorkBooks[]<>NONE then 
            pdispWorkBooks.Release; 
        if);
        (if pdispWorkBook[]<>NONE then 
            pdispWorkBook.Release; 
        if);
        (if pdispWorksheet[]<>NONE then 
            pdispWorksheet.Release; 
        if);
        (if pdispRange[]<>NONE then 
            pdispRange.Release; 
        if);
        (if pdispCharts[]<>NONE then 
            pdispCharts.Release; 
        if);
        (if pdispChart[]<>NONE then 
            pdispChart.Release; 
        if);
        (if trace_com then
            'CoUninitialize'->putline;
        if);
        CoUninitialize;
        (if trace_com then
            'CoUninitialize done'->putline;
        if);
     #);
   
   putRangeReal:
     (# name: ^text;
        value: @real;
        status: @boolean;
        pDispRange: ^IDispatch;
     enter (name[], value)
     do true -> status;
        dparms.release;
        (NONE, name[]) -> dparms.putString;
        (pDispWorksheet[], 'Range', dparms[], mydata.vResult[]) 
          -> PropertyGetByName
          -> status;
        (if not status then
            'putRangeReal failed' -> putline;
            leave putRangeReal;
        if);
        mydata.vResult[] -> VARIANT_init(# do pDispval -> pDispRange[] #);
        
        dparms.release;
        (NONE, value) -> dparms.putReal;
        (pDispRange[], 'Value', dparms[]) 
          -> PropertyPutByName
          -> status;
        (if not status then
            'putRangeReal failed' -> putline;
            leave putRangeReal;
        if); 
     exit status
     #);
   
   dParms: @DispatchParams;
   
do ControlExcel:
     (# 
     do 0->CoInitialize;   
        excel_progid -> clsstr.setText;
        clsstr[] -> CLSIDFromProgID -> CLSID_XL[];
        IDispatch_IID -> IID_IDispatch; 
        
        (if trace_com then
            'Create component and get dispinterface'->putLine;
        if);
        (CLSID_XL[], NONE, CLSCTX_LOCAL_SERVER, IID_IDispatch[])
          -> CoCreateInstance->pDispExcelApp[];
        
        (if pDispExcelApp[]<>NONE then
            (if trace_com then
                'Succeeded creating component.'->putLine;
            if);
         else
            'failed creating component.'->putLine;
            leave ControlExcel;
        if);

        (*'Setting pDispExcelApp.Width:' -> putline;
        (NONE, 600.0) -> dParms.putReal;
        (pDispExcelApp[], 'Width', dparms[]) -> PropertyPutByName -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;*)
        
        (if trace_com then
            'Invoking pDispExcelApp.Visible:' -> putline;
        if);
        (NONE, true) -> dParms.putBoolean;
        (pDispExcelApp[], 'Visible', dparms[]) -> PropertyPutByName -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        
        'set wbs = [application].Workbooks' -> VB;
        (pDispExcelApp[], 'Workbooks', dparms[], mydata.varg1[]) 
          -> PropertyGetByName 
          -> status;
        (if not status then
            leave ControlExcel;
        if);
        mydata.varg1[] -> VARIANT_init(# do pdispVal->pdispWorkbooks[] #);

        'set wb = wbs.Add(Template := xlWorksheet)' -> VB;
        (if use_dual then 
            (# dual: ^Workbooks;
               RHS: @int32holder;
            do 'Calling Add using vtable' -> putline;
               pdispWorkbooks[] -> dual[]; (* should be a QIF? *)
               (*(mydata.varg, lcid, RHS[]) -> dual.add -> hr;*)
            #);
            (if hr.succeeded then
             else
                ('pdispWorkbooks.Add(template:=xlWorksheet) failed',hr.value) 
                  -> ComErrorMessage;
                leave ControlExcel;
            if);
         else
            ('Template', xlWorksheet) -> dParms.putInt16;
            (pdispWorkbooks[], 'Add', dparms[], DISPATCH_METHOD, mydata.varg2[]) 
              -> InvokeByName
              -> status;
            (if not status then
                leave ControlExcel;
            if);
            dparms.release;
            mydata.varg2[] -> VARIANT_init(# do pdispVal->pdispWorkBook[] #);
        if);
        
        'set ws = wb.Worksheets(1)' -> VB;
        (NONE,1) -> dParms.putInt16;
        (pdispWorkBook[], 'Worksheets', dparms[], mydata.varg2[]) 
          -> PropertyGetByName 
          -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        mydata.varg2[] -> VARIANT_init(# do pdispVal->pdispWorkSheet[] #);
        
        'set r = ws.Range("A1:D1")' -> VB;
        (NONE, 'A1:D1') -> dparms.putString;
        (pdispWorkSheet[], 'Range', dparms[], mydata.varg2[]) 
          -> PropertyGetByName 
          -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        mydata.varg2[] -> VARIANT_init(# do pdispVal->pdispRange[] #);
        
        'r.Value = Array("North", "South", "East", "West")' -> VB;
        
        (NONE, 8, ('North', 'South', 'East', 'West')) -> dparms.putStringArray;
        (pdispRange[], 'Value', dparms[]) 
          -> PropertyPutByName 
          -> status;
        dparms.release;
        (if not status then
            leave ControlExcel;
        if);
        
        'ws.Range("A2") = 5.2' -> VB;
        ('A2', 5.2) -> putRangeReal -> status;
        (if not status then
            leave ControlExcel;
        if);
        'ws.Range("B2") = 10.0' -> VB;
        ('B2', 10.0) -> putRangeReal -> status;
        (if not status then
            leave ControlExcel;
        if);
        'ws.Range("C2") = 8.0' -> VB;
        ('C2', 8.0) -> putRangeReal -> status;
        (if not status then
            leave ControlExcel;
        if);
        'ws.Range("D2") = 20.0' -> VB;
        ('D2', 20.0) -> putRangeReal -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        
        'set sourceRange= ws.Range("A1:D2")' -> VB;
        (NONE, 'A1:D2') -> dparms.putString;
        (pdispWorkSheet[], 'Range', dparms[], mydata.varg2[]) 
          -> PropertyGetByName 
          -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        mydata.varg2[] -> VARIANT_init(# do pdispVal->pdispRange[] #);
        
        'set crts = wb.Charts' -> VB;
        (pdispWorkbook[], 'Charts', dparms[], mydata.varg2[]) 
          -> PropertyGetByName 
          -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        mydata.varg2[] -> VARIANT_init(# do pdispVal->pdispCharts[] #);
        
        'set crt = crts.Add' -> VB;
        (pdispCharts[], 'Add', dparms[], DISPATCH_METHOD, mydata.varg2[]) 
          -> InvokeByName
          -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        mydata.varg2[] -> VARIANT_init(# do pdispVal->pdispChart[] #);
        
        'crt.ChartWizard source := sourcerange, gallery := xl3DPie, _\n'
        '    format := 7, plotBy := xlRows, categoryLabels := 1, _\n'
        '    seriesLabels := 0, haslegend := 2, title := "Sales Percentages"'
          -> VB;
        ('title', 'Sales Percentages') -> dparms.putString;
        (* No legend, see 
         * http://support.microsoft.com/support/kb/articles/Q133/3/18.asp
         *)
        ('hasLegend', 2)         -> dParms.putInt16;
        (* No Serieslabels are supplied *)
        ('seriesLabels', 0)      -> dParms.putInt16;
        (* North, south, east, west are categoryLabels (are in 1 row) *)
        ('categoryLabels', 1)    -> dParms.putInt16;
        ('plotBy', xlRows)       -> dParms.putInt16;
        ('format', 7)            -> dParms.putInt16;
        ('gallery', xl3DPie)     -> dParms.putInt16;
        ('source', pDispRange[]) -> dParms.putDispatch;
        (pdispChart[], 'ChartWizard', dparms[], DISPATCH_METHOD, NONE) 
          -> InvokeByName
          -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        
        'wb.Saved = TRUE' -> VB;
        (*  so that Excel won\'t ask whether to save doc on close *)
        (NONE, true) -> dParms.putBoolean;
        (pDispWorkBook[], 'Saved', dparms[]) -> PropertyPutByName -> status;
        (if not status then
            leave ControlExcel;
        if);
        dparms.release;
        
        
     #);
   
   CleanUp;
   
#)



