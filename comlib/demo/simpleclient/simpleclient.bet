ORIGIN '~beta/guienv/guienv';
INCLUDE 
'../simpleserver/applicationobject'
'../../comlib'
'../../Unknwn'
'../../comtypes';


--PROGRAM: descriptor--
guienv
(# CLSID_simpleserver: ^CLSID;
   IID_UNKNW,SimpleServer_IID: @IID;
   pIUnknown: ^IUnknown;
   pISimpleServer: ^ApplicationObject;
   clsstr: @BSTR;
   str: @BSTR;
   hr: @HRESULT;
   bh: @BSTRHolder;
   ih: @int16Holder;
   (* SimpleServer.Application *)
   simpleserver_progid: (# exit 'SimpleServer.Application' #);
   
do 0->CoInitialize;
   
   simpleserver_progid -> clsstr.setText;
   clsstr[] -> CLSIDFromProgID -> CLSID_simpleserver[];
   IUnknown_IID -> IID_UNKNW;
   ISimpleServer_IID->SimpleServer_IID;
   
   'Create simple server instance '->putLine;
   
   (CLSID_simpleserver[], NONE, CLSCTX_LOCAL_SERVER, IID_UNKNW[])
     -> CoCreateInstance->pIUnknown[];
   
   'CoCreateInstance done'->putLine;

   (if pIUnknown[]<>NONE then
       'Succeeded creating component.'->putLine;
       
       (pIUnknown[], SimpleServer_IID[]) -> Query -> pISimpleServer[];
       
       (if (pISimpleServer[]<>NONE) then
           'Calling pISimpleServer.printLong.' -> screen.putline;
           5 -> pISimpleServer.printLong;
           'Done calling pISimpleServer.printLong.' -> screen.putline;
           
           'Calling pISimpleServer.print.' -> screen.putline;
           'Something is happening' -> str.setText;
           str.wcharptr -> pISimpleServer.print;
           'Done calling pISimpleServer.print.' -> screen.putline; 
           
           'Releasing interfaces' -> putline;
           pISimpleServer.Release;
       if);
       pIUnknown.Release;
    else
       'Could not create component'->putline;
   if); 
   'CoUninitialize'->putline;
   CoUninitialize;
   'CoUninitialize done'->putline;
   stop;
#)
