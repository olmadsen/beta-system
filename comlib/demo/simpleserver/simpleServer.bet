ORIGIN '~beta/guienv/guienv';
INCLUDE 
'~beta/sysutils/cstring'
'~beta/win32lib/windowsmisc'
'~beta/win32lib/windef'
'~beta/win32lib/registry'
'~beta/win32lib/winerror'
'~beta/win32lib/win32apiinit'
'classfactory'
'applicationobject'
'comenv';

-- guienvlib: attributes --



RegisterServer:
  (# bOk: @boolean;
     cstrID: @cString;
     tempClsidTxt: ^text;
     szID: ^text;
     szCLSID: @text;
     szModulePath: @text;
     errMsg: ^text;
     noOfChars: @integer;
  do 'RegisterServer' -> screen.putline;
     
     outerL:
       (# mySetRegKeyValue: SetRegKeyValue
            (# noSuchRoot:: 
                 (# 
                 do &text[] -> errMsg[];
                    'noSuchRoot. '->errMsg.append; 
                    (if key[]<>NONE then
                        'key: ' -> errMsg.append;
                        key[] -> errMsg.putline;
                    if);
                    (if subkey[]<>NONE then
                        'subkey: ' -> errMsg.append;
                        subkey[] -> errMsg.putline;
                    if);
                    (if valueName[]<>NONE then
                        'valueName: ' -> errMsg.append;
                        valueName[] -> errMsg.putline;
                    if);
                    leave outerL
               #);
               createKeyFailed:: 
                 (# 
                 do &text[] -> errMsg[];
                    msg[]->errMsg.append; 
                    (if key[]<>NONE then
                        'key: ' -> errMsg.append;
                        key[] -> errMsg.putline;
                    if);
                    (if subkey[]<>NONE then
                        'subkey: ' -> errMsg.append;
                        subkey[] -> errMsg.putline;
                    if);
                    (if valueName[]<>NONE then
                        'valueName: ' -> errMsg.append;
                        valueName[] -> errMsg.putline;
                    if);
                    leave outerL
                 #);
               setValueFailed:: 
                 (# 
                 do &text[] -> errMsg[];
                    msg[]->errMsg.append; 
                    (if key[]<>NONE then
                        'key: ' -> errMsg.append;
                        key[] -> errMsg.putline;
                    if);
                    (if subkey[]<>NONE then
                        'subkey: ' -> errMsg.append;
                        subkey[] -> errMsg.putline;
                    if);
                    (if valueName[]<>NONE then
                        'valueName: ' -> errMsg.append;
                        valueName[] -> errMsg.putline;
                    if);
                    
                    leave outerL
                 #);
            #);
       do false -> bOk;
          
          (* Obtain the path to this module s executable file for later use. *)
          MAX_PATH+1 -> szModulePath.extend;
          (get_beta_instance,@@szModulePath.t[1],MAX_PATH) 
            -> GetModuleFileName -> noOfChars;
          (if noOfChars>0 then
              noOfChars -> szModulePath.lgth -> szModulePath.pos;
           else
              &text[] -> errMsg[];
              'GetModuleFileName failed.' -> errMsg.append; 
              leave outerL
          if);                    
          
          (****************************************************************
           Create registry entries for SimpleServer
           ****************************************************************)
          (* Create some base key strings. *)
          '{' -> tempClsidTxt[];
          SimpleServer_CLSID
            -> tempClsidTxt.append;
          '}' -> tempClsidTxt.put;
          
          tempClsidTxt[]  -> cstrID.set;
          (if cstrID=0 then 
              &text[] -> errMsg[];
              'StringFromGUID2A failed' -> errMsg.append; 
              leave outerL 
          if);
          cstrID.get -> szID[];
          cstrID.free;
          
          'CLSID\\' -> szCLSID.append;
          szID[] -> szCLSID.append;
          
          (* Create ProgID keys. *)
          (HKEY_CLASSES_ROOT,
          'SimpleServer.Application.1.0',
          NONE,
          REG_SZ,
          '',
          'Simple Server Application')
            -> mySetRegKeyValue;
          
          (HKEY_CLASSES_ROOT,
          'SimpleServer.Application.1.0',
          'CLSID',
          REG_SZ,
          '',
          szID[])
            -> mySetRegKeyValue;
            
          (* Create VersionIndependentProgID keys. *)
          (HKEY_CLASSES_ROOT,
          'SimpleServer.Application',
          NONE,
          REG_SZ,
          '',
          'SimpleServer Application')
            -> mySetRegKeyValue;
          
          (HKEY_CLASSES_ROOT,
          'SimpleServer.Application',
          'CurVer',
          REG_SZ,
          '',
          'SimpleServer.Application.1.0')
            -> mySetRegKeyValue;
          
          (HKEY_CLASSES_ROOT,
          'SimpleServer.Application',
          'CLSID',
          REG_SZ,
          '',
          szID[])
            -> mySetRegKeyValue;
          
          (* Create entries under CLSID. *)
          (HKEY_CLASSES_ROOT,
          szCLSID[],
          NONE,
          REG_SZ,
          '',
          'Simple Server Application')
            -> mySetRegKeyValue;
          
          (HKEY_CLASSES_ROOT,
          szCLSID[],
          'ProgID',
          REG_SZ,
          '',
          'SimpleServer.Application.1.0')
            -> mySetRegKeyValue;
          
          (HKEY_CLASSES_ROOT,
          szCLSID[],
          'VersionIndependentProgID',
          REG_SZ,
          '',
          'SimpleServer.Application')
            -> mySetRegKeyValue;
          
          (HKEY_CLASSES_ROOT,
          szCLSID[],
          'NotInsertable',
          REG_SZ,
          '',
          NONE)
            -> mySetRegKeyValue;
          
          (HKEY_CLASSES_ROOT,
          szCLSID[],
          'LocalServer32',
          REG_SZ,
          '',
          szModulePath[])
            -> mySetRegKeyValue;
          
          
          
       #);
  exit bOk
  #);

UnregisterServer:
  (# 
  do
  #);


--PROGRAM: descriptor--
guienv
(# 
   theComEnv: @comenv;
   theAutoApplObj: @ApplicationObject;
   
   oleLibInitialized: @HRESULT;
   
   onStartApplication::
     (# 
     do 'onStartApplication' -> screen.putline;
        0 -> OleInitialize -> oleLibInitialized;
        (if oleLibInitialized.succeeded then
            'OleInitialize succeeded.' -> screen.putline;
         else
            'OleInitialize failed.' -> screen.putline;
        if);
     #);
   
   onQuit::
     (# 
     do '***** onQuit ****' -> screen.putLine;
        (if oleLibInitialized.succeeded  then
            OleUninitialize;
        if);
     #);
   
   mainwindow: @window
     (# open::< (# do (400,400)->size; #);
        eventHandler::<
          (# onAboutToClose::< (# do terminate #)
          #);
        
     #);
   arg: ^text;
   shuttingDown: @boolean;
do theAutoApplObj.print;
   (for i:NoOfArguments repeat
        i -> Arguments -> arg[];
        
        (if TRUE
         //'-RegServer' -> arg.equalNCS
         //'/RegServer' -> arg.equalNCS then
            
            (* Register COM server and exit *)
            true -> shuttingDown;
            RegisterServer;
            
            terminate;
            
         //'-UnregServer' -> arg.equalNCS
         //'/UnregServer' -> arg.equalNCS then
            
            (* Unregister COM server and exit *)
            true -> shuttingDown;
            UnregisterServer;
            
            terminate;
        if);
   for);
   
   
   
   (if not shuttingDown then
       theAutoApplObj.init;
       (mainWindow[],theAutoApplObj[]) 
         -> theComEnv.initClassFact;
       mainWindow.open; 
   if);
#)
