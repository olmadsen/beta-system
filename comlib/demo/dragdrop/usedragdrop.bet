ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls'
        '~beta/win32lib/winuserconsts'
        '~beta/comlib/comlib'
        'dragdrop';
-- InterfaceObjectLib: Attributes --
DropTarget:
  (#
     private: @<<SLOT private:Descriptor>>;
     register:<
     (* Makes this(interfaceobject) work as a possible drop target. *)
       (#
          onError:< exception
            (# 
            do 'COleDropTarget error: unable to register window.'->msg.append
            #)
       <<SLOT register:DoPart>>
       #);
     revoke:<
     (* Removes this(interfaceobject) as a drop target. *)
       (#
          onError:< exception
            (# 
            do
               'COleDropTarget error: unable to revoke window registering.'
                 ->msg.append
            #)
       <<SLOT revoke:DoPart>>
       #);
     media:<
     (* Furtherbind to specify which type you want (text/bitmap/...),
      and what action (copy/move/...) should be taken when dropped. *)
       (#
          types: (* Types supported by the clipboard data. *) [0] @integer;
          modifiers:
          (* What modifiers are enabled?
           (bitwise AND of MK_SHIFT, MK_CONTROL, MK_LBUTTON, ...).*) @integer;
          actions:
          (* What actions are supported by the clipboard data?
           (bitwise AND of DROPEFFECT_MOVE/COPY/LINK/...). *) @integer;
          preferredType:
          (* Which of the types in 'types' do you prefer. (0 for none of them). *)
          @integer;
          action: (* Which of the actions in 'actions' should be taken. *)
            @integer;
          ;
          typeIsSupported: (* Is the type on of the supported types? *)
            (# type: @integer; answer: @boolean
            enter type
            do
               loop:
               (for i: types.range repeat
                 (if type = types[i] then true->answer; leave loop if)
               for)
            exit answer
            #);
          actionIsSupported: (* Is the action one of the supported actions? *)
            (# action: @integer; answer: @boolean
            enter action
            do (if (action %band actions) <> 0 then true->answer if)
            exit answer
            #);
          modifierIsPressed: (* Is the modifier enabled? *)
            (# modifier: @integer; answer: @boolean
            enter modifier
            do (if (modifier %band modifiers) <> 0 then true->answer if)
            exit answer
            #)
       enter (types,modifiers,actions)
       <<SLOT media:DoPart>>
       exit (preferredType,action)
       #);
     drop:<
     (* Furtherbind to accept a drop.
      NOTE: the drop virtual should process the data (in 'objectHandle') immediately,
      as the data is reclaimed after drop has been called. *)
       (#
          type: (* The type of the data (e.g. CF_TEXT). *) @integer;
          action:
          (* The action that should be taken (DROPEFFECT_COPY/MOVE/...). *)
            @integer;
          objectHandle:
          (* A "handle" to the data.
           Use the auxillary functions to extract the content in case
           of a standard format. *) @int32;
          ;
          asText:
          (* If the data is known to be regular text, then
           this function returns a reference to a copy of the text. *)
            (# t: ^text <<SLOT dropAsText:DoPart>> exit t[] #)
       enter (type,action,objectHandle)
       <<SLOT drop:DoPart>>
       #)
  #)  

-- program: Descriptor --
guienv
  (#
     mainWindow: @window
       (#
          theDropTarget: @DropTarget
            (#
               media:: 
                 (# 
                 do
                    (if CF_TEXT->typeIsSupported then
                        CF_TEXT->preferredType
                    if)
                 #);
               drop:: 
                 (# 
                 do
                    (if type = CF_TEXT then
                        '\nFoelgende er blevet droppet:'->putline;
                        asText->putline;
                        newline
                     else
                        'Hvilken type er blevet droppet?'->putline
                    if)
                 #)
            #);
          open::< 
            (# 
            do
               (265,140)->size;
               (500,140)->position;
               target_btn.open;
               'Drag\'n\'drop demo'->title;
               theDropTarget.register
            #);
          eventHandler::< 
            (#
               onAboutToClose::<  (#  do theDropTarget.revoke; terminate #)
            #);
          target_btn: @pushbutton
            (#
               open::< 
                 (# 
                 do
                    'Drop Target'->label;
                    (225,100)->size;
                    (20,20)->position;
                    INNER
                 #);
               eventHandler::< 
                 (#
                    onMouseUp::<  (#  do theDropTarget.revoke; terminate #)
                 #)
            #)
       #);
     InitOLE:
       (# hr: @HRESULT
       do
          0->OleInitialize->hr;
          (if hr.failed then 'OleInitialize failed!'->putline if)
       #)
  do InitOLE; mainWindow.open
  #)  

