ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE 'holders';
BODY 'private/comlibbody';
--LIB: attributes--

(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-98
 *       All rights reserved.
 *)


HRESULT: 
  (# value: @int32;
     succeeded: (# exit value>=0 #);
     failed: (# exit value<0 #);
  enter value
  exit THIS(HRESULT)[]
  #);

(* HRESULT: IntegerObject
 *   (# 
 *      succeeded: (# exit value>=0 #);
 *      failed: (# exit value<0 #);
 *   #);
 * SUCCEEDED: 
 *   (# Hresult: @Int32; 
 *   enter Hresult
 *   exit (Hresult>=0)
 *   #);
 *)

GUID:
  (# (* The C type is:
      * typedef struct _GUID
      * {
      *   unsigned long Data1;
      *   unsigned short Data2;
      *   unsigned short Data3;
      *   unsigned char Data4[8];
      * } GUID;
      *)
     traceGUID: (# exit false #);
     Binary: [16]@Char;
     setFromText:
       (* Accepts this format only; no checking!
        * '32bb8320-b41b-11cf-a6bb-0080c7b2d682'
        * FIXME: there are standard COM functions we could call.
        *)
       (# <<SLOT GUIDsetFromTextLib: attributes>>;
          t: ^Text; p: @Int32;
       enter T[]
       <<SLOT GUIDSetFromTextDP: dopart>>   
       #);
     getAsText:
       (# T: ^Text;
       <<SLOT GUIDGetAsTextDP: dopart>>
       exit T[]
       #);
  enter setFromText
  exit getAsText
  #);     

CLSID: GUID(# #);
IID: GUID(# #);

IUnknown: COM   
  (# QueryInterface:< 
       (# hres, iidref, ppv: @Int32;
       enter (iidref, ppv)
       exit hres
       #); 
     AddRef:< (# do INNER #);
     Release:< (# do INNER #);       
     Query: 
       (# theIID: ^IID;  
          Iptr: ^IUnknown;
          hr: @HRESULT;
       enter theIID[]
       do (if ((@@theIID.Binary[1], @@Iptr)->QueryInterface->hr).failed then
              NONE->Iptr[];
          if);
       exit Iptr[]
       #); 
  #);

IDispatch: IUnknown
  (# 
     GetTypeInfoCount:<
       (# 
       #);
     GetTypeInfo:<
       (# 
       #);
     GetIDsOfNames:<
       (# 
       #); 
     Invoke:<
       (# 
       #);
  #);

VARIANT: holder
  (# 
     substance: ^substance_descr;
     substance_descr: DATA
       (# vt: (* VARTYPE *) @int16;
          wReserved1: (* WORD *) @int16;
          wReserved2: (* WORD *) @int16;
          wReserved3: (* WORD *) @int16;
          long3: @int32;
          long4: @int32;
       #);
     
     vt: (# enter substance.vt exit substance.vt #);
     
     (* union members for different accesses of (long3,long4) *)
     lVal: (* LONG *) _long(# #);
     bVal: (* BYTE *) _byte(# #);
     iVal: (* SHORT *) _short(# #);
     fltVal: (* FLOAT *) _float(# #);
     dblVal: (* DOUBLE*) _double(# #);
     boolVal: (* VARIANT_BOOL *) _long(# #);
     bool: (* _VARIANT_BOOL *) _long(# #);
     scode: (* SCODE *) _long(# #);
     cyVal: (* CY *) 
       (# 
       enter (substance.long3,substance.long4)
       exit (substance.long3,substance.long4)
       #);
     date: (* DATE *) _double(# #);
     bstrVal: (* BSTR *) _long(# #);
     
     punkVal: (* IUnknown __RPC_FAR* *) _long(# #); (* OK? *)
     pdispVal: (* IDispatch __RPC_FAR* *) _long(# #); (* OK? *)
     parray: (* SAFEARRAY __RPC_FAR* *) _long(# #); (* OK? *)
     
     pbVal: (* BYTE __RPC_FAR* *) _long(# #);
     piVal: (* SHORT __RPC_FAR* *) _long(# #);
     plVal: (* LONG __RPC_FAR* *) _long(# #);
     pfltVal: (* FLOAT __RPC_FAR* *) _long(# #);
     pdblVal: (* DOUBLE __RPC_FAR* *) _long(# #);
     pboolVal: (* VARIANT_BOOL __RPC_FAR* *) _long(# #);
     pbool: (* _VARIANT_BOOL __RPC_FAR* *) _long(# #);
     pscode: (* SCODE __RPC_FAR* *) _long(# #);
     pcyVal: (* CY __RPC_FAR* *) _long(# #);
     pdate: (* DATE __RPC_FAR* *) _long(# #);
     pbstrVal: (* BSTR __RPC_FAR* *) _long(# #);
     ppunkVal: (* IUnknown __RPC_FAR* __RPC_FAR* *) _long(# #);
     ppdispVal: (* IDispatch __RPC_FAR* __RPC_FAR* *) _long(# #);
     pparray: (* SAFEARRAY __RPC_FAR* __RPC_FAR* *) _long(# #);
     pvarVal: (* VARIANT __RPC_FAR* *) _long(# #);
     
     byref: (* PVOID *) _long(# #);
     cVal: (* CHAR *) _ubyte(# #);
     uiVal: (* USHORT *) _ushort(# #);
     ulVal: (* ULONG *) _long(# #);
     intVal: (* INT *) _long(# #);
     uintVal: (* UINT *) _long(# #);
     
     pdecVal: (* DECIMAL __RPC_FAR* *) _long(# #);
     pcVal: (* CHAR __RPC_FAR* *) _long(# #);
     puiVal: (* USHORT __RPC_FAR* *) _long(# #);
     pulVal: (* ULONG __RPC_FAR* *) _long(# #);
     pintVal: (* INT __RPC_FAR* *) _long(# #);
     puintVal: (* UINT __RPC_FAR* *) _long(# #);
     
     (* Union alternative to (long3, long4) *)
     decVal: (* DECIMAL *)(* NYI *) 
     (# 
     enter (substance.long3, substance.long4)
     exit (substance.long3, substance.long4)
     #);
     
     (* help patterns *)
     _ubyte:
       (# 
       enter (# b: @int8 enter b do (b,1) ->substance.long3.%putByte #)
       exit 1->substance.long3.%getByte
       #);
     _ushort:
       (#
       enter (# i: @int16 enter i do (i,1) ->substance.long3.%putShort #)
       exit 1->substance.long3.%getShort
       #);
     _long:
       (# enter substance.long3 exit substance.long3 #);
     _byte:
       (# 
       enter (# b: @int8 enter b do (b,1) ->substance.long3.%putByte #)
       exit 1->substance.long3.%getSignedByte
       #);
     _short:
       (#
       enter (# i: @int16 enter i do (i,1) ->substance.long3.%putShort #)
       exit 1->substance.long3.%getSignedShort
       #);
     _float: 
       (# r: @real
       enter (# enter r 
             do r->substance.long3; 
                'Warning: VARIANT.float: precision lost!' -> putline;
             #)
       exit (# do substance.long3 -> r exit r #)
       #);
     _double:
       (# r: @real
       enter (# enter r 
             do 1->r.%getLong -> substance.long3; 
                2->r.%getLong -> substance.long4;
             #)
       exit (# 
            do (substance.long3,1)->r.%putLong; 
               (substance.long4,2)->r.%putLong; 
            exit r
            #)
       #);

  #);
CoInitialize: external
  (# LPVoid,hresult: @Int32;
  enter LPVoid
  do CallStd; 
  exit hresult
  #);
CoUninitialize: external
  (# 
  do CallStd;
  #);


CoCreateInstance:
  (# theclsid: ^CLSID; 
     outer: ^IUnknown;
     Flags: @Int32;
     theiid: ^IID;
     Iptr: ^IUnknown;
     hr: @HRESULT;
     CoCreateInstance: external
       (# pUnkOuter: ^IUnknown;
          rclsid, dwClsContext, riid, ppv, hr: @Int32;
       enter (rclsid, pUnkOuter[], dwClsContext, riid, ppv)
       do callStd
       exit hr
       #);
  enter (theclsid[], outer[], Flags, theiid[])
  do (@@theclsid.Binary[1], outer[], Flags, @@theiid.Binary[1], @@Iptr)
       -> CoCreateInstance->hr;
     (if hr.failed then
         ('CoCreateInstance failed', hr.value) -> ComErrorMessage;
         NONE->Iptr[];
     if);
  exit Iptr[]
  #);
          
CLSCTX_INPROC_SERVER: (# exit 1 #);

ComErrorMessage: external
  (# msg: [0]@char;
     hr: @int32;
  enter (msg, hr)
  do CallC
  #)

