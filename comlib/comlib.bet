ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE 'holders';
BODY 'private/comlibbody';
--LIB: attributes--

(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-98
 *       All rights reserved.
 *)


HRESULT: 
  (# value: @integer;
     succeeded: (# exit value>=0 #);
     failed: (# exit value<0 #);
  enter value
  exit THIS(HRESULT)[]
  #);

(* HRESULT: IntegerObject
 *   (# 
 *      succeeded: (# exit value>=0 #);
 *      failed: (# exit value<0 #);
 *   #);
 * SUCCEEDED: 
 *   (# Hresult: @Integer; 
 *   enter Hresult
 *   exit (Hresult>=0)
 *   #);
 *)

GUID:
  (# (* The C type is:
      * typedef struct _GUID
      * {
      *   unsigned long Data1;
      *   unsigned short Data2;
      *   unsigned short Data3;
      *   unsigned char Data4[8];
      * } GUID;
      *)
     traceGUID: (# exit false #);
     Binary: [16]@Char;
     setFromText:
       (* Accepts this format only; no checking!
        * '32bb8320-b41b-11cf-a6bb-0080c7b2d682'
        * FIXME: there are standard COM functions we could call.
        *)
       (# <<SLOT GUIDsetFromTextLib: attributes>>;
          t: ^Text; p: @Integer;
       enter T[]
       <<SLOT GUIDSetFromTextDP: dopart>>   
       #);
     getAsText:
       (# T: ^Text;
       <<SLOT GUIDGetAsTextDP: dopart>>
       exit T[]
       #);
  enter setFromText
  exit getAsText
  #);     

CLSID: GUID(# #);
IID: GUID(# #);

IUnknown: COM   
  (# QueryInterface:< 
       (# hres, iidref, ppv: @Integer;
       enter (iidref, ppv)
       exit hres
       #); 
     AddRef:< (#do INNER #);
     Release:< (# do INNER #);       
     Query: 
       (# theIID: ^IID;  
          Iptr: ^IUnknown;
          hr: @HRESULT;
       enter theIID[]
       do (if ((@@theIID.Binary[1], @@Iptr)->QueryInterface->hr).failed then
              NONE->Iptr[];
          if);
       exit Iptr[]
       #); 
  #);

IDispatch: IUnknown
  (# 
     GetTypeInfoCount:<
       (# 
       #);
     GetTypeInfo:<
       (# 
       #);
     GetIDsOfNames:<
       (# 
       #); 
     Invoke:<
       (# 
       #);
  #);

CoInitialize: external
  (# LPVoid,hresult: @Integer;
  enter LPVoid
  do CallStd; 
  exit hresult
  #);
CoUninitialize: external
  (# 
  do CallStd;
  #);


CoCreateInstance:
  (# theclsid: ^CLSID; 
     outer: ^IUnknown;
     Flags: @Integer;
     theiid: ^IID;
     Iptr: ^IUnknown;
     hr: @HRESULT;
     CoCreateInstance: external
       (# pUnkOuter: ^IUnknown;
          rclsid, dwClsContext, riid, ppv, hr: @Integer;
       enter (rclsid, pUnkOuter[], dwClsContext, riid, ppv)
       do callStd
       exit hr
       #);
  enter (theclsid[], outer[], Flags, theiid[])
  do (@@theclsid.Binary[1], outer[], Flags, @@theiid.Binary[1], @@Iptr)
       -> CoCreateInstance->hr;
     (if hr.failed then
         ('cocreateinstance failed', hr.value) -> ComErrorMessage;
         NONE->Iptr[];
     if);
  exit Iptr[]
  #);
          
CLSCTX_INPROC_SERVER: (# exit 1 #);

ComErrorMessage: external
  (# msg: [0]@char;
     hr: @integer;
  enter (msg, hr)
  do CallC
  #)

