ORIGIN '../astlevel';
INCLUDE '../betacfl';
INCLUDE '../grammarinit';
INCLUDE '~beta/persistentstore/persistentstore';

-- program: descriptor --
(#
   mps: @astInterface;
   beta: @mps.beta;
   
   group: ^astInterface.fragmentGroup;
   ps: @PersistentStore;
   name: ^text;
   names: @list
     (# element:: text;
     #);
   printNames:
     (# exp: ^astInterface.expanded;
     enter exp[]
     do 'scanning' -> putline;
        beta.nameDcl -> exp.suffixWalkForProd
        (# scanCat:: beta.nameDcl;
           nameDecl: ^astInterface.nameDecl;
        do current.getNameDecl -> nameDecl[];
           nameDecl.gettext -> names.append;
        #);
        'done' -> putline;
     #);
do ps.init;
   mps.astLevelInit;
   ('~beta/grammars/beta/beta','beta',screen[])
     ->beta.betagrammarInit
   (#
      MPSerror::
        (# do (failure,msg[])->stop #);
      noParserAvailable:: 
        (# do (failure,msg[])->stop #)
   #); 
   
   'load: ' -> puttext;
   getLine -> name[];
   name[] -> ps.openWrite;
   ('root', mps.fragmentGroup##) -> ps.get -> group[];
   group.fullname -> putline;
   group.scan
   (# frag: ^astInterface.FragmentForm;
   do current.fullname -> putline;
      current[] -> frag[];
      frag.root[] -> printNames;
   #);
#)

