ORIGIN '../astlevel';
INCLUDE '../betacfl';
INCLUDE '../grammarinit';
INCLUDE '~beta/sysutils/objinterface';


-- program: descriptor --
(#
   mps: @astInterface;
   beta: @mps.beta;
   prepare:
     (#
     do mps.astLevelInit;
        ('~beta/grammars/beta/beta','beta',screen[])
          ->beta.betagrammarInit
        (#
           MPSerror::
             (# do (failure,msg[])->stop #);
           noParserAvailable:: 
             (# do (failure,msg[])->stop #)
        #); 
     #);
   processFragment:
     (# frag: ^astInterface.FragmentForm;
        root: ^astInterface.Expanded;
     enter frag[]
     do frag.fullname -> putline;
        frag.root[] -> root[];
        (1, screen[], beta[]) -> root.dump;
        screen.newline;
        'info [' -> putline;
        (if frag.header[] <> NONE then
            frag.info.scanAll
            (#
            do ch -> putint;
               newline;
            #);
        if);
        ']' -> putline;
     #);
   processGroup:
     (# group: ^astInterface.FragmentGroup;
     enter group[]
     do group.scan
        (#
        do current[] -> processFragment;
        #);
     #);
   ProcessFile:
     (# name: ^Text;
     enter name[]
     do (name[], screen[]) -> mps.top.open -> processGroup;
     #);
do prepare;
   (if NoOfArguments = 2 then
       2 -> arguments -> ProcessFile;
   if);
#)
