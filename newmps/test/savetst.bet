ORIGIN '../astlevel';
INCLUDE '../betacfl';
INCLUDE '../grammarinit';
INCLUDE '~beta/persistentstore/persistentstore';

-- program: descriptor --
(#
   mps: @astInterface;
   beta: @mps.beta;
   
   group: ^astInterface.fragmentGroup;
   name: ^text;
   ps: @PersistentStore;
   names: @list
		(# element:: text;
		#);
   printNames:
		(# exp: ^astInterface.expanded;
		enter exp[]
		do 'scanning' -> putline;
			beta.nameDcl -> exp.suffixWalkForProd
				(# scanCat:: beta.nameDcl;
					nameDecl: ^astInterface.nameDecl;
				do current.getNameDecl -> nameDecl[];
					nameDecl.gettext -> names.append;
				#);
			'done' -> putline;
		#);
do ps.init;
	mps.astLevelInit;
   ('~beta/grammars/beta/beta','beta',screen[])
     ->beta.betagrammarInit
   (#
      MPSerror::
        (# do (failure,msg[])->stop #);
      noParserAvailable:: 
        (# do (failure,msg[])->stop #)
   #); 
   
   'parse: ' -> puttext;
   getLine -> name[];
   (name[], screen[]) -> mps.top.open -> group[];
	
   (if group[] <> NONE then
		group.scan
   	(# frag: ^astInterface.FragmentForm;
		do current.fullname -> putline;
			current[] -> frag[];
			frag.root[] -> printNames;
		#);
		name[] -> ps.create;
		(group[], 'root') -> ps.put;
		ps.close;
   else
   	  'failed to parse!' -> putline;
   if);

#)

