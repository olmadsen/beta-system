ORIGIN 'astlevel';


BODY 'private/dependencybody';


-- astInterfaceLib: attributes --


(* 
 * This a new implementation of DependencyGraph-building using real syntax
 * trees.
 *
 * NOTE: There are two versions of scanning the dependencygraph.
 *
 *       - The first version is "scanNonCompiled" this is used by
 *         the compiler and will only process the part of the
 *         the dependencygraph that needs attention by the compiler.
 *
 *       - The second version is "scanExtent" + "scanDomain". This
 *         processes the entire dependency graph and is used by tools.
 *         
 *       This fragment only implements the compilerversion.
 *)
DependencyGraph:
  (# <<SLOT DependencyGraphLib: attributes>>;
     TargetDirectory, TargetMachine: ^text;
     infoStream, messageStream, traceStream, errorStream: ^Stream;
	  
	  init:<
	    (#
		 enter (infoStream[], messageStream[], traceStream[], errorStream[])  
		 <<SLOT DependencyGraphInit: doPart>>
		 
		 #);
	  
	  scanNonCompiled: scanner
	    (# translate:<
		      (# fg: ^fragmentGroup
				enter fg[]
				do INNER
				#);
			 addTranslate::
			   (#
				<<SLOT DependencyGraphScanNonCompiledAddTranslate: doPart>>
				#);
		 <<SLOT DependencyGraphScanNonCompiled: doPart>>
		 #);
	   
	  scanner:
	    (# <<SLOT scannerLib: attributes>>;
		    giveDomain:< BooleanValue; 
		    init:< (# do INNER #);
			 beforeGroup:<
			   (* Called before the group is beeing processed. *)
			   (# fg: ^FragmentGroup;
				enter fg[]
				do INNER;
				#);
			 afterGroup:<
			   (* Called after the group has been processed. 
			    * If "insert" is true, the group will be
				 * added to a list of groups needing translation.
				 *)
			   (# fg: ^FragmentGroup;
				   insert: @boolean;
				enter fg[]
				do INNER;
				exit insert
				#);
		    addGroup:<
			   (# fg: ^FragmentGroup;
				enter fg[]
				do INNER;
				#);
		    addTranslate:<
			   (# fg: ^FragmentGroup;
				enter fg[]
				do INNER;
				#);
		    
			 buildProperty:<
			   (# objFile,directory,command: ^text; dependencies: [0]^text;
			   enter (objFile[], directory[], dependencies, command[])
			   do INNER
			   #);
			 currentProperty:<
			   (# propKind: @integer; arg: ^text
	         enter (propKind, arg[])
				do INNER;
				#);
			 
			 current: ^FragmentGroup;
	       root: ^Text;
	    enter root[]
		 do INNER;
		 #);
		 
		 
    (*
	  * The following is the marks used to mark what has been done
	  * to a group.
	  *)
	  
	 OpenMark: (# exit 0 #);     (* The group has just been openend *)
	 ExamineMark: (# exit 1 #);  (* The group is beeing examined *)
	 ProcessMark: (# exit 2 #);  (* The group has been processed (translated) *)
	 
    DGP: @<<SLOT DependencyGraphPrivate: descriptor>>;
	 
  #);
  