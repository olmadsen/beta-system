ORIGIN '~beta/basiclib/betaenv';

-- streamLib: attributes --

(* Operations for reading and writing "bytecode" data on
 * a stream.
 *
 * All the put operations uses the put operation on the 
 * stream.
 * All the get operatiuons uses the get operation on the
 * stream.
 * The data is written in a byte format that are endianess
 * independent. Also all data is unaligned.
 *
 *)

putInt8u:
  (# u8: @int8u;
     doPut: @put;
  enter u8
  do u8 -> doPut;
  #);
putInt16u:
  (# u16: @int16u;
     doPut: @put;
  enter u16
  do u16 %srl 8 -> doPut;
     u16 %band 0xFF -> doPut;
  #);
putInt32u:
  (# u32: @int32u;
     doPut: @put;
  enter u32
  do u32 %srl 24 -> doPut;
     (u32 %srl 16) %band 0xFF -> doPut;
     (u32 %srl 8) %band 0xFF -> doPut;
     u32  %band 0xFF -> doPut;
  #);
putBinText:
  (# txt: ^text;
     doPut: @putInt8u;
     doPutInt32u: @putInt32u;
  enter txt[]
  do txt.lgth -> doPutInt32u;
     txt.scanAll
     (#
     do ch -> doPut;
     #);
  #);
getInt8u:
  (# doGet: @get;
     u8: @int8u;
  do doGet -> u8;
  exit u8
  #);
getInt16u:
  (# doGet: @get;
     u16: @int16u;
  do (doGet %sll 8) %bor doGet -> u16;
  exit u16
  #);
getInt32u:
  (# doGet: @get;
     u32: @int32u;
  do (doGet %sll 24) %bor (doGet %sll 16) %bor (doGet %sll 8) %bor doGet -> u32;
  exit u32
  #);
getBinText:
  (# doGetInt32u: @getInt32u;
     doGet: @getInt8u;
     txt: ^Text;
     lgth: @int32u;
     pos: @integer;
  do &Text[] -> txt[];
     doGetInt32u -> lgth;
     lgth -> txt.T.new;
     1 -> pos;
     (for lgth repeat
          doGet -> txt.T[pos];
          pos + 1 -> pos;
     for);
     lgth -> txt.lgth;
  exit txt[]
  #);
