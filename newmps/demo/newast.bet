ORIGIN '~beta/newmps/astlevel';

INCLUDE '~beta/newmps/betacfl';
INCLUDE '~beta/newmps/grammarinit';

-- program: descriptor --
(# mps: @astInterface;
   beta: @mps.beta;
   prepare:
     (#
     do mps.astLevelInit;
        ('~beta/grammars/beta/beta','beta',screen[])
          ->beta.betagrammarInit
        (#
           MPSerror::
             (# do (failure,msg[])->stop #);
           noParserAvailable:: 
             (# do (failure,msg[])->stop #)
        #); 
     #);
   group: ^astInterface.FragmentGroup;
   form: ^astInterface.FragmentForm;
   
   createAttributesForm:
     (# new: ^astInterface.beta.AttributesForm;
     do (beta.attributesform, form[]) -> beta.newAst -> new[];
        createAttributes -> new.putAttributes;
     exit new[]
     #);
   createAttributes:
     (# new: ^astInterface.beta.Attributes;
     do (beta.attributes, form[]) -> beta.newAst -> new[];
        (1, createPatternDecl) -> new.put;
     exit new[]
     #);
   createPatternDecl:
     (# new: ^astInterface.beta.PatternDecl;
     do (beta.PatternDecl, form[]) -> beta.newAst -> new[];
        createNames -> new.putNames;
        createObjectDescriptor -> new.putObjectDescriptor;
     exit new[]
     #);
   createNames:
     (# new: ^astInterface.AST;
     do beta.Names -> beta.newUnExpanded -> new[];
     exit new[]
     #);
   createObjectDescriptor:
     (# new: ^astInterface.AST;
     do beta.objectDescriptor -> beta.newUnExpanded -> new[];
     exit new[]
     #);
do prepare;
   mps.newFragmentGroup -> group[];
   'foo' -> group.fullname;
  
   beta[] -> mps.newFragmentForm -> form[];
   form[] -> group.fragmentlist.addFragment;
   'program' -> form.name;
   createAttributesForm -> form.root[];
   
   group.markAsChanged;
#)
