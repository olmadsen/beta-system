ORIGIN '~beta/newmps/astlevel';

INCLUDE '~beta/newmps/betacfl';
INCLUDE '~beta/newmps/grammarinit';

-- program: descriptor --
(# mps: @astInterface;
   beta: @mps.beta;
   prepare:
     (#
     do mps.astLevelInit;
        ('~beta/grammars/beta/beta','beta',screen[])
          ->beta.betagrammarInit
        (#
           MPSerror::
             (# do (failure,msg[])->stop #);
           noParserAvailable:: 
             (# do (failure,msg[])->stop #)
        #); 
     #);
   group: ^astInterface.FragmentGroup;
   form: ^astInterface.FragmentForm;
   
   createAttributesForm:
     (# new: ^astInterface.beta.AttributesForm;
     do (beta.attributesform, form[]) -> beta.newAst -> new[];
        createAttributes -> new.putAttributes;
     exit new[]
     #);
   createAttributes:
     (# new: ^astInterface.beta.Attributes;
     do (beta.attributes, form[]) -> beta.newAst -> new[];
        (1, createPatternDecl) -> new.put;
     exit new[]
     #);
   createPatternDecl:
     (# new: ^astInterface.beta.PatternDecl;
     do (beta.PatternDecl, form[]) -> beta.newAst -> new[];
        createNames -> new.putNames;
        createObjectDescriptor -> new.putObjectDescriptor;
     exit new[]
     #);
   createNames:
     (# new: ^astInterface.beta.Names;
     do (beta.Names, form[]) -> beta.newAst -> new[];
        (1, 'Point' -> createName) -> new.put;
     exit new[]
     #);
   createName:
     (# name: ^Text;
        new: ^astInterface.beta.NameDcl;
        theNameDecl: ^astInterface.NameDecl;
     enter name[]
     do (beta.nameDcl, form[]) -> beta.newAst -> new[];
        (mps.nameDecl, form[]) -> beta.newAst -> theNameDecl[];
        name[] -> theNameDecl.puttext;
        theNameDecl[] -> new.putNameDecl;
     exit new[]
     #);
   createObjectDescriptor:
     (# new: ^astInterface.beta.ObjectDescriptor;
     do (beta.objectDescriptor, form[]) -> beta.newAst -> new[];
        createOptional -> new.putPrefixOpt;
        createMainPart -> new.putMainPart;
     exit new[]
     #);
   createOptional:
     (# new: ^astInterface.Optional;
     do (mps.prodno.optional, form[]) -> beta.newAst -> new[];
     exit new[]
     #);
   createMainPart:
     (# new: ^astInterface.beta.MainPart;
     do (beta.mainPart, form[]) -> beta.newAst -> new[];
        createEmptyAttributes -> new.putAttributes;
        createActionPart -> new.putActionPart;
     exit new[]
     #);
   createEmptyAttributes:
     (# new: ^astInterface.beta.Attributes;
     do (beta.Attributes, form[]) -> beta.newAst -> new[];
        (1, createOptional) -> new.put;
     exit new[]
     #);
   createActionPart:
     (# new: ^astInterface.beta.ActionPart;
     do (beta.ActionPart, form[]) -> beta.newAst -> new[];
        createOptional -> new.putEnterPartOpt;
        createOptional -> new.putDoPartOpt;
        createOptional -> new.putExitPartOpt;
     exit new[]
     #);
do prepare;
   mps.newFragmentGroup -> group[];
   'foo' -> group.fullname;
   
   'ORIGIN' -> group.prop.addProp
   (#
   do '~beta/basiclib/betaenv' -> addstring;
   #);
   beta[] -> mps.newFragmentForm -> form[];
   form[] -> group.fragmentlist.addFragment;
   'lib' -> form.name;
   createAttributesForm -> form.root[];
   
   group.markAsChanged;
#)
