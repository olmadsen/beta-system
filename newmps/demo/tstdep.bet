ORIGIN '~beta/dependency/scanNonCompiled';
INCLUDE '~beta/dependency/betaast';
INCLUDE '~beta/postwimp/foundation/private/heapbody';
-- program: descriptor --
(# root: ^Text;
   
   mps: @astInterface;
   beta: @mps.beta;
   dep: @mps.dependencyGraph;
   
   GroupList: List
     (# element:: mps.FragmentGroup;
     #);
   groups: ^GroupList;
   
   prepare:
     (#
     do mps.astLevelInit;
        ('~beta/grammars/beta/beta','beta',screen[])
          ->beta.betagrammarInit
        (#
           MPSerror::
             (# do (failure,msg[])->stop #);
           noParserAvailable:: 
             (# do (failure,msg[])->stop #)
        #); 
        dep.init;
     #);
   collect:
     (#
     do &GroupList[] -> groups[];
        root[] -> dep.scanNonCompiled
        (# translate::
             (#
             do fg[] -> groups.append;
             #);
        do '.' -> put;
        #);
        newline;
     #);
   print:
     (#
     do groups.scan
        (#
        do current.fullname -> puttext;
           '!' -> putline;
        #);
     #);
   realOpen:
     (# countNameDcls:
          (# frag: ^astInterface.FragmentForm;
             exp: ^astInterface.Expanded;
             count: @integer;
          enter frag[]
          do frag.root[] -> exp[];
             beta.nameDcl -> exp.suffixWalkForProd
             (#
             do count + 1 -> count;
             #);
             frag.fullname -> puttext;
             ' : ' -> puttext;
             count -> putint;
             newline;
          #);
     do groups.scan
        (# count: @integer;
        do current.realopen;
           current.scan
           (#
           do current[] -> countNameDcls;
           #);
           current.close;
           scanHeap
             (# type:: MPS.Ast;
             do (*** count + 1 -> count; **)
             #);
           'COUNT: ' -> puttext;
           count -> putint;
           newline;
        #);
     #);
   work:
     (#
     do collect;
        print;
        realOpen;
     #);
do (if NoOfArguments = 2 then
       2 -> arguments -> root[];
    else
       '~beta/xcompiler/TST/tst' -> root[];
   if);
   prepare;
   work;
   cycle (# #);
#)
