ORIGIN '../index';
INCLUDE '~beta/prettyprinter/astviewer';

-- PrettyPrinterBody: doPart --
do (if false then
       (# pp: ^AstViewer;
          path: ^Text;
          
          output: @Printer
            (# mark: @integer;
               errorBuffer: @Text;
               putString::
                 (# 
                 do string[] -> st.puttext; 
                    mark + string.length -> mark;
                 #);
               blank::
                 (#
                 do ' ' -> st.put;
                    mark + 1 -> mark;
                 #);
               newline::
                 (#
                 do (if errorBuffer.length > 0 then
                        errorBuffer[] -> st.puttext;
                        errorBuffer.clear;
                     else
                        st.newline;
                    if);
                    0 -> mark;
                 #);
               putError::
                 (#
                 do errorBuffer.newline;
                    (for mark repeat
                         ' ' -> errorbuffer.put;
                    for);
                    '^' -> errorBuffer.put;
                    errorBuffer.newLine;
                    '***** ' -> errorBuffer.putText;
                    error -> semanticErrorText -> errorBuffer.putText;
                    errorBuffer.newLine;
                 #);
            #);
          
       do &AstViewer[] -> pp[];
          '~beta/grammars/beta/beta.pspec' -> expandToFullPath -> path[];
          ('beta', path[]) -> pp.init;
          (a[], output[]) -> pp.printOn;
          output.newline;
       #);
    else
       (a[], st[]) -> grammartable.default.prettyAsIs;
   if);
