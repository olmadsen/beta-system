ORIGIN '../getslotbinding';

-- lib: attributes --
debug: (# exit false #);

---getSlotBinding_scan:doPart---
do head[]->R[];
   L:
     (if (R[] <> none ) then
         R.F[]->ThisGroup[]; 
         INNER ; 
         R.succ[]->R[]; 
         restart L
     if)
--getSlotBinding_domain:doPart--
do Insert:
     (# R: ^Elm
     do false->inserted;
        head[]->R[];
        L:
          (if (R[] <> none ) then
              (if R.F[] = F[] then leave insert if);
              R.succ[]->R[];
              restart L
          if);
        &Elm[]->R[];
        F[]->R.F[];
        head[]->R.succ[];
        R[]->Head[];
        True->inserted;
        (if debug then
            'marked: '->screen.putText;
            f.fullname->screen.putline;
        if);

     #)
--getSlotBindingDoPart:dopart---
do (# Find:
        (# F: ^Fragment; G: ^FragmentGroup
        enter F[]
        do (if (F[] <> none ) then
               (if F.FragType = groupType then
                   (if (F[]->G[]->domain) then
                       G.scanIncludes
                       (# F1: ^Fragment; fn: ^text
                       do current.fullname->fn[];
                          fn[] -> top.grouptable.find -> F1[];
                          (if F1[] = NONE then
                              (fn[],screen[])->top.open->F1[];
                          if);
                          F1[] -> &Find
                       #);
                       G.origin->&Find
                   if)
               if)
           if)
        #);
      sl: ^slotDesc
   do this(unExpanded).theSlot -> sl[];
      (if debug then
          'GetBinding of '->screen.putText;
          sl.name -> putline;
          ' for '->screen.putText;
          g.fullName->screen.putline;
      if);
      G[]->&Find;
      
      domain.Scan (* scan visible groups for forms *)
      (#
      do (if debug then
             'slots: group=' -> puttext;
             thisGroup.name -> putline;
         if);
         thisGroup.Scan
         (# F: ^FragmentForm; B: ^SlotDesc
         do (if debug then
                'slots: frag=' -> puttext;
                current.name -> putline;
            if);
            (if current.FragType = FormType then
                (if debug then
                    'slots: form' -> putline;
                if);
                current[]->f[];
                f.binding[]->b[];
                (if (b[] <> none ) then
                    (if debug then
                        'slots: binding=' -> puttext;
                        b.name -> putline;
                    if);
                    (if (b[]->sl.equal) then current[]->&found if)
                 else
                    (if debug then
                        'slots: binding is NONE' -> putline;
                    if);
                if)
            if)
         #)
      #)

   #)
