ORIGIN '~beta/mps/astlevel';

-- astInterfaceLib: attributes --


Dependency:
  (# ProcessMark: (# exit 1 #);
	  
     machine: ^text;
     name: ^Text;
     bodyqueue: @ContainerList
       (# Element:: FragmentGroup;
       #);
     doGroup:<
       (# current: ^FragmentGroup;
       enter current[]
       do INNER;
       #);
     ProcessGroupByName:
       (# name: ^Text;
       enter name[]
       do (name[], screen[]) -> top.open -> ProcessGroup;
       #);
     ProcessBodyByName:
       (# name: ^text;
          group: ^FragmentGroup;
       enter name[]
       do (name[], screen[]) -> top.open -> group[];
          (if group[] <> NONE then
              (if group.controller.status <> ProcessMark then
                  group[] -> bodyqueue.append;
              if);
          if);
       #);
     ProcessGroup:
       (# group: ^FragmentGroup;
       enter group[]
       do (if group[] <> NONE then
              (if group.controller.status <> ProcessMark then
                  (if group.origin = NONE then
                      group.setupOrigin;
                  if);
                  group.origin -> ProcessGroup;
                  group.scanIncludes
                  (#
                  do current.fullname -> ProcessGroupByName;
                  #);
                  ProcessMark -> group.controller.status;
                  group[] -> doGroup;
                  
                  group.prop.scanProp
                  (#
                     doProp::
                       (# t: ^text;
                          on: @boolean;
                       do prop.makelc;
                          (if true
                           //'body' -> prop.equal then
                              
                              group.fullname -> stripPathName -> t[];
                              (if t[] = NONE then
                                  currentDirectory -> t[];
                              if);
                              scanParameters
                              (# doString::
                                   (#
                                   do 'BODY: ' -> puttext;
                                      (s[], t[]) -> thePathHandler.convertFilePath -> ProcessBodyByName;
                                   #)
                              #);
                              
                           //'mdbody' -> prop.equal then
                              group.fullname -> stripPathName -> t[];
                              false -> on;
                              scanParameters
                              (# doName::
                                   (#
                                   do (if machine[] -> n.equal then
                                          true -> on;
                                       else
                                          false -> on;
                                      if);
                                   #);
                                 doString::
                                   (#
                                   do (if on then
                                          (s[], t[]) -> thePathHandler.convertFilePath -> ProcessBodyByName;
                                      if);
                                   #)
                              #);
                          if);
                       #);
                  #);
              if);
          if);
       #);
  enter name[]
  do 'ppcmac' -> machine[];
     name[] -> ProcessGroupByName;
     loop:
       (if not bodyqueue.empty then
           (bodyqueue.head -> bodyqueue.delete).elm[] -> ProcessGroup;
           restart loop;
       if);
  #);
