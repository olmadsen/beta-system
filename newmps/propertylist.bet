ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/containers/list';

-- lib: attributes --
ConstValue: (# exit 1 #);
NameValue: (# exit 2 #);
StringValue: (# exit 3 #);

ParameterList: List
  (# element:: Parameter;
  #);
Property:
  (# name: ^text;
     parameters: @ParameterList;
     
     addParameter:
       (# par: ^Parameter;
          value: ^Text;
       enter value[]
       do &Parameter[] -> par[];
          INNER;
          value[] -> par.value[];
          par[] -> parameters.append;
       exit par[]
       #);
     addString: addParameter
       (#
       do StringValue -> par.type;
       #);
     addName: addParameter
       (#
       do NameValue -> par.type;
       #);
     addConst: addParameter
       (#
       do ConstValue -> par.type;
       #);
     
     print:
       (#
       do name[] -> puttext;
          parameters.scan
          (#
          do ' ' -> put;
             current.print;
          #);
          
       #);
     
  #);
Parameter:
  (# value: ^text;
     type: @integer;
     print:
       (#
       do (if type
           //StringValue then
              '\'' -> put;
              value[] -> puttext;
              '\'' -> put;
           else
              value[] -> puttext;
          if);
       #);
  #);
PropertyList:
  (# properties: @list
       (# element:: Property;
       #);
     addProp:
       (# name: ^Text;
          prop: ^Property;
       enter name[]
       do &Property[] -> prop[];
          name[] -> prop.name[];
          prop[] -> properties.append;
       exit prop[]
       #);
     scanProp:
       (# currentParList: ^ParameterList;
          doProp:< 
            (# prop: ^text;
               getName: 
                 (# notAName:< (# do INNER #);
                    name: ^text;
                 do 
                 exit name[]
                 #);
               getConst: 
                 (# notAConst:< (# do INNER #);
                    const: @integer
                 do 
                 exit const
                 #);
               getString: 
                 (# notAString:< (# do INNER #);
                    string: ^text
                 do 
                 exit string[]
                 #);
               scanParameters: 
                 (# doConst:< (# c: @integer enter c do INNER #);
                    doString:< (# s: ^text enter s[] do INNER #);
                    doName:< (# n: ^text enter n[] do INNER #);
                 do currentParList.scan
                    (#
                    do (if current.type
                        //ConstValue then
                           current.value.asInt -> doConst;
                        //StringValue then
                           current.value.copy -> doString;
                        //NameValue then
                           current.value.copy -> doName;
                       if);
                    #);
                 #)
               
            enter prop[]
            do INNER
            #);
       do properties.scan
          (#
          do current.parameters[] -> currentParList[];
             current.name.copy -> doProp;
          #);
       #);
     print:
       (#
       do properties.scan
          (#
          do current.print;
             ';' -> putline;
          #);
       #);
  #);
