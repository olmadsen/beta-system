ORIGIN '../newindex'; 
LIB_ITEM 'betaastindex';
INCLUDE '~beta/mps/private/astparser';
INCLUDE '~beta/pretty/astviewer';
INCLUDE '../semanticerrortext';
-----prettyPrinterBody: descriptor-----
(# xx: @astViewer;
   outputStream: @xx.output
     (# errorBuffer: @text;
        mark: @integer;
        
        put::< 
          (# 
          do ch->st.put;
             mark+1 -> mark;
          #);
        putText::< 
          (# 
          do s[]->st.putText;
             mark + s.length -> mark;
          #);
        putInt::< 
          (# t: ^text;
          do &text[] ->t[];
             n -> t.putint;
             mark + t.length -> mark;
             t[]->st.puttext;
          #);
        newLine::< 
          (# 
          do (if errorBuffer.length > 0 then
                 errorBuffer[] -> st.putText;
                 errorBuffer.clear;
              else
                 st.newline;
             if);
             0 -> mark;
          #);
        printError::< 
        (# 
        do errorBuffer.newline;
           (for mark-1 repeat
                ' ' -> errorbuffer.put;
           for);
           '^' -> errorBuffer.put;
           errorBuffer.newLine;
           '***** ' -> errorBuffer.putText;
           theAst.semanticError->semanticErrorText->errorBuffer.putText;
           errorBuffer.newLine;
        #)
     #);
   initialized: @ boolean; (* OLM 91121 *)
do (if not initialized then
       (* for grammar change, see the comment below;
        * here it is only pretty-printer tables that are defined.
        * Search for "'~beta/grammars/beta"
        *)
       '~beta/grammars/beta/beta'->xx.pp.init;
       true->initialized
   if);
       
   (if a[] = none then 'NONE \n'->st.putText
   else
       outputStream[]->xx.out[]; 
       outputStream.init;
       editorMode->xx.pp.editorMode;
       80->xx.pp.width;
       (a[],level)->xx.pp.present;
   if)
#)
