ORIGIN 'index'
(* sematic operations for BETA;
 * extracted form index.bet in order
 * to make it easier to distinguish
 * index.bet from mps/astlevel.bet
 *)
---ASTindexLib:attributes---
descNo:
  (#
  enter (# v: @integer enter v do (v,1)->putPossibleSlot #)
  exit 1->getPossibleSlot
  #);
descId:
  (# inx1: @integer; f: ^fragment
  enter ((inx1,f[]),descNo)
  exit
     (# t: ^text; p: @integer; gram: @grammar
     do
        &text[]->t[];
        (if frag.root.symbolInx = gram.attributesForm then
            frag.father->f[];
            f.name->t[];
            thePathHandler.directoryChar->t.FindAll(#  do inx->P #);
            (P+1,t.length)->t.sub->t[]
         else
	    (*//gram.DescriptorForm//doPart//mainPart*)
            frag.nameT->t
        if)
     exit (t[],descNo)
     #)
  #);
origin:
  (# f: ^fragmentForm
  enter (frag.a[index+offset.attribute+1],f[])
  exit (frag.a[index+offset.attribute+1],frag[])
  #);
slotOrigin:
  (#
  enter
     (# inx1: @integer; f: ^fragmentForm
     enter (inx1,f[])
     do (inx1,2)->putPossibleSlot
     #)
  exit (2->getPossibleSlot,frag[])
  #);
Xorigin:
  (#
  enter
     (# inx1: @integer; f: ^fragmentForm
     enter (inx1,f[])
     do ((inx1,f[]),2)->putNodePossibleSlot
     #)
  exit 2->getNodePossibleSlot
  #);
size:
  (#
  enter (# v: @integer enter v do (v,3)->putPossibleSlot #)
  exit 3->getPossibleSlot
  #);
attSize: (* must be eliminated *)
  (#
  enter (# v: @integer enter v do (v,4)->putPossibleSlot #)
  exit 4->getPossibleSlot
  #);
virtSize:
  (#
  enter (# v: @integer enter v do (v,4)->putPossibleSlot #)
  exit 4->getPossibleSlot
  #);
kind:
  (#
  enter (# v: @integer enter v do (v,5)->putPossibleSlot #)
  exit 5->getPossibleSlot
  #);
dclRoot:
  (#
  enter
     (# inx1: @integer; f: ^fragmentForm
     enter (inx1,f[])
     do (inx1,6)->putPossibleSlot
     #)
  exit (6->getPossibleSlot,frag[])
  #);
lib:
  (#
  enter
     (# inx1: @integer; f: ^fragmentForm
     enter (inx1,f[])
     do (inx1,7)->putPossibleSlot
     #)
  exit (7->getPossibleSlot,frag[])
  #);
next:
  (#
  enter
     (# inx1: @integer; f: ^fragmentForm
     enter (inx1,f[])
     do (inx1,1)->putPossibleSlot
     #)
  exit (1->getPossibleSlot,frag[])
  #);
originOff:
  (#
  enter (*(# v: @integer enter v do (v,8)->putPossibleSlot #)*)
     (# v: @integer enter v
     do (v,1)->frag.a[index+offset.attribute+7].%putShort
     #)
  exit (*8->getPossibleSlot*)
     1 -> frag.a[index+offset.attribute+7].%getShort
  #);
returnOff:
  (#
  enter (*(# v: @integer enter v do (v,8)->putPossibleSlot #)*)
     (# v: @integer enter v
     do (v,0)->frag.a[index+offset.attribute+7].%putShort
     #)
  exit (*8->getPossibleSlot*)
     0 -> frag.a[index+offset.attribute+7].%getShort
  #);
sort:
  (# s: @ASTindex
  enter s
  exit
     (# gram: @grammar
     do
        father->s;
        (* No. IT is not always the grandfather.
         * In a labelledImp it is the father *)
        (if s.label <> gram.labelledImp then s.father->s if)
     exit s
     #)
  #);
evalKind: (* att no. 1!! *)
  (#
  enter frag.a[index+offset.attribute]
  exit frag.a[index+offset.attribute]
  #);
insOff: (* att no. 2 !! *)
  (#
  enter frag.a[index+offset.attribute+1]
  exit frag.a[index+offset.attribute+1]
  #);
NXoff:
  (#
  enter frag.a[index+offset.attribute]
  exit frag.a[index+offset.attribute]
  #);
NXsize:
  (#
  enter frag.a[index+offset.attribute+1]
  exit frag.a[index+offset.attribute+1]
  #);
access:
  (#
  enter
     (# v: @integer
     enter v
     do (@@ frag.a[index+offset.attribute],v)->tos'%putByte[0]'
     #)
  exit frag.a[index+offset.attribute]->tos'%getByte[0]'
  #);
off:
  (#
  enter
     (# v: @integer;
     enter v
     do (@@ frag.a[index+offset.attribute],v)->tos'%putBits[8,24]'
     #)
  exit frag.a[index+offset.attribute]->tos'%getBits[8,24]'
  #);
left:
  (# inx1: @integer
  enter
     (# f: ^fragmentForm
     enter (inx1,f[])
     do
        (@@ frag.a[index+offset.attribute+1],inx1->tos'%shiftRight[1]')
          ->tos'%putShort[0]'
     #)
  exit
     (frag.a[index+offset.attribute+1]->tos'%getShort[0]'->tos'%shiftLeft[1]',
     frag[])
  #);
right:
  (# inx1: @integer
  enter
     (# f: ^fragmentForm
     enter (inx1,f[])
     do
        (@@ frag.a[index+offset.attribute+1],inx1->tos'%shiftRight[1]')
          ->tos'%putShort[1]'
     #)
  exit
     (frag.a[index+offset.attribute+1]->tos'%getShort[1]'->tos'%shiftLeft[1]',
     frag[])
  #);
virtDcl:
  (#
  enter
     (# b,f: @ASTindex
     enter b
     do father->f; f.father->f; ((b.index,b.frag[]),1)->f.putNodeAttribute
     #)
  exit
     (# f: @ASTindex
     do father->f; f.father->f
     exit 1->f.getNodeAttribute
     #)
  #);
restartAdr:
  (#
  enter
     (# v: @integer
     enter v
     do (@@ frag.a[index+offset.attribute+1],v)->tos'%putShort[0]';
     #)
  exit frag.a[index+offset.attribute+1]->tos'%getShort[0]'
  #);
leaveAdr:
  (#
  enter
     (# v: @integer
     enter v
     do (@@ frag.a[index+offset.attribute+1],v)->tos'%putShort[1]';
     #)
  exit frag.a[index+offset.attribute+1]->tos'%getShort[1]'
  #);
dclRef:
  (#
  enter (frag.packInxToValue->frag.a[index+offset.attribute])
  exit frag.a[index+offset.attribute]->frag.valueToIndex
  #);
on:
  (#
  enter frag.a[index+offset.attribute+1]
  exit frag.a[index+offset.attribute+1]
  #);
pn:
  (# f: ^fragmentForm
  enter
     (# V: @integer
     enter V
     do (V,0,16) -> frag.a[index+offset.attribute+2].%putBits
        (*(@@ frag.a[index+offset.attribute+2],V)->tos'%putBits[0,16]'*)
     #)
  exit (0,16) -> frag.a[index+offset.attribute+2].%getBits
     (* frag.a[index+offset.attribute+2]->tos'%getbits[0,16]'*)
  #);
onForThis:
  (* to put/get recomputed on for this(p) *)
  (#
  enter
     (# V: @integer
     enter V
     do (V,16,16) -> frag.a[index+offset.attribute+2].%putBits
     #)
  exit (16,16) -> frag.a[index+offset.attribute+2].%getBits
  #);
descRef:
  (#
  enter (frag.packInxToValue->frag.a[index+offset.attribute+3])
  exit frag.a[index+offset.attribute+3]->frag.valueToIndex
  #);
dscRef: (* to replace descRef *)
  (#
  enter (frag.packInxToValue->frag.a[index+offset.attribute+3])
  exit frag.a[index+offset.attribute+3]->frag.valueToIndex
  #);
