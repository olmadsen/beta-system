origin 'semanticerrortext';
include '~beta/basiclib/file'
---program:descriptor---
(# printT:
     (# T: ^text
     enter T[]
     do T.scanAll
        (#
        do (if ch = ascii.newline then
               '<br>\n   ' -> msg.puttext
            else
               ch -> msg.put
           if);
        #)
     #);
   msg: @file;
   T: ^text
do 'messages.html' -> msg.name;
   msg.openWrite;
   
   '<html>' -> msg.putline;
   
   '<h2>Semantic Errror Messages</h2>' -> msg.putline;
   '<DL>' -> msg.putline;
   (For i: lastMsgNo repeat
        (if not(i -> isWarning) then
            i 
              -> semanticErrortext 
            (# noMsg::(#do true -> skip #)#)
            -> T[];
            (if t.length > 0 then
                '<DT>Error  ' -> msg.putline;
                i -> msg.putint;
                ': <DD><tt>' -> msg.puttext;
                T[] -> printT;
                '</tt>\n' -> msg.puttext
            if)
        if)
   for);
   '</DL>' -> msg.putline;

   '<h2>\nSemantic Warnings</h2>' -> msg.putline;   
    '<DL>' -> msg.putline;
   (For i: lastMsgNo repeat
        (if (i -> isWarning) then
            i 
              -> semanticErrortext 
            (# noMsg::(#do true -> skip #)#)
            -> T[];
            (if t.length > 0 then
                '<DT>Warning  ' -> msg.putline;
                i -> msg.putint;
                ': <DD><tt>' -> msg.puttext;
                T[] -> printT;
                '</tt>\n' -> msg.puttext
            if)
        if)
   for);
   
   '</ul>' -> msg.putline;
   
   '</html>' -> msg.putline;
   msg.close
#)
