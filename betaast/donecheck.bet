ORIGIN '~beta/basiclib/v1.4/betaenv';
INCLUDE '~beta/mps/v5.0.1/astlevel';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1986-1992
 *       All rights reserved.
 *)
--- lib: attributes ---
GetProp: 
  (# AST: ^ASTInterface;
     FG: ^AST.FragmentGroup; 
     P: @Text; 
     res: @integer;
     Txt: ^Text;
  enter(P,AST[],FG[])
  do &Text[]->Txt[];
     L: FG.prop.scanProp
       (# doProp::< 
            (# do
               (if (P[]->Prop.equal) // true then
                   scanParameters
                   (# doConst::< (# do C -> res; leave L #);
                      doString::< (# do S[] -> Txt[]; leave L #)
                   #);
               if);
            #);
       #);
     INNER  (* ugly *)
  exit res
  #);
setDoneCheckProperty: 
  (# doneCheck: @boolean; 
     AST: ^ASTInterface;
     FG: ^AST.fragmentGroup;
     propvalue: @integer;
  enter(doneCheck,AST[],FG[])
  do (if donecheck
      //true then 1 -> propValue   
      //false then 0 -> propValue
     if);
     'donecheck' -> FG.prop.addprop(# do propValue -> addConst #); 
  #);
getDoneCheckProperty: 
  (# AST: ^ASTInterface;
     FG: ^AST.fragmentGroup;
  enter(AST[],FG[]) 
  exit (1 = (('donecheck',AST[],FG[]) -> getProp))
  #);

setGroupIDProperty: 
  (# doneCheck: @boolean; 
     AST: ^ASTInterface;
     FG: ^AST.fragmentGroup;
     value: ^Text;
  enter(value[],AST[],FG[])
  do 'unique_group_id' -> FG.prop.addprop(# do value[] -> addString #); 
  #);
getGroupIDProperty: 
  (# AST: ^ASTInterface;
     FG: ^AST.fragmentGroup;
     value: ^Text;
  enter(AST[],FG[]) 
  do ('unique_group_id',AST[],FG[]) -> getProp(# do Txt[]->value[] #);
     (if value.length//0 then NONE->value[] if);
  exit value[]
  #)

