ORIGIN '~beta/guienv/guienv';

BODY 'private/movieplayerbody';


-- guienvLib: attributes --

movie:
  (* A movie represents quicktime movies that can be
   * played in a moviePlayer.
   *)
  (# <<SLOT movieLib: attributes>>;
     read:
       (* Read the specified movie file. *)
       (# theFileName: ^text;
       enter theFileName[]
       <<SLOT movieRead: doPart>>
       #);
     userSelectFile:
       (* Let the user select a movie file in a standard file
        * dialog.
        *)
       (# success: @boolean;
       <<SLOT movieUserSelectFile: doPart>>
       exit success
       #);
     duration:
       (* Return the duration of the movie in milliseconds *)
       (# theDuration: @integer;
       <<SLOT movieDuration: doPart>>
       exit theDuration
       #);
     fileName:
       (* The name of the movie file. *)
       (# theFileName: ^text;
       <<SLOT movieFilename: doPart>>
       exit theFileName[]
       #);
     displaySize:
       (* The natural width and height of the movie. *)
       (# width, height: @integer;
       <<SLOT movieDisplaySize: doPart>>
       exit (width, height)
       #);
     private: @<<SLOT moviePrivate: descriptor>>;
  #);


-- windowLib: attributes --

moviePlayer: windowitem
  (# <<SLOT moviePlayerLib: attributes>>;
     controllerVisible:< (# value: @boolean; do TRUE -> value; INNER exit value#);
		  (* Furtherbind  controllerVisible to "false -> value" 
		   * to hide the controller.
			*)
     automaticResize:< (# value: @boolean; do TRUE -> value; INNER exit value#);
	  	  (* Furtherbind  automaticResize to "false -> value" 
		   * to prevent the moviePlayer from resizing autmatically
			* based on the natural size of the movie.
			*)
     contents:
       (* The movie that are selected in the moviePlayer. *)
       (#
       enter (# enter  theMovie[] <<SLOT moviePlayerSetContents: doPart>> #)
       exit theMovie[]
       #);
     read:
       (* Read the specified movie file. This is a shortcut for initialising a 
        * Movie.
        *)
       (# theFileName: ^text;
       enter theFileName[]
       <<SLOT movieplayerRead: doPart>>
       #);
     userSelectFile:
       (* Let the user select a movie file in a standard file
        * dialog.This is a shortcut for initialising a 
        * Movie.
        *)
       (# success: @boolean;
       <<SLOT movieplayerUserSelectFile: doPart>>
       exit success
       #);
     
     currentTime:
       (* The current time  is measured in milliseconds *)
       (# theTime: @integer;
       enter (# enter theTime <<SLOT moviePlayerSetCurrentTime: doPart>> #)
       exit (# <<SLOT moviePlayerGetCurrentTime: doPart>> exit theTime #)
       #);
     currentFrame:
       (* The current frame *)
       (# theFrame: @integer;
       enter (# enter theFrame <<SLOT moviePlayerSetCurrentFrame: doPart>> #)
       exit (# <<SLOT moviePlayerGetCurrentFrame: doPart>> exit theFrame #)
       #);
     
     play:
       (* Start playing. *)
       (# <<SLOT moviePlayerPlay: doPart>> #);
     stop:
       (* Stop playing. *)
       (# <<SLOT moviePlayerStop: doPart>> #);
     pause:
       (* Pause playing. *)
       (# <<SLOT moviePlayerPause: doPart>> #);
     open::<
       (# create::(# <<SLOT moviePlayerCreate:doPart >>#);
       <<SLOT moviePlayerOpen: doPart>>
       #);
     close::<
       (#
       <<SLOT moviePlayerClose: doPart>>
       #);
     eventHandler::<
       (# contentsChanged: event
            (# name:^text;
            enter name[]
            do INNER;
            #);
          onContentsChanged:< contentsChanged;
          
          onActivate::<
            (# <<SLOT moviePlayerOnActivate: doPart>> #);
          onDeactivate::<
            (# <<SLOT moviePlayerOnDeactivate: doPart>> #);
          onMouseDown::<
            (# <<SLOT moviePlayerOnMouseDown: doPart>> #);
          onRefresh::<
            (# <<SLOT moviePlayerOnRefresh: doPart>> #);
       #);
     theMovie: ^movie;
     private: @<<SLOT moviePlayerPrivate: descriptor>>;
  #);
