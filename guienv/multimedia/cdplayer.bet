ORIGIN '~beta/guienv/guienv';
BODY 'private/cdplayerbody';

-- guienvLib: attributes --


cdPlayer:
  (* This is an interface to controlling the CD player
   * build into the computer.
   *)
  (# <<SLOT cdPlayerLib: attributes>>;
     deviceError:< exception
       (# errMsg: ^text;
       enter errMsg[]
       do inner deviceError
       #);
     allocationError:< exception
       (# errMsg: ^text;
       enter errMsg[]
       do inner allocationError
       #);
     open:<
       (# <<SLOT cdPlayerOpen: doPart>> #);
     close:<
       (# <<SLOT cdPlayerClose: doPart>> #);
     cdPresent:
       (* Tells if a cd is present in the cd drawer. *)
       (# isCDPresent: @boolean;
       <<SLOT cdPlayerIsCDPresent: doPart>>
       exit isCDPresent
       #);
     play:
       (* Play starting at the specified track number. *)
       (# track: @integer;
       enter track
       <<SLOT cdPlayerStart: doPart>>
       #);
     stop:
       (* Stop playing, rewinding to the start of the CD *)
       (#
       <<SLOT cdStop: doPart>>
       #);
     pause:
       (* Pause playing. *)
       (#
       <<SLOT cdPause: doPart>>
       #);
     resume:
       (* Resume playing. *)
       (#
       <<SLOT cdPlayerResume: doPart>>
       #);
     duration:
       (* Return the total running time of the current CD in milliseonds. *)
       (# theDuration: @integer;
       <<SLOT cdPlayerDuration: doPart>>
       exit theDuration
       #);
     currentTime:
       (* This is the current time in milliseconds. *)
       (# theTime: @integer;
          theTrack:@integer;
       enter (# enter (theTrack,theTime) <<SLOT cdPlayerSetCurrentTime: doPart>> #)
       exit (# <<SLOT cdPlayerGetCurrentTime: doPart>> exit (theTrack,theTime) #)
       #);
     numberOfTracks:
       (* Return the number of tracks on the current CD. *)
       (# tracks: @integer;
       <<SLOT cdPlayerNumberOfTracks: doPart>>
       exit tracks
       #);
     private: @<<SLOT cdPlayerPrivate: descriptor>>;
  #);
