ORIGIN '../glenvbody';

INCLUDE '../../openGLXlib';
INCLUDE '~beta/guienv/private/X11/guienv_unixprivate';

LINKOPT sun4s '-lGLw -lGLU -lGL'
        sgi '-lGLw -lGLU -lGL -lXm -lXt -lX11 -lXext'
default '';

-- glCanvasCreate: doPart --
do INNER;
   (if widgetID = 0 then
       (# fatherWidget: @integer;
          options: [10] @integer;
          screen: @integer;
          args: @argList;
       do father.widgetID -> fatherwidget;
          fatherwidget -> XtDisplay -> private.GLdisplay;
          GLX_DOUBLEBUFFER -> options[1];
          GLX_RGBA -> options[2];
          0 -> options[3];
          
          (* choose openGL capable visual with depthbuffer *)
          private.GLdisplay -> XDefaultScreen -> screen;
          (private.GLdisplay,  screen, @@options[1])
            -> glXChooseVisual
            -> private.GLvisual;
          (if private.GLvisual=0 then
              'No appropriate visual found!'->putline;
              stop;
          if);
          (private.GLdisplay, private.GLvisual, 0, GL_TRUE)
            ->glXCreateContext
            ->private.GLcontext;
          (if private.GLcontext=0 then
              'Error creating openGL context!'->putline;
              stop;
          if);
          
          (1,GLwNvisualInfo,private.GLvisual)-> args.set;
          (2,GLwNallocateOtherColors,1)-> args.set;
          (3,GLwNallocateBackground,0)-> args.set; 
          ('glxcanvas',openGLWidgetClass,fatherwidget,args[],3)
            -> XtCreateManagedWidget
            -> private.glXwidget
            -> widgetID;
          
          
          private.glXWidget -> XtWindow -> private.GLwindow;
          
          (private.GLdisplay,private.GLwindow,private.GLcontext)->glXMakeCurrent;

       #);
   if);
       
       
-- glCanvasMakeCurrent: doPart --
do (private.GLdisplay,private.GLwindow,private.GLcontext)->glXMakeCurrent;
   
-- glCanvasSwapBuffers: doPart --
do (private.GLdisplay,private.GLwindow)->glXSwapBuffers;
       
-- glCanvasLib: attributes --
openGLWidgetClass:external
  (# widget:@integer;
  exit widget
  #);
GLwNvisualInfo:IntegerObject
  (# do 'visualInfo'->charReptoCString -> value #);
GLwNInstallColormap:IntegerObject
  (# do 'installColormap'->charReptoCString -> value #);
GLwNallocateOtherColors:IntegerObject
  (# do 'allocateOtherColors'->charReptoCString -> value #);
GLwNallocateBackground:IntegerObject
  (# do 'allocateBackground'->charReptoCString -> value #);


-- glCanvasPrivate: descriptor --
(# glXWidget:@integer;
   GLdisplay:@integer;
   GLwindow:@integer;
   GLvisual:@integer;
   GLcontext:@integer;
#)
