ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/fields';
INCLUDE '~beta/guienv/openGL/openGL';
INCLUDE '~beta/opengl/glb';
-- program:descriptor--
guienv
(# 
   owindow:@window
     (# 
        ogl:@openGLarea
          (# 
             spintimer:@timer
               (# action::<(# do spin+2->spin;
                              true->update;
                           #);
               #);
             spin:@real;
             open::
               (# 
               do 
                  (200,200)->size;
                  true->bindright;
                  true->bindbottom;
                  GL_FLAT->glShadeModel;
                  50->spintimer.start;
                  GL_MODELVIEW->glMatrixMode;
		  GL_LINE_SMOOTH -> glEnable;
		  GL_BLEND -> glEnable;
		  (GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA) -> glBlendFunc;
		  (GL_LINE_SMOOTH_HINT, GL_DONT_CARE) -> glHint;
		  1.5 -> glLineWidth;
               #);
             eventhandler::
               (# 
                  onrefresh::
                    (# (* Draw using openGL code *)
                       draw:@glBWireCube;
                    do 
                       (1.0,0.0,0,0)->glClearColor;
                       GL_COLOR_BUFFER_BIT->glClear; 
                       glPushMatrix;
                       (spin,1,1,0)->glRotated;
                       (-10,-10,10)->glTranslated;
                       (0,0,1)->glColor3d;
                       20->draw;
                       glBSwapBuffers;
                       glPopMatrix;
                    #);
                  onFrameChanged::
                    (# h,w:@integer;
                    do newframe.size->(w,h);
                       (0,0,w,h)->glViewport;
                       GL_PROJECTION->glMatrixMode;
                       glLoadIdentity;
                       (-50,50,-50,50,-50,50)->glOrtho;
                       GL_MODELVIEW->glMatrixMode;
                       glLoadIdentity;
                    #);
               #);
          #);
        open::
          (# do ogl.open;
          #);
     #);
   Mainwindow:@window
          (# menubarType:: standardMenubar
               (# fileMenu:: standardFileMenu
                    (# newMenuAction: @menuAction
                         (# onSelect:: (# do 'New...'->putline #) #);
                       saveMenuAction: @menuAction
                         (# onSelect:: (# do 'Saving...'->putline #) #);
                       saveAsMenuAction: @menuAction
                         (# onSelect:: (# do 'Saving As...'->putline #) #);
                       quitMenuAction: @menuAction
                         (# onSelect:: (# do 'Quitting...'->putline; terminate #) #);
                       open::
                         (#
                         do newMenuAction[]->newMenuItem.attach;
                            saveMenuAction[]->saveMenuItem.attach;
                            saveAsMenuAction[]->saveAsMenuItem.attach;
                            quitMenuAction[]->quitMenuItem.attach
                         #)
                    #);
                  editMenu:: standardEditMenu
               #);
             t:@texteditor(# #);
             eventhandler::
               (# onAboutToClose:: (# do terminate #) #);
             open:: 
               (# 
               do 'OpenGL test program'->title;
                  t.open;
               #)
          #)
do
   Mainwindow.open;
   owindow.open;
#)
