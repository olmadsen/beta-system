
ORIGIN '~henryml/beta/guienv/guienv';
INCLUDE '~henryml/beta/guienv/private/X11/guienv_unixprivate';
INCLUDE '~henryml/beta/guienv/graphics';
INCLUDE '~henryml/beta/guienv/private/X11/graphics_unixbody';






-- program: descriptor --
guienv
(#
   XImage: data
     (# width: @integer;
        height: @integer;
        xoffset: @integer;
        format: @integer;
        data: @integer;
        byte_order: @integer;
        bitmap_unit: @integer;
        bitmap_bit_order: @integer;
        bitmap_pat: @integer;
        depth: @integer;
        bytes_per_line: @integer;
        bits_per_pixel: @integer;
        red_mask: @integer;
        green_mask: @integer;
        blue_mask: @integer;
        obdata: @integer;
        hook1, hook2, hook3, hook4, hook5, hook6: @integer;
     #);
   XPutImage: External (* 6.7 *)
     (# display,drawable,gc,srcX,srcY,dstX,dstY,width,height: @integer;
        image: ^XImage;
     enter (display,drawable,gc,image[],srcX,srcY,dstX,dstY,width,height)
     #);
   
   BetaReadPNGFileToXImage: external
     (# display: @integer;
        name: [1]@char;
        image: @integer;
        error: @integer;
     enter (display, name, image)
     exit error
     #);
   image: ^XImage;
   error: @integer;
   w: @window
     (# c: @canvas
          (# eventHandler::
               (# 
                  onRefresh::
                    (#
                    do graphics
                       (#
                       do (private.display, private.window, private.gc,
                          image[], 0, 0, 0, 0, image.width, image.height) -> XPutImage;
                       #);
                    #);
                  
               #);
             open::
               (#
               do (image.width, image.height) -> size;
               #);
          #);
        open::
          (#
          do (image.width, image.height) -> size;
             c.open;
          #);
     #);
   name: ^text;
do 2 -> arguments -> name[];
   (private.display, name, @@image) -> BetaReadPNGFileToXImage -> error;
   (if error
    //0 then
       w.open;
    //1 then
       'No such file' -> putLine;
    //2 then
       'Wrong format' -> putLine;
    //3 then
       'Not enough memory' -> putLine;
   if);
   
#)
