ORIGIN '../controlsbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1994-96
 *       All rights reserved.
 *)
INCLUDE '../../guienvactions';
INCLUDE '../../utils/guienvadds';
INCLUDE 'guienv_ntiprivate';
INCLUDE '~beta/win32lib/v1.6/winuserconsts';
INCLUDE '~beta/win32lib/v1.6/systemmetrics';
INCLUDE '~beta/sysutils/v1.6/cstring';

-- GUIENVcontrolOnEnabledChanged: doPart --
do inner;
   
-- GUIENVpushButtonOnMouseDown: doPart --
do inner;
   
-- GUIENVpushButtonOnRefresh: doPart --
do inner;
   
-- GUIENVpushButtonHiliteChanged: doPart --
do inner;
   
-- GUIENVoptionButtonOnRefresh: doPart --
do inner;
   
   
-- optionButtonLib: attributes --
sblc: (# exit 4 #); (* The space between the label and the combobox. *)

translate:
  (# h,v: @integer;
     ntpPtr: @integer
  enter (h,v)
  do (h,v) -> makeNtPointPtr -> ntpPtr;
     (interfaceObjectID,ntpPtr) -> ClientToScreen;
     ntpPtr -> getPointFromNtPointPtr -> (h,v);
     ntpPtr -> free;
  exit (h,v)
  #);

getBounds:
  (# theNtRectPtr: @integer;
     r: ^rectangle;
     ID: @integer;
  enter ID
     do 16 -> malloc -> theNtRectPtr;
     (ID,theNtRectPtr) -> GetClientRect;
     &rectangle[] -> r[];
     theNtRectPtr -> getRectFromNtRectPtr -> r;
     r.topLeft -> translate -> r.topLeft;
     r.bottomRight -> translate -> r.bottomRight;
     theNtRectPtr -> free;
  exit r[]
  #);

menuChanged:
  (# theMenu: ^menu;
  enter theMenu[]
  do updateContents;
  #);

updateContents:
  (# thePopUpmenu: ^menu;
     curSel,noOfItems: @integer;
  do 
     (dropDownListID,CB_GETCURSEL,0,0) -> sendMessage -> curSel;
     
     popupMenu -> insertNewMenu;
     
     (dropDownListID,CB_GETCOUNT,0,0) -> sendMessage -> noOfItems;
     (if (noOfItems >= curSel) then 
         (dropDownListID,CB_SETCURSEL,curSel,0) -> sendMessage;
     if);
  #);

optionButtonPopupMenu:
  (#
  enter private.optionButtonPopupMenu[]
  exit private.optionButtonPopupMenu[]
  #);

addOptionButtonMenuItem:
  (# listBoxInx: @integer;
     theMenuitem: ^private.optionButtonPopupMenu.menuitem;
     elm: ^private.menuItemListInx;
  enter (listBoxInx,theMenuitem[])
  do &private.menuItemListInx[] -> elm[];
     listBoxInx -> elm.listBoxInx; theMenuitem[] -> elm.theMenuitem[];
     elm[]  -> private.menuItems.append;
  #);

clearOptionButtonMenuItems:
  (# 
  do private.menuItems.clear;
  #);

findoptionButtonMenuItem:
  (# listBoxInx: @integer; theMenuitem: ^private.optionButtonPopupMenu.menuitem;
  enter listBoxInx
  do listBoxInx -> private.menuItems.findByID -> theMenuitem[];
  exit theMenuitem[]
  #);

mbLabelID:
  (# 
  enter private.mbLabelID
  exit private.mbLabelID
  #);
dropDownListID:
  (# 
  enter private.dropDownListID
  exit private.dropDownListID
  #);

updateHeightOfDropDownList:
  (# w,h: @integer;
     itemH,noOfItems: @integer;
  do (dropDownListID,CB_GETITEMHEIGHT,0,0) -> SendMessage -> itemH;
     (dropDownListID,CB_GETCOUNT,0,0) -> SendMessage -> noOfItems;
     private.theComboBox.size -> (w,h);
     (w,itemH*noOfItems+private.theComboBoxHeight) -> private.theComboBox.size;
  #);


insertNewMenu:
  (# cstr: @cString; inx: @integer;
     noOfItems: @integer;
     theMenu: ^menu;
  enter theMenu[]
  do 
     (if (optionButtonPopupMenu<>none) then
         (if (dropDownListID<>0) then
             (dropDownListID,CB_RESETCONTENT,0,0) -> sendMessage;
             clearOptionButtonMenuItems;
             none -> optionButtonPopupMenu;
         if);
     if);
     
     (if (theMenu[]<>none) then
         theMenu[] -> optionButtonPopupMenu;
         theMenu.scan
         (# 
         do current.name -> cstr.set;
            (if cstr<>0 then
                (dropDownListID,CB_ADDSTRING,0,cstr.charPtr)->sendMessage 
                  -> inx;
                (if inx
                 //CB_ERR
                 //CB_ERRSPACE then
                    'optionButtonSetPopupMenu failed!! Errorcode: '
                      -> screen.puttext;
                    GetLastError -> screen.putint; screen.newline;
                 else
                    (inx,current[]) -> addoptionButtonMenuItem;
                if);
                cstr.free;
                if);
         #);
         (dropDownListID,CB_GETCOUNT,0,0) -> sendMessage -> noOfItems;
         (if noOfItems
          //CB_ERR then 
             'optionButton: CB_GETCOUNT failed!! Errorcode: ' -> puttext;
             GetLastError -> putint; newline;
          //0 then
          else
             (dropDownListID,CB_SETCURSEL,0,0) -> sendMessage -> inx;
         if);
         updateHeightOfDropDownList;
     if);
  #);

-- GUIENVoptionButtonprivate: descriptor --
(# mbLabelID,dropDownListID: @integer;
   
   theVisibleChangedAction: @visibleChangedAction
     (# 
     do (if this(optionButton).visible then
            theComboBox.bringToFront;
        if);
     #);
   
   theEnableTargetAction: @enableTargetAction
     (# 
     do (if theComboBox.isOpen then 
            theComboBox[] -> target; 
        if);
     #);
   
   optFrameChangedAction: @frameChangedAction
     (# w,h,w2,h2,widthOfLabel: @integer;
        l: ^text;
        theTextStyle: ^textStyle;
        margin: @integer;
     do theComboBox.size -> (w,h);
        theEvent.newFrame.size -> (w2,h2);
        
        (if border.visible then
            (if (border.style=borderStyles.simple) then
                2 -> margin;
             else
                3 -> margin;
            if);
        if);
        
        this(optionButton).label -> l[];
        this(optionButton).style -> theTextStyle[];  
        (if (l[]<>NONE) and (theTextStyle[]<>NONE) then
            l[] -> theTextStyle.widthOfText -> widthOfLabel;
            (if l.length>0 then widthOfLabel+sblc -> widthOfLabel; if);
         else
            16 -> widthOfLabel;
        if);
        
        (theEvent.newFrame.left+widthOfLabel+margin,theEvent.newFrame.top+2) 
          -> theComboBox.position;
        
        (if w2 - widthOfLabel > 16 then
            (w2-widthOfLabel-(2*margin),h) -> theComboBox.size;
         else
            (16,h) -> theComboBox.size;
        if);
     #);
   
   theComboBoxHeight,leftMargin,maxWidthOfItem,maxHeight: @integer;
   theComboBox: @windowItem
     (# theComboBoxMethods: windowItemNotificationMethods
          (# handleNotification::
               (# processCommand:
                    (# type: @integer;
                       index: @integer;
                       theMenu: ^menu;
                       theMenuitem: ^theMenu.menuitem;
                    do info.wParam.hiWord -> type;
                       (if type
                        //CBN_SELENDOK then
                           (info.lParam,CB_GETCURSEL,0,0) -> SendMessage -> index;
                           (if index=CB_ERR then
                            else
                               OptionButtonPopupMenu[] -> theMenu[];
                               index -> findOptionButtonMenuItem 
                                 -> theMenuitem[];
                               (if theMenuitem[]=NONE then
                                   'cannot find menuitem in OptionButton!!!' 
                                     -> putline;
                                else
                                   theMenuitem.theEventHandler.onSelect;
                                   this(optionButton).theEventHandler.onCurrentItemChanged;
                               if);
                           if);
                           
                        //CBN_DROPDOWN then
                           updateContents;
                        else
                           false -> didSomething;
                       if);
                    #);
                  didSomething: @boolean;
               do (if info.message=WM_COMMAND then processCommand; if);
                  didSomething or info.handled -> info.handled;
               #);
          #);
        eventHandler::
          (# onMouseDown::
               (# do this(optionButton).theEventHandler.onMouseDown; #);
             onMouseUp::
               (# do this(optionButton).theEventHandler.onMouseUp; #);
             onKeyDown::
               (# windowID: @integer;
               do this(theComboBox).interfaceObjectID -> windowID;
                  (if ch 
                   //ASCII.rs
                   //ASCII.us
                   //VK_UP      
                   //VK_DOWN then
                      (if (((windowID,CB_GETDROPPEDSTATE,0,0)->SendMessage)=0) then 
                          (windowID,CB_SHOWDROPDOWN,1,0)->SendMessage;
                      if);
                   //ascii.sp then 
                      (if (((windowID,CB_GETDROPPEDSTATE,0,0)->SendMessage)<>0) then 
                          (*true -> theComboBoxFather.hadCBN_SELENDOK;*)
                          (windowID,CB_SHOWDROPDOWN,0,0)->SendMessage;
                       else
                          (windowID,CB_SHOWDROPDOWN,1,0)->SendMessage;
                      if);
                   //ascii.esc then
                      (if (((windowID,CB_GETDROPPEDSTATE,0,0)->SendMessage)<>0) then
                          (windowID,CB_SHOWDROPDOWN,0,0)->SendMessage;
                      if);
                  if);
                  this(optionButton).theEventHandler.onKeyDown; 
               #);
          #);
        
        isOpen: @boolean;
        open::
          (# create::
               (# 
               do 'COMBOBOX' -> windowItemClass;
                  isSubClass -> windowItemClassStatus;
                  CBS_DROPDOWNLIST -> windowItemStyle;
                  &theComboBoxMethods[] -> theNotiMethods;
               #);
             x,y,w,h: @integer;
          do interfaceObjectID -> dropDownListID;
             (* hide; *)
             calcTheHeight;
             (100,theComboBoxHeight) -> size;
             true -> isOpen;
          #);
        
        close::
          (# windowID: @integer;
          do this(theComboBox).interfaceObjectID -> windowID;
             (if (((windowID,CB_GETDROPPEDSTATE,0,0)->SendMessage)<>0) then 
                 (windowID,CB_SHOWDROPDOWN,0,0)->SendMessage;
             if);
             false -> isOpen;
          #);
        
        setTextStyle:
          (# theTextStyle: ^textStyle;
          enter theTextStyle[]
          do (this(theComboBox).interfaceObjectID,
             WM_SETFONT,theTextStyle.textStyleID,1) 
               -> SendMessage;
          #);
        
        noOfItems: integerValue
          (# 
          do (this(theComboBox).interfaceObjectID,CB_GETCOUNT,0,0) 
               -> SendMessage -> value;
             (if value=CB_ERR then
                 'CB_GETCOUNT falied. Errorcode: ' -> screen.puttext;
                 GetLastError -> putint; newline;
                 0 -> value;
             if);
          #);
        
        calcTheHeight:
          (# r: ^rectangle; hwndTmp: @integer;
          do 
             ('COMBOBOX', '',
             WS_CHILD + CBS_DROPDOWNLIST,
             0, 0, 0, 0,
             father.interfaceObjectID, 0, uienvHInstance, 0)
               -> CreateWindow 
               -> hwndTmp;
             (if hwndTmp<>0 then
                 
                 hwndTmp -> getBounds -> r[];
                 (r.bottom - r.top) -> theComboBoxHeight;
                 
                 hwndTmp -> DestroyWindow
              else
                 28 -> theComboBoxHeight;
             if);
          #);
        
        preferredSize:
          (# prefW, prefH: @integer;
             theTextStyle: ^textStyle;
             itemH, noOfItems: @integer;
             widthOfItem: @integer;
             scrollbarWidth: @integer;
          do this(optionButton).style -> theTextStyle[];         
             SM_CXVSCROLL -> GetSystemMetrics -> scrollbarWidth;
             0 -> maxWidthOfItem;
             
             (if optionButtonPopupMenu[]=NONE then
                 20 -> maxWidthOfItem;
              else
                 optionButtonPopupMenu.scan
                 (# tempName: ^text;
                 do (if theTextStyle[]<>NONE then
                        current.name -> tempName[];
                        tempName[] -> theTextStyle.widthOfText 
                          -> widthOfItem;
                     else
                        current.name -> tempName[];
                        tempName.length + 2 -> widthOfItem;
                    if);
                    (if widthOfItem>maxWidthOfItem then
                        widthOfItem -> maxWidthOfItem;
                    if);
                 #);
             if);
             
             maxWidthOfItem + scrollbarWidth + leftMargin + 10  -> prefW;
             (interfaceObjectID,CB_GETITEMHEIGHT,0,0)-> SendMessage -> itemH;
             (interfaceObjectID,CB_GETCOUNT,0,0) -> SendMessage -> noOfItems;
             itemH*noOfItems+theComboBoxHeight -> prefH; 
             
          exit (prefW,prefH)
          #);
     #);
   
   dropListWidth: @integer; 

   optionButtonPopupMenu: ^menu;
   
   menuItemListInx:
     (# listBoxInx: @integer;
        theMenuitem: ^optionButtonPopupMenu.menuitem;
     #);
   menuItems: @list
     (# element::< menuItemListInx;
        findByID:
          (# ID: @integer;
             theMenuitem: ^optionButtonPopupMenu.menuitem;
          enter ID
          do	l: scan
               (#
               do (if current.listBoxInx=ID then
                      current.theMenuitem[] -> theMenuitem[];
                      leave l;
                  if);
               #);
          exit theMenuitem[]
          #);
     #);   
#)

