ORIGIN 'guienv_ntibody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1994-97
 *       All rights reserved.
 *)

INCLUDE '~beta/win32lib/v1.2/wingdi';

-- drawborderbody: descriptor --
(# (* 3D border styles *)
   BDR_RAISEDOUTER: (# exit  16x0001 #);
   BDR_SUNKENOUTER: (# exit  16x0002 #);
   BDR_RAISEDINNER: (# exit  16x0004 #);
   BDR_SUNKENINNER: (# exit  16x0008 #);
   
   BDR_OUTER: (# exit        16x0003 #);
   BDR_INNER: (# exit        16x000c #);
   BDR_RAISED: (# exit       16x0005 #);
   BDR_SUNKEN: (# exit       16x000a #);
   
   EDGE_RAISED: (# exit      (BDR_RAISEDOUTER + BDR_RAISEDINNER) #);
   EDGE_SUNKEN: (# exit      (BDR_SUNKENOUTER + BDR_SUNKENINNER) #);
   EDGE_ETCHED: (# exit      (BDR_SUNKENOUTER + BDR_RAISEDINNER) #);
   EDGE_BUMP: (# exit        (BDR_RAISEDOUTER + BDR_SUNKENINNER) #);
   
   (* Border flags *)
   BF_LEFT: (# exit          16x0001 #);
   BF_TOP: (# exit           16x0002 #);
   BF_RIGHT: (# exit         16x0004 #);
   BF_BOTTOM: (# exit        16x0008 #);
   
   BF_TOPLEFT: (# exit       (BF_TOP + BF_LEFT) #);
   BF_TOPRIGHT: (# exit      (BF_TOP + BF_RIGHT) #);
   BF_BOTTOMLEFT: (# exit    (BF_BOTTOM + BF_LEFT) #);
   BF_BOTTOMRIGHT: (# exit   (BF_BOTTOM + BF_RIGHT) #);
   BF_RECT: (# exit          (BF_LEFT + BF_TOP + BF_RIGHT + BF_BOTTOM) #);
   
   BF_FLAT: (# exit         16x4000 #); (* For flat rather than 3D borders *)
   BF_MONO: (# exit         16x8000 #);
   BF_ADJUST: (# exit       16x2000 #);

   BF_DIAGONAL: (# exit      16x0010 #);
   
   DrawEdge: external
     (# hdc: @integer;
        theRect: ^ntRECT;
        edge: @integer;
        flags: @integer;
        result: @integer;
     enter (hdc,theRect[],edge,flags)
     do callStd
     exit result
     #);
   
   FrameRect: external
     (# hdc: @integer; (* HDC *)
        theRect: ^ntRECT; (* const RECT FAR* *)
        hbr: @integer; (* HBRUSH *)
        result: @integer; (* int *)
     enter (hdc,theRect[],hbr)
     do 'FrameRect' -> callStd;
     exit result
     #);
   
   drawTheEdge: 
     (# theRect: @ntRECT;
        hdc: @integer;
        left,right,top,bottom: @integer;
        edgeType: @integer;
        flags: @integer;
        hbr: @integer;
     enter (left,right,top,bottom,edgeType,flags)
     do (* theWindowItem.interfaceObjectID -> GetDC -> hdc; *)
        
        (theWindowItem.interfaceObjectID,0,DCX_PARENTCLIP + DCX_CLIPSIBLINGS + DCX_CLIPCHILDREN ) -> GetDCEx -> hdc; 
        
        left -> theRect.left;
        right -> theRect.right;
        top -> theRect.top;
        bottom -> theRect.bottom;        
        
        (if theStyle=1 then
            BLACK_BRUSH -> GetStockObject -> hbr;
            (hdc,theRect[],hbr) -> FrameRect;
         else
            (hdc,theRect[],edgeType,flags) -> DrawEdge;
        if);
        
        (theWindowItem.interfaceObjectID,hdc) -> ReleaseDC;
     #);
do (if theStyle
    //1 then (* simple *)
       (x,x+width,y,y+height,EDGE_SUNKEN,BF_RECT+BF_MONO) -> drawTheEdge;
    //4 then (* shadowIn *)
       (x,x+width,y,y+height,EDGE_SUNKEN,BF_RECT) -> drawTheEdge;
    //5 then (* shadowOut *)
       (x,x+width,y,y+height,EDGE_RAISED,BF_RECT) -> drawTheEdge;
    //3 then (* etchedIn *)
       (x,x+width,y,y+height,EDGE_ETCHED,BF_RECT) -> drawTheEdge;
    //2 then (* etchedOut *)
       (x,x+width,y,y+height,EDGE_BUMP,BF_RECT) -> drawTheEdge;
   if);
#)
