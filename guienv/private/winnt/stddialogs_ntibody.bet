ORIGIN  '../stddialogsbody';
MAKE default 'external/stdfiledlg.make';
OBJFILE default '$/stdfiledlg.obj';
INCLUDE 'guienv_ntiprivate';
INCLUDE '~beta/win32lib/v1.0/messageboxsupport';
INCLUDE '~beta/win32lib/v1.0/commdlg';
INCLUDE '~beta/win32lib/v1.0/dialogcontrolmanagement';
INCLUDE '~beta/win32lib/v1.0/winuserconsts';
INCLUDE '~beta/sysutils/v1.4.2/cstring';

-- noteUserBody: doPart --
do inner;
   MB_ICONINFORMATION+MB_OK+MB_TASKMODAL -> messageDialogPrivate.type;
   'NOTE' -> messageDialogPrivate.mtitle;

-- alertUserBody: doPart --
do MB_ICONEXCLAMATION+MB_OK+MB_TASKMODAL -> messageDialogPrivate.type;
   'ALERT' -> messageDialogPrivate.mtitle;

-- fileSelectionDialogBody: doPart --
do inner;
   fileDialogPrivate.openfiledlgType -> fileDialogPrivate.type;
   'Open' -> fileDialogPrivate.ftitle;
   
-- fileCreationDialogBody: doPart --
do inner;
   fileDialogPrivate.savefiledlgType -> fileDialogPrivate.type;
   'Create' -> fileDialogPrivate.ftitle;
   
-- GUIENVdialog: doPart --
do inner;
   
-- GUIENVfileDialog: doPart --
do inner;
   (# stdopenfiledlg: external
        (# owner: @integer;
           winapiFilter: @integer;
           defaultPath: @integer;
           defaultLabel: @integer;
           dialogTitle: [1] @char;
           resultPath: @integer; (* LPCSTR *)
           length: @integer;
        enter (owner,winapiFilter,defaultPath,defaultLabel,dialogTitle)
        do callC
        exit resultPath
        #);
      
      stdsavefiledlg: external
        (# owner: @integer;
           winapiFilter: @integer;
           defaultPath: @integer;
           defaultLabel: @integer;
           dialogTitle: [1] @char;
           resultPath: @integer; (* LPCSTR *)
        enter (owner,winapiFilter,defaultPath,defaultLabel,dialogTitle)
        do callC
        exit resultPath
        #);
      
      winapiFilter,defaultPath,defaultLabel: @cString;
      resultPath: @cString;
      hwnd: @integer;
   do (if title[] = None then
          fileDialogPrivate.ftitle[] -> title[];
      if);
      (if filter[]=none then
          'All Files |*.*|' -> winapiFilter.set;
       else
          filter[] -> winapiFilter.set;
      if);      
      (if label[]<>none then
          label[] -> defaultLabel.set;
       else
          '' -> defaultLabel.set;
      if);
      (if path[]<>none then
          path[] -> defaultPath.set;
       else
          '' -> defaultPath.set;
      if);
      
      (if owner[]=none then
          GetActiveWindow -> hWnd;
          (if hWnd=0 then GetDeskTopWindow -> hWnd; if);
       else
          owner.interfaceObjectID -> hWnd;
      if);
      
      
      (if fileDialogPrivate.type
       //fileDialogPrivate.savefiledlgType then
          (hwnd,winapiFilter,defaultPath,defaultLabel,title) -> stdsavefiledlg -> resultPath;
       //fileDialogPrivate.openfiledlgType then 
          (hwnd,winapiFilter,defaultPath,defaultLabel,title) -> stdopenfiledlg -> resultPath;
       else
          '**WARNING: Do not know type in fileDialog.' -> screen.putLine;
      if);
      (if resultPath.charPtr=0 then
          none -> fileName[];
       else
          resultPath.get -> fileName[];
      if);
      winapiFilter.free;
      defaultLabel.free;
      defaultPath.free;
   #);
   
-- GUIENVmessageDialog: doPart --
do inner;
   (if title[] = None then
       messageDialogPrivate.mtitle[] -> title[];
   if);
   (if owner[]=none then
       (0,message,title,messageDialogPrivate.type) -> MessageBox;
    else
       (owner.interfaceObjectID,message,title,messageDialogPrivate.type) -> MessageBox;
   if);

-- GUIENVdialogPrivate: descriptor --
(# 
#)

-- GUIENVfileDialogPrivate: descriptor --
(# type: @integer;
   openfiledlgType: (# exit 1 #);
   savefiledlgType: (# exit 2 #);
   ftitle: @text;
#)

-- GUIENVmessageDialogPrivate: descriptor --
(# type: @integer; 
   mtitle: @text;
#)
