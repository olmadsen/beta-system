ORIGIN '../graphmathbody';

(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1994-97
 *       All rights reserved.
 *)

INCLUDE '~beta/basiclib/v1.6/external'
        '~beta/win32lib/v1.6/ntinterface'
        '~beta/win32lib/v1.6/wingdi'
        '~beta/win32lib/v1.6/wingdiconsts';

-- regionLib: attributes --

assertNonNil:
  (#
  do (if private.regionHandle = 0 then
         Exception
         (#
         do 'a region handle is NULL' -> msg.append;
         #);
     if);
  #);

RGN_AND:  (# exit 1 #);
RGN_OR:   (# exit 2 #);
RGN_XOR:  (# exit 3 #);
RGN_DIFF: (# exit 4 #);
RGN_COPY: (# exit 5 #);


ntRECText: ExternalRecord
  (# 
     left: @long (# pos::< (# do 0 -> value; #); #); (* int left *)
     top: @long (# pos::< (# do 4 -> value; #); #); (* int top *)
     right: @long (# pos::< (# do 8 -> value; #); #); (* int right *)
     bottom: @long (# pos::< (# do 12 -> value; #); #); (* int bottom *)
  #);

-- GUIENVregionBounds: descriptor --
(# theNtRect: @ntRECText;
do assertNonNil;
   8 -> malloc -> theNtRect.ptr;
   (if theNtRect.ptr<>0 then
       
       (private.regionHandle,theNtRect.ptr) -> GetRgnBox;
       theNtRect.left -> theRectangle.left;
       theNtRect.top -> theRectangle.top;
       theNtRect.right -> theRectangle.right;
       theNtRect.bottom -> theRectangle.bottom;
       
       theNtRect.ptr -> free;
   if);
#)

-- GUIENVregionAllocate: descriptor --
(# win32Points: @(# p: [8] @ Integer; #);
do (@@win32Points.p[1],4,WINDING) 
     -> CreatePolygonRgn 
     -> private.regionHandle;
   (if private.regionHandle = 0 then
       Exception
       (#
       do 'Failed to allocate Region' -> msg.append;
       #);
   if);
#)

-- GUIENVregionDispose: descriptor --
(#
do assertNonNil;
   private.regionHandle -> DeleteObject;
   0 -> private.regionHandle;
#)

-- GUIENVregionEmpty: descriptor --
(# 
do assertNonNil;
   dispose;
   allocate;
#)

-- GUIENVregionSetFromRectangle: descriptor --
(#
do assertNonNil;
   dispose;
   (theRectangle.left,theRectangle.top,
   theRectangle.right,theRectangle.bottom) 
     -> CreateRectRgn -> private.regionHandle;
#)

-- GUIENVregionOffset: descriptor --
(#
do assertNonNil;
   (private.regionHandle, delta.h, delta.v) -> OffsetRgn;
#)

-- GUIENVregionInset: descriptor --
(#
do 'Not implemented.' -> screen.putLine;
#)

-- GUIENVregionIntersection: descriptor --
(#
do assertNonNil;
   src1.assertNonNil;
   src2.assertNonNil;
   
   (private.regionHandle,
   src1.private.regionHandle,
   src2.private.regionHandle,
   RGN_AND
   ) -> CombineRgn;
#)

-- GUIENVregionUnion: descriptor --
(#
do assertNonNil;
   src1.assertNonNil;
   src2.assertNonNil;
   
   (private.regionHandle,
   src1.private.regionHandle,
   src2.private.regionHandle,
   RGN_OR
   ) -> CombineRgn;
#)

-- GUIENVregionDifference: descriptor --
(#
do assertNonNil;
   src1.assertNonNil;
   src2.assertNonNil;
   
   (private.regionHandle,
   src1.private.regionHandle,
   src2.private.regionHandle,
   RGN_DIFF
   ) -> CombineRgn;
#)

-- GUIENVregionXOr: descriptor --
(#
do assertNonNil;
   src1.assertNonNil;
   src2.assertNonNil;
   
   (private.regionHandle,
   src1.private.regionHandle,
   src2.private.regionHandle,
   RGN_XOR
   ) -> CombineRgn;
#)

-- GUIENVregionContainsPoint: descriptor --
(#
do ((private.regionHandle,pt.h,pt.v) -> PtInRegion)<>0
     -> value; 
#)

-- GUIENVregionContainsRectangle: descriptor --
(# theNtRect: @ntRECText;
do assertNonNil;
   8 -> malloc -> theNtRect.ptr;
   (if theNtRect.ptr<>0 then
       
       theRectangle.left -> theNtRect.left;
       theRectangle.top -> theNtRect.top;
       theRectangle.right -> theNtRect.right;
       theRectangle.bottom -> theNtRect.bottom;
       
       ((private.regionHandle,theNtRect.ptr) -> RectInRegion)<>0 -> value;
       
       theNtRect.ptr -> free;
   if);
#)

-- GUIENVregionIsEqual: descriptor --
(#
do assertNonNil;
   theRegion.assertNonNil;
   ((private.regionHandle, theRegion.private.regionHandle) -> EqualRgn)<>0 -> value;
#)

-- GUIENVregionIsEmpty: descriptor --
(# r: @region;
do assertNonNil;
   r.allocate;
   ((private.regionHandle, r.private.regionHandle) -> EqualRgn)<>0 -> value;
#)

-- GUIENVregionPrivate: descriptor --
(#
   regionHandle: @integer; 
#)

-- GUIENVregionEnter: descriptor --
(#
do assertNonNil;
   r.assertNonNil;
   (private.regionHandle,
   r.private.regionHandle,
   0,
   RGN_COPY
   ) -> CombineRgn;
#)

-- GUIENVregionExit: descriptor --
(#
do THIS(region)[] -> r[];
#)
