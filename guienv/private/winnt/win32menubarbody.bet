ORIGIN 'guienv_ntiprivate';

INCLUDE 'win32menubarprivate';
INCLUDE 'win32menulib';

-- GUIENVwin32Menubardelete: doPart --
do (# theWin32Menu: ^win32Menu; 
      result: @integer;
      position: @integer;
   do theMenu[] -> private.win32Menus.findMenu -> theWin32Menu[];
      (if theWin32Menu[]<>none then
          theWin32Menu[] -> private.win32Menus.remove;
          
          private.win32Menus.scan
          (# 
          do (if (current.position>theWin32Menu.position) then
                 current.position - 1 -> current.position;
             if);
          #);
          
          (interfaceObjectID,theWin32Menu.position-1,MF_BYPOSITION) -> RemoveMenu -> result;
          (if result=0 then
              'win32MenubarDelete(1), removeMenu failed!! Error: ' -> puttext;
              GetLastError -> putint; newline;
              'Name of menu: ' -> puttext; theMenu.name -> putline;
              'theWin32Menu.position: ' -> puttext; theWin32Menu.position -> putInt; newline;
          if);
          theWin32Menu.close;
      if);
   #)
   
   
-- GUIENVwin32Menubarappend: doPart --
do (# theWin32Menu: ^win32Menu;
      name: ^text;
      result: @integer;
   do &win32Menu[] -> theWin32Menu[];
      this(win32menubar)[] -> theWin32Menu.private.theWin32Menubar[];
      theMenu[] -> theWin32Menu.theMenu[];
      theWin32Menu.open;
      theWin32Menu[] -> private.win32Menus.append;
      private.win32Menus.size -> theWin32Menu.private.position;
      theMenu.name -> name[];
      name.copy -> name[];
      '&' -> name.prepend;
      
      (interfaceObjectID,MF_POPUP + MF_STRING,theWin32Menu.interfaceObjectID,name) 
        -> AppendMenu -> result;
      (if result=0 then
          'menubarAppend failed!! Error: ' -> puttext;
          GetLastError -> putint; newline;
      if);
   #);
   
-- GUIENVwin32Menubarinsert: doPart --
do (# theWin32Menu: ^win32Menu;
      name: ^text;
      result: @integer;
      theApplMenubar: ^menubar;
      position: @integer;
   do &win32Menu[] -> theWin32Menu[];
      theMenu[] -> theWin32Menu.theMenu[];
      this(win32menubar)[] -> theWin32Menu.private.theWin32Menubar[];
      theWin32Menu.open;

      theMenu.name -> name[];
      name.copy -> name[];
      '&' -> name.prepend;
      
      applicationMenubar -> theApplMenubar[];
      theApplMenubar.private.menubarMenus.size -> position;
      position -> theWin32Menu.private.position;
      private.win32Menus.scan
      (# 
      do (if ( current.private.position>(position-1) ) then
             current.private.position + 1 -> current.private.position;
         if);
      #);
      theWin32Menu[] -> private.win32Menus.append;
      (interfaceObjectID,position - 1,
      MF_BYPOSITION + MF_POPUP + MF_STRING, theWin32Menu.interfaceObjectID,name) 
        -> InsertMenu -> result;
      (if result=0 then
          'InsertMenu failed in modifymenuitem. Error: ' -> puttext;
          GetLastError -> putint; newline;
      if);      
   #);



-- GUIENVwin32Menubarcreate: doPart --
do false -> doAddToList;

-- GUIENVwin32Menubaropen: doPart --
do CreateMenu -> this(win32Menubar).interfaceObjectID;
   this(win32Menubar)[]->this(guienv).private.win32Menubars.append;
   
-- GUIENVwin32MenubarClose: doPart --
do (# result: @integer; 
   do this(win32Menubar)[]->this(guienv).private.win32Menubars.delete;
      private.win32Menus.scan
      (# 
      do current.close;
      #);
      interfaceObjectID -> DestroyMenu -> result;
      (if result=0 then
          (if (interfaceObjectID -> isMenu)=0 then
              'Win32Menubar does not have a valid menu handle in close win32menubar.' -> putline;
          if);
      if);
      none -> theWindow[];
      (result,'Win32MenubarClose, Destroymenu: ') -> checkNullError;
      0 -> this(win32Menubar).interfaceObjectID;
      (if private.hMenubarAccelTable<>0 then
          (if (private.hMenubarAccelTable -> DestroyAcceleratorTable)=0 then
             'DestroyAcceleratorTable failed. Error: ' -> screen.puttext;
              GetLastError -> putint; newline;
         if);
         0 -> private.hMenubarAccelTable;
      if);
   #)

