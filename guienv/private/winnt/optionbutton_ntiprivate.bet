ORIGIN '../controlsbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1994-95
 *       All rights reserved.
 *)
INCLUDE '../../guienvactions';
INCLUDE 'guienv_ntiprivate';
INCLUDE '~beta/win32lib/v1.1/winuserconsts';

-- optionButtonLib: attributes --
translate:
  (# h,v: @integer;
     ntpPtr: @integer
  enter (h,v)
  do (h,v) -> makeNtPointPtr -> ntpPtr;
     (interfaceObjectID,ntpPtr) -> ClientToScreen;
     ntpPtr -> getPointFromNtPointPtr -> (h,v);
     ntpPtr -> free;
  exit (h,v)
  #);

getBounds:
  (# theNtRectPtr: @integer;
     r: ^rectangle;
     ID: @integer;
  enter ID
     do 16 -> malloc -> theNtRectPtr;
     (ID,theNtRectPtr) -> GetClientRect;
     &rectangle[] -> r[];
     theNtRectPtr -> getRectFromNtRectPtr -> r;
     r.topLeft -> translate -> r.topLeft;
     r.bottomRight -> translate -> r.bottomRight;
     theNtRectPtr -> free;
  exit r[]
  #);

onPopup:
  (# thePopUpmenu: ^menu;
  do popupMenu -> thePopUpMenu[];
     NONE -> popupMenu;
     thePopUpMenu[] -> popupMenu;
  #);

optionButtonPopupMenu:
  (#
  enter private.optionButtonPopupMenu[]
  exit private.optionButtonPopupMenu[]
  #);

addOptionButtonMenuItem:
  (# listBoxInx: @integer;
     theMenuitem: ^private.optionButtonPopupMenu.menuitem;
     elm: ^private.menuItemListInx;
  enter (listBoxInx,theMenuitem[])
  do &private.menuItemListInx[] -> elm[];
     listBoxInx -> elm.listBoxInx; theMenuitem[] -> elm.theMenuitem[];
     elm[]  -> private.menuItems.append;
  #);

clearOptionButtonMenuItems:
  (# 
  do private.menuItems.clear;
  #);

findoptionButtonMenuItem:
  (# listBoxInx: @integer; theMenuitem: ^private.optionButtonPopupMenu.menuitem;
  enter listBoxInx
  do listBoxInx -> private.menuItems.findByID -> theMenuitem[];
  exit theMenuitem[]
  #);

mbLabelID:
  (# 
  enter private.mbLabelID
  exit private.mbLabelID
  #);
dropDownListID:
  (# 
  enter private.dropDownListID
  exit private.dropDownListID
  #);

-- GUIENVoptionButtonprivate: descriptor --
(# mbLabelID,dropDownListID: @integer;
   optionButtonPopupMenu: ^menu;
   
   optFrameChangedAction: @frameChangedAction
     (# 
     do 'got framechanged for optionbutton' -> putline;
        'oldFrame: ' -> puttext; theEvent.oldFrame -> putrectangle; newline;
        'newFrame: ' -> puttext; theEvent.newFrame -> putrectangle; newline;
     #);
   
   menuItemListInx:
     (# listBoxInx: @integer;
        theMenuitem: ^optionButtonPopupMenu.menuitem;
     #);
   menuItems: @list
     (# element::< menuItemListInx;
        findByID:
          (# ID: @integer;
             theMenuitem: ^optionButtonPopupMenu.menuitem;
          enter ID
          do	l: scan
               (#
               do (if current.listBoxInx=ID then
                      current.theMenuitem[] -> theMenuitem[];
                      leave l;
                  if);
               #);
          exit theMenuitem[]
          #);
     #);   
#)

