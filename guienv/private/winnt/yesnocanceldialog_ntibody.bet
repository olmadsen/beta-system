ORIGIN 'yesnocanceldialogbody';
INCLUDE '~beta/win32lib/v1.2/winuserconsts';
INCLUDE '~beta/win32lib/v1.2/messageboxsupport';
INCLUDE '~beta/guienv/v1.5/private/winnt/guienv_ntiprivate';

-- yesNoDialogOpen: doPart --
do style -> private.style;
   
-- yesNoDialogRunDialog: doPart --
do (# fatherID: @integer;
   do (if title.length=0 then
          'DHM QUESTION' -> title;
      if);
      (if message[]=NONE then
          &text[] -> message[];
      if);
      (if private.style<>0 then
          private.style + MB_SETFOREGROUND -> private.style;
          MB_ICONQUESTION -> MessageBeep;
          
          
          this(guienv).private.windows.scan
          (# windowID: @integer;
          do current.interfaceObjectID -> windowID;
             (if windowID<>0 then
                 (if current.floating then
                     (windowID,HWND_NOTOPMOST,0,0,0,0,SWP_NOMOVE + SWP_NOSIZE + SWP_NOACTIVATE) 
                       -> SetWindowPos;
                 if);
             if);
          #);
          
          
          (if father[]=NONE then
              (0,message,title,private.style) -> MessageBox -> answer;
           else
              father.interfaceObjectID -> fatherID;
              (fatherID,message,title,private.style) -> MessageBox -> answer;
          if);
          
          this(guienv).private.windows.scan
          (# windowID: @integer;
          do current.interfaceObjectID -> windowID;
             (if windowID<>0 then
                 (if current.floating then
                     (windowID,HWND_TOPMOST,0,0,0,0,SWP_NOMOVE + SWP_NOSIZE + SWP_NOACTIVATE) 
                       -> SetWindowPos;
                 if);
             if);
          #);
       else
          noStyleException;
      if);
   #)
-- yesNoDialogAttributes: descriptor --
(# style: @integer;
#)
