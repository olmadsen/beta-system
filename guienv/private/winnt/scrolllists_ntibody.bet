ORIGIN '../scrolllistsbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1994-96
 *       All rights reserved.
 *)
INCLUDE 'guienv_ntiprivate';
INCLUDE 'guienvattributes';
INCLUDE '~beta/win32lib/v1.2/winuserconsts';
INCLUDE '~beta/win32lib/v1.2/errorhandling';
INCLUDE '~beta/sysutils/v1.5/cstring';

-- GUIENVscrollListnumberOfItems: descriptor --
assertOpen
(# location::(# do 'ScrollList.numberOfItems'->t[] #); 
   windowID: @integer;
do interfaceObjectID -> windowID;
   (windowID,LB_GETCOUNT,0,0) -> SendMessage -> value;
   (if value=LB_ERR then
       'LB_GETCOUNT falied. Errorcode: ' -> puttext;
       GetLastError -> putint; newline;
       0 -> value;
   if);
#)

-- GUIENVscrollListInsert: descriptor --
assertOpen
(# location::(# do 'ScrollList.insert'->t[] #);
   cStr: @cString;
   windowID: @integer;
   beforeInx: @integer;
do (if beforeItem>0 then
       interfaceObjectID -> windowID;
       '' -> cStr.set;
       beforeItem - 1 -> beforeInx;
       (for numOfItems repeat
            (windowID,LB_INSERTSTRING,beforeInx,cstr.charPtr)
              -> SendMessage;
       for);
       cStr.free;
   if);
#)

-- GUIENVscrollListPrepend: descriptor --
assertOpen
(# location::(# do 'ScrollList.prepend'->t[] #);
   result: @integer;
   cstr: @cString;
   windowID: @integer;
do interfaceObjectID -> windowID;
   '' -> cstr.set;
   (for i:numOfItems repeat
        (windowID,LB_INSERTSTRING,i-1,cstr.charPtr) 
          -> SendMessage -> result;
        (if result
         //LB_ERR then
         //LB_ERRSPACE then
            'scrollListPrepend failed! Errorcode: ' -> puttext;
            GetLastError -> putint; newline;
        if);
   for);
   cstr.free;
#)

-- GUIENVscrollListAppend: descriptor --
assertOpen
(# location::(# do 'ScrollList.append'->t[] #); 
   result: @integer;
   cstr: @cString;
   windowID: @integer;
do interfaceObjectID -> windowID;
   '' -> cstr.set;
   (for i:numOfItems repeat
        (windowID,LB_INSERTSTRING,-1,cstr.charPtr) 
          -> SendMessage -> result;
        (if result
         //LB_ERR then
         //LB_ERRSPACE then
            'scrollListAppend failed! Errorcode: ' -> puttext;
            GetLastError -> putint; newline;
        if);
   for);
   cstr.free;
#)

-- GUIENVscrollListDelete: descriptor --
assertOpen
(# location::(# do 'ScrollList.delete'->t[] #); 
   inx: @integer; result: @integer; upper: @integer;
   windowID: @integer;
do interfaceObjectID -> windowID;
   firstItem -> inx;
   (this(scrollList).numberOfItems, numOfItems + firstItem - 1)->min->upper;
   (if (inx<numberOfItems+1) and (inx>0) then
       firstItem -> inx;
       l:
         (if (inx>upper) then leave l
          else
             (windowID,LB_DELETESTRING,firstItem-1,0) 
               -> SendMessage -> result;
             (if result=LB_ERR then
                 'scrollListDelete failed. Errorcode: ' -> puttext;
                 GetLastError -> putint; newline;
             if);
             inx + 1 -> inx;
             restart l;
         if);
   if);
#)

-- GUIENVscrollListDeleteFirst: descriptor --
assertOpen
(# location::(# do 'ScrollList.deleteFirst'->t[] #);
do (1,numOfItems)  -> delete;
#)

-- GUIENVscrollListDeleteLast: descriptor --
assertOpen
(# location::(# do 'ScrollList.deleteLast'->t[] #);
do (numberOfItems - numOfItems,numOfItems) -> delete;
#)

-- GUIENVscrollListSetItemHeight: descriptor --
assertopen
(# location::(# do 'ScrollList.SetItemHeight'->t[] #);
   result: @integer;
   windowID: @integer;
do interfaceObjectID -> windowID;
   (windowID,LB_SETITEMHEIGHT,0,h) -> SendMessage -> result;
   (if result=LB_ERR then
       'scrollListSetItemHeight failed!! Errorcode: ' -> puttext;
       GetLastError -> putint; newline;
   if);
#)

-- GUIENVscrollListGetItemHeight: descriptor --
assertopen
(# location::(# do 'ScrollList.getItemHeight'->t[] #);
   windowID: @integer;
do interfaceObjectID -> windowID;
   (windowID,LB_GETITEMHEIGHT,0,0) -> SendMessage -> h;
   (if h=LB_ERR then
       'scrollListGetItemHeight failed!! Errorcode: ' -> puttext;
       GetLastError -> putint; newline;
       0 -> h;
   if);
#)

-- GUIENVscrollListSetSingleSelection: descriptor --
assertOpen
(# location::(# do 'ScrollList.setSingleSelection'->t[] #);
do isSingle -> isSingleSel;
#)

-- GUIENVscrollListGetSingleSelection: descriptor --
assertOpen
(# location::(# do 'ScrollList.getSingleSelection'->t[] #);
do isSingleSel -> isSingle;
#)

-- GUIENVItemGetItemRectangle: descriptor --
assertOpen
(# location::(# do 'ScrollList.getItemRectangle'->t[] #);
   theNtRect: @integer; result: @integer;
   windowID: @integer;
do interfaceObjectID -> windowID;
   16 -> malloc -> theNtRect;
   (windowID,LB_GETITEMRECT,theItem,theNtRect) -> SendMessage
     -> result;
   (if result=LB_ERR then
       'scrollListGetItemRectangle failed!! Errorcode: ' -> puttext;
       GetLastError -> putint; newline;
   if);
   theNtRect -> TOS'%adrGetLong'  -> theRectangle.left;
   theNtRect + 4 -> TOS'%adrGetLong' -> theRectangle.top;
   theNtRect + 8 -> TOS'%adrGetLong' -> theRectangle.right;
   theNtRect + 12 -> TOS'%adrGetLong' -> theRectangle.bottom;
   theNtRect -> free;
#)

-- GUIENVselectionClear: descriptor --
assertOpen
(# location::(# do 'Scrolllist.selection.clear'->t[] #);
   result: @integer;
   windowID: @integer;
do interfaceObjectID -> windowID;
   (if isSingleSel then
       (windowID,LB_SETCURSEL,-1,0) -> SendMessage -> result;    
    else
       (windowID,LB_SETSEL,0,-1) -> SendMessage -> result;    
       (if result=LB_ERR then
       'selectionClear failed. Errorcode: ' -> puttext;
       GetLastError -> putint;
       if);
   if);
#)

-- GUIENVselectionscrollIntoView: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.scrollIntoView'->t[] #);
   result: @integer; theItem: @integer;
   windowID: @integer;
do interfaceObjectID -> windowID;
   first -> theItem;
   (if ( (theItem>0) and (theItem<numberOfItems+1) ) then
     (windowID,LB_SETTOPINDEX,theItem-1,0) -> SendMessage -> result;
     (if result=LB_ERR then
        'selectionScrollIntoView failed. Errorcode: ' -> puttext;
        GetLastError -> putint; newline;
     if);
   if);
#)

-- GUIENVselectionFirst: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.first'->t[] #);
   selCount: @integer; result: @integer;
   sel: [0]@integer;
   windowId: @integer
do interfaceObjectID -> windowId;
   (if isSingleSel then
       (windowId,LB_GETCURSEL,0,0) -> SendMessage -> selCount;
       (if selCount=LB_ERR then
           0 -> value;
        else
           selCount+1 -> value;
       if);
    else
       (windowId,LB_GETSELCOUNT,0,0) -> SendMessage -> selCount;
       (if selCount=LB_ERR then
           'SelectionFirst failed. LB_GETSELCOUNT. Errorcode: ' -> puttext;
           GetLastError -> putint; newline;
        else
           (if (selCount>0) then
               selCount -> sel.new;
               (windowId,LB_GETSELITEMS,selCount,@@sel[1])->SendMessage 
                 -> result;
               (if result=LB_ERR then
                   'SelectionFirst failed. LB_GETSELITEMS. Errorcode: '->puttext;
                   GetLastError -> putint; newline;
                else
                   sel[1] + 1 -> value;
               if);
           if);
       if);
   if);
#)

-- GUIENVselectionLast: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.last'->t[] #);
   selCount: @integer; result: @integer;
   sel: [0]@integer;
   windowId: @integer;
do interfaceObjectID -> windowId;
   (if isSingleSel then
       (windowId,LB_GETCURSEL,0,0) -> SendMessage -> selCount;
       (if selCount=LB_ERR then
           0 -> value;
        else
           selCount+1 -> value;
       if);
    else
       (windowId,LB_GETSELCOUNT,0,0) -> SendMessage -> selCount;
       (if selCount=LB_ERR then
           'SelectionLast failed. LB_GETSELCOUNT. Errorcode: ' -> puttext;
           GetLastError -> putint; newline;
        else
           (if (selCount>0) then
               selCount -> sel.new;
               (windowId,LB_GETSELITEMS,selCount,@@sel[1]) 
                 -> SendMessage -> result;
               (if result=LB_ERR then
                   'SelectionLast failed. LB_GETSELITEMS. Errorcode: ' -> puttext;
                   GetLastError -> putint; newline;
                else
                   sel[sel.range] + 1 -> value;
               if);
           if);
       if);
   if);
#)

-- GUIENVselectionSelectItem: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.selectItem'->t[] #);
   result: @integer;
   windowID: @integer;
do interfaceObjectID -> windowId;
   (if ( (theItem>0) and (theItem<numberOfItems+1) ) then
       (if isSingleSel then
           (windowId,LB_SETCURSEL,theItem-1,0) 
             -> SendMessage -> result;    
        else
           (if not extend then
               (windowId,LB_SETSEL,0,-1) -> SendMessage -> result;
           if);
           (windowId,LB_SETSEL,1,theItem-1) -> SendMessage -> result;
           (if result=LB_ERR then
               'selectionSelect failed. Errorcode: ' -> puttext;
               GetLastError -> putint;
           if);
       if);
   if);
#)

-- GUIENVselectionDeselect: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.deselect'->t[] #);
   result: @integer;
   windowId: @integer;
do (if ( (theItem>0) and (theItem<numberOfItems+1) ) then
       interfaceObjectID -> windowId;
       (if isSingleSel then
           (windowId,LB_SETCURSEL,-1,0) -> SendMessage -> result;    
        else
           (windowId,LB_SETSEL,0,theItem-1) -> SendMessage -> result;
           (if result=LB_ERR then
               'selectionDeselect failed. Errorcode: ' -> puttext;
               GetLastError -> putint;
           if);
       if);
   if);
#)

-- GUIENVselectionHas: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.has'->t[] #);
   result: @integer;
   windowId: @integer;
do (if true
    // (theItem<0) 
    // (theItem>numberOfItems) then false -> value;
    else
       interfaceObjectID -> windowId;
       (windowId,LB_GETSEL,theItem-1,0) -> SendMessage -> result;
       (if true
        //(result=0) then false -> value;
        //(result>0) then true -> value;
        //(result=LB_ERR) then false -> value;
           'scrollListSelection failed. Errorcode: ' -> puttext;
           GetLastError -> putint; newline;
       if);
   if);
#)

-- GUIENVscrollListScanSelection: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.scan'->t[] #);
   selCount: @integer; result: @integer;
   sel: [0]@integer;
   windowId: @integer;
do interfaceObjectID -> windowId;
   (windowId,LB_GETSELCOUNT,0,0) -> SendMessage -> selCount;
   (if selCount=LB_ERR then
      'scrollListScanSelection failed. LB_GETSELCOUNT. Errorcode: ' -> puttext;
      GetLastError -> putint; newline;
   else
     (if (selCount>0) then
       selCount -> sel.new;
       (windowId,LB_GETSELITEMS,selCount,@@sel[1]) -> SendMessage 
         -> result;
       (if result=LB_ERR then
         'scrollListScanSelection failed. LB_GETSELITEMS. Errorcode: ' -> puttext;
         GetLastError -> putint; newline;
       else
         (for inx: sel.range repeat
           sel[inx] + 1 -> current;
           inner scanSelection;
         for);
       if);
     if);
   if);
   
#)

-- GUIENVscrollListScan: descriptor --
assertOpen
(# location::(# do 'ScrollList.scan'->t[] #);
do (for inx: numberOfItems repeat
       inx -> current;
       inner scan;
   for);
#)

-- GUIENVscrollListCreate: descriptor --
(# wStyle: @integer;
do isSubClass -> windowItemClassStatus;
   'LISTBOX' -> windowItemClass;
   true -> singleSelection;
   inner create;
   
   (LBS_STANDARD,LBS_NOINTEGRALHEIGHT) -> tos'%or' -> wStyle;
   (wStyle,LBS_DISABLENOSCROLL) -> tos'%or' -> wStyle;
   (wStyle,LBS_NOTIFY) -> tos'%or' -> wStyle;
   
   (if not singleSelection then
       (wStyle,LBS_EXTENDEDSEL) -> tos'%or' -> wStyle;
   if);
   wStyle -> windowItemStyle;
#)

-- GUIENVscrollListOpen: descriptor --
(# theTextStyle: ^textStyle;
   windowID, textStyleID: @integer;
do interfaceObjectID -> windowID;
   ((0,0),(100,100)) -> windowItemFrame;
   defaultScrollListTextStyle -> theTextStyle[];
   (if theTextStyle[]=NONE then
       &textStyle[] -> theTextStyle[];
       'Arial' -> theTextStyle.name;
       10 -> theTextStyle.size;       
       theTextStyle.create;
       theTextStyle[] -> defaultScrollListTextStyle;
   if);
   theTextStyle[] -> scrollListStyle;
   theTextStyle.textStyleID -> textStyleID;
   (windowID,WM_SETFONT,textStyleID,1) -> SendMessage;
   inner open;
#)

-- GUIENVscrollListClose: descriptor --
(# 
   theTextStyle: ^textStyle;
do inner close;
   scrollListStyle -> theTextStyle[];
   (if theTextStyle[]<>defaultScrollListTextStyle then
       (if theTextStyle[]<>NONE then
           theTextStyle.dispose;
       if);
   if);
#)

-- GUIENVScrollListonFrameChanged: descriptor --
(#
do inner onFrameChanged;
#)

-- GUIENVScrollListonRefresh: descriptor --
(#
do inner onRefresh;
#)

-- GUIENVScrollListonMouseDown: descriptor --
(#
do inner onMouseDown;
#)

-- GUIENVScrollListonActivate: descriptor --
(#
do inner onActivate;
#)

-- GUIENVScrollListonDeactivate: descriptor --
(#
do inner onDeactivate;
#)

-- scrollListLib: attributes --
isSingleSel:
  (# 
  enter private.isSingle
  exit private.isSingle
  #);
scrollListStyle:
  (#
  enter private.scrollListStyle[]
  exit private.scrollListStyle[]
  #);

handleNotification:
  (# notifyType: @integer;
     index: @integer;
  enter notifyType
  do (if notifyType
      //LBN_ERRSPACE then 
         '*** WARNING: lbn_errspace.' -> putLine;
      //LBN_SELCHANGE then
         (interfaceObjectID,LB_GETANCHORINDEX,0,0) -> SendMessage -> index;
         (index+1, false) -> theEventHandler.onSelect;
      //LBN_DBLCLK then
         (interfaceObjectID,LB_GETANCHORINDEX,0,0) -> SendMessage -> index;
         (index+1, true) -> theEventHandler.onSelect;
     if);
  #);



-- GUIENVscrollListPrivate: descriptor --
(# isSingle: @boolean;
   scrollListStyle: ^textStyle;
   maxWidthText: @text;
   updateMaxLength:
     (# theText: ^text;
     enter theText[]
     do (if (theText.length>maxWidthText.length) then 
            theText -> maxWidthText; 
        if);
     #);
   preferredSize:
     (# prefWidth, prefHeight: @integer;
        theTextStyle: ^textStyle;
        widthOfLabel: @integer;
        fatherWidth, fatherHeight: @integer;
     do size -> (prefWidth, prefHeight);
       scrollListStyle[] -> theTextStyle[];
        maxWidthText[] -> theTextStyle.widthOfText -> widthOfLabel;
        widthOfLabel + 18 -> prefWidth;
        numberOfItems * itemHeight -> prefHeight;
        father.size -> (fatherWidth, fatherHeight);
        (* (if prefHeight>fatherHeight then fatherHeight - (2*itemHeight) -> prefHeight; if); *)
     exit (prefWidth, prefHeight)
     #);
#)

-- GUIENVtextScrollListSet: descriptor --
assertOpen
(# location::(# do 'TextScrollList.set'->t[] #);
   result: @integer;
   cstr: @cString;
   windowID: @integer;
do (if ((theItem>0) and (theItem<numberOfItems+1)) then
       interfaceObjectID -> windowID;
       (windowID,LB_DELETESTRING,theItem-1,0) -> SendMessage -> result;
       theText[] -> cstr.set;
       (windowID,LB_INSERTSTRING,theItem-1,cstr.charPtr) 
         -> SendMessage -> result;
       (if result
        //LB_ERR 
        //LB_ERRSPACE then
           'scrollListSet failed. Errorcode: ' -> puttext;
           GetLastError -> putint; newline;
        else 
           theText[] -> private.updateMaxLength;
       if);
       cstr.free;
   if);
#)

-- GUIENVtextScrollListGet: descriptor --
assertOpen
(# location::(# do 'TextScrollList.get'->t[] #);
   length: @integer; cstr: @cString;
   windowID: @integer;
do (if ( (theItem>0) and (theItem<numberOfItems+1) ) then
       interfaceObjectID -> windowID;
       (windowID,LB_GETTEXTLEN,theItem-1,0) -> SendMessage ->length;
       (if length=LB_ERR then
           'LB_GETTEXTLENGTH failed. Errorcode: ' -> screen.putText;
           GetLastError -> screen.putint; screen.newline;
        else
           length+1 -> cstr.init;
           (windowID,LB_GETTEXT,theItem-1,cstr.charPtr)-> SendMessage 
             -> length;
           (if length=LB_ERR then
               'LB_GETTEXT failed. Errorcode: ' -> screen.putText;
               GetLastError -> screen.putint; screen.newline;
            else
               cstr.get -> theText[];
               theText.T.range -> theText.pos -> theText.lgth;
               cstr.free;
           if);
       if);
   if);
#)

-- GUIENVtextScrollListSetTextStyle: descriptor --
assertOpen
(# location::(# do 'TextScrollList.setTextStyle'->t[] #);
   windowID: @integer;
do theStyle[] -> scrollListStyle;
   theStyle.create;
   interfaceObjectID -> windowID;
   (windowID,WM_SETFONT,theStyle.textStyleID,1) -> SendMessage;
#)

-- GUIENVtextScrollListGetTextStyle: descriptor --
assertOpen
(# location::(# do 'TextScrollList.getTextStyle'->t[] #);
do scrollListStyle -> theStyle[];
#)

-- GUIENVtextScrollListCreate: descriptor --
(#
do inner create;
#)

-- GUIENVtextScrollListOpen: descriptor --
(#
do inner open;
#)

-- GUIENVtextScrollListClose: descriptor --
(#
do inner close;
#)

-- GUIENVscrollListputLayout: doPart --
do

-- GUIENVscrollListgetLayout: doPart --
do

-- GUIENVtextscrollListputLayout: doPart --
do

-- GUIENVtextscrollListgetLayout: doPart --
do
