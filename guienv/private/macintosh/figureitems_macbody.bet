ORIGIN '../figureitemsbody';

INCLUDE 'guienv_macprivate';
INCLUDE '~beta/maclib/v3.0/quickdraw';
INCLUDE 'raster_macprivate';
INCLUDE '../../graphics';
INCLUDE 'graphicslib';

-- GUIENVpenSetForegroundColor: descriptor --
(#
do assertOpen;
	theColor -> penForegroundColor;
#)

-- GUIENVpenGetForegroundColor: descriptor --
(#
do assertOpen;
	penForegroundColor -> theColor;
#)

-- GUIENVpenSetBackgroundColor: descriptor --
(#
do assertOpen;
	theColor -> penBackgroundColor;
#)

-- GUIENVpenGetBackgroundColor: descriptor --
(#
do assertOpen;
	penBackgroundColor -> theColor;
#)

-- GUIENVpenSetStipple: descriptor --
(#
do assertOpen;
	p[] -> penStipple;
#)

-- GUIENVpenGetStipple: descriptor --
(#
do assertOpen;
	penStipple -> p[];
#)

-- GUIENVpenSetSize: descriptor --
(#
do assertOpen;
	(value,value) -> figureItemPenSize;
#)

-- GUIENVpenGetSize: descriptor --
(#
do assertOpen;
	figureItemPenSize -> (value,value); (*(width,height);*)
#)



-- GUIENVfigureItemCreate: descriptor --
(#
do noneBackGround -> backGroundStyle;
   true -> this(windowItem).private.isFigureItem;
   inner create;
#)

-- GUIENVfigureItemOpen: descriptor --
(#
do patterns.black[] -> pen.stipple;
	1 -> pen.size;
	(65535, 65535, 65535) -> pen.backgroundColor;
	(0, 0, 0) -> pen.foreGroundColor;
	true -> this(windowItem).private.updateOnResize;
   inner open;
#)

-- GUIENVfigureItemOnRefresh: descriptor --
(#
do	inner onRefresh;
#)

-- figureItemLib: attributes --

	
penForegroundColor:
	(#
	enter private.penForegroundColor
	exit private.penForegroundColor
	#);
penBackgroundColor:
	(#
	enter private.penBackgroundColor
	exit private.penBackgroundColor
	#);
penStipple:
	(#
	enter private.penStipple[]
	exit private.penStipple[]
	#);
figureItemPenSize:
	(#
	enter private.penSize
	exit private.penSize
	#);
figureItemInk:
	(#
	enter private.figureItemInk
	exit private.figureItemInk
	#);
	
drawWithPen: father.graphics
  (# 
  do (if this(figureItem).pen.stipple<>none then
  			(* false -> clipChildren; *)
         this(figureItem).pen.size -> pen.size;
         this(figureItem).pen.backgroundColor -> pen.backgroundColor;
         this(figureItem).pen.foregroundColor -> pen.foregroundColor;
         this(figureItem).pen.stipple -> pen.stipple;
			inner;
     if);
  #);
  
-- GUIENVfigureItemPrivate: descriptor --
(#	penForegroundColor,penBackgroundColor: @color;
	penStipple: ^raster;
	penSize: @point;
	figureItemInk: @integer;
#)

-- GUIENVlineSetStart: descriptor --
(#
do assertOpen;
	theStart -> lineStart;
	adjustFrame;
#)

-- GUIENVlineGetStart: descriptor --
(#
do assertOpen;
	lineStart -> theStart;
#)

-- GUIENVlineSetEnd: descriptor --
(#
do assertOpen;
	theEnd -> lineEnd;
	adjustFrame;
#)

-- GUIENVlineGetEnd: descriptor --
(#
do assertOpen;
	lineEnd -> theEnd;
#)

-- GUIENVLineOpen: descriptor --
(#
do	
	inner open;
#)

-- GUIENVlineOnRefresh: descriptor --
(#
do	drawWithPen
   (# 
   do (start,end) -> drawLine;
   #);
	inner onRefresh;
#)

-- GUIENVlineonFrameChanged: descriptor --
(# dh, dv: @integer;
	oldWidth, newWidth, oldHeight, newHeight: @integer;
do	(if not private.adjustingFrame then
		oldFrame.size -> (oldWidth, oldHeight);
		newFrame.size -> (newWidth, newHeight);
		(if (oldWidth = newWidth) and (oldHeight = newHeight) then
			(newFrame.left - oldFrame.left) -> dh;
			(newFrame.top - oldFrame.top) -> dv;
			(dh, dv) -> private.lineStart.add;
			(dh, dv) -> private.lineEnd.add;
		else
			newFrame.topLeft -> private.lineStart;
			(newFrame.right - pen.size, newFrame.bottom - pen.size)
				-> private.lineEnd;
		if);
	if);
#)

-- GUIENVlineonHiliteChanged: descriptor --
(#
do	inner onHiliteChanged;
#)

-- lineLib: attributes --
lineStart:
	(#
	enter private.lineStart
	exit private.lineStart
	#);
lineEnd:
	(#
	enter private.lineEnd
	exit private.lineEnd
	#);
adjustFrame:
	(#	f: @rectangle;
		
	do true -> private.adjustingFrame;
		(lineStart,lineEnd) -> f.setFromPoints;
		f.right + pen.size -> f.right;
		f.bottom + pen.size -> f.bottom;
		f -> frame;
		false -> private.adjustingFrame;
	#);
-- GUIENVlinePrivate: descriptor --
(# lineStart,lineEnd: @point;
	adjustingFrame: @boolean;
#)

-- GUIENVFillSetTile: descriptor --
(#
do	assertOpen;
	p[] -> fillTile;
#)

-- GUIENVfillGetTile: descriptor --
(#
do assertOpen;
	fillTile -> p[];
#)

-- GUIENVFillSetForegroundColor: descriptor --
(#
do	assertOpen;
	theColor -> fillForegroundColor;
#)

-- GUIENVfillGetForegroundColor: descriptor --
(#
do assertOpen;
	fillForegroundColor -> theColor;
#)

-- GUIENVFillSetBackgroundColor: descriptor --
(#
do assertOpen;
	theColor -> fillBackgroundColor;
#)

-- GUIENVfillGetBackgroundColor: descriptor --
(#
do assertOpen;
	fillBackgroundColor -> theColor;
#)

-- GUIENVshapeOpen: descriptor --
(#
do (65535, 65535, 65535) -> private.fillBackgroundColor;
   (0, 0, 0) -> private.fillForegroundColor;
	inner open;
#)

-- GUIENVshapeOnRefresh: descriptor --
(#
do	inner onRefresh;
#)

-- GUIENVshapeonHiliteChanged: descriptor --
(#
do	inner onHiliteChanged;
#)

-- shapeLib: attributes --
fillTile:
	(#
	enter private.fillTile[]
	exit private.fillTile[]
	#);
fillBackgroundColor:
	(#
	enter private.fillBackgroundColor
	exit private.fillBackgroundColor
	#);
fillForegroundColor:
	(#
	enter private.fillForegroundColor
	exit private.fillForegroundColor
	#);
getMacRect:
	(#	r: @rectangle;
		mr: ^macRect;
	do frame -> r;
		r[] -> makeMacrect -> mr[];
	exit mr[]
	#);
drawWithFill: father.graphics
  (# 
  do (if fill.tile<>none then
  			(* false -> clipChildren; *)
         fill.foreGroundColor -> pen.foreGroundColor;
         fill.backgroundColor -> pen.backgroundColor;
         fill.tile -> pen.stipple; 
         inner;
     if);
  #);

-- GUIENVshapePrivate: descriptor --
(# fillTile: ^raster;
	fillBackgroundColor,fillForegroundColor: @color;
#)

-- GUIENVovalOpen: descriptor --
(#
do	inner open;
#)

-- GUIENVovalOnRefresh: descriptor --
(#
do	drawWithFill
   (# 
   do frame -> fillOval;
   #);
   drawWithPen
   (# 
   do frame -> drawOval;
   #);
	inner onRefresh;
#)

-- GUIENVrectOpen: descriptor --
(#
do	inner open;
#)

-- GUIENVrectOnRefresh: descriptor --
(#
do	drawWithFill
   (# 
   do frame  -> fillRect;
   #);
   drawWithPen
   (# 
   do frame -> drawRect;
   #);
	inner onRefresh;
#)

-- GUIENVRoundRectOpen: descriptor --
(#
do	inner open;
#)

-- GUIENVroundRectSetRoundness: descriptor --
(#
do	assertOpen;
	(theOvalHeight,theOvalWidth) ->  roundrectRoundNess
#)

-- GUIENVroundRectGetRoundness: descriptor --
(#
do assertOpen;
	roundrectRoundNess -> (theOvalHeight,theOvalWidth)
#)

-- GUIENVroundRectOnRefresh: descriptor --
(#
do	drawWithFill
   (# 
   do (frame,((0,0),roundness)) -> fillRoundRect;
   #);
   drawWithPen
   (# 
   do (frame,((0,0),roundness)) -> drawRoundRect;
   #);
	inner onRefresh;
#)

-- roundRectLib: attributes --
roundrectRoundNess:
	(#
	enter private.roundrectRoundNess
	exit private.roundrectRoundNess
	#);
-- GUIENVroundRectPrivate: descriptor --
(#	roundrectRoundNess: @point;
#)

-- GUIENVWedgeOpen: descriptor --
(#
do	inner open;
#)

-- GUIENVwedgeSetStartAngle: descriptor --
(#
do assertOpen;
	angle -> wedgeStartAngle;
#)

-- GUIENVwedgeGetStartAngle: descriptor --
(#
do assertOpen;
	wedgeStartAngle -> angle;
#)

-- GUIENVwedgeSetEndAngle: descriptor --
(#
do assertOpen;
	angle -> wedgeEndAngle;
#)

-- GUIENVwedgeGetEndAngle: descriptor --
(#
do assertOpen;
	wedgeEndAngle -> angle;
#)

-- GUIENVwedgeOnRefresh: descriptor --
(#
do	drawWithPen
   (# 
   do (frame,startAngle,endAngle) -> drawSlice;
   #);
   drawWithFill
   (# 
   do (frame,startAngle,endAngle) -> fillSlice;
   #);
	inner onRefresh;
#)

-- wedgeLib: attributes --
wedgeStartAngle:
	(#
	enter private.wedgeStartAngle
	exit private.wedgeStartAngle
	#);
wedgeEndAngle:
	(#
	enter private.wedgeEndAngle
	exit private.wedgeEndAngle
	#);
-- GUIENVwedgePrivate: descriptor --
(#	wedgeStartAngle,wedgeEndAngle: @integer;
#)

-- GUIENVpolygonSetPoints: descriptor --
(#
do	assertOpen;
	thePoints.range -> private.thePoints.new;
   (for i: thePoints.range repeat
        &point[] -> private.thePoints[i][];
        thePoints[i] -> private.thePoints[i];
   for);
	adjustFrame;
#)

-- GUIENVpolygonGetPoints: descriptor --
(#
do	assertOpen;
	private.thePoints -> private.thePoints;
#)

-- GUIENVpolygonOpen: descriptor --
(#
do	inner open;
	0 -> private.thePoints.new;
#)

-- GUIENVpolygonOnRefresh: descriptor --
(#
do	(if private.thePoints.range > 2 then
		drawWithFill
			(#
			do this(polygon).private.thePoints -> fillPolygon;
			#);
		drawWithPen
			(#
			do this(polygon).private.thePoints -> drawPolygon;
			#);
	if);
	inner onRefresh;
#)

-- GUIENVpolygononFrameChanged: descriptor --
(# dh, dv: @integer;
	oldWidth, newWidth, oldHeight, newHeight: @integer;
do	(if not private.adjustingFrame then
		oldFrame.size -> (oldWidth, oldHeight);
		newFrame.size -> (newWidth, newHeight);
		(if (oldWidth = newWidth) and (oldHeight = newHeight) then
			(newFrame.left - oldFrame.left) -> dh;
			(newFrame.top - oldFrame.top) -> dv;
			(for i: private.thePoints.range repeat
				(dh, dv) -> private.thePoints[i].add;
			for);
		if);
	if);
#)

-- GUIENVpolygonPrivate: descriptor --
(#	thePoints: [0] ^point;
	adjustingFrame: @boolean;
#)

-- polygonLib: attributes --

adjustFrame:
	(# f: @rectangle;
		x, y: @integer;
	do true -> private.adjustingFrame;
		(if private.thePoints.range > 0 then
			private.thePoints[1]
				-> f.topLeft -> f.bottomRight;
			(for i: private.thePoints.range repeat
				private.thePoints[i] -> (x, y);
				(x, f.left) -> min -> f.left;
				(x, f.right) -> max -> f.right;
				(y, f.top) -> min -> f.top;
				(y, f.bottom) -> max -> f.bottom;
			for);
			f.right + pen.size -> f.right;
			f.bottom + pen.size -> f.bottom;
			f -> frame;
		if);
		false -> private.adjustingFrame;
	#);

