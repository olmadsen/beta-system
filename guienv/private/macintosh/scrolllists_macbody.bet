ORIGIN '../scrolllistsbody'
[[
-- Include 'guienv_macprivate'
-- Include '~beta/maclib/v3.0/listmanager'

-- windowLib: attributes --
saveUpdateRgn:
	(# updateRgn, updateRgnCopy: @integer;
		updateRgnOffset: (# exit 122 #);
	do private.windowPointer + updateRgnOffset -> TOS'%adrGetLong' -> updateRgn;
		NewRgn -> updateRgnCopy;
		(updateRgn, updateRgnCopy) -> CopyRgn;
		inner;
		(updateRgnCopy, updateRgn) -> CopyRgn;
		updateRgnCopy -> DisposeRgn;
	#);
	
-- GUIENVscrollListnumberOfItems: descriptor --
(#
do	private.scrollListHandle -> ScrollListGetNumberOfItems -> value;
#)

-- GUIENVscrollListInsert: descriptor --
(# ignore: @integer;
do	focusScrollList
		(#
		do (numOfItems,beforeItem - 1,private.scrollListHandle) -> LAddRow -> ignore;
		#);
#)

-- GUIENVscrollListPrepend: descriptor --
(#	ignore: @Integer;
do focusScrollList
		(#
		do (numOfItems,0,private.scrollListHandle) -> LAddRow -> ignore;
		#);
#)

-- GUIENVscrollListAppend: descriptor --
(#	ignore: @Integer;
do focusScrollList
		(#
		do (numOfItems,-1,private.scrollListHandle) -> LAddRow -> ignore;
		#);
#)

-- GUIENVscrollListDelete: descriptor --
(#
do focusScrollList
		(#
		do (numOfItems,firstItem - 1,private.scrollListHandle) -> LDelRow;
		#);
#)

-- GUIENVscrollListDeleteFirst: descriptor --
(#
do focusScrollList
		(#
		do (numOfItems,0,private.scrollListHandle) -> LDelRow;
		#);
#)

-- GUIENVscrollListDeleteLast: descriptor --
(#
do focusScrollList
		(#
		do (numOfItems,numberOfItems - numOfItems,private.scrollListHandle) -> LDelRow;
		#);
#)

-- GUIENVscrollListSetItemHeight: descriptor --
(#
do focusScrollList
		(#
		do (private.scrollListHandle,h) -> ScrollListSetItemHeight;
		#);
#)

-- GUIENVscrollListGetItemHeight: descriptor --
(#
do private.scrollListHandle -> ScrollListGetItemHeight -> h;
#)

-- GUIENVscrollListSetSingleSelection: descriptor --
(#
do (private.scrollListHandle,isSingle) -> ScrollListSetSingle;
#)

-- GUIENVscrollListGetSingleSelection: descriptor --
(#
do	private.scrollListHandle -> ScrollListGetSingle -> isSingle;
#)

-- GUIENVItemGetItemRectangle: descriptor --
(#	cellRect: @macRect;
	theCell: @Cell;
	a,b,c,d: @Integer;
do	(0,theItem - 1) -> theCell.setPoint;
	(cellRect[],theCell,private.scrollListHandle) -> LRect;
	cellRect -> ((a,b),(c,d));
	((a,b),(c,d)) -> theRectangle;
#)

-- GUIENVselectionClear: descriptor --
(#
do scanSelection
		(#
		do current -> selection.deselect;
		#);
#)

-- GUIENVselectionscrollIntoView: descriptor --
(#
do	focusScrollList
		(#
		do private.scrollListHandle -> LAutoScroll;
		#);
#)

-- GUIENVselectionFirst: descriptor --
(#	theCell: @Cell;
	isSelect,h,v: @integer;
do	(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
	(if isSelect = 1 then
		theCell.getPoint -> (h,v);
		v +1 -> value;
	else
		0 -> value;
	if);
#)

-- GUIENVselectionLast: descriptor --
(#	theCell: @Cell;
	isSelect,h,v: @integer;
do	(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
	(if isSelect = 1  then
		loop:
			(if isSelect = 1  then
				(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
				theCell.getPoint -> (h,v);
				v  + 1 -> v;
				(h,v) -> theCell.setPoint;
				restart Loop;
			if);
		theCell.getPoint -> (h,v);
		v - 1 -> value;
	else 
		0 -> value;
	if);
#)

-- GUIENVselectionSelectItem: descriptor --
(#	theCell: @Cell;
do	(if extend//false then clear if);
	(0,theItem - 1)  -> theCell.setPoint;
	focusScrollList
		(#
		do (true,theCell,private.scrollListHandle) -> LSetSelect;
		#);
#)

-- GUIENVselectionDeselect: descriptor --
(#	theCell: @Cell;
do	focusScrollList
		(#
		do (0,theItem - 1) -> theCell.setPoint;
			(false,theCell,private.scrollListHandle) -> LSetSelect;
		#);
#)

-- GUIENVselectionHas: descriptor --
(#	theCell: @Cell;
	isSelect: @integer;
do	(0,theItem - 1) -> theCell.setPoint;
	(false,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
	isSelect -> value;
#)

-- GUIENVscrollListScanSelection: descriptor --
(#	isSelect,ignore,v: @integer;
	theCell: @Cell;
do	(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
	loop:
		(if isSelect = 1  then
			theCell.getPoint -> (ignore,current);
			current + 1 -> current;
			inner scanSelection;
			(0,current) -> theCell.setPoint;
			(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
			restart Loop
		if);
#)

-- GUIENVscrollListScan: descriptor --
(#
do	(for inx: numberOfItems repeat
		inx -> current;
		inner scan;
 	for);
#)

-- GUIENVscrollListCreate: descriptor --
(#
do	whiteBackGround -> backGroundStyle;
	inner create;
#)

-- GUIENVscrollListOpen: descriptor --
(#	cellSize: @macPoint;
	bounds,dataBounds: ^macRect;
	theStyle: ^textStyle;
	theWindowPointer: @integer;
do	((0,0),(100,100)) -> privateWindowItemFrame;
	father.calculateFocusForChildren;
	calculateBounds -> makeMacRect -> bounds[];
	calculateDataBounds -> makeMacRect -> dataBounds[];
	&textStyle[] -> theStyle[];
	'Chicago' -> theStyle.name;
	12 -> theStyle.size;
	theStyle[] -> scrollListStyle;
	(2000,0) -> cellSize.setPoint;
	windowPointer -> theWindowPointer;
	focusScrollList
		(#
		do (bounds[],dataBounds[],cellSize,0,theWindowPointer,true,false,false,true)
				-> LNew -> private.scrollListHandle;
		#);
	(if this(window).style = plain then
		borderStyles.simple -> border.style;
	else
		borderStyles.shadowIn -> border.style;
	if);
	true -> border.visible;
	inner open;
#)

-- GUIENVscrollListClose: descriptor --
(# LDispose: External
	(#	lHandle: @Integer;
	enter lHandle
	do '{$3F3C,$0028,$A9E7}' -> PascalTrap;
	#);
do inner close;
	private.scrollListHandle -> LDispose;
	0 -> private.scrollListHandle;
#)

-- GUIENVScrollListonFrameChanged: descriptor --
(#	width,height: @integer;
do calculateSize -> (width,height);
	focusScrollList
		(#
		do saveUpdateRgn
				(#
				do (width,height,private.scrollListHandle) -> LSize;
				#);
		#);
	 inner onFrameChanged;
#)

-- GUIENVScrollListonRefresh: descriptor --
(#
do focusScrollList
		(#	r: @rectangle;
			mr: ^macRect;
		do private.scrollListHandle -> ScrollListUpdate;
		#);
	inner onRefresh;
#)

-- GUIENVScrollListonMouseDown: descriptor --
(#	
	ignore: @boolean;
do focusScrollList
		(#	macPt: @macPoint;
		do localPosition -> macPt.setPoint;
			(macPt,this(guienv).private.event.modifiers,private.scrollListHandle) -> LClick -> ignore;
		#);
	inner onMouseDown;
#)

-- GUIENVScrollListonActivate: descriptor --
(#
do focusScrollList
		(#
		do (true,private.scrollListHandle) -> LActivate;
		#);
	inner onActivate;
#)

-- GUIENVScrollListonDeactivate: descriptor --
(#
do focusScrollList
		(#
		do (false,private.scrollListHandle) -> LActivate;
		#);
	inner onDeactivate;
#)

-- scrollListLib: attributes --
scrollListStyle:
	(#
	enter private.scrollListStyle[]
	exit private.scrollListStyle[]
	#);
calculateSize:
	(# width,height: @integer
	do size -> (width,height);
		width - 18 -> width;
		height - 4 -> height;
	exit (width,height)
	#);
calculateBounds:
	(#	r: ^rectangle;
	do &rectangle[] -> r[];
		(2,2) -> r.topLeft;
		calculateSize -> r.size;
	exit r[]
	#);
calculateDataBounds:
	(#	r: ^rectangle;
	do &rectangle[] -> r[];
		((0,0),(1,0)) -> r;
	exit r[]
	#);
focusScrollList: focus
	(#
	do scrollListStyle -> useTextStyle;
		ResetPen;
	   inner;
	#);
ScrollListGetNumberOfItems: external
	(# listHandle: @integer;
		value: @integer;
	enter listHandle
	exit value
	#);
ScrollListSetItemHeight: external
	(# listHandle: @integer;
		value: @integer;
	enter (listHandle,value)
	#);
ScrollListGetItemHeight: external
	(# listHandle: @integer;
		value: @integer;
	enter listHandle
	exit value
	#);
ScrollListSetSingle: external
	(# listHandle: @integer;
		value: @integer;
	enter (listHandle,value)
	#);
ScrollListGetSingle: external
	(# listHandle: @integer;
		value: @integer;
	enter listHandle
	exit value
	#);
ScrollListUpdate: external
	(# listHandle: @integer;
	enter listHandle
	#);
scrollListHandle:
	(#
	enter private.scrollListHandle
	exit private.scrollListHandle
	#);
-- GUIENVscrollListPrivate: descriptor --
(# scrollListHandle: @integer;
	scrollListStyle: ^textStyle;
#)

-- GUIENVtextScrollListSet: descriptor --
(#
do focusScrollList
		(#	theCell: @Cell;
			h: @integer;
		do	(0,theItem -1) -> theCell.setPoint;
			scrollListHandle -> h;
			(if (theText[] <> none) and (theText.lgth <> 0) then
				(@@theText.T[1],theText.lgth,theCell,h) -> LSetCell;
			else
				(0, 0, theCell, h) -> LSetCell;
			if);
		#);
#)

-- GUIENVtextScrollListGet: descriptor --
(#	count: @shortRef;
	theCell: @cell;
	h: @integer;
do	&text[] -> theText[];
	500 -> theText.extend;
	500 -> count;
	(0,theItem -1) -> theCell.setPoint;
	scrollListHandle -> h;
	(@@theText.T[1],count[],theCell,h) -> LGetCell;
	count -> theText.lgth -> theText.pos;
#)

-- GUIENVtextScrollListSetTextStyle: descriptor --
(#
do	theStyle[] -> scrollListStyle;
	updateMaybe;
#)

-- GUIENVtextScrollListGetTextStyle: descriptor --
(#
do scrollListStyle -> theStyle[];
#)

-- GUIENVtextScrollListCreate: descriptor --
(#
do inner create;
#)

-- GUIENVtextScrollListOpen: descriptor --
(#
do inner open;
#)

-- GUIENVtextScrollListClose: descriptor --
(#
do inner close;
#)
-- GUIENVscrollListputLayout: doPart --
do

-- GUIENVscrollListgetLayout: doPart --
do

-- GUIENVtextscrollListputLayout: doPart --
do

-- GUIENVtextscrollListgetLayout: doPart --
do


--]]
