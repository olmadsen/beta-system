ORIGIN '../scrolllistsbody'
[[
-- Include 'uienv_macprivate'
-- Include '~beta/maclib/v2.7.1/listmanager'
-- UIENVscrollListnumberOfItems: descriptor --
(#
do	scrollListHandle -> ScrollListGetNumberOfItems -> value;
#)

-- UIENVscrollListInsert: descriptor --
(# ignore: @integer;
do	focusScrollList
		(#
		do (numOfItems,beforeItem - 1,private.scrollListHandle) -> LAddRow -> ignore;
		#);
#)

-- UIENVscrollListPrepend: descriptor --
(#	ignore: @Integer;
do focusScrollList
		(#
		do (numOfItems,0,private.scrollListHandle) -> LAddRow -> ignore;
		#);
#)

-- UIENVscrollListAppend: descriptor --
(#	ignore: @Integer;
do focusScrollList
		(#
		do (numOfItems,-1,private.scrollListHandle) -> LAddRow -> ignore;
		#);
#)

-- UIENVscrollListDelete: descriptor --
(#
do focusScrollList
		(#
		do (numOfItems,firstItem - 1,private.scrollListHandle) -> LDelRow;
		#);
#)

-- UIENVscrollListDeleteFirst: descriptor --
(#
do focusScrollList
		(#
		do (numOfItems,0,private.scrollListHandle) -> LDelRow;
		#);
#)

-- UIENVscrollListDeleteLast: descriptor --
(#
do focusScrollList
		(#
		do (numOfItems,numberOfItems - numOfItems,private.scrollListHandle) -> LDelRow;
		#);
#)

-- UIENVscrollListSetItemHeight: descriptor --
(#
do focusScrollList
		(#
		do (private.scrollListHandle,h) -> ScrollListSetItemHeight;
		#);
#)

-- UIENVscrollListGetItemHeight: descriptor --
(#
do private.scrollListHandle -> ScrollListGetItemHeight -> h;
#)

-- UIENVscrollListSetSingleSelection: descriptor --
(#
do (private.scrollListHandle,isSingle) -> ScrollListSetSingle;
#)

-- UIENVscrollListGetSingleSelection: descriptor --
(#
do	private.scrollListHandle -> ScrollListGetSingle -> isSingle;
#)

-- UIENVItemGetItemRectangle: descriptor --
(#	cellRect: @macRect;
	theCell: @Cell;
	a,b,c,d: @Integer;
do	(0,theItem - 1) -> theCell.setPoint;
	(cellRect[],theCell,private.scrollListHandle) -> LRect;
	cellRect -> ((a,b),(c,d));
	((a,b),(c,d)) -> theRectangle;
#)

-- UIENVselectionClear: descriptor --
(#
do scanSelection
		(#
		do current -> selection.deselect;
		#);
#)

-- UIENVselectionscrollIntoView: descriptor --
(#
do	focusScrollList
		(#
		do private.scrollListHandle -> LAutoScroll;
		#);
#)

-- UIENVselectionFirst: descriptor --
(#	theCell: @Cell;
	isSelect,h,v: @integer;
do	(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
	(if isSelect//true then
		theCell.getPoint -> (h,v);
		v +1 -> value;
	else
		0 -> value;
	if);
#)

-- UIENVselectionLast: descriptor --
(#	theCell: @Cell;
	isSelect,h,v: @integer;
do	(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
	(if isSelect//true then
		loop:
			(if isSelect//true then
				(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
				theCell.getPoint -> (h,v);
				v  + 1 -> v;
				(h,v) -> theCell.setPoint;
				restart Loop;
			if);
		theCell.getPoint -> (h,v);
		v - 1 -> value;
	else 
		0 -> value;
	if);
#)

-- UIENVselectionSelectItem: descriptor --
(#	theCell: @Cell;
do	(if extend//false then clear if);
	(0,theItem - 1)  -> theCell.setPoint;
	focusScrollList
		(#
		do (true,theCell,private.scrollListHandle) -> LSetSelect;
		#);
#)

-- UIENVselectionDeselect: descriptor --
(#	theCell: @Cell;
do	focusScrollList
		(#
		do (0,theItem - 1) -> theCell.setPoint;
			(false,theCell,private.scrollListHandle) -> LSetSelect;
		#);
#)

-- UIENVselectionHas: descriptor --
(#	theCell: @Cell;
	isSelect: @integer;
do	(0,theItem - 1) -> theCell.setPoint;
	(false,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
	isSelect -> value;
#)

-- UIENVscrollListScanSelection: descriptor --
(#	isSelect,ignore,v: @integer;
	theCell: @Cell;
do	(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
	loop:
		(if isSelect//true then
			theCell.getPoint -> (ignore,current);
			current + 1 -> current;
			inner scanSelection;
			(0,current) -> theCell.setPoint;
			(true,theCell[],private.scrollListHandle) -> LGetSelect -> isSelect;
			restart Loop
		if);
#)

-- UIENVscrollListScan: descriptor --
(#
do	(for inx: numberOfItems repeat
		inx -> current;
		inner scan;
 	for);
#)

-- UIENVscrollListCreate: descriptor --
(#
do	inner create;
#)

-- UIENVscrollListOpen: descriptor --
(#	cellSize: @macPoint;
	bounds,dataBounds: ^macRect;
	theStyle: ^textStyle;
	theWindowPointer: @integer;
do	((0,0),(100,100)) -> windowItemFrame;
	calculateFocus;
	calculateBounds -> makeMacRect -> bounds[];
	calculateDataBounds -> makeMacRect -> dataBounds[];
	&textStyle[] -> theStyle[];
	'Chicago' -> theStyle.name;
	12 -> theStyle.size;
	theStyle[] -> scrollListStyle;
	(2000,0) -> cellSize.setPoint;
	windowPointer -> theWindowPointer;
	focusScrollList
		(#
		do (bounds[],dataBounds[],cellSize,0,theWindowPointer,true,false,false,true)
				-> LNew -> private.scrollListHandle;
		#);
	inner open;
#)

-- UIENVscrollListClose: descriptor --
(#
do inner close;
	private.scrollListHandle -> LDispose;
	0 -> private.scrollListHandle;
#)

-- UIENVScrollListonFrameChanged: descriptor --
(#	width,height: @integer;
do calculateSize -> (width,height);
	focusScrollList
		(#
		do (width,height,private.scrollListHandle) -> LSize;
		#);
	 inner onFrameChanged;
#)

-- UIENVScrollListonRefresh: descriptor --
(#
do focusScrollList
		(#	r: @rectangle;
			mr: ^macRect;
		do private.scrollListHandle -> ScrollListUpdate;
			PenNormal;
			((0,0),size) -> r;
			r[] -> makeMacRect -> mr[];
			mr[] -> FrameRect;
		#);
	inner onRefresh;
#)

-- UIENVScrollListonMouseDown: descriptor --
(#	event: ^eventRecord;
	ignore: @boolean;
do focusScrollList
		(#	macPt: @macPoint;
		do localPosition -> macPt.setPoint;
			currentEvent -> event[];
			(macPt,event.modifiers,private.scrollListHandle) -> LClick -> ignore;
		#);
	inner onMouseDown;
#)

-- UIENVScrollListonActivate: descriptor --
(#
do focusScrollList
		(#
		do (true,private.scrollListHandle) -> LActivate;
		#);
	inner onActivate;
#)

-- UIENVScrollListonDeactivate: descriptor --
(#
do focusScrollList
		(#
		do (false,private.scrollListHandle) -> LActivate;
		#);
	inner onDeactivate;
#)

-- scrollListLib: attributes --
scrollListStyle:
	(#
	enter private.scrollListStyle[]
	exit private.scrollListStyle[]
	#);
calculateSize:
	(# width,height: @integer
	do size -> (width,height);
		width - 17 -> width;
		height - 2 -> height;
	exit (width,height)
	#);
calculateBounds:
	(#	r: ^rectangle;
	do &rectangle[] -> r[];
		(1,1) -> r.topLeft;
		calculateSize -> r.size;
	exit r[]
	#);
calculateDataBounds:
	(#	r: ^rectangle;
	do &rectangle[] -> r[];
		((0,0),(1,0)) -> r;
	exit r[]
	#);
focusScrollList: focus
	(#
	do scrollListStyle -> useTextStyle;
		inner;
	#);
ScrollListGetNumberOfItems: external
	(# listHandle: @integer;
		value: @integer;
	enter listHandle
	exit value
	#);
ScrollListSetItemHeight: external
	(# listHandle: @integer;
		value: @integer;
	enter (listHandle,value)
	#);
ScrollListGetItemHeight: external
	(# listHandle: @integer;
		value: @integer;
	enter listHandle
	exit value
	#);
ScrollListSetSingle: external
	(# listHandle: @integer;
		value: @integer;
	enter (listHandle,value)
	#);
ScrollListGetSingle: external
	(# listHandle: @integer;
		value: @integer;
	enter listHandle
	exit value
	#);
ScrollListUpdate: external
	(# listHandle: @integer;
	enter listHandle
	#);
scrollListHandle:
	(#
	enter private.scrollListHandle
	exit private.scrollListHandle
	#);
-- UIENVscrollListPrivate: descriptor --
(# scrollListHandle: @integer;
	scrollListStyle: ^textStyle;
#)

-- UIENVtextScrollListSet: descriptor --
(#
do focusScrollList
		(#	theCell: @Cell;
			h: @integer;
		do	(0,theItem -1) -> theCell.setPoint;
			scrollListHandle -> h;
			(@@theText.T[1],theText.lgth,theCell,h) -> LSetCell;
		#);
#)

-- UIENVtextScrollListGet: descriptor --
(#	count: @shortRef;
	theCell: @cell;
	h: @integer;
do	&text[] -> theText[];
	500 -> theText.extend;
	500 -> count;
	(0,theItem -1) -> theCell.setPoint;
	scrollListHandle -> h;
	(@@theText.T[1],count[],theCell,h) -> LGetCell;
	count -> theText.lgth -> theText.pos;
#)

-- UIENVtextScrollListSetTextStyle: descriptor --
(#
do	theStyle[] -> scrollListStyle;
	updateMaybe;
#)

-- UIENVtextScrollListGetTextStyle: descriptor --
(#
do scrollListStyle -> theStyle[];
#)

-- UIENVtextScrollListCreate: descriptor --
(#
do inner create;
#)

-- UIENVtextScrollListOpen: descriptor --
(#
do inner open;
#)

-- UIENVtextScrollListClose: descriptor --
(#
do inner close;
#)

--]]
