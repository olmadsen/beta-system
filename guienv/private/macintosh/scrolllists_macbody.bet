ORIGIN '../scrolllistsbody';
INCLUDE 'guienv_macprivate';
INCLUDE '~beta/maclib/v3.1/listmanager';
INCLUDE '../../graphics';


-- windowLib: attributes --
saveUpdateRgn:
  (# updateRgn, updateRgnCopy: @integer;
     updateRgnOffset: (# exit 122 #);
  do private.windowPointer + updateRgnOffset -> TOS'%adrGetLong' -> updateRgn;
     NewRgn -> updateRgnCopy;
     (updateRgn, updateRgnCopy) -> CopyRgn;
     inner;
     (updateRgnCopy, updateRgn) -> CopyRgn;
     updateRgnCopy -> DisposeRgn;
  #);

-- GUIENVscrollListnumberOfItems: descriptor --
(#
do	assertOpen;
	THIS(scrollList).private.scrollListHandle -> ScrollListGetNumberOfItems -> value;
#)

-- GUIENVscrollListInsert: descriptor --
(# ignore: @integer;
do assertOpen;
	setupPortForListoperations
   (#
   do (numOfItems,beforeItem - 1,THIS(scrollList).private.scrollListHandle) -> LAddRow -> ignore;
   #);
#)

-- GUIENVscrollListPrepend: descriptor --
(#	ignore: @Integer;
do assertOpen;
	setupPortForListoperations
   (#
   do (numOfItems,0,THIS(scrollList).private.scrollListHandle) -> LAddRow -> ignore;
   #);
#)

-- GUIENVscrollListAppend: descriptor --
(#	ignore: @Integer;
do assertOpen;
	setupPortForListoperations
   (#
   do (numOfItems,-1,THIS(scrollList).private.scrollListHandle) -> LAddRow -> ignore;
   #);
#)

-- GUIENVscrollListDelete: descriptor --
(#
do assertOpen;
	setupPortForListoperations
   (#
   do (numOfItems,firstItem - 1,THIS(scrollList).private.scrollListHandle) -> LDelRow;
   #);
#)

-- GUIENVscrollListDeleteFirst: descriptor --
(#
do assertOpen;
	setupPortForListoperations
   (#
   do (numOfItems,0,THIS(scrollList).private.scrollListHandle) -> LDelRow;
   #);
#)

-- GUIENVscrollListDeleteLast: descriptor --
(#
do assertOpen;
	setupPortForListoperations
   (#
   do (numOfItems,numberOfItems - numOfItems,THIS(scrollList).private.scrollListHandle) -> LDelRow;
   #);
#)

-- GUIENVscrollListSetItemHeight: descriptor --
(#
do assertOpen;
	setupPortForListoperations
   (#
   do (THIS(scrollList).private.scrollListHandle,h) -> ScrollListSetItemHeight;
   #);
#)

-- GUIENVscrollListGetItemHeight: descriptor --
(#
do assertOpen;
	THIS(scrollList).private.scrollListHandle -> ScrollListGetItemHeight -> h;
#)


-- GUIENVItemGetItemRectangle: descriptor --
(#	cellRect: @macRect;
   theCell: @Cell;
   a,b,c,d: @Integer;
do	assertOpen;
	(0,theItem - 1) -> theCell.setPoint;
   (cellRect[],theCell,THIS(scrollList).private.scrollListHandle) -> LRect;
   cellRect -> ((a,b),(c,d));
   ((a,b),(c,d)) -> theRectangle;
#)

-- GUIENVselectionClear: descriptor --
(#
do assertOpen;
	scanSelection
   (#
   do current -> selection.deselect;
   #);
#)

-- GUIENVselectionscrollIntoView: descriptor --
(#
do	assertOpen;
	setupPortForListoperations
   (#
   do THIS(scrollList).private.scrollListHandle -> LAutoScroll;
   #);
#)

-- GUIENVselectionFirst: descriptor --
(#	theCell: @Cell;
   h,v: @integer;
   isSelect: @boolean;
do	assertOpen;
	(true,theCell[],THIS(scrollList).private.scrollListHandle) -> LGetSelect -> isSelect;
   (if isSelect then
       theCell.getPoint -> (h,v);
       v +1 -> value;
    else
       0 -> value;
   if);
#)

-- GUIENVselectionLast: descriptor --
(#	theCell: @Cell;
   h,v: @integer;
   isSelect: @boolean;
do	assertOpen;
	(true,theCell[],THIS(scrollList).private.scrollListHandle) -> LGetSelect -> isSelect;
   (if isSelect then
       loop:
         (if isSelect  then
             (true,theCell[],THIS(scrollList).private.scrollListHandle) -> LGetSelect -> isSelect;
             theCell.getPoint -> (h,v);
             v  + 1 -> v;
             (h,v) -> theCell.setPoint;
             restart Loop;
         if);
       theCell.getPoint -> (h,v);
       v - 1 -> value;
    else 
       0 -> value;
   if);
#)

-- GUIENVselectionSelectItem: descriptor --
(#	theCell: @Cell;
do	assertOpen;
	(if not extend then clear if);
   (0,theItem - 1)  -> theCell.setPoint;
   setupPortForListoperations
   (#
   do (true,theCell,THIS(scrollList).private.scrollListHandle) -> LSetSelect;
   #);
#)

-- GUIENVselectionDeselect: descriptor --
(#	theCell: @Cell;
do	assertOpen;
	setupPortForListoperations
   (#
   do (0,theItem - 1) -> theCell.setPoint;
      (false,theCell,THIS(scrollList).private.scrollListHandle) -> LSetSelect;
   #);
#)

-- GUIENVselectionHas: descriptor --
(#	theCell: @Cell;
   isSelect: @boolean;
do	assertOpen;
	(0,theItem - 1) -> theCell.setPoint;
   (false,theCell[],THIS(scrollList).private.scrollListHandle) -> LGetSelect -> isSelect;
   isSelect -> value;
#)

-- GUIENVscrollListScanSelection: descriptor --
(#	ignore,v: @integer;
   isSelect: @boolean;
   theCell: @Cell;
do	assertOpen;
	(true,theCell[],THIS(scrollList).private.scrollListHandle) -> LGetSelect -> isSelect;
   loop:
     (if isSelect then
         theCell.getPoint -> (ignore,current);
         current + 1 -> current;
         inner scanSelection;
         (0,current) -> theCell.setPoint;
         (true,theCell[],THIS(scrollList).private.scrollListHandle) -> LGetSelect -> isSelect;
         restart Loop
     if);
#)

-- GUIENVscrollListScan: descriptor --
(#
do	assertOpen;
	(for inx: numberOfItems repeat
             inx -> current;
             inner scan;
 	for);
#)

-- GUIENVscrollListCreate: descriptor --
(#
do	
	whiteBackGround -> backGroundStyle;
   inner create;
#)

-- GUIENVscrollListOpen: descriptor --
(#	cellSize: @macPoint;
   bounds,dataBounds: ^macRect;
   theStyle: ^textStyle;
   theWindowPointer: @integer;
do	((0,0),(100,100)) -> privateWindowItemFrame;
   calculateBounds -> makeMacRect -> bounds[];
   calculateDataBounds -> makeMacRect -> dataBounds[];
   &textStyle[] -> theStyle[];
   'Geneva' -> theStyle.name;
   10 -> theStyle.size;
   textFaces.bold -> theStyle.face;
   
   theStyle[] -> scrollListStyle;
   (2000,0) -> cellSize.setPoint;
	THIS(guienv).private.white -> this(windowItem).private.background;
   windowPointer -> theWindowPointer;
   setupPortForListoperations
   (#
   do (bounds[],dataBounds[],cellSize,0,theWindowPointer,true,false,false,true)
        -> LNew -> THIS(scrollList).private.scrollListHandle;
		 (if THIS(scrollList).private.scrollListHandle = 0 then
		    memoryFailure;
		 if);
   #);
	(if multipleSelection then
		(THIS(scrollList).private.scrollListHandle,False) -> ScrollListSetSingle;
	else
		(THIS(scrollList).private.scrollListHandle,True) -> ScrollListSetSingle;
	if);
   (if this(window).style = plain then
       borderStyles.simple -> border.style;
    else
       borderStyles.shadowIn -> border.style;
   if);
   true -> border.visible;
   inner open;
#)

-- GUIENVscrollListClose: descriptor --
(# LDispose: External
     (#	lHandle: @Integer;
     enter lHandle
     do '{$3F3C,$0028,$A9E7}' -> PascalTrap;
     #);
do inner close;
   THIS(scrollList).private.scrollListHandle -> LDispose;
   0 -> THIS(scrollList).private.scrollListHandle;
	NONE -> private.scrollListStyle[];
#)

-- GUIENVScrollListonFrameChanged: descriptor --
(#	width,height: @integer;
do calculateSize -> (width,height);
   setupPortForListoperations
   (#
   do saveUpdateRgn
      (#
      do (width,height,THIS(scrollList).private.scrollListHandle) -> LSize;
      #);
   #);
   inner onFrameChanged;
#)

-- GUIENVScrollListonRefresh: descriptor --
(#
do setupPortForListoperations
   (#	r: @rectangle;
      mr: ^macRect;
   do THIS(scrollList).private.scrollListHandle -> ScrollListUpdate;
   #);
   standardGraphics
     (#
     do  (((0, 0), size), borderStyles.shadowIn) -> drawBorder;
	 #);
   inner onRefresh;
#)

-- GUIENVScrollListonMouseDown: descriptor --
(#	
   ignore: @boolean;
	
	bounds: ^rectangle;
	pt: @point;
	doit: setupPortForListoperations
		(#	macPt: @macPoint;
		do pt -> macPt.setPoint;
			(macPt,this(guienv).private.event.modifiers,THIS(scrollList).private.scrollListHandle) -> LClick -> ignore;
		#);
	firstItem: @integer;
do (if defaultBehaviour then
		calculateBounds -> bounds[];
		localposition -> pt;
		(if pt -> bounds.containsPoint then
			inner onMouseDown;
			doit;
			false -> sendOnMouseup;
			theEventHandler.onMouseUp;
			(if multipleSelection then
				theEventHandler.onSelect;
			else
				(selection.first, ignore) -> theEventHandler.onSelect;
			if);
		else
			doit;
			false -> sendOnMouseup;
			NONE -> lastClickedObject;
		if);
	else
		INNER onMouseDown;
	if);
#)

-- GUIENVScrollListonActivate: descriptor --
(#
do setupPortForListoperations
   (#
   do (true,THIS(scrollList).private.scrollListHandle) -> LActivate;
   #);
   inner onActivate;
#)

-- GUIENVScrollListonDeactivate: descriptor --
(#
do setupPortForListoperations
   (#
   do (false,THIS(scrollList).private.scrollListHandle) -> LActivate;
   #);
   inner onDeactivate;
#)

-- GUIENVScrollListOnVisibleChanged: doPart --
do (if viewable then
		(true, THIS(scrollList).private.scrollListHandle) -> LSetDrawingMode;
	else
		(false, THIS(scrollList).private.scrollListHandle) -> LSetDrawingMode;
	if);
	INNER;


-- scrollListLib: attributes --
scrollListStyle:
  (#
  enter private.scrollListStyle[]
  exit private.scrollListStyle[]
  #);
calculateSize:
  (# width,height: @integer
  do size -> (width,height);
     width - 19 -> width;
     height - 4 -> height;
  exit (width,height)
  #);
calculateBounds:
  (#	r: ^rectangle;
  do &rectangle[] -> r[];
     (2,2) -> r.topLeft;
     calculateSize -> r.size;
  exit r[]
  #);
calculateDataBounds:
  (#	r: ^rectangle;
  do &rectangle[] -> r[];
     ((0,0),(1,0)) -> r;
  exit r[]
  #);

setupPortForListoperations: graphics
  (#
  do scrollListStyle -> useTextStyle;
     INNER;
  #);

ScrollListGetNumberOfItems: external
  (# listHandle: @integer;
     value: @integer;
  enter listHandle
  exit value
  #);
ScrollListSetItemHeight: external
  (# listHandle: @integer;
     value: @integer;
  enter (listHandle,value)
  #);
ScrollListGetItemHeight: external
  (# listHandle: @integer;
     value: @integer;
  enter listHandle
  exit value
  #);
ScrollListSetSingle: external
  (# listHandle: @integer;
     value: @boolean;
  enter (listHandle,value)
  #);
ScrollListGetSingle: external
  (# listHandle: @integer;
     value: @boolean;
  enter listHandle
  exit value
  #);
ScrollListUpdate: external
  (# listHandle: @integer;
  enter listHandle
  #);
scrollListHandle:
  (#
  enter THIS(scrollList).private.scrollListHandle
  exit THIS(scrollList).private.scrollListHandle
  #);
-- GUIENVscrollListPrivate: descriptor --
(# scrollListHandle: @integer;
   scrollListStyle: ^textStyle;
#)

-- GUIENVtextScrollListSet: descriptor --
(#
do assertOpen;
	setupPortForListoperations
   (#	theCell: @Cell;
      h: @integer;
   do	(0,theItem -1) -> theCell.setPoint;
      scrollListHandle -> h;
		(theCell, theText[], h) -> SetCell;
   #);
#)

-- GUIENVtextScrollListGet: descriptor --
(#	
   theCell: @cell;
	h: @integer;
do	assertOpen;
   (0,theItem -1) -> theCell.setPoint;
	scrollListHandle -> h;
   (theCell, h) -> GetCell -> theText[];
#)

-- GUIENVtextScrollListSetTextStyle: descriptor --
(#
do	assertOpen;
	theStyle[] -> scrollListStyle;
   updateMaybe;
#)

-- GUIENVtextScrollListGetTextStyle: descriptor --
(#
do assertOpen;
	scrollListStyle -> theStyle[];
#)

-- GUIENVtextScrollListCreate: descriptor --
(#
do inner create;
#)

-- GUIENVtextScrollListOpen: descriptor --
(#
do inner open;
#)

-- GUIENVtextScrollListClose: descriptor --
(#
do inner close;
#)
