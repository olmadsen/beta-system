ORIGIN '../fieldsbody'
[[
-- Include 'uienv_macprivate'
-- Include 'texteditstuff'
-- Include '~beta/maclib/v2.7.1/scrap'
-- Include 'quickdrawstuff'
-- Include 'windowsstuff'

-- lib: attributes --
minMax:
	(#	minimum,value,maximum: @integer;
	enter (minimum,value,maximum)
	do ((minimum,value) -> Max,maximum) -> Min -> value;
	exit value
	#);
-- UIENVmovieFieldSetContents: descriptor --
(#
do
#)

-- UIENVmovieFieldGetContents: descriptor --
(#
do
#)

-- UIENVmovieFieldSetScaleToFit: descriptor --
(#
do
#)

-- UIENVmovieFieldGetScaleToFit: descriptor --
(#
do
#)

-- UIENVmovieFieldCreate: descriptor --
(#
do
#)

-- UIENVmovieFieldOpen: descriptor --
(#
do
#)

-- UIENVmovieFieldClose: descriptor --
(#
do
#)

-- UIENVmovieFieldPrivate: descriptor --
(#
do
#)

-- UIENVtextFieldonFrameChanged: descriptor --
(# TextEditUpdateViewRect: external
		(# teHandle: @integer;
			view: ^macRect;
		enter (teHandle,view[])
		#);
	view: ^macRect;
do calcViewRect -> makeMacRect -> view[];
	(textFieldHandle,view[]) -> TextEditUpdateViewRect;
	adjustScrollvaluesMaybe;
	inner onFrameChanged;
#)

-- UIENVtextFieldonKeyDown: descriptor --
(#	start,end,length: @integer;
	peformEdit: @boolean;
do selection -> (start,end);
	end - start -> length;
	true -> peformEdit;
	(if length<>0 then
		(start,-length) -> theEventHandler.onBeforeChange -> peformEdit;
	if);
	(if peformEdit then
		(if ch<>8 (* backspace *) then
			(start,1) -> theEventHandler.onBeforeChange -> peformEdit;
		if);
		(if peformEdit then
			focus
				(# 
				do  (ch,textFieldHandle) -> TEKey
				#);
			handleTextChanged;
		if);
	if);
	inner onKeyDown;
#)

-- UIENVtextFieldonMouseDown: descriptor --
(#
do this(textField)[] -> target;
	focus
		(# macPt: @macPoint;
		do localPosition -> macPt.setPoint;
			(macPt,false,textfieldHandle) -> TEClick;
			adjustScrollValuesMaybe;
		#);
	inner onMouseDown;
#)

-- UIENVtextFieldonMouseUp: descriptor --
(#
do inner onMouseUp;
#)

-- UIENVtextFieldonRefresh: descriptor --
(# view: ^macRect;
	r: @rectangle;
do ((0,0),size) -> r;
	r[] -> makeMacRect -> view[];
	focus
		(#	
		do (view[],textFieldHandle) -> TEUpdate;
			PenNormal;
			view[] -> FrameRect;
		#);
	inner onRefresh;
#)

-- UIENVtextFieldOnEnableTarget: descriptor --
(#
do focus
		(#
		do textFieldHandle -> TEActivate;
		#);
	inner onEnableTarget;
#)

-- UIENVtextFieldOnDisableTarget: descriptor --
(#
do focus
		(#
		do textFieldHandle -> TEDeactivate;
		#);
	inner onDisableTarget;
#)

-- textFieldonIdle: descriptor --
(#
do focus
		(#
		do textFieldHandle -> TEIdle;
		#);
	inner onIdle;
#)

-- UIENVtextFieldDoPaste: descriptor --
(# offset: @longInt;
	error: @integer;
	TextType: (# exit 1413830740 #);
	clipboardLength: @integer;
	start,end,length: @integer;
	performEdit: @boolean;
do getClipboardLength -> clipboardLength;
	(if clipboardLength + length < 32000 then
		(if clipboardLength<>0 then
			selection -> (start,end);
			end - start -> length;
			true -> performEdit;
			(if length<>0 then
				(start,-length) -> theEventHandler.onBeforeChange -> performEdit;
			if);
			(if performEdit then
				(start,clipboardLength) -> theEventHandler.onBeforeChange -> performEdit;
				(if performEdit then
					TEFromScrap -> error;
					focus
						(#
						do textFieldHandle -> TEstylePaste;
						#);
					handleTextChanged;
				if);
			if);
		if);
	if);
#)

-- UIENVtextFieldDoCopy: descriptor --
(# error: @integer;
do ZeroScrap -> error;
	(if error//0 then
		textFieldHandle -> TECopy;
		TEToScrap -> error;
	if);
#)

-- UIENVtextFieldDoCut: descriptor --
(# error: @integer;
	start,end,length: @integer;
	performEdit: @boolean;
do ZeroScrap -> error;
	(if error//0 then
		selection -> (start,end);
		end - start -> length;
		(if length<>0 then
			(start,-length) -> theEventHandler.onBeforeChange -> performEdit;
			(if performEdit then
				focus
					(#
					do textFieldHandle -> TECut;
					#);
				TEToScrap -> error;
				handleTextChanged;
			if);
		if);
	if);
#)

-- UIENVtextFieldDoClear: descriptor --
(# start,end,length: @integer;
	performEdit: @boolean;
do selection -> (start,end);
	end - start -> length;
	(if length<>0 then
		(start,-length) -> theEventHandler.onBeforeChange -> performEdit;
		(if performEdit then
			focus
				(#
				do textFieldHandle -> TEDelete;
				#);
			handleTextChanged;
		if);
	if);
#)

-- UIENVtextFieldSetContents: descriptor --
(#
do textFieldDefaultStyle -> useTextStyle;
	(@@theText.T[1],theText.lgth,textFieldHandle) -> TESetText;
	(if theText.styleInfo<>0 then
		(0,theText.lgth,theText.styleInfo,textFieldHandle,false)
			-> TEUseStyleScrap;
	if);
	(textFieldHandle,0,0) -> TextEditSetSelection;
	adjustScrollValuesMaybe;
	updateMaybe;
#)

-- UIENVtextFieldGetContents: descriptor --
(#	len: @integer;
	textHandle: @integer;
	address: @integer;
	start,end: @integer;
do	&styledText[] -> theText[];
	length -> len;
	len - theText.T.range -> theText.extend;
	textFieldHandle -> TEGetText -> textHandle;
	textHandle -> TOS'%adrGetLong' -> address;
	(address,@@theText.T[1],len) -> BlockMove;
	len -> theText.lgth -> theText.pos;
	(0,len) -> withSelection
		(#
		do textFieldHandle -> TEGetStyleScrapHandle -> theText.styleInfo;
		#);
#)

-- UIENVtextFieldGetChar: descriptor --
(#
do (textFieldHandle,pos) -> TextEditGetChar -> ch;
#)

-- UIENVtextFieldLength: descriptor --
(# TextEditGetLength: external
		(# teHandle: @integer;
			length: @integer;
		enter teHandle
		exit length
		#);
do textFieldHandle -> TextEditGetLength -> value;
#)

-- UIENVtextFieldScanText: descriptor --
(#	teHandle: @integer;
	current,increment: @integer;
	
do	(0,start,length) -> minMax -> start;
	(0,end,length) -> minMax -> end;
	(if start<>end then
		textFieldHandle -> teHandle;
		start -> current;
		(if start < end then
			1 -> increment;
		else
			-1 -> increment;
		if);
		scan:
			(if current<>end then
				(teHandle,current) -> TextEditGetChar -> ch;
				inner scanText;
				current + increment -> current;
				restart scan;
			if);
	if);
#)

-- UIENVtextFieldPosToPt: descriptor --
(# macPt: @macPoint;
do (pos,textFieldHandle) -> TEGetPoint -> macPt;
	macPt.getPoint -> pt;
#)

-- UIENVtextFieldPtToPos: descriptor --
(# macPt: @macPoint;
do	pt -> macPt.setPoint;
	(macPt,textFieldHandle) -> TEGetOffset -> pos;
#)

-- UIENVtextFieldSelectionStart: descriptor --
(#	TextEditGetSelectionStart: external
		(# teHandle: @integer;
			start: @integer;
		enter teHandle
		exit start
		#);
do textFieldHandle -> TextEditGetSelectionStart -> value;
#)

-- UIENVtextFieldSelectionEnd: descriptor --
(# TextEditGetSelectionEnd: external
		(# teHandle: @integer;
			end: @integer;
		enter teHandle
		exit end
		#);
do textFieldHandle -> TextEditGetSelectionEnd -> value;
#)

-- UIENVtextFieldSelectionContents: descriptor --
(# 
do &text[] -> theText[];
	(start,end) -> scanText
		(#
		do ch -> theText.put;
		#);
#)

-- UIENVtextFieldSelectionScrollIntoView: descriptor --
(#
do focus
		(#
		do textFieldHandle -> TESelView;
		#);
	adjustScrollvaluesMaybe;
#)

-- UIENVtextFieldSelectionSet: descriptor --
(#
do focus
		(#
		do (theStart,theEnd,textFieldHandle) -> TESetSelect;
		#);
#)

-- UIENVtextFieldSetDefaultStyle: descriptor --
(#
do style[] -> textFieldDefaultStyle;
#)

-- UIENVtextFieldGetDefaultStyle: descriptor --
(#
do textFieldDefaultStyle -> style[];
#)

-- UIENVtextFieldIsOneStyle: descriptor --
(#
do (* Not implemented !!! *)
#)

-- UIENVtextFieldSetOneSize: descriptor --
(#	macStyle: @textStyleRec;
do	theSize -> macStyle.tsSize;
	(macStyle[],doSize,start,end) -> setStyleElement;
#)

-- UIENVtextFieldSetOneFont: descriptor --
(#	macStyle: @textStyleRec;
	style: @textStyle;
do	theFont[] -> style.name;
	style.familyID -> macStyle.tsFont;
	(macStyle[],doFont,start,end) -> setStyleElement;
#)

-- UIENVtextFieldSetOneFace: descriptor --
(#	macStyle: @textStyleRecord;
do	theFace -> macStyle.face;
	(macStyle[],doFace,start,end) -> setStyleElement;
#)

-- UIENVtextFieldSetOneStyle: descriptor --
(#	macStyle: @textStyleRec;
do	theStyle.familyID -> macStyle.tsFont;
	theStyle.size -> macStyle.tsSize;
	theStyle.face -> macStyle.tsFace;
	(macStyle[],doSize + doFace + doFont,start,end) -> setStyleElement;
#)

-- UIENVtextFieldScanTextWithStyle: descriptor --
(#
do (* Not implemented !!! *)
#)

-- UIENVtextFieldSetMargin: descriptor --
(#
do	(leftMargin,topMargin) -> textFieldMargin;
#)

-- UIENVtextFieldGetMargin: descriptor --
(#
do textFieldMargin ->  (leftMargin,topMargin);
#)

-- UIENVtextFieldInsert: descriptor --
(#
do focus
		(#
		do (@@theText.T[1],theText.lgth,textFieldHandle) -> TEInsert
		#);
#)

-- UIENVtextFieldDelete: descriptor --
(#
do focus
		(#
		do textFieldHandle -> TEDelete;
		#);
#)

-- UIENVtextFieldCreate: descriptor --
(#
do inner create;
#)

-- UIENVtextFieldOpen: descriptor --
(# style: ^textStyle;
do &textStyle[] -> style[];
	'Palatino' -> style.name;
	12 -> style.size;
	style[] -> textFieldDefaultStyle;
	textFieldDefaultStyle -> useTextStyle;
	(4,4) -> textFieldMargin;
	((0,0),(100,100)) -> windowItemFrame;
	calculateFocus;
	focus
		(# view,dest: ^macRect;
		do calcViewRect -> makeMacRect -> view[];
			calcInitialDestRect -> makeMacRect -> dest[];
			(dest[],view[]) -> TEStyleNew -> textFieldHandle;
			(true,textFieldHandle) -> TEAutoView;
		#);
	false -> updateOnResize;
	cursors.iBeam[] -> theCursor;
	inner open;
#)

-- UIENVtextFieldClose: descriptor --
(#
do inner close;
	textFieldHandle -> TEDispose;
#)

-- textFieldLib: attributes --
handleTextChanged:
	(# 
	do adjustScrollvaluesMaybe;
		theEventHandler.onTextChanged;
	#);
adjustScrollvaluesMaybe:
	(# theTextEditor: ^textEditor;
	do textFieldScroller -> theTextEditor[];
		(if theTextEditor[]<>none then
			theTextEditor.adjustScrollValues;
		if);
	#);
getClipboardLength: integerValue
	(# offset: @longInt;
		TextType: (# exit 1413830740 #);
	do (0,TextType,offset[]) -> GetScrap -> value;
	#);
calcViewRect:
	(#	r: ^rectangle;
		left,top: @integer;
		width,height: @integer;
		t: @text;
	do &rectangle[] -> r[];
		textFieldMargin -> (left,top);
		(left+1,top+1) -> r.topLeft;
		size -> (width,height);
		width - 1 -> r.right;
		height - 1 -> r.bottom;
	exit r[]
	#);
calcInitialDestRect:
	(#	r: ^rectangle;
		left,top: @integer;
		width,height: @integer;
	do &rectangle[] -> r[];
		textFieldMargin -> (left,top);
		(left+1,top+1) -> r.topLeft;
		size -> (width,height);
		1000 -> r.right;
		height - top - 1 -> r.bottom;
	exit r[]
	#);
textFieldHandle:
	(#
	enter private.textFieldHandle
	exit private.textFieldHandle
	#);
textFieldDefaultStyle:
	(#
	enter private.textFieldDefaultStyle[]
	exit private.textFieldDefaultStyle[]
	#);
textFieldMargin:
	(#
	enter private.textFieldMargin
	exit private.textFieldMargin
	#);
textFieldScroller:
	(#
	enter private.textFieldScroller[]
	exit private.textFieldScroller[]
	#);
TextEditSetSelection: external
	(# teHandle: @integer;
		start,end: @integer;
	enter (teHandle,start,end)
	#);
TextEditGetSelection: external
	(# teHandle: @integer;
		startP,endP: @integer;
	enter (teHandle,startP,endP)
	#);
TextEditGetChar: external
	(#	teHandle: @integer;
		inx: @integer;
		ch: @integer;
	enter (teHandle,inx)
	exit ch
	#);
withSelection:
	(# start,end: @integer;
		curStart,curEnd: @integer;
	enter (start,end)
	do (textFieldHandle,@@curStart,@@curEnd) -> TextEditGetSelection;
		(textFieldHandle,start,end) -> TextEditSetSelection;
		inner;
		(textFieldHandle,curStart,curEnd) -> TextEditSetSelection;
	#);
setStyleElement:
	(#	macStyle: ^textStyleRec;
		curStart,curEnd,start,end: @integer;
		currentSelection: @boolean;
		element: @integer;
	enter (macStyle[],element,start,end)
	do	selection -> (curStart,curEnd);
		(curStart = start) and (curEnd = end) -> currentSelection;
		(if currentSelection then
			focus
				(#
				do (element,macStyle[],true,textFieldHandle) -> TESetStyle;
				#);
		else
			(start,end) -> withSelection
				(#
				do (element,macStyle[],false,textFieldHandle) -> TESetStyle;
				#);
			updateMaybe;
		if);
		adjustScrollvaluesMaybe;
	#);
-- UIENVtextFieldPrivate: descriptor --
(# textFieldHandle: @integer;
	textFieldDefaultStyle: ^textStyle;
	textFieldMargin: @point;
	textFieldScroller: ^textEditor;
#)

-- UIENVabstractScrollerScroll: descriptor --
(#
do inner scroll;
#)

-- UIENVabstractScrollerCreate: descriptor --
(#
do inner create;
#)

-- UIENVabstractScrollerOpen: descriptor --
(#
do ((0,0),(200,200)) -> windowItemFrame;
	calculateFocus;
	&scrollerVerticalScrollbar[] -> verticalScrollbar[];
	this(abstractScroller)[] -> verticalScrollbar.open;
	&scrollerHorizontalScrollbar[] -> horizontalScrollbar[];
	this(abstractScroller)[] -> horizontalScrollbar.open;
	inner open;
#)

-- UIENVabstractScrollerClose: descriptor --
(#
do inner close;
	verticalScrollbar.close;
	horizontalScrollbar.close;
#)

-- abstractScrollerLib: attributes --
scrollerVerticalScrollbar: scrollbar
	(#	open::<
			(# width,height: @integer;
			do this(abstractScroller).size -> (width,height);
				((width - 16,0),(width,height - 15)) -> frame;
				true -> bindTop;
				true -> bindRight;
				false -> bindLeft;
				true -> bindBottom;
			#);
		eventHandler::<
			(#	onButtonDown::<
					(#
					do (0,scrollAmount) -> scroll;
						focusZero;
					#);
				onButtonUp::<
					(#
					do (0,-scrollAmount) -> scroll;
						focusZero;
					#);
				onPageUp::<
					(#
					do (0,-pageScrollAmount) -> scroll;
						focusZero;
					#);
				onPageDown::<
					(#
					do (0,pageScrollAmount) -> scroll;
						focusZero;
					#);
				onThumbMoved::<
					(#
					do (0,amount) -> scroll;
						focusZero;
					#);
			#);
	#);
scrollerHorizontalScrollbar: scrollbar
	(#	open::<
			(# width,height: @integer;
			do this(abstractScroller).size -> (width,height);
				((0,height - 16),(width - 15,height)) -> frame;
				true -> bindLeft;
				false -> bindTop;
				true -> bindRight;
				true -> bindBottom;
			#);
		eventHandler::<
			(#	onButtonDown::<
					(#
					do (scrollAmount,0) -> scroll;
						focusZero;
					#);
				onButtonUp::<
					(#
					do (-scrollAmount,0) -> scroll;
						focusZero;
					#);
				onPageUp::<
					(#
					do (-pageScrollAmount,0) -> scroll;
						focusZero;
					#);
				onPageDown::<
					(#
					do (pageScrollAmount,0) -> scroll;
						focusZero;
					#);
				onThumbMoved::<
					(# 
					do (amount,0) -> scroll;
						focusZero;
					#);
			#);
	#);
-- UIENVabstractScrollerPrivate: descriptor --
(#
#)

-- textEditorLib: attributes --

adjustScrollValues:
	(#	TextEditGetScrollvalues: external
			(#	editTextHandle: @integer;
				hMaxP,hP,vMaxP,vP: @integer;
			enter (editTextHandle,hMaxP,hP,vMaxP,vP)
			#);
		TextEditAdjustScroll: external
			(#	editTextHandle: @integer;
				h,v: @integer;
			enter (editTextHandle,h,v)
			#);
		hMax,h,vMax,v: @integer;
		t: @text;
		width,height: @integer;
	do (contents.textFieldHandle,@@hMax,@@h,@@vMax,@@v) -> TextEditGetScrollvalues;
		hMax -> horizontalScrollbar.maxValue;
		h -> horizontalScrollbar.value;
		vMax -> verticalScrollbar.maxValue;
		v -> verticalScrollbar.value;
		contents.focus
			(#
			do (contents.textFieldHandle,h,v) -> TextEditAdjustScroll;
			#);
		contents.size -> (width,height);
		width -> horizontalScrollbar.pageScrollAmount;
		height -> verticalScrollbar.pageScrollAmount;
	#);
-- UIENVtextEditorScroll: descriptor --
(#
do contents.focus
		(#
		do (-dh,-dv,contents.textFieldHandle) -> TEPinScroll;
		#);
	inner scroll;
#)

-- UIENVtextEditorCreate: descriptor --
(#
do inner create;
#)

-- UIENVtextEditorOpen: descriptor --
(# width,height: @integer;
do	this(textEditor)[] -> contents.textFieldScroller;
	this(textEditor)[] -> contents.open;
	size -> (width,height);
	((0,0),(width - 15,height - 15)) -> contents.frame;
	true -> contents.bindLeft;
	true -> contents.bindTop;
	true -> contents.bindRight;
	true -> contents.bindBottom;
	inner open;
#)

-- UIENVtextEditorClose: descriptor --
(#
do inner close;
#)

-- UIENVscrollerScroll: descriptor --
(#	newH,newV: @integer;
	h,v: @integer;
	width,height,viewWidth,viewHeight: @integer;
	t: @text;
	updateRgn: @integer;
	saved: @boolean;
do	contents.position -> (h,v);
	contents.size -> (width,height);
	private.view.size -> (viewWidth,viewHeight);
	(viewWidth - width,h - dh,0) -> MinMax -> newH;
	(viewHeight - height,v - dv,0) -> MinMax -> newV;
	contents.focus
		(#	mr: ^macRect;
			r: @rectangle;
		do ((0,0),contents.size) -> r;
			r[] -> makeMacRect -> mr[];
			newRgn -> updateRgn;
			(mr[],newH - h,newV - v,updateRgn) -> ScrollRect;
			updateRgn -> InvalRgn;
			updateRgn -> DisposeRgn;
		#);
	contents.automaticUpdate -> saved;
	false -> contents.automaticUpdate;
	(newH,newV) -> contents.position;
	saved -> contents.automaticUpdate;
	this(window).update;
	inner scroll;
#)

-- UIENVscrollerCreate: descriptor --
(#
do	inner create;
#)

-- UIENVscrollerOpen: descriptor --
(# width,height: @integer;
do	this(scroller)[] -> private.view.open;
	private.view[] -> contents.open;
	size -> (width,height);
	((1,1),(width - 16,height - 16)) -> private.view.frame;
	true -> private.view.bindLeft;
	true -> private.view.bindTop;
	true -> private.view.bindRight;
	true -> private.view.bindBottom;
	((0,0),(1000,1000)) -> contents.frame;
	adjustScrollValues;
	
	inner open;
#)

-- UIENVscrollerClose: descriptor --
(#
do inner close;
#)

-- scrollerLib: attributes --
adjustScrollValues:
	(#	width,height,viewWidth,viewHeight: @integer;
		h,v: @integer;
	do contents.size -> (width,height);
		private.view.size -> (viewWidth,viewHeight);
		width - viewWidth -> horizontalScrollbar.maxValue;
		height - viewHeight -> verticalScrollbar.maxValue;
		
		contents.position -> (h,v);
		(width - viewWidth,-h,0) -> MinMax -> h;
		(height - viewHeight,-v,0) -> MinMax -> v;
		h -> horizontalScrollbar.value;
		v -> verticalScrollbar.value;
		
		(h,v) -> setScroll;
		
		viewWidth -> horizontalScrollbar.pageScrollAmount;
		viewHeight -> verticalScrollbar.pageScrollAmount;
	#);
setScroll:
	(#	h,v: @integer;
		currentH,currentV: @integer;
	enter (h,v)
	do contents.position -> (currentH,currentV);
		(if (h<>currentH) and (v<>currentV) then
			(currentH - h,currentV - v) -> scroll;
		if);
	#);
-- UIENVscrollerPrivate: descriptor --
(# view: @canvas;
#)

--]]
