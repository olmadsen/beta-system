ORIGIN '../fieldsbody';

INCLUDE 'guienv_macprivate';
INCLUDE '~beta/maclib/v3.0/textedit';
INCLUDE '~beta/maclib/v3.0/scrap';
INCLUDE '~beta/maclib/v3.0/quickdraw';
INCLUDE '~beta/maclib/v3.0/windows';

-- lib: attributes --
between:
	(#	minimum,value,maximum: @integer;
	enter (minimum,value,maximum)
	do ((minimum,value) -> Max, maximum) -> Min -> value;
	exit value
	#);
-- GUIENVmovieFieldSetContents: descriptor --
(#
do
#)

-- GUIENVmovieFieldGetContents: descriptor --
(#
do
#)

-- GUIENVmovieFieldSetScaleToFit: descriptor --
(#
do
#)

-- GUIENVmovieFieldGetScaleToFit: descriptor --
(#
do
#)

-- GUIENVmovieFieldCreate: descriptor --
(#
do
#)

-- GUIENVmovieFieldOpen: descriptor --
(#
do
#)

-- GUIENVmovieFieldClose: descriptor --
(#
do
#)

-- GUIENVmovieFieldPrivate: descriptor --
(#
do
#)

-- GUIENVtextFieldonFrameChanged: descriptor --
(# view: ^macRect;
do calcViewRect -> makeMacRect -> view[];
	(textFieldHandle,view[]) -> TextEditUpdateViewRect;
	adjustScrollvaluesMaybe;
	inner onFrameChanged;
#)

-- GUIENVtextFieldonKeyDown: descriptor --
(#	start,end,length: @integer;
	peformEdit: @boolean;
do selection -> (start,end);
	end - start -> length;
	true -> peformEdit;
	(if length<>0 then
		(start,-length) -> theEventHandler.onBeforeChange -> peformEdit;
	if);
	(if peformEdit then
		(if ch<>8 (* backspace *) then
			this(guienv).private.globalTheText.clear;
			ch -> this(guienv).private.globalTheText.put;
			(start,1) -> theEventHandler.onBeforeChange -> peformEdit;
		if);
		(if peformEdit then
				focusTextField
					(# 
					do  (ch,textFieldHandle) -> TEKey
					#);
			handleTextChanged;
		if);
	if);
	inner onKeyDown;
#)

-- GUIENVtextFieldonMouseDown: descriptor --
(#
do inner onMouseDown;
	this(textField)[] -> target;
	focusTextField
		(# macPt: @macPoint;
		do localPosition -> macPt.setPoint;
			(macPt,false,textfieldHandle) -> TEClick;
			adjustScrollValuesMaybe;
			onMouseUp;
		#);
#)

-- GUIENVtextFieldonMouseUp: descriptor --
(#
do inner onMouseUp;
#)

-- GUIENVtextFieldonRefresh: descriptor --
(# view: ^macRect;
	r: @rectangle;
do ((0,0),size) -> r;
	r[] -> makeMacRect -> view[];
	focusTextField
		(# 
		do (* view[] -> EraseRect; *)
		   (view[],textFieldHandle) -> TEUpdate;
		#);
	(if private.textFieldScroller[] = NONE then
		(((0, 0), size), borderStyles.shadowIn) -> drawBorder;
	if);
	inner onRefresh;
#)

-- GUIENVtextFieldOnEnableTarget: descriptor --
(#
do focusTextField
		(#
		do textFieldHandle -> TEActivate;
		#);
	private.installTimer;
	inner onEnableTarget;
#)

-- GUIENVtextFieldOnDisableTarget: descriptor --
(#
do focusTextField
		(#
		do textFieldHandle -> TEDeactivate;
		#);
	private.deleteTimer;
	inner onDisableTarget;
#)

-- GUIENVtextFieldonIdle: descriptor --
(#
do focusTextField
		(#
		do textFieldHandle -> TEIdle;
		#);
	inner onIdle;
#)

-- GUIENVtextFieldDoPaste: descriptor --
(# offset: @longInt;
	error: @integer;
	TextType: (# exit 1413830740 #);
	clipboardLength: @integer;
	start,end,length: @integer;
	performEdit: @boolean;
do assertOpen;
	getClipboardLength -> clipboardLength;
	(if (clipboardLength + length < 32000) then
		(if clipboardLength<>0 then
			selection -> (start,end);
			end - start -> length;
			true -> performEdit;
			(if length<>0 then
				(start,-length) -> theEventHandler.onBeforeChange -> performEdit;
			if);
			(if performEdit then
				(start,clipboardLength) -> theEventHandler.onBeforeChange -> performEdit;
				(if performEdit then
					TEFromScrap -> error;
					focusTextField
						(#
						do textFieldHandle -> TEstylePaste;
						#);
					handleTextChanged;
				if);
			if);
		if);
	if);
#)

-- GUIENVtextFieldDoCopy: descriptor --
(# error: @integer;
do assertOpen;
	ZeroScrap -> error;
	(if error//0 then
		textFieldHandle -> TECopy;
		TEToScrap -> error;
	if);
#)

-- GUIENVtextFieldDoCut: descriptor --
(# error: @integer;
	start,end,length: @integer;
	performEdit: @boolean;
do assertOpen;
	ZeroScrap -> error;
	(if error//0 then
		selection -> (start,end);
		end - start -> length;
		(if length<>0 then
			(start,-length) -> theEventHandler.onBeforeChange -> performEdit;
			(if performEdit then
				focusTextField
					(#
					do textFieldHandle -> TECut;
					#);
				TEToScrap -> error;
				handleTextChanged;
			if);
		if);
	if);
#)

-- GUIENVtextFieldDoClear: descriptor --
(# start,end,length: @integer;
	performEdit: @boolean;
do assertOpen;
	selection -> (start,end);
	end - start -> length;
	(if length<>0 then
		(start,-length) -> theEventHandler.onBeforeChange -> performEdit;
		(if performEdit then
			focusTextField
				(#
				do textFieldHandle -> TEDelete;
				#);
			handleTextChanged;
		if);
	if);
#)

-- GUIENVtextFieldSetContents: descriptor --
(#
do assertOpen;
	textFieldDefaultStyle -> useTextStyle;
	(theText[], textFieldHandle) -> TextEditSetText;
	(if theText.styleInfo<>0 then
		(0, theText.lgth, theText.styleInfo, false, textFieldHandle)
			-> TEUseStyleScrap;
	if);
	(textFieldHandle,0,0) -> TextEditSetSelection;
	adjustScrollValuesMaybe;
	updateMaybe;
#)

-- GUIENVtextFieldGetContents: descriptor --
(#	len: @integer;
	textHandle: @integer;
	address: @integer;
	start,end: @integer;
	t: ^text;
do	assertOpen;
	&styledText[] -> theText[];
	textFieldHandle -> TextEditGetText -> t[];
	t -> theText;
	(0,len) -> withSelection
		(#
		do textFieldHandle -> TEGetStyleScrapHandle -> theText.styleInfo;
		#);
#)

-- GUIENVtextFieldGetChar: descriptor --
(#
do assertOpen;
	(textFieldHandle,pos) -> TextEditGetChar -> ch;
#)

-- GUIENVtextFieldLength: descriptor --
(# TextEditGetLength: external
		(# teHandle: @integer;
			length: @integer;
		enter teHandle
		exit length
		#);
do assertOpen;
	textFieldHandle -> TextEditGetLength -> value;
#)

-- GUIENVtextFieldScanText: descriptor --
(#	teHandle: @integer;
	current,increment: @integer;
	
do	assertOpen;
	(0,start,length) -> between -> start;
	(0,end,length) -> between -> end;
	(if start<>end then
		textFieldHandle -> teHandle;
		start -> current;
		(if start < end then
			1 -> increment;
		else
			-1 -> increment;
		if);
		scan:
			(if current<>end then
				(teHandle,current) -> TextEditGetChar -> ch;
				inner scanText;
				current + increment -> current;
				restart scan;
			if);
	if);
#)

-- GUIENVtextFieldPosToPt: descriptor --
(# macPt: ^macPoint;
do assertOpen;
	(pos,textFieldHandle) -> TEGetPoint -> macPt[];
	macPt.getPoint -> pt;
#)

-- GUIENVtextFieldPtToPos: descriptor --
(# macPt: @macPoint;
do	assertOpen;
	pt -> macPt.setPoint;
	(macPt,textFieldHandle) -> TEGetOffset -> pos;
#)

-- GUIENVtextFieldSelectionStart: descriptor --
(#	TextEditGetSelectionStart: external
		(# teHandle: @integer;
			start: @integer;
		enter teHandle
		exit start
		#);
do assertOpen;
	textFieldHandle -> TextEditGetSelectionStart -> value;
#)

-- GUIENVtextFieldSelectionEnd: descriptor --
(# TextEditGetSelectionEnd: external
		(# teHandle: @integer;
			end: @integer;
		enter teHandle
		exit end
		#);
do assertOpen;
	textFieldHandle -> TextEditGetSelectionEnd -> value;
#)

-- GUIENVtextFieldSelectionContents: descriptor --
(# 
do assertOpen;
	&text[] -> theText[];
	(start,end) -> scanText
		(#
		do ch -> theText.put;
		#);
#)

-- GUIENVtextFieldSelectionScrollIntoView: descriptor --
(#
do assertOpen;
	focusTextField
		(#
		do textFieldHandle -> TESelView;
		#);
	adjustScrollvaluesMaybe;
#)

-- GUIENVtextFieldSelectionSet: descriptor --
(#
do assertOpen;
	(if privateAutomaticUpdate and viewable then
		focusTextField
			(#
			do (theStart,theEnd,textFieldHandle) -> TESetSelect;
			#);
	else
		setupPortClipEmpty
			(#
			do (theStart,theEnd,textFieldHandle) -> TESetSelect;
			#);
	if);
#)

-- GUIENVtextFieldSetDefaultStyle: descriptor --
(#
do assertOpen;
	style[] -> textFieldDefaultStyle;
#)

-- GUIENVtextFieldGetDefaultStyle: descriptor --
(#
do assertOpen;
	textFieldDefaultStyle -> style[];
#)

-- GUIENVtextFieldIsOneStyle: descriptor --
(#
do (* Not implemented !!! *)
#)

-- GUIENVtextFieldSetOneSize: descriptor --
(#	macStyle: @textStyleRec;
do	assertOpen;
	theSize -> macStyle.tsSize;
	(macStyle[],doSize,start,end) -> setStyleElement;
#)

-- GUIENVtextFieldSetOneFont: descriptor --
(#	macStyle: @textStyleRec;
	style: @textStyle;
do	assertOpen;
	theFont[] -> style.name;
	style.familyID -> macStyle.tsFont;
	(macStyle[],doFont,start,end) -> setStyleElement;
#)

-- GUIENVtextFieldSetOneFace: descriptor --
(#	macStyle: @textStyleRec;
do	assertOpen;
	theFace -> macStyle.tsFace;
	(macStyle[],doFace,start,end) -> setStyleElement;
#)

-- GUIENVtextFieldSetOneStyle: descriptor --
(#	macStyle: @textStyleRec;
do	assertOpen;
	theStyle.familyID -> macStyle.tsFont;
	theStyle.size -> macStyle.tsSize;
	theStyle.face -> macStyle.tsFace;
	(macStyle[],doSize + doFace + doFont,start,end) -> setStyleElement;
#)

-- GUIENVtextFieldScanTextWithStyle: descriptor --
(#
do (* Not implemented !!! *)
#)

-- GUIENVtextFieldSetMargin: descriptor --
(# view: ^macRect;
do	assertOpen;
	(leftMargin,topMargin) -> textFieldMargin;
	calcViewRect -> makeMacRect -> view[];
	(textFieldHandle,view[]) -> TextEditUpdateViewRect;
	adjustScrollvaluesMaybe;
#)

-- GUIENVtextFieldGetMargin: descriptor --
(#
do assertOpen;
	textFieldMargin ->  (leftMargin,topMargin);
#)

-- GUIENVtextFieldInsert: descriptor --
(#
do assertOpen;
	(if privateAutomaticUpdate and viewable then
		focusTextField
			(#
			do (theText[], textFieldHandle) -> TextEditInsert;
			#);
	else
		setupPortClipEmpty
			(#
			do (theText[], textFieldHandle) -> TextEditInsert;
			#);
	if);
	adjustScrollvaluesMaybe;
#)

-- GUIENVtextFieldDelete: descriptor --
(#
do assertOpen;
	(if privateAutomaticUpdate and viewable then
		focusTextField
			(#
			do textFieldHandle -> TEDelete;
			#);
	else
		setupPortClipEmpty
			(#
			do textFieldHandle -> TEDelete;
			#);
	if);
	adjustScrollvaluesMaybe
#)

-- GUIENVtextFieldCreate: descriptor --
(#
do whiteBackGround -> backGroundStyle;
	inner create;
#)

-- GUIENVtextFieldOpen: descriptor --
(# style: ^textStyle;
do &textStyle[] -> style[];
	'Courier' -> style.name;
	12 -> style.size;
	style[] -> textFieldDefaultStyle;
	textFieldDefaultStyle -> useTextStyle;
	(4,4) -> textFieldMargin;
	((0,0),(1000,1000)) -> privateWindowItemFrame;
	focusTextField
		(# view,dest: ^macRect;
		do calcViewRect -> makeMacRect -> view[];
			calcInitialDestRect -> makeMacRect -> dest[];
			(dest[],view[]) -> TEStyleNew -> textFieldHandle;
			(true,textFieldHandle) -> TEAutoView;
		#);
	false -> this(windowitem).private.updateOnResize;
	cursors.iBeam[] -> theCursor;
	(if this(window).style = plain then
		borderStyles.simple -> border.style;
	else
		borderStyles.shadowIn -> border.style;
	if);
	true -> border.visible;
	cursors.iBeam[] -> theCursor;
	THIS(guienv).private.white[] -> this(windowItem).private.background[];
	inner open;
#)

-- GUIENVtextFieldClose: descriptor --
(#
do inner close;
	textFieldHandle -> TEDispose;
	0 -> textFieldHandle;
	NONE -> private.textFieldDefaultStyle[];
	(0, 0) -> private.textFieldMargin;
	NONE -> private.textFieldScroller[];
	
#)

-- textFieldLib: attributes --

focusTextField: setupClipForDrawing
	(#
	do ResetPen;
		INNER;
	#);
	
TextEditUpdateViewRect: external
	(# teHandle: @integer;
		view: ^macRect;
	enter (teHandle,view[])
	#);
	
handleTextChanged:
	(# 
	do adjustScrollvaluesMaybe;
		theEventHandler.onTextChanged;
	#);
adjustScrollvaluesMaybe:
	(# theTextEditor: ^textEditor;
	do textFieldScroller -> theTextEditor[];
		(if theTextEditor[]<>none then
			theTextEditor.adjustScrollValues;
		if);
	#);
getClipboardLength: integerValue
	(# offset: @longInt;
		TextType: (# exit 1413830740 #);
	do (0,TextType,offset[]) -> GetScrap -> value;
	#);
calcViewRect:
	(#	r: ^rectangle;
		left,top: @integer;
		width,height: @integer;
		t: @text;
	do &rectangle[] -> r[];
		textFieldMargin -> (left,top);
		(left+2,top+2) -> r.topLeft;
		size -> (width,height);
		width - 2 -> r.right;
		height - 2 -> r.bottom;
	exit r[]
	#);
calcInitialDestRect:
	(#	r: ^rectangle;
		left,top: @integer;
		width,height: @integer;
	do &rectangle[] -> r[];
		textFieldMargin -> (left,top);
		(left+2,top+2) -> r.topLeft;
		size -> (width,height);
		1000 -> r.right;
		height - top - 2 -> r.bottom;
	exit r[]
	#);
textFieldHandle:
	(#
	enter private.textFieldHandle
	exit private.textFieldHandle
	#);
textFieldDefaultStyle:
	(#
	enter private.textFieldDefaultStyle[]
	exit private.textFieldDefaultStyle[]
	#);
textFieldMargin:
	(#
	enter private.textFieldMargin
	exit private.textFieldMargin
	#);
textFieldScroller:
	(#
	enter private.textFieldScroller[]
	exit private.textFieldScroller[]
	#);
TextEditSetSelection: external
	(# teHandle: @integer;
		start,end: @integer;
	enter (teHandle,start,end)
	#);
TextEditGetSelection: external
	(# teHandle: @integer;
		startP,endP: @integer;
	enter (teHandle,startP,endP)
	#);
TextEditGetChar: external
	(#	teHandle: @integer;
		inx: @integer;
		ch: @integer;
	enter (teHandle,inx)
	exit ch
	#);
withSelection:
	(# start,end: @integer;
		curStart,curEnd: @integer;
	enter (start,end)
	do (textFieldHandle,@@curStart,@@curEnd) -> TextEditGetSelection;
		(textFieldHandle,start,end) -> TextEditSetSelection;
		inner;
		(textFieldHandle,curStart,curEnd) -> TextEditSetSelection;
	#);
setStyleElement:
	(#	macStyle: ^textStyleRec;
		curStart,curEnd,start,end: @integer;
		currentSelection: @boolean;
		element: @integer;
	enter (macStyle[],element,start,end)
	do	selection -> (curStart,curEnd);
		(curStart = start) and (curEnd = end) -> currentSelection;
		(if currentSelection then
			focusTextField
				(#
				do (element,macStyle[],true,textFieldHandle) -> TESetStyle;
				#);
		else
			(start,end) -> withSelection
				(#
				do (element,macStyle[],false,textFieldHandle) -> TESetStyle;
				#);
			updateMaybe;
		if);
		adjustScrollvaluesMaybe;
	#);
-- GUIENVtextFieldPrivate: descriptor --
(# textFieldHandle: @integer;
	textFieldDefaultStyle: ^textStyle;
	textFieldMargin: @point;
	textFieldScroller: ^textEditor;
	installTimer:
		(#
		do theTimer[] -> this(GUIenv).private.timerQueue.insert;
		#);
	deleteTimer:
		(#
		do theTimer[] -> this(GUIenv).private.timerQueue.delete;
		#);
	theTimer: @timerAction
		(# doIt:@ focusTextField
			(#
			do textFieldHandle -> TEIdle;
			#);
		do (if textFieldHandle <> 0 then
				doIt;
			if);
		#);
	
#)

-- GUIENVabstractScrollerScroll: descriptor --
(#
do assertOpen;
	inner scroll;
#)

-- GUIENVabstractScrollerCreate: descriptor --
(#
do inner create;
#)

-- GUIENVabstractScrollerOpen: descriptor --
(#
do ((0,0),(200,200)) -> privateWindowItemFrame;
	&scrollerVerticalScrollbar[] -> THIS(abstractScroller).private.VerticalScrollbar[];
	this(abstractScroller)[] -> THIS(abstractScroller).private.VerticalScrollbar.open;
	&scrollerHorizontalScrollbar[] -> THIS(abstractScroller).private.HorizontalScrollbar[];
	this(abstractScroller)[] -> THIS(abstractScroller).private.HorizontalScrollbar.open;
	true -> border.visible;
	inner open;
#)

-- GUIENVabstractScrollerClose: descriptor --
(#
do inner close;
	NONE -> private.HorizontalScrollbar[];
	NONE -> private.VerticalScrollbar[];
#)

-- abstractScrollerLib: attributes --
scrollerVerticalScrollbar: scrollbar
	(#	open::<
			(# width,height: @integer;
			do this(abstractScroller).size -> (width,height);
				((width - 17,1),(width - 1,height - 16)) -> frame;
				true -> bindTop;
				true -> bindRight;
				false -> bindLeft;
				true -> bindBottom;
			#);
		eventHandler::<
			(#	onButtonDown::<
					(#
					do (0,scrollAmount) -> scroll;
						setupClipForDrawing;
					#);
				onButtonUp::<
					(#
					do (0,-scrollAmount) -> scroll;
						setupClipForDrawing;
					#);
				onPageUp::<
					(#
					do (0,-pageScrollAmount) -> scroll;
						setupClipForDrawing;
					#);
				onPageDown::<
					(#
					do (0,pageScrollAmount) -> scroll;
						setupClipForDrawing;
					#);
				onThumbMoved::<
					(#
					do (0,amount) -> scroll;
						setupClipForDrawing;
					#);
			#);
	#);
scrollerHorizontalScrollbar: scrollbar
	(#	open::<
			(# width,height: @integer;
			do this(abstractScroller).size -> (width,height);
				((1,height - 17),(width - 16,height-1)) -> frame;
				true -> bindLeft;
				false -> bindTop;
				true -> bindRight;
				true -> bindBottom;
			#);
		eventHandler::<
			(#	onButtonDown::<
					(#
					do (scrollAmount,0) -> scroll;
						setupClipForDrawing;
					#);
				onButtonUp::<
					(#
					do (-scrollAmount,0) -> scroll;
						setupClipForDrawing;
					#);
				onPageUp::<
					(#
					do (-pageScrollAmount,0) -> scroll;
						setupClipForDrawing;
					#);
				onPageDown::<
					(#
					do (pageScrollAmount,0) -> scroll;
						setupClipForDrawing;
					#);
				onThumbMoved::<
					(# 
					do (amount,0) -> scroll;
						setupClipForDrawing;
					#);
			#);
	#);
-- GUIENVabstractScrollerPrivate: descriptor --
(# verticalScrollbar, horizontalScrollbar: ^scrollbar;
#)

-- textEditorLib: attributes --

adjustScrollValues:
	(#	TextEditGetScrollvalues: external
			(#	editTextHandle: @integer;
				hMaxP,hP,vMaxP,vP: @integer;
			enter (editTextHandle,hMaxP,hP,vMaxP,vP)
			#);
		TextEditAdjustScroll: external
			(#	editTextHandle: @integer;
				h,v: @integer;
			enter (editTextHandle,h,v)
			#);
		hMax,h,vMax,v: @integer;
		t: @text;
		width,height: @integer;
	do (contents.textFieldHandle,@@hMax,@@h,@@vMax,@@v) -> TextEditGetScrollvalues;
		hMax -> THIS(abstractScroller).private.HorizontalScrollbar.maxValue;
		h -> THIS(abstractScroller).private.HorizontalScrollbar.value;
		vMax -> THIS(abstractScroller).private.VerticalScrollbar.maxValue;
		v -> THIS(abstractScroller).private.VerticalScrollbar.value;
		contents.size -> (width,height);
		width -> THIS(abstractScroller).private.HorizontalScrollbar.pageScrollAmount;
		height -> THIS(abstractScroller).private.VerticalScrollbar.pageScrollAmount;
	#);
-- GUIENVtextEditorScroll: descriptor --
(#
do contents.assertOpen;
	contents.focusTextField
		(#
		do (-dh,-dv,contents.textFieldHandle) -> TEPinScroll;
		#);
	inner scroll;
#)

-- GUIENVtextEditorCreate: descriptor --
(#
do inner create;
#)

-- GUIENVtextEditorOpen: descriptor --
(# width,height: @integer;
do	this(textEditor)[] -> contents.textFieldScroller;
	this(textEditor)[] -> contents.open;
	size -> (width,height);
	((2, 2), (width - 17, height - 17)) -> contents.frame;
	(* ((0,0),(width - 15,height - 15)) -> contents.frame; *)
	true -> contents.bindLeft;
	true -> contents.bindTop;
	true -> contents.bindRight;
	true -> contents.bindBottom;
	false -> contents.border.visible;
	inner open;
#)

-- GUIENVtextEditorClose: descriptor --
(#
do inner close;
#)

-- GUIENVtexteditorprivate: descriptor --
(#
#)
-- GUIENVscrollerScroll: descriptor --

(* Denne version virker, men er lidt grim: *)
(# h, v: @integer;
	width,height,viewWidth,viewHeight: @integer;
	newH,newV: @integer;
	saved: @boolean;
do true -> private.scrolling;



	contents.position -> (h, v);
	contents.size -> (width,height);
	private.view.size -> (viewWidth,viewHeight);
	
	
	
	h - dh -> h;
	v - dv -> v;
	
	(*
	 * ((viewWidth - width, 0) -> Min, h - dh, 0) -> between -> newH;
	 * ((viewHeight - height, 0) -> Min, v - dv, 0) -> between -> newV;
	 *)
	 
	(if (newH <> h) OR (newV <> v) then
 		contents.privateAutomaticUpdate -> saved;
		false -> contents.privateAutomaticUpdate;
		(newH,newV) -> contents.position;
		saved -> contents.privateAutomaticUpdate;
		(newH, newV) -> contents.position;
		contents.theEventHandler.onRefresh;
	if);
	false -> private.scrolling;
#)


-- GUIENVscrollerCreate: descriptor --
(#
do	inner create;
#)

-- GUIENVscrollerOpen: descriptor --
(# width,height: @integer;
do	this(scroller)[] -> private.view.open;
	whiteBackGround -> private.view.backGroundStyle;
	private.view[] -> contents.open;
	whiteBackGround -> contents.backGroundStyle;
	size -> (width,height);
	((2,2),(width - 17,height - 17)) -> private.view.frame;
	true -> private.view.bindLeft;
	true -> private.view.bindTop;
	true -> private.view.bindRight;
	true -> private.view.bindBottom;
	((0,0),(1000,1000)) -> contents.frame;
	adjustScrollValues;
	
	inner open;
#)

-- GUIENVscrollerClose: descriptor --
(#
do inner close;
#)

-- scrollerLib: attributes --
adjustScrollValues:
	(#	width,height,viewWidth,viewHeight: @integer;
		minVerticalValue, minHorizontalValue: @integer;
		h,v: @integer;
		maxWidth, maxHeight: @integer;
	do contents.position -> (h,v);
	
		-h -> minHorizontalValue;
		-v -> minVerticalValue;
		
		contents.size -> (width,height);
		private.view.size -> (viewWidth,viewHeight);
		
		(width - viewWidth, 	minHorizontalValue) -> Max -> maxWidth;
		(height - viewHeight, 	minVerticalValue) -> Max -> maxHeight;
		
		maxWidth -> THIS(abstractScroller).private.HorizontalScrollbar.maxValue;
		maxHeight -> THIS(abstractScroller).private.VerticalScrollbar.maxValue;
		
		(*
		 * (0, -h, maxWidth) -> between -> h;
		 * (0, -v, maxHeight) -> between -> v;
		 *)
		 
		-h -> THIS(abstractScroller).private.HorizontalScrollbar.value;
		-v -> THIS(abstractScroller).private.VerticalScrollbar.value;
		
		(*
		 * (h,v) -> setScroll;
		 *)
		
		viewWidth -> THIS(abstractScroller).private.HorizontalScrollbar.pageScrollAmount;
		viewHeight -> THIS(abstractScroller).private.VerticalScrollbar.pageScrollAmount;
	#);

setScroll:
	(#	h,v: @integer;
		currentH,currentV: @integer;
	enter (h,v)
	do contents.position -> (currentH,currentV);
		(if ((h<>currentH) and (v<>currentV)) then
			(currentH - h,currentV - v) -> scroll;
		if);
	#);

adjustScrollValues1:
	(* OLD version - there are som minor problems *)
	(#	width,height,viewWidth,viewHeight: @integer;
		h,v: @integer;
		maxWidth, maxHeight: @integer;
	do contents.size -> (width,height);
		private.view.size -> (viewWidth,viewHeight);
		
		(width - viewWidth, 	0) -> Max -> maxWidth;
		(height - viewHeight, 	0) -> Max -> maxHeight;
		
		maxWidth -> THIS(abstractScroller).private.HorizontalScrollbar.maxValue;
		maxHeight -> THIS(abstractScroller).private.VerticalScrollbar.maxValue;
		
		contents.position -> (h,v);
		(0, -h, maxWidth) -> between -> h;
		(0, -v, maxHeight) -> between -> v;
		h -> THIS(abstractScroller).private.HorizontalScrollbar.value;
		v -> THIS(abstractScroller).private.VerticalScrollbar.value;
		
		(h,v) -> setScroll;
		
		viewWidth -> THIS(abstractScroller).private.HorizontalScrollbar.pageScrollAmount;
		viewHeight -> THIS(abstractScroller).private.VerticalScrollbar.pageScrollAmount;
	#);
-- GUIENVscrollerOnFrameChanged: descriptor --
(#
do adjustScrollValues;
	inner onFrameChanged;
#)

-- GUIENVscrollerPrivate: descriptor --
(# scrolling: @boolean;
   view: @canvas
	(# eventHandler::
			(# onChildFrameChanged::
					(#
					do (if not scrolling then
							adjustScrollValues; 
						if);
					#);
			#);
	#);
#)

-- GUIENVputmovieFieldLayout: doPart --
do

-- GUIENVgetmovieFieldLayout: doPart --
do

-- GUIENVtextFieldputLayout: doPart --
do

-- GUIENVtextFieldgetLayout: doPart --
do

-- GUIENVabstractScrollerputLayout: doPart --
do

-- GUIENVabstractScrollergetLayout: doPart --
do

-- GUIENVtextEditorputLayout: doPart --
do

-- GUIENVtextEditorgetLayout: doPart --
do

-- GUIENVscrollerputLayout: doPart --
do

-- GUIENVscrollergetLayout: doPart --
do

-- GUIENVtextFieldBeforeChangeTheText: doPart --
do this(guienv).private.globalTheText.copy -> value[];
