ORIGIN '../graphicsbody';

INCLUDE 'guienv_macprivate';
INCLUDE '~beta/maclib/v3.1/quickdraw';
INCLUDE 'pixmapbody';

-- graphicsLib: attributes --

setupPortForGraphics: setupClipForDrawing
  (# 
  do ResetPen;
  	 (if this(windowItem).private.background[] = NONE then
     	this(GUIenv).private.background -> pen.backGroundColor;
	else
		this(windowItem).private.background -> pen.backGroundColor;
	if);
     INNER;
  #);

makeMacColor:
  (# theColor: ^RGBColor;
     red, green, blue: @integer;
  enter	 (red, green, blue)
  do &RGBColor[] -> theColor[];
     red -> theColor.red;
     green -> theColor.green;
     blue -> theColor.blue;
  exit theColor[]
  #);

-- GUIENVgraphicsPenSize: descriptor --
(#
do (value,value) -> PenSize;
#)

-- GUIENVgraphicsPenForegroundColor: descriptor --
(#
do theColor -> makeMacColor -> RGBForeColor
#)

-- GUIENVgraphicsPenBackgroundColor: descriptor --
(#
do theColor -> makeMacColor -> RGBBackColor
#)

-- GUIENVgraphicsPenStipple: descriptor --
(# data: [2] @integer;
do	(* (if b.isPixPat then
            b.rasterPrivatePart.pixPatAddress -> PenPixPat;
	 else
            b.patternData -> data;
            @@data[1] -> PenPat;
	if);
	*)
#)

-- GUIENVgraphicsPenMode: descriptor --
(# qdMode: @integer;
do (if m
    //transferModes.copy then srcCopy -> qdMode;
    //transferModes.invertCopy then notSrcCopy -> qdMode;
    //transferModes.erase then srcBic -> qdMode;
    //transferModes.andBlend then 0 -> qdMode;
    //transferModes.orBlend then srcOr -> qdMode;
    //transferModes.xorBlend then srcXor -> qdMode;
    //transferModes.notAndBlend then 0 -> qdMode;
    //transferModes.notOrBlend then notSrcOr -> qdMode;
   if);
   qdMode -> PenMode;
#)

-- GUIENVgraphicsStyle: descriptor --
(#
do	theTextStyle.familyID -> TextFont;
   theTextStyle.size -> TextSize;
   theTextStyle.face -> TextFace;
#)

-- GUIENVgraphicsMove: descriptor --

(# Move: External (# dh,dv: @ShortInt; enter (dh,dv) do '$A894' -> PascalTrap; #);
do	(p.h,p.v) -> Move;
#)

-- GUIENVgraphicsMoveTo: descriptor --

(# MoveTo: External (# h,v: @ShortInt; enter (h,v) do '$A893' -> PascalTrap; #);
do (p.h,p.v) -> MoveTo;
#)

-- GUIENVgraphicsDraw: descriptor --

(# Line: External (# dh,dv: @ShortInt;  enter (dh,dv) do '$A892'->PascalTrap;  #);
do	(p.h,p.v) -> Line;
#)

-- GUIENVgraphicsDrawTo: descriptor --

(#
do (p.h,p.v) -> LineTo;
#)

-- GUIENVgraphicsDrawSpot: descriptor --

(#
do	savePenState
   (#
   do (1,1) -> PenSize;
      (p.h,p.v) -> MoveTo;
      (p.h,p.v) -> LineTo;
   #);
#)

-- GUIENVgraphicsDrawLine: descriptor --

(#
do	
   savePenState
   (#
   do (p1.h, p1.v) -> MoveTo;
      (p2.h,p2.v) -> LineTo;
   #);
#)

-- GUIENVgraphicsDrawText: descriptor --

(# 
do t[] -> DrawBetaText;
#)

-- GUIENVgraphicsDrawPolygon: descriptor --

(# macPoly: @integer;
do	points -> makeMacPoly -> macPoly;
   macPoly -> FramePoly;
   macPoly -> KillPoly;
#)

-- GUIENVgraphicsDrawRect: descriptor --

(#
do	r[] -> makeMacRect -> FrameRect;
#)

-- GUIENVgraphicsDrawRoundRect: descriptor --

(#	mr: ^macRect;
   width,height: @integer;
do r[] -> makeMacRect -> mr[];
   roundNess.size -> (width,height);
   (mr[],width,height) -> FrameRoundRect;
#)

-- GUIENVgraphicsDrawOval: descriptor --

(#	mr: ^macRect;
do r[] -> makeMacRect -> FrameOval;
#)

-- GUIENVgraphicsDrawSlice: descriptor --

(# mr: ^macRect;
do r[] -> makeMacRect -> mr[];
   (mr[],fromAngle,toAngle) -> FrameArc;
#)

-- GUIENVgraphicsFillPolygon: descriptor --

(# macPoly: @integer;
do	points -> makeMacPoly -> macPoly;
   macPoly -> PaintPoly;
   macPoly -> KillPoly;
#)

-- GUIENVgraphicsFillRect: descriptor --

(#
do	r[] -> makeMacRect -> PaintRect;
#)

-- GUIENVgraphicsFillRoundRect: descriptor --

(#	mr: ^macRect;
   width,height: @integer;
do r[] -> makeMacRect -> mr[];
   roundNess.size -> (width,height);
   (mr[],width,height) -> PaintRoundRect;
#)

-- GUIENVgraphicsFillOval: descriptor --

(#	mr: ^macRect;
do r[] -> makeMacRect -> PaintOval;
#)

-- GUIENVgraphicsFillSlice: descriptor --

(# mr: ^macRect;
do r[] -> makeMacRect -> mr[];
   (mr[],toAngle,toAngle) -> PaintArc;
#)

-- GUIENVgraphicsDrawRaster: descriptor --

(# 
   srcMcRect, dstMcRect: ^macRect;
   srcRect, dstRect: @rectangle;
   gworld: @integer;
   wp: @integer;
do 
   from -> srcRect.topLeft;
   (width, height) -> srcRect.size;
   to -> dstRect.topLeft;
   (width, height) -> dstRect.size;
   srcRect[] -> makeMacRect -> srcMcRect[];
   dstRect[] -> makeMacRect -> dstMcRect[];
   
   p.private.gworld -> gworld;
   (if gworld <> 0 then
       this(window).private.windowPointer -> wp;
	   (if wp <> 0 then
      	  (gworld, wp, srcMcRect[], dstMcRect[], 0, 0)  -> CopyGWorldToWindow;
	   if);
   if);

  
#)

-- GUIENVgraphicsPrivate: descriptor --
(#
#)

-- GUIENVgraphicsBody: descriptor --
(#
do assertOpen;
	setupPortForGraphics
   (#
   do INNER graphics;
   #);
#)


