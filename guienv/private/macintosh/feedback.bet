ORIGIN '../../guienv';

-- windowItemLib: attributes --

buttonFeedback:
	(# onHilite:<
			(# do INNER; #);
		onLowlite:<
			(# do INNER; #);
		onReleaseInside:<
			(# do INNER; #);
		area: @rectangle;
	enter area
	do trackMouse
			(# 
				hilited: @boolean;
				hilite: @
					(#
					do onHilite;
						true -> hilited;
					#);
				lowlite: @
					(#
					do onLowlite;
						false -> hilited;
					#);
				mousePress::
					(#
					do hilite;
					#);
				mouseMove::
					(#
					do (if curPt -> area.containsPoint then
							(if NOT hilited then
								hilite;
							if);
						else
							(if hilited then
								lowlite;
							if);
						if);
					#);
				mouseRelease::
					(#
					do (if hilited then
							lowlite;
							onReleaseInside;
						if);
					#);
			#);
	#);