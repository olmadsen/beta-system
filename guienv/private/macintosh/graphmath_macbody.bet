ORIGIN '../graphmathbody';

INCLUDE '~beta/maclib/v3.0/quickdraw';
INCLUDE '~beta/maclib/v3.0/memory';

-- regionLib: attributes --

assertNonNil:
	(#
	do (if private.regionHandle = 0 then
			Exception
				(#
				do 'a region handle is NULL' -> msg.append;
				#);
		if);
	#);

-- GUIENVregionBounds: descriptor --
(# rgnRec: @regionRecord;
	rgnBBounds : @rgnRec.rgnBBox;
	initFromHandle: @rgnRec.initFromHandle;
do private.regionHandle -> HLock;
	private.regionHandle -> initFromHandle;
	rgnBBounds.left -> theRectangle.left;
	rgnBBounds.top -> theRectangle.top;
	rgnBBounds.right -> theRectangle.right;
	rgnBBounds.bottom -> theRectangle.bottom;
	private.regionHandle -> HUnLock;
#)

-- GUIENVregionAllocate: descriptor --
(#
do NewRgn -> private.regionHandle;
	(if private.regionHandle = 0 then
		Exception
			(#
			do 'Failed to allocate Region' -> msg.append;
			#);
	if);
#)

-- GUIENVregionDispose: descriptor --
(#
do assertNonNil;
	private.regionHandle -> DisposeRgn;
	0 -> private.regionHandle;
#)

-- GUIENVregionEmpty: descriptor --
(#
do assertNonNil;
	private.regionHandle -> SetEmptyRgn;
#)

-- GUIENVregionSetFromRectangle: descriptor --
(#
do assertNonNil;
	(private.regionHandle, theRectangle.left,
								  theRectangle.top,
								  theRectangle.right,
								  theRectangle.bottom) -> SetRectRgn;
#)

-- GUIENVregionOffset: descriptor --
(#
do assertNonNil;
	(private.regionHandle, delta.h, delta.v) -> OffsetRgn;
#)

-- GUIENVregionInset: descriptor --
(#
do assertNonNil;
	(private.regionHandle, delta.h, delta.v) -> InsetRgn;
#)

-- GUIENVregionIntersection: descriptor --
(#
do assertNonNil;
	src1.assertNonNil;
	src2.assertNonNil;
	(src1.private.regionHandle,
	 src2.private.regionHandle,
	 private.regionHandle) -> SectRgn;
#)

-- GUIENVregionUnion: descriptor --
(#
do assertNonNil;
	src1.assertNonNil;
	src2.assertNonNil;
	(src1.private.regionHandle,
	 src2.private.regionHandle,
	 private.regionHandle) -> UnionRgn;
#)

-- GUIENVregionDifference: descriptor --
(#
do assertNonNil;
	src1.assertNonNil;
	src2.assertNonNil;
	(src1.private.regionHandle,
	 src2.private.regionHandle,
	 private.regionHandle) -> DiffRgn;
#)

-- GUIENVregionXOr: descriptor --
(#
do assertNonNil;
	src1.assertNonNil;
	src2.assertNonNil;
	(src1.private.regionHandle,
	 src2.private.regionHandle,
	 private.regionHandle) -> XorRgn;
#)

-- GUIENVregionContainsPoint: descriptor --
(# macPt: @macPoint;
do assertNonNil;
	pt -> macPt.setPoint;
	(macPt, private.regionHandle) -> PtInRgn
#)

-- GUIENVregionContainsRectangle: descriptor --
(# mr: @macRect;
do assertNonNil;
	theRectangle.left -> mr.left;
	theRectangle.top -> mr.top;
	theRectangle.right -> mr.right;
	theRectangle.bottom -> mr.bottom;
	(mr[], private.regionHandle) -> RectInRgn -> value;
#)

-- GUIENVregionIsEqual: descriptor --
(#
do assertNonNil;
	theRegion.assertNonNil;
	(private.regionHandle, theRegion.private.regionHandle) -> EqualRgn -> value;
	
#)

-- GUIENVregionIsEmpty: descriptor --
(#
do assertNonNil;
	private.regionHandle -> EmptyRgn -> value;
#)

-- GUIENVregionPrivate: descriptor --
(# regionHandle: @integer;
#)

-- GUIENVregionEnter: descriptor --
(#
do assertNonNil;
	r.assertNonNil;
	(r.private.regionHandle, private.regionHandle) -> CopyRgn;
#)

-- GUIENVregionExit: descriptor --
(#
do THIS(region)[] -> r[];
#)


