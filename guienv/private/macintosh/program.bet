ORIGIN '../guienv'
[[
-- include '../fields'
-- menulib: attributes --
addMenuItem:
	(#	theMenuitem: ^menuitem;
		name: ^text;
	enter name[]
	do &dynamicMenuitem[] -> theMenuitem[];
		theMenuitem.open;
		name[] -> theMenuitem.name;
		theMenuitem[] -> append;
	#);
-- program: descriptor --
uienv
	(#	
		fileMenu: @menu
			(# newitem: @menuitem
					(#	eventHandler::<
							(# onSelect::<
									(# theWindow: ^window;
									do &windowType[] -> theWindow[];
										theWindow.open;
									#);
							#);
						open::<
							(#
							do 'New' -> name;
								'N' -> key;
							#);
					#);
				quititem: @menuitem
					(#	eventHandler::<
							(# onSelect::<
									(#
									do Terminate;
									#);
							#);
						open::<
							(#
							do 'Quit' -> name;
								'Q' -> key;
							#);
					#);
				open::<
					(#
					do 'File' -> name;
						newitem.open;
						newitem[] -> append;
						quititem.open;
						quititem[] -> append;
					#);
			#);
		editMenu: @menu
			(#	undoItem: @dynamicMenuitem;
				separator1: @separator;
				cutItem: @dynamicMenuitem;
				copyItem: @dynamicMenuitem;
				pasteItem: @dynamicMenuitem;
				clearItem: @dynamicMenuitem;
				open::<
					(#
					do 'Edit' -> name;
						undoItem.open;
						'Undo' -> undoItem.name;
						'Z' -> undoItem.key;
						undoItem[] -> append;
						
						separator1.open;
						'-' -> separator1.name;
						separator1[] -> append;
						
						cutItem.open;
						'Cut' -> cutItem.name;
						'X' -> cutItem.key;
						cutItem[] -> append;
						
						copyItem.open;
						'Copy' -> copyItem.name;
						'C' -> copyItem.key;
						copyItem[] -> append;
						
						pasteItem.open;
						'Paste' -> pasteItem.name;
						'V' -> pasteItem.key;
						pasteItem[] -> append;
						
						clearItem.open;
						'Clear' -> clearItem.name;
						clearItem[] -> append;
					#);
			#);
		fontMenu: @menu
			(#	open::<
					(#
					do	'Font' -> name;
						'Chicago' -> addMenuItem;
						'Palatino' -> addMenuItem;
						'Geneva' -> addMenuItem;
						'Monaco' -> addMenuItem;
						'Times' -> addMenuItem;
					#);
				
			#);
		sizeMenu: @menu
			(#	open::<
					(#
					do 'Size' -> name;
						'9' -> addMenuItem;
						'10' -> addMenuItem;
						'12' -> addMenuItem;
						'14' -> addMenuItem;
						'18' -> addMenuItem;
						'24' -> addMenuItem;
					#);
			#);
		faceMenu: @menu
			(#	open::<
					(#
					do 'Face' -> name;
						'Plain' -> addMenuItem;
						'Bold' -> addMenuItem;
						'Italic' -> addMenuItem;
					#);
			#);
		menubarType::<
			(# open::<
					(#
					do fileMenu.open;
						fileMenu[] -> append;
						editMenu.open;
						editMenu[] -> append;
						fontMenu.open;
						fontMenu[] -> append;
						sizeMenu.open;
						sizeMenu[] -> append;
						faceMenu.open;
						faceMenu[] -> append;
					#);
			#);
		windowType: window
			(# 
				textEditor1: @textEditor
					(# contentsType::<
							(# handleCut: @editMenu.menuAction
									(#	onStatus::<
											(# start,end: @integer;
											do selection -> (start,end);
												start<>end -> value;
											#);
										onSelect::<
											(#
											do cut;
											#);
									#);
								handleCopy: @editMenu.menuAction
									(#	onStatus::<
											(# start,end: @integer;
											do selection -> (start,end);
												start<>end -> value;
											#);
										onSelect::<
											(#
											do copy;
											#);
									#);
								handlePaste: @editMenu.menuAction
									(#	onStatus::<
											(#
											do 
											#);
										onSelect::<
											(#
											do paste;
											#);
									#);
								handleClear: @editMenu.menuAction
									(#	onStatus::<
											(# start,end: @integer;
											do selection -> (start,end);
												start<>end -> value;
											#);
										onSelect::<
											(#
											do clear;
											#);
									#);
								handleFont: fontMenu.menuAction
									(#	onSelect::<
											(#
											do (selection.start,selection.end,theMenuitem.name) -> setOneFont;
											#);
									#);
								handleSize: sizeMenu.menuAction
									(#	onSelect::<
											(# t: ^text;
											do theMenuitem.name -> t[];
												0 -> t.setPos;
												(selection.start,selection.end,t.getInt) -> setOneSize;
											#);
									#);
								eventHandler::<
									(#	onBeforeChange::<
											(#
											do true -> allow;
											#);
										
										onEnableTarget::<
											(#
											do handleCut[] -> editMenu.cutItem.attach;
												handleCopy[] -> editMenu.copyItem.attach;
												handlePaste[] -> editMenu.pasteItem.attach;
												handleClear[] -> editMenu.clearItem.attach;
												sizeMenu.scan
													(#	theDynamicItem: ^sizeMenu.dynamicMenuitem;
													do current[] -> theDynamicItem[];
														&handleSize[] -> theDynamicItem.attach;
													#);
												fontMenu.scan
													(#	theDynamicItem: ^fontMenu.dynamicMenuitem;
													do current[] -> theDynamicItem[];
														&handleFont[] -> theDynamicItem.attach;
													#);
											#);
										onDisableTarget::<
											(#
											do editMenu.cutItem.detach;
												editMenu.copyItem.detach;
												editMenu.pasteItem.detach;
												editMenu.clearItem.detach;
												sizeMenu.scan
													(#	theDynamicItem: ^sizeMenu.dynamicMenuitem;
													do current[] -> theDynamicItem[];
														theDynamicItem.detach;
													#);
												fontMenu.scan
													(#	theDynamicItem: ^fontMenu.dynamicMenuitem;
													do current[] -> theDynamicItem[];
														theDynamicItem.detach;
													#);
											#);
									#);
							#);
						open::<
							(#
							do
							#);
					#);
				open::<
					(# t: ^text;
					do 'Window ' -> t[]; 
						count -> t.putInt;
						t[] -> title;
						(500,600) -> size;
						(count*5 + 5,count*5 + 40) -> position;
						count + 1 -> count;
						textEditor1.open;
					#);
				close::<
					(#
					do 
					#);
				reportEvent:
					(# msg: ^text;
					enter msg[]
					do ': ' -> msg.prepend;
						title -> msg.prepend;
						msg[] -> putText;
					#);
				futte: @boolean;
				eventHandler::<
					(# onKeyDown::<
							(#
							do 
							#);
						onMouseDown::<
							(#	t: @styledText;
							do 'Dette er starten' -> t.putText;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								'Dette er midten' -> t.putText;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								t.newLine;
								'Her er slutningen' -> t.putLine;
								t[] -> textEditor1.contents.contents;
							#);
					#);
			#);
		count: @integer;
	do 1 -> count;
	#)
--]]
