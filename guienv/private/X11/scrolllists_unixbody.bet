ORIGIN '../scrolllistsbody';

INCLUDE 'widget';
INCLUDE 'guienvattributes';
INCLUDE 'motifstuff';
INCLUDE 'guienv_unixprivate';
INCLUDE 'callbacks';

-- scrollListLib: attributes --

selectMouseup: buttonEventSelector
  (# 
  do theEventHandler.onMouseUp;
  #);
listButtonReleaseProcessor: eventProcessor
  (# 
  do &selectMouseup[] -> es[];
  #);

selectMouseDown: buttonEventSelector
  (# 
  do  theEventHandler.onMouseDown;
  #);
listButtonPressProcessor: eventProcessor
  (# 
  do &selectMouseDown[] -> es[];
  #);

selectKeyDown: keyEventSelector
  (# 
  do  theEventHandler.onKeyDown;
  #);
listKeyPressProcessor: eventProcessor
  (# 
  do &selectKeyDown[] -> es[];
  #);

listItemOffset: (# exit 16 #);
listCallbackSelector: motifCallbackSelector
  (# item: @integer;
  do callDataAddress + listItemOffset -> TOS'%adrGetLong' -> item;
     inner;
  #);

selectDefaultAction: listCallbackSelector
  (#
  do (item, true) -> theEventHandler.onSelect;
  #);
defaultActionProcessor: callbackProcessor
  (# 
  do &selectDefaultAction[] -> cb[];
  #);

selectOnSelect: listCallbackSelector
  (# 
  do (item, false) -> theEventHandler.onSelect;
  #);
selectProcessor: callbackProcessor
  (# 
  do &selectOnSelect[] -> cb[];
  #);

motifScrollList: interfaceObject
  (#
  #);
listWidget:
  (# 
  enter private.listWidget.widgetID
  exit private.listWidget.widgetID
  #);
inRange: booleanValue
  (# index: @integer;
  enter index
  do (index > 0) and (index <= numberOfItems) -> value;
  #);
-- GUIENVscrollListnumberOfItems: descriptor --
assertOpen
(# location::(# do 'ScrollList.numberOfItems'->t[] #);
do XmNitemCount -> private.listWidget.getIntegerResource -> value;
#)

-- GUIENVscrollListInsert: descriptor --
assertOpen
(# location::(# do 'ScrollList.insert'->t[] #);
   mStr: @motifString;
do '' -> mStr.setText;
   (for numOfItems repeat
        (listWidget,mStr,beforeItem) -> XmListAddItem;
   for);
   mStr.destroy;
#)

-- GUIENVscrollListPrepend: descriptor --
assertOpen
(# location::(# do 'ScrollList.prepend'->t[] #);
do (1,numOfItems) -> insert;
#)

-- GUIENVscrollListAppend: descriptor --
assertOpen
(# location::(# do 'ScrollList.append'->t[] #);
do (0,numOfItems) -> insert;
#)

-- GUIENVscrollListDelete: descriptor --
assertOpen
(# location::(# do 'ScrollList.delete'->t[] #);
do (listWidget,numOfItems,firstItem) -> XmListDeleteItemsPos;
#)

-- GUIENVscrollListDeleteFirst: descriptor --
assertOpen
(# location::(# do 'ScrollList.deleteFirst'->t[] #);
do (1,numOfItems)  -> delete;
#)

-- GUIENVscrollListDeleteLast: descriptor --
assertOpen
(# location::(# do 'ScrollList.deleteLast'->t[] #);
do (numberOfItems - numOfItems,numOfItems) -> delete;
#)

-- GUIENVscrollListSetItemHeight: descriptor --
assertopen
(# location::(# do 'ScrollList.SetItemHeight'->t[] #);
do 'Warning: ScrollList.SetItemHeight is not possible in Motif implementation.'
     -> putline
#)

-- GUIENVscrollListGetItemHeight: descriptor --
(# (* x, y, width, height: @integer;
    *    widget: @integer;
    *    ok: @integer;
    *    XmListPosToBounds: external
    *      (#
    *         widget, item, xP, yP, widthP, heightP: @integer;
    *         OK: @boolean;
    *      enter (widget, item, xP, yP, widthP, heightP)
    *      exit OK
    *      #);
    * do listWidget -> widget;
    *    (widget, 1, 0, 0, 0, @@height + 2) -> XmListPosToBounds -> OK;
    *    (if OK then
    *        height -> h;
    *    if);
    *)
do 'Warning: Scrolllist.GetItemHeight is not implemented in Motif version' 
     -> putline;
#)

-- GUIENVscrollListSetSingleSelection: descriptor --
assertOpen
(# location::(# do 'ScrollList.setSingleSelection'->t[] #);
do (if isSingle then
       (XmNselectionPolicy,XmBROWSE_SELECT) 
         -> private.listWidget.setIntegerResource;
    else
       (XmNselectionPolicy,XmMULTIPLE_SELECT) 
         -> private.listWidget.setIntegerResource;
   if);
#)

-- GUIENVscrollListGetSingleSelection: descriptor --
assertOpen
(# location::(# do 'ScrollList.getSingleSelection'->t[] #);
   policy: @char;
do XmNselectionPolicy -> private.listWidget.getCharResource -> policy;
   (if policy
    // XmBROWSE_SELECT then true -> isSingle
    // XmMULTIPLE_SELECT then false -> isSingle
   if);
#)

-- GUIENVItemGetItemRectangle: descriptor --
(#
do
#)

-- GUIENVselectionClear: descriptor --
assertOpen
(# location::(# do 'Scrolllist.selection.clear'->t[] #);
do listWidget -> XmListDeselectAllItems;
#)

-- GUIENVselectionscrollIntoView: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.scrollIntoView'->t[] #);
   theitem, topItem: @integer;
   vis: @integer;
   args: @argList;
do selection.first -> theitem;
   XmNtopItemposition -> private.listwidget.getIntegerResource -> topitem;
   XmNvisibleItemCount -> private.listwidget.getIntegerResource -> vis;
   (if (theitem<topitem) or (topitem+vis<=theitem) then
       (* item not visible: center selection.first in visible region *)
       theitem - vis div 2 -> theitem;
       (if theitem<=0 then
           1 -> theitem
       if);
       (listWidget,theitem) -> XmListSetPos;
   if);
#)

-- GUIENVselectionFirst: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.first'->t[] #);
   positionList: @integer;
   positionListCount: @integer;
do (if ((private.listWidgetID,@@positionList,@@positionListCount)
         ->XmListGetSelectedPos) =1 then
       positionList -> TOS'%adrGetLong' -> value;
   if);
#)

-- GUIENVselectionLast: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.last'->t[] #);
   positionList: @integer;
   positionListCount: @integer;
   adrOfLast: @integer;
   hasSelection: @boolean;
do (if ((private.listWidgetID,@@positionList,@@positionListCount) 
         -> XmListGetSelectedPos)=1 then
       positionList + (positionListCount - 1)*4 -> adrOfLast;
       adrOfLast  -> TOS'%adrGetLong' -> value;
   if);
#)

-- GUIENVselectionSelectItem: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.selectItem'->t[] #);
do (if not extend then
       clear;
   if);
   (listWidget,theItem,0) -> XmListSelectPos;
#)

-- GUIENVselectionDeselect: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.deselect'->t[] #);
do (listWidget,theItem) -> XmListDeselectPos;
#)

-- GUIENVselectionHas: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.has'->t[] #);
do l: scanSelection
   (# 
   do (if current=theItem then
          true -> value;
          leave l;
      if);
   #);
#)

-- GUIENVscrollListScanSelection: descriptor --
assertOpen
(# location::(# do 'ScrollList.selection.scan'->t[] #);
   positionList: @integer;
   positionListCount: @integer;
   adrOfCurrent: @integer;
do (if ((private.listWidgetID,@@positionList,@@positionListCount) 
         -> XmListGetSelectedPos)=1 then
       (for i: positionListCount repeat
            positionList + (i - 1)*4 -> adrOfCurrent;
            adrOfCurrent -> TOS'%adrGetLong' -> current;
            inner scanSelection;
       for);
   if);
#)

-- GUIENVscrollListScan: descriptor --
assertOpen
(# location::(# do 'ScrollList.scan'->t[] #);
do (for i: numberOfItems repeat;
        i -> current;
        inner scan;
   for);
#)

-- GUIENVscrollListCreate: descriptor --
(#
do inner create;
#)

-- GUIENVscrollListOpen: descriptor --
(#
do (listButtonReleaseProcessor##, XButtonReleaseMask, listWidget) 
     -> appendEventProcessorForSubWidget;
   (listButtonPressProcessor##, XButtonPressMask, listWidget) 
     -> appendEventProcessorForSubWidget;
   (listKeyPressProcessor##, XKeyPressMask, listWidget) 
     -> appendEventProcessorForSubWidget;
   (defaultActionProcessor##, XmNdefaultActionCallback, listWidget)
     -> addCallbackProcessorForSubWidget;
   (selectProcessor##, XmNbrowseSelectionCallback, listWidget)
     -> addCallbackProcessorForSubWidget;
   (selectProcessor##, XmNmultipleSelectionCallback, listWidget)
     -> addCallbackProcessorForSubWidget;
   (XmNautomaticSelection, True) -> private.listWidget.setBooleanResource;
   inner open;
#)

-- GUIENVscrollListClose: descriptor --
(#
do inner close;
#)

-- GUIENVscrolllistonframechanged: descriptor --
(#
do inner onFrameChanged;
#)

-- GUIENVscrolllistondeactivate: descriptor --
(#
do inner onDeactivate;
#)

-- GUIENVscrolllistonrefresh: descriptor --
(#
do inner onRefresh;
#)

-- GUIENVscrolllistonmousedown: descriptor --
(#
do inner onMouseDown;
#)

-- GUIENVscrolllistonactivate: descriptor --
(#
do inner onActivate;
#)


-- GUIENVscrollListPrivate: descriptor --
(#
   listWidget: @interfaceObject;
   listWidgetID: @integer;
   
#)

-- GUIENVtextScrollListSet: descriptor --
assertOpen
(# location::(# do 'TextScrollList.set'->t[] #);
   mStr: @motifString;
   address: @integer;
do theText[] -> mStr.setText;
   mstr -> address;
   (this(scrollList).private.listWidgetID,@@address,1,theItem) -> XmListReplaceItemsPos;
   mStr.destroy;
#)

-- GUIENVtextScrollListGet: descriptor --
assertOpen
(# location::(# do 'TextScrollList.get'->t[] #);
   itemsList: @integer;
   mStr: @motifString;
   adrOfItem: @integer;
do (if theItem -> inRange then
       XmNitems -> private.listWidget.getPointerResource -> itemsList;
       itemsList + (theItem - 1)*4 -> adrOfItem;
       adrOfItem -> TOS'%adrGetLong' -> mStr;
       mStr.getText -> theText[];
   if);
#)

-- GUIENVtextScrollListSetTextStyle: descriptor --
assertOpen
(# location::(# do 'TextScrollList.setTextStyle'->t[] #);
do (XmNfontList,theStyle.createFontList) -> private.listWidget.setIntegerResource;
   
#)

-- GUIENVtextScrollListGetTextStyle: descriptor --
assertOpen
(# location::(# do 'TextScrollList.getTextStyle'->t[] #);
   fontList: @integer;
do &textStyle[] -> theStyle[];
   
   XmNfontList -> private.listWidget.getIntegerResource -> fontList;
   (if fontList <> 0 then
       fontList -> theStyle.initWithFontList;
   if);
#)

-- GUIENVtextScrollListCreate: descriptor --
(# args: @argList;
do inner create;
   (if widgetID=0 then
       (1,XmNscrollbarDisplayPolicy,XmAUTOMATIC) -> args.set;
       (2,XmNlistSizePolicy,XmCONSTANT) -> args.set;
       (father.widgetID,'textscrolllist',args[],2) -> XmCreateScrolledList -> listWidget -> widgetID;
       listWidget -> this(scrollList).private.listWidgetID;
      
       (XmNhighLightThickness,0) -> setIntegerResource;
       listWidget -> XtManageChild;
       (XtNwidth,50) -> setIntegerResource;
       (XmNlistMarginHeight,2) -> setIntegerResource;
       (XmNlistMarginWidth,2) -> setIntegerResource;
       
       listWidget -> XtParent -> widgetID;
   if);
#)

-- GUIENVtextScrollListOpen: descriptor --
(#
do inner open;
#)

-- GUIENVtextScrollListClose: descriptor --
(#
do inner close;
#)
