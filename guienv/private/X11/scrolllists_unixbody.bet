ORIGIN '../scrolllistsbody';

INCLUDE 'widget';
INCLUDE 'guienvattributes';
INCLUDE 'motifstuff';
INCLUDE 'guienv_unixprivate';
INCLUDE 'callbacks';

[[
-- scrollListLib: attributes --

selectMouseup: buttonEventSelector
  (# 
  do theEventHandler.onMouseUp;
  #);
listButtonReleaseProcessor: eventProcessor
  (# 
  do &selectMouseup[] -> es[];
  #);

selectMouseDown: buttonEventSelector
  (# 
  do  theEventHandler.onMouseDown;
  #);
listButtonPressProcessor: eventProcessor
  (# 
  do &selectMouseDown[] -> es[];
  #);

selectKeyDown: keyEventSelector
  (# 
  do  theEventHandler.onKeyDown;
  #);
listKeyPressProcessor: eventProcessor
  (# 
  do &selectKeyDown[] -> es[];
  #);

listItemOffset: (# exit 16 #);
listCallbackSelector: motifCallbackSelector
  (# item: @integer;
  do callDataAddress + listItemOffset -> TOS'%adrGetLong' -> item;
     inner;
  #);

selectDefaultAction: listCallbackSelector
  (#
  do (item, true) -> theEventHandler.onSelect;
  #);
defaultActionProcessor: callbackProcessor
  (# 
  do &selectDefaultAction[] -> cb[];
  #);

selectOnSelect: listCallbackSelector
  (# 
  do (item, false) -> theEventHandler.onSelect;
  #);
selectProcessor: callbackProcessor
  (# 
  do &selectOnSelect[] -> cb[];
  #);

motifScrollList: interfaceObject
  (#
  #);
listWidget:
  (# 
  enter private.listWidget.widgetID
  exit private.listWidget.widgetID
  #);
inRange: booleanValue
  (# index: @integer;
  enter index
  do (index > 0) and (index <= numberOfItems) -> value;
  #);
-- GUIENVscrollListnumberOfItems: descriptor --
assertOpen
(#
do XmNitemCount -> private.listWidget.getIntegerResource -> value;
#)

-- GUIENVscrollListInsert: descriptor --
assertOpen
(# mStr: @motifString;
do '' -> mStr.setText;
   (for numOfItems repeat
        (listWidget,mStr,beforeItem) -> XmListAddItem;
   for);
   mStr.destroy;
#)

-- GUIENVscrollListPrepend: descriptor --
assertOpen
(#
do (1,numOfItems) -> insert;
#)

-- GUIENVscrollListAppend: descriptor --
assertOpen
(#
do (0,numOfItems) -> insert;
#)

-- GUIENVscrollListDelete: descriptor --
assertOpen
(#
do (listWidget,numOfItems,firstItem) -> XmListDeleteItemsPos;
#)

-- GUIENVscrollListDeleteFirst: descriptor --
assertOpen
(#
do (1,numOfItems)  -> delete;
#)

-- GUIENVscrollListDeleteLast: descriptor --
assertOpen
(#
do (numberOfItems - numOfItems,numOfItems) -> delete;
#)

-- GUIENVscrollListSetItemHeight: descriptor --
(#
do (* Not Implemented !!! *)
   (* This is not possible in Motif *)
#)

-- GUIENVscrollListGetItemHeight: descriptor --
(# (* x, y, width, height: @integer;
    *    widget: @integer;
    *    ok: @integer;
    *    XmListPosToBounds: external
    *      (#
    *         widget, item, xP, yP, widthP, heightP: @integer;
    *         OK: @boolean;
    *      enter (widget, item, xP, yP, widthP, heightP)
    *      exit OK
    *      #);
    * do listWidget -> widget;
    *    (widget, 1, 0, 0, 0, @@height + 2) -> XmListPosToBounds -> OK;
    *    (if OK then
    *        height -> h;
    *    if);
    *)
   (* Not Implemented !!! *)
   
#)

-- GUIENVscrollListSetSingleSelection: descriptor --
assertOpen
(#
do (if isSingle//true then
       (XmNselectionPolicy,XmBROWSE_SELECT) -> private.listWidget.setIntegerResource;
    else
       (XmNselectionPolicy,XmMULTIPLE_SELECT) -> private.listWidget.setIntegerResource;
   if);
#)

-- GUIENVscrollListGetSingleSelection: descriptor --
assertOpen
(# policy: @char;
do XmNselectionPolicy -> private.listWidget.getCharResource -> policy;
   (if policy
    // XmBROWSE_SELECT then true -> isSingle
    // XmMULTIPLE_SELECT then false -> isSingle
   if);
#)

-- GUIENVItemGetItemRectangle: descriptor --
(#
do
#)

-- GUIENVselectionClear: descriptor --
assertOpen
(#
do listWidget -> XmListDeselectAllItems;
#)

-- GUIENVselectionscrollIntoView: descriptor --
assertOpen
(# firstSelectedItem: @integer;
do selection.first -> firstSelectedItem;
   (listWidget,firstSelectedItem) -> XmListSetPos;
#)

-- GUIENVselectionFirst: descriptor --
assertOpen
(# positionList: @integer;
   positionListCount: @integer;
   hasSelection: @boolean;
do (listWidget,@@positionList,@@positionListCount) -> XmListGetSelectedPos -> hasSelection;
   (if hasSelection//true then
       positionList -> TOS'%adrGetLong' -> value;
   if);
#)

-- GUIENVselectionLast: descriptor --
assertOpen
(# positionList: @integer;
   positionListCount: @integer;
   adrOfLast: @integer;
   hasSelection: @boolean;
do (listWidget,@@positionList,@@positionListCount) -> XmListGetSelectedPos -> hasSelection;
   (if hasSelection//true then
      positionList + (positionListCount - 1)*4 -> adrOfLast;
      adrOfLast  -> TOS'%adrGetLong' -> value;
   if);
#)

-- GUIENVselectionSelectItem: descriptor --
assertOpen
(#
do (if extend//false then
       clear;
   if);
   (listWidget,theItem,false) -> XmListSelectPos;
#)

-- GUIENVselectionDeselect: descriptor --
assertOpen
(#
do (listWidget,theItem) -> XmListDeselectPos;
#)

-- GUIENVselectionHas: descriptor --
assertOpen
(#
do l: scanSelection
   (# 
   do (if current//theItem then
          true -> value;
          leave l;
      if);
   #);
#)

-- GUIENVscrollListScanSelection: descriptor --
assertOpen
(# positionList: @integer;
   positionListCount: @integer;
   adrOfCurrent: @integer;
   hasSelection: @boolean;
do (listWidget,@@positionList,@@positionListCount) -> XmListGetSelectedPos -> hasSelection;
   (if hasSelection//true then
       (for i: positionListCount repeat
            positionList + (i - 1)*4 -> adrOfCurrent;
            adrOfCurrent -> TOS'%adrGetLong' -> current;
            inner scanSelection;
       for);
   if);
#)

-- GUIENVscrollListScan: descriptor --
assertOpen
(#
do (for i: numberOfItems repeat;
        i -> current;
        inner scan;
   for);
#)

-- GUIENVscrollListCreate: descriptor --
(#
do inner create;
#)

-- GUIENVscrollListOpen: descriptor --
(#
do (listButtonReleaseProcessor##, XButtonReleaseMask, listWidget) 
     -> appendEventProcessorForSubWidget;
   (listButtonPressProcessor##, XButtonPressMask, listWidget) 
     -> appendEventProcessorForSubWidget;
   (listKeyPressProcessor##, XKeyPressMask, listWidget) 
     -> appendEventProcessorForSubWidget;
   (defaultActionProcessor##, XmNdefaultActionCallback, listWidget)
     -> addCallbackProcessorForSubWidget;
   (selectProcessor##, XmNbrowseSelectionCallback, listWidget)
     -> addCallbackProcessorForSubWidget;
   (selectProcessor##, XmNmultipleSelectionCallback, listWidget)
     -> addCallbackProcessorForSubWidget;
   (XmNautomaticSelection, True) -> private.listWidget.setIntegerResource;
   inner open;
#)

-- GUIENVscrollListClose: descriptor --
(#
do inner close;
#)

-- GUIENVscrolllistonframechanged: descriptor --
(#
do inner onFrameChanged;
#)

-- GUIENVscrolllistondeactivate: descriptor --
(#
do inner onDeactivate;
#)

-- GUIENVscrolllistonrefresh: descriptor --
(#
do inner onRefresh;
#)

-- GUIENVscrolllistonmousedown: descriptor --
(#
do inner onMouseDown;
#)

-- GUIENVscrolllistonactivate: descriptor --
(#
do inner onActivate;
#)


-- GUIENVscrollListPrivate: descriptor --
(#
   listWidget: @interfaceObject;
   
#)

-- GUIENVtextScrollListSet: descriptor --
assertOpen
(# mStr: @motifString;
   address: @integer;
do theText[] -> mStr.setText;
   mstr -> address;
   (listWidget,@@address,1,theItem) -> XmListReplaceItemsPos;
   mStr.destroy;
#)

-- GUIENVtextScrollListGet: descriptor --
assertOpen
(# itemsList: @integer;
   mStr: @motifString;
   adrOfItem: @integer;
do (if theItem -> inRange//true then
       XmNitems -> private.listWidget.getPointerResource -> itemsList;
       itemsList + (theItem - 1)*4 -> adrOfItem;
       adrOfItem -> TOS'%adrGetLong' -> mStr;
       mStr.getText -> theText[];
   if);
#)

-- GUIENVtextScrollListSetTextStyle: descriptor --
assertOpen
(#
do (XmNfontList,theStyle.createFontList) -> setIntegerResource;
#)

-- GUIENVtextScrollListGetTextStyle: descriptor --
assertOpen
(# 
do &textStyle[] -> theStyle[];
   XmNfontList -> getIntegerResource -> theStyle.initWithFontList;
#)

-- GUIENVtextScrollListCreate: descriptor --

(# args: @argList;
do inner create;
   (if widgetID//0 then
       (1,XmNscrollbarDisplayPolicy,XmAUTOMATIC) -> args.set;
       (2,XmNlistSizePolicy,XmCONSTANT) -> args.set;
       (father.widgetID,'textscrolllist',args[],2) -> XmCreateScrolledList -> listWidget -> widgetID;
      
       (XmNhighLightThickness,0) -> setIntegerResource;
       listWidget -> XtManageChild;
       (XtNwidth,50) -> setIntegerResource;
       (XmNlistMarginHeight,2) -> setIntegerResource;
       (XmNlistMarginWidth,2) -> setIntegerResource;
       
       listWidget -> XtParent -> widgetID;
   if);
#)

-- GUIENVtextScrollListOpen: descriptor --
(#
do inner open;
#)

-- GUIENVtextScrollListClose: descriptor --
(#
do inner close;
#)
-- GUIENVscrollListputLayout: doPart --
do

-- GUIENVscrollListgetLayout: doPart --
do

-- GUIENVtextscrollListputLayout: doPart --
do

-- GUIENVtextscrollListgetLayout: doPart --
do


--]]
