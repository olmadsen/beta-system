ORIGIN 'guienv_unixprivate';
LIB_ITEM 'guienv';
BODY 'errorbody';


-- guienvLib: attributes --

errorHandler: 
  (* Errors arising from the X Toolkit itself can be caught by instatiating
   * and installing an instance of ErrorHandler before any widget is 
   * initialized.
   *)
  (# xterror:< exception
       (# message: ^text;
       enter message[]
       do '\nX Toolkit Error: ' -> msg.puttext;
          message[] -> msg.putline;
          msg[]->screen.puttext;
          INNER;
       #);
     xliberror:< exception
       (# message: ^text;
       enter message[]
       do 'Xlib Error: ' -> msg.puttext;
          message[] -> msg.putline;
          msg[]->screen.puttext;
          INNER;
       #);
     xlibfatalerror:< exception
       (# message: ^text;
          BetaExit: external
            (# exitCode: @integer;
            enter exitCode
            #);
       enter message[]
       do 'Xlib Error: ' -> msg.puttext;
          message[] -> msg.putline;
          msg[]->screen.puttext;
          32 -> BetaExit;
          INNER;
       #);
     install: 
       (#
       <<SLOT ErrorHandlerInstall: doPart>>
       #);
  #);
