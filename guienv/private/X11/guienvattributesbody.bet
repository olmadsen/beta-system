ORIGIN 'guienvattributes';
INCLUDE 'guienv_unixprivate';
INCLUDE '~beta/sysutils/v1.5/objinterface';
[[


-- GUIENVidleProcedureAdd: descriptor --
(# 
do (this(guienv).private.appcon,private.callWorkProc##,0) 
     -> XtAppAddWorkProc -> private.id;
#)

-- GUIENVidleProcedureDelete: descriptor --
(# 
do private.id -> XtRemoveWorkProc;
#)

-- idleProcedurePrivate: descriptor --
(# id: @integer;
   callWorkProc: external
     (# client_data: @integer;
        result: @boolean;
     enter client_data
     do cExternalEntry;
        (if XsystemEnvPresent then
            this(idleProcedure)[] -> XsystemEnvHandleCallback;
         else
            this(idleProcedure) -> result;
        if);
     exit result
     #);
#)

-- GUIENVwhatButton: descriptor --
(# button1,button2,button3: @boolean;
do 
   (((mask,button1Mask) -> tos'%and') = button1Mask) -> button1;
   (((mask,button2Mask) -> tos'%and') = button2Mask) -> button2;
   (((mask,button3Mask) -> tos'%and') = button3Mask) -> button3;
   (if true
    //button1 then 1 -> button;
    //button2 then 2 -> button;
    //button3 then 3 -> button;
    else
       0 -> button
   if);
#)

-- GUIENVmouseGetMouseInfo: descriptor --
(# x,y,x_root,y_root: @integer;
   rootID,childID: @integer;
   mask, displayID: @integer;
   onWindowsDisplay: @integer;
do &mouseInfo[] -> info[];
   display -> displayID;
   (displayID,windowID,
   @@rootID,@@childID,
   @@x_root,@@y_root,
   @@x,@@y,
   @@mask) -> XQueryPointer -> onWindowsDisplay;
   (x,y) -> info.local;
   (x_root,y_root) -> info.global;
   mask -> whatButton -> info.button;
   (((mask,shiftMask) -> tos'%and') = shiftMask) -> info.shiftKey;
   (((mask,controlMask) -> tos'%and') = controlMask) -> info.controlKey;
   (((mask,mod1Mask) -> tos'%and') = mod1Mask) -> info.metaKey;
   (((mask,lockMask) -> tos'%and') = lockMask) -> info.capsLock;
#)
-- GUIENVgetDisplay: descriptor --
(# 
do private.display -> value;
#)

-- GUIENVblackPixelValue: descriptor --
(# 
do private.theDeviceInfo.blackPixelValue -> value;
#)

-- GUIENVwhitePixelValue: descriptor --
(# 
do private.theDeviceInfo.whitePixelValue -> value;
#)


-- GUIENVinterfaceObjectAssertOpen: descriptor --
(# 
do (if private.isOpen then
       inner assertOpen;
    else
       (if private.beingClosed then
           (* Just ignore request *)
        else
           location -> notOpenedError;
       if);
   if);
#)


-- GUIENVinterfaceObjectCreateSimple: descriptor --
(# args: @arglist;
do (1,XtNwidth,100) -> args.set;
   (2,XtNheight,100) -> args.set;
   (3,XtNborderWidth,0) -> args.set;
   (class,father,this(interfaceObject)[] -> getPatternName,args[],3) -> createManagedArgs;
#)

-- GUIENVwindowitemHit: descriptor --
(# 
do (if visible then
       p -> private.hit -> value;
    else
       false -> value
   if);
#)

-- GUIENVwindowitemSetHit: descriptor --
(# 
do hit## -> private.hit##;
#)

-- GUIENVtextStyleFontID: descriptor --
(#
do 
   (if private.fontStruct<>0 then
       (private.fontStruct + 4) -> TOS'%adrGetLong' -> value;
   if);
#)

-- GUIENVtextStyleFontStruct: descriptor --
(#
do private.fontStruct -> value;
#)

-- GUIENVtextStyleSetFontStruct: descriptor --
(# 
do value -> private.fontStruct;
#)

-- GUIENVtextStyleGetFontInfo: descriptor --
(# getfontinfo: external
     (# fontStruct,ascent,descent: @integer;
     enter (fontStruct,ascent,descent)
     #);
do (if private.fontStruct<>0 then
       &fontInfo[] -> info[];
       (private.fontStruct,@@info.ascent,@@info.descent) -> getfontinfo;
   if);
#)

-- GUIENVtextStyleAllocate: descriptor --
(# fontSpec:
     (# spec: ^text;
        bold,italic: @boolean;
     do &text[] -> spec[];
        '-*-' -> spec.putText;
        (if not private.name.empty then
            private.name[] -> spec.putText;
         else
            '*' -> spec.put;
        if);
        '-' -> spec.put;
        (((private.face,textFaces.bold) -> TOS'%and') = textFaces.bold) -> bold;
        (if bold then
            'bold' -> spec.putText;
         else
            'medium' -> spec.putText;
        if);
        '-' -> spec.put;
        (((private.face,textFaces.italic) -> TOS'%and') = textFaces.italic) -> italic;
        (if italic then
            'i' -> spec.put;
         else
            'r' -> spec.put;
        if);
        '-*-*-*-' -> spec.putText;
        (if private.size=0 then
            '*' -> spec.put;
         else
            (private.size * 10) -> spec.putInt;
        if);
        '-*' -> spec.putText;
     exit spec[]
     #);
   spec: ^text;
do (if fontStruct=0 then
       fontSpec -> spec[];
       (display,spec) -> XLoadQueryFont -> private.fontStruct;
       (if private.fontStruct=0 then
           trace
           (# 
           do 'font ' -> putText;
              spec[] -> putText;
              ' is not avaible' -> putLine;
           #);
       if);
   if);
#)

-- GUIENVtextStyleDispose: descriptor --
(#
do (if private.fontStruct<>0 then
       (display,private.fontStruct) -> XFreeFont;
       0 -> private.fontStruct;
   if);
#)

-- GUIENVTextStyleCreateFontList: descriptor --
(#
do allocate;
   (fontStruct,'Michael') -> fontList.add;
   trace
   (# 
   do 'fontlist = ' -> putText;
      fontList -> putInt;
      newLine;
   #);
#)

-- GUIENVTextStyleinitWithFontList: descriptor --
(#
do l: fontList.scan
     (# 
     do fontStruct -> setFontStruct;
        leave l;
     #);
#)
--]]
