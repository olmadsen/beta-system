ORIGIN '../stddialogsbody'
[[
-- include 'widget'
-- Include 'motifstuff'
-- Include 'guienv_unixprivate'
-- Include 'guienvattributes'

-- GUIenvLib: attributes --

CenterOnScreen:
  (# shell: @integer;
     w, h: @shortint;
     args: @arglist;
  enter shell
  do (* Shell is assumed to be realized by now, but NOT mapped *)
     (1, XtNwidth, @@w)  -> args.set;
     (2, XtNheight, @@h)  -> args.set;
     (shell, args[], 2) -> XtGetValues;
     (THIS(guienv).private.screenWidth-w)  div 2 -> w;
     (THIS(guienv).private.screenHeight-h) div 2 -> h;
     (1, XtNx, w)  -> args.set;
     (2, XtNy, h)  -> args.set;
     (shell, args[], 2) -> XtSetValues;
     shell -> XtMapWidget;
  #);

XtAppCreateShell: external
  (# name: [1] @char;
     class: [1] @char;
     widgetClass: @integer;
     display: @integer;
     args: ^argList;
     nArgs: @integer;
     newWidget: @integer;
  enter (name,class,widgetclass,display,args[],nArgs)
  do callC
  exit newWidget
  #);

createDialogShell:
  (# owner: ^window;
     dialogShell: @integer;
  enter owner[]
  do (# parentID: @integer;
        args: @argList;
     do (if owner[]<>none then
            owner.widgetID -> parentID;
            (parentID,'dialogshell',args[],0) -> XmCreateDialogShell -> dialogShell;
         else
            (1, XtNmappedWhenManaged, 0) -> args.set;
            (this(GUIenv).private.appName,
            this(GUIenv).private.appClass,
            TopLevelShellWidgetClass,
            this(GUIenv).private.display,
            args[], 1) -> XtAppCreateShell -> dialogShell;
        if);
     #);
  exit dialogShell
  #);
 XtAddCallback: external
  (# widget: @integer;
     name: @integer;
     callbackP: @integer;
     clientData: @integer;
  enter (widget, name, callbackP, clientData)
  #);

-- noteUserBody: doPart --
do inner;
   XmDIALOG_MESSAGE -> messageDialogPrivate.type;

-- alertUserBody: doPart --
do inner;
   XmDIALOG_WARNING -> messageDialogPrivate.type;
  

-- filecreationdialogbody: doPart --
do inner;

-- fileSelectionDialogBody: doPart --
do inner;

-- GUIENVdialog: doPart --
do inner;
   private.box -> XtManageChild;
   private.dialogShell -> XtRealizeWidget;
   (if private.center then private.dialogShell -> CenterOnScreen; if);
   l: (if not private.done then
          (this(guienv).private.appCon,XtIMAll) -> XtAppProcessEvent;
          restart l;
      if);
   private.dialogShell -> XtDestroyWidget;
   private.activateAddress -> freeCBF;
   (if private.cancelAddress<>0 then private.cancelAddress -> freeCBF if);

-- GUIENVfileDialog: doPart --
do inner;
   (# helpButtonId: @integer;
      defaultButton: @integer;
      textField: @integer;
      txt: @integer;
      cancelButton: @integer;
      mtitle: @motifString;
      defaultButtonActivate: external
        (# w,client_data,call_data: @integer;
        enter (w,client_data,call_data)
        do cExternalEntry;
           textField->txt;
           (1,XmNvalue,@@stringAddress) -> args.set;
           (txt,args[],1) -> XtGetValues;
           stringAddress -> cStringToCharRep -> t;
           t.copy -> fileName[];
           stringAddress-> XtFree;
           private.box -> XtUnmanageChild;
           true -> private.done;
        #);
      cancelButtonActivate: external
        (# w,client_data,call_data: @integer;
        enter (w,client_data,call_data)
        do cExternalEntry;
           NONE -> fileName[];
           private.box  -> XtUnmanageChild;
           true -> private.done;
        #);
      args: @argList;
      nargs: @integer;
      str1, str2, str3: @motifString;
      t: @text;
      stringAddress: @integer;
   do owner[] -> createDialogShell -> private.dialogShell;
      (if owner[]=None then
          true -> private.center;
       else
          nargs + 1 -> nargs;
          (nargs, XmNdialogStyle, XmDIALOG_FULL_APPLICATION_MODAL) -> args.set;
      if);
      (if title[] = None then
          'File' -> title[];
      if);
      title[] -> mtitle.setText;
      nargs + 1 -> nargs;
      (nargs, XmNdialogTitle, mtitle) -> args.set;
      (if filter[]<>none then
          filter[] -> str1.setText;
          nargs + 1 -> nargs;
          (nargs, XmNpattern, str1) -> args.set;
      if);
      (if label[]<>none then
          label[] -> str2.setText;
          nargs + 1 -> nargs;
          (nargs, XmNselectionLabelString, str2) -> args.set;
      if);
      (if path[]<>none then
          path[] -> str3.setText;
          nargs + 1 -> nargs;
          (nargs, XmNdirectory, str3) -> args.set;
      if);
      (private.dialogShell, 'FileSelectionDialog',args[], nargs) -> XmCreateFileSelectionBox
        -> private.box;
      str1.destroy;
      str2.destroy;
      str3.destroy;
      mtitle.destroy;
      (private.box, XmDIALOG_DEFAULT_BUTTON) -> XmFileSelectionBoxGetChild -> defaultButton;
      (private.box, XmDIALOG_TEXT) -> XmFileSelectionBoxGetChild -> textField;
      ( private.box,XmDIALOG_CANCEL_BUTTON) -> XmFileSelectionBoxGetChild -> cancelButton;
      defaultButtonActivate## -> makeCBF -> private.activateAddress;
      cancelButtonActivate## -> makeCBF -> private.cancelAddress;
      (defaultButton,XmNActivateCallback,private.activateAddress,0) -> XtAddCallback;
      (cancelButton,XmNActivateCallback,private.cancelAddress,0) -> XtAddCallback;
      (private.box,XmDIALOG_HELP_BUTTON) -> XmFileSelectionBoxGetChild -> helpButtonID;
      helpButtonID -> XtUnmanageChild;
   #)
   

-- GUIENVmessageDialog: doPart --
do inner;
   (# mstr: @motifString;
      mtitle: @motifString;
      args: @arglist;
      noteButtonActivate: external
        (# w,client_data,call_data: @integer;
        enter (w,client_data,call_data)
        do cExternalEntry;
           private.box -> XtUnmanageChild;
           true -> private.done;
        #);
      okButtonID: @integer;
      cancelButtonID: @integer;
      helpButtonID: @integer;
   do owner[] -> createDialogShell -> private.dialogShell;
      message[] -> mstr.setText;
      (if title[] = None then
          'Message' -> title[];
      if);
      title[] -> mtitle.setText;
      (if owner[]<>none then
          (1, XmNmessageString, mstr) -> args.set;
          (2, XmNdialogTitle, mtitle) -> args.set;
          (3, XmNdialogStyle, XmDIALOG_FULL_APPLICATION_MODAL) -> args.set;
          (4, XmNdialogType, messageDialogPrivate.type) -> args.set;
          (private.dialogShell,'messagebox',args[],4) -> XmCreateMessageBox -> private.box;
       else
          true -> private.center;
          (1, XmNmessageString, mstr) -> args.set;
          (2, XmNdialogTitle, mtitle) -> args.set;
          (3, XmNdefaultPosition, 0) -> args.set;
          (4, XmNdialogType, messageDialogPrivate.type) -> args.set;
          (private.dialogShell,'messagebox',args[],4) -> XmCreateMessageBox -> private.box;
      if);
      mstr.destroy;
      mtitle.destroy;
      (private.box,XmDIALOG_CANCEL_BUTTON) -> XmMessageBoxGetChild -> cancelButtonID;
      (private.box,XmDIALOG_OK_BUTTON) -> XmMessageBoxGetChild -> okButtonID;
      (private.box,XmDIALOG_HELP_BUTTON) -> XmMessageBoxGetChild -> helpButtonID;
      noteButtonActivate## -> makeCBF -> private.activateAddress;
      (okButtonID,XmNActivateCallback,private.activateAddress,0) -> XtAddCallback;
      (*(cancelButtonID,XmNActivateCallback, private.activateAddress,0) -> XtAddCallback;*)
      cancelButtonID -> XtUnmanageChild;
      helpButtonID -> XtUnmanageChild;
   #);


-- GUIENVdialogPrivate: descriptor --
(# dialogShell, box: @integer;
   center: @boolean;
   done: @boolean;
   activateAddress, cancelAddress: @integer;
   
#)

-- GUIENVfileDialogPrivate: descriptor --
(# 
#)

-- GUIENVmessageDialogPrivate: descriptor --
(# type: @integer;
   
   
#)


--]]
