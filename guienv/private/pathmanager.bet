ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE 'datastructures/association';

INCLUDE '~beta/sysutils/v1.6/envstring';
INCLUDE '~beta/sysutils/v1.6/pathhandler';
INCLUDE '~beta/basiclib/v1.6/file';
INCLUDE '~beta/basiclib/v1.6/directory';


-- lib: attributes --

pathManager:
  (# init:<
       (#
       do dict.init;
          INNER;
       #);
     add:
       (# path: ^text;
       enter path[]
       do (# theDirectory: @directory;
          do path[] -> convertPath -> path[];
             (if path[] -> exists then
                 (if path[] -> isDirectory then
                     path[] -> theDirectory.name;
                     theDirectory.scanEntries
                     (# name: ^text;
                        fullpath: ^text;
                     do found.path -> name[];
                        path.copy -> fullPath[];
                        directoryChar -> fullPath.put;
                        name[] -> fullPath.putText;
                        (if fullpath[] -> isFile then
                            (name[], fullpath[]) -> dict.insert;
                        if);
                     #);
                 if);
             if);
          #);
       #);
     lookup:
       (# name: ^text;
          path: ^text;
       enter name[]
       do (# 
          do name[] -> dict.lookup -> path[];
          #);
       exit path[]
       #);
     
     dict: @dictionary (# element:: text #);
  #);

convertPath:
  (# path: ^text;
     convertedPath: ^text;
  enter path[]
  do (# 
     do path.copy -> convertedPath[];
     #);
  exit convertedPath[]
  #);

exists: booleanValue
  (# name: ^text;
  enter name[]
  do l: (# e: @diskEntry;
        do name[] -> e.path;
           e.exists
           (# error:: (# do false -> value; leave l;#) #) -> value;
        #);
  #);
isDirectory: booleanValue
  (# name: ^text;
  enter name[]
  do l: (# e: @diskEntry;
        do name[] -> e.path;
           e.isDirectory
           (# error:: (# do false -> value; leave l;#) #) -> value;
        #);
  #);
isFile: booleanValue
  (# name: ^text;
  enter name[]
  do l: (# e: @diskEntry;
        do name[] -> e.path;
           e.isFile
           (# error:: (# do false -> value; leave l;#) #) -> value;
        #);
  #);
