ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE 'datastructures/association';

INCLUDE '~beta/sysutils/v1.6/envstring';
INCLUDE '~beta/sysutils/v1.6/pathhandler';
INCLUDE '~beta/basiclib/v1.6/file';
INCLUDE '~beta/basiclib/v1.6/directory';


-- lib: attributes --

pathManager:
  (# init:<
       (#
       do dict.init;
          INNER;
       #);
     add:
       (# path: ^text;
       enter path[]
       do 
          (# theDirectory: @directory;
		  	 exists: theDirectory.entry.exists
			 	(# error:: (# do true -> continue #);
				#);
			 isDirectory: theDirectory.entry.isDirectory
			 	(# error:: (# do true -> continue #);
				#)
          do path[] -> theDirectory.name;
             (if exists then
                 (if isDirectory then                     
                     theDirectory.scanEntries
                     (# name: ^text;
                        fullpath: ^text;
						entry: @diskEntry;
						isFile: @entry.isFile
							(# error:: (# do true -> continue #);
							#);
                     do found.path -> name[];
                        path.copy -> fullPath[];
                        (if (fullPath.length -> fullPath.inxGet) <> directoryChar then
                            directoryChar -> fullPath.put;
                        if);
                        name[] -> fullpath.append;
						fullpath[] -> entry.path;
                        (if isFile then
                            (name[], fullpath[]) -> dict.insert;
                        if);
                     #);
                 if);
             if);
          #);
       #);
     lookup:
       (# name: ^text;
          path: ^text;
       enter name[]
       do (# 
          do name[] -> dict.lookup -> path[];
          #);
       exit path[]
       #);
     
     dict: @dictionary (# element:: text #);
  #);

exists: booleanValue
  (# name: ^text;
  enter name[]
  do l: (# e: @diskEntry;
        do name[] -> e.path;
           e.exists
           (# error:: (# do false -> value; leave l;#) #) -> value;
        #);
  #);
isDirectory: booleanValue
  (# name: ^text;
  enter name[]
  do l: (# e: @diskEntry;
        do name[] -> e.path;
           e.isDirectory
           (# error:: (# do false -> value; leave l;#) #) -> value;
        #);
  #);
isFile: booleanValue
  (# name: ^text;
  enter name[]
  do l: (# e: @diskEntry;
        do name[] -> e.path;
           e.isFile
           (# error:: (# do false -> value; leave l;#) #) -> value;
        #);
  #);
