ORIGIN '../graphicscanvas';

MDBODY nti 'winnt/graphicscanvas_nti'
       ppcmac 'macintosh/graphicscanvas_mac'
       default 'X11/graphicscanvas_X11';
 


-- GraphicsCanvasAdd: doPart --
do child[] -> children.append;

-- GraphicsCanvasRemove: doPart --
do child[] -> children.at -> children.delete;

-- ItemDrawOn: doPart --
do theSurface.push;
   (x, y) -> theSurface.translate;
   INNER;
   theSurface.pop;
   

-- RectDrawOn: doPart --
do (if fill[] <> NONE then
      fill[] -> theSurface.pen;
      (width, height) -> theSurface.fillRect;
   if);
   (if stroke[] <> NONE then
      stroke[] -> theSurface.pen;
      (width, height) -> theSurface.drawRect;
   if);
   
-- OvalDrawOn: doPart --
do (if fill[] <> NONE then
      fill[] -> theSurface.pen;
      (width, height) -> theSurface.fillOval;
   if);
   (if stroke[] <> NONE then
      stroke[] -> theSurface.pen;
      (width, height) -> theSurface.drawOval;
   if);

-- PolygonDrawOn: doPart --
do (if fill[] <> NONE then
      fill[] -> theSurface.pen;
      points[] -> theSurface.fillPolygon;
   if);
   (if stroke[] <> NONE then
      stroke[] -> theSurface.pen;
      points[] -> theSurface.drawPolygon;
   if);
   

-- TextItemDrawOn: doPart --
do style[] -> theSurface.style;
   (if stroke[] <> NONE then
       stroke[] -> theSurface.pen;
       (if content[] <> NONE then
           content[] -> theSurface.drawText;
       if);
   if);
   
-- PixmapItemDrawOn: doPart --
do (if content[] <> NONE then
      content[] -> theSurface.drawPixmap;
   if);

-- CompositeAdd: doPart --
do child[] -> children.append;

-- CompoisteRemove: doPart --
do child[] -> children.at -> children.delete;

-- CompositeDrawOn: doPart --
do children.scan
     (#
     do theSurface[] -> current.drawOn;
     #);

-- GraphicsCanvasOnMouseDown: doPart --
do localPosition -> select -> target[];
   (if target[] <> NONE then
       localPosition -> target.onMouseDown;
   if);
   
-- GraphicsCanvasOnMouseUp: doPart --
do (if target[] <> NONE then
       localPosition -> target.onMouseUp;
       NONE -> target[];
   if);
