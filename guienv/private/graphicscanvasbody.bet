ORIGIN '../graphicscanvas';

MDBODY nti 'winnt/graphicscanvas_nti'
       ppcmac 'macintosh/graphicscanvas_mac'
       default 'X11/graphicscanvas_X11';

-- GUIenvLib: attributes --


getShared: objectPool.get
  (# type:: PixmapSurface;
     
     init::
       (# screen: @Rectangle;
          image: ^Pixmap;
       do &Pixmap[] -> image[];
          screenRectangle -> screen;
          screen.size -> image.init;
          image[] -> obj.init;
       #);
  #);

-- LayerAdd: doPart --
do child[] -> children.append;
   
-- LayerRemove: doPart --
do child[] -> children.at -> children.delete;
   
-- LayerDrawOn: doPart --
do children.scan
   (#
   do theSurface[] -> current.drawOn;
   #);
   

-- GraphicsCanvasAdd: doPart --
do child[] -> children.append;

-- GraphicsCanvasRemove: doPart --
do child[] -> children.at -> children.delete;

-- ItemDrawOn: doPart --
do theSurface.push;
   (x, y) -> theSurface.translate;
   INNER;
   theSurface.pop;
   
-- LineDrawOn: doPart --
do (if stroke[] <> NONE then
       stroke[] -> theSurface.pen;
   if);
   strokewidth -> theSurface.width;
   (start[], end[]) -> theSurface.drawLine;

-- RectDrawOn: doPart --
do (if fill[] <> NONE then
      fill[] -> theSurface.pen;
      (width, height) -> theSurface.fillRect;
   if);
   (if stroke[] <> NONE then
      stroke[] -> theSurface.pen;
      (width, height) -> theSurface.drawRect;
   if);
   
-- OvalDrawOn: doPart --
do strokewidth -> theSurface.width;
   (if fill[] <> NONE then
      fill[] -> theSurface.pen;
      (width, height) -> theSurface.fillOval;
   if);
   (if stroke[] <> NONE then
       stroke[] -> theSurface.pen;
       (width, height) -> theSurface.drawOval;
   if);

-- PolygonDrawOn: doPart --
do (if fill[] <> NONE then
      fill[] -> theSurface.pen;
      points[] -> theSurface.fillPolygon;
   if);
   (if stroke[] <> NONE then
      stroke[] -> theSurface.pen;
      points[] -> theSurface.drawPolygon;
   if);
   

-- TextItemDrawOn: doPart --
do style[] -> theSurface.style;
   (if stroke[] <> NONE then
       stroke[] -> theSurface.pen;
       (if content[] <> NONE then
           content[] -> theSurface.drawText;
       if);
   if);
   
-- PixmapItemDrawOn: doPart --
do 'drawon:' -> putline;
   (if content[] <> NONE then
       'calling' -> putline;
      content[] -> theSurface.drawPixmap;
   if);

-- CompositeAdd: doPart --
do child[] -> children.append;

-- CompoisteRemove: doPart --
do child[] -> children.at -> children.delete;
   
-- CompositeClear: doPart --
do children.clear;
   
-- CompositeDrawOn: doPart --
do children.scan
     (#
     do theSurface[] -> current.drawOn;
     #);

-- GraphicsCanvasOnMouseDown: doPart --
do localPosition -> select -> current[];
   (if current[] <> NONE then
       localPosition -> current.onMouseDown;
   if);
   
-- GraphicsCanvasOnMouseUp: doPart --
do (if current[] <> NONE then
       localPosition -> current.onMouseUp;
       NONE -> current[];
   if);
-- GraphicsCanvasAddLayer: doPart --
do theLayer[] -> layers.append;
   
-- GraphicsCanvasRemoveLayer: doPart --
do theLayer[] -> layers.at -> layers.delete;
