ORIGIN 'linkedlist';

-- linkedListLib: attributes --
link:
  (# next: ^link;
     elm: ^element;
  #);

-- linkedListAppend: descriptor --
(# aLink: ^link;
do &link[] -> aLink[];
   e[] -> aLink.elm[];
   (if private.last[]//none then
       alink[] -> private.first[] -> private.last[];
    else
       aLink[] -> private.last.next[];
       aLink[] -> private.last[];
   if);
#)

-- linkedListPrepend: descriptor --
(# aLink: ^link;
do &link[] -> aLink[];
   e[] -> aLink.elm[];
   (if private.first[]//none then
       alink[] -> private.first[] -> private.last[];
    else
       private.first[] -> aLink.next[];
       aLink[] -> private.first[];
   if);
#)

-- linkedListDelete: descriptor --
(# prev,current: ^link;
do private.first[] -> current[];
   search: (if current[]<>None//true then
               (if current.elm[]//e[] then
                   none -> current.elm[];
                   (if current[]//private.first[] then
                       (if private.first[]//private.last[] then
                           none -> private.first[] -> private.last[];
                        else
                           current.next[] -> private.first[];
                       if);
                    else
                       current.next[] -> prev.next[];
                       (if current[]//private.last[] then
                           prev[] -> private.last[];
                       if);
                   if);
                   leave search;
               if);
               current[] -> prev[];
               current.next[] -> current[];
               restart search;
           if)
#)

-- linkedListMember: descriptor --
(# 
do false -> value;
   search: scan
     (# 
     do (if e[]//current[] then
            true -> value;
            leave search;
        if);
     #);
#)

-- linkedListScan: descriptor --
(# current: ^link;
do private.first[] -> current[];
   search: (if current[]<>None//true then
               current.elm[] -> this(scan).current[];
               inner scan;
               current.next[] -> current[];
               restart search;
           if);
#)

-- linkedListPrivate: descriptor --
(# first,last: ^link;
#)
