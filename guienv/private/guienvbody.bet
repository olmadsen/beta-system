ORIGIN '../guienv';
INCLUDE '~beta/basiclib/numberio';
INCLUDE '~beta/sysutils/pathhandler';
LIB_ITEM 'guienv';
MDBODY ppcmac 'macintosh/guienv_macbody'
       nti 'winnt/guienv_ntibody'
       default 'X11/guienv_unixbody';

--- BifrostPrivate: descriptor ---
(# 
   (* Device independent bifrost attributes *)
   PH: @pathhandler;
   halfW: @Real
     (* half the linewidth used in Shape.StrokeShape.  Placed here for
      * efficiency to avoid passig it as parameter to every Segment involved
      *);
   putWarn: 
     (# 
     do (if displaywarnings then
            (if WarnStream[]=NONE then
                screen[] -> WarnStream[];
            if);
            INNER;
        if);
     #);
   (* Static object to handle warnings. Yields much
    * smaller code than if using dynamic items.
    * Currently only used for Bifrost, but could be
    * used for all guienv warnings. Would give a uniform
    * treatment of warnings:
    *   warnings are put to WarnStreem (which used may specify), 
    *   iff displayWarnings is true.
    *)
   putWarnText: @putwarn
     (# t: ^text;
     enter t[]
     do t[] -> WarnStream.puttext;
     #);
   putWarnLine: @putwarn
     (# t: ^text;
     enter t[]
     do (if t[]=none then
            WarnStream.newline;
         else
            t[] -> WarnStream.putline;
        if);
     #);
   putWarnInt: @putwarn
     (# i: @integer
     enter i
     do i -> WarnStream.putint;
     #);
   putWarnHex: @putwarn
     (# i: @integer
     enter i
     do i -> WarnStream.puthex;
     #);
   putWarnIntWidth: @putwarn
     (# i,w: @integer
     enter (i, w)
     do i -> WarnStream.putint(# format::<(#do w->width#)#);
     #);
   putWarnReal: @putwarn
     (# i: @real
     enter i
     do i -> WarnStream.putreal;
     #);
   putWarnIntWidthLine: @putwarn
     (# i,w: @integer
     enter (i, w)
     do i -> WarnStream.putint(# format::<(#do w->width#)#);
        WarnStream.newline;
     #);
   putWarnIntLine: @putwarn
     (# i: @integer
     enter i
     do i -> WarnStream.putint; WarnStream.newline;
     #);
   putWarnHexLine: @putwarn
     (# i: @integer
     enter i
     do i -> WarnStream.puthex; WarnStream.newline;
     #);
   putWarnRealLine: @putwarn
     (# i: @real
     enter i
     do i -> WarnStream.putreal; WarnStream.newline;
     #);
   
   
#)

--- GUIENVtextStyleWidthOfTextCommon: dopart ---
do (# oneLineWidthOfText: integerValue
        (# str: ^text
        enter str[]
        <<SLOT GUIENVtextStyleStringWidth: doPart>>
        #);
   do str.reset;
      loop:
        (if not str.eos then
          (value,str.getline->oneLineWidthOfText)->max->value;
          restart loop
      if)
   #)
   
