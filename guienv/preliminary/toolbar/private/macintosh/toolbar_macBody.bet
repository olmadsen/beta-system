ORIGIN '../toolbarBody';

-- toolBarButtonLib: attributes --

-- GUIEnvTbButtonSetToolTip: doPart --
do theTip[] -> private.theTip[];
   
-- GUIEnvTbButtonGetToolTip: doPart --
do private.theTip[] -> theTip[];

-- GUIEnvToolBarButtonCreate: doPart --
do inner;
   
-- GUIEnvToolBarButtonOpen: doPart --
do (24,22) -> size;
   (0,0) -> position;
   false -> showLabel;
   true -> border.visible;
   borderStyles.shadowOut -> border.style;
   inner;
   
-- GUIEnvToolBarButtonOnMouseDown: doPart --
do borderStyles.shadowIn -> border.style;
   true -> update;
   trackMouse
   (# mouseMove::
        (# r: @rectangle; 
        do ((0,0),size) -> r;
           (if (curPt -> r.containsPoint) then
               'curPt inside frame' -> screen.putline;
               
               (if border.style=borderStyles.shadowOut then
                   borderStyles.shadowIn -> border.style;
                   true -> update;
               if);
            else
               (if border.style=borderStyles.shadowIn then
                   borderStyles.shadowOut -> border.style;
                   true -> update;
               if);
           if);
        #);
      MouseRelease::
        (#
        do borderStyles.shadowOut -> border.style;
           true -> update;
        #); 
   #);
   inner;
   
-- GUIEnvToolBarButtonOnMouseUp: doPart --
do inner;
   
-- GUIEnvToolBarButtonOnStyleChanged: doPart --
do inner;
   
-- GUIEnvToolBarButtonPrivateAttr: descriptor --
(# theTip: ^text;
#)

-- toolbarLib: attributes --

-- GUIEnvToolbarCreate: doPart --
do inner create;
   
-- GUIEnvToolbarOpen: doPart --
do (# w,h: @integer;
   do 
      true -> border.visible;
      borderStyles.shadowOut -> border.style;
      (if father[]<>NONE then 
          father.size -> (w,h);
       else
          100 -> w;
      if);
      (w,30) -> size;
      (0,0) -> position;
      true -> bindRight;
   #);
   
   inner;
   
   
-- GUIEnvToolbarAdd: doPart --
do (# w,h,w2,h2,x,y: @integer;
   do (if theButton[]<>NONE then
          theButton.size -> (w,h);
          (private.theButtons.lastX+4,4) -> theButton.position;
          w + 4 + private.theButtons.lastX -> private.theButtons.lastX;
          this(toolBar).size -> (w2,h2);
          (if h+4>h2 then
              (w2,h+6) -> this(toolBar).size;
          if);
      if);
   #);
   
-- GUIEnvTBonFatherFrameChanged: doPart --
do 
   inner;
   
-- GUIEnvTBonFrameChanged: doPart --
do 
   inner;
   
-- GUIEnvTBonRefresh: doPart --
do inner onRefresh;

-- GUIenvToolbarPrivate: descriptor --
(# hwndTB, hWndTT: @integer;
   theButtons: @list
     (# element:: toolBarButton;
        lastX: @integer;
     #);
#)
