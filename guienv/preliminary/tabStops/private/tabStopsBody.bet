ORIGIN '../tabStops';
INCLUDE
'~beta/containers/v1.6/list'
'~beta/sysutils/v1.6/objinterface';

-- tabStopsLib: attributes --


-- tabStopsAdd: doPart --
do theWindowItem[]-> private.theStops.add;
   
-- tabStopsOnTab: doPart --
do direction -> private.theStops.onTab;
   
-- tabStopsSetCurFocus: doPart --
do theWindowItem[] -> private.theStops.setCurFocus;
   
-- tabStopsSelectFirst: doPart --
do private.theStops.selectFirst;
   
-- tabStopsSelectLast: doPart --
do private.theStops.selectLast;
   
-- tabStopsPrivateAttr: descriptor --
(# theStops: @cyclicList
     (#
        element:: windowItem;
        curFocus: ^theCellType;
        noOfControls: @integer;
        add:
          (# theWindowItem: ^windowItem; 
          enter theWindowItem[]
          do noOfControls+1-> noOfControls; 
             theWindowItem[]-> append; 
          #);
        onTab:
          (# direction: @integer; (* 1 = forward, -1 = backward *) 
             tempNewElm: ^windowItem;
          enter direction
          do (if curFocus[] = none then selectFirst;  if);
             (if curFocus[] <> none then
                 (if direction = 1 then
                     (curFocus.next).elm[] -> tempNewElm[];
                     (if not tempNewElm.enabled then
                         curFocus.next->curFocus[];
                     if);
                     (curFocus.next).elm[]->target;
                     curFocus.next->curFocus[];
                  else
                     (curFocus.prev).elm[] -> tempNewElm[];
                     (if not tempNewElm.enabled then
                         curFocus.prev->curFocus[];
                     if);
                     (curFocus.prev).elm[]->target;
                     curFocus.prev->curFocus[];
                 if);
                 
                 (* 'onTab. Focus is: '->puttext;
                  curFocus.elm[]->getPatternName->putline; *)
                 
             if);
          #);
        setCurFocus:
          (# theWindowItem: ^windowItem; 
          enter theWindowItem[]
          do
             L: scan
               (# 
               do (if current[] = theWindowItem[] then
                      current[]->at->curFocus[];
                      
                      (* 'setCurFocus. Focus is: '->puttext;
                       curFocus.elm[]->getPatternName->putline; *)

                      leave L
                  if);
               #);
             
          #);
        selectFirst:
          (# 
          do (if head <> none then
                 head->curFocus[]; (head).elm[]->target; 
             if);
          #);
        
        selectLast:
          (# 
          do (if last <> none then
                 head ->curFocus[]; (last).elm[] -> target;
             if);
          #);
     #); 
#)
