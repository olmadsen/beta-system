ORIGIN '~beta/guienv/v1.6/guienv';
BODY 'private/editSheetBody';

-- windowLib: Attributes --
editSheet: canvas
  (#
     <<SLOT editSheetLib:Attributes>>;
     
     rowNotFound:< notification
       (# msg: ^text;
       enter msg[]
       do inner rowNotFound
       #);
     
     eventHandler::< 
       (#
          onEnableTarget::<  
            (#  <<SLOT editSheetOnEnableTarget: doPart>> #);
          
          onMouseDown::<
            (# <<SLOT editSheetOnMouseDown: doPart>> #);
          
          onMouseUp::<
            (# <<SLOT editSheetOnMouseUp: doPart>> #);
       #);
     
     open::<  
       (#  <<SLOT editSheetOpen:DoPart>> #);
     
     close::< 
       (#  <<SLOT editSheetClose:DoPart>> #);
     
     style:
       (* the text style used for drawing *)
       (# theStyle: ^textStyle
       enter (#  enter theStyle[] <<SLOT editSheetSetStyle:DoPart>> #)
       exit (# <<SLOT editSheetGetStyle:DoPart>> exit theStyle[] #)
       #);
     
     setReadOnly:
       (* Changes the permission on the entire editSheet. 
        * true -> setReadOnly; sets the permission to readOnly.
        * By default the permission is write allowed.
        *)
       (# value: @boolean; 
       enter value
       <<SLOT editSheetSetReadOnly:DoPart>>
       #);
     
     clear:<
       (# <<SLOT editSheetClear:DoPart>> #);
     
     refresh:
       (#  <<SLOT editSheetRefresh:DoPart>> #);
     
     fitToContents:
       (# <<SLOT editSheetFitToContents:DoPart>> #);
     
     noOfColumns: integerValue 
       (#  <<SLOT editSheetNoOfColumns:DoPart>> #);
     
     noOfRows: integerValue
       (# <<SLOT editSheetNoOfRows:DoPart>> #);
     
     headingInfo: 
       (# label: @text; width: @integer; inx: @integer;  #);
     
     addHeading:
       (# theHeading: ^headingInfo; 
       enter theHeading[]
       <<SLOT editSheetAddHeading:DoPart>>
       #);
     
     onSelectionChanged:<
       (* Called each time a new cell is selected.
        * The previous selection is (c2,r2), the new selection is (c1,r1).
        *)
       (# c1,r1,c2,r2: @integer;  
       enter (c1,r1,c2,r2) 
       do INNER onSelectionChanged
       #);
     
     selection:
       (# c,r: @integer; 
       enter (#  enter (c,r) <<SLOT editSheetSetSelection:DoPart>> #)
       exit (# <<SLOT editSheetGetSelection:DoPart>> exit (c,r) #)
       #);
     
     onTouched:<
       (#  do INNER onTouched;  #);
     
     onNavigate:<
       (* Called when one of the navigation keys are pressed. c,r is the new
        * default selection.
        *)
       (# c: @integer; (* column *)
          r: @integer; (* row *)
          key: @char;  (* The key pressed. *)
       enter (key,c,r)
       do inner onNavigate
       exit (c,r)
       #);
     
     disableOnTouchedEvents:
       (# <<SLOT editSheetDisableOnTouchedEvents:DoPart>> #);
     
     enableOnTouchedEvents:
       (#  <<SLOT editSheetEnableOnTouchedEvents:DoPart>> #);
     
     row:
       (#
          r: @integer;
          
          init:< 
            (#  <<SLOT rowInit:DoPart>> #);
          
          theColor:
            (# value: @color; 
            enter (# enter value <<SLOT rowSetColor:DoPart>> #)
            exit (#  <<SLOT rowGetColor:DoPart>> exit value #)
            #);
          
          addValue:
            (# value: ^text;  enter value[] <<SLOT rowAddValue:DoPart>> #);
          
          getValue:
            (# i: @integer; v: ^text; 
            enter i
            <<SLOT rowGetValue:DoPart>>
            exit v[]
            #);
          
          setValue:
            (# i: @integer;
               v: ^text;
            enter (i,v[])
            <<SLOT rowSetValue:DoPart>>
            #);
          
          onMouseDown:<
            (# globalPosition: @point;
               i,btn: @integer;
            enter (i,btn,globalPosition)
            do INNER ; 
            #);
          
          onMouseUp:<
            (# globalPosition: @point;
               i,btn: @integer;
            enter (i,btn,globalPosition)
            do INNER ; 
            #);
          
          private: @<<SLOT rowPrivate:Descriptor>>;
       #);
     
     addRow:
       (# theRow: ^row;
       enter theRow[]
       <<SLOT editSheetAddRow:DoPart>>
       #);
     
     deleteRow:
       (# theRow: ^row;
       enter theRow[]
       <<SLOT editSheetDeleteRow: doPart>>
       #);
     
     private: @<<SLOT editSheetPrivate: descriptor>>;
  #)
