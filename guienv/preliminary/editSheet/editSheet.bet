ORIGIN '~beta/guienv/v1.6/guienv';
BODY 'private/editSheetBody';

-- windowLib: Attributes --
editSheet: canvas
  (#
     <<SLOT editSheetLib:Attributes>>;
     
     CELL_NOT_FOUND: (# exit -1 #);
     ROW_NOT_FOUND: (# exit -2 #);
     
     rowNotFound:< notification
       (# msg: ^text;
       enter msg[]
       do inner rowNotFound
       #);
     
     eventHandler::< 
       (#
          onEnableTarget::<  
            (#  <<SLOT editSheetOnEnableTarget: doPart>> #);
          
          onMouseDown::<
            (# <<SLOT editSheetOnMouseDown: doPart>> #);
          
          onMouseUp::<
            (# <<SLOT editSheetOnMouseUp: doPart>> #);
          
          onBeforeChange:<
            (# allow: @boolean;
               c: @integer; (* column *)
               r: @integer; (* row *)
               theText: ^text;
               thePixmap: ^pixmap;
            enter (theText[],thePixmap[],c,r)
            do (if theText[]<>NONE then
                   (if (1->theText.inxGet)=ascii.cr then
                       false -> allow;
                    else
                       true -> allow;
                   if);
                else
                   true -> allow;
               if);
               inner;
            exit allow
            #);
       #);
     
     open::<  
       (#  <<SLOT editSheetOpen:DoPart>> #);
     
     close::< 
       (#  <<SLOT editSheetClose:DoPart>> #);
     
     style:
       (* the text style used for drawing *)
       (# theStyle: ^textStyle
       enter (#  enter theStyle[] <<SLOT editSheetSetStyle:DoPart>> #)
       exit (# <<SLOT editSheetGetStyle:DoPart>> exit theStyle[] #)
       #);
     
     setReadOnly:
       (* Changes the permission on the entire editSheet if colNo is 0.
        * Otherwise the permission on column colNo is changed.
        * true -> setReadOnly; sets the permission to readOnly.
        * By default the permission is write allowed.
        *)
       (# value: @boolean; colNo: @integer;
       enter (value, colNo)
       <<SLOT editSheetSetReadOnly:DoPart>>
       #);
     
     clear:<
       (# <<SLOT editSheetClear:DoPart>> #);
     
     refresh:
       (#  <<SLOT editSheetRefresh:DoPart>> #);
     
     fitToContents:
       (# <<SLOT editSheetFitToContents:DoPart>> #);
     
     noOfColumns: integerValue 
       (#  <<SLOT editSheetNoOfColumns:DoPart>> #);
     
     noOfRows: integerValue
       (# <<SLOT editSheetNoOfRows:DoPart>> #);
     
     headingInfo: 
       (# label: @text; width: @integer; inx: @integer;  #);
     
     addHeading:
       (# theHeading: ^headingInfo; 
       enter theHeading[]
       <<SLOT editSheetAddHeading:DoPart>>
       #);
     
     onFocusChanged:<
       (* Called each time a new cell is selected.
        * The previous selection is (c2,r2), the new selection is (c1,r1).
        *)
       (# c1,r1,c2,r2: @integer;  
       enter (c1,r1,c2,r2) 
       do INNER onFocusChanged
       #);
     
     focus:
       (# c,r: @integer; 
       enter (# enter (c,r) <<SLOT editSheetSetFocus:DoPart>> #)
       exit (# <<SLOT editSheetGetFocus:DoPart>> exit (c,r) #)
       #);
     
     multipleSelection:<
       (* if multipleSelection is TRUE the user is allowed to select
        * multiple elements in the list at a time.  
        *)
       booleanValue;
     
     selection: @
       (# 
          textColor:
            (# value: @color; 
            enter (# enter value <<SLOT setSelTextColor: doPart>> #)
            exit (#  <<SLOT getSelTextColor: doPart>> exit value #)
            #);
          
          backgroundColor:
            (# value: @color; 
            enter (# enter value <<SLOT setSelBgColor: doPart>> #)
            exit (#  <<SLOT getSelBgColor: doPart>> exit value #)
            #);
          
          first: 
            (# theItem: ^row;
            <<SLOT editSheetSelectionFirst: doPart>> 
            exit theItem[]
            #);
          
          clear: 
            (# <<SLOT editSheetSelectionClear: doPart>> #);
          
          scan:
            (# current: ^row;
            <<SLOT editSheetSelectionScan: doPart>>
            #);
       #);
     
     onTouched:<
       (#  do INNER onTouched;  #);
     
     onNavigate:<
       (* Called when one of the navigation keys are pressed. c,r is the new
        * default selection.
        *)
       (# c: @integer; (* column *)
          r: @integer; (* row *)
          key: @char;  (* The key pressed. *)
       enter (key,c,r)
       do inner onNavigate
       exit (c,r)
       #);
     
     disableOnTouchedEvents:
       (# <<SLOT editSheetDisableOnTouchedEvents:DoPart>> #);
     
     enableOnTouchedEvents:
       (#  <<SLOT editSheetEnableOnTouchedEvents:DoPart>> #);
     
     cellTextColor:
       (* Sets the color used to draw the text in cell (r,c).
        * The editSheet must contain at least r rows.
        *)
       (# value: @color; c,r: @integer;
          failure:< notification
            (# errorCode: @integer;
            enter errorCode
            do inner;
            #);
       enter (# enter (c,r,value) <<SLOT setCellTextColor: doPart>> #)
       exit (#  enter (c,r) <<SLOT getCellTextColor: doPart>> exit value #)
       #);
     
     row:
       (#
          r: @integer;
          
          init:< 
            (#  <<SLOT rowInit:DoPart>> #);
          
          height:
            (# value: @integer
            enter (# enter value <<SLOT rowSetHeight: doPart>> #)
            exit (#  <<SLOT rowGetHeight: doPart>> exit value #)
            #);
          
          addValue:
            (# value: ^text;  enter value[] <<SLOT rowAddValue:DoPart>> #);
          
          getValue:
            (# i: @integer; v: ^text; 
            enter i
            <<SLOT rowGetValue:DoPart>>
            exit v[]
            #);
          
          setValue:
            (# i: @integer;
               v: ^text;
            enter (i,v[])
            <<SLOT rowSetValue:DoPart>>
            #);
          
          cellPixmap:
            (* Set/get a pixmap for cell (this(row).r,c) *)
            (# value: ^pixmap; c: @integer;
            enter (# enter (c,value[]) <<SLOT setCellPixmap: doPart>> #)
            exit (#  enter c <<SLOT getCellPixmap: doPart>> exit value[] #)
            #);
          
          select:
            (# extend: @boolean;
            enter extend
            <<SLOT rowSelect: doPart>> 
            #);
          
          deselect:
            (# <<SLOT rowDeselect: doPart>> #);
          
          onMouseDown:<
            (# globalPosition: @point;
               i,btn: @integer;
            enter (i,btn,globalPosition)
            do INNER ; 
            #);
          
          onMouseUp:<
            (# globalPosition: @point;
               i,btn: @integer;
            enter (i,btn,globalPosition)
            do INNER ; 
            #);
          
          update:
            (* Redraws the row *)
            (# <<SLOT rowUpdate: doPart>> #);
          
          private: @<<SLOT rowPrivate:Descriptor>>;
       #);
     
     addRow:
       (# theRow: ^row;
       enter theRow[]
       <<SLOT editSheetAddRow:DoPart>>
       #);
     
     deleteRow:
       (# theRow: ^row;
       enter theRow[]
       <<SLOT editSheetDeleteRow: doPart>>
       #);
     
     private: @<<SLOT editSheetPrivate: descriptor>>;
  #)
