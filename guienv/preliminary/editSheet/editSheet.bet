ORIGIN '~beta/guienv/v1.6/guienvall';
BODY 'private/editSheetBody';

-- windowLib: Attributes --
editSheet: canvas
  (#
     <<SLOT editSheetLib:Attributes>>;
     private:
       @<<SLOT editSheetPrivate:Descriptor>>;
     eventHandler::< 
       (#
          onEnableTarget::<  (#  <<SLOT editSheetOnEnableTarget:DoPart>> #);
          
       #);
     open::<  (#  <<SLOT editSheetOpen:DoPart>> #);
     close::< 
       (#  <<SLOT editSheetClose:DoPart>> #);
     style:
     (* the text style used for drawing *)
       (# theStyle: ^textStyle
       enter (#  enter theStyle[] <<SLOT editSheetSetStyle:DoPart>> #)
       exit (# <<SLOT editSheetGetStyle:DoPart>> exit theStyle[] #)
       #);
     
     (* Changes the permission on the entire editSheet. 
      * true -> setReadOnly; sets the permission to readOnly.
      * By default the permission is write allowed.
      *)
     setReadOnly:
       (# value: @boolean; 
       enter value
       <<SLOT editSheetSetReadOnly:DoPart>>
       #);
     
     clear:<
       (# <<SLOT editSheetClear:DoPart>> #);
     
     refresh:
       (#  <<SLOT editSheetRefresh:DoPart>> #);
     
     fitToContents:
       (# <<SLOT editSheetFitToContents:DoPart>> #);
     
     noOfColumns: integerValue 
       (#  <<SLOT editSheetNoOfColumns:DoPart>> #);
     
     noOfRows: integerValue
       (# <<SLOT editSheetNoOfRows:DoPart>> #);
     
     headingInfo: (# label: @text; width: @integer; inx: @integer;  #);
     
     addHeading:
       (# theHeading: ^headingInfo; 
       enter theHeading[]
       <<SLOT editSheetAddHeading:DoPart>>
       #);
     
     (* Called each time a new cell is selected.
      * The previous selection is (i2,j2), the new selection is (i1,j1).
      *)
     onSelectionChanged:<
       (# i1,j1,i2,j2: @integer;  enter (i1,j1,i2,j2) do INNER #);
     
     selection:
       (# i,j: @integer; 
       enter (#  enter (i,j) <<SLOT editSheetSetSelection:DoPart>> #)
       exit (# <<SLOT editSheetGetSelection:DoPart>> exit (i,j) #)
       #);
     
     onNeedNewRow:< 
       (#  <<SLOT editSheetOnNeedNewRow:DoPart>> #);
     
     onTouched:<
       (#  do INNER onTouched;  #);
     
     disableOnTouchedEvents:
       (# <<SLOT editSheetDisableOnTouchedEvents:DoPart>> #);
     
     enableOnTouchedEvents:
       (#  <<SLOT editSheetEnableOnTouchedEvents:DoPart>> #);
     
     row:
       (#
          j: @integer;
          
          init:< 
            (#  <<SLOT rowInit:DoPart>> #);
          
          theColor:
            (# value: @color; 
            enter (# enter value <<SLOT rowSetColor:DoPart>> #)
            exit (#  <<SLOT rowGetColor:DoPart>> exit value #)
            #);
          
          addValue:
            (# value: ^text;  enter value[] <<SLOT rowAddValue:DoPart>> #);
          
          getValue:
            (# i: @integer; v: ^text; 
            enter i
            <<SLOT rowGetValue:DoPart>>
            exit v[]
            #);
          
          setValue:
            (# i: @integer;
               v: ^text;
            enter (i,v[])
            <<SLOT rowSetValue:DoPart>>
            #);
          
          onMouseDown:<
            (# globalPosition: @point;
               i,btn: @integer;
            enter (i,btn,globalPosition)
            do INNER ; 
            #);
          
          onMouseUp:<
            (# globalPosition: @point;
               i,btn: @integer;
            enter (i,btn,globalPosition)
            do INNER ; 
            #);
          
          private: @<<SLOT rowPrivate:Descriptor>>;
       #);
     
     addRow:
       (# theRow: ^row;
       enter theRow[]
       <<SLOT editSheetAddRow:DoPart>>
       #);
  #)

