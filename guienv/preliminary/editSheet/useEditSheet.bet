ORIGIN '~beta/guienv/guienv';
INCLUDE 
'editSheet'
     '~beta/guienv/graphics'
     '~beta/guienv/controls'
     '~beta/guienv/utils/navigationkeys'
'../resources/pixmaps';

(* RESOURCE nti 'test.rc'; *)

-- lib: attributes --
putPoint:
  (# h,v: @integer; 
  enter (h,v)
  do '('->put; h->putInt; ','->put; v->putInt; ')'->put; 
  #);
putRectangle:
  (# top,left,bottom,right: @integer; 
  enter ((top,left),(bottom,right))
  do
     '( '->putText;
     (top,left)->putPoint;
     ' , '->putText;
     (bottom,right)->putPoint;
     ' )'->putLine;
     
  #);

--PROGRAM: descriptor--
guienv
(# mainWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# openItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  #);
                         #);
                       open:: (# do 'Open' -> name #)
                    #);
                  
                  refreshItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  myCanvas.refresh; #);
                         #);
                       open:: (# do 'Refresh' -> name #)
                    #);
                  
                  addRowItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  myCanvas.insertRow; #);
                         #);
                       open:: (# do 'Add Row' -> name #)
                    #);
                  
                  deleteRowItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  myCanvas.removeRow; #);
                         #);
                       open:: (# do 'Delete Row' -> name #)
                    #);
                  
                  getFocusItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# i,j: @integer;
                              do 'Selected cell = ' -> screen.putText;
                                 myCanvas.focus-> (i,j) -> putpoint; newline; 
                                 'Contents of selectied cell = ' -> screen.putText;
                                 L:
                                   myCanvas.scanRows
                                   (# v: ^text;
                                   do (if current.r=j then
                                          i -> current.getValue -> v[];
                                          (if v[]<>NONE then
                                              v[] -> screen.putline;
                                          if);
                                      if);
                                   #);
                              #);
                         #);
                       open:: (# do 'Get Focus' -> name #)
                    #);
                  
                  scanSelectionItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# 
                              do myCanvas.scanSelection;
                              #);
                         #);
                       open:: (# do 'Scan Selection' -> name #)
                    #);
                  
                  clearSelectionItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# 
                              do myCanvas.clearSelection;
                              #);
                         #);
                       open:: (# do 'Clear Selection' -> name #)
                    #);
                  
                  selectRowItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# 
                              do 2 -> myCanvas.selectRow;
                              #);
                         #);
                       open:: (# do 'Select row no 2' -> name #)
                    #);
                  
                  
                  clearItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# 
                              do (* myCanvas.theRows.clear; *)
                                 myCanvas.clear; 
                                 myCanvas.theRows.scan
                                 (# 
                                 do (for i:myCanvas.noOfColumns repeat
                                         i -> current.getValue -> putline;
                                    for);
                                    current[] -> myCanvas.addRow;
                                 #);
                              #);
                         #);
                       open:: (# do 'Clear' -> name #)
                    #);
                  
                  toggleOnTouchItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# 
                              do (if myCanvas.onTouchedEventsEnabled then
                                     myCanvas.disableOnTouchedEvents;
                                     false->myCanvas.onTouchedEventsEnabled;
                                  else
                                     myCanvas.enableOnTouchedEvents;
                                     true->myCanvas.onTouchedEventsEnabled;
                                 if);
                              #);
                         #);
                       open:: (# do 'Toggle onTouch' -> name #)
                    #);
                  
                  scanRowsItem: @menuItem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# 
                              do myCanvas.scanRows
                                 (# 
                                 do 'current.r = ' -> puttext;
                                    current.r -> putint; newline;
                                 #);
                              #);
                         #);
                       open:: 
                         (# do 'Scan Rows' -> name #)
                    #);
                  
                  quitItem: @menuItem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# 
                              do mainWindow.theEventHandler.onAboutToClose;
                              #);
                         #);
                       open:: 
                         (# do 'Quit' -> name #)
                    #);
                  open::
                    (#
                    do '&File' -> name;
                       openItem.open; openItem[] -> append;
                       getFocusItem.open; getFocusItem[] -> append;
                       refreshItem.open; refreshItem[] -> append;
                       addRowItem.open; addRowItem[] -> append;
                       deleteRowItem.open; deleteRowItem[] -> append;
                       clearItem.open; clearItem[] -> append;
                       scanSelectionItem.open; scanSelectionItem[]->append;
                       clearSelectionItem.open; clearSelectionItem[] -> append;
                       selectRowItem.open; selectRowItem[] -> append;
                       toggleOnTouchItem.open; toggleOnTouchItem[]->append;
                       scanRowsItem.open; scanRowsItem[]->append;
                       quitItem.open; quitItem[] -> append
                    #);
               #);
             
             open:: 
               (# 
               do fileMenu.open; fileMenu[] -> append;
               #);
          #); (* menubarType *)
        
        
        myCanvas: @editSheet
          (# onTouchedEventsEnabled: @boolean;
             
             
             scanSelection:
               (# 
               do 'Selection = ' -> screen.putline;
                  selection.scan
                  (# 
                  do current.r -> putint; newline; 
                  #);
               #);
             
             clearSelection:
               (# 
               do selection.clear
               #);
             
             selectRow:
               (# r: @integer;
               enter r
               do theRows.scan
                  (# 
                  do (if current.r=r then
                         true -> current.select;
                     if);
                  #);
               #);
             
             eventHandler::
               (# onMouseUp::
                    (# 
                    do (* 'editSheet.onMouseUp' -> putline; *)
                    #);
                  onMouseDown::
                    (# 
                    do (* 'editSheet.onMouseDown' -> putline; *)
                    #);
                  onKeyDown::
                    (# 
                    do (* 'myCanvas.onKeyDown' -> putLine; *)
                    #);
                  
                  onBeforeChange::
                    (# 
                    do (if theText[]<>NONE then
                           (if (1->theText.inxGet)=ascii.cr then
                               'c,r = ' -> puttext;
                               c -> putint; ', ' -> puttext; r -> putint; newline;
                               (if r mod 5 = 0 then
                                   true -> allow
                                else
                                   false -> allow;
                               if);
                            else
                               'key<>ascii.cr' -> putline;
                           if);
                       if);
                    #);
               #);
             
             onTouched::
               (# 
               do 'onTouched' -> screen.putline;
               #);
             onFocusChanged::
               (# j: @integer;
               do theRows.scan
                  (# v: ^text;
                  do (* j + 1 -> j;
                      *                      (for i:noOfColumns repeat
                      *                           i -> current.getValue -> v[];
                      *                           (if v[]<>NONE then
                      *                               v[] -> screen.putText; 
                      *                               '  ' -> screen.putText;
                      *                           if);
                      *                      for);
                      *                      screen.newline;
                      *)
                  #);
               #);
             
             myRow: row
               (# 
                  init::  
                    (#  
                    do contTypeMenu.open;  
                       
                    #);
                  contTypeMenu: @menu
                    (#
                       reefItem,dryItem,openItem,osotItem,flatItem,coflItem,
                       twdkItem,soTankItem: @menuItem
                         (#
                            eventHandler:: 
                              (# onSelect::  (#  do (3,name)->setValue;  #); 
                              #);
                         #);
                       open::< 
                         (# 
                         do dryItem.open;
                            'Dry'->dryItem.name;
                            dryItem[]->append;
                            reefItem.open;
                            'REEF'->reefItem.name;
                            reefItem[]->append;
                            openItem.open;
                            'Open'->openItem.name;
                            openItem[]->append;
                         #);
                    #);
                  
                  onMouseUp::
                    (# where: @point; 
                    do 
                       (if btn
                        //3 then 
                           'Calling popUp.' -> putline;
                           globalPosition->globalToLocal->where;
                           'where= ' -> puttext; where -> putpoint; newline;
                           
                           (1,where,THIS(window).contents)->contTypeMenu.popUp;
                        //2 then
                           
                       if);
                    #);
               #);
             
             multipleSelection::
               (# do true -> value #);
             
             
             
             onNavigate::
               (# 
               do 'onNavigate. c,r = ' -> screen.putText;
                  c -> putint; ', ' -> puttext; r -> putint; newline; 
                  (if key
                   //leftArrow then
                      'leftArrow' -> putline;
                   //rightArrow then
                      'rightArrow' -> putline;
                   //upArrow then
                      'upArrow' -> putline;
                   //downArrow then
                      'downArrow' -> putline;
                   //ascii.cr then
                      'ascii.cr' -> putline;
                  if);
               #);
             
             theRows: @list
               (# element:: myRow;
               #);
             
             pm: @pixmap;
             
             open::
               (# shippingHInfo: headingInfo
                    (# onMouseDown::
                         (# 
                         do 'MouseDown. Label = ' -> screen.putText;
                            label[] -> putline;
                            
                            
                         #);
                    #);
                  recDel,haul,lin,mode,loadDisc: ^shippingHInfo;
                  t: ^text; aRow: ^myRow;
               do true -> onTouchedEventsEnabled;
                  (4,0) -> position;
                  (200,500) -> size; 
                  &shippingHInfo[] -> recDel[];
                  125 -> recDel.width;
                  'R/D Receipt or Delivery' -> recDel.label;
                  recDel[] -> addHeading;
                  
                  &shippingHInfo[] -> haul[];
                  80 -> haul.width;
                  'Haulier' -> haul.label;
                  haul[] -> addHeading;
                  
                  &shippingHInfo[] -> lin[];
                  35 -> lin.width;
                  'Line' -> lin.label;
                  lin[] -> addHeading;
                  
                  &shippingHInfo[] -> mode[];
                  60 -> mode.width;
                  'Mode' -> mode.label;
                  mode[] -> addHeading;
                  
                  &shippingHInfo[] -> loadDisc[];
                  164 -> loadDisc.width;
                  'Load / Discharge Port' -> loadDisc.label;
                  loadDisc[] -> addHeading;
                  
                  (true,3) -> setReadOnly;
                  
                  'hyper.png' -> pm.read;
                  
                  (* (0,25600,0) -> selection.textColor; *)
                  
                  (for j:5 repeat
                       &myRow[] -> aRow[];
                       aRow.init;
                       (if (j mod 5) = 0 then
                           50 -> aRow.height;
                           
                       if);
                       (for i:3 repeat
                            &text[] -> t[];
                            '(' -> t.put; i -> t.putInt;
                            ',' -> t.put; j -> t.putInt; ')' -> t.put;
                            t[] -> aRow.addValue;
                       for);
                       aRow[] -> addRow;
                       (* (for i:50 repeat
                        *                             &text[] -> t[];
                        *                             '(' -> t.put; i -> t.putInt;
                        *                             ',' -> t.put; j -> t.putInt; ')' -> t.put;
                        *                             (i,t[]) -> aRow.setValue;
                        *                        for);
                        *)
                       aRow[] -> theRows.append;
                       
                       (if (j mod 3) = 0 then
                           (2,pm[]) -> aRow.cellPixmap;
                       if);
                       (3,j,(32000,0,0)) -> cellTextColor
                       (# failure::
                            (# 
                            do 'errorCode = '  -> puttext; errorCode -> putint; newline;
                            #);
                       #);
                  for);
                  
                  (* fitToContents; *)
                  true -> bindright -> bindBottom;
               #);
             
             insertRow:
               (# aRow: ^myRow;
                  t: ^text;
               do &myRow[] -> aRow[];
                  aRow.init;
                  (for i:5 repeat
                       &text[] -> t[];
                       '(' -> t.put; i -> t.putInt;
                       ',' -> t.put; noOfRows+1 -> t.putInt; ')' -> t.put;
                       t[] -> aRow.addValue;
                  for);
                  aRow[] -> addRow; 
                  
                  aRow[] -> theRows.append; 
                  (* clear;
                   *                   theRows.scan
                   *                   (# 
                   *                   do current[] -> addRow;
                   *                   #);
                   *)
                  
                  (* true -> aRow.select; *)
                  
                  (* (1,2) -> focus; *)
               #);
             
             removeRow:
               (# colNo,rowNo: @integer;
                  theRow: ^myRow;
               do focus -> (colNo,rowNo);
                  L:
                    theRows.scan
                    (# 
                    do (if current.r = rowNo then
                           current[] -> theRow[];
                           leave L
                       if);
                    #);
                  (if theRow[]<>NONE then
                      newline;
                      'Before delete...' -> putline;
                      scanRows
                      (# 
                      do current.r -> putint; ' ' -> put;
                      #);
                      newline;
                      
                      theRow[] -> deleteRow;
                      
                      'After delete...' -> putline;
                      scanRows
                      (# 
                      do current.r -> putint; ' ' -> put;
                      #);
                      newline;
                      
                      theRow[] -> theRows.at -> theRows.delete;
                      
                  if);
               #);
          #);
        
        
        open::
          (# 
          do (600,600) -> size; 
             myCanvas.open;
             myCanvas.disableOnTouchedEvents; 
             false->myCanvas.onTouchedEventsEnabled; 
          #);
        eventHandler::
          (# onAboutToClose::
               (# do terminate; #);
          #);
     #);
do mainWindow.open;
#)
