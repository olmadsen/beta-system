ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE 'editSheet';
INCLUDE '~beta/guienv/v1.6/graphics';
INCLUDE '~beta/guienv/v1.6/controls';

-- lib: attributes --
putPoint:
  (# h,v: @integer; 
  enter (h,v)
  do '('->put; h->putInt; ','->put; v->putInt; ')'->put; 
  #);
putRectangle:
  (# top,left,bottom,right: @integer; 
  enter ((top,left),(bottom,right))
  do
     '( '->putText;
     (top,left)->putPoint;
     ' , '->putText;
     (bottom,right)->putPoint;
     ' )'->putLine;
     
  #);

--PROGRAM: descriptor--
guienv
(# mainWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# openItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  #);
                         #);
                       open:: (# do 'Open' -> name #)
                    #);
                  
                  refreshItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  myCanvas.refresh; #);
                         #);
                       open:: (# do 'Refresh' -> name #)
                    #);
                  
                  getSelectionItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# i,j: @integer;
                              do 'Selected cell = ' -> screen.putText;
                                 myCanvas.selection-> (i,j) -> putpoint; newline; 
                                 'Contents of selectied cell = ' -> screen.putText;
                                 L:
                                   myCanvas.theRows.scan
                                   (# v: ^text;
                                   do (if current.j=j then
                                          i -> current.getValue -> v[];
                                          (if v[]<>NONE then
                                              (* v[] -> screen.putline;  *)
                                          if);
                                      if);
                                   #);
                              #);
                         #);
                       open:: (# do 'Get Selection' -> name #)
                    #);
                  
                  clearItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# 
                              do myCanvas.theRows.clear;
                                 myCanvas.clear; 
                              #);
                         #);
                       open:: (# do 'Clear' -> name #)
                    #);
                  
                  toggleOnTouchItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# 
                              do (if myCanvas.onTouchedEventsEnabled then
                                     myCanvas.disableOnTouchedEvents;
                                     false->myCanvas.onTouchedEventsEnabled;
                                  else
                                     myCanvas.enableOnTouchedEvents;
                                     true->myCanvas.onTouchedEventsEnabled;
                                 if);
                              #);
                         #);
                       open:: (# do 'Toggle onTouch' -> name #)
                    #);
                  
                  quitItem: @menuItem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# 
                              do mainWindow.theEventHandler.onAboutToClose;
                              #);
                         #);
                       open:: 
                         (# do 'Quit' -> name #)
                    #);
                  open::
                    (#
                    do '&File' -> name;
                       openItem.open; openItem[] -> append;
                       getSelectionItem.open; getSelectionItem[] -> append;
                       refreshItem.open; refreshItem[] -> append;
                       clearItem.open; clearItem[] -> append;
                       toggleOnTouchItem.open; toggleOnTouchItem[]->append;
                       quitItem.open; quitItem[] -> append
                    #);
               #);
             
             open:: 
               (# 
               do fileMenu.open; fileMenu[] -> append;
               #);
          #); (* menubarType *)
        
        
        myCanvas: @editSheet
          (# onTouchedEventsEnabled: @boolean;
             myWindow: @window
               (# isOpen: @boolean;
                  open:: 
                    (# 
                    do (50,50) -> position; (200,200)->size; 
                       true -> isOpen;
                    #); 
                  close::
                    (# 
                    do false -> isOpen;
                    #);
               #);
             
             onTouched::
               (# 
               do 'onTouched' -> screen.putline;
               #);
             onSelectionChanged::
               (# j: @integer;
               do theRows.scan
                  (# v: ^text;
                  do j + 1 -> j;
                     (for i:5 repeat
                          i -> current.getValue -> v[];
                          (if v[]<>NONE then
                              (* v[] -> screen.putline; *)
                          if);
                     for);
                  #);
               #);
             
             myRow: row
               (# 
                  init::  (#  do contTypeMenu.open;  #);
                  contTypeMenu: @menu
                    (#
                       reefItem,dryItem,openItem,osotItem,flatItem,coflItem,
                       twdkItem,soTankItem: @menuItem
                         (#
                            eventHandler:: 
                              (# onSelect::  (#  do (3,name)->setValue;  #); 
                              #);
                         #);
                       open::< 
                         (# 
                         do dryItem.open;
                            'Dry'->dryItem.name;
                            dryItem[]->append;
                            reefItem.open;
                            'REEF'->reefItem.name;
                            reefItem[]->append;
                            openItem.open;
                            'Open'->openItem.name;
                            openItem[]->append;
                         #);
                    #);
                  
                  onMouseUp::
                    (# where: @point; 
                    do 'globalPosition= ' -> puttext;
                       globalPosition.h -> putint; ', ' -> puttext;
                       globalPosition.v -> putint; newline;
                       'btn= ' -> puttext; btn -> putint; newline;
                       newline;
                       (if btn
                        //3 then 
                           'Calling popUp.' -> putline;
                           globalPosition->globalToLocal->where;
                           'where= ' -> puttext; where -> putpoint; newline;
                           
                           (1,where,THIS(window).contents)->contTypeMenu.popUp;
                        //2 then
                           (if myWindow.isOpen then
                               myWindow.bringToFront;
                            else
                               myWindow.open;
                               myWindow.bringToFront;
                           if);
                       if);
                    #);
               #);
             
             onNeedNewRow::
               (# aRow: ^myRow; t: ^text;
               do &myRow[] -> aRow[];
                  aRow.init;
                  (for i:5 repeat
                       &text[] -> t[];
                       '(' -> t.put; i -> t.putInt;
                       ',' -> t.put; noOfRows -> t.putInt; ')' -> t.put;
                       t[] -> aRow.addValue;
                  for);
                  aRow[] -> addRow;
                  aRow[] -> theRows.append;
               #);
             
             theRows: @list
               (# element:: myRow;
               #);
             
             open::
               (# recDel,haul,lin,mode,loadDisc: ^headingInfo;
                  t: ^text; aRow: ^myRow;
               do true -> onTouchedEventsEnabled;
                  (600,600) -> size; 
                  &headingInfo[] -> recDel[];
                  122 -> recDel.width;
                  'R/D Receipt or Delivery' -> recDel.label;
                  recDel[] -> addHeading;
                  
                  &headingInfo[] -> haul[];
                  80 -> haul.width;
                  'Haulier' -> haul.label;
                  haul[] -> addHeading;
                  
                  &headingInfo[] -> lin[];
                  35 -> lin.width;
                  'Line' -> lin.label;
                  lin[] -> addHeading;
                  
                  &headingInfo[] -> mode[];
                  60 -> mode.width;
                  'Mode' -> mode.label;
                  mode[] -> addHeading;
                  
                  &headingInfo[] -> loadDisc[];
                  140 -> loadDisc.width;
                  'Load / Discharge Port' -> loadDisc.label;
                  loadDisc[] -> addHeading;
                  
                  (* true -> setReadOnly; *)
                  
                  (for j:5 repeat
                       &myRow[] -> aRow[];
                       aRow.init;
                       (for i:5 repeat
                            &text[] -> t[];
                            '(' -> t.put; i -> t.putInt;
                            ',' -> t.put; j -> t.putInt; ')' -> t.put;
                            t[] -> aRow.addValue;
                       for);
                       aRow[] -> addRow;
                       (* (for i:5 repeat
                        *                             &text[] -> t[];
                        *                             '(' -> t.put; i -> t.putInt;
                        *                             ',' -> t.put; j -> t.putInt; ')' -> t.put;
                        *                             (i,t[]) -> aRow.setValue;
                        *                        for);
                        *)
                       aRow[] -> theRows.append;
                  for);
                  
               #);
          #);
        open::
          (# 
          do (600,600) -> size; 
             myCanvas.open;
             myCanvas.disableOnTouchedEvents; 
             false->myCanvas.onTouchedEventsEnabled; 
          #);
        eventHandler::
          (# onAboutToClose::
               (# do terminate; #);
          #);
     #);
do mainWindow.open;
#)
