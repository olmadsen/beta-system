ORIGIN '../moviebody';
INCLUDE        '~beta/basiclib/file'
       '~beta/sysutils/cstring'
       '~beta/win32lib/fileio'
       '~beta/win32lib/windef'
       '~beta/win32lib/windowmanagement'
       '~beta/guienv/private/winnt/guienv_ntiprivate'
'avistuff';

LINKOPT nti 
'vfw32.lib'
'winmm.lib';

-- aviMovieLib: attributes --

-- movieInit: doPart --
do TRUE -> private.isOpen;
   
-- movieDispose: doPart --
do FALSE -> private.isOpen;
   
   
-- movieGetDisplaySize: doPart --
do 
   
-- movieGoToBeginning: doPart --
do (if private.isOpen then
       
   if);
   
-- movieGoToEnd: doPart --
do (if private.isOpen then
       
   if);
   
-- movieSetTime: doPart --
do 
   
-- movieGetTime: doPart --
do 
-- movieEnter: doPart --
do name.copy -> private.name[];


-- movieExit: doPart --
do (if private.name[]<>NONE then
       private.name -> name; 
   if);


--- moviePrivate: descriptor ---
(# name: ^text;
   isOpen: @boolean;
#)

-- aviMovieFileLib: attributes --
openMF:
  (* general procedure for openRead, openWrite and openReadWrite *)
  (# permissions: @integer; (* OF_READ, OF_WRITE, OF_READWRITE. *)
     result: @integer;
  enter permissions
  do 
  #);

-- movieFileSetName: doPart --
do outerL:
     (# aFile: @file;
     do false -> private.existing;
        fileName.copy -> private.theFileName[];
        private.theFileName[] -> aFile.name;
        (if aFile.entry.exists
            (# error::
                 (# do leave outerL; #)
            #) then
            (if aFile.entry.isFile then
                true -> private.existing;
            if);
        if);
     #);
   
-- movieFileGetName: doPart --
do (if private.theFileName[]<>NONE then
       private.theFileName -> fileName;
   if);
   
-- movieFileExists: doPart --
do outerL:
     (# aFile: @file;
     do (if private.theFileName[]<>NONE then
            private.theFileName[] -> aFile.name;
            aFile.entry.exists
            (# error::
                 (# do leave outerL; #)
            #) -> value;
        if);
     #);
   
-- movieFileGetFile: doPart --
do (# resultPath, winapiFilter: @cString;
   do 'Microsoft AVI Files( *.avi)|*.avi|QuickTime Movies( *.mov)|*.mov|' -> winapiFilter.set;
      winapiFilter -> openAVIfile -> resultPath;
      
      (if resultPath.charPtr=0 then
          none -> private.theFileName[];
       else
          resultPath.get -> private.theFileName[];
          resultPath.free;
      if);
      winapiFilter.free;
      (if private.theFileName[]<>NONE then
          (private.theFileName.length>0) -> userConfirmed;         
      if);
   #)
   
-- movieFileGetMovie: doPart --
do (if private.theFileName[]<>NONE then
       private.theFileName -> newMovie;
   if);
   
-- movieFileOpenRead: doPart --
do OF_READ -> openMF;
   
-- movieFileClose: doPart --
do 
   
-- movieFilePrivate: descriptor --
(# theFileName: ^text; 
   existing: @boolean;
#)

-- avimovieViewLib: attributes --
movieViewMethods: windowItemMethods
  (# 
     dispatchMessage::
       (# didSomething: @boolean;
          
          setFrameSize:
            (# theNtRectPtr: @integer;
               r: ^rectangle;
            do 16 -> malloc -> theNtRectPtr;
               (interfaceObjectID,theNtRectPtr) -> GetClientRect;
               &rectangle[] -> r[];
               theNtRectPtr -> getRectFromNtRectPtr -> r;
               r.size -> this(movieView).private.windowItemFrame.size;
               theNtRectPtr -> free;
            #);
          
          processWM_Size:
            (# width,height: @integer;
            do (info.lParam.loWord,info.lParam.hiWord) ->  (width,height);
               (* If the user selects a view that is already shown the
                * values for new width and height set by MCI and returned in
                * the WM_SIZE message are zero. This is an error. We correct
                * it by calculating the client rectangle and resetting the 
                * windowItemFrame. *)
               (if (width>0) and (height>0) then
                   mfPrivate.newFrame -> mfPrivate.oldFrame;
                   frame -> mfPrivate.newFrame;
                else
                   setFrameSize;
               if);
            #);
       do (if info.message = WM_SIZE then
             processWM_Size; 
          if);
          didSomething or info.handled -> info.handled;
       #);
  #);

(* window styles *)
MCIWNDF_NOAUTOSIZEWINDOW: (# exit 16x0001 #); (* when movie size changes *)
MCIWNDF_NOPLAYBAR: (# exit 16x0002 #); (* no toolbar *)
MCIWNDF_NOAUTOSIZEMOVIE: (# exit 16x0004 #);  (* when window size changes *)
MCIWNDF_NOMENU: (# exit 16x0008 #); (* no popup menu from RBUTTONDOWN *)
MCIWNDF_SHOWNAME: (# exit 16x0010 #); (* show name in caption *)
MCIWNDF_SHOWPOS: (# exit  16x0020 #); (* show position in caption *)
MCIWNDF_SHOWMODE: (# exit 16x0040 #); (* show mode in caption *)
MCIWNDF_SHOWALL: (# exit  16x0070 #); (* show all *)

MCIWNDF_NOTIFYMODE: (# exit 16x0100 #);
MCIWNDF_NOTIFYPOS: (# exit  16x0200 #);
MCIWNDF_NOTIFYSIZE: (# exit 16x0400 #);
MCIWNDF_NOTIFYERROR: (# exit 16x1000 #);
MCIWNDF_NOTIFYALL: (# exit  16x1F00 #);
MCIWNDF_NOOPEN: (# exit 16x8000 #);
MCIWNDF_NOTIFYMEDIA: (# exit 16x0880 #);

MCIWNDF_NOTIFYANSI: (# exit 16x0080 #);

MCIWNDM_NOTIFYMODE: (# exit WM_USER + 200 #);
MCIWNDM_NOTIFYPOS: (# exit WM_USER + 201 #);
MCIWNDM_NOTIFYSIZE: (# exit WM_USER + 202 #);
MCIWNDM_NOTIFYMEDIA: (# exit WM_USER + 203 #);
MCIWNDM_NOTIFYERROR: (# exit WM_USER + 205 #);


-- movieViewGetDisplaySize: doPart --
do (# windowId: @integer;
      theNtRectPtr: @integer;
      value: ^rectangle;
      result: @integer;
      MCIWNDM_GET_SOURCE: (# exit  (WM_USER + 140) #);
   do (* (300,300) -> (width,height); *)
      interfaceObjectID -> windowID;
      16 -> malloc -> theNtRectPtr;
      (if theNtRectPtr<>0 then
          (windowID, MCIWNDM_GET_SOURCE, 0, theNtRectPtr)->sendMessage->result;
          (if result=0 then
              &rectangle[] -> value[];
              theNtRectPtr -> getRectFromNtRectPtr -> value;
              theNtRectPtr -> free;
              value.size -> (width,height);
              NONE -> value[];
           else
              (0,0) -> (width,height);
          if);
       else
          (300,300) -> (width,height);
      if);
   #);
   
-- movieViewGetMovieName: doPart --
do (# theFilename: @cString;
      res: @integer;
   do MAX_PATH -> theFilename.init;
      (interfaceObjectId, theFilename, MAX_PATH) 
        -> myMCIWndGetFileName -> res;
      (if res=0 then
          theFilename.get -> theName[];
       else
          'movieViewGetMovieName failed!' -> screen.putline;
      if);
      
      theFilename.free;
   #);
   
-- movieViewSetContents: doPart --
do (# windowID: @integer; t: @text; openRes: @integer;
   do interfaceObjectID -> windowID;
      (if theMovie[] <> none then
          (* We do not close the current MCI device. myMCIWndOpen does this
           * automatically if we assign a new movie or it is done when the
           * movie field is closed. If we call myMCIWndClose at this point
           * calling myMCIWndOpen right afterwards will not work. *)
          none -> theMovie[]; 
      if);
      (if theNewMovie[]<>NONE then
          (windowID, theNewMovie, 0) -> myMCIWndOpen -> openRes; 
          this(movieView)[] -> target;
          theNewMovie[] -> theMovie[];
      if);
   #);
   
-- movieViewStart: doPart --
do (# windowID: @integer;
      start, end: @integer;
   do interfaceObjectID -> windowID;
      (if duration<1 then
          windowID -> myMCIWndPlay;
       else
          windowID -> myMCIWndGetPosition -> start;
          start + duration -> end;
          (windowID, end) -> myMCIWndPlayTo;
      if);
   #);
   
-- movieViewStop: doPart --
do (# windowID: @integer;
   do interfaceObjectID -> windowID;
      windowID -> myMCIWndStop;
   #);
   
-- movieViewGoToBeginning: doPart --
do (# windowID: @integer;
   do interfaceObjectID -> windowID;
      windowId -> myMCIWndHome;
   #);
   
-- movieViewGoToEnd: doPart --
do (# windowID: @integer;
   do interfaceObjectID -> windowID;
      windowId -> myMCIWndEnd;
   #);
   
-- movieViewSetTime: doPart --
do (# windowID: @integer;
   do interfaceObjectID -> windowID;
      (windowId, t) -> myMCIWndSeek;
   #);
   
-- movieViewGetTime: doPart --
do (# windowID: @integer;
   do interfaceObjectID -> windowID;
      windowID -> myMCIWndGetPosition -> t;
   #);
   
-- movieViewGetDuration: doPart --
do (# windowID: @integer;
   do interfaceObjectID -> windowID;
      windowID -> myMCIWndUseTime;
      windowID -> myMCIWndGetLength -> movieDuration;
   #);
   
-- movieViewCreate: doPart --
do (# wStyle: @integer;
   do mfPrivate.owner.open;
      (if not this(guienv).private.isMCIWndClassReg then
          (if (uienvHInstance -> MCIWndRegisterClass)= 0 then
              'MCIWndRegisterClass failed.' -> screen.putLine;
           else
              TRUE -> this(guienv).private.isMCIWndClassReg;
          if);
      if);
      
      &movieViewMethods[] -> theMethods; 
      
      MCIWNDF_NOTIFYANSI %Bor MCIWNDF_NOTIFYMEDIA -> wStyle; 
      (* Above line tos_converted from: (MCIWNDF_NOTIFYANSI,MCIWNDF_NOTIFYMEDIA) -> tos'%or' -> wStyle; *)
      wStyle %Bor MCIWNDF_NOTIFYSIZE -> wStyle; 
      (* Above line tos_converted from: (wStyle,MCIWNDF_NOTIFYSIZE) -> tos'%or' -> wStyle; *)
      wStyle %Bor MCIWNDF_NOTIFYMODE -> wStyle; 
      (* Above line tos_converted from: (wStyle,MCIWNDF_NOTIFYMODE) -> tos'%or' -> wStyle; *)
      wStyle %Bor MCIWNDF_NOTIFYPOS -> wStyle; 
      (* Above line tos_converted from: (wStyle,MCIWNDF_NOTIFYPOS) -> tos'%or' -> wStyle; *)
      wStyle %Bor MCIWNDF_NOOPEN -> wStyle; 
      (* Above line tos_converted from: (wStyle,MCIWNDF_NOOPEN) -> tos'%or' -> wStyle; *)
      
      wStyle -> windowItemStyle; 
      'MCIWndClass' -> windowItemClass;  
      isSubClass -> windowItemClassStatus;
      INNER create;
   #);
   
-- movieViewOpen: doPart --
do INNER;
   (interfaceObjectID, mfPrivate.owner.interfaceObjectID) -> myMCIWndSetOwner;
   interfaceObjectID -> myMCIWndUseTime;
   (* (interfaceObjectID, MCIWNDF_NOTIFYALL, 0)
    -> myMCIWndChangeStyles;
    *)
   
-- movieViewClose: doPart --
do inner close;
   interfaceObjectID -> myMCIWndClose;
   
-- movieViewMouseDown: doPart --
do inner;
   
-- movieViewMouseUp: doPart --
do inner;
   
-- movieViewRefresh: doPart --
do inner;
   
-- movieViewOnFrameChanged: doPart --
do TRUE -> mfPrivate.inOnFrameChanged;
   inner onFrameChanged;
   FALSE -> mfPrivate.inOnFrameChanged;
   
-- movieViewPrivate: descriptor --
(# newFrame, oldFrame: @rectangle;
   inOnFrameChanged: @boolean;
   theMovieController: @integer; 
   owner: @windowItem
     (# ownerMethods: windowItemMethods
          (# 
             dispatchMessage::
               (# didSomething: @boolean;
                  
                  processMCIWNDM_NOTIFYMODE:
                    (# 
                    do 'processMCIWNDM_NOTIFYMODE is called. mode= ' 
                         -> screen.putText;
                       info.lParam  -> putInt; newline;
                    #);
                  
                  processMCIWNDM_NOTIFYMEDIA:
                    (# theNewName: @cString;
                       newName: ^text;
                    do true -> didSomething;
                       info.lParam -> theNewName;
                       theNewName.get 
                         -> this(movieView).theEventHandler.onMediaChanged;
                    #);
                  
                  processMCIWNDM_NOTIFYSIZE:
                    (# newFrame: @rectangle;
                       w,h: @integer;
                    do (if not mfPrivate.inOnFrameChanged then
                           (if not 
                               (mfPrivate.newFrame->mfPrivate.oldFrame.isEqual)
                               then
                               (mfPrivate.oldFrame,mfPrivate.newFrame) 
                                 -> this(movieView).theEventHandler.onFrameChanged;
                           if);
                       if);
                    #);
                  
                  processMCIWNDM_NOTIFYPOS:
                  (# 
                  do (* 'processMCIWNDM_NOTIFYPOS. lParam= ' -> putText;
                      info.lParam -> putInt; newline;
                      *)
                  #);
                  
                  putPoint:
                    (# h,v: @integer;
                    enter (h,v)
                    do '(' -> put;
                       h -> putInt;
                       ',' -> put;
                       v -> putInt;
                       ')' -> put;
                    #);
                  
                  putRectangle:
                    (# top, left, bottom, right: @integer;
                    enter ((top, left), (bottom, right))
                    do '( ' -> putText;
                       (top, left) -> putPoint; ' , ' -> putText;
                       (bottom, right) -> putPoint; ' )' -> putLine;
                    #);
               do 
                  (if info.message 
                   //MCIWNDM_NOTIFYMODE then (* processMCIWNDM_NOTIFYMODE;  *)
                   //MCIWNDM_NOTIFYPOS then processMCIWNDM_NOTIFYPOS;
                   //MCIWNDM_NOTIFYSIZE then
                      processMCIWNDM_NOTIFYSIZE;
                      
                   //MCIWNDM_NOTIFYMEDIA then
                      processMCIWNDM_NOTIFYMEDIA;
                   //MCIWNDM_NOTIFYERROR then
                      (* 'Got a MCIWNDM_NOTIFYERROR' -> screen.putLine; *)
                   else
                      (* info.message  -> putint; newline; *)
                  if);
                  didSomething or info.handled -> info.handled;
               #);
          #);
        open::
          (# create::
               (# 
               do uienvStandardWindowClass -> windowItemClass;
                  isStandardClass  -> windowItemClassStatus;
                  &ownerMethods[] -> theMethods;
               #);
          do hide; 
          #);
     #);
#)
