ORIGIN '~beta/guienv/guienv';
BODY 'private/moviebody';

--- windowLib: attributes ---
movie:
  (# <<slot aviMovieLib: attributes>>;
	
     init: 
       (# <<SLOT movieInit: doPart>> #);
     
     dispose:
       (# <<SLOT movieDispose: doPart>> #);
     
     getDisplaySize: (* returns this(movie)'s es
                      * size *)
       (# width, height: @integer;
       <<SLOT movieGetDisplaySize: doPart>>
       exit (width,height)
       #);          
     
     goToBeginning: (* positions the movie at the beginning *)
       (# <<SLOT movieGoToBeginning: doPart>> #);
     
     goToEnd: (* positions the movie at the end *)
       (# <<SLOT movieGoToEnd: doPart>> #);
     
     time:
       (* Evaluate the enter part to go to the specified position in
        * the movie (position the movie at the specified time).
        * Evaluate the exit part to obtain the current position
        * (current time).
        *)
       (# t: @integer;
       enter  (# enter t <<SLOT movieSetTime: doPart>> #)
       exit  (# <<SLOT movieGetTime: doPart>> exit t #)
       #);
     
     private: 
       @<<SLOT moviePrivate: descriptor>>;
     
  enter  (# name: @text; enter name <<SLOT movieEnter: doPart>>  #)
  exit  (# name: @text; <<SLOT movieExit: doPart>> exit name #)
     
  #) (* movie *);

movieFile:
  (# <<SLOT aviMovieFileLib: attributes>>;
     
     name:
       (* The file name of this(movieFile).
        * Evaluating the exit part returns the full path name.
        *)
       (# fileName: @text;
       enter  (# enter fileName <<SLOT movieFileSetName: doPart>> #)
       exit  (# <<SLOT movieFileGetName: doPart>> exit fileName #)
       #);
     
     exists: booleanValue
       (# <<SLOT movieFileExists: doPart>> #);
     
     getFile:
       (* presents the user with a dialog for chosing a file for
        * opening. The dialog box contains a file preview (not the
        * same as the movie's preview). After completion of getFile,
        * the file can be opened with openRead.
        * If the user cancels the dialog, false is returned in
        * userConfirmed.
        *)
       (# userConfirmed: @boolean;
       <<SLOT movieFileGetFile: doPart>>
       exit  userConfirmed
       #);
     
     getMovie:
       (# newMovie: @movie;
       <<SLOT movieFileGetMovie: doPart>>
       exit newMovie
       #);          
     
     openRead: (* opens this(movieFile) for reading only *)
       (# <<SLOT movieFileOpenRead: doPart>> #);
     
     close:
       (# <<SLOT movieFileClose: doPart>> #);          
     
     private: 
       @<<SLOT movieFilePrivate: descriptor>>;
  #);

movieView: windowItem
  (* a movieView is a windowItem in which movies can be played. *)
  (# <<slot avimovieViewLib: attributes>>;
     
     getDisplaySize: (* returns the size of the movie in this movieView. *)
       (# width, height: @integer;
       <<SLOT movieViewGetDisplaySize: doPart>>
       exit (width,height)
       #);
     
     getMovieName:
       (# theName: ^text;
       <<SLOT movieViewGetMovieName: doPart>>
       exit theName[]
       #);
     
     contents:
       (* reference to the movie in this(moveiField).
        * Used for entering a movie into this(movieView).
        *)
       (#
       enter  (# theNewMovie: ^movie;
              enter theNewMovie[]
              <<SLOT movieViewSetContents: doPart>>
              #)
       exit  theMovie[]
       #);
     
     start:
       (* starts playing the movie from its current position if the
        * field contains a movie. Otherwise start is a no-operation.
        * If duration is 0 the movie is played to the end.
        *)
       (# duration: @integer;
       enter duration
       <<SLOT movieViewStart: doPart>> 
       #);
     
     stop: (* stops playing the movie, but retains the position *)
       (# <<SLOT movieViewStop: doPart>> #);
     
     goToBeginning: (* positions the movie at the beginning *)
       (# <<SLOT movieViewGoToBeginning: doPart>> #);
     
     goToEnd: (* positions the movie at the end *)
       (# <<SLOT movieViewGoToEnd: doPart>> #);
     
     time:
       (* Evaluate the enter part to go to the specified position in
        * the movie (position the movie at the specified time).
        * Evaluate the exit part to obtain the current position
        * (current time).
        *)
       (# t: @integer;
       enter  (# enter t <<SLOT movieViewSetTime: doPart>> #)
       exit  (# <<SLOT movieViewGetTime: doPart>> exit t #)
       #);
     
     duration:
       (* returns the duration of the movie in this(movieView), expressed in
        * this(movie)'s time coordinate system. The measuring unit of
        * the coordinate system is determined by the timeScale, q.v.
        *)
       (# movieDuration: @integer;
       <<SLOT movieViewGetDuration: doPart>>
       exit movieDuration
       #);
     
     open::< 
       (# create::< (# <<SLOT movieViewCreate: doPart>> #);
       <<SLOT movieViewOpen: doPart>> 
       #);
     
     close::< 
       (# <<SLOT movieViewClose: doPart>> #);
     
     eventHandler::<
       (# onMouseDown::< 
            (# <<SLOT movieViewMouseDown: doPart>> #);
          onMouseUp::< 
            (# <<SLOT movieViewMouseUp: doPart>> #);
          onRefresh::< 
            (# <<SLOT movieViewRefresh: doPart>> #);
          onFrameChanged::<
            (# <<SLOT movieViewOnFrameChanged: doPart>> #);
          
          mediaChanged: event
            (# newName: ^text;
            enter newName[]
            do inner 
            #);
          onMediaChanged:< mediaChanged;
       #);
     
     theMovie: ^movie;
     
     mfPrivate: @<<SLOT movieViewPrivate: descriptor>>;
     
  #) (* end of movieView *);
