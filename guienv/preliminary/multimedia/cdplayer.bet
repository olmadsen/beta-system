ORIGIN '~beta/guienv/guienv';
BODY 'private/cdplayerbody';

-- guienvLib: attributes --

absoluteTime:
	(* Absolute time is specified in minutes, seconds and frames. 
	 * The ranges is:
	 *      min:       0 - 99
	 *      sec:       0 - 59
	 *      frame:     0 - 74 
	 * one frame is 1/74 of a second.
	 *)
	(# min: @integer;
		sec: @integer;
		frame: @integer;
	#);

cdPlayer:
	(* This is an interface to controlling the CD player
	 * build into the computer.
	 *)
	(# <<SLOT cdPlayerLib: attributes>>;
		cdPresent:
		  (* Tells if a cd is present in the cd drawer. *)
		  (# isCDPresent: @boolean;
		  <<SLOT cdPlayerIsCDPresent: doPart>>
		  exit isCDPresent
		  #);
		play:
			(* Play starting at the specified track number. *)
			(# track: @integer;
			enter track
			<<SLOT cdPlayerStart: doPart>>
			#);
		stop:
			(* Stop playing, rewinding to the start of the CD *)
			(#
			<<SLOT cdStop: doPart>>
			#);
		pause:
		   (* Pause playing. *)
			(#
			<<SLOT cdPause: doPart>>
			#);
		resume:
			(* Resume playing. *)
			(#
			<<SLOT cdPlayerResume: doPart>>
			#);
		length:
			(* Return the total running time of the current CD in absolute time. *)
			(# theLength: ^absoluteTime
			<<SLOT cdPlayerLength: doPart>>
			exit theLength[]
			#);
		position:
			(* This is the current position in absolute time. *)
			(# pos: ^absoluteTime;
			enter (# enter pos[] <<SLOT cdPlayerSetPosition: doPart>> #)
			exit (# <<SLOT cdPlayerGetPosition: doPart>> exit pos[] #)
			#);
		numberOfTracks:
			(* Return the number of tracks on the current CD. *)
			(# tracks: @integer;
			<<SLOT cdPlayerNumberOfTracks: doPart>>
			exit tracks
			#);
		private: @<<SLOT cdPlayerPrivate: descriptor>>;
	#);
