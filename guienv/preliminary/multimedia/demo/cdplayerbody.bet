ORIGIN 'cdplayer';
INCLUDE
'~beta/guienv/v1.6/controls'
'~beta/guienv/v1.6/scrolllists'
'~beta/guienv/v1.6/stddialogs'
'../cdaudiodevice';

-- cdPlayerWindowPrivate: Descriptor --
(#
   theCDDevice: @cdAudioDevice
     (# deviceError::
          (# 
          do (none,errMsg[],none) -> alertUser;
             terminate;
          #);
        
        allocationError::
          (# 
          do (none,errMsg[],none) -> alertUser;
             terminate;
          #);
     #);
   
   isPlaying: @boolean;
   selectedTrack: @integer;
   
   doubleClickAction:
     (# 
     enter selectedTrack
     do selectedTrack -> theCDDevice.play;
        stopButton.enable;
        playButton.disable;
     #);
   
   playButton: @pushButton
     (#
        open::<
          (# 
          do
             true->bindBottom;
             false->bindTop;
             'Play'->label;
             (60,20)->size;
             (39,224)->position;
          #);
        eventHandler::
          (# onMouseUp::
               (# 
               do (if (selectedTrack > 0) 
                      and 
                      (selectedTrack <= theCDDevice.noOfTracks) then
                      selectedTrack -> theCDDevice.play;
                      true -> isPlaying;
                      stopButton.enable;
                      disable;
                  if);
               #);
          #);
     #);
   stopButton: @pushButton
     (#
        open::<
          (# 
          do
             true->bindBottom;
             false->bindTop;
             'Stop'->label;
             (60,20)->size;
             (122,224)->position;
             disable;
          #);
        
        eventHandler::
          (# onMouseUp::
               (# 
               do theCDDevice.stop;
                  false -> isPlaying;
                  disable;
                  playButton.enable;
               #);
          #);
     #);
   tracksScrollList: @textScrollList
     (#
        open::<
          (# 
          do
             true->bindBottom;
             true->bindRight;
             (244,200)->size;
             (247,44)->position;
          #);
        
        addTracks:
          (# noOfTracks: @integer;
             theText: @text;
          enter noOfTracks
          do 
             (if noOfTracks > 0 then
                 1 -> selectedTrack;
                 noOfTracks -> append;
                 (for i: noOfTracks repeat
                      theText.clear;
                      'Track ' -> theText.append;
                      i -> theText.putint;
                      (i,theText[]) -> SetText;
                 for);
             if);
          #);
        
        eventHandler::
          (# 
             onMouseUp::
               (# theSelection: ^text;
               do (if doubleclick then
                      Selection.first -> selectedTrack -> doubleClickAction;
                   else
                      Selection.first -> selectedTrack;
                  if);
               #);
          #);
     #);
   
   tracks_label: @staticText
     (#
        open::<
          (#  do 'Tracks:'->label; (36,13)->size; (247,13)->position;  #)
     #)
#)  

-- cdPlayerWindowOpen: DoPart --
do 'CD Audio Player' -> title;
   (519,274)->size;
   private.playButton.open;
   private.stopButton.open;
   private.tracksScrollList.open;
   private.tracks_label.open;
   
   private.theCDDevice.open;
   private.theCDDevice.noOfTracks -> private.tracksScrollList.addTracks;
   INNER  

