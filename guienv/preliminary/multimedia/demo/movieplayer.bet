ORIGIN '~beta/guienv/v1.6/guienv'; 
BODY 'moviePlayerBody';
INCLUDE 
'../movie';

-- guienvLib: attributes --

moviePlayer: window
  (# 
     theFileId: ^text;          
     
     (* PlayUserSelectedMovie lets the user choose a movie file.
      * The filename is stored in theField.
      * If the user cancels the file dialog, PlayUserSelectedMovie does 
      * nothing.
      *)
     PlayUserSelectedMovie:
       (# <<SLOT MPPlayUserSelectedMovie: doPart>> #);
     
     PlayMovieByFileId:
       (#
       enter theFileId[]
       <<SLOT MPPlayMovieByFileId: doPart>>
       #);
     
     getFileId: 
       (# 
       <<SLOT MPgetFileId: doPart>>
       exit theFileId[] 
       #);
     
     runMovieInterval:	(* getSelectedText *) 
       (# start,duration: @integer;
       enter (start,duration)
       <<SLOT MPRunMovieInterval: doPart>>
       #);     
     
     open::< 
       (# <<SLOT MPOpen: doPart>> #);
     
     close::< 
       (# <<SLOT MPClose: doPart>> #);
     
     private: @<<SLOT MPPrivate: Descriptor>>;
     
  #);


-- program: descriptor --
guienv
(# myMoviePlayer: @moviePlayer
     (# menubarType::<
          (# fileMenu: @menu
               (# openItem: @menuitem
                    (# eventHandler:: 
                         (# onSelect::
                              (# 
                              do PlayUserSelectedMovie;
                              #) 
                         #);
                       open::< (# do 'Open' -> name; 'O' -> key; #);
                    #);
                  exitItem: @menuitem
                    (# eventHandler:: 
                         (# onSelect::
                              (# do terminate; #) 
                         #);
                       open::< (# do 'Exit' -> name #);
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       openItem.open; openItem[] -> append;
                       exitItem.open; exitItem[] -> append;
                    #);
               #);
             open::< (# do fileMenu.open; fileMenu[] -> append #);
          #);
        eventHandler::
          (# onAboutToClose:: (# do terminate; #); #);
     #);
do myMoviePlayer.open
#)
