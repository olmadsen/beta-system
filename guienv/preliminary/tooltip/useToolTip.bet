ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE 
'tooltip'
'~beta/guienv/v1.6/controls'
'~beta/guienv/v1.6/graphics'
'~beta/guienv/v1.6/utils/guienvadds'
'~beta/guienv/v1.6/utils/rasteradds'
'../resources/pixmaps';

--PROGRAM: descriptor--
guienv
(# mainWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# openItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  #);
                         #);
                       open:: (# do 'Open' -> name #)
                    #);
                  
                  deleteItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  mainWindow.myCanvas.deleteToolTip; #);
                         #);
                       open:: (# do 'Delete tool tip' -> name #)
                    #);
                  
                  setDelayItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  mainWindow.myCanvas.setDelayTime; #);
                         #);
                       open:: (# do 'Set Delay Time' -> name #)
                    #);
                  
                  quitItem: @menuItem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# 
                              do mainWindow.theEventHandler.onAboutToClose;
                              #);
                         #);
                       open:: 
                         (# do 'Quit' -> name #)
                    #);
                  open::
                    (#
                    do '&File' -> name;
                       openItem.open; openItem[] -> append;
                       deleteItem.open; deleteItem[] -> append;
                       setDelayItem.open; setDelayItem[] -> append;
                       quitItem.open; quitItem[] -> append
                    #);
               #);
             
             open:: 
               (# 
               do fileMenu.open; fileMenu[] -> append;
               #);
          #); (* menubarType *)
        
        myCanvas: @canvas
          (# tip: @toolTipControl;
             
             deleteToolTip:
               (# 
               do myBtn.myTool[] -> tip.delete;
                  myRectTool2[] -> tip.delete;
               #);
             
             setDelayTime:
               (# 
               do (tip.AUTOPOP,30000) -> tip.setDelayTime;
               #);
             
             myBtn: @pushButton
               (# myTool: @Tool;
                  open::
                    (# 
                    do (150,25) -> size;
                       (10,50) -> position;
                       'Ok' -> label;
                       myBtn[] -> myTool.theWindowItem[];
                       'Ok button' -> myTool.theTip[];
                    #);
                  
               #);
             
             myRectTool1: @rectTool;
             myRect1: @rectangle;
             myRectTool2: @rectTool;
             myRect2: @rectangle;
             open::
               (# 
               do mainWindow.size -> size;
                  TRUE -> bindBottom -> bindRight;
                  tip.open;
                  myBtn.open;
                  myBtn.myTool[] -> tip.add;
                  
                  myCanvas[] -> myRectTool1.theWindowItem[];
                  'Hello Joe' -> myRectTool1.theTip[];
                  ((0,0),(4,4)) -> myRect1;
                  myRect1[] -> myRectTool1.theRect[];
                  
                  myRectTool1[] -> tip.add;
                  
                  myCanvas[] -> myRectTool2.theWindowItem[];
                  'World' -> myRectTool2.theTip[];
                  ((50,4),(54,8)) -> myRect2;
                  myRect2[] -> myRectTool2.theRect[];
                  
                  myRectTool2[] -> tip.add;
               #);
             
             close::
               (# 
               do tip.close;
               #);
             
             eventHandler::
               (# onRefresh::
                    (# 
                    do graphics
                       (# 
                       do myRect1 -> drawRect;
                          myRect2 -> drawRect;
                       #);
                    #);
               #);
          #);
        
        open::
          (# 
          do (700,300) -> size; 
             myCanvas.open;
          #);
        eventHandler::
          (# onAboutToClose::
               (# do terminate; #);
          #);
     #);
do mainWindow.open;
#)
