ORIGIN '../tooltipBody';
INCLUDE '~beta/Xt/v1.10/xtlib.bet';
INCLUDE '~beta/guienv/v1.6/private/X11/guienv_unixprivate';
BUILD default '$$/LiteClue.o' 'external/LiteClue.c' 
              '$CC -c -o $0 $1';

-- GUIENVtooltipLib: attributes --
putintline:
  (# i:@integer;
  enter i
  do i->putint;
     newline;
  #);

-- GUIENVtooltipSetDelayTime: doPart --
do (# r:@arglist
   do
      (if durationType
       // INITIAL then
          (1,tooltippriv.XcgNwaitperiod,delay)->r.set;
          (tooltippriv.toolTipWidget,r[],1)->XtSetValues;
       // RESHOW then
          (1,tooltippriv.XcgNcancelWaitPeriod,delay)->r.set;
          (tooltippriv.tooltipWidget,r[],1)->XtSetValues;
       else
          'Only INITIAL and RESHOW supported on X!'->putline;
      if);
   #);

-- GUIENVtoolTipControlAdd: doPart --
do    
   (if thetool##=rectTool## then
       'Rectangle tools not supported, only whole windowitems!'->putline;
    else
       (tooltippriv.toolTipWidget,theTool.theWindowItem.widgetID,
       theTool.theTip,0,0)->tooltippriv.XcgLiteClueAddWidget;
   if);
   
-- GUIENVtoolTipControlDelete: doPart --
do 
   
-- GUIENVtooltipOpen: doPart --
do tooltippriv.initialize;
   
-- GUIENVtooltipClose: doPart --
do 
   
-- tooltipPrivAttr: descriptor --
(# 
   XcgNwaitperiod: integerobject
     (# do 'waitPeriod'->charReptoCString->value #);
   XcgNcancelWaitPeriod: integerobject
     (# do 'cancelWaitPeriod'->charReptoCString->value #);
   initialize:
     (# r:@arglist;
        t:@text;
        widget:@integer;
        white:@integer;
        cmap,screen,disp:@integer;
        thecolor:@Xcolor;
     enter (widget,t)
     do
        ('popup_shell',xcgLiteClueWidgetClass,this(window).widgetID,r[],0)
          ->XtCreatePopupShell->ToolTipWidget;
        this(window).displayID->disp;
        disp->XDefaultScreen->screen;
        (disp,screen)->XDefaultColorMap->cmap;
        254*256->thecolor.red;
        255*256->thecolor.green;
        0->thecolor.blue;
        (disp,cmap,thecolor[])->XAllocColor->white;
        (if white = 0 then 
            'Could not allocate color'->putline;
         else
            theColor.pixel->white;
        if);
        (1,XtNbackground,white)->r.set;
        (toolTipWidget,r[],1)->XtSetValues;
         
     #);
        
   (* Function supplied by widget. Add another widget for watching events *)
   XcgLiteClueAddWidget:external
     (# 
        widget:@integer;
        watch:@integer;
        print:[1]@char;
        s,o:@integer;
     enter(widget,watch,print,s,o)
     do callC
     #);
   
   xcgLiteClueWidgetClass:external
     (# val:@integer;
     do 'getLiteClueWidgetClass'->callC
     exit val
     #);
          
   toolTipWidget:@integer;
   
#)
