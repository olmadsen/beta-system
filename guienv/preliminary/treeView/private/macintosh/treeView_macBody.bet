ORIGIN '../treeViewBody';

INCLUDE
	'~beta/guienv/v1.6/controls'
	'~beta/guienv/v1.6/graphics'
	'~beta/guienv/v1.6/private/datastructures/sequence';

INCLUDE
	'~beta/sysutils/v1.6/objinterface';
	
-- TreeViewLib: attributes --

white: (# exit (0xFFFF,0xFFFF,0xFFFF) #);
light_gray: (# exit (0xCCCC,0xCCCC,0xCCCC) #);

leftMargin: (# exit 5 #);
topMargin: (# exit 5 #);
indent: (# exit 18 #);
inset: (# exit 7 #);
lineHeight: (# exit 16 #);
togglerSize: (# exit 9 #);

treeItemList: sequence
  (# element:: treeItem;
  #);
  
collapsedState: (# exit 1 #);
expandedState: (# exit 2 #);

toggler: windowItem
	(# state: @integer;
		center:
			(# x, y: @integer;
				width, height: @integer;
			enter (x, y)
			do size -> (width, height);
				x - width div 2 -> x;
				y - height div 2 -> y;
				(x, y) -> position;
			#);
	   open::<
			(#
			do expandedState -> state;
				white -> backgroundColor;
				(togglerSize, togglerSize) -> size;
				inner;
			#);
		eventHandler::<
			(# onCollapse:<
					(#
					do INNER;
					#);
				onExpand:<
					(#
					do INNER;
					#);
				onMouseDown::
					(#
					do (if state
							//collapsedState then
								onExpand;
								expandedState -> state;
								update;
							//expandedState then
								onCollapse;
								collapsedState -> state;
								update;
						if);
					#);
				onRefresh::
					(#
					do graphics
							(# width, height: @integer;
							do size -> (width, height);
								(2, height div 2) -> moveTo;
								(width - 3, height div 2) -> drawTo;
								(if state = collapsedState then
									(width div 2, 2) -> moveTo;
									(width div 2, height - 3) -> drawTo;
								if);
								patterns.gray[] -> pen.stipple;
								((0, 0), (width, height)) -> drawRect;
							#);
					#);
			#);
	#);
imageView: windowItem
	(# icon:
			(#
			enter (# enter theIcon[] do update #)
			exit theIcon[]
			#);
		eventHandler::<
			(# onRefresh::<
					(#
					do graphics
							(# width, height: @integer;
								iconWidth, iconHeight: @integer;
								x, y: @integer;
							do (if theIcon[] <> NONE then
									size -> (width, height);
									16 -> iconWidth;
									16 -> iconHeight;
									(width - iconWidth) div 2 -> x;
									(height - iconHeight) div 2 -> y;
									(theIcon[],(0, 0), (x, y), iconWidth, iconHeight) -> drawRaster;
								if);
							#);
						INNER;
					#);
			#);
		theIcon: ^pixmap;
	#);
changed:
	(# compute:
			(# items: ^treeItemList;
				father: ^treeItem;
				from, to: @point;
			enter (father[], items[])
			do items.scan
					(#
					do (x, y) -> current.position;
					   (if father[] <> NONE then
							father.bottomAnchor -> from;
							current.leftAnchor -> to;
							(from.h, to.v) -> current.tiPrivate.theToggler.center;
						else
							current.tiPrivate.theToggler.hide;
						if);
						y + lineHeight -> y;
						(if not current.collapsed then
							x + indent -> x;
							(current[], current.tiprivate.children[]) -> &compute;
							x - indent -> x;
						if);
					#);
			#);
		x, y: @integer;
	do (leftMargin, topMargin) -> (x, y);
		(none, tvprivate.children[]) -> compute;
		update;
	#);
signalBeforeOpenError:
	(# what:<
			(# t: ^text;
			do INNER;
			exit t[]
			#);
		msg: ^text;
	do &text[] -> msg[];
		'Attempt to ' -> msg.putText;
		(if what = NONE then
			'use' -> msg.putText;
		else
			what -> msg.putText;
			' in' -> msg.putText;
		if);
		' treeView "' -> msg.putText;
		this(treeView)[] -> getPatternName -> msg.append;
		'" before calling open' -> msg.append;
		msg[] -> apiError;
	#);

-- treeitemLib: attributes --
signalBeforeOpenError:
	(# what:<
			(# t: ^text;
			do INNER;
			exit t[]
			#);
		msg: ^text;
	do &text[] -> msg[];
		'Attempt to ' -> msg.putText;
		(if what = NONE then
			'use' -> msg.putText;
		else
			what -> msg.putText;
			' in' -> msg.putText;
		if);
		' treeItem "' -> msg.putText;
		this(treeItem)[] -> getPatternName -> msg.append;
		'" before calling open' -> msg.append;
		msg[] -> apiError;
	#);
	

hasChildren:
	(# exit (tiprivate.children.size > 0) #);
hasIcon:
	(# exit (tiprivate.notSelectedIcon[] <> NONE) #);
position:
	(# x, y: @integer;
	enter (x, y)
	do (if hasIcon then
			(x + 18, y) -> tiprivate.theStaticText.position;
			(x, y) -> tiprivate.theImageView.position;
		else
			(x, y) -> tiprivate.theStaticText.position;
		if);
	#);
bottomAnchor:
  (# x, y: @integer;
     width, height: @integer;
  do tiprivate.leader.position -> (x, y);
     tiprivate.leader.size -> (width, height);
	  x + inset -> x;
	  y + height -> y;
  exit (x, y)
  #);
leftAnchor:
  (# x, y: @integer;
     width, height: @integer;
  do tiprivate.leader.position -> (x, y);
     tiprivate.leader.size -> (width, height);
	  y + height div 2 -> y;
  exit (x, y)
  #);
selectedState:
	(#
	do light_gray -> tiprivate.theStaticText.backgroundColor;
	#);
unselectedState:
	(#
	do white -> tiprivate.theStaticText.backgroundColor;
	#);
collapsed:
	(#
	enter tiprivate.collapsed
	exit tiprivate.collapsed
	#);
hideRecursive:
	(#
	do scan
			(#
			do current.hide;
			   (if not current.collapsed then
					current.hideRecursive;
				if);
			#);
	#);
showRecursive:
	(#
	do scan
			(#
			do current.show;
			   (if not current.collapsed then
					current.showRecursive;
				if);
			#);
	#);
hide:
	(#
	do tiprivate.theStaticText.hide;
		(if hasIcon then
			tiprivate.theImageView.hide;
		if);
		(if hasChildren then
			tiprivate.theToggler.hide;
		if);
	#);
show:
	(#
	do tiprivate.theStaticText.show;
		(if hasIcon then
			tiprivate.theImageView.show;
		if);
		(if hasChildren then
			tiprivate.theToggler.show;
		if);
	#);
toggle:
	(#
	do (if collapsed then
			expand;
		else
			collapse;
		if);
	#);
-- GUIEnvTreeItemSetName: doPart --
do (if tiprivate.initialized then
      theName[] -> tiprivate.theStaticText.label;
   else
      signalBeforeOpenError;
   if);

-- GUIEnvTreeItemGetName: doPart --
do (if tiprivate.initialized then
      tiprivate.theStaticText.label -> theName[];
   else
      signalBeforeOpenError;
   if);
   
-- GUIEnvTreeItemSetIcons: doPart --
do notSelectedIcon[] -> tiprivate.notSelectedIcon[];
	notSelectedIcon[] -> tiprivate.theImageView.icon;
	tiprivate.theStaticText.position -> tiprivate.theImageView.position;
	
	(18, 0) -> tiprivate.theStaticText.move;
	tiprivate.theImageView.show;
	tiprivate.theImageView[] -> tiprivate.leader[];
   
-- GUIEnvTreeItemGetIcons: doPart --
do 'GUIEnvTreeItemSetIcons: doPart' -> putLine;
   
-- GUIEnvTreeItemAppend: doPart --
do (if tiprivate.initialized then
		(if not hasChildren then
			tiprivate.theToggler.show;
		if);
      theItem.open;
      theItem[] -> tiprivate.children.append;
   else
      signalBeforeOpenError;
   if);


-- GUIEnvTreeItemPrepend: doPart --
do (if tiprivate.initialized then
      theItem[] -> tiprivate.children.prepend;
   else
      signalBeforeOpenError;
   if);


-- GUIEnvTreeItemInsert: doPart --
do 'GUIEnvTreeItemInsert: doPart' -> putLine;
   
-- GUIEnvTreeItemDelete: doPart --
do 'GUIEnvTreeItemDelete: doPart' -> putLine;

-- GUIEnvTreeItemCreate: doPart --
do inner;

-- GUIEnvTreeItemOpen: doPart --
do tiprivate.children.init;
	tiprivate.theImageView.open;
   tiprivate.theStaticText.open;
	tiprivate.theToggler.open;
	tiprivate.theStaticText[] -> tiprivate.leader[];
   true -> tiprivate.initialized;
   inner;
   
-- GUIEnvTreeItemScan: doPart --
do (if tiprivate.initialized then
      tiprivate.children.scan
			(#
			do current[] -> this(scan).current[];
				inner scan;
			#);
   else
      signalBeforeOpenError;
   if);

-- GUIEnvTreeItemSelect: doPart --
do (if tvprivate.selected[] <> this(treeItem)[] then
		(if tvprivate.selected[] <> none then
			tvprivate.selected.unselectedState;
			NONE -> tvprivate.selected[];
		if);
		this(treeItem)[]  -> tvprivate.selected[];
		selectedState;
		onSelect;
	if);

-- GUIEnvTreeItemDeSelect: doPart --
do 'GUIEnvTreeItemSelect: doPart' -> putLine;

-- GUIEnvTreeItemExpand: doPart --
do (if collapsed then
	   showRecursive;
		false -> collapsed;
		changed;
   if);

   
-- GUIEnvTreeItemCollapse: doPart --
do (if not collapsed then

		'======== COLLAPSE ========' -> putLine;
	   hideRecursive;
		true -> collapsed;
		changed;
		'======== DONE ========' -> putLine;
   if);
   

-- GUIEnvTreeItemPrivate: descriptor --
(# initialized: @boolean;
   collapsed: @boolean;
	father: ^treeItem;
   children: @treeItemList;
	notSelectedIcon: ^pixmap;
	theToggler: @toggler
		(# eventHandler::
				(# onExpand::
						(#
						do this(treeItem).expand;
						#);
					onCollapse::
						(#
						do this(treeItem).collapse;
						#);
				#);
			open::
				(#
				do hide;
				#);
		#);
	leader: ^windowItem;
	theImageView: @imageView
		(# open::<
				(#
				do (16, 16) -> size;
					white -> backgroundColor;
					hide;
				#);
			eventHandler::
				(# onMouseDown::
						(#
						do this(treeItem).select;
						#);
				#);
		#);
   theStaticText: @staticText
		(# open::
				(#
				do white -> backgroundColor;
				#);
			eventHandler::
				(# onLabelChanged::
						(#
						do fitToContents;
						#);
					onMouseDown::
						(#
						do this(treeItem).select;
						#);
				#);
		#);
#)


-- GUIEnvTreeViewAppend: doPart --
do (if tvprivate.initialized then
      theItem.open;
      theItem[] -> tvprivate.children.append;
	else
	  signalBeforeOpenError;
   if);
   
-- GUIEnvTreeViewPrepend: doPart --
do (if tvprivate.initialized then
      theItem[] -> tvprivate.children.prepend;
	else
	  signalBeforeOpenError;
   if);
   
-- GUIEnvTreeViewInsert: doPart --
do 'GUIEnvTreeViewInsert: doPart' -> putLine;

-- GUIEnvTreeViewScan: doPart --
do (if tvprivate.initialized then
      tvprivate.children.scan
	    (#
		do current[] -> this(scan).current[];
		   inner scan;
		#);
	else
	  signalBeforeOpenError;
   if);
   
-- GUIENVTreeViewSelectionFirst: doPart --
do 'GUIENVTreeViewSelectionFirst: doPart' -> putLine;


-- GUIEnvTreeViewNoOfElms: doPart --
do (if tvprivate.initialized then
      tvprivate.children.size -> value;
	else
	  signalBeforeOpenError;
   if);
   
-- GUIEnvTreeViewClear: doPart --
do 'GUIEnvTreeViewClear: doPart' -> putLine;

-- GUIenvTreeViewCreate: doPart --
do inner create;
   
   
-- GUIenvTreeViewOpen: doPart --
do white -> backgroundColor;
   tvprivate.children.init;
   true -> tvprivate.initialized;
   inner;   
   changed;
   
-- GUIenvTreeViewClose: doPart --
do false -> tvprivate.initialized;
   inner;
   
-- GUIenvTreeViewPrivate: descriptor --
(# children: @treeItemList;
   initialized: @boolean;
	selected: ^treeItem;
#)

-- GUIEnvTreeViewEventHandlerOnMouseUp: doPart --
do inner;

-- GUIEnvTreeViewEventHandlerOnMouseDown: doPart --
do inner;

--treeViewOnRefresh: doPart --
do graphics
		(# drawConnector:
			  (# from, to: @point;
			  enter (from, to)
			  do from -> moveTo;
			  	  (from.h, to.v) -> drawTo;
				  to -> drawTo;
			  #);
			draw:
			  (# from: @point;
			  	  to: ^treeItemList;
			  enter (from, to[])
			  do to.scan
			       (#
					 do (if not current.collapsed then
					 	    (current.bottomAnchor, current.tiprivate.children[]) -> &draw;
						  if);
					    (from, current.leftAnchor) -> drawConnector;
					 #);
			  #);
		do patterns.gray[] -> pen.stipple;
			tvprivate.children.scan
		     (#
			  do (if not current.collapsed then
			  			(current.bottomAnchor, current.tiprivate.children[]) -> &draw;
				  if);
			  #);
		#);
		
