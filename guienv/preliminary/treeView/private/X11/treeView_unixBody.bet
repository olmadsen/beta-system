ORIGIN '../treeViewBody';

INCLUDE
       '~beta/guienv/controls'
       '~beta/guienv/graphics'
       '~beta/guienv/private/datastructures/sequence';


-- treeitemLib: attributes --

position:
  (# x, y: @integer;
  enter (x, y)
  do (x, y) -> tiprivate.theStaticText.position;
  #);
bottomAnchor:
  (# x, y: @integer;
     width, height: @integer;
  do tiprivate.theStaticText.position -> (x, y);
     tiprivate.theStaticText.size -> (width, height);
	  x + 5 -> x;
	  y + height -> y;
  exit (x, y)
  #);
leftAnchor:
  (# x, y: @integer;
     width, height: @integer;
  do tiprivate.theStaticText.position -> (x, y);
     tiprivate.theStaticText.size -> (width, height);
	  y + height div 2 -> y;
  exit (x, y)
  #);
selectedState:
	(#
	do light_gray -> tiprivate.theStaticText.backgroundColor;
	#);
unselectedState:
	(#
	do white -> tiprivate.theStaticText.backgroundColor;
	#);
collapsed:
	(#
	enter tiprivate.collapsed
	exit tiprivate.collapsed
	#);
hideRecursive:
	(#
	do scan
			(#
			do current.hide;
			   (if not current.collapsed then
					current.hideRecursive;
				if);
			#);
	#);
showRecursive:
	(#
	do scan
			(#
			do current.show;
			   (if not current.collapsed then
					current.showRecursive;
				if);
			#);
	#);
hide:
	(#
	do tiprivate.theStaticText.hide;
	#);
show:
	(#
	do tiprivate.theStaticText.show;
	#);
toggle:
	(#
	do (if collapsed then
			expand;
		else
			collapse;
		if);
	#);
-- GUIEnvTreeItemSetName: doPart --
do (if tiprivate.initialized then
      theName[] -> tiprivate.theStaticText.label;
   else
      'Setting the name of a treeItem before calling open' -> apiError;
   if);

-- GUIEnvTreeItemGetName: doPart --
do (if tiprivate.initialized then
      tiprivate.theStaticText.label -> theName[];
   else
      'Getting the name of a treeItem before calling open' -> apiError;
   if);
   
-- GUIEnvTreeItemSetIcons: doPart --
do 'GUIEnvTreeItemSetIcons: doPart' -> putLine;
   
-- GUIEnvTreeItemGetIcons: doPart --
do 'GUIEnvTreeItemSetIcons: doPart' -> putLine;
   
-- GUIEnvTreeItemAppend: doPart --
do (if tiprivate.initialized then
      theItem.open;
      theItem[] -> tiprivate.children.append;
   else
      'Appending to a treeItem before calling open' -> apiError;
   if);


-- GUIEnvTreeItemPrepend: doPart --
do (if tiprivate.initialized then
      theItem[] -> tiprivate.children.prepend;
   else
      'Prepending to a treeItem before calling open' -> apiError;
   if);


-- GUIEnvTreeItemInsert: doPart --
do 'GUIEnvTreeItemInsert: doPart' -> putLine;
   
-- GUIEnvTreeItemDelete: doPart --
do 'GUIEnvTreeItemDelete: doPart' -> putLine;

   
   
-- GUIEnvTreeItemCreate: doPart --
do inner;

-- GUIEnvTreeItemOpen: doPart --
do tiprivate.children.init;
   tiprivate.theStaticText.open;
   true -> tiprivate.initialized;
   inner;
   
-- GUIEnvTreeItemScan: doPart --
do (if tiprivate.initialized then
      tiprivate.children.scan
	    (#
		do current[] -> this(scan).current[];
		   inner scan;
		#);
   else
      'Scanning a treeItem before calling open' -> apiError;
   if);

-- GUIEnvTreeItemSelect: doPart --
do (if tvprivate.selected[] <> this(treeItem)[] then
		(if tvprivate.selected[] <> none then
			tvprivate.selected.unselectedState;
			NONE -> tvprivate.selected[];
		if);
		this(treeItem)[]  -> tvprivate.selected[];
		selectedState;
		onSelect;
	if);

-- GUIEnvTreeItemDeSelect: doPart --
do 'GUIEnvTreeItemSelect: doPart' -> putLine;

-- GUIEnvTreeItemExpand: doPart --
do (if collapsed then
	   showRecursive;
		false -> collapsed;
		changed;
   if);

   
-- GUIEnvTreeItemCollapse: doPart --
do (if not collapsed then
	   hideRecursive;
		true -> collapsed;
		changed;
   if);
   

-- GUIEnvTreeItemPrivate: descriptor --
(# initialized: @boolean;
   collapsed: @boolean;
   children: @treeItemList;
   theStaticText: @staticText
		(# open::
				(#
				do white -> backgroundColor;
				#);
			eventHandler::
				(# onLabelChanged::
						(#
						do fitToContents;
						#);
					onMouseDown::
						(#
						do this(treeItem).select;
							(if doubleClick then
								this(treeItem).toggle;
							if);
						#);
				#);
		#);
#)

-- TreeViewLib: attributes --

white: (# exit (0xFFFF,0xFFFF,0xFFFF) #);
light_gray: (# exit (0xCCCC,0xCCCC,0xCCCC) #);

treeItemList: sequence
  (# element:: treeItem;
  #);
  
changed:
  (# compute:
       (# items: ^treeItemList;
	   enter items[]
	   do items.scan
	        (#
			do (x, y) -> current.position;
				y + 20 -> y;
				(if not current.collapsed then
					x + 30 -> x;
					current.tiprivate.children[] -> &compute;
					x - 30 -> x;
				if);
			#);
	   #);
	 x, y: @integer;
  do (5, 5) -> (x, y);
     tvprivate.children[] -> compute;
	  update;
  #);

-- GUIEnvTreeViewAppend: doPart --
do (if tvprivate.initialized then
      theItem.open;
      theItem[] -> tvprivate.children.append;
	else
	  'Appending to a treeView before calling open' -> apiError;
   if);
   
-- GUIEnvTreeViewPrepend: doPart --
do (if tvprivate.initialized then
      theItem[] -> tvprivate.children.prepend;
	else
	  'Prepending to a treeView before calling open' -> apiError;
   if);
   
-- GUIEnvTreeViewInsert: doPart --
do 'GUIEnvTreeViewInsert: doPart' -> putLine;

-- GUIEnvTreeViewScan: doPart --
do (if tvprivate.initialized then
      tvprivate.children.scan
	    (#
		do current[] -> this(scan).current[];
		   inner scan;
		#);
	else
	  'Scanning a treeView before calling open' -> apiError;
   if);
   
-- GUIENVTreeViewSelectionFirst: doPart --
do 'GUIENVTreeViewSelectionFirst: doPart' -> putLine;


-- GUIEnvTreeViewNoOfElms: doPart --
do (if tvprivate.initialized then
      tvprivate.children.size -> value;
	else
	  'Getting noOfElms of a treeView before calling open' -> apiError;
   if);
   
-- GUIEnvTreeViewClear: doPart --
do 'GUIEnvTreeViewClear: doPart' -> putLine;

-- GUIenvTreeViewCreate: doPart --
do inner create;
   
   
-- GUIenvTreeViewOpen: doPart --
do white -> backgroundColor;
   tvprivate.children.init;
   true -> tvprivate.initialized;
   inner;   
   changed;
   
-- GUIenvTreeViewClose: doPart --
do false -> tvprivate.initialized;
   inner;
   
-- GUIenvTreeViewPrivate: descriptor --
(# children: @treeItemList;
   initialized: @boolean;
	selected: ^treeItem;
#)

-- GUIEnvTreeViewEventHandlerOnMouseUp: doPart --
do inner;

-- GUIEnvTreeViewEventHandlerOnMouseDown: doPart --
do inner;

--treeViewOnRefresh: doPart --
do graphics
		(# drawConnector:
			  (# from, to: @point;
			  enter (from, to)
			  do from -> moveTo;
			  	  (from.h, to.v) -> drawTo;
				  to -> drawTo;
			  #);
			draw:
			  (# from: @point;
			  	  to: ^treeItemList;
			  enter (from, to[])
			  do to.scan
			       (#
					 do (if not current.collapsed then
					 	    (current.bottomAnchor, current.tiprivate.children[]) -> &draw;
						  if);
					    (from, current.leftAnchor) -> drawConnector;
					 #);
			  #);
		do tvprivate.children.scan
		     (#
			  do (if not current.collapsed then
			  			(current.bottomAnchor, current.tiprivate.children[]) -> &draw;
				  if);
			  #);
		#);
		
