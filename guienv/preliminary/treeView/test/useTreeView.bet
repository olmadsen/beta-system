ORIGIN '../treeView';
RESOURCE ppcmac 'useTreeView.r';

INCLUDE '../../resources/pixmaps';

-- program: descriptor --
GUIenv
	(# theWindow: @window
			(# theTreeView: @treeView
					(# folderIcon: @pixmap;
						myTreeItem: treeItem
						  (# addItem:
								 (# theName: ^text;
									 theItem: ^treeItem;
								 enter theName[]
								 do &treeItem[] -> theItem[];
									 theItem[] -> append;
									 theName[] -> theItem.name;
								 #);
								onSelect::
									(#
									do name -> title;
									#);
						  #);
						contents: @treeItem
							(#
								animals: @myTreeItem
								  (# open::
										 (#
										 do (folderIcon[],folderIcon[],folderIcon[],folderIcon[]) -> icons;
										 	 'Animals' -> name;
											 'Dog' -> addItem;
											 'Cat' -> addItem;
											 'Horse' -> addItem;
										 #);
								  #);
								actors: @myTreeItem
								  (# open::
										 (#
										 do (folderIcon[],folderIcon[],folderIcon[],folderIcon[]) -> icons;
										 	 'Actors' -> name;
											 'Winona Ryder' -> addItem;
											 'Andie MacDowel' -> addItem;
											 'Michelle Pfeiffer' -> addItem;
										 #);
								  #);
								open::
								  (#
								  do 'Contents' -> name;
								  	   animals[] -> append;
										actors[] -> append;
								  #);
							#);
						open::
						  (#
						  do THIS(window).size -> size;
							  true -> bindRight;
							  true -> bindBottom;
							  ('folder', 128) -> folderIcon.loadIconResource;
							  contents[] -> append;
						  #);
					#);
			open::
				(#
				do 'Tree View Test' -> title;
					theTreeView.open;
				#);
			#);
	do theWindow.open;
	#)