ORIGIN '../treeView';
INCLUDE '~beta/basiclib/file'
'~beta/basiclib/directory';

-- windowLib: attributes --

directoryBrowser: treeView
  (# folderIcon, fileIcon: @pixmap;
     
     directoryItem: treeItem
       (# theDirectory:
            (# directoryName: ^text;
               d: @directory;
            enter directoryName[]
            do directoryName[] -> d.name;
               d.entry.path.name -> name;
               d.scanEntries
               (# 
               do select
                  (# whenDir::
                       (# theItem: ^directoryItem;
                       do &directoryItem[] -> theItem[];
                          theItem[] -> THIS(directoryItem).append;
                          foundFullPath -> theItem.theDirectory;
                       #);
                     whenFile::
                       (# theItem: ^fileItem;
                       do &fileItem[] -> theItem[];
                          theItem[] -> THIS(directoryItem).append;
                          foundFullPath -> theItem.theFile;
                       #);
                  #);
               #);
            #);
       #);
     fileItem: treeItem
       (# theFile:
            (# fileName: ^text;
               f: @file;
            enter fileName[]
            do fileName[] -> f.name;
               f.entry.path.name -> name;
            #);
       #);
     rootDirectory:
       (# rootName: ^text;
          d: @directory;
       enter rootName[]
       do rootName[] -> d.name;
          rootItem[] -> append;
          d.entry.path.name -> rootItem.name;
          d.scanEntries
          (# 
          do select
             (# whenDir::
                  (# theItem: ^directoryItem;
                  do &directoryItem[] -> theItem[];
                     theItem[] -> rootItem.append;
                     foundFullPath -> theItem.theDirectory;
                  #);
                whenFile::
                  (# theItem: ^fileItem;
                  do &fileItem[] -> theItem[];
                     theItem[] -> rootItem.append;
                     foundFullPath -> theItem.theFile;
                  #);
             #);
          #);
       #);
     rootItem: @treeItem;
  #);

-- program: descriptor --
GUIenv
(# theWindow: @window
     (# theDirectoryBrowser: @directoryBrowser
          (#
             open::
               (#
               do 'Gong:beta:r4.2:guienv:' -> rootDirectory;
               #);
          #);
        open::
          (#
          do 'Directory Browser Demo' -> title;
             theDirectoryBrowser.open;
             size -> theDirectoryBrowser.size;
             true -> theDirectoryBrowser.bindRight;
             true -> theDirectoryBrowser.bindBottom;
          #);
     #);
do theWindow.open;
#)
