ORIGIN '~beta/guienv/v1.6/guienv';
BODY 'private/listViewBody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1997
 *       All rights reserved.
 *)

-- lib: attributes --
columnInfo:
  (# width: @integer; (* in pixels *)
     title: ^text;
  #);
-- windowLib: attributes --
listView: canvas
  (# <<SLOT listViewLib: attributes>>;
     
     resourceAllocationError:< exception
       (# errorMsg: ^text;
       enter errorMsg[]
       do inner;
       #);
     
     apiError:< exception
       (# errorMsg: ^text;
       enter errorMsg[]
       do inner;
       #);
     
     (* List View Styles *)
     viewStyles: @
       (# largeIcon: (# exit 16x0000 #);
          report:    (# exit 16x0001 #);
          smallIcon: (# exit 16x0002 #);
          list:      (# exit 16x0003 #);
       #);
     
     noOfSubItems:< integerValue;
     icon_view:< booleanValue;
     smallIcon_view:< booleanValue;
     list_view:< booleanValue;
     report_view:< booleanValue;
     
     listItem: interfaceObject
       (# <<SLOT listItemLib: attributes>>;
          name:
            (* The name is the text in the line corresponding to
             * this(listItem).
             *)
            (# theName: ^text;
            enter (# enter theName[] <<SLOT GUIEnvListItemSetName: doPart>> #)
            exit (# <<SLOT GUIEnvListItemGetName: doPart>> exit theName[] #)
            #);
          
          icon:
            (# theIcon: ^pixmap;
            enter (# enter theIcon[] <<SLOT GUIEnvListItemSetIcon: doPart>>#)
            exit (# <<SLOT GUIEnvListItemGetIcon: doPart>> exit theIcon[] #)
            #);                    
          
          addSubText:
            (# subText: ^text;
            enter subText[]
            <<SLOT GUIEnvListItemAddSubText: doPart>>
            #);
          
          type: @integer;
          
          delete:<
            (* Delete this(listItem) from this(listView). *)
            (# <<SLOT GUIEnvListItemDelete: doPart>> #);
          
          (* The programmer should not call open directly. Open is called
           * when the item is appended, prepended or inserted. 
           *)
          open::<
            (# create::<
                 (# <<SLOT GUIEnvListItemCreate: doPart>> #);
               <<SLOT GUIEnvListItemOpen: doPart>> 
            #);
          
          onSelect:<
            (* Called when this(listItem) is selected by the user or
             * the programmer. 
             *)
            (# do inner; #);
          
          onDoubleClick:<
            (# do inner; #);
          
          onRMouseUp:<
            (# globalPosition: @point;
            enter globalPosition
            do INNER ; 
            #);
          
          select:
            (* Select this(listItem). If singeSelection is true, all
             * other items are deSelected.
             *)
            (# <<SLOT GUIEnvListItemSelect: doPart>> #);
          deSelect:
            (* Call this to deSelect this(listItem). *)
            (# <<SLOT GUIEnvListItemDeSelect: doPart>> #);
          private: @<<SLOT GUIEnvListItemPrivate: descriptor>>;
       #);
     
     multipleSelection:<
       (* if multipleSelection is TRUE the user is allowed to select
        * multiple elements in the list at a time.  
        *)
       booleanValue;
     
     selection: @
       (# 
          first: 
            (# theItem: ^listItem;
            <<SLOT GUIEnvListViewSelectionFirst: doPart>> 
            exit theItem[]
            #);
          
          clear: 
            (* deselects all items in THIS(listView) *)
            (# <<SLOT GUIEnvListViewSelectionClear: doPart>> #);
          
          scan:
            (# current: ^listItem;
            <<SLOT GUIEnvListViewSelectionScan: doPart>>
            #);
       #);
     noOfItems: integerValue
       (# <<SLOT GUIEnvListViewNoOfItems: doPart>> #);
     
     scan:
       (# current: ^listItem;
       <<SLOT GUIEnvListViewScan: doPart>>
       #);
     
     clear: (* Removes all listItems. *)
       (# <<SLOT GUIEnvListViewClear: doPart>> #);
     
     layOutStyle:
       (# lStyle: @integer;
       enter (# enter lStyle <<SLOT setLayOutStyle: doPart>> #)
       exit (# <<SLOT getLayOutStyle: doPart>> exit lStyle #)
       #);
     
     (* The number of listItems . *)
     initialRange:< integerValue
       (* This pattern is used to define the space initially allocated
        * for this(listView).
        *)
       (# do 25 -> value; inner; #);
     
     initialNoOfIcons:< integerValue
       (# do 4 -> value; inner; #);
     
     addColumn:
       (# theColumnInfo: ^columnInfo;
       enter theColumnInfo[]
       <<SLOT GUIEnvListViewAddColumn: doPart>>
       #);
     
     addListItem:
       (# theItem: ^listItem;
       enter theItem[]
       <<SLOT GUIEnvListViewAddListItem: doPart>>
       #);
     
     open::<
       (# create::<
            (# <<SLOT GUIEnvListViewCreate: doPart>> #);
       <<SLOT GUIEnvListViewOpen: doPart>> 
       #);
     
     close::<
       (# <<SLOT GUIEnvListViewClose: doPart>> #);
     
     eventHandler::<
       (# onMouseUp::<
            (# <<SLOT GUIEnvListViewEventHandlerOnMouseUp: doPart>> #);
          onMouseDown::<
            (# <<SLOT GUIEnvListViewEventHandlerOnMouseDown: doPart>> #);
       #);
     private: @<<SLOT GUIEnvListViewPrivate: descriptor>>;
  #);
