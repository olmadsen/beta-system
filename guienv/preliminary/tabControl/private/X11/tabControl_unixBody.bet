ORIGIN '../tabControlBody';
INCLUDE 
'~beta/guienv/v1.6/private/datastructures/sequence'
'~beta/guienv/v1.6/controls'
'~beta/guienv/v1.6/graphics'
'~beta/guienv/v1.6/utils/guienvadds';

-- tabControlLib: attributes --
  tab:
       (#
          page: ^canvas;
          lab: ^text;
          init:
            (# width,height: @integer
            enter (lab[],page[])
            do
               THIS(tabControl)[]->tb.open;
               (0,20)->page.position;
               page.bringBack;
               size->(width,height);
               (width,height-20)->page.size;
               (if private.tabs.empty then page[]->selection if);
               THIS(tab)[]->private.tabs.append
            #);
          tb: @tabButton;
          tabButton: pushButton
            (#
               fitToContents:: 
                 (# width,height: @integer; 
                 do
                    label->(style).widthOfText->width;
                    (width+5,20)->size;
                    true->doneInInner
                 #);
               open:: 
                 (# width,height: @integer; 
                 do
                    lab[]->label;
                    fitToContents;
                    (this(tabControl).private.currentPosition,0)->position;
                    size->(width,height);
                    this(tabControl).private.currentPosition+width->this(tabControl).private.currentPosition
                 #);
               eventHandler::< 
                 (# onMouseUp::<  (#  do page[]->selection #) #)
            #);
          
       exit THIS(tab)[]
       #);

-- GUIEnvTabControlAdd: doPart --
do  (# theTab: ^tab
    do
       &tab[]->theTab[];
       (theLabel[],theCanvas[]) ->theTab.init;
    #)
   
-- GUIEnvTabControlDelete: doPart --
do 'GUIEnvTabControlDelete: doPart' -> putLine;

-- GUIenvTabControlCreate: doPart --
do inner create;
   
-- GUIEnvTabControlOpen: doPart --
do private.tabs.init;
   father.size -> size;
   inner open;
   
-- GUIEnvTabControlClose: doPart --
do inner close;
   
-- GUIEnvTabControlSelect: doPart --
do theCanvas[] -> selection;
   
-- GUIEnvTabControlSetSelection: doPart --
do (if onBeforeSelect then
       theCanvas[]->private.selected[];
       private.selected.bringToFront;
       private.selected[]->onSelect
   if)
   
-- GUIEnvTabControlGetSelection: doPart --
do private.selected[]->theCanvas[]
   
-- GUIenvTabControlEventHandlerOnMouseUp: doPart --
do inner onMouseUp;
   
-- GUIenvTabControlEventHandlerOnMouseDown: doPart --
do inner onMouseDown;
   
-- GUIenvTabControlEventHandlerOnFatherFrameChanged: doPart --
do inner onFatherFrameChanged;
   
-- GUIenvTabControlEventHandlerOnActivate: doPart --
do inner onActivate;
   
-- GUIenvTabControlPrivate: descriptor --
(# currentPosition: @integer;
   tabs: @sequence (# element:: tab #);
   selected: ^canvas;
#)
