ORIGIN '../tabControlBody';
INCLUDE '~beta/guienv/private/datastructures/sequence';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/graphics';
INCLUDE '~beta/guienv/utils/guienvadds';


-- tabControlLib: attributes --

ON: (# exit 1 #);
OFF: (# exit 2 #);

maxComponent: (# exit 0xFFFF #);
lighter:
(# c: @color;
  up: integerValue
	(# comp: @integer;
	enter comp
	do comp + (comp * 30) div 100 -> value;
	   (if value > maxComponent then
		   maxComponent -> value;
	   if);
	#);
enter c
do c.red -> up -> c.red;
  c.green -> up -> c.green;
  c.blue -> up -> c.blue;
exit c
#);
darker:
(# c: @color;
  down: integerValue
	(# comp: @integer;
	enter comp
	do comp - (comp * 20) div 100 -> value;
	   (if value < 0 then
		   0 -> value;
	   if);
	#);
enter c
do c.red -> down -> c.red;
  c.green -> down -> c.green;
  c.blue -> down -> c.blue;
exit c
#);

-- GUIEnvTabSetLabel: doPart --
do theLabel[] -> tabPrivate.label[];
   
-- GUIEnvTabGetLabel: doPart --
do tabPrivate.label[] -> theLabel[];
   
-- GUIEnvTabSetIcon: doPart --
do theIcon[] -> tabPrivate.icon[];
   
-- GUIEnvTabGetIcon: doPart --
do tabPrivate.icon[] -> theIcon[];
   
-- GUIEnvTabSetPage: doPart --
do thePage[] -> tabPrivate.page[];
   
-- GUIEnvTabGetPage: doPart --
do tabPrivate.page[] -> thePage[];
   
-- GUIEnvTabOpen: doPart --
do INNER;
   (# width, height: @integer;
   do 
      THIS(tabControl)[]->tabPrivate.tb.open;
      (2,22)->tabPrivate.page.position;
      tabprivate.page.bringBack;
      size->(width,height);
      (width - 4,height-24)->tabPrivate.page.size;
      (if tabs.tabsPrivate.storage.empty then this(tab)[]->selection if);
      THIS(tab)[]->tabs.tabsPrivate.storage.append;
   #)
   
-- GUIEnvTabClose: doPart --
do INNER;
   
-- GUIEnvTabSelect: doPart --
do this(tab)[] -> selection;
   
-- GUIenvTabPrivate: descriptor --
(# page: ^canvas;
   label: ^text;
   icon: ^pixmap;
   
   tb: @windowItem
     (# 
        theStyle: @textStyle;
        
        fitToContents:: 
          (# width,height: @integer; 
          do (if label[] <> none then
             	 label[] -> theStyle.widthOfText->width;
              else
             	 60 -> width;
             if);
             (width+16,20)->size;
             true->doneInInner
          #);
        open:: 
          (# width,height: @integer; 
          do 'Helvetica' -> theStyle.name;
             10 -> theStyle.size;
             textFaces.bold -> theStyle.face;
             fitToContents;
             (this(tabControl).private.currentPosition,0)->position;
             size->(width,height);
             this(tabControl).private.currentPosition+width->this(tabControl).private.currentPosition
          #);
        eventHandler::< 
          (# onMouseDown::<  (#  do this(tab)[]->selection #);
		  
             onRefresh::
               (#
               do graphics
                  (# 
                     alignmentLeft: (# exit 1 #);
					alignmentRight: (# exit 2 #);
					alignmentCenter: (# exit 3 #);
						
					drawCenteredText:
						(# theStyle: ^textStyle;
							theText: ^text;
							box: @rectangle;
							alignment: @integer;
							
							textWidth, textHeight: @integer;
							boxWidth, boxHeight: @integer;
							baseline: @integer;
						enter (theStyle[], theText[], box, alignment)
						do (if theText[] <> NONE then
								theStyle[] -> style;
								box.size -> (boxWidth, boxHeight);
								theStyle.ascent + theStyle.descent -> textHeight;
								box.top + (boxHeight - textHeight) div 2 + theStyle.ascent -> baseline;
								(if alignment
									//alignmentLeft then
										(box.left, baseLine) -> moveTo;
									//alignmentRight then
										theText[] -> theStyle.widthOfText -> textWidth;
										(box.right - textWidth, baseLine) -> moveTo;
									//alignmentCenter then
										theText[] -> theStyle.widthOfText -> textWidth;
										((boxWidth - textWidth) div 2 + box.left, baseLine) -> moveTo;
								if);
								theText[] -> drawText;
							if);
						#);

                     drawBorder:
                       (# topColor,bottomColor: @color;
                       do backgroundColor->lighter->topColor;
                          backgroundColor->darker->bottomColor;
                          topColor->pen.foregroundColor;
                          (0, 2) -> moveTo;
							(0, height -1) -> drawTo;
							(1, 1) -> moveTo;
							(1, height - 1) -> drawTo;
							(2, 0) -> moveTo;
							(2, 2) -> drawTo;
							(3, 0) -> moveTo;
							(width - 5, 0) -> drawTo;
							(3, 1) -> moveTo;
							(width - 5, 1) -> drawTo;
											
                          bottomColor->pen.foregroundColor;
                         (width -4, 0) -> moveTo;
							(width - 4, 1) -> drawTo;
							(width - 3, 0) -> moveTo;
							(width - 3, 2) -> drawTo;
							(width - 2, 1) -> moveTo;
							(width - 2, height - 1) -> drawTo;
							(width - 1, 2) -> moveTo;
							(width - 1, height - 1) -> drawTo;
                          
                       #);
                     width, height: @integer;
                  do size -> (width, height);
				  	drawBorder;
                     (if label[] <> none then
					 	 (0, 0, 0) -> pen.foreGroundColor;
                         (theStyle[], label[], ((2, 2), (width - 2, height)), alignmentCenter) -> drawCenteredText;
                     if);
                  #);
               #);
          #)
     #);
#)

-- GUIEnvTabsSize: doPart --
do tabsPrivate.storage.size -> value;
   
-- GUIEnvTabsScan: doPart --
do tabsPrivate.storage.scan
   (#
   do current[] -> THIS(scan).current[];
      INNER scan;
   #);
   
-- GUIenvTabsPrivate: descriptor --
(# storage: @sequence
     (# element:: tab;
     #);
#)

-- GUIenvTabControlCreate: doPart --
do INNER;
   
-- GUIEnvTabControlOpen: doPart --
do tabs.tabsPrivate.storage.init;
   INNER;
   
-- GUIEnvTabControlClose: doPart --
do INNER;
   tabs.scan
   (#
   do current.close
   #);
   tabs.tabsPrivate.storage.clear;
   
   tabs.tabsPrivate.storage.init
-- GUIEnvTabControlSetSelection: doPart --
do theTab[] -> private.selected[];
   theTab.tabPrivate.page.bringToFront;
   update;
   
-- GUIEnvTabControlGetSelection: doPart --
do private.selected[] -> theTab[];
   
-- GUIenvTabControlEventHandlerOnMouseUp: doPart --
do inner;
   
-- GUIenvTabControlEventHandlerOnMouseDown: doPart --
do inner;
   
-- GUIenvTabControlEventHandlerOnFatherFrameChanged: doPart --
do inner;
   
-- GUIenvTabControlEventHandlerOnActivate: doPart --
do inner;
   
-- GUIenvTabControlEventHandlerOnRefresh: doPart --
do graphics
	(# width, height: @integer;
		lightColor, darkColor: @color;
		start, end: @integer;
		x, y, w, h: @integer; 
	do (if this(tabControl).private.selected[] <> NONE then
			this(tabControl).private.selected.tabPrivate.tb.position -> (x, y);
			this(tabControl).private.selected.tabPrivate.tb.size -> (w, h);
			x -> start;
			x + w -> end;
		if);
	
	
		backgroundColor->lighter->lightColor;
       backgroundColor->darker->darkColor;
		size -> (width, height);
		lightColor -> pen.foreGroundColor;
		(0, 20) -> moveTo;
		(0, height - 1) -> drawTo;
		(1, 20) -> moveTo;
		(1, height - 2) -> drawTo;
		(2, 20) -> moveTo;
		(start, 20) -> drawTo;
		(2, 21) -> moveTo;
		(start, 21) -> drawTo;
		(end, 20) -> moveTo;
		(width - 1, 20) -> drawTo;
		(end, 21) -> moveTo;
		(width - 2, 21) -> drawTo;
		darkColor -> pen.foreGroundColor;
		(1, height - 1) -> moveTo;
		(width - 1, height - 1) -> drawTo;
		(2, height - 2) -> moveTo;
		(width , height - 2) -> drawTo;
		(width - 2, 22) -> moveTo;
		(width - 2, height - 2) -> drawTo;
		(width - 1, 21) -> moveTo;
		(width - 1, height - 2) -> drawTo;
	#);
	inner;

-- GUIenvTabControlPrivate: descriptor --
(# currentPosition: @integer;
   selected: ^tab;
#)

