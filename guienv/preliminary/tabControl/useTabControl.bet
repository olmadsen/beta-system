ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE 'tabControl';
INCLUDE '~beta/guienv/v1.6/graphics';
INCLUDE '~beta/guienv/v1.6/controls';

--PROGRAM: descriptor--
guienv
(# mainWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# openItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do  #);
                         #);
                       open:: (# do 'Open' -> name #)
                    #);
                  
                  exitItem: @menuItem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# 
                              do mainWindow.theEventHandler.onAboutToClose;
                              #);
                         #);
                       open:: 
                         (# do 'Exit' -> name #)
                    #);
                  open::
                    (#
                    do '&File' -> name;
                       openItem.open; openItem[] -> append;
                       exitItem.open; exitItem[] -> append
                    #);
               #);
             
             open:: 
               (# 
               do fileMenu.open; fileMenu[] -> append;
               #);
          #); (* menubarType *)
        
        myCanvas: @canvas
          (# 
             myTabControl: @tabControl
               (# 
                  myPage: canvas
                    (# 
                       name: ^text;
                       
                       myButton: @radioButton
                         (# open::
                              (# 
                              do (10,40) -> position; (70,18) -> size;
                                 'Highlight' -> label;
                              #);
                         #);
                       
                       eventHandler::
                         (# onRefresh::
                              (# 
                              do graphics
                                 (# 
                                 do (20,20) -> moveTo;
                                    (if name[]<>NONE then
                                        name[] -> drawText;
                                    if);
                                 #);
                              #);
                         #);
                       open::
                         (# do myButton.open; #);
                    #);
                  open::
                    (# p: ^myPage; t: ^text;
                    do 
                       TRUE -> bindBottom -> bindRight;
                       (for i:10 repeat
                            &myPage[] -> p[];
                            p.open;
                            'Canvas No. ' -> t[];
                            i -> t.putint;
                            t.copy -> p.name[];
                            (p[],p.name[],NONE) -> add;
                       for);
                    #);
               #);
             open::
               (# 
               do mainWindow.size -> size;
                  TRUE -> bindBottom -> bindRight;
                  myTabControl.open;
               #);
          #);
        
        open::
          (# 
          do (300,300) -> size; 
             myCanvas.open;
          #);
        eventHandler::
          (# onAboutToClose::
               (# do terminate; #);
          #);
     #);
do mainWindow.open;
#)
