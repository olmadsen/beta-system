ORIGIN '~beta/guienv/v1.6/guienv';
BODY 'private/tabControlBody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1997
 *       All rights reserved.
 *)

-- windowLib: attributes --

tabControl: canvas
  (# <<SLOT tabControlLib: attributes>>;
     
     add:
       (# theCanvas: ^canvas;
          theLabel: ^text;
          theIcon: ^pixmap;
       enter (theCanvas[],theLabel[],theIcon[])
       <<SLOT GUIEnvTabControlAdd: doPart>>
       #);
     
     delete:
       (# theCanvas: ^canvas;
       enter theCanvas[]
       <<SLOT GUIEnvTabControlDelete: doPart>>
       #);
     
     open::<
       (# create::<
            (# <<SLOT GUIenvTabControlCreate: doPart>> #);
       <<SLOT GUIEnvTabControlOpen: doPart>> 
       #);
     
     close::<
       (# <<SLOT GUIEnvTabControlClose: doPart>> #);
     
     select:
       (# theCanvas: ^canvas;
       enter theCanvas[]
       <<SLOT GUIEnvTabControlSelect: doPart>>
       #);
     
     onBeforeSelect:<
       (* Called when the selection is about to change;
        * Assign FALSE to allow to prevent the selection from changing.
        *)
       (# allow: @boolean;
       do TRUE -> allow;
          inner; 
       exit allow
       #);   
     
     onSelect:<
       (* Called when the selection has been changed by the end user *)
       (# theCanvas: ^canvas;
       enter theCanvas[]
       do inner; 
       #); 
     
     selection:
       (# theCanvas: ^canvas;
       enter (# enter theCanvas[] <<SLOT GUIEnvTabControlSetSelection: doPart>> #)
       exit (# <<SLOT GUIEnvTabControlGetSelection: doPart>> exit theCanvas[] #)
       #);
     
     eventHandler::<
       (# onMouseUp::<
            (# <<SLOT GUIenvTabControlEventHandlerOnMouseUp: doPart>> #);
          onMouseDown::<
            (# <<SLOT GUIenvTabControlEventHandlerOnMouseDown: doPart>> #);
          onFatherFrameChanged::<
            (# <<SLOT GUIenvTabControlEventHandlerOnFatherFrameChanged: doPart>> #);
          onActivate::<
            (# <<SLOT GUIenvTabControlEventHandlerOnActivate: doPart>> #);
       #);
     private: @<<SLOT GUIenvTabControlPrivate: descriptor>>;
  #);
