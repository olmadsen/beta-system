ORIGIN 'guienv';
LIB_DEF 'guienvcontrols' '../lib';
BODY 'private/controlsbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1991-96
 *       All rights reserved.
 *)
-- windowLib: Attributes --
(* idx=2 *)
control: windowitem
(* a control is a graphical object in the window that the user can
 * use to perform actions. All user interaction with the control
 * that can result in an action should give some kind of visual
 * feedback.
 *)
  (#
     <<SLOT controlLib:Attributes>>;
     open::<
       (#
          create::<
            (# 
            do
               <<SLOT GUIENVcontrolCreate:Descriptor>>
            #);
          
       do <<SLOT GUIENVcontrolOpen:Descriptor>>
       #);
     eventHandler::<
       (#
          onEnabledChanged::<
            (# 
            <<SLOT GUIENVcontrolOnEnabledChanged:DoPart>>
            #);
          
       #);
     close::< (#  do <<SLOT GUIENVcontrolClose:Descriptor>> #);
     private:
       @<<SLOT GUIENVcontrolPrivate:Descriptor>>;
     
  #)
  (* control *)
  ;
scrollbar: control
(* A scrollbar controls the scrolling of a textfield or picture
 * etc.
 *)
  (#
     <<SLOT scrollbarLib:Attributes>>;
     (* idx+ *)
     eventhandler::<
       (#
          thumbMoved: event
          (* is called whenever a user has moved the thumb of
           * THIS(scrollbar)
           *)
            (# amount: @integer; 
            enter amount
            do <<SLOT GUIENVthumbMoved:Descriptor>>; 
            #);
          onThumbMoved:< thumbMoved;
          pageDown: event
          (* called when the user clicks in the page down area *)
            (#  do <<SLOT GUIENVscrollbarPageDown:Descriptor>>;  #);
          onPageDown:< pageDown;
          pageUp: event
          (* called when the user clicks in the page up area *)
            (#  do <<SLOT GUIENVscrollbarPageUp:Descriptor>> #);
          onPageUp:< pageUp;
          buttonDown: event
          (* called when the user clicks at the down button *)
            (#  do <<SLOT GUIENVscrollbarButtonDown:Descriptor>> #);
          onButtonDown:<
           buttonDown;
          buttonUp: event (* called when the user clicks at the up button *)
            (#  do <<SLOT GUIENVscrollbarButtonUp:Descriptor>> #);
          onButtonUp:< buttonUp;
          pageScrollAmountChanged:
           event (* called when the scrollamount is changed *)
            (#  do INNER ;  #);
          onPageScrollAmountChanged:< pageScrollAmountChanged;
          scrollAmountChanged: event
          (* called when the scrollamount is changed *)
            (#  do INNER ;  #);
          onScrollAmountChanged:< scrollAmountChanged;
          maxValueChanged: event (* called when the max value is changed *)
            (#  do INNER ;  #);
          onMaxValueChanged:< maxValueChanged;
          valueChanged: event (* called when the value is changed *)
            (#  do INNER ;  #);
          onValueChanged:< valueChanged;
          onFrameChanged::<
            (#  do <<SLOT GUIENVScrollbarOnFrameChanged:Descriptor>> #);
          onRefresh::<
            (# 
            do
               <<SLOT GUIENVScrollbarOnRefresh:Descriptor>>
            #);
          onMouseDown::<
            (#  do <<SLOT GUIENVScrollbarOnMouseDown:Descriptor>> #);
          onActivate::<
            (# 
            do
               <<SLOT GUIENVScrollbarOnActivate:Descriptor>>
            #);
          onDeactivate::<
            (#  do <<SLOT GUIENVScrollbarOnDeactivate:Descriptor>> #);
          
       #);
     (* idx- *)
     vertical:<
     (* Specifies if THIS(scrollbar) is vertical, false is the
      * default value
      *) booleanValue;
     scrollAmount:
     (* scrollAmount is the amount the scrollbars thumb will move,
      * when the user clicks in the up button or in the down button
      *)
       (# value: @integer
       enter
         (# 
         enter value
         do <<SLOT GUIENVscrollbarSetScrollAmount:Descriptor>>
         #)
       exit
         (# 
         do
            <<SLOT GUIENVscrollbarGetScrollAmount:Descriptor>>
         exit value
         #)
       #);
     pageScrollAmount:
     (* pageScrollAmount is the amount the scrollbars thumb will
      * move, when the user clicks in the page down or page up area
      *)
       (# value: @integer; 
       enter
         (# 
         enter value
         do <<SLOT GUIENVscrollbarSetPageScrollAmount:Descriptor>>
         #)
       exit
         (# 
         do
            <<SLOT GUIENVscrollbarGetPageScrollAmount:Descriptor>>
         exit value
         #)
       #);
     maxValue:
     (* this combines setMaxValue and getMaxValue.  Evaluate the
      * enter part to set the maximum value and evaluate the exit
      * part to get the maximum value
      *)
       (# value: @integer
       enter
         (# 
         enter value
         do <<SLOT GUIENVscrollbarSetMaxValue:Descriptor>>
         #)
       exit
         (# 
         do
            <<SLOT GUIENVscrollbarGetMaxValue:Descriptor>>
         exit value
         #)
       #);
     value:
     (* evaluate the enter part to set the value and evaluate the
      * exit part to the maximum value.  The thumb of the scrollbar
      * is drawn acording to maxValue and the current value. That
      * is, if maxValue is 100 and value is 50, the thumb will be
      * drawn in the middle of the scrollbar
      *)
       (# pos: @integer; 
       enter
         (#  enter pos do <<SLOT GUIENVscrollbarSetValue:Descriptor>> #)
       exit
         (# 
         do
            <<SLOT GUIENVscrollbarGetValue:Descriptor>>
         exit pos
         #)
       #);
     length:
     (* the length is either the height or the width of the frame
      * depending of the orientation
      *)
       (# theLength: @integer; 
       enter
         (# 
         enter theLength
         do <<SLOT GUIENVscrollbarSetLength:Descriptor>>
         #)
       exit
         (# 
         do
            <<SLOT GUIENVscrollbarGetLength:Descriptor>>
         exit theLength
         #)
       #);
     open::<
       (# create::< (#  do <<SLOT GUIENVscrollbarCreate:Descriptor>> #)
       do
          <<SLOT GUIENVscrollbarOpen:Descriptor>>
       #);
     close::< (#  do <<SLOT GUIENVscrollbarClose:Descriptor>>;  #);
     private:
       @<<SLOT GUIENVscrollbarPrivate:Descriptor>>;
     
  #)
  (* scrollbar *)
  ;
button: control
(* this is the abstract superpattern for all button-like controls *)
  (#
     <<SLOT buttonLib:Attributes>>;
     (* idx+ *)
     eventhandler::<
       (#
          labelChanged: event
          (* is called whenever the label is changed *)
            (#  do INNER ;  #);
          onLabelChanged:< labelChanged;
          styleChanged: event (* is called whenever the style is changed *)
            (#  do INNER #);
          onStyleChanged:< styleChanged;
          onFrameChanged::<
            (#  do <<SLOT GUIENVButtonOnFrameChanged:Descriptor>> #);
          onRefresh::<
            (# 
            do
               <<SLOT GUIENVButtonOnRefresh:Descriptor>>
            #);
          onMouseDown::<
            (#  do <<SLOT GUIENVButtonOnMouseDown:Descriptor>> #);
          
       #);
     (* idx- *)
     label:
     (* the label is the text displayed in THIS(button).  The event
      * labelChanged is called, when the label is changed
      *)
       (# theLabel: ^text; 
       enter
         (#  enter theLabel[] do <<SLOT GUIENVbuttonSetLabel:Descriptor>> #)
       exit
         (#  do <<SLOT GUIENVbuttonGetLabel:Descriptor>> exit theLabel[] #)
       #);
     style: (* the text style used for drawing the label  *)
       (# theStyle: ^textStyle
       enter
         (#  enter theStyle[] do <<SLOT GUIENVbuttonSetStyle:Descriptor>> #)
       exit
         (#  do <<SLOT GUIENVbuttonGetStyle:Descriptor>> exit theStyle[] #)
       #);
     foregroundColor:
       (# c: @color
       enter c
       do <<SLOT GUIENVForeGroundColor:Descriptor>>
       #);
     open::<
       (#
          create::<
            (# 
            do
               <<SLOT GUIENVbuttonCreate:Descriptor>>
            #)
       do <<SLOT GUIENVbuttonOpen:Descriptor>>
       #);
     close::<
       (# 
       do
          <<SLOT GUIENVbuttonClose:Descriptor>>
       #);
     private: @<<SLOT GUIENVbuttonPrivate:Descriptor>>;
     
  #)
  (* button *)
  ;
pushButton: button
(* this is a button like the OK and Cancel buttons in dialogs *)
  (#
     <<SLOT pushButtonLib:Attributes>>;
     open::<
       (#
          create::<
            (# 
            do
               <<SLOT GUIENVpushButtonCreate:Descriptor>>
            #)
       do <<SLOT GUIENVpushButtonOpen:Descriptor>>
       #);
     close::<
       (# 
       do
          <<SLOT GUIENVpushButtonClose:Descriptor>>
       #);
     eventHandler::<
       (#
          onMouseDown::< (#  <<SLOT GUIENVpushButtonOnMouseDown:DoPart>> #);
          onRefresh::< (#  <<SLOT GUIENVpushButtonOnRefresh:DoPart>> #);
          onHiliteChanged::<
            (# 
            <<SLOT GUIENVpushButtonHiliteChanged:DoPart>>
            #);
          
       #);
     private: @<<SLOT GUIENVpushButtonPrivate:Descriptor>>;
     
  #)
  (* pushButton *)
  ;
staticText: button
(* normally a staticText is used to label editText fields *)
  (#
     <<SLOT staticTextLib:Attributes>>;
     (* idx+ *)
     eventhandler::<
       (#
          onRefresh::<
            (# 
            do
               <<SLOT GUIENVstaticTextOnRefresh:Descriptor>>
            #);
          
       #);
     (* idx- *)
     open::<
       (# create::< (#  do <<SLOT GUIENVstaticTextCreate:Descriptor>> #)
       do
          <<SLOT GUIENVstaticTextOpen:Descriptor>>
       #);
     close::< (#  do <<SLOT GUIENVstaticTextClose:Descriptor>> #);
     private:
       @<<SLOT GUIENVstaticTextPrivate:Descriptor>>;
     
  #)
  (* staticText *)
  ;
iconButton: button
(* an icon has a label, which is drawn centered just below the
 * image of the icon
 *)
  (#
     <<SLOT iconButtonLib:Attributes>>;
     (* idx+ *)
     eventhandler::<
       (#
          showLabelChanged: event
          (* called when showLabel is changed *) (#  do INNER #);
          onShowLabelChanged:< showLabelChanged;
          iconChanged: event (* Called when the icon is changed *)
            (#  do INNER #);
          onIconChanged:< iconChanged;
          onRefresh::<
            (#  do <<SLOT GUIENViconButtonOnRefresh:Descriptor>> #);
          onHiliteChanged::<
            (# 
            do
               <<SLOT GUIENViconbuttonOnHiliteChanged:Descriptor>>
            #);
          onMouseDown::< (#  <<SLOT iconButtonOnMouseDown:DoPart>> #);
          
       #);
     (* idx- *)
     showLabel:
     (* if true, the label is shown centered under the image of the
      * Icon
      *)
       (# doShow: @boolean
       enter
         (# 
         enter doShow
         do <<SLOT GUIENViconButtonSetShowLabel:Descriptor>>
         #)
       exit
         (# 
         do <<SLOT GUIENViconButtonGetShowLabel:Descriptor>>
         exit doShow
         #)
       #);
     icon:
       (# theIcon: ^pixmap; 
       enter
         (# 
         enter theIcon[]
         do
            <<SLOT GUIENViconButtonSetIcon:Descriptor>>
         #)
       exit
         (# 
         do <<SLOT GUIENViconButtonGetIcon:Descriptor>>; 
         exit theIcon[]
         #)
       #);
     open::<
       (#
          create::<
            (# 
            do
               <<SLOT GUIENViconButtonCreate:Descriptor>>
            #)
       do <<SLOT GUIENViconButtonOpen:Descriptor>>
       #);
     close::<
       (# 
       do
          <<SLOT GUIENViconButtonClose:Descriptor>>
       #);
     private: @<<SLOT GUIENViconButtonPrivate:Descriptor>>;
     
  #)
  (* iconButton *)
  ;
optionButton: button
(* a optionButton has a menu, which pops up, when the user clicks
 * at the button. A normal way to use a optionButton is to set the
 * label of the button to the current selected item in the menu
 *)
  (#
     <<SLOT optionButtonLib:Attributes>>;
     (* idx+ *)
     eventhandler::<
       (#
          currentItemChanged: event
          (* called when currentItem is changed *) (#  do INNER #);
          onCurrentItemChanged:< currentItemChanged;
          popUpMenuChanged: event (* called when popUpMenu is changed *)
            (#  do INNER #);
          onPopUpMenuChanged:< popUpMenuChanged;
          onLabelChanged::<
            (#  do <<SLOT GUIENVoptionButtonOnLabelChanged:Descriptor>> #);
          onStyleChanged::<
           (#  do <<SLOT GUIENVoptionButtonOnStyleChanged:Descriptor>> #);
          onRefresh::
            (# 
            <<SLOT GUIENVoptionButtonOnRefresh:DoPart>>
            #);
          
       #);
     (* idx- *)
     currentItem:
     (* the current item is the number of the item, which name is
      * currently shown in the popup box.  You can get a reference
      * to that item by calling: currentItem ->
      * theMenu.getItemByNumber -> theItem[];
      *)
       (# itemNo: @integer
       enter
         (# 
         enter itemNo
         do <<SLOT GUIENVoptionButtonSetCurItem:Descriptor>>
         #)
       exit
         (# 
         do <<SLOT GUIENVoptionButtonGetCurItem:Descriptor>>
         exit itemNo
         #)
       #);
     popupMenu:
     (* evaluate the enter part to set the menu that pops up in
      * THIS(optionButton). And evaluate the exit part to get the
      * menu
      *)
       (# popupMenu: ^menu; 
       enter
         (# 
         enter popupMenu[]
         do <<SLOT GUIENVoptionButtonSetPopupMenu:Descriptor>>
         #)
       exit
         (# 
         do
            <<SLOT GUIENVoptionButtonGetPopupMenu:Descriptor>>
         exit PopupMenu[]
         #)
       #);
     open::<
       (#
          create::< (#  do <<SLOT GUIENVoptionButtonCreate:Descriptor>> #)
       do <<SLOT GUIENVoptionButtonOpen:Descriptor>>
       #);
     close::<
       (# 
       do
          <<SLOT GUIENVoptionButtonClose:Descriptor>>
       #);
     private: @<<SLOT GUIENVoptionButtonprivate:Descriptor>>;
     
  #)
  (* optionButton *)
  ;
toggleButton: button
(* this is the abstract superpattern for all buttons that toggle
 * between two states (on/off buttons)
 *)
  (#
     <<SLOT toggleButtonLib:Attributes>>;
     (* idx+ *)
     eventhandler::<
       (#
          stateChanged: event
          (* this event is called whenever the state of
           * THIS(toggleButton) is changed
           *) (#  do INNER #);
          onStateChanged:< stateChanged;
          onMouseUp::< (#  <<SLOT GUIENVtoggleButtonOnMouseUp:DoPart>> #);
          
       #);
     (* idx- *)
     state:
       (# theState: @boolean; 
       enter
         (# 
         enter theState
         do
            <<SLOT GUIENVtoggleButtonSetState:Descriptor>>
         #)
       exit
         (# 
         do <<SLOT GUIENVtoggleButtonGetState:Descriptor>>
         exit theState
         #)
       #);
     open::<
       (#
          create::<
            (# 
            do
               <<SLOT GUIENVtoggleButtonCreate:Descriptor>>
            #)
       do <<SLOT GUIENVtoggleButtonOpen:Descriptor>>
       #);
     close::<
       (# 
       do
          <<SLOT GUIENVtoggleButtonClose:Descriptor>>
       #);
     private: @<<SLOT GUIENVtoggleButtonPrivate:Descriptor>>;
     
  #)
  (* toggleButton *)
  ;
radioButton: toggleButton
(* a radioButton is mostly used in a radiobutton cluster, where
 * only one radioButton is set at a time. A radioButton is thus
 * useful to let the user choose among different alternatives
 *)
  (#
     <<SLOT radioButtonLib:Attributes>>;
     open::<
       (#
          create::<
            (# 
            do
               <<SLOT GUIENVradioButtonCreate:Descriptor>>
            #)
       do <<SLOT GUIENVRadioButtonOpen:Descriptor>>
       #);
     close::<
       (# 
       do
          <<SLOT GUIENVRadioButtonClose:Descriptor>>
       #);
     private: @<<SLOT GUIENVradioButtonPrivate:Descriptor>>;
     
  #)
  (* radioButton *)
  ;
checkBox: toggleButton
(* this is useful for setting options in dialogs *)
  (#
     <<SLOT checkBoxLib:Attributes>>;
     open::<
       (#
          create::<
            (# 
            do
               <<SLOT GUIENVcheckBoxCreate:Descriptor>>
            #);
          
       do <<SLOT GUIENVcheckBoxOpen:Descriptor>>
       #);
     close::<
       (# 
       do
          <<SLOT GUIENVcheckBoxClose:Descriptor>>
       #);
     private: @<<SLOT GUIENVcheckBoxPrivate:Descriptor>>;
     
  #)
  (* checkBox *)
  ;
editText: control
(* this is a simple version of textField. Only one textStyle is
 * allowed.  The purpose of this control is to build dialogs
 *)
  (#
     <<SLOT editTextLib:Attributes>>;
     style:
     (* an editText can only have one textStyle.  Evaluate the
      * enter part to set the textStyle.  Evaluate the exit part to
      * get the textStyle
      *)
       (# txStyle: ^textStyle
       enter
         (# 
         enter txStyle[]
         do <<SLOT GUIENVeditTextSetStyle:Descriptor>>
         #)
       exit
         (# 
         do
            <<SLOT GUIENVeditTextGetStyle:Descriptor>>
         exit txStyle[]
         #)
       #);
     contents:
     (* the contents of an editText is text.  Evaluate the enter
      * part to set the contents, and evaluate the exit part to get
      * the contents
      *)
       (# str: ^text
       enter
         (#  enter str[] do <<SLOT GUIENVeditTextSetContents:Descriptor>> #)
       exit
         (#  do <<SLOT GUIENVeditTextGetContents:Descriptor>> exit str[] #)
       #);
     (* idx+ *)
     eventhandler::<
       (#
          onFrameChanged::<
            (#  do <<SLOT GUIENVeditTextonFrameChanged:Descriptor>> #);
          onKeyDown::<
            (# 
            do
               <<SLOT GUIENVeditTextonKeyDown:Descriptor>>
            #);
          onMouseDown::<
            (#  do <<SLOT GUIENVeditTextonMouseDown:Descriptor>> #);
          onRefresh::<
            (# 
            do
               <<SLOT GUIENVeditTextonRefresh:Descriptor>>
            #);
          onEnableTarget::<
            (#  do <<SLOT GUIENVeditTextonEnableTarget:Descriptor>> #);
          onDisableTarget::<
            (# 
            do
               <<SLOT GUIENVeditTextonDisableTarget:Descriptor>>
            #);
          
       #);
     (* idx- *)
     open::<
     (* the textStyle of THIS(editText) is initially set to the
      * system's textStyle
      *)
       (# create::< (#  do <<SLOT GUIENVeditTextCreate:Descriptor>> #); 
       do
          <<SLOT GUIENVeditTextOpen:Descriptor>>
       #);
     close::< (#  do <<SLOT GUIENVeditTextClose:Descriptor>> #);
     private:
       @<<SLOT GUIENVeditTextPrivate:Descriptor>>;
     
  #)
  (* editText *)
  ;
defaultButton:
(* the defaultButton in the window recieves a mouseUp event when
 * the user presses the return-key
 *)
  (# theButton: ^button
  enter
    (#  enter theButton[] do <<SLOT GUIENVsetDefaultButton:Descriptor>> #)
  exit
    (# 
    do
       <<SLOT GUIENVgetDefaultButton:Descriptor>>
    exit theButton[]
    #)
  #)
  (* defaultButton *)
  ;
  

