ORIGIN 'guienv';
BODY 'private/scrolllistsbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1991-95
 *       All rights reserved.
 *)
-- windowLib: attributes --
(* idx=2 *)
scrollList: windowItem
  (# <<SLOT scrollListLib: attributes>>;
     numberOfItems: integerValue
       (* returns the number of items in THIS(scrollList). The items
        * in the scrollList are indexed from 1 to size. That is, size
        * is the index to the last item
        *)
       (# do <<SLOT GUIENVscrollListnumberOfItems: descriptor>> #);
     insert: 
       (* the specified number of items (numOfItems) are inserted
        * before the specified item (beforeItem). The items in the
        * scrollList are indexed from 1 to value returned by "size"
        *)
       (# beforeItem, numOfItems: @integer;
       enter (beforeItem, numOfItems)
       do <<SLOT GUIENVscrollListInsert: descriptor>>
       #);
     prepend: 
       (* the specified number of items (numOfItems) are inserted at
        * the beginning of THIS(scrollList). The items in the
        * scrollList are indexed from 1 to value returned by "size"
        *)
       (# numOfItems: @integer;
       enter numOfItems
       do <<SLOT GUIENVscrollListPrepend: descriptor>>
       #);
     append: 
       (* the specified number of items (numOfItems) are inserted at
        * the end of THIS(scrollList). The items in the scrollList are
        * indexed from 1 to value returned by "size"
        *)
       (# numOfItems: @integer;
       enter numOfItems
       do <<SLOT GUIENVscrollListAppend: descriptor>>
       #);
     delete: 
       (* the specified range of items are deleted from
        * THIS(scrollList).  FirstItem is the index of the first item
        * to be deleted.  The items in the scrollList are indexed from
        * 1 to value returned by "size"
        *)
       (# firstItem, numOfItems: @integer;
       enter (firstItem, numOfItems)
       do <<SLOT GUIENVscrollListDelete: descriptor>>
       #);
     deleteFirst: 
       (* the specified range of items are deleted from the beginning
        * of THIS(scrollList). The items in the scrollList are indexed
        * from 1 to value returned by "size"
        *)
       (# numOfItems: @integer;
       enter numOfItems
       do <<SLOT GUIENVscrollListDeleteFirst: descriptor>>
       #);
     deleteLast: 
       (* the specified range of items are deleted from the beginning
        * of THIS(scrollList). The items in the scrollList are indexed
        * from 1 to value returned by "size"
        *)
       (# numOfItems: @integer;
       enter numOfItems
       do <<SLOT GUIENVscrollListDeleteLast: descriptor>>
       #);
     itemHeight: 
       (* the height in pixels of a Item in THIS(scrollList) is the
        * same for all items.  Evaluate the enter-part to set the
        * height.  Evaluate the exit-part to get the height
        *)
       (# h: @integer;
       enter (# enter h do <<SLOT GUIENVscrollListSetItemHeight: descriptor>> #)
       exit (#  do <<SLOT GUIENVscrollListGetItemHeight: descriptor>>  exit h #)
       #);
     singleSelection: 
       (* if singleSelection is TRUE the user is only allowed to
        * select a single element in the list at a time.  Evaluate the
        * enter-part to set this property.  Evaluate the exit-part to
        * get this property
        *)
       (# isSingle: @boolean;
       enter (# enter isSingle do <<SLOT GUIENVscrollListSetSingleSelection: descriptor>> #)
       exit (# do <<SLOT GUIENVscrollListGetSingleSelection: descriptor>> exit isSingle  #)
       #);
     getItemRectangle: 
       (* returns the rectangle occupied by the item specified by the
        * item index "theItem". The rectangle is in terms of the
        * coordinate system of the father of THIS(scrollList)
        *)
       (# theItem: @integer;
          theRectangle: @rectangle;
       enter theItem
       do <<SLOT GUIENVItemGetItemRectangle: descriptor>>
       exit theRectangle
       #);
     (* idx+ *)
     selection: @
       (# clear: 
            (* deselects all items in THIS(scrollList) *)
            (# do <<SLOT GUIENVselectionClear: descriptor>> #);
          scrollIntoView: 
            (* scrolls THIS(scrollList) so the selected item are
             * visible
             *)
            (# do <<SLOT GUIENVselectionscrollIntoView: descriptor>> #);
          first: integerValue
            (* returns the index to the first selected item in
             * THIS(scrollList). There might be items between the
             * first and the last selected item that are not selected
             *)
            (# do <<SLOT GUIENVselectionFirst: descriptor>> #);
          last: integerValue
            (* returns the index to the last selected item in
             * THIS(scrollList). There might be items between the
             * first and the last selected item that are not selected
             *)
            (# do <<SLOT GUIENVselectionLast: descriptor>> #);
          select: 
            (* selects the item specified by the item index
             * "theItem".  if extend is TRUE the item is added to the
             * selection, otherwise the selection is first emptied
             *)
            (# theItem: @integer;
               extend: @boolean;
            enter (theItem,extend)
            do <<SLOT GUIENVselectionSelectItem: descriptor>>
            #);
          deselect: 
            (* deselects the item specified by the item index
             * "theItem".  If the item wasn't selected nothing happens
             *)
            (# theItem: @integer;
            enter theItem
            do <<SLOT GUIENVselectionDeselect: descriptor>>
            #);
          has: booleanValue
            (* returns whether the item specified by the item index
             * "theItem".  is selected
             *)
            (# theItem: @integer
            enter theItem
            do <<SLOT GUIENVselectionHas: descriptor>>
            #);
       #) (* selection *);
     (* idx- *)
     scanSelection: 
       (# current: @integer;
       do <<SLOT GUIENVscrollListScanSelection: descriptor>>
       #);
     scan: 
       (# current: @integer;
       do <<SLOT GUIENVscrollListScan: descriptor>>
       #);
     open::< 
       (# create::< (# do <<SLOT GUIENVscrollListCreate: descriptor>> #);
       do <<SLOT GUIENVscrollListOpen: descriptor>> 
       #);
     close::< (# do <<SLOT GUIENVscrollListClose: descriptor>> #);
     (* idx+ *)
     eventhandler::<
       (# select: event
            (* Called, when the user selects an item in 
             * this(scrollList). `Item' is the index of
             * the item in this(scrollList) and `doubleClick'
             * is true it the item was selected by a double
             * click.
             *)
            (# item: @integer;
               doubleClick: @boolean;
            enter (item, doubleClick)
            do inner;
            #);
          onSelect:< select;
          onFrameChanged::<
            (# do <<SLOT GUIENVScrollListonFrameChanged: descriptor>> #);
          onRefresh::<
            (# do <<SLOT GUIENVScrollListonRefresh: descriptor>> #);
          onMouseDown::<
            (# do <<SLOT GUIENVScrollListonMouseDown: descriptor>> #);
          onActivate::<
            (# do <<SLOT GUIENVScrollListonActivate: descriptor>> #);
          onDeactivate::<
            (# do <<SLOT GUIENVScrollListonDeactivate: descriptor>> #);
       #);
     (* idx- *)
     layoutType::< scrollListLayout;
     putLayout::< (* private *) (# <<SLOT GUIENVscrollListputLayout: doPart>> #);
     getLayout::< (* private *) (# <<SLOT GUIENVscrollListgetLayout: doPart>> #);
     private: @<<SLOT GUIENVscrollListPrivate: descriptor>>;
  #) (* scrollList *);
textScrollList: scrollList
  (# <<SLOT textScrollListLib: attributes>>;
     setText: 
       (* the item specified by the item index "theItem" is set to
        * the text "theText"
        *)
       (# theText: ^text;
          theItem: @integer;
       enter (theItem,theText[])
       do <<SLOT GUIENVtextScrollListSet: descriptor>>
       #);
     getText: 
       (* the text in the item specified by the item index "theItem"
        * is returned
        *)
       (# theText: ^text;
          theItem: @integer;
       enter theItem
       do <<SLOT GUIENVtextScrollListGet: descriptor>>
       exit theText[]
       #);
     style: 
       (* the style used to display the item texts in
        * THIS(textScrollList).  Evaluate the enter-part to set the
        * style.  Evaluate the exit-part to get the style
        *)
       (# setTextStyle: 
            (# theStyle: ^textStyle;
            enter theStyle[]
            do <<SLOT GUIENVtextScrollListSetTextStyle: descriptor>>
            #);
          getTextStyle: 
            (# theStyle: ^textStyle;
            do <<SLOT GUIENVtextScrollListGetTextStyle: descriptor>>
            exit theStyle[]
            #);
       enter setTextStyle
       exit getTextStyle
       #);
     open::< 
       (# create::< (# do <<SLOT GUIENVtextScrollListCreate: descriptor>> #);
       do <<SLOT GUIENVtextScrollListOpen: descriptor>> 
       #);
     close::< (# do <<SLOT GUIENVtextScrollListClose: descriptor>>; #);
     layoutType::< textScrollListLayout;
     putLayout::< (* private *) (# <<SLOT GUIENVtextscrollListputLayout: doPart>> #);
     getLayout::< (* private *) (# <<SLOT GUIENVtextscrollListgetLayout: doPart>> #);
  #) (* textScrollList *);
