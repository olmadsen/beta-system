ORIGIN '~beta/guienv/graphicscanvas';

-- program: descriptor --
GUIenv
(# 
   toucan: @Pixmap;
   
   main: @Window
     (# theCanvas: @GraphicsCanvas
          (# title: @TextItem;
             frame: @Rect;
             theImageItem: @PixmapItem;
             theLine: @Line;
             
             
             theOval: @Oval
               (# onMouseDown::
                    (#
                    do 'oval mouse-down' -> putline;
                    #);
                  onMouseMove::
                    (#
                    do 'oval mouse-move' -> putline;
                    #);
                  onMouseUp::
                    (#
                    do 'oval mouse-up' -> putline;
                    #);
               #);
             
             blue: @Color;
             gray: @Color;
             
             Rail: Line
               (# 
                  onMouseDown::
                    (#
                    do 'rail mouse-down ' -> puttext;
                       strokewidth -> putint;
                       newline;
                    #);
                  onMouseMove::
                    (#
                    do 'rail mouse-move ' -> puttext;
                       strokewidth -> putint;
                       newline;
                    #);
                  onMouseUp::
                    (#
                    do 'rail mouse-up ' -> puttext;
                       strokewidth -> putint;
                       newline;
                    #);
               #);
             
             CreateLine:
               (# x1, y1, x2, y2: @real;
                  width: @integer;
                  theLine: ^Line;
               enter (x1, y1, x2, y2, width)
               do &Rail[] -> theLine[];
                  theLine.init;
                  &Point2d[] -> theLine.start[];
                  (x1, y1) -> theLine.start;
                  &Point2d[] -> theLine.end[];
                  (x2, y2) -> theLine.end;
                  width -> theLine.strokewidth;
                  blue[] -> theLine.stroke[];
                  theLine[] -> add;
               #);
             open::
               (#
               do 0.1 * 0xFFFF -> blue.red;
                  0.2 * 0xFFFF -> blue.green;
                  0.5 * 0xFFFF -> blue.blue;
                  
                  0.8 * 0xFFFF -> gray.red;
                  0.8 * 0xFFFF -> gray.green;
                  0.85 * 0xFFFF -> gray.blue;
                  
                  (0xFFFF, 0xFFFF, 0xFFFF) -> backgroundColor;
                  (800, 600) -> size;
                  title.init;
                  'Hello, World' -> title.content[];
                  blue[] -> title.stroke[];
                  13 -> title.x;
                  40 -> title.y;
                  
                  frame.init;
                  (200, 60) -> (frame.x, frame.y);
                  80 -> frame.width;
                  30 -> frame.height;
                  blue[] -> frame.stroke[];
                  gray[] -> frame.fill[];
                  
                  theOval.init;
                  200 -> theOval.x;
                  100 -> theOval.y;
                  80 -> theOval.width;
                  40 -> theOval.height;
                  blue[] -> theOval.stroke[];
                  gray[] -> theOval.fill[];
                  
                  theImageItem.init;
                  toucan[] -> theImageItem.content[];
                  200 -> theImageItem.x;
                  300 -> theImageItem.y;
                  theImageItem[] -> add;
                  
                  title[] -> add;
                  frame[] -> add;
                  theOval[] -> add;
                  
                  (for inx: 5 repeat
                       (13 + inx*10, 200 + inx*10, 300+ inx * 10, 13+ inx * 10, inx) -> CreateLine;
                  for);
               #);
          #);
        open::
          (#
          do 
             'Graphics Canvas' -> Title;
             (800, 600) -> size;
             theCanvas.open;
             
          #);
     #);
do 'toucan.png' -> toucan.read;
   main.open;
#)
