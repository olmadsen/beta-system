ORIGIN '~beta/guienv/graphicscanvas';
INCLUDE '~beta/guienv/controls';

-- program: descriptor --
GUIenv
(# 
   toucan: @Pixmap;
   
   main1: @MainWindow;
   main2: @MainWindow;
   
   MainWindow: Window
     (# MenuBarType::
          (# filemenu: @Menu
               (# saveitem: @MenuItem
                    (# eventHandler::
                         (# onSelect::
                              (#
                              do ('image.png', ((200 - 4, 300 - 4), 
                                 (200 + toucan.width + 4, 300 + toucan.height + 4))) -> theCanvas.writePNGarea;
                              #);
                         #);
                       open::
                         (#
                         do 'Save' -> name;
                         #);
                    #);
                  open::
                    (#
                    do 'File' -> name;
                       saveitem.open;
                       saveitem[] -> append;
                    #);
               #);
             open::
               (#
               do filemenu.open;
                  filemenu[] -> append;
               #);
          #);
        theCanvas: @GraphicsCanvas
          (# title: @TextItem;
             frame: @Rect;
             theImageItem: @PixmapItem;
             theLine: @Line;
             times36: @TextStyle;
             
             
             theOval: @Oval
               (# drag: @boolean;
                  mx, my: @integer;
                  
                  onMouseDown::
                    (#
                    do true -> drag;
                       (x, y) -> (mx, my);
                    #);
                  onMouseMove::
                    (# dx, dy: @integer;
                    do (if drag then
                           x - mx -> dx;
                           y - my -> dy;
                           THIS(Oval).x + dx -> THIS(Oval).x;
                           THIS(Oval).y + dy -> THIS(Oval).y;
                           x -> mx;
                           y -> my;
                           changed;
                       if);
                    #);
                  onMouseUp::
                    (#
                    do false -> drag;
                    #);
               #);
             
             blue: @Color;
             gray: @Color;
             
             poly: @Polygon;
             
             Rail: Line
               (# 
                  onMouseDown::
                    (#
                    do 'rail mouse-down ' -> puttext;
                       strokewidth -> putint;
                       newline;
                    #);
                  onMouseMove::
                    (#
                    do 'rail mouse-move ' -> puttext;
                       strokewidth -> putint;
                       newline;
                    #);
                  onMouseUp::
                    (#
                    do 'rail mouse-up ' -> puttext;
                       strokewidth -> putint;
                       newline;
                    #);
               #);
             
             CreateLine:
               (# x1, y1, x2, y2: @real;
                  width: @integer;
                  theLine: ^Line;
               enter (x1, y1, x2, y2, width)
               do &Rail[] -> theLine[];
                  theLine.init;
                  &Point2d[] -> theLine.start[];
                  (x1, y1) -> theLine.start;
                  &Point2d[] -> theLine.end[];
                  (x2, y2) -> theLine.end;
                  width -> theLine.strokewidth;
                  blue[] -> theLine.stroke[];
                  theLine[] -> add;
               #);
             open::
               (# outline: ^PointList;
               do 0.1 * 0xFFFF -> blue.red;
                  0.2 * 0xFFFF -> blue.green;
                  0.5 * 0xFFFF -> blue.blue;
                  
                  0.8 * 0xFFFF -> gray.red;
                  0.8 * 0xFFFF -> gray.green;
                  0.85 * 0xFFFF -> gray.blue;
                  'Times' -> times36.name;
                  12 -> times36.size;
                  
                  (0xFFFF, 0xFFFF, 0xFFFF) -> backgroundColor;
                  (800, 600) -> size;
                  title.init;
                  'Hello, World' -> title.content[];
                  times36[] -> title.style[];
                  blue[] -> title.stroke[];
                  45 -> title.angle;
                  60 -> title.x;
                  600 -> title.y;
                  
                  frame.init;
                  (200 - 4, 300 - 4) -> (frame.x, frame.y);
                  toucan.width + 8 -> frame.width;
                  toucan.height + 8 -> frame.height;
                  blue[] -> frame.stroke[];
                  gray[] -> frame.fill[];
                  
                  theOval.init;
                  200 -> theOval.x;
                  100 -> theOval.y;
                  80 -> theOval.width;
                  40 -> theOval.height;
                  blue[] -> theOval.stroke[];
                  gray[] -> theOval.fill[];
                  
                  theImageItem.init;
                  toucan[] -> theImageItem.content[];
                  200 -> theImageItem.x;
                  300 -> theImageItem.y;
                  
                  
                  title[] -> add;
                  frame[] -> add;
                  theOval[] -> add;
                  theImageItem[] -> add;
                  
                  title.outline -> outline[];
                  poly.init;
                  blue[] -> poly.stroke[];
                  outline.scan
                  (#
                  do current[] -> poly.points.append;
                  #);
                  (outline.head).elm[] -> poly.points.append;
                  poly[] -> add;
                  (for inx: 5 repeat
                       (13 + inx*10, 200 + inx*10, 300+ inx * 10, 13+ inx * 10, inx) -> CreateLine;
                  for);
               #);
          #);
        
        horScrollbar: @Scrollbar
          (# open::
               (#
               do ((0, 600), (800, 616)) -> frame;
                  1000 -> MaxValue;
               #);
             eventHandler::
               (# onValueChanged::
                    (# x, y: @integer;
                    do theCanvas.pan -> (x, y);
                       (-value, y) -> theCanvas.pan;
                    #);
               #);
          #);
        verScrollbar: @Scrollbar
          (# vertical:: (# do true -> value #);
             open::
               (#
               do ((800, 0), (816, 600)) -> frame;
                  1000 -> MaxValue;
               #);
             eventHandler::
               (# onValueChanged::
                    (# x, y: @integer;
                    do theCanvas.pan -> (x, y);
                       (x, -value) -> theCanvas.pan;
                    #);
               #);
          #);
        open::
          (#
          do 
             'Graphics Canvas' -> Title;
             (816, 616) -> size;
             theCanvas.open;
             horScrollbar.open;
             verScrollbar.open;
          #);
     #);
do 'toucan.png' -> toucan.read;
   main1.open;
   main2.open;
#)
