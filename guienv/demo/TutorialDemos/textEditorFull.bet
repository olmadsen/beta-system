ORIGIN  '~beta/guienv/v1.3/guienv';
INCLUDE '~beta/guienv/v1.3/fields';
INCLUDE '~beta/guienv/v1.3/stddialogs';
INCLUDE '~beta/basiclib/v1.4/file';
-- program: descriptor --
guienv
(# textWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# textFile: @file;
                  openItem: @menuitem
                    (# eventHandler::
                         (# onSelect::
                              (# tmpText: @styledText;
                              do textWindow[]
                                   ->fileSelectionDialog
                                   ->textFile.name;
                                 textFile.openRead;
                                 textFile.scan
                                   (# while:: (# do true->value #)
                                   do ch->tmpText.put
                                   #);
                                 tmpText[]
                                   ->txtEdit.contents.contents;
                                 textFile.close
                         #) #);
                       open:: (# do 'Open'->name #)
                    #);
                  saveItem: @menuitem
                    (# eventHandler::
                         (# onSelect::
                              (#
                              do textFile.openWrite;
                                 txtEdit.contents.contents
                                   ->textFile.puttext;
                                 textFile.close
                         #) #);
                       open:: (# do 'Save'->name #)
                    #);
                  quitItem: @menuitem
                    (# eventHandler:: 
                         (# onSelect:: (# do terminate #) #);
                       open:: (# do 'Quit'->name #)
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       openItem.open; openItem[]->append;
                       saveItem.open; saveItem[]->append;
                       quitItem.open; quitItem[]->append
               #) #);
             open:: (# do fileMenu.open; fileMenu[] -> append #);
          #);
        txtEdit: @textEditor
          (# open::
               (#
               do textWindow.size->size;
                  true->bindBottom;
                  true->bindRight
          #) #);
        open::
          (#
          do 'textEditorFull'->title;
             txtEdit.open
          #)
     #)
do textWindow.open
#)
