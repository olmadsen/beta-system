ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/fields';
INCLUDE '~beta/guienv/v1.6/stddialogs';
INCLUDE '~beta/basiclib/v1.6/file';
-- program: descriptor --
guienv
(# 
	onOpenDocument:: (# do fileName[] -> putLine #);
	onStartApplication:: (# do 'show splash screen'  -> putLine;#);
	
	textWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# textFile: @file;
                  openItem: @menuitem
                    (# eventHandler::
                         (# onSelect::
                              (# tmpText: @styledText;
                                 fileName: ^text;
                              do textWindow[]
                                   ->fileSelectionDialog
                                   ->fileName[];
                                 (if fileName[]<>NONE then
                                     fileName[]->textFile.name;
                                     textFile.openRead;
                                     textFile.scan
                                     (# while:: (# do true->value #)
                                     do ch->tmpText.put
                                     #);
                                     tmpText[]
                                       ->txtEdit.contents.contents;
                                     textFile.close
                                 if)
                              #)
                         #);
                       open:: (# do 'Open'->name #)
                    #);
                  saveItem: @menuitem
                    (# eventHandler::
                         (# onSelect::
                              (#
                              do (if textFile.name<>NONE then
                                     (if (textFile.name).length > 0 then
                                         textFile.openWrite;
                                         txtEdit.contents.contents
                                           ->textFile.puttext;
                                         textFile.close
                                     if);
                                 if)
                              #) 
                         #);
                       open:: (# do 'Save'->name #)
                    #);
                  quitItem: @menuitem
                    (# eventHandler:: 
                         (# onSelect:: (# do terminate #) #);
                       open:: (# do 'Quit'->name #)
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       openItem.open; openItem[]->append;
                       saveItem.open; saveItem[]->append;
                       quitItem.open; quitItem[]->append
               #) #);
             open:: (# do fileMenu.open; fileMenu[] -> append #);
          #);
        txtEdit: @textEditor
          (# open::
               (#
               do textWindow.size->size;
                  true->bindBottom;
                  true->bindRight
          #) #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        open::
          (#
          do 'textEditorFull'->title;
             txtEdit.open
          #)
     #)
do textWindow.open
#)
