ORIGIN '~beta/guienv/v1.5/guienv';
INCLUDE '~beta/guienv/v1.5/controls'
--- program: descriptor ---
guienv
(# authorName: @text;
   isReport: @boolean;
   theDialog: @window 
     (# cTitleLabel: @staticText 
          (# open:: 
               (# 
               do (10, 10)->position; (95,25)->size;
                  'Title: '->label
          #) #);
        cTitle: @editText 
          (# open::
               (# do (115, 10)->position; (150,25)->size #)
          #);
        cAuthorLabel: @staticText 
          (# open:: 
               (# 
               do (10, 40)->position; (95,25)->size;
                  'Author: '->label
          #) #);
        cAuthor: @editText 
          (# open:: 
               (# do (115, 40)->position; (150,25)->size #)
          #);
        cReport: @checkBox 
          (# open:: 
               (# 
               do (10, 70)->position;  (100,25)->size;
                  'Report'->label
               #);
             eventhandler:: 
               (# onMouseUp:: (# do not state->state #) #);
          #);
        cCancel: @pushButton
          (# open:: 
               (# 
               do (115, 70)->position; (80,25)->size;
                  'Cancel'->label
               #);
             eventhandler:: 
               (# onMouseUp:: (# do theDialog.close #) #)
          #);
        cOk: @pushButton
          (# open:: 
               (# 
               do (115+150-30, 70)->position; (30,25)->size; 
                  'OK'->label
               #);
             eventhandler:: 
               (# onMouseUp:: 
                    (# 
                    do (* store values, then *)
                       theDialog.close
               #) #)
          #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        open:: 
          (# 
          do (40,40)->position; (275,100)->size;
             'dialog'->title;
             cOk.open; cCancel.open;
             cTitleLabel. open; cTitle.open;
             cAuthorLabel.open; cAuthor.open;
             cReport.open;
             cOk[]->defaultButton
          #)
     #)
do theDialog.open;
   theDialog.showModal
#)
