ORIGIN '~beta/guienv/v1.4/guienv'
--- program: descriptor ---
guienv
(# menuWindow: @window
     (# menubarType::
          (# animalMenu: @menu
               (# iCat: @menuItem
                    (# eventhandler::
                         (# onSelect:: (# do 'Cat chosen'->putline  #) #);
                       open:: (# do 'Cat'->name #)
                    #);
                  iBear: @menuItem
                    (# eventhandler::
                         (# onSelect:: (# do 'Bear chosen'->putline #)#);
                       open:: (# do 'Bear'->name #)
                    #);
                  iEagle: @menuItem 
                    (# eventhandler::
                         (# onSelect:: (# do 'Eagle chosen'->putline #);
                            onStatus:: (# do false->value #) 
                         #);
                       open:: (# do 'Eagle'->name #)
                    #);
                  open:: 
                    (# sep: ^menuItem
                    do 'Animals'->name;
                       iCat.open; iBear.open; iEagle.open;
                       iCat[]->animalMenu.append;
                       iBear[]->animalMenu.append;
                       &separator[]->sep[]; sep.open; sep[]->animalMenu.append;
                       iEagle[]->animalMenu.append
                    #)
               #);
             open:: (# do animalMenu.open; animalMenu[]->append #)
          #);
        open:: (# do 'menus'->title #)
     #)
do menuWindow.open
#)
