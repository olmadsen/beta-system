ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/tabControl'
        '~beta/guienv/graphics'
        '~beta/guienv/controls';
-- PROGRAM: Descriptor --
guienv
  (#
     mainWindow: @window
       (#
          menubarType:: 
            (#
               fileMenu: @menu
                 (#
                    openItem: @menuItem
                      (#
                         eventHandler:: 
                           (# onSelect::  (#  do myCanvas.addNewTab;  #); 
                           #);
                         open::  (#  do 'Add tab'->name #)
                      #);
                    changePageItem: @menuItem
                      (#
                         eventHandler:: 
                           (# onSelect::  (#  do myCanvas.changePage;  #); 
                           #);
                         open::  (#  do 'Change Page'->name #)
                      #);
                    closeTabItem: @menuItem
                      (#
                         eventHandler:: 
                           (# onSelect::  (#  do myCanvas.closePage;  #); 
                           #);
                         open::  (#  do 'Close Page'->name #)
                      #);
                    exitItem: @menuItem
                      (#
                         eventHandler:: 
                           (#
                              onSelect:: 
                                (# 
                                do mainWindow.theEventHandler.onAboutToClose; 
                                #);
                              
                           #);
                         open::  (#  do 'Exit'->name #)
                      #);
                    open:: 
                      (# 
                      do
                         'File'->name;
                         openItem.open;
                         openItem[]->append;
                         changePageItem.open;
                         changePageItem[]->append;
                         closeTabItem.open;
                         closeTabItem[]->append;
                         exitItem.open;
                         exitItem[]->append
                      #);
                    
                 #);
               open::  (#  do fileMenu.open; fileMenu[]->append;  #);
               
            #);
          (* menubarType *)
          myCanvas: @canvas
            (#
               addNewTab:
                 (# pT: ^myTabControl.myTab; t: ^text; 
                 do &myTabControl.myTab[]->pT[]; pT.open; 
                 #);
               changePage: (#  do myTabControl.changePage;  #);
               closePage: (#  do myTabControl.closePage;  #);
               myTabControl: @tabControl
                 (#
                    onBeforeSelectionChange:: 
                      (# curSel: ^myTab; 
                      do
                         selection->curSel[];
                         (if curSel[] <> none then
                             curSel.label->putline; 
                         if);
                         
                      #);
                    closePage:
                      (# curSel: ^myTab; 
                      do
                         selection->curSel[];
                         (if curSel[] <> none then curSel.close;  if);
                         
                      #);
                    changePage:
                      (# curSel: ^myTab; newPage: ^curSel.myPage; 
                      do
                         tabNo10[]->curSel[];
                         (if curSel[] <> none then
                             canvasNo+1->canvasNo;
                             &curSel.myPage[]->newPage[];
                             newPage.open;
                             'New page'->newPage.name[];
                             newPage[]->curSel.page;
                             'New Page'->curSel.label;
                             
                         if);
                         
                      #);
                    tabNo10: ^myTab;
                    myTab: tab
                      (#
                         onSelect:: 
                           (# amyPage: ^myPage; 
                           do 'onSelect. label = '->puttext; label->putline; 
                           #);
                         myPage: canvas
                           (#
                              name: ^text;
                              myButton: @radioButton
                                (#
                                   open:: 
                                     (# 
                                     do
                                        (10,40)->position;
                                        (70,18)->size;
                                        'Highlight'->label;
                                        
                                     #);
                                   
                                #);
                              eventHandler:: 
                                (#
                                   onRefresh:: 
                                     (# 
                                     do
                                        graphics
                                          (# 
                                          do
                                             (20,20)->moveTo;
                                             (if name[] <> none then
                                                 name[]->drawText; 
                                             if);
                                             
                                          #);
                                        
                                     #);
                                   
                                #);
                              open::  (#  do myButton.open;  #);
                              
                           #);
                         open:: 
                           (# p: ^myPage; t: ^text; 
                           do
                              canvasNo+1->canvasNo;
                              &myPage[]->p[];
                              THIS(myTabControl)[]->p.father[];
                              p.open;
                              'Canvas No. '->t[];
                              canvasNo->t.putint;
                              t.copy->p.name[];
                              p[]->page;
                              t[]->label;
                              
                           #);
                         
                      #);
                    canvasNo: @integer;
                    open:: 
                      (# pT: ^myTab; 
                      do
                         father.size->size;
                         TRUE->bindBottom->bindRight;
                         (for i: 10 repeat
                           &myTab[]->pT[]; pT.open; pT[]->tabNo10[]; 
                         for);
                         
                      #);
                    
                 #);
               open:: 
                 (# 
                 do
                    mainWindow.size->size;
                    TRUE->bindBottom->bindRight;
                    myTabControl.open;
                    
                 #);
               
            #);
          open::  (#  do (300,300)->size; myCanvas.open;  #);
          eventHandler:: 
            (# onAboutToClose::  (#  do terminate;  #);  #);
          
       #);
     
  do mainWindow.open; 
  #)  

