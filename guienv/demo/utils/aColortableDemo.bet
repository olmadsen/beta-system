ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/colorTable';
-- program: Descriptor --
(* This demo illustrates the use of the colorTable;
 * ColorTable is a mapping between colornames and 
 * their respective rgb-values.
 * 
 * The colortable includes a number of predefined
 * colors, but enables the user to use a 
 * user-defined colorfile. This demo uses 
 * a file: colornames.small
 * 
 * The demo also show how to use backgroundcolor
 * on windowitems and windows.
 *) guienv
  (#
     aColorTable: @colorTable;
     aWindow: @window
       (#
          c1,c2: @canvas (# open::< (#  do (40,40)->size;  #);  #);
          open::<
            (# col1,col2,col3: @color; 
            do (* Set up background colors *)
               'Green'->title;
               'green'->aColorTable.lookup->col1;
               col1->backgroundcolor;
               c1.open;
               (40,40)->c1.position;
               'purple'->aColorTable.lookup->col2;
               (* Note: purble is not defined in colornames.small, but is a default color*)
               col2->c1.backgroundcolor;
               c2.open;
               (100,100)->c2.position;
               'gold1'->aColorTable.lookup->col3;
               col3->c2.backgroundcolor;
               
            #);
          
       #);
     
  do
     'colornames.small'->aColorTable.loadFile (# merge:: trueobject #);
     aColorTable.scan
       (#  do currentName[]->putline;  #);
     aWindow.open;
     
  #)  

