ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/treeview';
INCLUDE '~beta/basiclib/file';
INCLUDE '~beta/basiclib/directory';
INCLUDE '~beta/guienv/fields';

--- windowLib: attributes ---

directoryBrowser: treeView
  (# fileItem: item
       (# f: @file
            (# AccessError:: (# do true->continue #);
               NoSuchFileError:: (# do true->continue #);
               OtherError:: (# do true->continue #);
            #);
          thePath:
            (# fileName: ^text;
            enter fileName[]
            do fileName[]->f.name;
               f.entry.path.name->label[];
            #);
       #);
     directoryItem: folder
       (# directoryName: ^text;
          d: @directory;
          thePath:
            (#
            enter directoryName[]
            do directoryName[]->d.name;
               d.entry.path.name->label[];
            #);
          setupDirectory:
            (#
            do 'scanning directory "'->puttext;
               directoryname[]->puttext;
               '" ... '->puttext;
               d.scanEntries
               (# error:: (# do true->continue #);
               do select
                  (# error:: (# do true->continue #);
                     whenDir::
                       (# theItem: ^directoryItem;
                       do (if true
                           // '.'->(found.path).equal then
                           // '..'->(found.path).equal then
                           else
                              &directoryItem[]->theItem[];
                              theItem[]->THIS(directoryItem).items.append;
                              foundFullPath->theItem.thePath;
                          if)
                       #);
                     whenFile::
                       (# theItem: ^fileItem;
                       do &fileItem[]->theItem[];
                          theItem[]->THIS(directoryItem).items.append;
                          foundFullPath->theItem.thePath;
                       #);
                  #);
               #);
               'DONE'->putline;
            #);
          onExpand:: (# do setupDirectory #);
          onCollapse:: (# do items.clear #);
       #);
     rootDirectory:
       (# d: @directory;
       enter d.name
       do d.entry.path->rootItem.thePath;
          rootItem[]->root
       #);
     rootItem: @directoryItem;
  #);

-- program: descriptor --
guienv
(# theWindow: @window
     (# eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        scrolltv: @scroller
          (# contentsType:: directoryBrowser
               (# open::
                    (#
                    do (if noofarguments >= 2 then
                           2->arguments->rootDirectory;
                        else
                           '.'->rootDirectory
                       if)
                    #)
               #);
             open::
               (# v,tmp: @integer
               do theWindow.size->(tmp,v);
                  (200,v)->size;
                  TRUE->bindBottom
               #)
          #);
        open::
          (#
          do 'Directory Browser Demo'->title;
             scrolltv.open;
          #);
     #);
do theWindow.open;
#)
