ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/fields';
INCLUDE '~beta/guienv/fieldsactions';
INCLUDE '~beta/guienv/controls';
(* This is a silly demo that shows how a beforeChangeAction can be
 * used to 'eat' every second keystroke in a textField.
 *)
--- program: descriptor ---
guienv
(# allowEdit: @boolean;
   theWindow: @window
     (# theTextField: @textField
          (# aBeforeChangeAction: @beforeChangeAction
               (# do not allowEdit -> allowEdit #);
             eventHandler::
               (# onBeforeChange::
                    (# do allowEdit -> allow #);
               #);
             open::
               (# 
               do true -> bindBottom -> bindRight;
                  aBeforeChangeAction[] -> appendAction
               #)
          #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        open::
          (# 
          do 'BeforeChangeAction Demo' -> title;
             (40,40) -> position;
             (300,300) -> size;
             true -> allowEdit;
             theTextfield.open;
             (300,300) -> theTextfield.size
          #)
     #)
do theWindow.open
#)
