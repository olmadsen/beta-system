ORIGIN '~beta/guienv/v1.3/guienv';
INCLUDE '~beta/guienv/v1.3/controls';
INCLUDE '~beta/guienv/v1.3/guienvactions';
(* This demo shows how to prepend/append actions to keyDown events in
 * an editText control.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# menubarType::
          (# testMenu: @menu
               (# quitItem: @menuItem
                    (# open::
                         (# do 'Quit' -> name #);
                       eventHandler::
                         (# onSelect::
                              (# do terminate #)
                         #)
                    #);
                  open::
                    (# do quitItem.open;  quitItem[] -> append #)
               #);
             open::
               (# do testMenu.open; testMenu[] -> append #)
          #);
        editText1: @editText
          (# beforeKeyDown: @keyDownAction
               (# do 'My ' -> puttext #);
             afterKeyDown1: @keyDownAction
               (# do 'is ' -> puttext #);
             afterKeyDown2: @keyDownAction
               (# do 'EditText1' -> putline #);
             open::
               (# 
               do (40,40) -> position;
                  (100,30) -> size;
                  beforeKeyDown[] -> prependAction;
                  afterKeyDown1[] -> appendAction;
                  afterKeyDown2[] -> appendAction
               #);
             eventHandler::
               (# onKeyDown::
                    (# do 'name ' -> puttext #)
               #)
          #);
        open::
          (# 
          do (400,400) -> size;
             editText1.open;
             contents -> target
          #)
     #)
do theWindow.open
#)
