ORIGIN '~beta/guienv/v1.2/guienv';
INCLUDE '~beta/guienv/v1.2/controls';
INCLUDE '~beta/guienv/v1.2/guienvactions';

(* This demo shows how to prepend/append actions to keyDown events
 * in an editText control. 
 *)

-- program: descriptor --
guienv
(# 
   theWindow: @window
     (# menubarType::
          (# testMenu: @menu
               (# quitItem: @menuitem
                    (# open::
                         (# do 'Quit' -> name; #);
                       eventHandler::
                         (# onSelect::
                              (# do Terminate #);
                         #);
                    #);
                  open::
                    (# do quitItem.open;  quitItem[] -> append; #);
               #);
             open::
               (# do testMenu.open; testMenu[] -> append; #);
          #);
        editText1: @editText
          (# beforeKeyDown: @keyDownAction
               (# do 'Jeg ' -> putText; #);
             afterKeyDown1: @keyDownAction
               (# do 'Michael ' -> putText; #);
             afterKeyDown2: @keyDownAction
               (# do 'Lassen.' -> putLine; #);
             open::
               (# 
               do (40,40) -> position;
                  (100,30) -> size;
                  beforeKeyDown[] -> prependAction;
                  afterKeyDown1[] -> appendAction;
                  afterKeyDown2[] -> appendAction;
               #);
             eventHandler::
               (# onKeyDown::
                    (# do 'hedder ' -> putText; #);
               #);
          #);
        open::
          (# 
          do (400,400) -> size;
             editText1.open;
             contents -> target;
          #);
     #);
do theWindow.open;
#)
