ORIGIN  '~beta/guienv/v1.2/fields';
INCLUDE '~beta/guienv/v1.2/stddialogs';
INCLUDE '~beta/basiclib/v1.4/file';
(* This demo shows how to build a simple texteditor. *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# textFile: @file;
                  openItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# theText: @StyledText; path: ^text;
                              do theWindow[] -> fileSelectionDialog -> path[];
                                 (if path[]<>NONE then
                                     path[] -> textFile.name;
                                     textFile.openRead;
                                     textFile.scan
                                     (# while:: (# do true->value #);
                                     do ch -> theText.put
                                     #);
                                     theText[]
                                       ->theTextEditor.contents.contents;
                                     textFile.close
                                 if)
                         #)#);
                       open:: (# do 'Open' -> name #)
                    #);
                  saveItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# theText: @Text; tempName: ^text;
                              do textFile.name -> tempName[];
                                 (if tempName.length>0 then
                                     textFile.openWrite;
                                     theTextEditor.contents.contents
                                       ->textFile.puttext;
                                     textFile.close
                                 if)
                         #)#);
                       open:: 
                         (# do 'Save' -> name #)
                    #);
                  quitItem: @menuItem
                    (# eventHandler:: 
                         (# onSelect:: (# do terminate #) #);
                       open:: 
                         (# do 'Quit' -> name #)
                    #);
                  open::
                    (#
                    do 'File' -> name;
                       openItem.open; openItem[] -> append;
                       saveItem.open; saveItem[] -> append;
                       quitItem.open; quitItem[] -> append
               #)#);
             open:: 
               (# do fileMenu.open; fileMenu[] -> append #)
          #); (* menubarType *)
        thetextEditor: @textEditor
          (# open:: 
               (# 
               do 'TextEditor' -> title;
                  theWindow.size -> size;
                  true -> bindBottom -> bindRight
               #)
          #);
        open::
          (#
          do 'thetextEditor' -> title;
             (400,400) -> size;
             thetextEditor.open
          #)
     #)
do theWindow.open
#)
