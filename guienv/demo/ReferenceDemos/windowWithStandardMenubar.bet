ORIGIN '~beta/guienv/v1.4/guienv';
(* This demo shows how to create a simple window with a
 * standardMenubar where the file menu only has one menuitem. It also
 * illustrates how to further bind the onMouseDown, onMouseUp,
 * onKeyDown event patterns.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# menubarType:: standardMenubar
          (# fileMenu::
               (# quitItem: @menuitem
                    (# open::
                         (# do 'Quit' -> name #);
                      eventHandler::
                         (# onSelect::
                              (# do terminate #)
                         #)
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       quitItem.open; quitItem[] -> append
                    #)
               #)
          #);
        eventHandler::
          (# onAboutToClose:: (# do terminate #);
             onMouseDown::
               (# 
               do 'MouseDown: ' -> puttext;
                  buttonState -> putint;
                  (if doubleClick then
                      ' doubleclick' -> puttext
                  if);
                  newLine
               #);
             onMouseUp::
               (# 
               do 'onMouseUp ' -> puttext;
                  buttonState -> putint;
                  (if doubleClick then
                      ' doubleclick' -> puttext
                  if);
                  newLine
               #);
             onKeyDown::
               (# 
               do 'onKeyDown: ' -> puttext;
                  ch -> put;
                  newLine
               #)
          #)
     #)
do theWindow.open
#)
