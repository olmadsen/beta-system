ORIGIN '~beta/guienv/v1.2/guienv';

(* This demo shows how to create a simple window with a standardMenubar
 * where the file menu only has one menuitem. It also illustrates how to
 * further bind the onMouseDown, onMouseUp, onKeyDown event patterns.
 *)

-- program: descriptor --
guienv
(# theWindow: @window
     (# menubarType:: standardMenubar
          (# fileMenu::
               (# quitItem: @menuitem
                    (# open::
                         (# do 'Quit' -> name; #);
                      eventHandler::
                         (# onSelect::
                              (# do Terminate #);
                         #);
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       quitItem.open; quitItem[] -> append;
                    #);
               #);
          #);
        eventHandler::
          (# onMouseDown::
               (# 
               do 'MouseDown: ' -> putText;
                  buttonState -> putInt;
                  (if doubleClick//true then
                      ' doubleclick' -> putText;
                  if);
                  newLine;
               #);
             onMouseUp::
               (# 
               do 'onMouseUp ' -> putText;
                  buttonState -> putInt;
                  (if doubleClick//true then
                      ' doubleclick' -> putText;
                  if);
                  newLine;
               #);
             onKeyDown::
               (# 
               do 'onKeyDown: ' -> putText;
                  ch -> put;
                  newLine;
               #);
          #);
     #);
do theWindow.open;
#)
