ORIGIN '~beta/guienv/v1.4.1/guienv';
INCLUDE '~beta/guienv/v1.4.1/controls';
(* This demo shows how to create a window with two pushButtons, and
 *  how to give a button a new size on runtime.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# menubarType::
          (# testMenu: @menu
               (# sizeItem: @menuItem
                    (# open::
                         (# do 'Set size' -> name #);
                       eventHandler::
                         (# onSelect::
                              (# do (200,200)->helloButton.size #);
                         #);
                    #);
                  quitItem: @menuItem
                    (# open::
                         (# do 'Quit' -> name #);
                       eventHandler::
                         (# onSelect::
                              (# do terminate #);
                         #);
                    #);
                  open::
                    (# 
                    do sizeItem.open; sizeItem[] -> append;
                       quitItem.open; quitItem[] -> append
                    #)
               #);
             open::
               (# 
               do testMenu.open; testMenu[] -> append
               #)
          #);
        quitButton: @pushButton
          (# eventHandler::
               (# onMouseUp::
                    (# 
                    do 'Good bye, World' -> putline;
                       terminate
                    #)
               #);
             open::
               (# 
               do (40,40) -> position;
                  (100,30) -> size;
                  'Quit' -> label
               #)
          #);
        helloButton: @pushButton
          (# eventHandler::
               (# onLabelChanged::
                    (# 
                    do 'helloButton.onLabelChanged' -> putline
                    #);
                  onMouseUp::
                    (# 
                    do 'Hello, World - My name is ' -> puttext;
                       label -> putline
                    #)
               #);
             open::
               (# 
               do (40,80) -> position;
                  (100,30) -> size; 
                  'hello,world' -> putline;
                  'Hello' -> label
               #)
          #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        open::
          (# 
          do (400,400) -> size;
             quitButton.open;
             helloButton.open;
             contents -> target
          #)
     #)
do theWindow.open
#)
