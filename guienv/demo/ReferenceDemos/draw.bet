ORIGIN '~beta/guienv/v1.3.1/guienv';
INCLUDE '~beta/guienv/v1.3.1/figureitems';
(* This demo gives an example of how you can draw lines and polygons
 * with mouse using the figureitems "line" and "polygon".  Clicking
 * with the left mouse button defines a node in the polygon/line,
 * clicking with the right mouse button ends the definition of the
 * polygon/line and draws it on the screen.
 *)
--- program: descriptor ---
guienv
(# drawEditor: @window
     (# points: @
          (# ps: [16] ^point;
             top: @integer;
             init:
               (# 
               do 0 -> top;
                  (for i: ps.range repeat
                       &point[] -> ps[i][]
                  for)
               #);
             clear:
               (# do 0 -> top #);
             add:
               (# p: @point;
                  size: @integer
               enter p
               do top + 1 -> top;
                  (if top > ps.range then
                      ps.range -> size;
                      size -> ps.extend;
                      (for i: size repeat
                           &point[] -> ps[size + i][]
                      for)
                  if);
                  p -> ps[top]
               #);
          exit ps[1:top]
          #);
        polygonEditor: polygon
          (# open::
               (# do patterns.dkgray[] -> fill.tile #);
             eventHandler::
               (# onMouseDown::
                    (#
                    do drag;
                       this(polygonEditor)[] -> father.selection.set
                    #)
               #)
          #);
        lineEditor: line
          (# eventHandler::
               (# onMouseDown::
                    (#
                    do drag;
                       this(lineEditor)[] -> father.selection.set
                    #)
               #)
          #);
        definingPoly: @boolean;
        eventHandler::
          (# onMouseDown::
               (# ps: [0] ^point
               do (if definingPoly then
                      (if buttonState
                       //1 then
                          localPosition -> points.add
                       //3 then
                          localPosition -> points.add;
                          points -> ps;
                          (if ps.range > 2 then
                              ps -> createPolygon
                           else
                              ps -> createLine
                          if);
                          false -> definingPoly
                      if);
                   else
                      (if buttonState=1 then
                          true -> definingPoly;
                          points.clear;
                          localPosition -> points.add
                      if)
                  if)
               #)
          #);
        createPolygon:
          (# ps: [0] ^point;
             poly: ^polygon
          enter ps
          do &polygonEditor[] -> poly[];
             poly.open;
             ps -> poly.points
          #);
        createLine:
          (# l: ^line;
             ps: [0] ^point
          enter ps
          do &lineEditor[] -> l[];
             l.open;
             ps[1] -> l.start;
             ps[2] -> l.end
          #);
        open::
          (# 
          do (400,500) -> size;
             contents -> target;
             points.init
          #)
     #)
do drawEditor.open
#)
