ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';
(* This demo shows how to use the pixmap pattern when creating an
 * iconButton control. Each time you click in the window a new
 * iconButton is created and positioned at the point where you click.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# iconButtonType: iconButton
          (# pm: @pixmap;
             open::
               (# 
               do 'picture' -> pm.read;
                  pm[] -> icon;
                  true -> showLabel;
                  'Mjolner BETA' -> label;
                  (140,100) -> size;
                  true -> border.visible;
                  borderStyles.shadowOut -> border.style
               #);
             eventHandler::
               (# onMouseDown::
                    (# do drag #)
               #)
          #);
        eventHandler::
          (# onAboutToClose:: (# do terminate #);
             onMouseDown::
               (# theIconButton: ^IconButtonType;
               do &iconButtonType[] -> theIconButton[];
                  theIconButton.open;
                  localPosition -> theIconButton.position
               #)
          #);
        open::
          (# do (500,500) -> size #)
     #)
do theWindow.open
#)
