ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/graphics';
(* This demo is an example showing how read a bitmap from a file, and
 * using the graphics pattern to draw the bitmap on the screen.
 *)
--- program: descriptor ---
guienv
(# pm: @pixmap;
   theWindow: @window
     (# eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        obj: @windowItem
          (# open::
               (# 
               do (50,50) -> position;
                  (pm.width,pm.height) -> size
               #);
             eventHandler::
               (# onRefresh::
                    (# 
                    do graphics
                       (# 
                       do (pm[], (0, 0), (0, 0), pm.width,pm.height)
                            -> drawRaster
                       #)
                    #);
                  onMouseDown::
                    (# do drag #)
               #)
          #);
        open::
          (# 
          do (400,400) -> size;
             obj.open
          #)
     #)
do 'picture.x11' -> pm.read;
   theWindow.open
#)
