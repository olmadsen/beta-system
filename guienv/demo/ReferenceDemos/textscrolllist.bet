ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/scrolllists';
(* This demo gives a simple example of how to use the textScrollList
 * windowitem.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# quitItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do terminate #);
                         #);
                       open::
                         (# do 'Quit' -> name #)
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       quitItem.open; quitItem[] -> append
                    #)
               #);
             open::
               (# 
               do fileMenu.open; fileMenu[] -> append
               #)
          #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        theTextScrollList: @textScrollList
          (# eventHandler::
               (# onMouseDown::
                    (# 
                    do (if doubleClick then
                           selection.first -> gettext -> putline
                       if)
                    #)
               #);
             open::
               (# v: @point;
                  noOfItems: @integer
               do (5,5) -> position;
                  theWindow.size -> v;
                  (10,10) -> v.subtract;
                  v -> size;
                  true -> bindBottom;
                  true -> bindRight;
                  5 -> append;
                  theTextScrollList.numberOfItems -> noOfItems;
                  (noOfItems-4,'Abraham Lincoln 1') -> settext;
                  (noOfItems-3,'Abraham Lincoln 2') -> settext;
                  (noOfItems-2,'Abraham Lincoln 3') -> settext;
                  (noOfItems-1,'Abraham Lincoln 4') -> settext;
                  (noOfItems,'Abraham Lincoln 5') -> settext
               #)
          #);
        open::
          (# 
          do (400,400) -> size;
             theTextScrollList.open;
             theTextScrollList[] -> target
          #)
     #)
do theWindow.open
#)
