ORIGIN '~beta/guienv/v1.2/guienv';
INCLUDE '~beta/guienv/v1.2/scrolllists';

(* This demo gives a simple example of how to use the textScrollList
 * windowitem.
 *)

-- program: descriptor --
guienv
(# theWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (#
                  quitItem: @menuitem
                    (# eventHandler::
                         (# onSelect::
                              (# do Terminate #);
                         #);
                       open::
                         (# do 'Quit' -> name; #);
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       quitItem.open; quitItem[] -> append;
                    #);
               #);
             open::
               (# 
               do fileMenu.open; fileMenu[] -> append;
               #);
          #);
        theTextScrollList: @textScrollList
          (# eventHandler::
               (# onMouseDown::
                    (# 
                    do (if doubleClick//true then
                           Selection.first -> getText -> putLine;
                       if);
                    #);
               #);
             open::
               (# v: @point;
                  noOfItems: @integer;
               do (5,5) -> position;
                  theWindow.size -> v;
                  (10,10) -> v.subtract;
                  v -> size;
                  true -> bindBottom;
                  true -> bindRight;
                  5 -> append;
                  theTextScrollList.numberOfItems -> noOfItems;
                  (noOfItems-4,'Abraham Lincoln 1') -> setText;
                  (noOfItems-3,'Abraham Lincoln 2') -> setText;
                  (noOfItems-2,'Abraham Lincoln 3') -> setText;
                  (noOfItems-1,'Abraham Lincoln 4') -> setText;
                  (noOfItems,'Abraham Lincoln 5') -> setText;
               #);
          #);
        open::
          (# 
          do (400,400) -> size;
             theTextScrollList.open;
             theTextScrollList[] -> target;
          #);
     #);
do theWindow.open; 
#)
