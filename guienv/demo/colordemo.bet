ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/utils/colorTable';

--- program: descriptor ---

(* This demo illustrates the use of the colorTable;
 * ColorTable is a mapping between colornames and 
 * their respective rgb-values.
 * 
 * The colortable includes a number of predefined
 * colors, but enables the user to use a 
 * user-defined colorfile. This demo uses 
 * a file: colornames.small
 * 
 * The demo also show how to use backgroundcolor
 * on windowitems and windows.
 *)

guienv
(# ct: @colorTable;
   win:@window
     (# 
        c1,c2:@canvas
          (# open::<
               (# 
               do (40,40)->size;
               #);
          #);
        open::<
          (# col1,col2,col3:@color;
          do (* Set up background colors *)
             'Green'->title;
             'green'->ct.lookup->col1;
             (*col1->backgroundcolor; *)
             
             c1.open; 
             (40,40)->c1.position;
             'purple'->ct.lookup->col2; (* Note: purble is not defined in colornames.small, but is a default color*)
             col2->c1.backgroundcolor;
             
             c2.open;
             (100,100)->c2.position;
             'gold1'->ct.lookup->col3;
             col3->c2.backgroundcolor;
          #);
     #);
   win2:@window
     (# open::<
          (# c:@color;
          do 'Cornflower Blue'->title;
             'cornflower blue'->ct.lookup->c;
             (*c->backgroundcolor;*)
             (400,400)->position;
          #);
     #);
do
   'colornames.small'->ct.loadFile(# merge::trueobject #) ;
   ct.scan(# do currentName[]->putline; #);
   win.open;
   win2.open;
#)



