ORIGIN '~beta/guienv/v1.6/demo/Basics/Custom/groupbox';

-- program: descriptor --
GUIenv
 (# theWindow: @window
     (# resizeable:: (# do False -> value #);
         theGroupBox: @groupBox
           (# nameLab: @staticText
              (# open::
                  (#
                  do 'Name:' -> label;
                     (10, 35) -> position;
                     fitToContents;
                  #);
              #);
             nameFld: @editText
               (# open::
                   (# 
                   do (50, 30) -> position;
                      FitToContents;
                   #);
               #);
             IDLab: @staticText
              (# open::
                  (#
                  do 'ID:' -> label;
                     (10, 65) -> position;
                     fitToContents;
                  #);
              #);
             IDFld: @editText
              (# open::
                  (#
                  do (50, 60) -> position;
                     fitToContents;
                  #);
              #);
             open::
              (#
              do ' Identification: ' -> caption;
                 (10, 10) -> position;
                 (300, 100) -> size;
                 nameLab.open;
                 nameFld.open;
                 nameFld[] -> target;
                 IDLab.open;
                 IDFld.open;
                 
              #);
          #);
        open::
         (# width, height: @integer;
         do 'Simple Groupbox Demo' -> Title;
            theGroupBox.open;
            theGroupBox.size -> (width, height);
            (width + 20, height + 20) -> size;
         #);
        close::
         (# do Terminate; #);
     #);
 do theWindow.open;
 #)