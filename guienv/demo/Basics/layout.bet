ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/basiclib/numberio';

-- program: descriptor -- 
GUIenv
(# 
  
    makeColor:
      (# red, green, blue: @real;
         r, g, b: @integer;
      enter (red, green, blue)
      do red * 0xFFFF -> r;
         green * 0xFFFF -> g;
         blue * 0xFFFF -> b;
      exit (r, g, b)
      #);
   
    toplevelWindow: @Window
     (# sidebar: @Canvas
          (# editingPanelToggle: @PushButton
               (#
                  open::
                    (#
                    do (16, 16) -> size;
                       (2, 8) -> position;
                       '<' -> label;
                    #);
                  eventHandler::
                    (# onMouseUp::
                         (# width, height: @Integer;
                            editingPanelWidth, editingPanelHeight: @Integer;
                         do (if editingPanel.visible then
                               false -> editingPanel.visible;
                               '>' -> label;
                               THIS(Window).size -> (width, height);
                               (width - 20, height) -> executionPanel.size;
                               (20, 0) -> executionPanel.position;
                            else
                               true -> editingPanel.visible;
                               '<' -> label;
                               THIS(Window).size -> (width, height);
                               editingPanel.size -> (editingPanelWidth, editingPanelHeight);
                               (width - editingPanelWidth - 20, height) -> executionPanel.size;
                               (editingPanelWidth + 20, 0) -> executionPanel.position;
                            if);
                         #);
                    #);
               #);
             open::
               (# 
               do (20, 800) -> size;
                  (0xE6 / 255, 0xE6 / 255, 0xE6 / 255) -> makeColor -> backgroundColor;
                  true -> bindBottom;
                  editingPanelToggle.open;
               #);
          #);
        editingPanel: @Canvas
          (# adjustHorizontal:
               (# horizontal: @Integer;
                  width, height: @Integer;
               enter horizontal
               do size -> (width, height);
                  (width + horizontal, height) -> size;
                  true -> update;
               #);
             browser: @Canvas
               (# open::
                    (#
                    do 
                       (360 - 4, 260) -> size;
                       (0xE6 / 255, 1.0, 0xCC / 255) -> makeColor -> backgroundColor;
                       true -> border.visible;
                       borderStyles.simple -> border.style;
                       true -> bindRight;
                    #);
               #);
             editor: @Canvas
               (# open::
                    (#
                    do (0, 260) -> position;
                       (360 - 4, 540) -> size;
                       (1.0, 1.0, 0x99 / 255) -> makeColor -> backgroundColor;
                       true -> border.visible;
                       borderStyles.simple -> border.style;
                       true -> bindRight;
                       true -> bindBottom;
                    #);
               #);
             open::
               (#
               do (20, 0) -> position;
                  (360, 800) -> size;
                  true -> bindBottom;
                  browser.open;
                  editor.open;
               #);
             eventHandler::
               (# onMouseDown::
                    (#
                    do trackMouse
                         (# mouseMove::
                              (#
                              do h -> adjustHorizontal;
                                 h -> executionPanel.adjustHorizontal;
                              #);
                         #);
                    #);
               #);
          #);
        executionPanel: @Canvas
          (# adjustHorizontal:
               (# horizontal: @Integer;
                  width, height: @Integer;
                  x, y: @Integer;
               enter horizontal
               do size -> (width, height);
                  (width - horizontal, height) -> size;
                  position -> (x, y);
                  (x + horizontal, y) -> position;
                  true -> update;
               #);
             diagram: @Canvas
               (# adjustHorizontal:
                      (# width, height: @Integer;
                         x, y: @Integer;
                         horizontal: @Integer;
                      enter horizontal
                      do size -> (width, height);
                         width - horizontal -> width;
                         (width, height) -> size;
                         position -> (x, y);
                         x + horizontal -> x;
                         (x, y) -> position;
                         true -> update;
                      #);
                  open::
                    (#
                    do (820, 600) -> size;
                       (0xCC / 0xFF, 0xE5 / 0xFF, 1.0) -> makeColor -> backgroundColor;
                       true -> border.visible;
                       borderStyles.simple -> border.style;
                       true -> bindBottom;
                       true -> bindRight;
                    #);
               #);
             bottombar: @Canvas
               (# adjustHorizontal:
                    (# width, height: @Integer;
                       x, y: @Integer;
                       horizontal: @Integer;
                    enter horizontal
                    do size -> (width, height);
                       width - horizontal -> width;
                       (width, height) -> size;
                       position -> (x, y);
                       x + horizontal -> x;
                       (x, y) -> position;
                       true -> update;
                    #);
                  open::
                    (#
                    do (0, 600) -> position;
                       (820, 20) -> size;
                       (0xE6 / 255, 0xE6 / 255, 0xE6 / 255) -> makeColor -> backgroundColor;
                       true -> border.visible;
                       borderStyles.simple -> border.style;
                       false -> bindTop;
                       true -> bindBottom;
                       true -> bindRight;
                    #);
               #);
             console: @Canvas
               (# adjustHorizontal:
                    (# width, height: @Integer;
                       x, y: @Integer;
                       horizontal: @Integer;
                    enter horizontal
                    do size -> (width, height);
                       width - horizontal -> width;
                       (width, height) -> size;
                       position -> (x, y);
                       x + horizontal -> x;
                       (x, y) -> position;
                       true -> update;
                    #);
                  open::
                    (#
                    do (0, 620) -> position;
                       (820, 180) -> size;
                       (0xFF / 255, 0xE6 / 255, 0xCC / 255) -> makeColor -> backgroundColor;
                       true -> border.visible;
                       borderStyles.simple -> border.style;
                       false -> bindTop;
                       true -> bindBottom;
                       true -> bindRight;
                    #);
               #);
             open::
               (#
               do (380, 0) -> position;
                  (820, 800) -> size;
                  diagram.open;
                  bottombar.open;
                  console.open;
                  true -> bindRight;
                  true -> bindBottom;
               #);
          #);
        open::
          (#
          do 'Layout Mockup' -> title;
             (1200, 800) -> size;
             (600, 400) -> minSize;
             (0xE6 / 255, 0xE6 / 255, 0xE6 / 255) -> makeColor -> backgroundColor;
             sidebar.open;
             editingPanel.open;
             executionPanel.open;
          #);
     #);
do toplevelWindow.open;
#)
