ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';
-- program: descriptor --
GUIenv
	(# mainWindow: @window
			(# resizeable:: (# do false -> value #);
				anOptionButton: @optionButton
					(#	eventHandler::
							(# onCurrentItemChanged::
									(# 
									do (if currentItem
											//1 then 'London' -> THIS(window).title;
											//2 then 'Copenhagen' -> THIS(window).title;
											//3 then 'Paris' -> THIS(window).title;
										if);
									#);
							#);
						aMenu: @menu
							(# addMenuitem:
									(# itsName: ^text;
									enter itsName[]
									do (# theMenuItem: ^menuItem;
										do &menuItem[] -> theMenuItem[];
											theMenuItem.open;
											itsName[] -> theMenuItem.name;
											theMenuItem[] -> append;
										#);
									#);
								open::
									(#
									do 'England' -> addMenuitem;
										'Denmark' -> addMenuItem;
										'France' -> addMenuItem;
									#);
							#);
						open::
							(# 
							do (13, 13) -> position;
								aMenu.open;
								aMenu[] -> popupMenu;
								'Country:' -> label;
								fitToContents;
							#);
					#);
				open::
					(# width, height: @integer;
					do '' -> title;
						anOptionButton.open;
						anOptionButton.size -> (width, height);
						(width+ 26, height + 26) -> size;
					#);
				close::
					(#
					do Quit;
					#);
			#);		
	do mainWindow.open;
	#)
