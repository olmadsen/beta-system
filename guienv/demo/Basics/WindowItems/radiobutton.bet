ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';
-- program: descriptor --
GUIenv
(# mainWindow: @window
     (# resizeable:: (# do false -> value #);
        radioButton1: @radiobutton
          (# eventHandler::
               (# onStateChanged::
                    (#
                    do (if state then
                           'Et simpelt RadioButton eksempel' -> THIS(window).title;
                           false -> radioButton2.state;
                       if);
                    #);
               #);
             open::
               (# aTextStyle: ^textStyle;
               do 'Dansk titel' -> label;
                  
                  (13, 13) -> position;
                  
                  fitToContents;
               #);
          #);
        radioButton2: @radiobutton
          (# eventHandler::
               (# onStateChanged::
                    (#
                    do (if state = true then
                           'A Simple Radiobutton Example' -> THIS(window).title;
                           false -> radioButton1.state;
                       if);
                    #);
               #);
             open::
               (#
               do 'English Title' -> label;
                  
                  (13, 40) -> position;
                  
                  fitToContents;
               #);
          #);
        
        open::
          (# width, height: @integer;
          do '' -> title;
             radioButton1.open;
             radioButton2.open;
             true -> radioButton1.state;
             (300, 100) -> size;
          #);
        close::
          (#
          do terminate
          #);
     #);		
do mainWindow.open;
#)
