ORIGIN '~beta/guienv/v1.6/fields';

-- program: descriptor --
GUIenv
 (# red: (# exit (0xFFFF, 0xAFFF, 0xAFFF) #);
    white: (# exit (0xFFFF, 0xFFFF, 0xFFFF) #);
    light_gray: (# exit (0xDDDD, 0xDDDD, 0xDDDD) #);
    
    theWindow: @window
     (# theScroller: @scroller
         (# contentsType::
             (# currentPos: @integer;
                current: ^entry;
                entry: staticText
                 (# selected: @boolean;
                    eventHandler::
                     (# onMouseDown::
                         (#
                         do (if current[] <> THIS(entry)[] then
                              (if current[] <> NONE then
                                 light_gray -> current.backgroundColor;
                              if);
                              red -> backgroundColor;
                              THIS(entry)[] -> current[];
                            if);
                         #);
                     #);
                    open::
                      (# height: @integer;
                      do light_gray -> backgroundColor;
                         (style).lineHeight -> height;
                         (1000, height) -> size;
                         (0, currentPos) -> position;
                         currentPos + height + 2 -> currentPos;
                      #);
                 #);
                open::
                  (# name: ^text;
                     anEntry: ^entry;
                  do white -> backgroundColor;
                     2 -> currentPos;
                     (for i: 200 repeat
                        &entry[] -> anEntry[];
                        anEntry.open;
                        'Entry Number ' -> name[];
                        i -> name.putInt;
                        name[] -> anEntry.label;
                     for);
                     
                  #);
             #);
            open::
             (# 
             do theWindow.size -> size;
                (1000, contents.currentPos) -> contents.size;
                TRUE -> bindRight;
                TRUE -> bindBottom;
             #);
         #);
        open::
         (#
         do 'Simple Scroller Demo' -> Title;
            theScroller.open;
         #);
        close::
         (# do Terminate #);
     #);
 do theWindow.open;
 #)