ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/scrolllists';
-- program: descriptor --
GUIenv
(# mainWindow: @window
     (# aScrollList: @textScrollList
          (# multipleSelection:: (# do true -> value #);
             open::
               (# aTextStyle: ^textStyle;
               do &textStyle[] -> aTextStyle[];
                  'Times' -> aTextStyle.name;
                  12 -> aTextStyle.size;
                  textFaces.bold -> aTextStyle.face;
                  aTextStyle[] -> style;
                  (13, 13) -> position;
                  
                  (200, 300) -> size;
                  100 -> append;
                  (1, '1 bottle on the wall') -> setText;
                  (2, 100) -> forTo
                  (# t: @text;
                  do t.clear;
                     inx -> t.putInt;
                     ' bottles on the wall' -> t.putText;
                     (inx, t[]) -> setText;
                  #);
               #);
          #);
        open::
          (# width, height: @integer;
          do 'Simple List Example' -> title;
             aScrollList.open;
             aScrollList.size -> (width, height);
             (width+ 26, height + 26) -> size;
             true -> aScrollList.bindRight;
             true -> aScrollList.bindBottom;
          #);
        close::
          (#
          do terminate
          #);
     #);		
do mainWindow.open;
#)
