ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';
-- program: descriptor --
GUIenv
	(# 
	
		menubarType::
			(# fileMenu: @menu
					(# newItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(# aDocumentWindow: ^documentWindow;
											do	&documentWindow[] -> aDocumentWindow[];
												aDocumentWindow.open;
											#);
									#);
								open::
									(#
									do 'New' -> name;
										'N' -> key;
									#);
							#);
						closeItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	(if currentDocumentWindow[] <> NONE then
													currentDocumentWindow.userClose;
												if);
											#);
									#);
								open::
									(#
									do 'Close' -> name;
										'W' -> key;
									#);
							#);
						quitItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	Quit;
											#);
									#);
								open::
									(#
									do 'Quit' -> name;
										'Q' -> key;
									#);
							#);
						open::
							(#
							do 'File' -> name;
								newItem.open;
								newItem[] -> append;
								closeItem.open;
								closeItem[] -> append;
								quitItem.open;
								quitItem[] -> append;
							#);
					#);
				open::
					(#
					do fileMenu.open;
						fileMenu[] -> append;
					#);
			#);
			
					
		currentDocumentWindow: ^documentWindow;
			
		documentWindow: window
			(# userClose:
					(#
					do (if theEventHandler.onAboutToClose then
							close;
						if);
					#);
				eventHandler::
					(# onAboutToClose::
							(* This is called by the system, when the user
							 * does something to close the window.
							 * If you add a menucommand or button that
							 * closes the window, you should call 
							 *    theEventHandler.onAboutToClose
							 * yourself and then only close the window if it 
							 * returns true - see the "userClose" pattern above).
							 *)
							(# theAskDialog: @askDialog
									(# onCancel::
											(#
											do false -> okToClose;
											#);
										onConfirm::
											(#
											do true -> okToClose;
											#);
									#);
							do 'Do you really want do close the window?' -> theAskDialog.run;
							#);
						onActivate::
							(#
							do THIS(documentWindow)[] -> currentDocumentWindow[];
							#);
						onDeactivate::
							(#
							do NONE -> currentDocumentWindow[];
							#);
					#);
				open::
					(#
					do 'Document Window' -> title;
						currentPosition -> position;
						(5, 20) -> currentPosition.add;
						(300, 200) -> size;
					#);
			#);
			
		askDialog: window
			(# type:: windowTypes.dialog;
				resizeable:: (# do false -> value #);
				
				run:
					(# msg: ^text;
					enter msg[]
					do open; (* NOTE: Normally open will display the dialog on
								 * 		the screen, which is undesirable in this 
								 *			case, since the "showModal" will take care
								 *			of that. Therefore open is furtherbound
								 *			to hide the window. See below.
					          *)
						msg[] -> message.label;
						showModal;
					#);
				open::
					(#
					do hide;
						'' -> title;
						(100, 100) -> position;
						(300, 83) -> size;
						message.open;
						cancelButton.open;
						confirmButton.open;
						confirmButton[] -> defaultButton;
					#);
					
				onConfirm:<
					(# do close; INNER; #);
				onCancel:<
					(# do close; INNER; #);
			
				message: @staticText
					(# open::
							(#
							do '' -> label;
								(10, 10) -> position;
								(280, 30) -> size;
							#);
					#);
				confirmButton: @pushButton
					(# eventHandler::
							(# onMouseUp::
									(#
									do onConfirm;
									#);
							#);
						open::
							(#
							do (230, 53) -> position;
								(60, 20) -> size;
								'OK' -> label;
							#);
					#);
				cancelButton: @pushButton
					(# eventHandler::
							(# onMouseUp::
									(#
									do onCancel;
									#);
							#);
						open::
							(#
							do (157, 53) -> position;
								(60, 20) -> size;
								'Cancel' -> label;
							#);
					#);
				
			#);

		
		aDocumentWindow: @documentWindow;
		currentPosition: @point;
	do (5, 40) -> currentPosition;
		aDocumentWindow.open;
	#)
  