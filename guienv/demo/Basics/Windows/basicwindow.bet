ORIGIN '~beta/guienv/v1.6/guienv';
-- program: descriptor --
GUIenv
(# menubarType::
     (# fileMenu: @menu
          (# quitItem: @menuItem
               (# eventHandler::
                    (# onSelect::
                         (#
                         do terminate;
                         #);
                    #);
                  open::
                    (#
                    do 'Quit' -> name;
                       'Q' -> key;
                    #);
               #);
             open::
               (#
               do 'File' -> name;
                  quitItem.open;
                  quitItem[] -> append;
               #);
          #);
        open::
          (#
          do fileMenu.open;
             fileMenu[] -> append;
          #);
     #);
   basicWindow: @window
     (# open::
          (#
          do (* The "open" operation displays the window on the screen.
              * You can change the properties of the window at any
              * time when the window has been opened.
              * 
              * Here you set the properties to the initial values.
              *)
             
             'Basic Window' -> title;  (* The Title is displayed in the
                                        * titlebar of the window, if
                                        * it has a titlebar.
                                        *)
             (10, 40) -> position;     (* The position of the window is defined as
                                        * the position of the contents area on the
                                        * on the screen, not the titlebar.
                                        * you should not give the window a
                                        * position of (0, 0), since this will place
                                        * the titlebar under the menubar, if the desktop
                                        * has a menubar.
                                        *)
             (300, 200) -> size;		  (* The size is defined as the size of the contents area,
                                                   * the actual size of the window will be bigger if it
                                                   * has a border and titlebar (which is controlled by the
                                                   * system).
                                                   *)
          #);
        close::
          (#
          do (*
              * The "close" operation removes the window from the screen
              * it is not allowed to use any of the windows operation
              * when it is closed, but still allowed in the furtherbinding
              * of "close".
              *)
             
             terminate;  (* Quit the application when this window closes *)
          #);
     #);
do basicWindow.open;
#)
