ORIGIN '~beta/guienv/v1.6/guienv';
-- program: descriptor --
GUIenv
	(# menubarType::
			(# fileMenu: @menu
					(# newItem: @menuItem
							(# counter: @integer;
								eventHandler::
									(# onSelect::
											(# anApplicationWindow: ^applicationWindow;
												itsTitle: ^text;
											do	&applicationWindow[] -> anApplicationWindow[];
												&text[] -> itsTitle[];
												'Application Window <<' -> itsTitle.putText;
												counter -> itsTitle.putInt;
												'>>' -> itsTitle.putText;
												itsTitle[] -> anApplicationWindow.open;
												counter + 1 -> counter;
											#);
									#);
								open::
									(#
									do 'New' -> name;
										'N' -> key;
										1 -> counter;
									#);
							#);
						quitItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	Quit;
											#);
									#);
								open::
									(#
									do 'Quit' -> name;
										'Q' -> key;
									#);
							#);
						open::
							(#
							do 'File' -> name;
								newItem.open;
								newItem[] -> append;
								quitItem.open;
								quitItem[] -> append;
							#);
					#);
				
				open::
					(#
					do fileMenu.open;
						fileMenu[] -> append;
						windowMenu.open;
						windowMenu[] -> append;
					#);
			#);
			
		(*
		 * A menu does not need to be declared inside "menubarType::< (# ... #)"
		 * We declare the windowMenu here, to make it easier to access.
		 *)
		windowMenu: @menu
			(# windowMenuItem: menuItem
					(# theWindow: ^applicationWindow;
						open::
							(#
							enter theWindow[]
							do theWindow.title -> name;
							#);
						eventHandler::
							(# onSelect::
									(#
									do (if theWindow.visible then
											theWindow.bringToFront;
										else
											theWindow.show;
											theWindow.bringToFront;
										if);
									#);
							#);
					#);
				items: @list
					(# element:: windowMenuItem;
					#);
					
				add:
					(# theWindow: ^applicationWindow;
						aWindowMenuItem: ^windowMenuItem;
					enter theWindow[]
					do &windowMenuItem[] -> aWindowMenuItem[];
						theWindow[] -> aWindowMenuItem.open;
						aWindowMenuItem[] -> append;
						aWindowMenuItem[] -> items.append;
					#);
				remove:
					(# theWindow: ^applicationWindow;
						theWindowMenuItem: ^windowMenuItem;
					enter theWindow[]
					do searching: items.iterate
							(#
							do (if current.elm.theWindow[] = theWindow[] then
									current.elm[] -> delete;
									current.elm.close;
									current[] -> items.delete;
									leave searching;
								if);
							#);
					#);
				open::<
					(#
					do 'Window' -> name;
						items.init;
						INNER;
					#);
				close::<
					(#
					do items.clear;
						INNER;
					#);
			#);
					
		applicationWindow: window
			(# open::<
					(# myTitle: ^text;
					enter myTitle[]
					do myTitle[] -> title;
						currentPosition -> position;
						(5, 20) -> currentPosition.add;
						INNER;
						this(applicationWindow)[] -> windowMenu.add;
					#);
				close::<
					(#
					do INNER;
						this(applicationWindow)[] -> windowMenu.remove;
					#);
			#);
			
			
		
		currentPosition: @point;
		
	do (10, 40) -> currentPosition;
	#)
  