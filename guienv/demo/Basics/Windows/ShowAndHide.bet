ORIGIN '~beta/guienv/v1.6/guienv';
-- program: descriptor --
GUIenv
	(# menubarType::
			(# fileMenu: @menu
					(# showItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	basicWindow.show;
											#);
									#);
								open::
									(#
									do 'Show the Window' -> name;
									#);
							#);
						hideItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	basicWindow.hide;
											#);
									#);
								open::
									(#
									do 'Hide the Window' -> name;
									#);
							#);
						showHideItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	(if basicWindow.visible then
													basicWindow.hide;
												else
													basicWindow.show;
												if);
											#);
									#);
								open::
									(#
									do 'Show/Hide the Window' -> name;
									#);
							#);
						quitItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	Quit;
											#);
									#);
								open::
									(#
									do 'Quit' -> name;
										'Q' -> key;
									#);
							#);
						open::
							(#
							do 'File' -> name;
								showItem.open;
								showItem[] -> append;
								hideItem.open;
								hideItem[] -> append;
								showHideItem.open;
								showHideItem[] -> append;
								quitItem.open;
								quitItem[] -> append;
							#);
					#);
				open::
					(#
					do fileMenu.open;
						fileMenu[] -> append;
					#);
			#);
		basicWindow: @window
			(# open::
					(#
					do hide; (* The window will normally be shown on the
								 * screen when it is opened, but if you call
								 * hide inside open, the window will not be shown.
								 * NOTE: The effect of calling hide inside open
								 * 		is NOT that the window is first shown
								 *			and then removed from the screen - the
								 *			window will simply not appear on the screen, this
								 *			is because the displaying of the window is
								 *			delayed until open has completed.
								 *)
						 
						'Basic Window' -> title;
						(10, 40) -> position;
						(300, 200) -> size;
					#);
				close::
					(#
					do (*
						 * The "close" operation removes the window from the screen
						 * it is not allowed to use any of the windows operation
						 * when it is closed, but still allowed in the furtherbinding
						 * of "close".
						 *)
						 
						Quit;  (* Quit the application when this window closes *)
					#);
			#);
	do basicWindow.open;
	#)
  