ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';

-- program: descriptor --
GUIenv
(# menubarType::
     (# fileMenu: @menu
          (# quitItem: @menuItem
               (# eventHandler::
                    (# onSelect::
                         (#
                         do terminate;
                         #);
                    #);
                  open::
                    (#
                    do 'Quit' -> name;
                       'Q' -> key;
                    #);
               #);
             open::
               (#
               do 'File' -> name;
                  quitItem.open;
                  quitItem[] -> append;
               #);
          #);
        open::
          (#
          do fileMenu.open;
             fileMenu[] -> append;
          #);
     #);
   basicWindow: @window
     (# resizeable:: (# do false -> value #);
        label: @staticText
          (# open::
               (# aTextStyle: ^textStyle;
               do 
                  &textStyle[] -> aTextStyle[];
                  'Times' -> aTextStyle.name;
                  14 -> aTextStyle.size;
                  textFaces.bold -> aTextStyle.face;
                  aTextStyle[] -> style;
                  
                  'BETA home page: http://www.mjolner.dk' -> label;
                  
                  (10, 10) -> position;
                  fitToContents;
               #);
          #);
        open::
          (# width, height: @integer;
          do 'Basic Window' -> title;
             (10, 40) -> position;
             label.open; (* The label will be placed in the content area of
                          * the window.
                          *)
             label.size -> (width, height);
             (width + 20, height + 20) -> size;
             true -> (contents).border.visible;
             borderStyles.shadowIn -> (contents).border.style;
          #);
        close::
          (#
          do (*
              * The "close" operation removes the window from the screen
              * it is not allowed to use any of the windows operation
              * when it is closed, but still allowed in the furtherbinding
              * of "close".
              *)
             
             terminate;  (* Quit the application when this window closes *)
          #);
     #);
do basicWindow.open;
#)
