ORIGIN '~beta/guienv/v1.6/guienv';
-- program: descriptor --
GUIenv
	(# menubarType::
			(# fileMenu: @menu
					(# newItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(# aNormalWindow: ^normalWindow
											do	&normalWindow[] -> aNormalWindow[];
												aNormalWindow.open;
											#);
									#);
								open::
									(#
									do 'New' -> name;
										'N' -> key;
									#);
							#);
						quitItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	Quit;
											#);
									#);
								open::
									(#
									do 'Quit' -> name;
										'Q' -> key;
									#);
							#);
						open::
							(#
							do 'File' -> name;
								newItem.open;
								newItem[] -> append;
								quitItem.open;
								quitItem[] -> append;
							#);
					#);
				open::
					(#
					do fileMenu.open;
						fileMenu[] -> append;
					#);
			#);
			
		paletteWindow: @window
			(# type:: windowTypes.palette; (* Furtherbinding type to windowTypes.palette, will
													  * make this(Window) a palette window. This
													  * Means that the window will stay on top of
													  * all the normal windows.
													  *
													  * Note this might not work under some X11 window
													  * mangers that do not allow this kind of functionality.
													  *)
			
				open::
					(#
					do 'Palette Window' -> title;
						(300, 40) -> size;
						(200, 40) -> position;
					#);
			#);
			
		normalWindow: window
			(# open::<
					(#
					do 'Normal Window' -> title;
						
						currentPosition -> position;
						(5, 20) -> currentPosition.add;
						
						(300, 200) -> size;
						INNER;
					#);
			#);
		currentPosition: @point;
		
	do (10, 40) -> currentPosition;
		paletteWindow.open;
	#)
  