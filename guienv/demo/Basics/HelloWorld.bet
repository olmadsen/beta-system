ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';
-- program: descriptor --
GUIenv
	(# menubarType::
			(# fileMenu: @menu
					(# quitItem: @menuItem
							(# eventHandler::
									(# onSelect::
											(#
											do	Quit;
											#);
									#);
								open::
									(#
									do 'Quit' -> name;
										'Q' -> key;
									#);
							#);
						open::
							(#
							do 'File' -> name;
								quitItem.open;
								quitItem[] -> append;
							#);
					#);
				open::
					(#
					do fileMenu.open;
						fileMenu[] -> append;
					#);
			#);
		mainWindow: @window
			(# resizeable:: (# do false -> value #);
				helloWorld: @staticText
					(# open::
							(# aTextStyle: ^textStyle;
							do &textStyle[] -> aTextStyle[];
								'Times' -> aTextStyle.name;
								24 -> aTextStyle.size;
								textFaces.bold -> aTextStyle.face;
								aTextStyle[] -> style;
								
								'Hello, World!' -> label;
								
								fitToContents;
							#);
					#);
				open::
					(#
					do '' -> title;
						helloWorld.open;
						helloWorld.size -> size;
					#);
			#);
	do mainWindow.open;
	#)
