ORIGIN '~beta/guienv/v1.6/demo/groupbox';

-- windowLib: attributes --

radioGroup: groupBox
 (# radio: radioButton
 	 (# eventHandler::<
	 	  (# onStateChanged::<
		      (#
			  do (if state = TRUE then
			        (if theCurrentRadio[] <> NONE then
					   FALSE -> theCurrentRadio.state;
					if);
			  		THIS(radio)[] -> theCurrentRadio[];
				 if);
				 INNER;
			  #);
		    onLabelChanged::<
			 (#
			 do fitToContents;
			    INNER;
			 #);
		  #);
       open::<
	    (#
		do currentPosition -> position;
		   currentPosition.v + distance -> currentPosition.v;
		   INNER;
		#);
	 #);
	
	currentRadio:
	 (# newRadio: ^radio;
	 enter (# 
	       enter newRadio[]
	       do TRUE -> newRadio.state;
		   #)
	 exit theCurrentRadio[]
	 #);
	 
	open::<
	 (#
	 do (10, 20) -> currentPosition;
	    20 -> distance;
		INNER;
     #);
	currentPosition: @point;
	distance: @integer;
	theCurrentRadio: ^radio; 
 #);