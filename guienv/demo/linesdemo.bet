ORIGIN '~beta/guienv/graphicscanvas';
INCLUDE '~beta/basiclib/math';
INCLUDE '~beta/sysutils/objinterface';

-- program: descriptor --
GUIenv
(# 
   
   main: @Window
     (# theCanvas: @GraphicsCanvas
          (# 
             blue: @Color;
             gray: @Color;
             global: @integer;
             
             MainLayer: @Layer;
             
             theTimer: @Timer
               (#
                  action::
                    (#
                    do global + 1 -> global;
                       CreatePicture;
                       update;
                    #);
               #);
             
             
             Rail: Line
               (# onMouseDown::
                    (#
                    do 'hit!' -> putline;
                    #);
               #);
             CreateLine:
               (# x1, y1, x2, y2: @real;
                  width: @integer;
                  theLine: ^Line;
               enter (x1, y1, x2, y2, width)
               do &Rail[] -> theLine[];
                  theLine.init;
                  &Point2d[] -> theLine.start[];
                  (x1, y1) -> theLine.start;
                  &Point2d[] -> theLine.end[];
                  (x2, y2) -> theLine.end;
                  width -> theLine.strokewidth;
                  blue[] -> theLine.stroke[];
                  theLine[] -> thePicture.add;
               #);
             thePicture: @Composite
               (# onMouseDown::
                    (#
                    do 'hit ' -> puttext;
                       target[] -> getPatternName -> putline;
                    #);
               #);
             
             CreatePicture:
               (# r1, r2: @integer;
                  cx, cy: @integer;
                  x1, x2, y1, y2: @real;
                  degree: @integer;
                  step: @integer;
                  cosa, sina: @real;
                  angle: @real;
                  vx, vy: @real;
                  ux, uy: @real;
                  l: @integer;
                  
                  
               do thePicture.clear;
                  400 -> thePicture.x;
                  300 -> thePicture.y;
                  0 -> cx;
                  0 -> cy;
                  70 -> r1;
                  300 - 13 -> r2;
                  
                  global -> degree;
                  60 -> step;
                  (for 6 repeat
                       (degree * pi) / 180 -> angle;
                       angle -> sin -> sina;
                       angle -> cos -> cosa;
                       cosa * r1 + cx -> x1;
                       sina * r1 + cy -> y1;
                       cosa * r2 + cx -> x2;
                       sina * r2 + cy -> y2;
                       x2 - x1 -> vx;
                       y2 - y1 -> vy;
                       vx * vx + vy * vy -> sqrt -> l;
                       - (vy / l) -> ux;
                       vx / l -> uy;
                       
                       x1 - ((ux * 5 * 10) - (ux * 10)) / 2 -> x1;
                       x2 - ((ux * 5 * 10) - (ux * 10)) / 2 -> x2;
                       y1 - ((uy * 5 * 10) - (uy * 10)) / 2 -> y1;
                       y2 - ((uy * 5 * 10) - (uy * 10)) / 2 -> y2;
                       
                       (for inx: 5 repeat
                            (x1 + (ux * inx * 10), y1 + (uy * inx * 10), 
                            x2  + (ux * inx * 10), y2 + (uy * inx * 10), inx) -> CreateLine;
                       for);
                       degree + 60 -> degree;
                  for);
               #);
             
             open::
               (# r1, r2: @integer;
                  cx, cy: @integer;
                  x1, x2, y1, y2: @real;
                  degree: @integer;
                  step: @integer;
                  cosa, sina: @real;
                  angle: @real;
                  vx, vy: @real;
                  ux, uy: @real;
                  l: @integer;
               do 0.1 * 0xFFFF -> blue.red;
                  0.2 * 0xFFFF -> blue.green;
                  0.5 * 0xFFFF -> blue.blue;
                  
                  0.8 * 0xFFFF -> gray.red;
                  0.8 * 0xFFFF -> gray.green;
                  0.85 * 0xFFFF -> gray.blue;
                  
                  (0xFFFF, 0xFFFF, 0xFFFF) -> backgroundColor;
                  (800, 600) -> size;
                  MainLayer.init;
                  MainLayer[] -> addLayer;
                  thePicture.init;
                  thePicture[] -> MainLayer.add;
                  CreatePicture;
                  15  -> theTimer.start;
               #);
          #);
        open::
          (#
          do 
             'Lines' -> Title;
             (800, 600) -> size;
             theCanvas.open;
             
          #);
     #);
do main.open;
#)
