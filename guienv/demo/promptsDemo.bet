ORIGIN '~beta/guienv/v1.5/guienv'; 
INCLUDE '~beta/guienv/v1.5/fields'; 
INCLUDE '~beta/guienv/v1.5/controls'; 
INCLUDE '~beta/guienv/v1.5/utils/prompts'; 
INCLUDE '~beta/guienv/v1.5/utils/guienvadds'; 
  
---- PROGRAM : descriptor --- 

guienv
(# win: @window
     (# h,v: @integer; (* Used to position the dialogs *)
        string: @staticText
          (# open:: (# do 'Please'->label; (10,10)->position; (150,30)->size  #) #);
        
        commandButton: @pushButton
          (# eventhandler::
               (# onMouseUp::< 
                    (# 
                    do position->(h,v); (h+10,v+10)->(h,v);
                       (win[],'New label text?','New Label','')
                         ->PromptForText(# ok:: (# do usertext[]->string.label #)#)
                    #)
               #);
             open::
               (# do 'Press here'->label; (10,40)->position; (150,30)->size #)
          #);
        quitButton: @pushButton
          (# eventhandler::
               (# onMouseUp::< 
                    (# 
                    do position->(h,v); (h+10,v+10)->(h,v);
                       (win[],'Print string?', 'Print string before stopping?') ->
                       PromptForBoolean(# ok::
                                            (#
                                            do string.label->putline;
                                               terminate
                                            #);
                                          notok::
                                            (# do stop #);
                                          cancel::< 
                                            (#
                                            do
                                            #);
                                       #);
                    #)
               #);
             open:: (# do 'Quit'->label; (10,90)->position; (150,30)->size  #)
          #);
        open::
          (#
          do commandButton.open;
             quitButton.open;
             string.open;
          #);
     #);
do win.open;
   'Hejsa' -> putLine;
#)

