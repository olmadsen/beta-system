ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
-- program: descriptor --
GUIenv
(# mainWindow: @window
     (# resizeable:: (# do false -> value #);
        aCheckBox: @checkBox
          (# eventHandler::
               (# onStateChanged::
                    (#
                    do (if state then
                           'Chexbox Example' -> THIS(window).title;
                        else
                           '' -> THIS(window).title;
                       if);
                    #);
               #);
             open::
               (# aTextStyle: ^textStyle;
               do &textStyle[] -> aTextStyle[];
                  'Times' -> aTextStyle.name;
                  12 -> aTextStyle.size;
                  textFaces.bold -> aTextStyle.face;
                  aTextStyle[] -> style;
                  
                  'Display Title' -> label;
                  
                  (13, 13) -> position;
                  
                  fitToContents;
                  
                  true -> state;
               #);
          #);
        open::
          (# width, height: @integer;
          do '' -> title;
             aCheckBox.open;
             aCheckBox.size -> (width, height);
             (width+ 126, height + 26) -> size;
          #);
        close::
          (#
          do terminate
          #);
     #);		
do mainWindow.open;
#)
