ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls'
        '~beta/guienv/fields'
        '~beta/basiclib/textUtils';
-- guienvlib: Attributes --
userLogin: window
  (#
     cUsernameLabel: @staticText
       (#
          open:: 
            (#  do (10,10)->position; (95,25)->size; 'User: '->label #)
       #);
     cUsername: @editText
       (#
          open::  (#  do (115,10)->position; (150,25)->size #);
          username: @text;
          
       #);
     cPasswordLabel: @staticText
       (#
          open:: 
            (#  do (10,45)->position; (95,25)->size; 'Password: '->label #)
       #);
     cPassword: @textfield
       (#
          open::  (#  do (115,45)->position; (150,25)->size #);
          pwd: @text;
          allowToInsert: @boolean;
          eventhandler:: 
            (#
               onBeforeChange:: 
                 (# 
                 do
                    (if allowToInsert then
                        true->allow; false->allowToInsert; 
                     else
                        true->allowToInsert;
                        thetext->pwd.append;
                        '*'->insert;
                        false->allow;
                        
                    if);
                    
                 #)
            #)
       #);
     cCancel: @pushButton
       (#
          open:: 
            (#  do (115,90)->position; (80,25)->size; 'Cancel'->label #);
          eventhandler:: 
            (# onMouseUp::  (#  do THIS(userLogin).close #) #)
       #);
     cLogin: @pushButton
       (#
          open:: 
            (# 
            do (115+150-50,90)->position; (50,25)->size; 'Login'->label
            #);
          eventhandler:: 
            (#
               onMouseUp:: 
                 (# 
                 do
                    cUsername.contents->cUsername.username.setText;
                    THIS(userlogin).close
                 #)
            #)
       #);
     open:: 
       (# 
       do
          (40,40)->position;
          (275,125)->size;
          'User Login'->title;
          cUsernameLabel.open;
          cUsername.open;
          cPasswordLabel.open;
          cPassword.open;
          cLogin.open;
          cCancel.open;
          cLogin[]->defaultButton
       #)
  #)  

-- program: Descriptor --
guienv
  (# ul: @userLogin
  do
     ul.open;
     ul.showModal;
     ul.cUsername.username[]->putline;
     ul.cPassword.pwd[]->putline;
     terminate
  #)  

