ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/containers/list'
        '~beta/guienv/controls'
        '~beta/basiclib/file'
        '~beta/guienv/utils/tabControl'
        '~beta/guienv/fields'
        '~beta/guienv/utils/tooltip'
        './treeView/treeViewDemoCanvas'
        './scroller/scrollerDemoCanvas'
        './toolTip/toolTipDemoCanvas'
        './tableView/tableViewDemoCanvas';
-- Program: Descriptor --
guiENV
  (#
     theWindow: @perodic
       (#
          demoCanvasList: @List
            (#
               element:: Canvas;
               get:
                 (# theCanvas: ^Canvas; theType: ##canvas; 
                 enter theType##
                 do
                    loop: (# 
                          do
                             THIS(demoCanvasList).scan
                             (# 
                             do
                                (if current##
                                 // theType## then
                                    current[]->theCanvas[]; leave loop; 
                                if)
                             #);
                             &theType[]->theCanvas[];
                             theCanvas[]->THIS(demoCanvasList).append;
                          #);
                    
                 exit theCanvas[]
                 #)
            #);
          
          treeViewButtonPressed:: 
            (#
               changeCategory:: 
                 (# 
                 do
                    'treeView/treeViewDemoCanvas.bet'->getSourceCode
                      ->sourceCode[];
                    treeViewCanvas##->demoCanvasList.get->demoCanvas[]
                 #)
            do changeCategory; 
            #);
          
          scrollerButtonPressed:: 
            (#
               changeCategory:: 
                 (# 
                 do
                    'scroller/scrollerDemoCanvas.bet'->getSourceCode
                      ->sourceCode[];
                    scrollerCanvas##->demoCanvasList.get->demoCanvas[]
                 #)
            do changeCategory
            #);
          toolTipButtonPressed:: 
            (#
               changeCategory:: 
                 (# 
                 do
                    'toolTip/toolTipDemoCanvas.bet'->getSourceCode
                      ->sourceCode[];
                    toolTipCanvas##->demoCanvasList.get->demoCanvas[]
                 #)
            do changeCategory
            #);
          tableViewButtonPressed:: 
            (#
               changeCategory:: 
                 (# 
                 do
                    'tableView/tableViewDemoCanvas.bet'->getSourceCode
                      ->sourceCode[];
                    tableVIewCanvas##->demoCanvasList.get->demoCanvas[]
                 #)
            do changeCategory; 
            #);
          
       #);
     getSourceCode:
       (# sourceFile: @file; fileName: ^Text; theSourceCode: @StyledText; 
       enter fileName[]
       do
          ''->theSourceCode;
          (if not fileName.empty then
              fileName[]->sourceFile.name;
              sourceFile.openRead;
              while:
                (# 
                do
                   sourceFile.getLine->theSourceCode.putline;
                   (if not sourceFile.Eos then restart while if)
                #);
              sourceFile.close
              
          if)
       exit theSourceCode[]
       #);
     
  do theWindow.open
  #)  

-- guienvLib: Attributes --
perodic: window
  (#
     menubarType:: 
       (#
          fileMenu: @menu
            (#
               iFile: @menuItem
                 (#
                    eventhandler:: 
                      (# onselect::  (#  do THIS(perodic).close #) #);
                    open::  (#  do 'Quit'->name #)
                 #);
               open:: 
                 (# 
                 do 'File'->name; iFile.open; iFile[]->fileMenu.append
                 #);
               
            #);
          open::  (#  do fileMenu.open; fileMenu[]->append #)
       #);
     tabControlCanvas: @canvas
       (#
          theTabControl: @tabControl
            (#
               myTab: tab
                 (#
                    canvasType:< Canvas;
                    tabCanvas: ^canvasType;
                    open::< 
                      (# 
                      do
                         &canvasType[]->tabCanvas[];
                         THIS(theTabControl)[]->tabCanvas.father[];
                         INNER ;
                         tabCanvas.open;
                         tabCanvas[]->page;
                         
                      #)
                 #);
               demoTab: @myTab
                 (#
                    newCanvas:
                      (# aCanvas: ^Canvas
                      enter aCanvas[]
                      do
                         (if aCanvas.father[] = none then 
                             THIS(theTabControl)[]->aCanvas.father[];     
                             aCanvas.open;
                         if);
                         INNER ;
                         aCanvas[]->page
                      #);
                    open::  (#  do 'Demo'->label #)
                 #);
               sourceCodeTab: @myTab
                 (#
                    canvasType::< sourceCanvas;
                    sourceCanvas: canvas
                      (#
                         theTextEditor: @texteditor
                           (#
                              contentsType:: 
                                (#
                                   eventHandler:: 
                                     (#
                                        onBeforeChange:: 
                                          (# 
                                          do
                                             (if length <= 1 then
                                                 FALSE->allow
                                             if)
                                          #)
                                     #)
                                #);
                              open::< 
                                (# x,y: @integer; 
                                do
                                   THIS(SourceCanvas).size->(x,y);
                                   (x,y)->size;
                                   true->bindBottom->bindRight
                                #)
                           #);
                         open::< 
                           (# 
                           do
                              (520,350)->size;
                              TRUE->bindRight->bindBottom;
                              theTextEditor.open
                           #)
                      #);
                    newSourceCode:
                      (# aText: ^StyledText
                      enter aText[]
                      do
                         aText[]
                           ->
                             sourceCodeTab.tabCanvas.theTextEditor.contents.
                               contents
                      #);
                    open::  (#  do 'Source Code'->label; INNER #)
                 #);
               open:: 
                 (# 
                 do
                    (630,470)->size;
                    TRUE->bindRight->bindBottom;
                    (0,0)->position;
                    demoTab.open;
                    sourceCodeTab.open;
                    demoTab[]->selection;
                    
                 #);
               
            #);
          open:: 
            (# 
            do
               (5,60)->position;
               (630,470)->size;
               TRUE->bindRight->bindBottom;
               theTabControl.open
            #)
       #);
     scrollerButton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::< 
            (# 
            do
               (50,50)->size;
               (475,5)->position;
               INNER ;
               FALSE->showlabel;
               './images/scroller3.png'->thePixMap.read;
               'Scroller Demo'->THIS(iconbutton).addToolTip;
               thePixmap[]->icon
            #);
          eventHandler::< 
            (# onMouseUp::<  (#  do scrollerButtonPressed #) #)
       #);
     treeViewbutton: @Iconbutton
       (#
          thePixmap: @pixmap;
          open::< 
            (# 
            do
               (50,50)->size;
               (530,5)->position;
               INNER ;
               FALSE->showlabel;
               './images/treeView2.png'->thePixMap.read;
               'Tree View Demo'->THIS(iconbutton).addToolTip;
               thePixmap[]->icon
            #);
          eventHandler::< 
            (# onMouseUp::<  (#  do treeViewButtonPressed;  #) #)
       #);
     toolTipButton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::< 
            (# 
            do
               (50,50)->size;
               (585,5)->position;
               INNER ;
               FALSE->showlabel;
               './images/toolTip1.png'->thePixMap.read;
               'Tool Tip Demo'->THIS(iconbutton).addToolTip;
               thePixmap[]->icon
            #);
          eventHandler::< 
            (# onMouseUp::<  (#  do toolTipButtonPressed #) #)
       #);
     tableViewButton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::< 
            (# 
            do
               (50,50)->size;
               (420,5)->position;
               INNER ;
               FALSE->showlabel;
               './images/tableView1.png'->thePixMap.read;
               'TabelView Demo'->THIS(iconbutton).addToolTip;
               thePixmap[]->icon
            #);
          eventHandler::< 
            (# onMouseUp::<  (#  do tableViewButtonPressed #) #)
       #);
     categoryChangeEvent:
       (#
          sourceCode: ^StyledText;
          demoCanvas: ^Canvas;
          changeCategory:<
            (# 
            do
               INNER ;
               sourceCode[]
                 ->tabControlCanvas.theTabControl.sourceCodeTab.newSourceCode;
               demoCanvas[]->tabControlCanvas.theTabControl.demoTab.newCanvas;
               
            #)
       do changeCategory; INNER
       #);
     treeViewButtonPressed:< categoryChangeEvent (#  do INNER #);
     scrollerButtonPressed:< categoryChangeEvent (#  do INNER #);
     toolTipButtonPressed:< categoryChangeEvent (#  do INNER #);
     tableViewButtonPressed:< categoryChangeEvent (#  do INNER #);
     close::  (#  do Terminate;  #);
     open::< 
       (# 
       do
          'Periodic'->title;
          (720,535)->size;
          tabControlCanvas.open;
          treeViewButton.open;
          scrollerButton.open;
          toolTipButton.open;
          tableViewButton.open;
          INNER ;
          
       #)
  #)  

