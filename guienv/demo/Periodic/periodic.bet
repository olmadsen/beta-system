ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls'
        '~beta/basiclib/file'
        '~beta/guienv/utils/tabControl'
        '~beta/guienv/fields'
        '~beta/guienv/utils/tooltip'
        './treeView/treeViewDemoCanvas'
        './scroller/scrollerDemoCanvas'
        './toolTip/toolTipDemoCanvas'
        './tableView/tableViewDemoCanvas';
-- Program: Descriptor --
guiENV
  (#
     theWindow: @perodic
       (#
          treeViewButtonPressed::
            (#
               changeCategory::
                 (# 
                 do
                    'treeview/treeviewdemoCanvas.bet'->getSourceCode
                      ->sourceCode[];
                    &treeViewCanvas[]->demoCanvas[]
                 #)
            do changeCategory; 
            #);
          scrollerButtonPressed::
            (#
               changeCategory::
                 (# 
                 do
                    'scroller/scrollerDemoCanvas.bet'->getSourceCode
                      ->sourceCode[];
                    &scrollerCanvas[]->demoCanvas[]
                 #)
            do changeCategory
            #);
          toolTipButtonPressed::
            (#
               changeCategory::
                 (# 
                 do
                    'toolTip/toolTipDemoCanvas.bet'->getSourceCode
                      ->sourceCode[];
                    &toolTipCanvas[]->demoCanvas[]
                 #)
            do changeCategory
            #);
          tableViewButtonPressed::
            (#
               changeCategory::
                 (# 
                 do
                    'tableView/tableViewDemoCanvas.bet'->getSourceCode
                      ->sourceCode[];
                    &tableVIewCanvas[]->demoCanvas[]
                 #)
            do changeCategory; 
            #);
          
       #);
     getSourceCode:
       (# sourceFile: @file; fileName: ^Text; theSourceCode: @StyledText; 
       enter fileName[]
       do
          ''->theSourceCode;
          (if not fileName.empty then
              fileName[]->sourceFile.name;
              sourceFile.openRead;
              while:
                (# 
                do
                   sourceFile.getLine->theSourceCode.putline;
                   (if not sourceFile.Eos then restart while if)
                #);
              
          if)
       exit theSourceCode[]
       #);
     
  do theWindow.open
  #)  

-- guienvLib: Attributes --
perodic: window
  (#
     menubarType::
       (#
          fileMenu: @menu
            (#
               iFile: @menuItem
                 (#
                    eventhandler::
                      (# onselect:: (#  do THIS(perodic).close #) #);
                    open:: (#  do 'Quit'->name #)
                 #);
               open::
                 (# 
                 do 'File'->name; iFile.open; iFile[]->fileMenu.append
                 #);
               
            #);
          open:: (#  do fileMenu.open; fileMenu[]->append #)
       #);
     tabControlCanvas: @canvas
       (#
          open::
            (#  do (5,60)->position; (630,470)->size; theTabControl.open #);
          theTabControl: @tabControl
            (#
               open::
                 (# 
                 do
                    (630,470)->size;
                    (0,0)->position;
                    demoTab.open;
                    sourceCodeTab.open;
                    demoTab[]->selection;
                    
                 #);
               myTab: tab
                 (#
                    canvasType:< Canvas;
                    tabCanvas: ^canvasType;
                    open::<
                      (# 
                      do
                         &canvasType[]->tabCanvas[];
                         THIS(theTabControl)[]->tabCanvas.father[];
                         INNER ;
                         tabCanvas.open;
                         tabCanvas[]->page;
                         
                      #)
                 #);
               demoTab: @myTab
                 (#
                    newCanvas:
                      (# aCanvas: ^Canvas
                      enter aCanvas[]
                      do
                         THIS(theTabControl)[]->aCanvas.father[];
                         INNER ;
                         aCanvas.open;
                         aCanvas[]->page
                      #);
                    open:: (#  do 'Demo'->label #)
                 #);
               sourceCodeTab: @myTab
                 (#
                    canvasType::< sourceCanvas;
                    sourceCanvas: canvas
                      (#
                         theTextEditor: @texteditor
                           (#
                              contentsType::
                                (#
                                   eventHandler::
                                     (#
                                        onBeforeChange::
                                          (# 
                                          do
                                             (if length <= 1 then
                                                 FALSE->allow
                                             if)
                                          #)
                                     #)
                                #);
                              open::<
                                (# x,y: @integer; 
                                do
                                   THIS(SourceCanvas).size->(x,y);
                                   (x,y)->size;
                                   true->bindBottom->bindRight
                                #)
                           #);
                         open::<
                           (#  do (520,350)->size; theTextEditor.open #)
                      #);
                    newSourceCode:
                      (# aText: ^StyledText
                      enter aText[]
                      do
                         aText[]
                           ->
                             sourceCodeTab.tabCanvas.theTextEditor.contents.
                               contents
                      #);
                    open:: (#  do 'Source Code'->label; INNER #)
                 #);
               
            #)
       #);
     scrollerButton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::<
            (# 
            do
               (50,50)->size;
               (475,5)->position;
               INNER ;
               FALSE->showlabel;
               './images/scroller3.png'->thePixMap.read;
               'Scroller Demo'->THIS(iconbutton).addToolTip;
               thePixmap[]->icon
            #);
          eventHandler::<
            (# onMouseUp::< (#  do scrollerButtonPressed #) #)
       #);
     treeViewbutton: @Iconbutton
       (#
          thePixmap: @pixmap;
          open::<
            (# 
            do
               (50,50)->size;
               (530,5)->position;
               INNER ;
               FALSE->showlabel;
               './images/treeView2.png'->thePixMap.read;
               'Tree View Demo'->THIS(iconbutton).addToolTip;
               thePixmap[]->icon
            #);
          eventHandler::<
            (# onMouseUp::< (#  do treeViewButtonPressed;  #) #)
       #);
     toolTipButton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::<
            (# 
            do
               (50,50)->size;
               (585,5)->position;
               INNER ;
               FALSE->showlabel;
               './images/toolTip1.png'->thePixMap.read;
               'Tool Tip Demo'->THIS(iconbutton).addToolTip;
               thePixmap[]->icon
            #);
          eventHandler::<
            (# onMouseUp::< (#  do toolTipButtonPressed #) #)
       #);
     tableViewButton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::<
            (# 
            do
               (50,50)->size;
               (420,5)->position;
               INNER ;
               FALSE->showlabel;
               './images/tableView1.png'->thePixMap.read;
               'TabelView Demo'->THIS(iconbutton).addToolTip;
               thePixmap[]->icon
            #);
          eventHandler::<
            (# onMouseUp::< (#  do tableViewButtonPressed #) #)
       #);
     categoryChangeEvent:
       (#
          sourceCode: ^StyledText;
          demoCanvas: ^Canvas;
          changeCategory:<
            (# 
            do
               INNER ;
               sourceCode[]
                 ->tabControlCanvas.theTabControl.sourceCodeTab.newSourceCode;
               demoCanvas[]->tabControlCanvas.theTabControl.demoTab.newCanvas;
               
            #)
       do INNER
       #);
     treeViewButtonPressed:< categoryChangeEvent (#  do INNER #);
     scrollerButtonPressed:< categoryChangeEvent (#  do INNER #);
     toolTipButtonPressed:< categoryChangeEvent (#  do INNER #);
     tableViewButtonPressed:< categoryChangeEvent (#  do INNER #);
     close:: (#  do Terminate;  #);
     open::<
       (# 
       do
          'Grand Unified Demo (GUD)'->title;
          (720,535)->size;
          tabControlCanvas.open;
          treeViewButton.open;
          scrollerButton.open;
          toolTipButton.open;
          tableViewButton.open;
          INNER ;
          
       #)
  #)  

