ORIGIN '~beta/guienv/controls';
INCLUDE '~beta/guienv/controls';
-- windowLib: Attributes --
controlCanvas: canvas
  (#
     horizontalScrollbar: @scrollbar
       (#
          eventHandler:: 
            (#
               thumbUsed: @Boolean;
               onThumbMoved::  (#  do True->thumbUsed #);
               onValueChanged:: 
                 (# aText: ^Text; 
                 do
                    'the Horizontal scrollbar'->controlInUse.new;
                    &Text[]->aText[];
                    value->aText.putInt;
                    (if thumbUsed then
                        ' (thumb used)'->aText.append; FALSE->thumbUsed; 
                    if);
                    aText[]->status.new
                 #);
               onMaxValueChanged::  (#  do  #)
            #);
          open::< 
            (# 
            do
               (355,16)->size;
               (40,120)->position;
               200->maxValue;
               1->scrollAmount;
               INNER
            #)
       #);
     aPushbutton: @pushbutton
       (#
          eventHandler:: 
            (#
               onMouseup::  (#  do 'not Pushed'->status.new #);
               onMouseDown:: 
                 (# 
                 do 'the Push button'->controlInUse.new; 'Pushed'->status.new
                 #)
            #);
          open::< 
            (# 
            do
               'Push button'->label;
               (120,20)->size;
               (420,355)->position;
               INNER
            #)
       #);
     usingStaticText: @statictext
       (#
          open::< 
            (# 
            do 'now Using:'->label; (150,16)->size; (45,30)->position; INNER
            #)
       #);
     controlInUse: @statictext
       (#
          new:
            (# aText: ^Text; 
            enter aText[]
            do aText[]->label; TRUE->update
            #);
          open::< 
            (# 
            do 'None'->label; (170,16)->size; (210,30)->position; INNER
            #)
       #);
     theSeparator: @separator
       (# open::<  (#  do (570,4)->size; (20,110)->position; INNER #)
       #);
     statusStatictext: @statictext
       (#
          open::< 
            (# 
            do 'Status:'->label; (60,16)->size; (45,60)->position; INNER
            #)
       #);
     status: @statictext
       (#
          new:
            (# aText: ^Text; 
            enter aText[]
            do aText[]->label; TRUE->update
            #);
          open::< 
            (# 
            do 'None'->label; (340,16)->size; (210,60)->position; INNER
            #)
       #);
     ScrollMaxoptionbutton: @optionbutton
       (#
          ;
          eventHandler:: 
            (#
               onCurrentItemChanged:: 
                 (# 
                 do
                    (if currentItem
                     // 1 then
                        200->horizontalScrollbar.maxValue;
                        'Max value for the scrollbar is changed to 200'
                          ->status.new
                     // 2 then
                        300->horizontalScrollbar.maxValue;
                        'Max value for the scrollbar is changed to 300'
                          ->status.new
                    if)
                 #);
               onMouseDown:: 
                 (#  do 'Scrollbar Max option button'->controlInUse.new #)
            #);
          aMenu: @Menu
            (#
               addMenuItem:
                 (# aName: ^Text; 
                 enter aName[]
                 do
                      (# aMenuItem: ^menuItem; 
                      do
                         &menuItem[]->aMenuItem[];
                         aMenuItem.open;
                         aName[]->aMenuItem.name;
                         aMenuItem[]->append
                      #)
                 #);
               open::  (#  do '200'->addMenuItem; '300'->addMenuItem;  #)
            #);
          open::< 
            (# 
            do
               'Scroll Max'->label;
               (350,40)->size;
               (45,155)->position;
               aMenu.open;
               aMenu[]->popupMenu;
               INNER
            #)
       #);
     open:: 
       (# 
       do
          horizontalScrollbar.open;
          aPushbutton.open;
          usingStaticText.open;
          controlInUse.open;
          theSeparator.open;
          statusStatictext.open;
          status.open;
          ScrollMaxoptionbutton.open;
          (600,410)->size;
          
       #)
  #)  

