ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/treeview'
        '~beta/guienv/fields'
        '~beta/guienv/controls'
        '~beta/guienv/utils/groupbox';
-- WindowLib: Attributes --
treeViewCanvas: Canvas
  (#
     theScroller: @Scroller
       (#
          contentsType:: aTreeView;
          open::  (#  do (0,0)->position; (290,440)->size #);
          
       #);
     theTreeView: ^aTreeView;
     aTreeView: TreeView
       (#
          node: folder
            (#
               isRoot: @boolean;
               onSelected:: 
                 (# 
                 do
                    label[]->selectedItemBox.folderText.label;
                    label[]->selectedItemBox.selectedText.label
                 #);
               onLabelChanged::  (#  do newLabel[]->label[] #)
            #);
          leaf: Item
            (#
               allowLabelEdit:: TrueObject;
               onSelected:: 
                 (# 
                 do
                    father.label[]->selectedItemBox.folderText.label;
                    label[]->selectedItemBox.selectedText.label
                 #);
               onLabelChanged::  (#  do newLabel[]->label[]; changed #)
            #);
          hideRoot:: FALSEObject;
          moveBackSelection:
            (# aItem: ^Item
            do findPreviousItem->aItem[]; aItem.onSelected; 
            #);
          findPreviousItem:
            (# aNode: ^Node; aItem: ^Item
            do
               findEnclosingNode->aNode[];
               (if selection[] <> aNode[] then
                   (if (selection[]->aNode.items.at).pred[] <> none then
                       (selection[]->aNode.items.at).pred.elm[]->aItem[]
                    else
                       aNode[]->aItem[]
                   if)
                else
                   aNode[]->aItem[]
               if)
            exit aItem[]
            #);
          findNode:
            (# aNode: ^Node
            do
               (if selection.isFolder then
                   selection[]->aNode[]
                else
                   (if selection.father[] <> none then
                       selection.father[]->aNode[]; 
                    else
                       root->aNode[]
                   if)
               if)
            exit aNode[]
            #);
          findEnclosingNode:
            (# aNode: ^Node
            do
               (if selection.father[] <> none then
                   selection.father[]->aNode[]
                else
                   root->aNode[]
               if)
            exit aNode[]
            #);
          insertItem:
            (# aNode: ^Node; aItem: ^Item
            enter aItem[]
            do findNode->aNode[]; aItem[]->aNode.addItem; changed
            #);
          insertLeaf:
            (# newLeaf: ^Leaf; aPixMap: @PixMap; 
            do
               &Leaf[]->newLeaf[];
               './images/betFileIcon.png'->aPixMap.read;
               aPixMap[]->newLeaf.icon;
               'new Leaf'->newLeaf.label[];
               newLeaf[]->insertItem
            #);
          insertNode:
            (# newNode: ^Node
            do
               &Node[]->newNode[];
               'New Node'->newNode.label[];
               newNode[]->insertItem;
               
            #);
          removeItem:
            (# theNode: ^Node; theItem: ^Item
            do
               selection[]->theItem[];
               (if theItem.isFolder then
                   (if selection.father[] <> none then
                       selection.father[]->theNode[]
                    else
                       root->theNode[]
                   if)
                else
                   findNode->theNode[]
               if);
               moveBackSelection;
               theItem[]->theNode.items.at->theNode.items.delete;
               changed
            #);
          makeRoot:
            (# aFolder: ^node
            do
               &node[]->aFolder[];
               'root'->aFolder.label[];
               true->aFolder.isRoot;
               aFolder[]->root;
               (root).onSelected;
               (root).onToggleSelected;
               
            #);
          open::  (# aItem: ^leaf; thePixMap: ^pixMap;  do makeRoot;  #)
       #);
     addLeafButton: @pushbutton
       (#
          open::< 
            (# 
            do (75,20)->size; (345,160)->position; 'Add Leaf'->label; INNER
            #);
          eventHandler::< 
            (# onMouseUp::<  (#  do theTreeView.insertLeaf #) #)
       #);
     removeItembutton: @pushbutton
       (#
          open::< 
            (# 
            do
               'Remove Item'->label; (95,20)->size; (345,195)->position; INNER
            #);
          eventHandler::< 
            (# onMouseUp::<  (#  do theTreeView.removeItem #) #)
       #);
     addNodebutton: @pushbutton
       (#
          open::< 
            (# 
            do 'Add Node'->label; (80,20)->size; (430,160)->position; INNER
            #);
          eventHandler::< 
            (# onMouseUp::<  (#  do theTreeView.insertNode #) #)
       #);
     SelectedItemBox: @groupbox
       (#
          open::< 
            (# 
            do
               'Selected Item'->title;
               (190,105)->size;
               (345,20)->position;
               folderLabelText.open;
               selectedLabelText.open;
               folderText.open;
               selectedText.open;
               INNER
            #);
          folderLabelText: @statictext
            (#
               open::< 
                 (# 
                 do
                    'Inside Folder:'->label;
                    (75,16)->size;
                    (15,55)->position;
                    INNER
                 #)
            #);
          selectedLabelText: @statictext
            (#
               open::< 
                 (# 
                 do
                    'Selected:'->label;
                    (60,16)->size;
                    (15,25)->position;
                    INNER
                 #)
            #);
          folderText: @statictext
            (#
               open::< 
                 (# 
                 do ''->label; (95,16)->size; (85,55)->position; INNER
                 #)
            #);
          selectedText: @statictext
            (#
               open::< 
                 (# 
                 do ''->label; (100,16)->size; (85,25)->position; INNER
                 #)
            #)
       #);
     open:: 
       (# 
       do
          (0,0)->position;
          (620,450)->size;
          SelectedItemBox.open;
          addLeafButton.open;
          removeItembutton.open;
          addNodebutton.open;
          theScroller.open;
          theScroller.contents[]->theTreeView[]
       #)
  #)  

