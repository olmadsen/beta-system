ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls'
        '~beta/guienv/utils/tableview';
-- WindowLib: Attributes --
tableViewCanvas: Canvas
  (#
     theTableview: @tableview
       (#
          tableItemType::
            (#
               open::
                 (# theText: ^Text; age: @Integer
                 enter (theText[],age)
                 do
                    (nameColumn[],theText[])->setColumnText;
                    theText.clear;
                    age->theText.putint;
                    (ageColumn[],theText[])->setColumnText
                 #);
               
            #);
          makeRow:
            (# theText: ^Text; theInteger: @Integer; item: ^tableItemType; 
            enter (theText[],theInteger)
            do
               &tableItemType[]->item[];
               (theText[],theInteger)->item.open;
               item[]->append;
               
            #);
          open::
            (# 
            do
               (10,10)->position;
               (255,200)->size;
               TRUE->bindRight;
               TRUE->bindBottom;
               nameColumn.open;
               nameColumn[]->appendColumn;
               ageColumn.open;
               ageColumn[]->appendColumn;
               ('Aragorn',88)->makeRow;
               ('Boromir',41)->makeRow;
               ('Frodo Baggins',51)->makeRow;
               ('Gandalf',0)->makeRow;
               ('Gimli',140)->makeRow;
               ('Legolas',0)->makeRow;
               ('Meriadoc Brandyback',47)->makeRow;
               ('Peregrin Took',29)->makeRow;
               ('Samwise Gamgee',39)->makeRow;
               ('Arwen Undomiel',2778)->makeRow;
               ('Bilbo Baggins',129)->makeRow;
               ('Celeborn',0)->makeRow;
               ('Denethor',84)->makeRow;
               ('Mads brogger',23)->makeRow;
               
            #);
          ageColumn: @column
            (# open:: (#  do 'age'->title; 200->width #) #);
          nameColumn: @column
            (# open:: (#  do 'name'->title; 200->width #) #)
       #);
     open::
       (# 
       do
          (440,305)->size;
          horizontalscroll.open;
          verticalScroll.open;
          theTableview.open;
          
       #);
     horizontalscroll: @pushbutton
       (#
          visible: @boolean;
          open::<
            (# 
            do
               'Toggle Horizontal scrollbar'->label;
               true->bindBottom;
               true->bindRight;
               false->bindLeft;
               false->bindTop;
               (135,20)->size;
               (295,190)->position;
               true->visible;
               INNER
            #);
          eventHandler::<
            (#
               onMouseUp::<
                 (# 
                 do
                    (if visible
                     // true then
                        false->visible->theTableView.horizontalScrollbarVisible;
                        
                     else
                        true->visible->theTableView.horizontalScrollbarVisible; 
                    if)
                 #)
            #)
       #);
     verticalScroll: @pushbutton
       (#
          visible: @boolean;
          open::<
            (# 
            do
               'Toggle Vertical scrollbar'->label;
               true->bindBottom;
               true->bindRight;
               false->bindLeft;
               false->bindTop;
               (135,20)->size;
               (295,215)->position;
               true->visible;
               INNER
            #);
          eventHandler::<
            (#
               onMouseUp::<
                 (# 
                 do
                    (if visible
                     // true then
                        false->visible->theTableView.verticalScrollbarVisible; 
                     else
                        true->visible->theTableView.verticalScrollbarVisible; 
                    if)
                 #)
            #)
       #)
  #)  

