ORIGIN 'manager';
LIB_DEF 'frigg_row' '../../lib';

-- windowLib: Attributes --
row: manager
  (#
     open::<  (#  do INNER ;  #);
     computeLayout:: 
       (#
          width,height: @integer;
          x,needed,count: @integer;
          maxHeight: @integer;
          last: ^windowItem;
          
       do
          size->(width,height);
          iterate
            (# w,h: @integer
            do
               current[]->last[];
               current.size->(w,h);
               needed+w->needed;
               (maxHeight,h)->max->maxHeight;
               count+1->count;
               
            #);
          (if count <> 0 then
              needed+margin.left+margin.right+(count-1)*distance->needed;
              (if horizontal.wrap then
                  margin.left->x
               else
                  (if horizontal.alignment
                   // alignLeft then
                      margin.left->x
                   // alignRight then
                      width-needed+margin.left->x
                   // alignCenter then
                      (width-needed) div 2+margin.left->x
                  if)
              if);
              (if horizontal.wrap then needed->width if);
              (if vertical.wrap then
                  maxHeight+margin.top+margin.bottom->height
              if);
              iterate
                (# w,h: @integer
                do
                   current.size->(w,h);
                   (if vertical.alignment
                    // alignTop then
                       (x,margin.top)->current.position
                    // alignCenter then
                       (x,margin.top+(height-margin.top-margin.bottom-h) div 2)
                         ->current.position
                    // alignBottom then
                       (x,(height-margin.bottom-h))->current.position
                   if);
                   x+w+distance->x;
                   
                #);
              (width,height)->size
           else
              (if vertical.wrap then
                 margin.top+margin.bottom -> height;
              if);
              (if horizontal.wrap then
                 margin.left+margin.right -> width;
              if);
              (width, height)->size;
          if);
          
       #)
  #)  

