ORIGIN '../scrolllists';
BODY 'private/listviewbody';

-- windowLib: attributes --


listView: textScrollList
  (* A textScrollList where each textline in the list corresponds to
   * a unique listItem object.
   *)
  (# <<SLOT listViewLib: attributes>>;
     listItem:
       (* A listItem object corresponds to a unique line in the List
        * The name if this(listItem) i the text in the corresponding
        * line. When the user selects on item by clicking, onSelect is
        * called.
        *)
       (# <<SLOT listItemLib: attributes>>;
          name:
            (* The name is the text in the line corresponding to
             * this(listItem).
             *)
            (# theName: ^text;
            enter (# enter theName[] <<SLOT GUIenvListViewListItemSetName: doPart>> #)
            exit (# <<SLOT GUIenvListViewListItemGetName: doPart>> exit theName[] #)
            #);
          position: integerValue
            (* The the item number of this(listItem) in the range
             * from 1 to numberOfItems.
             *)
            (# <<SLOT GUIenvListViewListItemGetPosition: doPart>> #);
          init:<
            (* Inserts this(listItem) in the list before
             * `beforeItem', if `beforeItem' is none this(listItem) is
             * appended. Init *must* be the first operation on this(listItem).
             *)
            (# beforeItem: ^listItem;
            enter beforeItem[]
            <<SLOT GUIenvListViewListItemInit: doPart>> 
            #);
          delete:<
            (* Delete this(listItem) from this(listView). *)
            (# <<SLOT GUIenvListViewListItemDelete: doPart>> #);
          onSelect:<
            (* Called when this(listItem) is selected by the user or
             * the programmer. If the user has doubleClicked
             * this(listItem) the `doubleClick' flag is true.
             *)
            (# doubleClick: @boolean;
            enter doubleClick
            do inner;
            #);
          select:
            (* Select this(listItem). If singeSelection is true, all
             * other items are deSelected.
             *)
            (# <<SLOT GUIenvListViewListItemSelect: doPart>> #);
          deSelect:
            (* Call this to deSelect this(listItem). *)
            (# <<SLOT GUIenvListViewListItemDeSelect: doPart>> #);
          private: @<<SLOT GUIenvListViewListItemPrivate: descriptor>>;
       #);
     listItems: @list
       (# element::< listItem #);
     open::<
       (# <<SLOT GUIenvListViewOpen: doPart>> #);
     close::<
       (# <<SLOT GUIenvListViewClose: doPart>> #);
     eventHandler::<
       (# onMouseUp::<
            (# <<SLOT GUIenvListViewEventHandlerOnMouseUp: doPart>> #);
       #);
     private: @<<SLOT GUIenvListViewPrivate: descriptor>>;
  #);
