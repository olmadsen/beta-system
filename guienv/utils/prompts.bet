ORIGIN '../guienv';
BODY 'private/promptsbody';

--GuienvLib: attributes--

(* Standard text dialogs for guienv.
 * Known problems:
 *     1. Buttons should be highlighted when invoked via <return>, 
 *        <esc> etc.
 *     2. Patterns should be specializations of special dialog pattern,
 *        which would make the window manager treat the prompts as dialogs.
 *)

Prompt: window
  (* Dialog with a message text and an OK and Cancel button.
   * When typing <Return> in the text fiels, the OK button
   * is invoked, and when typing <Escape> the cancel button 
   * is invoked.
   *)
  (# <<SLOT prompt: attributes>>;
     ok:< Object
       (* Called when the OK button is clicked *);
     notok:< Object
       (* Called when the 'No' button is pressed (only promptForBoolean )*);
     cancel:< object 
       (* Called when the Cancel button is clicked *);
     popup:<
       (* Used to pop up the dialog and wait for the text to be typed *)
       (# titleText, msgText: ^text;
          father: ^window;
       enter (father[], titleText[], msgText[])
       <<SLOT promptPopup: dopart>>
       #);

     open::< (# <<SLOT promptOpen: dopart>> #);
     private: @<<SLOT promptPrivate: descriptor>>;
  enter popup
  #);

PromptForText: Prompt
  (* Prompt with a field to enter a text added. *)
  (# <<SLOT promptForTextLib: attributes>>;
     
     usertext: ^text; (* The text entered by user *)
     linesInUsertext:< (* number of lines in the text field for user input *)
       integerValue(# do 1->value #);
     
     ok::<
       (# <<SLOT promptForTextOk: dopart>> #);
     
     validate:< BooleanValue
       (* Called when the OK button is pressed. THIS(Prompt) is only
        * dismissed (and INNER in the ok virtual called) if validate returns 
        * true.
        *)
       (# hiliteOnError: @boolean
            (* Hilite text on error. Default true *);
       <<SLOT promptForTextValidate: dopart>>
       #);
     popup::<
       (# defaultText: ^text;
       enter defaultText[]
       <<SLOT promptForTextPopup: dopart>>
       #);
     
     open::< (# <<SLOT promptForTextOpen: dopart>> #);
     textprivate: @<<SLOT promptForTextPrivate: descriptor>>;
  #);

PromptForInteger: PromptForText
  (* Special PromptForText that will only accept integer input *)
  (# <<SLOT promptForIntegerLib: attributes>>;
     userinteger: @integer (* The integer typed by the user *);
     validate::<
       (# 
       <<SLOT promptForIntegerValidate: dopart>> 
       #)
  #);

promptForBoolean: Prompt
  (* Special Prompt where the OK buttons has label 'Yes' and
   * there is an extra button with label 'No'.
   *)
  (# ok::<
       (# <<SLOT promptForBooleanOk: dopart>> #);
     notok::< (* Called when the 'No' button is pressed *)
       (# <<SLOT promptForBooleanNotok: dopart>> #);
     open::<
       (# <<SLOT promptForBooleanOpen: dopart>> #)
  #);
