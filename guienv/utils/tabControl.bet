ORIGIN '~beta/guienv/guienv';
LIB_DEF 'guienvutilstabctrl' '../../lib';
BODY 'private/tabControlBody';
(*
 * COPYRIGHT
 *       Copyright (C) Aarhus University
 *       All rights reserved.
 *)

-- windowLib: attributes --

tabControl: canvas
  (# <<SLOT tabControlLib: attributes>>;
     
     resourceAllocationError:< exception
       (# errorMsg: ^text;
       enter errorMsg[]
       do inner;
       #);
     
     apiError:< exception
       (# errorMsg: ^text;
       enter errorMsg[]
       do inner;
       #);
     
     useError:< exception
       (# errorMsg: ^text;
       enter errorMsg[]
       do inner;
       #);
     
     multiLine:<
       (* if multiLine is TRUE the tabControl displays multiple  
        * rows of tabs, if necessary, so all tabs are visible at once.   
        *)
       booleanValue;
     
     tab:
       (# label:
            (# theLabel: ^text;
            enter (# enter theLabel[] <<SLOT GUIEnvTabSetLabel: doPart>> #)
            exit (# <<SLOT GUIEnvTabGetLabel: doPart>> exit theLabel[] #)
            #);
          icon:
            (# theIcon: ^pixmap;
            enter (# enter theIcon[] <<SLOT GUIEnvTabSetIcon: doPart>> #)
            exit (# <<SLOT GUIEnvTabGetIcon: doPart>> exit theIcon[] #)
            #);
          page:      
            (# thePage: ^canvas;
            enter (# enter thePage[] <<SLOT GUIEnvTabSetPage: doPart>> #)
            exit (# <<SLOT GUIEnvTabGetPage: doPart>> exit thePage[] #)
            #);
          
          open:<
            (# <<SLOT GUIEnvTabOpen: doPart>> #);
          
          close:<
            (# <<SLOT GUIEnvTabClose: doPart>> #);
          
          select:
            (# <<SLOT GUIEnvTabSelect: doPart>> #);   
          
          onSelect:<
            (* Called when the selection has been changed by the end user *)
            (# 
            do inner onSelect; 
            #); 
          
          tabPrivate: @<<SLOT GUIenvTabPrivate: descriptor>>;
          
       #);
     
     tabs: @
       (# 
          size: integerValue
            (# <<SLOT GUIEnvTabsSize: doPart>> #);
          
          scan:
            (# current: ^tab;
            <<SLOT GUIEnvTabsScan: doPart>>
            #);
          
          tabsPrivate: @<<SLOT GUIenvTabsPrivate: descriptor>>;
       #);
     
     open::<
       (# create::<
            (# <<SLOT GUIenvTabControlCreate: doPart>> #);
       <<SLOT GUIEnvTabControlOpen: doPart>> 
       #);
     
     close::<
       (# <<SLOT GUIEnvTabControlClose: doPart>> #);
     
     onBeforeSelectionChange:<
       (* Called when the selection is about to change;
        * Assign FALSE to allow to prevent the selection from changing.
        *)
       (# allow: @boolean;
       do TRUE -> allow;
          inner; 
       exit allow
       #);
     
     selection:
       (# theTab: ^tab;
       enter (# enter theTab[] <<SLOT GUIEnvTabControlSetSelection: doPart>> #)
       exit (# <<SLOT GUIEnvTabControlGetSelection: doPart>> exit theTab[] #)
       #);
     
     eventHandler::<
       (# onMouseUp::<
            (# <<SLOT GUIenvTabControlEventHandlerOnMouseUp: doPart>> #);
          onMouseDown::<
            (# <<SLOT GUIenvTabControlEventHandlerOnMouseDown: doPart>> #);
          onFatherFrameChanged::<
            (# <<SLOT GUIenvTabControlEventHandlerOnFatherFrameChanged: doPart>> #);
          onActivate::<
            (# <<SLOT GUIenvTabControlEventHandlerOnActivate: doPart>> #);
		  onRefresh::<
		  	(# <<SLOT GUIenvTabControlEventHandlerOnRefresh: doPart>> #);
       #);
     private: @<<SLOT GUIenvTabControlPrivate: descriptor>>;
  #);
