ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/fields';
INCLUDE '~beta/guienv/stddialogs';
INCLUDE '~beta/basiclib/file';
INCLUDE '~beta/guienv/utils/textfieldaddswinnt';
-- program: descriptor --
guienv
(# 
   textWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# textFile: @file;
                  printItem: @menuitem
                    (# eventHandler:: 
                         (# onSelect:: (# do txtedit.contents.print #) #);
                       open:: (# do 'print'->name #)
                    #);
                  quitItem: @menuitem
                    (# eventHandler:: 
                         (# onSelect:: (# do terminate #) #);
                       open:: (# do 'Quit'->name #)
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       printItem.open; printItem[]->append;
                       quitItem.open; quitItem[]->append
               #) #);
             open:: (# do fileMenu.open; fileMenu[] -> append #);
          #);
        txtEdit: @textEditor
          (# open::
               (#
               do textWindow.size->size;
                  true->bindBottom;
                  true->bindRight
          #) #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        open::
          (#
          do 'textEditorFull'->title;
             txtEdit.open
          #)
     #)
do textWindow.open
#)
