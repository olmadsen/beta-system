ORIGIN '~beta/guienv/guienv';
LIB_DEF 'frigg_manager' '../../lib';

-- CanvasLib: attributes --

iterate:
  (# current: ^WindowItem;
     copy: @List
       (# element:: WindowItem;
       #);
  do copy.init;
     scan
     (#
     do current[] -> copy.append;
     #);
     copy.scan
     (#
     do current[] -> THIS(iterate).current[];
        INNER iterate;
     #);
  #);

-- windowLib: Attributes --
alignLeft: (#  exit 1 #);
alignTop: (#  exit 1 #);
alignRight: (#  exit 2 #);
alignBottom: (#  exit 2 #);
alignCenter: (#  exit 3 #);
manager: canvas
  (# 
     
     margin: @
       (#
          left:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theLeft then value->theLeft; changed if)
              #)
            exit theLeft
            #);
          top:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theTop then value->theTop; changed if)
              #)
            exit theTop
            #);
          right:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theRight then value->theRight; changed if)
              #)
            exit theRight
            #);
          bottom:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theBottom then value->theBottom; changed if)
              #)
            exit theBottom
            #);
          theLeft,theTop,theRight,theBottom: @integer
       #);
     horizontal: @
       (#
          alignment:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theAlignment then
                     value->theAlignment; changed
                 if)
              #)
            exit theAlignment
            #);
          wrap:
            (# value: @boolean
            enter
              (# 
              enter value
              do (if value <> theWrap then value->theWrap; changed if)
              #)
            exit theWrap
            #);
          theWrap: @boolean;
          theAlignment: @integer
       #);
     vertical: @
       (#
          alignment:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theAlignment then
                     value->theAlignment; changed
                 if)
              #)
            exit theAlignment
            #);
          wrap:
            (# value: @boolean
            enter
              (# 
              enter value
              do (if value <> theWrap then value->theWrap; changed if)
              #)
            exit theWrap
            #);
          theWrap: @boolean;
          theAlignment: @integer
       #);
     distance:
       (# value: @integer
       enter
         (# 
         enter value
         do (if value <> theDistance then value->theDistance; changed if)
         #)
       exit theDistance
       #);
     eventHandler::< 
       (#
          onFrameChanged::<  (#  do changed; INNER ;  #);
          onChildFrameChanged::< 
            (# 
            do (if not computingLayout then changed if); INNER ; 
            #);
          
       #);
     open::< 
       (# 
       do
          5->theDistance;
          5->margin.theLeft;
          5->margin.theTop;
          5->margin.theRight;
          5->margin.theBottom;
          alignLeft->horizontal.theAlignment;
          alignCenter->vertical.theAlignment;
          INNER ;
          
       #);
     computeLayout:<
       (# 
       do
          (if not computingLayout then
              true->computingLayout;
              (if changedFlag then INNER ; false->changedFlag if);
              false->computingLayout
          if);
          
       #);
     changed:
       (# theComputer: ^computer
       do
          true->changedFlag;
          getComputer->theComputer[];
          theComputer.activate;
          (if not changedFlag then  if)
       #);
     computingLayout,changedFlag: @boolean;
     theDistance: @integer
  #);
getComputer: objectPool.get (# type:: computer #);
computer:
  (#
     count: @integer;
     activate:
       (#  do (if not active then true->active; 1->t.start if) #);
     compute:
       (#
          computeLayout:
            (# theCanvas: ^canvas; 
            enter theCanvas[]
            do
               theCanvas.iterate
                 (# 
                 do
                    (if current## <= manager## then
                        current[]->&computeLayout;
                        current[]
                          ->qua (# as:: manager do thisObj.computeLayout #)
                     else
                        (if current## <= canvas## then
                            current[]->&computeLayout
                        if)
                    if)
                 #)
            #)
       do
          (if active then t.stop; contents->computeLayout; false->active;  if)
       #);
     t: @timer (# action::  (#  do compute #) #);
     active: @boolean
  #)  

