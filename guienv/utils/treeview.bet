ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/fields';

BODY 'private/treeviewbody';

--- windowlib: attributes ---
treeview: canvas
  (# <<SLOT treeviewLib: attributes>>;
     selection: ^item;
     item: (* abstract superpattern for elements in the tree *)
       (# <<SLOT treeviewItemLib: attributes>>;
          isFolder:< booleanValue;
          father: ^folder (* the folder in which this(item) is
                           * located - this attribute is only valid,
                           * if this(item) is added to the folder
                           * using addItem
                           *);
          icon:< (* the icon to show for this type of item *)
            (# i: ^guienv.pixmap
            enter i[]
            <<SLOT treeviewItemIcon: dopart>>
            #);
          label: ^text (* the text of this item *);
          popupmenu:< (* popup menu on this item *)
            (# m: ^guienv.menu
            enter m[]
            <<SLOT treeviewItemPopupMenu: dopart>>
            exit m[]
            #);
          scrollIntoView:
            (# sc: ^scroller
            enter sc[]
            <<SLOT treeviewItemScrollIntoView: dopart>>
            #);
          select:
            (# <<SLOT treeviewItemSelect: dopart>> #);
          deselect:
            (# <<SLOT treeviewItemDeselect: dopart>> #);
          onIconSelected:< (* invoked when item icon is selected *)
            (# ev: ^eventhandler.mouseevent
            enter ev[]
            <<SLOT treeviewItemOniconselected: dopart>>
            #);
          onSelected:< (* invoked when item is selected *)
            (# ev: ^eventhandler.mouseevent
            enter ev[]
            <<SLOT treeviewItemOnselected: dopart>>
            #);
          onLabelChanged:<
            (* invoked the label have been edited.  newLabel will
             * contain the edited label value.
             *)
            (# newLabel: ^text
            enter newLabel[]
            <<SLOT treeviewItemOnlabelchanged: dopart>>
            #);
          allowLabelEdit:< booleanValue;
          itemPrivate: @<<SLOT treeviewItemPrivate: descriptor>>;
       #);
     folder: item
       (# <<SLOT treeviewFolderLib: attributes>>;
          isFolder:: trueObject;
          items: @list(# element:: item #);
          addItem:
            (# i: ^item
            enter i[]
            do this(folder)[]->i.father[]; 
               i[]->items.append;  
               INNER
            #);
          prependItem:
            (# i: ^item
            enter i[]
            do this(folder)[]->i.father[]; 
               i[]->items.prepend;  
               INNER
            #);
          Icon::<(* the icon to show for this folder when open *)
            (# openicon: ^guienv.pixmap
            enter openicon[]
            <<SLOT treeviewFolderOpenedIcon: dopart>>
            #);
          onToggleSelected:< (* invoked when toggle icon is selected *)
            (# <<SLOT treeviewFolderOntoggleselected: dopart>> #);
          onIconSelected::<
            (# <<SLOT treeviewFolderOniconselected: dopart>> #);
          onSelected::<
            (# <<SLOT treeviewFolderOnselected: dopart>> #);
          onExpand:< (* invoked when folder is opened *)
            (# <<SLOT treeviewFolderOnexpand: dopart>> #);
          onCollapse:< (* invoked when folder is closed again *)
            (# <<SLOT treeviewFolderOncollapse: dopart>> #);
          isExpanded:<
            (# open:@boolean
              <<SLOT treeviewFolderIsExpanded: dopart>> 
            exit open
            #);
          folderPrivate: @<<SLOT treeviewFolderPrivate: descriptor>>;
       #);
     root:
       (# f: ^folder
       enter (# enter f[] <<SLOT treeviewSetroot: dopart>> #)
       exit (# <<SLOT treeviewGetroot: dopart>> exit f[] #)
       #);
     hideRoot:< (* if trueObject then root is not shown *)
       booleanValue;
     minSize:< (* Specifies the minimum size of this(treeView) *)
       (# width, height: @integer;
       do INNER;
       exit (width, height)
       #);
     changed:
       (# <<SLOT treeviewChanged: dopart>> #);
     eventHandler::<
       (# onRefresh::<
            (# <<SLOT treeviewOnrefresh: dopart>> #);
          onMouseDown::<
            (# <<SLOT treeviewOnmousedown: dopart>> #);
          onMouseUp::<
            (# <<SLOT treeviewOnmouseup: dopart>> #);
          onUpdate:< (* further bind onUpdate and change etc. newheight,
                      * to change the size of this treeview 
                      *)
            (# width,height:@integer;
               newwidth,newheight:@integer;
               updateDoneInInner:@boolean;
            enter (width,height)
            do width->newwidth;
               height->newheight;
               Inner;
               (if not updateDoneInInner then
                   (if (newwidth<>width) or (newheight<>height) then
                       (newwidth,newheight)->size
                    else
                       true->update;
                   if);
               if)
            #)
       #);
     open::<
       (# <<SLOT treeviewOpen: dopart>> #);
     treeviewPrivate: @<<SLOT treeviewPrivate: descriptor>>;
  #);
