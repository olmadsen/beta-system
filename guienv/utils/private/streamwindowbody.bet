ORIGIN '../streamwindow';
LIB_ITEM 'guienvutilsstreamwin';
INCLUDE '~beta/basiclib/file';
INCLUDE '~beta/guienv/stddialogs';
INCLUDE '~beta/guienv/fields';
INCLUDE '~beta/guienv/utils/prompts';
INCLUDE '~beta/sysutils/errorscreen';
--StreamWindowPrivate: descriptor--
(# stderr:@errorscreen;
   closeCalled:@boolean;
   logfiletype: file
     (# (* Ignore silently *)
        AccessError::
          (# do NONE -> logfile[]; true->continue #);
        WriteError::
          (# do NONE -> logfile[]; true->continue #);
        ReadError::
          (# do NONE -> logfile[]; true->continue #);
        EOSerror::
          (# do NONE -> logfile[]; true->continue #);
        NoSuchFileError::
          (# do NONE -> logfile[]; true->continue #);
        FileExistsError::
          (# do NONE -> logfile[]; true->continue #);
        NoSpaceError::
          (# do NONE -> logfile[]; true->continue #);
        OtherError::
          (# do NONE -> logfile[]; true->continue #);
     #);
   logfile: ^logfiletype;
   log: @texteditor
     (#
        inserting: @boolean;
        contentsType::
          (# eventhandler::
               (# onBeforeChange::(# do inserting->allow #)#)
          #);
        append: @
          (# msg: ^text;
             lgth: @integer;
             scrolltoview: @contents.selection.scrollIntoView;
             setselection: @contents.selection.set;
             insert: @contents.insert;
          enter msg[]
          do true->inserting;
             (contents.contents).lgth -> lgth;
             (lgth, lgth) -> setselection;
             scrolltoview;
             msg[]->insert;
             false->inserting;
          #);
        open::
          (# 
          do true->bindleft->bindtop->bindright->bindbottom; 
             (if logfilename[]<>none then
                 &logfiletype[] -> logfile[];
                 logfilename[] -> logfile.name;
                 logfile.openWrite
             if)
          #);
        close::
          (# 
          do (if logfile[]<>none then
                 logfile.close;
                 NONE -> logfile[]
             if);
          #);
     #);
#)

--StreamWindowStreamBody: MainPart--
(# 
   logtext: @text;
   put::
     (# 
     do (if ch=ascii.bel then
            beep
         else
            ch->logtext.put;
            (if private.logfile[]<>NONE then
                ch -> private.logfile.put
            if);
            (if (ch=ascii.newline) or (logtext.lgth>70) then flush if);
        if)
     #);
   puttext::
     (# 
     do txt[] -> logtext.puttext;
        (if private.logfile[]<>NONE then
            txt[] -> private.logfile.puttext;
        if);
        (if (logtext.lgth>70) then flush if);
     #);
   flush: @
     (# 
     do logtext[]->private.log.append;
        (if private.logfile[]<>NONE then
            private.logfile.flush;
        if);
        logtext.clear;
     #);
#)
--StreamWindowOpen: DoPart--
do INNER;
   private.log.open; 
   size -> private.log.size;
--StreamWindowVanish: DoPart--
do INNER;
   (if private.logfile[]<>NONE then
       private.logfile.close;
       private.logfile.delete
   if);
   
--StreamWindowShow: Descriptor--
(# do thestream.flush; THIS(Window).show; #)

-- streamwindowOnAboutToClose: doPart --
do (if hideOnCloseByUser and not(private.closeCalled->okToClose) then
       this(Window).hide;
   if);
   INNER onAboutToClose;
   
-- streamwindowClose: dopart --
do true->private.closeCalled; INNER close; false->private.closeCalled;
   
--StreamWindowFlush: Descriptor--
(# do thestream.flush #)
--StreamWindowSaveAs: Descriptor--
(# 
   dialog: @FileCreationDialog(# do dialogtitle[]-> Title[]#);
   out: @file;
   doit: @boolean;
   filename: ^text;
   checkfile:
     (# filetext: ^text;
     do (if out.entry.isDirectory then
            '\'' -> filetext[];
            filename[] -> filetext.append;
            '\'\nis not a regular file! Save ignored.' 
              -> filetext.append;
            (THIS(window)[], filetext[], 'Save Ignored') -> AlertUser;
            false -> doit;
        if);
     #);
   checkexistence:
     (# confirmtext: ^text;
        confirm: @promptForBoolean
          (# 
             cancel::(# do false -> doit #);
             notOk::(# do false -> doit #)
          #);
     do (if out.entry.exists then
            checkfile;
            (if doit then
                (* entry exist and is a regular file *)
                '\'' -> confirmtext[];
                filename[] -> confirmtext.append;
                '\'\nalready exist. Overwrite it?' 
                  -> confirmtext.append;
                (THIS(window)[], 'Confirm Overwrite', confirmtext[]) 
                  -> confirm;
            if);
        if);
     #);
do thestream.flush;
   THIS(window)[] -> dialog -> filename[];
   (if filename[]<>NONE then
       filename[] -> out.name;
       true -> doit;
       checkexistence;
       (if doit then
           out.openWrite;
           private.log.contents.contents -> out.puttext;
           out.close;
       if);
   if);
#)
