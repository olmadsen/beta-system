ORIGIN '../guienvadds';
INCLUDE '../graphicsadds';
INCLUDE '../track';

MDBODY default 'X11/guienvadds_X11body';
-- windowItemDefineRect: doPart --
do graphics
   (# 
   do track
      (# topWeight,leftWeight,rightWeight,bottomWeight: @integer;
         drawRect: @drawRectangle;
         mousePress::
           (# 
           do (if doTop then 1 -> topWeight; if);
              (if doLeft then 1 -> leftWeight; if);
              (if doRight then 1 -> rightWeight; if);
              (if doBottom then 1 -> bottomWeight; if);
              transferModes.xorBlend[] -> pen.mode;
              false -> clipChildren;
              r[] -> drawRect;
           #);
         mouseMove::
           (# 
           do (if (h <> 0) or (v <> 0) then
                  r[] -> drawRect;
                  r.top + v * topWeight -> r.top;
                  r.left + h * leftWeight -> r.left;
                  r.bottom + v * bottomWeight -> r.bottom;
                  r.right + h * rightWeight -> r.right;
                  (if (r.bottom - r.top < 7) then
                      (if doTop then
                          r.bottom - 7 -> r.top;
                       else
                          r.top + 7 -> r.bottom
                      if);
                  if);
                  (if (r.right - r.left < 7) then
                      (if doLeft then
                          r.right -7 -> r.left;
                       else
                          r.left + 7 -> r.right
                      if);
                  if);
                  r[] -> drawRect;
              if);
           #);
         mouseRelease::
           (# 
           do r[] -> drawRect;
           #);
      #);
   #)
   

-- windowItemLib: attributes --
origin:
  (# p: @point;
  do position -> p;
     (if this(windowItem)[]<> contents then
         father.origin -> p.add;
     if);
  exit p
  #);

   
-- GUIenvWindowTranslate: doPart --
do p -> result;
   from.origin -> result.add;
   to.origin -> result.subtract;

