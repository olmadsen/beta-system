ORIGIN '../optionboxbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1991-96
 *       All rights reserved.
 *)

INCLUDE '../../../private/winnt/guienv_ntiprivate';
INCLUDE '../../../private/winnt/guienvattributes';
INCLUDE '../../../private/winnt/controls_ntibody';

INCLUDE '../../guienvadds';

INCLUDE '~beta/win32lib/v1.2/gdistructs';
INCLUDE '~beta/win32lib/v1.2/wingdiconsts';
INCLUDE '~beta/win32lib/v1.2/wingdi';
INCLUDE '~beta/win32lib/v1.2/windowsmisc';
INCLUDE '~beta/win32lib/v1.2/mousecursorsupport';
INCLUDE '~beta/win32lib/v1.2/scrollbarsupport';
INCLUDE '~beta/win32lib/v1.2/winuserconsts';
INCLUDE '~beta/win32lib/v1.2/errorhandling';
INCLUDE '~beta/win32lib/v1.2/systemmetrics';
INCLUDE '~beta/win32lib/v1.2/keyboardinputsupport';

INCLUDE '~beta/sysutils/v1.6/cstring';


-- GUIENVitemSetName: doPart --
do assertOpen
   (# location::(# do 'item.setName'->t[] #);
   do (if t[]<>NONE then
          (if t.length>0 then
              t.copy -> private.theName[];
           else
              (if private.theName[]<>NONE then
                  private.theName.clear;
              if);
          if);
          theEventHandler.onNameChanged;
      if);
   #)
   
-- GUIENVitemGetName: doPart --
do assertOpen
   (# location::(# do 'item.getName'->t[] #);
   do private.theName.copy -> t[];
   #);
   
-- GUIENVitemPosition: doPart --
do assertOpen
   (# location::(# do 'item.getName'->t[] #);
   do private.thePosition -> value;
   #);
   
-- GUIENVitemPrivate: descriptor --
(# theName: ^text; 
   thePosition: @integer;
   inx: @integer; (* the index in the menu, optionBox or scrolllist. *)
   doUpdateList: @boolean;
#)

-- GUIENVoptionItemCreate: doPart --
do inner;
   
-- GUIENVoptionItemOpen: doPart --
do assertOpen
   (# location::(# do 'optionItem.open'->t[] #); 
   do this(optionItem)[] -> getPatternName -> private.theName[];
      FALSE -> private.doUpdateList;
      inner open;
      
      (if not (this(optionItem)[] -> 
          this(optionBox).private.theOptionItems.member) then
          this(optionItem)[] -> this(optionBox).private.theOptionItems.append;
          this(optionBox).private.theOptionItems.size 
            -> this(optionItem).private.thePosition;
          
          this(optionItem)[] -> this(optionBox).private.theCombobox.add;
      if);
      TRUE -> private.doUpdateList;
   #);
   
-- GUIENVoptionItemOnNameChanged: doPart --
do assertOpen
   (# location::(# do 'optionItem.onNameChanged'->t[] #); 
   do (if private.doUpdateList then
          (private.inx, private.theName)
            -> this(optionBox).private.theComboBox.changeString;
      if);
      inner onNameChanged;
   #);
   
   
-- optionBoxLib: attributes --

-- GUIENVoptionboxOnLabelChanged: doPart --
do assertOpen
   (# location::(# do 'optionBox.onLabelChanged'->t[] #); 
      l: ^text; 
      theTextStyle: ^textStyle;
   do buttonLabel -> l[];
      buttonStyle -> theTextStyle[];
      (if theTextStyle[]<>NONE then
          (if l.length=0 then
              0 -> private.leftMargin;
           else
              2 -> private.leftMargin;
          if);
          
          l[] -> private.theLabel.label;
          private.theLabel.updateFrame;
      if);
      inner onLabelChanged;
   #);
   
-- GUIENVoptionboxOnStyleChanged: doPart --
do assertOpen
   (# location::(# do 'optionBox.onStyleChanged'->t[] #); 
   do (# l: ^text; 
         theTextStyle: ^textStyle;
      do buttonLabel -> l[];
         buttonStyle -> theTextStyle[];
         (if theTextStyle[]<>NONE then
             (if l.length=0 then
                 0 -> private.leftMargin;
              else
                 2 -> private.leftMargin;
             if);
         
             theTextStyle[] -> private.theLabel.setTextStyle;
             theTextStyle[] -> private.theComboBox.setTextStyle;
             private.theLabel.updateFrame;
         if);
         
         inner onStyleChanged; 
      #); 
   #);
   
-- GUIENVoptionboxOnRefresh: doPart --
do inner;
   
-- GUIENVoptionboxOnEnableTarget: doPart --
do private.theComboBox[] -> target;
   inner;
   
-- GUIENVoptionboxOnDisableTarget: doPart --
do inner;
   
-- GUIENVoptionboxOnActivate: doPart --
do this(optionBox).bringBack;
   inner;
   
-- GUIENVoptionboxOnVisibleChanged: doPart --
do (if visible then
       private.theLabel.show;
       private.theComboBox.show;
    else
       private.theLabel.hide;
       private.theComboBox.hide;
   if);
   inner;
   
-- GUIENVoptionboxOnFrameChanged: doPart --
do (if private.theLabel.isOpen then
       private.theLabel.updateFrame;
   if);
   inner;
   
-- GUIENVoptionboxOnKeyDown: doPart --
do inner;
   
-- GUIENVoptionBoxNumberOfItems: doPart --
do assertOpen
   (# location::(# do 'optionBox.numberOfItems'->t[] #); 
   do private.theComboBox.noOfItems -> value;      
   #)
   
-- GUIENVoptionboxSetCurOption: doPart --
do assertOpen
   (# location::(# do 'optionBox.SetCurItem'->t[] #);
   do optionItemNo-1 -> private.theComboBox.setTheSelection;
   #)
   
-- GUIENVoptionboxGetCurOption: doPart --
do assertOpen
   (# location::(# do 'optionBox.GetCutItem'->t[] #);
   do 
      (private.theComboBox.interfaceObjectID,CB_GETCURSEL,0,0) 
        -> sendMessage -> optionItemNo;
      optionItemNo + 1 -> optionItemNo;
   #)
   
-- GUIENVoptionboxGetOptionItemByNumber: doPart --
do assertOpen
   (# location::(# do 'optionBox.getOptionItemByNumber'->t[] #); 
   do theOptionItemNo -> private.getItemByNumber -> theOptionItem[];
   #);
   
-- GUIENVgetNumberByOptionItem: doPart --
do assertOpen
   (# location::(# do 'optionBox.getNumberByOptionItem'->t[] #); 
   do (if theOptionItem[]<>NONE then
          theOptionItem.private.inx + 1 -> theOptionItemNo;
      if);
   #);
   
-- GUIENVoptionboxSetText: doPart --
do assertOpen
   (# location::(# do 'optionBox.setText'->t[] #); 
      theOptionItem: ^optionItem;
      theInx, theId: @integer;
      selInx: @integer;
   do (if theText[]<>NONE then
          theOptionItemNo -> private.getItemByNumber -> theOptionItem[];
          (if theOptionItem[]<>NONE then
              theText[] -> theOptionItem.name;
           else
              private.theComboBox.interfaceObjectID -> theId;
              (if theOptionItemNo<numberOfOptions+1 then
                  (theId,CB_GETCURSEL,0,0) -> sendMessage -> selInx;
                  theOptionItemNo-1 -> theInx;
                  (theId,CB_DELETESTRING,theInx,0) -> sendMessage;
                  
                  (theId,CB_INSERTSTRING,theInx,@@theText.T[1])-> sendMessage;
                  private.theOptionItems.scan
                  (# 
                  do (if current.private.inx>theOptionItemNo then
                         current.private.inx + 1 -> current.private.inx;
                     if);
                  #);
                  (if selInx=theInx then
                      theInx -> private.theComboBox.setTheSelection;
                  if);
               else
                  (theId,CB_ADDSTRING,0,@@theText.T[1]) -> sendMessage;
                  
                  (if ( (theId,CB_GETCOUNT,0,0) -> sendMessage)=1 then
                      0 -> private.theComboBox.setTheSelection;
                  if);
              if);
          if);
       if);
   #);
   
-- GUIENVoptionboxGetText: doPart --
do assertOpen
   (# location::(# do 'optionbox.getText'->t[] #);
      length: @integer; cstr: @cString;
      theId: @integer;
   do (if ( (theOptionItemNo>0) and (theOptionItemNo<numberOfOptions+1) ) then
          private.theComboBox.interfaceObjectID -> theId;
          (theId,CB_GETLBTEXTLEN,theOptionItemNo-1,0) -> SendMessage ->length;
          (if length=CB_ERR then
              'CB_GETLBTEXTLEN failed. Errorcode: ' -> screen.putText;
              GetLastError -> screen.putint; screen.newline;
           else
              length+1 -> cstr.init;
              (theId,CB_GETLBTEXT,theOptionItemNo-1,cstr.charPtr) 
                -> SendMessage -> length;
              (if length=CB_ERR then
                  'CB_GETLBTEXT failed. Errorcode: ' -> screen.putText;
                  GetLastError -> screen.putint; screen.newline;
               else
                  cstr.get -> theText[];
                  theText.T.range -> theText.pos -> theText.lgth;
                  cstr.free;
              if);
          if);
      if);
   #);
   
-- GUIENVoptionboxAppendText: doPart --
do assertOpen
   (# location::(# do 'optionBox.appendText'->t[] #); 
      theId: @integer;
      res: @integer;
   do (if theText[]<>NONE then
          private.theComboBox.interfaceObjectID -> theId;
          (theId,CB_ADDSTRING,0,@@theText.T[1]) -> sendMessage -> res;
          (if ( (theId,CB_GETCOUNT,0,0) -> sendMessage)=1 then
              0 -> private.theComboBox.setTheSelection;
          if);
      if);
   #);
   
-- GUIENVoptionboxDeleteByPosition: doPart --
do assertOpen
   (# location::(# do 'optionBox.deleteByNumber'->t[] #); 
      theOptionItem: ^optionItem;
      deleteRes: @integer;
      theId: @integer;
   do 
      (if ( (theOptionItemNo>0) 
          and 
          (theOptionItemNo<numberOfOptions+1) ) then
          private.theComboBox.interfaceObjectID -> theId;
          (theId,CB_DELETESTRING,
          theOptionItemNo-1,0) -> sendMessage -> deleteRes;
          theOptionItemNo -> private.getItemByNumber -> theOptionItem[];
          (if theOptionItem[]<>NONE then
              theOptionItem[] -> private.theOptionItems.delete;
           else
              'theOptionItem was none' -> screen.putline;
          if);
          private.theOptionItems.scan
          (# 
          do (if current.private.inx>theOptionItemNo then
                 current.private.inx - 1 -> current.private.inx;
             if);
          #);
       if);     
   #);
   
-- GUIENVoptionboxDelete: doPart --
do assertOpen
   (# location::(# do 'optionBox.delete'->t[] #); 
      theOptionItem: ^optionItem;
   do theOptionItem.private.inx+1 -> deleteByPosition;
   #);
   
-- GUIENVoptionboxClear: doPart --
do assertOpen
   (# location::(# do 'optionBox.clear'->t[] #); 
      theId: @integer;
   do private.theComboBox.interfaceObjectID -> theId;
      (theId,CB_RESETCONTENT,0,0) -> SendMessage;
      &private.optionItems[] -> private.theOptionItems[];      
   #);
   
-- GUIENVoptionboxCreate: doPart --
do uienvStandardWindowClass -> windowItemClass;
   isStandardClass  -> windowItemClassStatus;         
   inner;
   
-- GUIENVoptionboxOpen: doPart --
do (# theTextStyle: ^textStyle;
      
   do &private.optionItems[] -> private.theOptionItems[];      
      2 -> private.leftMargin;
      (5,20) -> size;
      private.theLabel.open;
      private.theComboBoxFather.open;
      private.theComboBoxFather[] -> private.theComboBox.open;
      
      inner open;
      
      buttonStyle -> theTextStyle[];
      (if theTextStyle[]<>NONE then
          theTextStyle[] -> private.theLabel.setTextStyle;
          theTextStyle[] -> private.theComboBox.setTextStyle;
      if);
      private.theLabel.updateFrame;
   #);
   
-- GUIENVoptionboxClose: doPart --
do inner;
   
-- GUIENVoptionboxprivate: descriptor --
(# maxWidthOfItem,maxHeight, theComboBoxHeight: @integer;
   leftMargin: @integer;
   
   theLabel: @staticText
     (# 
        eventHandler::
          (# onKeyDown::
               (# do this(optionBox).theEventHandler.onKeyDown; #);
             onMouseDown::
               (# do this(optionBox).theEventHandler.onMouseDown; #);
          #);
        
        open::
          (# do hide; #);
        
        setTextStyle:
          (# theTextStyle: ^textStyle;
          enter theTextStyle[]
          do (this(theLabel).interfaceObjectID,
             WM_SETFONT,theTextStyle.textStyleID,1) 
               -> SendMessage;
          #);
        
        updateFrame:
          (# l: ^text; 
             heightOfLabel,widthOfLabel,labelH: @integer;
             theTextStyle: ^textStyle;
             x,y: @integer;
             r: ^rectangle;
             
          do buttonLabel -> l[];
             buttonStyle -> theTextStyle[];
             l[] -> theTextStyle.widthOfText -> widthOfLabel;
             theTextStyle.lineHeight -> heightOflabel;
             
             theComboBox.interfaceObjectID -> getBounds -> r[];
             
             (if (r.bottom - r.top) > heightOfLabel then
                 ((r.bottom - r.top) div 2) - (heightOfLabel div 2) ->labelH;
              else
                 (heightOfLabel div 2) ->labelH
             if);
             this(optionBox).position -> (x,y);
             ( (x+leftMargin,y+labelH),
             (x+leftMargin+widthOfLabel,y+heightOflabel+labelH) ) 
               -> frame;
             theComboBox.updateFrame;
          #);
     #);
   
   theComboBoxFather: @canvas
     (# hadCBN_SELENDOK: @boolean;
        
        comboBoxMethods: windowItemMethods
          (# dispatchMessage::
               (# processCommand:
                    (# type: @integer;
                       id: @integer;
                       inx: @integer;
                       theOptionItem: ^optionItem;
                       
                       x,y,w,h: @integer;
                       itemH,noOfItems: @integer;
                       r: @rectangle;
                       ntrPtr: @integer;
                    do info.wParam.hiWord -> type;
                       info.wParam.loWord -> id;
                       (if type
                        //CBN_SELENDOK then
                           true -> hadCBN_SELENDOK;
                           
                        //CBN_CLOSEUP then 
                           (info.lParam,CB_GETCURSEL,0,0) 
                             -> SendMessage -> inx;
                           (if hadCBN_SELENDOK then
                               false -> hadCBN_SELENDOK;
                               inx+1 -> getItemByNumber -> theOptionItem[];
                               (if theOptionItem[]<>NONE then
                                   theOptionItem.theEventHandler.onSelect;
                               if);
                           if);
                        else
                           false -> didSomething;
                       if);
                    #);
                  
                  didSomething: @boolean;
               do true -> didSomething;
                  (if info.message 
                   //WM_COMMAND then processCommand; 
                   else
                      false -> didSomething;
                  if);
                  didSomething or info.handled -> info.handled;
               #);
          #);

        open::
          (# 
          do &comboBoxMethods[] -> theMethods;
             (* hide; *)
          #);
     #);
        
   theComboBox: @windowItem
     (# 
        eventHandler::
          (# onMouseDown::
               (# do this(optionBox).theEventHandler.onMouseDown; #);
             onMouseUp::
               (# do this(optionBox).theEventHandler.onMouseUp; #);
             onKeyDown::
               (# windowID: @integer;
               do this(theComboBox).interfaceObjectID -> windowID;
                  (if ch 
                   //VK_UP      
                   //VK_DOWN then
                      (if (((windowID,CB_GETDROPPEDSTATE,0,0)->SendMessage)=0) then 
                          (windowID,CB_SHOWDROPDOWN,1,0)->SendMessage;
                      if);
                   //ascii.sp then 
                      (if (((windowID,CB_GETDROPPEDSTATE,0,0)->SendMessage)<>0) then 
                          true -> theComboBoxFather.hadCBN_SELENDOK;
                          (windowID,CB_SHOWDROPDOWN,0,0)->SendMessage;
                       else
                          (windowID,CB_SHOWDROPDOWN,1,0)->SendMessage;
                      if);
                   //ascii.esc then
                      (if (((windowID,CB_GETDROPPEDSTATE,0,0)->SendMessage)<>0) then
                          (windowID,CB_SHOWDROPDOWN,0,0)->SendMessage;
                      if);
                  if);
                  this(optionBox).theEventHandler.onKeyDown; 
               #);
          #);
        
        open::
          (# create::
               (# 
               do 'COMBOBOX' -> windowItemClass;
                  isSubClass -> windowItemClassStatus;
                  CBS_DROPDOWNLIST -> windowItemStyle;
               #);
             x,y,w,h: @integer;
          do hide;
             (* (5,5) -> father.size;
              (5,5) -> size; *)
             calcTheHeight;
          #);
        
        close::
          (# windowID: @integer;
          do this(theComboBox).interfaceObjectID -> windowID;
             (if (((windowID,CB_GETDROPPEDSTATE,0,0)->SendMessage)<>0) then 
                 (windowID,CB_SHOWDROPDOWN,0,0)->SendMessage;
             if);
          #);
        
        setTextStyle:
          (# theTextStyle: ^textStyle;
          enter theTextStyle[]
          do (this(theComboBox).interfaceObjectID,
             WM_SETFONT,theTextStyle.textStyleID,1) 
               -> SendMessage;
          #);
        
        noOfItems: integerValue
          (# 
          do (this(theComboBox).interfaceObjectID,CB_GETCOUNT,0,0) 
               -> SendMessage -> value;
             (if value=CB_ERR then
                 'CB_GETCOUNT falied. Errorcode: ' -> screen.puttext;
                 GetLastError -> putint; newline;
                 0 -> value;
             if);
          #);
        
        setTheSelection:
          (# inx: @integer;
             noOfItems: @integer;
             theId: @integer;
          enter inx
          do this(theComboBox).interfaceObjectID -> theId;
             (theId,CB_GETCOUNT,0,0) -> sendMessage -> noOfItems;
             (if (inx>-1) and (inx<noOfItems+1) then
                 (theId,CB_SETCURSEL,inx,0) -> sendMessage;
             if);
          #);
        
        changeString:
          (# theInx,theId,selInx: @integer;
             theString: @text;
          enter (theInx, theString)
          do this(theComboBox).interfaceObjectID -> theId;
             (theId,CB_GETCURSEL,0,0) -> sendMessage -> selInx;
             
             (theId,CB_DELETESTRING,theInx,0) -> sendMessage;
             
             (theId,CB_INSERTSTRING,theInx,@@theString.T[1]) 
               -> sendMessage;
             
             (if selInx=theInx then
                 theInx -> setTheSelection;
             if);
          #);
        
        add:
          (# theItem: ^optionItem;
             theId: @integer;
             cstr: @cString; result,inx: @integer;
             menuItemId: @integer;
             noOfItems: @integer;
             w,h: @integer;
          enter theItem[]  
          do this(theComboBox).interfaceObjectID -> theId;
             theItem.name -> cstr.set;
             (theId,CB_ADDSTRING,0,cstr.charPtr) -> sendMessage -> inx;
             inx -> theItem.private.inx;
             (if inx
              //CB_ERR
              //CB_ERRSPACE then
                 'optionBoxAddToComboBox failed!! Errorcode: ' -> screen.puttext;
                 GetLastError -> screen.putint; screen.newline;
             if);
             
             (theId,CB_GETCOUNT,0,0) -> sendMessage -> noOfItems;
             (if noOfItems=1 then
                 0 -> theComboBox.setTheSelection;
             if);
          #);  
        
        calcTheHeight:
          (# r: ^rectangle; h: @integer;
          do this(theComboBox).interfaceObjectID -> getBounds -> r[];
             (r.bottom - r.top) -> theComboBoxHeight;
          #);
        
        updateFrame:
          (# x,y,w,h: @integer;
          do this(optionBox).position -> (x,y);
             theLabel.size -> (w,h);
             (x+w+leftmargin+2,y+topMargin) -> father.position;
             preferredSize -> (w,h);
             calcTheHeight;
             (w,theComboBoxHeight) -> father.size; 
             (w,h) -> this(theComboBox).size; 
          #);
        
        preferredSize:
          (# prefW, prefH: @integer;
             theTextStyle: ^textStyle;
             itemH, noOfItems: @integer;
             widthOfItem: @integer;
             scrollbarWidth: @integer;
          do this(optionBox).buttonStyle -> theTextStyle[];         
             SM_CXVSCROLL -> GetSystemMetrics -> scrollbarWidth;
             0 -> maxWidthOfItem;
             
             theOptionItems.scan
             (# tempName: ^text;
             do current.name -> tempName[];
                (if tempName.length>0 then
                    (if theTextStyle[]<>NONE then
                        tempName[] -> theTextStyle.widthOfText 
                          -> widthOfItem;
                        widthOfItem + 2 -> widthOfItem;
                     else
                        tempName.length + 2 -> widthOfItem;
                    if);
                    
                    (if widthOfItem>maxWidthOfItem then
                        widthOfItem -> maxWidthOfItem;
                    if);
                 else
                    'current.name.lenght = 0' -> putline;
                if);
             #);
             
             maxWidthOfItem + scrollbarWidth + leftMargin + 4  -> prefW;
             (interfaceObjectID,CB_GETITEMHEIGHT,0,0)-> SendMessage -> itemH;
             (interfaceObjectID,CB_GETCOUNT,0,0) -> SendMessage -> noOfItems;
             theComboBox.calcTheHeight;
             itemH*noOfItems+itemH+theComboBoxHeight -> prefH; 
          exit (prefW,prefH)
          #);
        
     #);         
   
   topMargin: @integerValue
     (# 
     do (if border.visible then
            (if border.style=borderstyles.simple then
                2 -> value;
             else
                3 -> value;
            if);
        if);
     #);      
   
   optionItems: linkedList
     (# element:: optionItem;
        size:
          (#  count: @integer;
          do this(theOptionItems).scan
             (# 
             do count + 1 -> count;
             #);
          exit count
          #);
     #);
   theOptionItems: ^optionItems;
   
   getItemByNumber:
     (# theOptionItemNo: @integer;
        theOptionItem: ^optionItem;
     enter theOptionItemNo
     do l: theOptionItems.scan
          (# 
          do (if current.private.inx+1=theOptionItemNo then
                 current[] -> theOptionItem[];
                 leave l;
             if);
          #);
     exit theOptionItem[]
     #);      
   
   translate:
     (# h,v: @integer;
        ntpPtr: @integer
     enter (h,v)
     do (h,v) -> makeNtPointPtr -> ntpPtr;
        (interfaceObjectID,ntpPtr) -> ClientToScreen;
        ntpPtr -> getPointFromNtPointPtr -> (h,v);
        ntpPtr -> free;
     exit (h,v)
     #);
   
   getBounds:
     (# theNtRectPtr: @integer;
        r: ^rectangle;
        ID: @integer;
     enter ID
     do 16 -> malloc -> theNtRectPtr;
        (ID,theNtRectPtr) -> GetClientRect;
        &rectangle[] -> r[];
        theNtRectPtr -> getRectFromNtRectPtr -> r;
        r.topLeft -> translate -> r.topLeft;
        r.bottomRight -> translate -> r.bottomRight;
        theNtRectPtr -> free;
     exit r[]
     #);
   
   preferredSize:
     (# prefW, prefH: @integer;
        l: ^text; theTextStyle: ^textStyle;
        heightOfLabel,widthOfLabel: @integer;
        itemH, noOfItems: @integer;
        widthOfItem,lineHeight: @integer;
        scrollbarWidth: @integer;
     do this(optionBox).buttonLabel -> l[];
        (if (l[]=none) then &text[] -> l[]; if);
        this(optionBox).buttonStyle -> theTextStyle[];
        (if theTextStyle[]<>NONE then
            l[] -> theTextStyle.widthOfText -> widthOfLabel;
            widthOfLabel + 2  -> widthOfLabel;
            theTextStyle.lineHeight -> lineHeight;
         else
            l.length + 5 -> widthOfLabel;
        if);
        
        SM_CXVSCROLL -> GetSystemMetrics -> scrollbarWidth;
        
        widthOfLabel + maxWidthOfItem + leftMargin + scrollbarWidth 
          -> prefW;   
        (if l.length>0 then
            4 + prefW -> prefW;
        if);
        theComboBox.calcTheHeight;
        (if lineHeight > theComboBoxHeight then
            lineHeight -> prefH;
         else
            theComboBoxHeight -> prefH;
        if);
        
        (if border.visible then
            (if border.style=borderStyles.simple then
                2 + prefW -> prefW;
                4 + prefH -> prefH;
             else
                4 + prefW -> prefW;
                6 + prefH -> prefH;
            if);
        if);
        
     exit (prefW,prefH)
     #);
#)
