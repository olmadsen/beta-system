ORIGIN '../rasteraddsbody';
INCLUDE '../../../private/winnt/raster_ntibody';
INCLUDE '../../../private/winnt/guienv_ntiprivate';
INCLUDE '~beta/win32lib/v1.1/systemmetrics';


-- bitmapLib: attributes --

iconGetWidth: external
  (# hIcon: @integer;
     width: @integer;
  enter hIcon
  do callC;
  exit width
  #);


LoadIconById: external
  (# hInstance: @integer; (* HINSTANCE *)
     id: @integer; (* LPCSTR *)
     hIcon: @integer; (* HICON *)
  enter (hInstance,id)
  do 'LoadIconA' -> callStd;
  exit hIcon
  #);

LoadIconByName: external
  (# hInstance: @integer; (* HINSTANCE *)
     name: [1] @char; (* LPCSTR *)
     hIcon: @integer; (* HICON *)
  enter (hInstance,name)
  do 'LoadIconA' -> callStd;
  exit hIcon
  #);

loadFromIconRes:
  (# id: @integer;
  enter id
  do (if id=0 then
         '****Warning icon id is 0. Cannot load icon.' -> putline;
      else
         (uienvHInstance,id) -> loadIconById -> handleToIcon;
         (if handleToIcon=0 then
             'LoadIcon failed: Error: ' -> screen.puttext;
             GetLastError -> screen.putInt; screen.newline;
          else
             true -> isIcon;
             SM_CXICON -> GetSystemMetrics -> rasterWidth;
             SM_CYICON -> GetSystemMetrics -> rasterHeight;
         if);
     if);
  #);


-- bitmapLib2: attributes --

loadBitmapById: external
  (# hInstance: @integer; (* HINSTANCE *)
     id: @integer; (* LPCSTR *)
     hBitmap: @integer; (* HBITMAP *)
  enter (hInstance,id)
  do 'LoadBitmapA' -> callStd;
  exit hBitmap
  #);

loadFromBitmapRes:
  (# id: @integer;
     success: @boolean;
  enter id 
  do false -> success;
     (if id=0 then
         '**WARNING: Bitmap id is 0. Cannot load bitmap.' -> putline;
      else
         (uienvHInstance,id) -> loadBitmapById -> handleToBitmap;
         (if handleToBitmap=0 then
             'LoadBitmap failed: Error: ' -> screen.puttext;
             GetLastError -> screen.putInt; screen.newline;
          else
             handleToBitmap -> bitMapGetWidth -> rasterWidth;
             handleToBitmap -> bitMapGetHeight -> rasterHeight;
             true -> success;
         if);
     if);
  exit success
  #);



-- iconLoadById: descriptor --
(# 
do id -> loadFromIconRes;
#)


-- bitmapResLoadById: doPart --
do id -> loadFromBitmapRes -> success;
   
-- bitmapResLoadByName: doPart --
do (# theString: @cString;
   do false -> success;
      (if name[]<>NONE then
          (if name.length>0 then
              name.copy -> theString.set;
              (uienvHInstance,theString.charptr)
                -> loadBitmapById -> handleToBitmap;
              (if handleToBitmap=0 then
                  'LoadBitmap failed: Error: ' -> screen.puttext;
                  GetLastError -> screen.putInt; screen.newline;
               else
                  handleToBitmap -> bitMapGetWidth -> rasterWidth;
                  handleToBitmap -> bitMapGetHeight -> rasterHeight;
                  true -> success;
              if);
              theString.free;
          if);
      if);
 #)
