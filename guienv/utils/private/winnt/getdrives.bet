ORIGIN '~beta/basiclib/v1.6/betaenv';

-- lib: attributes --

(* WINBASEAPI DWORD WINAPI GetLogicalDriveStringsA
 * ( DWORD nBufferLength, LPSTR lpBuffer ); 
 *)

GetLogicalDriveStrings: external
  (# bufferLength: @integer;
     pBuffer: @integer;
     bytesCopied: @integer;
  enter (bufferLength, pBuffer)
  do 'GetLogicalDriveStringsA' -> callStd;
  exit bytesCopied
  #);

getDrives:
  (# dwNumBytesForDriveStrings: @integer;
     drivesString1, drivesString2: ^text;
     nNumDrives, nDriveNum: @integer;
     
  do 0 -> nNumDrives;
     (* Get the number of bytes needed to hold all
      * the logical drive strings. *)   
     (0, 0) -> GetLogicalDriveStrings ->  dwNumBytesForDriveStrings;

     (* Allocate memory for the drive string names.
      *)
     &text[] -> drivesString1[];
     dwNumBytesForDriveStrings -> drivesString1.extend;
     
     (* Get the drives string names in our buffer. *)
     (dwNumBytesForDriveStrings, @@drivesString1.T[1]) 
       -> GetLogicalDriveStrings;
     
     dwNumBytesForDriveStrings -> drivesString1.lgth -> drivesString1.pos; 
     drivesString1.reset;
     &text[] -> drivesString2[];
     drivesString1.scanAll
     (# 
     do 
        (if ch
         //ascii.nul then
         //'\\' then ',' -> drivesString2.put;
         else
             ch -> drivesString2.put;
        if);
     #);
  exit drivesString2[]
  #)
