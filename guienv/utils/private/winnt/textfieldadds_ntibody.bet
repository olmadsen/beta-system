ORIGIN '../../textfieldadds';
INCLUDE '../../../private/winnt/fields_ntibody';
LIB_ITEM 'guienvutilstextfld';
-- lib: Attributes --
  

-- GUIENVTextFieldPositionToRowCol: Descriptor --
assertOpen
  (#
     location::  (#  do 'posToRowCol'->t[];  #);
     getRowCol:
       (#
          oldPos: @integer;
          row,col: @integer;
          curPos,prevPos,noOfChars: @integer;
          doLeave: @boolean;
          id: @integer;
          
       enter oldPos
       do
          interfaceobjectID->id;
          (id,EM_LINEFROMCHAR,oldPos,0)->SendMessage->row;
          L1:
            (# 
            do
               curPos->prevPos;
               (id,EM_LINEINDEX,row,0)->SendMessage->curPos;
               (id,EM_LINELENGTH,curPos,0)->SendMessage->noOfChars;
               (if curPos = - 1 then prevPos->curPos; true->doLeave;  if);
               (if
                   ((curPos <= (oldPos+row)) and
                   ((oldPos+row) <= (curPos+noOfChars))) or doLeave then
                   leave L1
                else
                   row+1->row; restart L1; ; 
               if);
               
            #);
          oldPos+row-curPos->col;
          
       exit (row,col)
       #);
     
  do pos->getRowCol->(row,col); row+1->row; col+1->col; 
  #)  

-- GUIENVTextFieldRowColToPosition: Descriptor --
assertOpen
  (# location::  (#  do 'rowColToPos'->t[];  #); tempCol: @integer; 
  do
     (interfaceObjectId,EM_LINEINDEX,row-1,0)->sendMessage->tempCol;
     tempCol+col-row->pos;
     
  #)  

-- GUIENVtextFieldAutomaticScrolling: DoPart --
do
(* it is not possible to change the ES_AUTOHSCROLL or 
 ES_AUTOVSCROLL dynamically*)   

-- GUIENVtextFieldDisableUpdate: DoPart --
do (interfaceObjectID,WM_SETREDRAW,0,0)->SendMessage;   

-- GUIENVtextFieldEnableUpdate: DoPart --
do (interfaceObjectID,WM_SETREDRAW,1,0)->SendMessage; update;   

-- GUIENVtextFieldUpdateLine: DoPart --
do
   (# pos,x,y,xMax,yMax,ntrPtr: @integer; r: @rectangle; 
   do	
      (interfaceObjectID,WM_SETREDRAW,1,0)->SendMessage;
      (lineNumber,1)->rowColtoPos->pos;
      (if pos>=0 then
          (* if the line is not visible
           * we don't have to do anything 
           *)
          pos -> posToPt->(x,y); 
          size->(xMax,yMax);
          ((x,y-charHeight),(xMax,y))->r;
          r[]->makeNtRectPtr->ntrPtr;
          (interfaceObjectID,ntrPtr,1)->InvalidateRect;
          ntrPtr->free
      if)
   #)  
-- GUIENVtextFieldUpdateRegion: doPart --
do
   (# scrollbarHeight: @integer;
      xMax,yMax,ntrPtr: @integer; r: @rectangle; 
   do	
      (interfaceObjectID,WM_SETREDRAW,1,0)->SendMessage;      
      (if (y1<0) or (y2<0) then
          update
       else
          SM_CXHSCROLL -> GetSystemMetrics -> scrollbarHeight; 
          size->(xMax,yMax);
          (if y2>yMax-scrollbarHeight then
              update
           else
              ((0,y1-charHeight),(xMax,y2))->r;
              r[]->makeNtRectPtr->ntrPtr;
              (interfaceObjectID,ntrPtr,1)->InvalidateRect;
              ntrPtr->free
          if)      
      if)
   #)
