ORIGIN '../iconnamebody';

INCLUDE '~beta/basiclib/v1.6/file';
INCLUDE '~beta/sysutils/v1.6/cstring';

-- lib: attributes --
GetModuleFileName: external
  (# 
     hModule: @integer; (*handle to module to find filename for*)
     lpFilename: @integer; (* pointer to buffer for module path*)
     nSize: @integer; (* size of buffer, in characters *)
     result: @integer;
  enter (hModule, lpFilename, nSize)
  do 'GetModuleFileNameA' -> callStd;
  exit result
  #);

MAX_PATH: (# exit 260 #);

getApplPath:
  (# thePath: ^Text;
     theFile: @file;
     tempName: @cString; 
     noOfChars: @integer;
  do 
     MAX_PATH+1 -> tempName.init;
     (if tempName.charPtr<>0 then
         (0,tempName.charPtr,MAX_PATH) 
           -> GetModuleFileName -> noOfChars;
         (if noOfChars>0 then
             tempName.get -> theFile.name;
             theFile.entry.path.head -> thePath[];
         if);
         tempName.free;
     if);
  exit thePath[]
  #);

-- iconButtonEnterIconName: doPart --
do (# fileName: ^text;
      bm: @bitMap;
   do getApplPath -> fileName[];
      '\\pixmaps\\' -> fileName.append;
      value[] -> fileName.append;
      '.bmp' -> fileName.append;
      fileName[] -> bm.readFromX11File;
      bm[] -> icon;
   #);
   
-- iconButtonExitIconName: doPart --
do 

