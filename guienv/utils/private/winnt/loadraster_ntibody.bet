ORIGIN '../loadrasterbody';
INCLUDE '~beta/sysutils/v1.5/envstring';
INCLUDE '~beta/basiclib/v1.5/file';
INCLUDE '../../rasteradds';

-- lib: attributes --
pathExists:
  (# value: @boolean;
     path: ^text;
     theEntry: @diskEntry;
  enter path[]
  do path[] -> theEntry.path;
     l: theEntry.exists
       (# 
          error::
            (#
            do leave l;
            #);
       #) -> value;
  exit value
  #);

-- GUIENVloadRaster: doPart --
do (# theBitMap: ^bitmapResource; 
      
      betaLibEnv: ^text;
      path: ^text;
      fileName: ^text;
      theBitMap2: ^bitMap; 
      didLoad: @boolean;
   do &bitmapResource[] -> theBitMap[] -> theRaster[];
      name[] -> theBitmap.loadByName -> didLoad;
      (if not didLoad then
          '$(BETALIB)' -> ExpandEnvVar
          (# defaultValue::< (# do 'c:\\beta' -> envvarvalue[]; #) #)
            -> betaLibEnv[];
          
          (if betaLibEnv[] <> NONE then
              
              
              
              betaLibEnv.copy -> path[];
              '\\bitmap\\interfacebuilders\\' -> path.append;
              path[] -> (name.copy).Prepend -> fileName[];          
              '.bmp' -> fileName.append; 
              (if fileName[] -> pathExists then
                  &bitmap[] -> theBitMap2[] -> theRaster[];
                  fileName[] -> theBitMap2.readFromX11File; 
               else
                  exception
                  (# 
                  do 'Not able to load ' -> msg.append;
                     filename[] -> msg.append;
                  #);
              if);
           else
              exception
              (# 
              do 'The BETAENV environment variable must be set' -> msg.append;
              #);
          if)
      if);
   #)
