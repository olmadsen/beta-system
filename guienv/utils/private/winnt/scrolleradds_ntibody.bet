ORIGIN '../scrolleraddsbody';
INCLUDE '../../../private/winnt/guienv_ntiprivate'
        '../../../private/winnt/fields_ntibody';
-- scrollerLib: Attributes --
windowItemIsVisible: booleanValue
  (# theWindowItem: ^windowItem;
  enter theWindowItem[]
  do false -> value;
  #);

-- GUIENVscrollerEnterTheScroll: DoPart --
do (-h,-v) -> scrollTo;
   
-- GUIENVscrollerExitTheScroll: DoPart --
do (interfaceObjectID, SB_HORZ)->GetScrollPos -> h;
   (interfaceObjectID, SB_VERT)->GetScrollPos -> v;
   
-- GUIENVscrollerScrollIntoView: DoPart --
do (# h, v, itemWidth, itemHeight: @integer;
      scrollX, scrollY: @integer;
      scrollerWidth, scrollerHeight: @integer;
      doScroll: @boolean;
   do false -> doScroll;
      theWindowItem.position -> (h,v);
      
      theWindowItem.size -> (itemWidth,itemHeight);
      theScroll -> (scrollX, scrollY);
      
      this(scroller).privateViewSize -> (scrollerWidth, scrollerHeight);
      (if v < scrollY then
          TRUE -> doScroll;
          v -> scrollY;
       else
          (if scrollY + scrollerHeight < v + itemHeight then
              v - scrollerHeight + itemHeight -> scrollY;
              TRUE -> doScroll;
          if);
      if);
      (if h < scrollX then
          h -> scrollX;
         TRUE -> doScroll;
       else
          (if scrollX + scrollerWidth < h + itemWidth then
              h - scrollerWidth + itemWidth -> scrollX;
              TRUE -> doScroll;
          if);
      if);
      (if doScroll then 
          (scrollX,scrollY) -> theScroll; 
      if);
   #);

-- GUIENVscrollerViewSize: DoPart --
do this(scroller).privateViewSize -> (width,height);

-- GUIENVscrollerNoTabbingToScrollbars: DoPart --
do

-- GUIENVscrollerContentsIsTarget: DoPart --
do 

