ORIGIN '../prompts';
INCLUDE '../../fields';
INCLUDE '../buttonadds';

(******** Prompt ********)

--promptPrivate: descriptor--
(# fatherpos, fathersize, mysize: @point;
   dialogopened: @boolean;
   popdown: @boolean;
   hSep: (# exit 14 #);
   vSep: (# exit 14 #);
   recalc:
     (# x,y,h,v,t: @integer; fr: @rectangle; p: @point;
     do cancelButton.size->okButton.size;
        (if this(prompt)##<=promptForBoolean## then
            cancelButton.size->notOkButton.size;
            notOkButton.size->p; p.h+hSep+h->h;
        if);
        okButton.size->p; p.h+hSep+h->h;
        cancelButton.size->p; p.h+hSep+h->h;
        theMessage.size->p; (hSep+p.h+hSep,hSep+h)->max->h;
        
        theMessage.size->p; p.v+vSep+v->v;
        (if this(prompt)##<=promptForText## then
            theMessage.size->p;
            ((*p.*)h-2*hSep,(p.v div linesInMessage)*linesInText+14)->theText.size;
            theText.size->p; p.v+vSep+v->v;
        if);
        okButton.size->p; p.v+vSep+v->v;
        v+vSep->v;
            
        (h,v)->mysize->THIS(prompt).size;
        
        hSep->x; vSep->y;
        (x,y)->theMessage.position;
        theMessage.size->p; p.v+vSep+y->y;
        (if this(prompt)##<=promptForText## then
            (x,y)->theText.position;
            theText.size->p; p.v+vSep+y->y;
        if);
        
        cancelButton.size->p; p.h->t;
        h-t-hSep->x;
        (x,y)->cancelButton.position;
        
        okButton.size->p; p.h->t;
        x-t-hSep->x;
        (x,y)->okButton.position;
        
        (if this(prompt)##<=promptForBoolean## then
            notOkButton.size->p; p.h->t;
            x-t-hSep->x;
            (x,y)->notOkButton.position;
        if);
     #);
   rescale:
     (# newsize: @point; hpct, vpct: @real; h,v: @integer
     do size->newsize;
        newsize.h/mysize.h->hpct;
        newsize.v/mysize.v->vpct;
        newsize->mysize;
        cancelButton.position->(h,v); (h*hpct,v*vpct)->cancelButton.position;
        cancelButton.size->(h,v); (h*hpct,v)->cancelButton.size;
        okButton.position->(h,v); (h*hpct,v*vpct)->okButton.position;
        okButton.size->(h,v); (h*hpct,v)->okButton.size;
        (if this(prompt)##<=promptForBoolean## then
            notOkButton.position->(h,v);(h*hpct,v*vpct)->notOkButton.position;
            notOkButton.size->(h,v); (h*hpct,v)->notOkButton.size;
        if);
        theMessage.position->(h,v); (h*hpct,v*vpct)->theMessage.position;
        theMessage.size->(h,v); (h*hpct,v)->theMessage.size;
        (if this(prompt)##<=promptForText## then
            theText.position->(h,v); (h*hpct,v*vpct)->theText.position;
            theText.size->(h,v); (h*hpct,v)->theText.size;
        if);
     #);
   linesInMessage,linesInText: @integer;
   theMessage: @staticText
     (# eventhandler::<
          (# onLabelChanged::<
               (# w,h: @integer;
               do (labelWidth,labelHeight*linesinMessage)->size;
               #);
             onFatherFrameChanged::
               (# do rescale #)
          #);
        open::
          (# do (hSep,vSep)->position #);
     #);
   promptButton: pushButton
     (# eventhandler::<
          (# onLabelChanged::<
               (# w,h: @integer
               do (labelWidth+14,labelHeight+14)->size;
               #)
          #)
     #);
   notOkButton: @promptButton
     (# eventHandler:: 
          (# onMouseUp:: 
               (# 
               do false->THIS(prompt).private.popdown;
                  notok;
                  (if THIS(prompt).private.popdown then THIS(Prompt).hide if)
               #)
          #);
        open::
          (#
          do 'NOT OK'->label;
             (110,80)->position;
          #);
     #);
   okButton: @promptButton
     (# eventHandler:: 
          (# onMouseUp:: 
               (# 
               do false->THIS(prompt).private.popdown;
                  ok;
                  (if THIS(prompt).private.popdown then THIS(Prompt).hide if)
               #)
          #);
        open::
          (#
          do 'OK'->label;
             (170,80)->position;
          #);
     #);
   cancelButton: @promptButton
     (* only valid for promptForBoolean *)
     (# eventHandler:: 
          (# onMouseUp:: (# do cancel; THIS(Prompt).hide #)#);
        open::
          (#
          do 'Cancel'->label;
             (230,80)->position;
          #);
     #);
   DialogText: textField
     (* only valid for promptForText *)
     (# eventhandler::
          (# onBeforeChange:: 
               (# txt: ^text
               do (if length=1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl
                       // ascii.cr then 
                          THIS(Prompt).private.okButton.theEventhandler.onMouseup;
                          false->allow;
                      if);
                  if)
               #);
             onKeyDown::
               (# 
               do (if ch=ascii.esc then 
                      THIS(Prompt).private.cancelButton.theEventhandler.onMouseup;
                  if)
               #);
          #);
     #);
   theTextSize: @point;
   theText: @DialogText
     (* only valid for promptForText *)
     (# open::
          (# do (hSep,40)->position #);
     #);
#)

--promptPopup: dopart--
do (if not private.dialogopened then
       open;
   if);
   titleText[]->title;
   1->private.linesInMessage;
   msgText.scanAll
   (#
   do (if ch=ascii.newline then
          private.linesInMessage+1->private.linesInMessage;
      if)
   #);
   msgText[]->private.theMessage.label;
   INNER;
   private.recalc;
   (if father[] <> NONE then
       father.size->private.fathersize;
       size->private.mysize;
       father.position->private.fatherpos;
       (private.fatherpos.v + (private.fathersize.v-private.mysize.v) div 2,
       private.fatherpos.h + (private.fathersize.h-private.mysize.h) div 2) 
         ->position;
    else 
       mouse.globalPosition->position;
   if);
   showmodal;
   
--promptOpen: dopart--
do (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       this(prompt).contents->private.theMessage.open;
       private.okButton.open;
       private.cancelButton.open;
       (*private.okButton[]->defaultButton;*)
       INNER;
   if);
   
(********* PromptForText **********)
   
--promptForTextOpen: dopart--
do private.theText.open;
   linesInUsertext->private.linesInText;
   INNER;
   
--promptForTextOk: dopart--
do private.theText.contents->usertext[];
   (if textprivate.validatevalue then 
       INNER; 
       true->THIS(prompt).private.popdown
    else
       system.beep;
       (if textprivate.hilite then private.theText.all 
             ->private.theText.selection
       if);
   if)
   
--promptForTextValidate: dopart--
do true->hiliteOnError;
   usertext.length>0->value; 
   INNER;
   hiliteOnError->textprivate.hilite;
   
--promptForTextPopup: dopart--
do private.theText.all->private.theText.selection;
   private.theText.delete;
   defaultText[]->private.theText.insert;
   (* private.theText.all->private.theText.selection;*)
   INNER;
   
--promptForTextPrivate: descriptor--
(# hilite: @boolean;
   validatevalue: @validate;
#)


(********* PromptForInteger **********)

--promptForIntegerValidate: dopart--
do usertext.reset;
   usertext.getInt(# syntaxerror::
                       (# do false->THIS(validate).value; true->continue #)
                  #)->userinteger;
   INNER;
   
(******* PromptForBoolean ************)
   
--promptForBooleanOk: dopart--
do true->THIS(prompt).private.popdown;
   INNER
   
--promptForBooleanNotOk: dopart--
do true->THIS(prompt).private.popdown;
   INNER
   
--promptForBooleanOpen: dopart--
do private.notokButton.open;
   'No'->private.notokButton.label;
   'Yes'->private.okButton.label;
   INNER;

