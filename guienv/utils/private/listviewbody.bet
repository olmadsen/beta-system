ORIGIN '../listview';
-- listItemLib: attributes --
assertInited:
  (# failure:< exception
       (# 
       do 'An operation is called on a listItem before init is called' 
            -> msg.putText;
          inner;
       #);
  do (if private.position//0 then
         failure;
     if);
  #);

-- GUIenvListViewListItemSetName: doPart --
do assertInited;
   (private.position, theName[]) -> setText;

-- GUIenvListViewListItemGetName: doPart --
do assertInited;
   private.position -> getText -> theName[];

-- GUIenvListViewListItemGetPosition: doPart --
do assertInited;
   private.position -> value;

-- GUIenvListViewListItemInit: doPart --
do (if beforeItem[]//none then
       1 -> this(listView).append;
       numberOfItems -> private.position;
       this(listItem)[] -> listItems.append;
    else
       (# theCell: ^listItems.theCellType;
       do beforeItem[] -> listItems.at -> theCell[];
          (theCell.elm.position, 1) -> this(listView).insert;
          theCell.elm.position -> private.position;
          theCell[] -> listItems.iterateFrom
          (# 
          do current.elm.private.position + 1 -> current.elm.private.position;
          #);
          (this(listItem)[], theCell[]) -> listItems.insertBefore;
       #);
   if);
   inner;

-- GUIenvListViewListItemDelete: doPart --
do assertInited;
   (private.position, 1) -> this(listView).delete;
   0 -> private.position;

-- GUIenvListViewListItemSelect: doPart --
do assertInited;
   (private.position, false) -> this(listView).selection.select;
   onSelect;

-- GUIenvListViewListItemDeSelect: doPart --
do assertInited;
   private.position -> this(listView).selection.deSelect;

-- GUIenvListViewListItemPrivate: descriptor --
(# 
   position: @integer;
#)

-- GUIenvListViewOpen: doPart --
do listItems.init;
   inner;

-- GUIenvListViewClose: doPart --
do inner;
   listItems.clear;

-- GUIenvListViewPrivate: descriptor --
(#
#)

-- GUIenvListViewEventHandlerOnMouseUp: doPart --
do (# selectedItem: @integer;
   do selection.first -> selectedItem;
      (if selectedItem<>0 then
          listItems.iterate
          (# 
          do (if current.elm.position//selectedItem then
                 doubleClick -> current.elm.onSelect;
             if);
          #);
      if)
   #);
     
