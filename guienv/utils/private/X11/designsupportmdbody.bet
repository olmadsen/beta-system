ORIGIN '../designsupportbody';
BUILD default '$$/getdesigndisplay.o' 'external/getdesigndisplay.c'
      '$CC -D$$ -I$MOTIFINC -c -o $0 $1';
INCLUDE '../../../private/X11/guienv_unixbody';

-- GUIENVdesignSetUp: doPart --
do (# initAppName:
        (* initializes private.appName to the name of the executed file
         * with the first letter in uppercase
         *)
        (# lastSlash: @integer;
        do 1 -> arguments -> private.appName[];
           '/' -> private.appName.findAll(# do inx->lastSlash #);
           (if lastSlash<>0 then (1,lastSlash)->private.appName.delete if);
           (1->private.appName.inxGet->ascii.upcase, 1) -> private.appName.inxPut;
           trace
           (#
           do 'initAppName: ' -> putText;
              private.appName[] -> putLine;
           #);
        #);
      initAppClass:
        (* initializes private.appClass to the name of the executed file with
         * the first letter in uppercase. If the first letter i X the second 
         * letter is also converted to uppercase
         *)
        (# lastSlash: @integer;
        do 1 -> arguments -> private.appClass[];
           '/' -> private.appClass.findAll(# do inx->lastSlash #);
           (if lastSlash<>0 then (1,lastSlash)->private.appClass.delete if);
           (1->private.appClass.inxGet->ascii.upcase, 1) -> private.appClass.inxPut;
           (if ((1->private.appClass.inxGet)='X') and (private.appClass.length>1) then 
               (2->private.appClass.inxGet->ascii.upcase, 2) -> private.appClass.inxPut;
           if);
           trace
           (#
           do 'initAppClass: ' -> putText;
              private.appClass[] -> putLine;
           #);
        #);
      getdesigndisplay: external
        (# display: @integer
        exit display
        #);

      getdesignappcon: external
        (# appcon: @integer;
        exit appcon
        #);
   do getdesignappcon -> private.appCon;
      getdesigndisplay -> private.display;
      initAppName;
      initAppClass;
      init;
      (&errorHandler[]).install;
      true -> private.dontRunLoop;
      true -> setUpDone;
   #);
