ORIGIN '../scrolleraddsbody';
INCLUDE '../../../private/X11/guienv_unixprivate'
        '../../../private/X11/fields_unixbody';
-- scrollerLib: Attributes --
XmScrollVisible: external
  (# scrollerID,widgetID: @integer; left,top: @integer; 
  enter (scrollerID,widgetID,left,top)
  #);
setScrollbarResource:
  (#
     vertical: @boolean;
     name: @integer;
     value: @integer;
     ID: @integer;
     args: @argList;
     
  enter (vertical,name,value)
  do
     (if vertical then
         XmNverticalScrollbar->getIntegerResource->ID; 
      else
         XmNhorizontalScrollbar->getIntegerResource->ID; 
     if);
     (1,name,value)->args.set;
     (ID,args[],1)->XtSetValues;
     
  #);
getScrollbarResource:
  (#
     vertical: @boolean;
     name: @integer;
     value: @integer;
     ID: @integer;
     args: @argList;
     
  enter (vertical,name)
  do
     (if vertical then
         XmNverticalScrollbar->getIntegerResource->ID; 
      else
         XmNhorizontalScrollbar->getIntegerResource->ID; 
     if);
     (1,name,@@ value)->args.set;
     (ID,args[],1)->XtGetValues;
     
  exit value
  #);
setScrollbarValue:
  (#
     vertical: @boolean;
     ID: @integer;
     value: @integer;
     itsValue,itsSliderSize,itsIncrement,itsPageIncrement: @integer;
     
  enter (vertical,value)
  do
     (if vertical then
         XmNverticalScrollbar->getIntegerResource->ID; 
      else
         XmNhorizontalScrollbar->getIntegerResource->ID; 
     if);
     (ID,@@ itsValue,@@ itsSliderSize,@@ itsIncrement,@@ itsPageIncrement)
       ->XmScrollBarGetValues;
     (ID,value,itsSliderSize,itsIncrement,itsPageIncrement,true)
       ->XmScrollBarSetValues;
     
  #);
  

-- GUIENVscrollerEnterTheScroll: DoPart --
do (true,v)->setScrollbarValue; (false,h)->setScrollbarValue;   

-- GUIENVscrollerExitTheScroll: DoPart --
do
   (true,XmNvalue)->getScrollbarResource->v;
   (false,XmNvalue)->getScrollbarResource->h;
     

-- GUIENVscrollerScrollIntoView: DoPart --
do (widgetID,theWindowItem.widgetID,5,5)->XmScrollVisible;   

-- GUIENVscrollerViewSize: DoPart --
do
     (#
        ID: @integer;
        args: @argList;
        itsWidth,itsHeight: @shortInt;
        
     do
        XmNclipWindow->getIntegerResource->ID;
        (1,XtNwidth,@@ itsWidth)->args.set;
        (2,XtNheight,@@ itsHeight)->args.set;
        (ID,args[],2)->XtGetValues;
        itsWidth->width;
        itsHeight->height;
        
     #);
     

-- GUIENVscrollerNoTabbingToScrollbars: DoPart --
do
   (true,XmNtraversalOn,0)->setScrollbarResource;
   (false,XmNtraversalOn,0)->setScrollbarResource;
     

-- GUIENVscrollerContentsIsTarget: DoPart --
do value->hiliteContents;   

