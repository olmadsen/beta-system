ORIGIN '../tooltipBody';
INCLUDE '~beta/X11/Xt/xtlib.bet';
INCLUDE '~beta/guienv/private/X11/guienv_unixprivate';
BUILD default '$$/LiteClue.o' 'external/LiteClue.c' 
              '$CC -g -c -o $0 $1';

-- lib: attributes --
putintline:
  (# i:@integer;
  enter i
  do i->putint;
     newline;
  #);

-- GUIENVtooltipSetDelayTime: doPart --
do (# r:@arglist
   do
      (if durationType
       // INITIAL then
          (1,tooltippriv.XcgNwaitperiod,delay)->r.set;
          (tooltippriv.toolTipWidget,r[],1)->XtSetValues;
       // RESHOW then
          (1,tooltippriv.XcgNcancelWaitPeriod,delay)->r.set;
          (tooltippriv.tooltipWidget,r[],1)->XtSetValues;
       else
          'Only INITIAL and RESHOW supported on X!'->putline;
      if);
   #);

-- GUIENVtoolTipControlAdd: doPart --
do    
   (if thetool##=rectTool## then
       'Rectangle tools not supported, only whole windowitems!'->putline;
    else
       (tooltippriv.toolTipWidget,theTool.theWindowItem.widgetID,
       theTool.theTip,0,0)->tooltippriv.XcgLiteClueAddWidget;
   if);
   
-- GUIENVtoolTipControlDelete: doPart --
do 
   
-- GUIENVtooltipOpen: doPart --
do tooltippriv.initialize;
   
-- GUIENVtooltipClose: doPart --
do 
   
-- tooltipPrivAttr: descriptor --
(# YAllocColor: external
     (# display: @integer;
        cmap: @integer;
        c: ^XColor;
        result: @integer;
     enter (display, cmap, c[])
     exit result
     #);

   XcgNwaitperiod: integerobject
     (# do 'waitPeriod'->charReptoCString->value #);
   XcgNcancelWaitPeriod: integerobject
     (# do 'cancelWaitPeriod'->charReptoCString->value #);
   initialize:
     (# r:@arglist;
        t:@text;
        widget:@integer;
        yellow:@integer;
        cmap,screen,disp:@integer;
        thecolor:@Xcolor;
     enter (widget,t)
     do
        (* 'Initializing'->putline; *)
        (* this(window).widgetID->putintline;  *)
        ('popup_shell',xcgLiteClueWidgetClass,this(window).widgetID,r[],0)
         ->XtCreatePopupShell->ToolTipWidget; 
        (* 'Shell Created'->putline;  *)
        this(window).displayID->disp;
        disp->XDefaultScreen->screen;
        (disp,screen)->XDefaultColorMap->cmap;
        0xffff->thecolor.red;
        0xffff->thecolor.green;
        0x9999->thecolor.blue;
        (disp,cmap,thecolor[])->YAllocColor->yellow;
        (if yellow = 0 then 
            'Could not allocate color for tooltip background'->putline;
         else
            theColor.pixel->yellow;
        if);
        (1,XtNbackground,yellow)->r.set;
        (toolTipWidget,r[],1)->XtSetValues;
         
     #);
        
   (* Function supplied by widget. Add another widget for watching events *)
   XcgLiteClueAddWidget:external
     (# 
        widget:@integer;
        watch:@integer;
        print:[1]@char;
        s,o:@integer;
     enter(widget,watch,print,s,o)
     do callC
     #);
   
   xcgLiteClueWidgetClass:external
     (# val:@integer;
     do 'getLiteClueWidgetClass'->callC
     exit val
     #);
          
   toolTipWidget:@integer;
   
#)


--GUIENVaddtooltip:doPart--
do
   &tool[]->thetool[];
   t[]->thetool.thetip[];
   this(windowitem)[]->thetool.thewindowitem[];
   
   (if this(window).private.theToolTipControl[] // NONE then (* This is first time *)
       &tooltipcontrol[]->tooltipc[];
       tooltipc.open;
       thetool[]->tooltipc.add;
       tooltipc[]->this(window).private.theToolTipControl[];
    else
       this(window).private.theToolTipControl[]->tooltipc[];
       thetool[]->tooltipc.add;
   if);
   

--GUIENVaddtooltipExt:doPart--
do
   &theWindow.tool[]->thetool[];
   t[]->thetool.thetip[];
   this(windowitem)[]->thetool.thewindowitem[];
   (*'addtooltipext: '->putText;theWindow[]->getPatternName->putLine;
   t[]->putLine;*)
   (if theWindow.private.theToolTipControl[] // NONE then (* This is first time *)
       &theWindow.tooltipcontrol[]->tooltipc[];
       tooltipc.open;
       thetool[]->tooltipc.add;
       tooltipc[]->theWindow.private.theToolTipControl[];
    else
       theWindow.private.theToolTipControl[]->tooltipc[];
       thetool[]->tooltipc.add;
   if);
    
