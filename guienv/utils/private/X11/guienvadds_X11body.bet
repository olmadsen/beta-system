ORIGIN '../guienvaddsbody';
LIB_ITEM 'guienvadds';
INCLUDE '../../../private/X11/guienv_unixbody';

-- windowItemlib: attributes --

XtQueryGeometry: external
  (# 
     w: @integer;
     intended: ^XtWidgetGeometry;
     preffered: ^XtWidgetGeometry;
     result: @integer;
  enter (w, intended[], preffered[])
  exit result
  #);

XtWidgetGeometry: data
  (# request_mode: @integer;
     x: @shortInt;
     y: @shortInt;
     width: @shortInt;
     height: @shortInt;
     sibling: @integer;
     stackmode: @integer;
  #);

XMask: (# exit 1 #);
YMask: (# exit 2 #);
WidthMask: (# exit 4 #);
HeighMask: (# exit 8 #);
StackingMask: (# exit 16 #);



-- windowItemBringToFront: doPart --
do (# ID: @integer;
      displayID, windowID: @integer;
   do widgetID -> ID;
      (if ID<>0 then
          ID -> XtWindow -> windowID;
          ID -> XtDisplay -> displayID;
          (if windowID<>0 then
              (displayID, windowID) -> XRaiseWindow;
          if);
      if);
      this(windowItem)[] 
        -> father.private.children.at 
        -> father.private.children.delete;
      this(windowItem)[]
        -> father.private.children.prepend;
   #);
   
-- windowItemBringToBack: doPart --
do (# ID: @integer;
      displayID, windowID: @integer;
   do widgetID -> ID;
      (if ID<>0 then
          ID -> XtWindow -> windowID;
          ID -> XtDisplay -> displayID;
          (if windowID<>0 then
              (displayID, windowID) -> XLowerWindow;
          if);
      if);
      this(windowItem)[] 
        -> father.private.children.at 
        -> father.private.children.delete;
      this(windowItem)[]
        -> father.private.children.append;
   #)
   
-- windowItemPreferredSize: doPart --
do (# ID: @integer;
     
      x: @COM
        (# preferred: @XtWidgetGeometry;
           intended: @XtWidgetGeometry;
        #);
      result: @integer;
   do widgetID -> ID;
      (if ID<>0 then
          (if suggest//true then
              suggestedWidth -> x.intended.width;
              suggestedHeight -> x.intended.height;
              WidthMask + HeighMask -> x.intended.request_mode;
          if);
          (ID, x.intended[], x.preferred[]) -> XtQueryGeometry -> result;
          x.preferred.width -> preferredWidth;
          x.preferred.height -> preferredHeight;
      if);
   #);

-- windowItemDrawShadows: doPart --
do (r, type) -> drawBorder;
   
-- lib: attributes --

unsignedShortToSignedShort:
  (# value: @integer;
  enter value
  do  (if value > 32768 then
           value - 65536 -> value;
      if);
  exit value
  #);

-- GUIENVwindowItemOrigin: doPart --
do (# ID: @integer;
      x_root, y_root: @shortRef;
   do widgetID -> ID;
      (ID, 0, 0, x_root[], y_root[]) -> XtTranslateCoords;
      x_root ->  x;
      y_root -> y;
      x -> unsignedShortToSignedShort -> x;
      y -> unsignedShortToSignedShort -> y;
   #);
-- GUIenvWindowItemDelegateMouseEvents: doPart --
do value -> private.delegate;
   
-- windowLaunchFile: descriptor --
(# 
   
#)

-- GUIENVsetWindowIcon: doPart --
do
   
-- GUIENVsetApplIcon: doPart --
do
   
--GUIENVsetWindowIconPixmap:dopart--
do (XtNiconPixmap, pm.private.pixmapID) -> this(window).setIntegerresource;
   
-- GUIENVloadWindowIcon: DoPart --
do
     (# iconPixmap: @pixmap; 
     do
        filename[]->iconPixmap.read;
        (XtNiconPixmap,iconPixmap.private.pixmapID) -> THIS(window).setIntegerresource; 
     #);
     
-- GUIENVloadMouseCursor: DoPart --
do
   'Error: loadMouseCursor is Windows-only. Use setMouseCursor instead.'->screen.putline;

-- GUIENVsetMouseCursor: DoPart --
do
   (* Create a new cursor and assign it to be the current cursor 
    * Beware of the parentheses :) *)
   (THIS(guienv).private.display,
   windowID,
   (THIS(guienv).private.display, symbolConstant) -> XCreateFontCursor)
     -> XDefineCursor;
