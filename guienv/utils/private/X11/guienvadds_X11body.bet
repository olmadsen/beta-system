ORIGIN '../guienvaddsbody';
INCLUDE '../../../private/X11/guienv_unixprivate';

-- windowItemlib: attributes --

XtQueryGeometry: external
  (# 
     w: @integer;
     intended: ^XtWidgetGeometry;
     preffered: ^XtWidgetGeometry;
     result: @integer;
  enter (w, intended[], preffered[])
  exit result
  #);

XtWidgetGeometry: data
  (# request_mode: @integer;
     x: @shortInt;
     y: @shortInt;
     width: @shortInt;
     height: @shortInt;
     sibling: @integer;
     stackmode: @integer;
  #);

XMask: (# exit 1 #);
YMask: (# exit 2 #);
WidthMask: (# exit 4 #);
HeighMask: (# exit 8 #);
StackingMask: (# exit 16 #);



-- windowItemBringToFront: doPart --
do (# ID: @integer;
      displayID, windowID: @integer;
   do widgetID -> ID;
      (if ID<>0 then
          ID -> XtWindow -> windowID;
          ID -> XtDisplay -> displayID;
          (if windowID<>0 then
              (displayID, windowID) -> XRaiseWindow;
          if);
      if);
   #);

-- windowItemPreferredSize: doPart --
do (# ID: @integer;
      preferred: @XtWidgetGeometry;
      intended: @XtWidgetGeometry;
      result: @integer;
   do widgetID -> ID;
      (if ID<>0 then
          (if suggest//true then
              suggestedWidth -> intended.width;
              suggestedHeight -> intended.height;
              WidthMask + HeighMask -> intended.request_mode;
          if);
          (ID, intended[], preferred[]) -> XtQueryGeometry -> result;
          preferred.width -> preferredWidth;
          preferred.height -> preferredHeight;
      if);
   #);

-- windowItemDrawShadows: doPart --
do (# GetTopShadowGC: external
        (* The widget must be a manager widget or descendant thereof *)
        (# w: @integer; (* The Manager widget *)
           gc: @integer; (* The gc that should be used for the top shadow *)
        enter w
        exit gc
        #);
      GetBottomShadowGC: external
        (* The widget must be a manager widget or descendant thereof *)
        (# w: @integer; (* The Manager widget *)
           gc: @integer; (* The gc that should be used for the bottom shadow *)
        enter w
        exit gc
        #);
      _XmDrawShadows: external
        (# display,drawable,topgc,bottomgc,x,y,width,height,thickness,type: @integer;
        enter (display,drawable,topgc,bottomgc,x,y,width,height,thickness,type)
        #);
      drawable: @integer;
      topGC, bottomGC: @integer;
      displayID: @integer;
      x, y, width, height: @integer;
   do windowID -> drawable;
      display -> displayID;
      father.widgetID -> GetTopShadowGC -> topGC;
      father.widgetID -> GetBottomShadowGC -> bottomGC;
      r.topLeft -> (x, y);
      r.size -> (width, height);
      (displayID,drawable,topGC,bottomGC,x, y, width, height,2,type) -> _XmDrawShadows;
   #)
   
-- GUIENVwindowItemOrigin: doPart --
do (# ID: @integer;
      x_root, y_root: @shortRef;
   do widgetID -> ID;
      (ID, 0, 0, x_root[], y_root[]) -> XtTranslateCoords;
      x_root ->  x;
      y_root -> y;
   #);
-- GUIenvWindowItemDelegateMouseEvents: doPart --
do value -> private.delegate;
