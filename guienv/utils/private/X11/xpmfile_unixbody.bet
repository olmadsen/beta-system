ORIGIN '../../raster_xpmfile';
INCLUDE '../../../private/X11/guienv_unixbody';
LINKOPT default '-lXpm';

-- LIB: attributes --
XpmAttributes:
  (# 
     valuemask: @integer; (* Specifies which attributes are defined *)
     visual:    @integer; (* Specifies the visual to use *)
     colormap:  @integer; (* Specifies the colormap to use *)
     depth:     @integer; (* Specifies the depth *)
     width:     @integer; (* Returns the width of the created pixmap *)
     height:    @integer; (* Returns the height of the created pixmap *)
     i1, i2, i3, i4, i5, i6, i7, i8, i9, i10,
     i11, i12, i13, i14, i15, i16, i17, i18, i19, i20,
     i21, i22, i23, i24, i25, i26: @integer;  (* The rest - I don't need it *)
  #);
XpmSuccess: 
  (# exit 0 #);
XpmDepth:
  (# exit 1 %sll 2 #);
XpmReadFileToPixmap: external
  (# display, drawable, pixmap_return, shapemask_return, attributes: @integer;
     filename: [0]@char;
     status: @integer;
  enter (display, drawable, filename, pixmap_return, shapemask_return, attributes)
  exit status
  #);

-- GUIENVbitmapReadFromXPMfile: descriptor --
(# att: @XpmAttributes;
   dpy, root, depth: @integer;
   pmap: @integer;
   status: @integer;
   mask: @integer;
do initDeviceInfo;
   (if filename[]=NONE then (failuretrace, 'bitmapReadFromXPMfile: filename is NONE')->stop; if);
   rasterprivatepart.theDeviceInfo.display -> dpy;
   rasterprivatepart.theDeviceInfo.defaultdepth -> depth;
   XpmDepth -> att.valuemask;
   dpy -> XDefaultRootWindow -> root;
   (dpy,root,filename,@@pmap, @@mask,@@att.valuemask) -> XpmReadFileToPixmap -> status;
   (if status<>XpmSuccess then
       'Failed to open XPM file '->puttext; filename[]->putline; 
    else
       pmap -> rasterprivatepart.xpixmap;
       att.width -> rasterprivatepart.width;
       att.height -> rasterprivatepart.height;
       mask -> rasterprivatepart.mask;
   if);
#)
