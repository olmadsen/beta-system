ORIGIN '../loadrasterbody';
INCLUDE '~beta/unixlib/v1.6/unixinterface';
INCLUDE '~beta/sysutils/v1.6/cstring';
INCLUDE '~beta/basiclib/v1.6/file';
-- lib: attributes --

pathExists:
  (# value: @boolean;
     path: ^text;
     theEntry: @diskEntry;
  enter path[]
  do path[] -> theEntry.path;
     l: theEntry.exists
     (# 
        error::
          (#
          do leave l;
          #);
     #) -> value;
  exit value
  #);
-- GUIENVloadRaster: doPart --
do (# betaLibEnv: @cString;
      path: ^text;
      fileName: ^text;
      theBitMap: ^bitMap;
   do 'BETALIB' -> getenv -> betaLibEnv;
      (if betaLibEnv <> 0 then
          betaLibEnv.get -> path[];
          '/bitmap/interfacebuilders/' -> path.append;
          path[] -> (name.copy).Prepend -> fileName[];
          '.xbm' -> fileName.append;
          (if fileName[] -> pathExists then
              &bitmap[] -> theBitMap[] -> theRaster[];
              fileName[] -> theBitMap.readFromX11File;
           else
              exception
              (# 
              do 'Not able to load ' -> msg.append;
                 filename[] -> msg.append;
              #);
          if);
       else
          exception
          (# 
          do 'The BETAENV environment variable must be set' -> msg.append;
          #);
      if)
   #)
