ORIGIN '../timerbody';
INCLUDE '../../../private/X11/guienv_unixprivate';

-- timerLib: attributes --
unInitialized: (# exit 0 #);
active: (# exit 1 #);
sleeping: (# exit 2 #);

-- GUIenvTimerActivate: doPart --
do interval -> private.interval;
   (if private.status
    //active then 
       (* Nothing *)
    //unInitialized then
       private.init;
       active -> private.status;
       private.install;
    //sleeping then
       active -> private.status;
       private.install;
   if);
   

-- GUIenvTimerPause: doPart --
do (if private.status = active then
       private.delete;
       sleeping -> private.status;
   if);

-- GUIenvTimerPrivate: descriptor --
(# theAction: @action;
   status: @integer;
   interval: @integer;
   appcontext: @integer;
   callbackPtr: @integer;
   timerID: @integer;
   callback: external
     (# data, id: @integer;
     enter (data, id)
     do cExternalEntry;
        0 -> timerID;
        (if status
         //active then
            install;
            theAction;
         //sleeping then
            (* Nothing *)
        if);
     #);
   install: @
     (# XtAppAddTimeOut: external
          (# app_context: @integer;
             interval: @integer;
             proc: @integer;
             data: @integer;
             ID: @integer;
          enter (app_context, interval, proc, data)
          exit ID
          #);
     do (appcontext, interval, callbackPtr, 0) -> XtAppAddTimeOut -> timerID;
     #);
   delete: @
     (# XtRemoveTimeOut: external
          (# timerID: @integer;
          enter timerID
          #);
     do timerID -> XtRemoveTimeOut;
        0 -> timerID;
     #);
   init:
     (# 
     do this(GUIenv).private.appCon -> appcontext;
        callback## -> makeCBF -> callbackPtr;
     #);
#)

