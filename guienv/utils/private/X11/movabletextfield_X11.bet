ORIGIN '../movabletextfieldbody';
INCLUDE '../../../private/X11/guienv_unixprivate';

-- movableTextFieldInit: doPart --
do (# ID: @integer;
      args: @argList;
   do WidgetID -> ID;
      (if ID = 0 then
          exception
          (# 
          do 'MovableTextField must be used in a widget based windowitem' -> msg.append;
          #);
       else
          (1, XmNmarginHeight, 1) -> args.set;
          (2, XmNmarginWidth, 1) -> args.set;
          (3, XmNhighlightThickness, 0) -> args.set;
          (ID, 'MovableTextField', args[], 3) -> XmCreateTextField -> private.textfieldID;
          (private.textfieldID, 0) -> XtSetMappedWhenManaged;
          private.textfieldID -> XtManageChild;
      if);
   #);

-- movableTextFieldActivate: doPart --
do (# args: @argList;
      SetAnswer: external
        (# value: @boolean;
        enter value
        #);
      x, y, width, height: @integer;
      cStr: @cString;
      windowID, displayID: @integer;
   do (str[] -> style.widthOfText) + 2*3 -> width;
      style.lineHeight + 2*3 -> height;
      p.h - 3 -> x;
      p.v - style.ascent - 3 -> y;
      str[] -> cStr.set;
      (1, XtNx, x) -> args.set;
      (2, XtNy, y) -> args.set;
      (3, XtNwidth, width) -> args.set;
      (4, XtNheight, height) -> args.set;
      (5, XmNvalue, cStr) -> args.set;
      true -> SetAnswer;
      (private.textfieldID, args[], 5) -> XtSetValues;
      false -> SetAnswer;
      private.textfieldID -> XtWindow -> windowID;
      private.textfieldID -> XtDisplay -> displayID;
      (displayID, windowID) -> XRaiseWindow;
      (private.textfieldID, 0, str.length, 0) -> XmTextFieldSetSelection;
      private.textfieldID -> XtMapWidget;
      (private.textfieldID, XmTRAVERSE_CURRENT) ->  XmProcessTraversal;
   #);

-- movableTextFieldDeactivate: doPart --
do (if verify then
       private.textfieldID -> XtUnmapWidget;
   if);
   
-- MovableTextFieldTheText: doPart --
do (# args: @argList;
      cstr: @cString;
   do (1, XmNvalue, 0) -> args.set;
      (private.textfieldID, args[], 1) -> XtGetValues;
      1 -> args.get -> cStr;
      cStr.get -> str[];
   #);

-- movableTextFieldPrivate: descriptor --
(# textfieldID: @integer;
#)

