ORIGIN '../../textfieldadds';

BUILD default '$$/textfieldadds.o' 'external/textfieldadds.c' '$CC -c -D$$ -I$MOTIFINC -o $0 $1';


INCLUDE '../../../private/X11/guienvattributes';

-- lib: attributes --
(* extern char * _XmStringSourceGetValue( 
 *                         XmTextSource source,
 * #if NeedWidePrototypes
 * int want_wchar) ;
 *)

XmTextDisableRedisplay: external
  (# id: @integer;
  enter id
  #);
XmTextEnableRedisplay: external
  (# id: @integer;
  enter id
  #);

_XmStringSourceGetValue: external
  (# source: @integer;
     want_wchar: @integer;
     value: @integer;
  enter (source, want_wchar)
  exit value
  #);

(* CharPosToRowCol (char *value, long pos, long *row, long *col) *)
CharPosToRowCol: external
  (# value: @integer;
     pos: @integer;
     rowP, colP: @integer;
  enter (value, pos, rowP, colP)
  #);

(* CharRowColToPos (char *value, long row, long col, long *pos) *)
CharRowColToPos: external
  (# value, row, col, posP, lgth: @integer;
  enter (value, lgth, row, col, posP)
  #);
-- GUIENVTextFieldPositionToRowCol: DescriptorForm --
assertOpen
(# location:: (# do 'posToRowCol' -> t[]; #);
   value: @integer;
do (if pos > length then
       indexError;
    else
       XmNvalue -> getPointerResource -> value;
       (if value <> 0 then
           (value, pos, @@row, @@col) -> CharPosToRowCol;
           value -> XtFree;
           row + 1 -> row;
           col + 1 -> col;
        else
           '"XmNvalue -> getPointerResource" failed' -> putLine;
           1 -> row;
           1 -> col;
       if);
   if);
#)  

-- GUIENVTextFieldRowColToPosition: DescriptorForm --
assertOpen
(#  location:: (# do 'rowColToPos' -> t[]; #);
   value: @integer;
   lgth: @integer;
do XmNvalue -> getPointerResource -> value;
   (if value <> 0 then
       length -> lgth;
       (value, lgth, row - 1, col - 1, @@pos) -> CharRowColToPos;
       value -> XtFree;
       (if pos = -1 then
           indexError;
       if);
    else
       '"XmNvalue -> getPointerResource" failed' -> putLine;
       0 -> pos;
   if);
#)  

-- GUIENVtextFieldAutomaticScrolling: doPart --
do assertOpen
   (# location:: (# do 'automaticScrolling' -> t[] #);
   do (XmNautoShowCursorPosition, value) -> setBooleanResource;
   #);
   
-- GUIENVtextFieldDisableUpdate: doPart --
do assertOpen
   (# location:: (# do 'disableUpdate' -> t[] #);
   do widgetID -> XmTextDisableRedisplay;
   #);

-- GUIENVtextFieldEnableUpdate: doPart --
do assertOpen
   (# location:: (# do 'enableUpdate' -> t[] #);
   do widgetID -> XmTextEnableRedisplay;
   #);
