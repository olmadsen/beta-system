ORIGIN '../walkingants';
INCLUDE '../timer';
INCLUDE '../loadraster';

INCLUDE '../../graphics';
INCLUDE '../graphicsadds';

-- GUIenvWalkingAntsStart: doPart --
do 1 -> private.current;
   (if interval = 0 then
       100 -> interval
   if);
   (if theCanvas[] = None then
       exception
       (# 
       do 'The Canvas is None in walkingants-start' -> msg.append;
       #);
   if);
   (if private.rectangles.empty then
       interval -> private.onTimer.activate;
   if);
   theRectangle[] -> private.rectangles.append;
   private.drawCurrent;

-- GUIenvWalkingAntsStop: doPart --
do private.onTimer.pause;
   private.rectangles.clear;
   true -> theCanvas.update;

-- GUIenvWalkingAntsInit: doPart --
do 'stripes1' -> loadRaster -> private.stripes[1][];
   'stripes2' -> loadRaster -> private.stripes[2][];
   'stripes3' -> loadRaster -> private.stripes[3][];
   'stripes4' -> loadRaster -> private.stripes[4][];
   'stripes5' -> loadRaster -> private.stripes[5][];
   'stripes6' -> loadRaster -> private.stripes[6][];
   'stripes7' -> loadRaster -> private.stripes[7][];
   'stripes8' -> loadRaster -> private.stripes[8][];
   oneWay -> style;
   inner;

-- GUIenvWalkingAntsPrivate: descriptor --
(# stripes: [8] ^raster;
   current: @integer;
   step: @integer;
   
   drawCurrent: @
     (# 
     do theCanvas.graphics
        (#
        do stripes[current][] ->  pen.stipple;
           rectangles.iterate
           (#
           do current.elm -> drawRect;
           #);
        #);
     #);
   
   onTimer: @timer
     (# action::
          (# 
          do (if style
              //oneWay then
                 (if current = 8 then
                     1 -> current;
                  else
                     1 + current -> current;
                 if);
              else
                 (if current 
                  //4 then
                     -1 -> step;
                  //1 then
                     1 -> step;
                 if);
                 current + step -> current;
             if);
             drawCurrent;
          #);
     #);
   rectangles: @list
     (# element:: rectangle;
     #);
#)

