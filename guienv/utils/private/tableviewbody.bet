ORIGIN '../tableview';
INCLUDE '../../graphics';
INCLUDE '../../fields';
INCLUDE '../graphicsadds';
INCLUDE '../defaultstyle';
INCLUDE '../movabletextfield';
INCLUDE '../colors';
INCLUDE '../scrolleradds';
INCLUDE '../navigationkeys';

-- TableViewCreate: doPart --
do inner create;
   
-- GUIENVtableViewTableItemEnterName: doPart --
do value[] -> private.name[];
   this(tableView).private.updateTable;

-- GUIENVtableViewTableItemExitName: doPart --
do private.name[] -> value[];

-- GUIENVtableViewTableItemEnterIcon: doPart --
do value[] -> private.icon[];
   this(tableView).private.computeItemHeight;
   this(tableView).private.updateTable;

-- GUIENVtableViewTableItemExitIcon: doPart --
do private.icon[] -> value[];

-- GUIENVTableViewTableItemPosition: doPart --
do private.position -> value;

-- GUIENVtableViewTableItemInit: doPart --
do (this(tableItem)[], beforeItem[]) -> this(tableView).private.insert;
   inner;
   this(tableView).private.updateTable;

-- GUIENVtableViewTableItemDelete: doPart --
do this(tableItem)[] -> this(tableView).private.delete;
   inner;
   this(tableView).private.updateTable;

-- GUIENVtableViewTableItemSelect: doPart --
do true -> selected;

-- GUIENVtableViewTableItemDeSelect: doPart --
do false -> selected;
   
-- GUIENVtableViewTableItemActivateEdit: doPart --
do (# x, y: @integer;
   do deactivateEdit;
      position 
        -> this(tableView).private.computeItemPosition -> (x, y);
      (name, style, (x, y)) 
        -> this(tableView).private.editFld.activate;
      this(tableItem)[] -> this(tableView).private.currentEdit[];
   #);
   
-- GUIENVtableViewTableItemScrollIntoView: doPart --
do (# x, y: @integer;
      scrollX, scrollY, scrollerWidth, scrollerHeight: @integer;
      theScroller: ^scroller;
   do this(tableView).private.theScroller[] -> theScroller[];
      (if theScroller[] <> none then
          (position - 1) * itemHeight 
          + this(tableView).private.margin -> y;
          theScroller.theScroll -> (scrollX, scrollY);
          theScroller.viewSize -> (scrollerWidth, scrollerHeight);
          (if y < scrollY then
              (scrollX, y) -> theScroller.theScroll;
           else
              (if scrollY + scrollerHeight < y then
                  (scrollX, y - scrollerHeight + itemHeight) -> theScroller.theScroll;
              if);
          if);
      if);
   #);
   
-- GUIENVtableViewDeactivateEdit: doPart --
do (if private.currentEdit[] <> none then
       private.editFld.deactivate;
   if);
   
   
-- GUIENVtableViewDisableUpdate: doPart --
do true -> private.updateDisabled;
   
-- GUIENVtableViewEnableUpdate: doPart --
do (if private.updateDisabled then
       false -> private.updateDisabled;
       private.computeItemHeight;
       update;
   if);

-- GUIENVtableViewTableItemPrivate: descriptor --
(# name: ^text;
   icon: ^pixmap;
   position: @integer;
   selected: @boolean;
   drawOne: this(tableView).private.tableGraphics
     (# x, y: @integer;
     do this(tableView).private.margin -> x;
        (this(tableView).private.margin
        + this(tableView).private.itemHeight * (position - 1))
          -> y;
        (this(tableItem)[], x, y, true) -> drawItem;
     #);
#)

-- GUIENVtableViewEnterStyle: doPart --
do value[] -> private.theStyle[];
   private.computeItemHeight;
   this(tableView).private.updateTable;

-- GUIENVtableViewExitStyle: doPart --
do private.theStyle[] -> value[];

-- GUIENVtableViewItemHeight: doPart --
do private.itemHeight -> value;
   
-- GUIENVtableViewFitToContents: doPart --
do private.preferredSize -> size;
   


-- GUIENVtableViewScan: doPart --
do private.items.scan
   (# 
   do current[] -> this(scan).current[];
      inner scan;
   #);

-- GUIENVtableViewOpen: doPart --
do (if father## <= scroller## then
       father[] -> private.theScroller[];
    else
       (if father.father## <= scroller## then
           father.father[] -> private.theScroller[];
       if);
   if);
   private.items.init;
   defaultStyle -> private.theStyle[];
   private.computeItemHeight;
   (* private.editFld.init; *)
   inner;

-- GUIENVtableViewClose: doPart --
do inner;
   private.items.clear;
   none -> private.theStyle[];
   0 -> private.itemHeight;

-- GUIENVtableViewOnRefresh: doPart --
do updateRect -> private.refresh;
   inner;


-- GUIENVtableViewOnMouseDown: doPart --
do (# theItem: ^tableItem;
   do deactivateEdit;
      localPosition -> private.findTableItem -> theItem[];
      (if theItem[] <> None then
          (if not theItem.selected then
              scan
              (# 
              do current.deSelect;
              #);
              theItem.select;
          if);
          this(onMouseDown)[] -> theItem.onMouseDown;
       else
          scan
          (# 
          do current.deSelect;
          #);
      if);
   #);
   inner;

-- GUIENVtableViewOnMouseUp: doPart --
do scan
   (# 
   do (if current.selected then 
          this(onMouseUp)[] -> current.onMouseUp;
          (if metaKey then
              current.activateEdit;
          if);
      if);
   #);
   inner;

-- GUIENVtableViewOnKeyDown: doPart --
do ch -> private.speedSearch;
   inner;
   
-- GUIENVtableViewOnEnableTarget: doPart --
do (if private.theScroller[] <> none then
       true -> private.theScroller.contentsIsTarget;
   if);
   inner;
   
-- GUIENVtableViewOnDisableTarget: doPart --
do (if private.theScroller[] <> none then
       false -> private.theScroller.contentsIsTarget;
   if);
   inner;
   
-- GUIENVtableViewPrivate: descriptor --
(# theScroller: ^scroller;
   theStyle: ^textStyle;
   itemHeight: @integer;
   iconWidth: @integer;
   updateDisabled: @boolean;
   
   borderSpacing: (# exit 3 #);
   iconSpacing: (# exit 4 #);
   spacing: (# exit 2 #);
   margin:
     (# value: @integer;
     do (if border.visible then
            2 + borderSpacing -> value;
         else
            borderSpacing -> value;
        if);
     exit value
     #);
   
   tableGraphics: graphics
     (# drawItem:
          (# theItem: ^tableItem;
             x, y: @integer;
             erase: @boolean;
          enter (theItem[], x, y, erase)
          do (# tx, ty: @integer;
                tw: @integer;
                ix, iy: @integer;
                ih, iw: @integer;
                
                icon: ^pixmap;
             do x + iconWidth + iconSpacing -> tx;
                (y 
                + (itemHeight - th) div 2 
                + theStyle.ascent)
                  -> ty;
                theItem.icon -> icon[];
                (if icon[] <> none then
                    icon.height -> ih;
                    icon.width -> iw;
                    x -> ix;
                    y + (itemHeight - ih) div 2 -> iy;
                    (icon[], (0, 0), (ix, iy), iw, ih) -> drawRaster;
                if);
                theItem.name -> theStyle.widthOfText -> tw;
                (if theItem.selected then
                    ((tx, y), (tx + tw, y + itemHeight)) -> fillRect;
                    (tx, ty) -> moveTo;
                    theBGcolor -> pen.foreGroundColor;
                    theItem.name -> drawText;
                    black -> pen.foreGroundColor;
                 else
                    (if erase then
                        theBGcolor -> pen.foreGroundColor;
                        ((tx, y), (tx + tw, y + itemHeight)) -> fillRect;
                        black -> pen.foreGroundColor;
                    if);
                    (tx, ty) -> moveTo;
                    theItem.name -> drawText;
                if);
             #);
          #);
        black: (# exit (0, 0, 0) #);
        white: (# exit (maxInt, maxInt, maxInt) #);
        theBGcolor: @color;

        th: @integer;
     do theStyle[] -> style;
        theStyle.lineHeight -> th;
        backgroundColor -> theBGcolor;
        (if border.visible then
            (# width, height: @integer;
            do size -> (width, height);
               (if (width >= 4) and (height >= 4) then
                   ((2, 2), (width - 2, height - 2)) -> clipRectangle;
               if);
            #);
        if);
        inner;
     #);
   
   
   updateTable:
     (# 
     do (if not updateDisabled then
            update;
        if);
     #);
   items: @list
     (# element:: this(tableView).element;
        assignPositions: scan
          (# inx: @integer;
          do inx + 1 -> inx;
             inx -> current.private.position;
          #);
     #);
   maxIconSize:
     (# width, height: @integer;
     do scan
        (# icon: ^pixmap;
        do current.icon -> icon[];
           (if icon[] <> None then
               (icon.width, width) -> Max -> width;
               (icon.height, height) -> Max -> height;
           if);
        #);
     exit (width, height)
     #);
   computeItemHeight:
     (# width, height: @integer;
     do maxIconSize -> (width -> iconWidth, height);
        (height + 2 * spacing, theStyle.lineHeight) -> Max -> itemHeight;
     #);
   computeItemPosition:
     (# position: @integer;
        x, y: @integer;
        width, ignoreheight: @integer;
     enter position
     do maxIconSize -> (width, ignoreheight);
        width + margin + iconSpacing -> x;
        (position - 1) * itemHeight
        + (itemHeight - theStyle.lineHeight) div 2 
        + theStyle.ascent + margin 
          -> y;
     exit (x, y)
     #);
   insert:
     (# item, before: ^tableItem;
     enter (item[], before[])
     do (if before[] = None then
            item[] -> items.append;
         else
            (item[], before[] -> items.at) -> items.insertBefore;
        if);
        items.assignPositions;
     #);
   delete:
     (# item: ^tableItem;
     enter item[]
     do item[] -> items.at -> items.delete;
        items.assignPositions;
     #);
   putPoint:
     (# x, y: @integer;
     enter (x, y)
     do '[' -> put;
        x -> putInt;
        ' ' -> put;
        y -> putInt;
        ']' -> put;
     #); 
   putRect:
     (# r: ^rectangle;
     enter r[]
     do r.topLeft -> putPoint;
        ' ' -> put;
        r.bottomRight -> putPoint;
     #);
   
   refresh: tableGraphics
     (# x, y: @integer;
        width, height: @integer;
        bounds: ^rectangle;
     enter bounds[]
     do
        maxIconSize -> (iconWidth, integerObject);
        margin -> x;
        margin -> y;
        scan
        (# 
        do (if (bounds.top < (y + itemHeight)) and ((y - itemHeight) < bounds.bottom) then
               (current[], x, y, false) -> drawItem;
           if);
           y + itemHeight -> y;
        #);
     #);

   preferredSize:
     (# width, height: @integer;
        maxTextWidth: @integer;
        iw, ih: @integer;
     do items.size * itemHeight + 2 * margin -> height;
        scan
        (# tw: @integer;
        do current.name -> theStyle.widthOfText -> tw;
           (tw, maxTextWidth) -> Max -> maxTextWidth;
        #);
        maxIconSize -> (iw, ih);
        2*margin + iw + iconSpacing + maxTextWidth -> width;
     exit (width, height)
     #);
   findTableItem:
     (# x, y: @integer;
        inx: @integer;
        theItem: ^tableItem;
        position: @integer;
     enter (x, y)
     do ((y - margin) div itemHeight) + 1 -> position;
        l: scan
          (# 
          do inx + 1 -> inx;
             (if inx = position then
                 current[] -> theItem[];
                 leave l;
             if);
          #);
     exit theItem[]
     #);
   editFld: @movableTextField
     (# 
        verify::
          (# 
          do (if currentEdit[] <> none then
                 (if theText -> currentEdit.verify then
                     theText -> currentEdit.name;
                 if);
                 none -> currentEdit[];
             if);
          #);
     #);
   currentEdit: ^tableItem;
   
   simpleFind:
     (# ch: @char;
        found: ^tableItem;
     enter ch
     do l: scan
        (# theName: ^text;
        do current.name -> theName[];
           (if theName.length >= 1 then
               (if ch = (1 -> theName.inxGet) then
                   current[] -> found[];
                   leave l;
               if);
           if);
        #);
     exit found[]
     #);
   speedSearch:
     (# ch: @char;
        foundItem: ^tableItem;
     enter ch
     do (if ch
         //leftArrow
         //rightArrow
         //upArrow then 
            moveUp
         //downArrow then
            moveDown
         else
            ch -> simpleFind -> foundItem[];
            (if foundItem[] <> none then
                scan
                (# 
                do current.deSelect;    
                #);
                foundItem.select;
            if);
        if);
     #);
   
   moveUp:
     (# position: @integer;
     do (if selectedItem[] = none then
            1 -> selectByNumber;
         else
            (if selectedItem.position <> 1  then
                selectedItem.position - 1  -> position;
                selectedItem.deselect;
                position -> selectByNumber;
            if);
        if);
     #);
   
   moveDown:
     (# position: @integer;
     do (if selectedItem[] = none then
            1 -> selectByNumber;
         else
            (if selectedItem.position <> noOfItems  then
                selectedItem.position + 1 -> position;
                selectedItem.deselect;
                position  -> selectByNumber;
            if);
        if);
     #);
   
   selectByNumber:
     (# position: @integer;
     enter position
     do search: scan
        (# 
        do (if current.position = position then
               current.select;
               leave search;
           if);
        #);
     #);
   noOfItems:
     (# 
     exit items.size
     #);
      
   selectedItem: ^tableItem;
#)

-- GUIENVtableViewTableItemEnterSelected: doPart --
do (if value <> private.selected then
       value -> private.selected;
       (* true -> update;*)
       private.drawOne;
       (if private.selected then
           this(tableItem)[] -> this(tableView).private.selectedItem[];
           scrollintoView;
           onSelect;
        else
           none -> this(tableView).private.selectedItem[];
       if);
   if);

-- GUIENVtableViewTableItemExitSelected: doPart --
do private.selected -> value;

