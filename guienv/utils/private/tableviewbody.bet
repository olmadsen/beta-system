ORIGIN '../tableview';
INCLUDE '~beta/containers/list'
        '~beta/guienv/utils/drawing'
        '~beta/guienv/controls'
        '~beta/guienv/private/datastructures/sequence';
-- abstractColumnSetTitle: DoPart --
do value.copy->private.theTitle[];   

-- abstractColumnGetTitle: DoPart --
do private.theTitle.copy->value[];   

-- abstractColumnSetWidth: DoPart --
do value->private.theWidth;   

-- abstractColumnGetWidth: DoPart --
do private.theWidth->value;   

-- abstractColumnLess: DoPart --
do
   left.private.data[private.index][]->leftData[];
   right.private.data[private.index][]->rightData[];
   INNER ;
   (if not doneInInner then
       less:
       (if leftData[] <> none then
           (if rightData[] <> none then
               (if true
                // (leftData.lgth < rightData.lgth) then
                   (for i: leftData.lgth repeat
                     (if leftData.T[i] <> rightData.T[i] then
                         (leftData.T[i] < rightData.T[i])->value; leave less; 
                     if)
                   for);
                   true->value;
                   
                // (leftData.lgth >= rightData.lgth) then
                   (for i: rightData.lgth repeat
                     (if leftData.T[i] <> rightData.T[i] then
                         (leftData.T[i] < rightData.T[i])->value; leave less; 
                     if)
                   for);
                   false->value;
                   
               if);
               
            else
               false->value; 
           if);
           
        else
           true->value; 
       if);
       
   if);
     

-- abstractColumnOpen: DoPart --
do
   'Column'->private.theTitle[];
   150->private.theWidth;
   0->private.index;
   INNER ;
     

-- abstractColumnClose: DoPart --
do INNER ; 0->private.theWidth; none ->private.theTitle[];   

-- abstractColumnPrivate: Descriptor --
(#
   theTitle: ^text;
   theWidth: @integer;
   pos: ^columnList.theCellType;
   index: @integer;
   
#)  

-- tableViewAppendColumn: DoPart --
do
   theColumn[]->private.columns.append->theColumn.private.pos[];
   private.columns.size->theColumn.private.index;
     

-- tableViewInsertColumn: DoPart --
do
   after.private.index->theColumn.private.index;
   (theColumn[],after.private.pos[])->private.columns.insertAfter
     ->theColumn.private.pos[];
   theColumn.private.pos[]
     ->private.columns.scanFrom
       (#  do current.private.index+1->current.private.index;  #);
     

-- tableViewHorizontalScrollbarVisibleExit: DoPart --
do private.horizontalScrollbar.visible->visible;   

-- tableViewHorizontalScrollbarVisibleEnter: DoPart --
do visible->private.horizontalScrollbar.visible; updateScrollbars;   

-- tableViewVerticalScrollbarVisibleEnter: DoPart --
do
   visible->private.verticalScrollbar.visible;
     (# width,height: @integer; 
     do
        size->(width,height);
        (if visible
         // true then
            private.cornerView.show;
            ((1,height-scrollbarheight-1),(width-scrollbarwidth,height-1))
              ->private.horizontalScrollbar.frame
         // false then
            private.cornerView.hide;
            ((1,height-scrollbarheight-1),(width,height-1))
              ->private.horizontalScrollbar.frame
        if)
     #);
   private.cornerView.update;
     

-- tableViewVerticalScrollBarVisibleExit: DoPart --
do private.verticalScrollbar.visible->visible;   

-- tableViewDeleteColumn: DoPart --
do
   theColumn.private.pos[]
     ->private.columns.scanFrom
       (#  do current.private.index-1->current.private.index;  #);
   theColumn.private.pos[]->private.columns.delete;
   none ->theColumn.private.pos[];
   0->theColumn.private.index;
     

-- columnWithIconTypeOnDrawTitle: DoPart --
do
     (#
        box: @rectangle;
        columnWithIconIndent: (#  exit 25 #);
        kind: @integer;
        
     do
        bounds->box;
        (if THIS(abstractColumn)[] = sortByColumn then
            g.bevelSmallPressed->kind; 
         else
            g.bevelSmallNormal->kind; 
        if);
        (box,kind)->g.drawBevel;
        g.black->g.pen.foreGroundColor;
        box.left+columnWithIconIndent->box.left;
        (THIS(tableView).private.titleStyle[],private.theTitle[],box,
         g.alignmentLeft)->g.drawCenteredText;
        
     #);
     

-- columnWithIconTypeOnDrawItem: DoPart --
do
     (# box,iconBox: @rectangle; icon: ^pixmap; expLeft,expTop: @integer; 
     do
        bounds->box;
        theTableItem.icon->icon[];
        (if icon[] <> none then
            box->iconBox;
            iconBox.left+theTableItem.iconIndent->iconBox.left;
            (icon[],iconBox,g.alignmentLeft)->g.drawCenteredIcon;
            
        if);
        (if data[] <> none then
            g.black->g.pen.foreGroundColor;
            box.left+theTableItem.textIndent->box.left;
            (THIS(tableView).private.bodyStyle[],data[],box,g.alignmentLeft)
              ->g.drawCenteredText;
            
        if);
        
     #);
     

-- columnSetAlignment: DoPart --
do value->private.theAlignment;   

-- columnGetAlignment: DoPart --
do private.theAlignment->value;   

-- columnOpen: DoPart --
do alignLeft->alignment; INNER ;   

-- columnClose: DoPart --
do INNER ;   

-- columnOnDrawTitle: DoPart --
do
     (#
        box: @rectangle;
        inset: (#  exit 5 #);
        align: @integer;
        kind: @integer;
        
     do
        bounds->box;
        (if THIS(column)[] = sortByColumn then
            g.bevelSmallPressed->kind; 
         else
            g.bevelSmallNormal->kind; 
        if);
        (box,kind)->g.drawBevel;
        g.black->g.pen.foreGroundColor;
        box.left+inset->box.left;
        box.right-inset->box.right;
        (if alignment = alignLeft then
            g.alignmentLeft->align; 
         else
            g.alignmentRight->align; 
        if);
        (THIS(tableView).private.titleStyle[],
         THIS(abstractColumn).private.theTitle[],box,align)->g.drawCenteredText;
        
     #);
     

-- columnOnDrawItem: DoPart --
do
     (# box: @rectangle; inset: (#  exit 5 #); align: @integer; 
     do
        (if data[] <> none then
            bounds->box;
            g.black->g.pen.foreGroundColor;
            box.left+inset->box.left;
            box.right-inset->box.right;
            (if alignment = alignLeft then
                g.alignmentLeft->align; 
             else
                g.alignmentRight->align; 
            if);
            (THIS(tableView).private.bodyStyle[],data[],box,align)
              ->g.drawCenteredText;
            
        if);
        
     #);
     

-- columnPrivate: Descriptor --
(# theAlignment: @integer;  #)  

-- tableItemSetMainText: DoPart --
do value.copy->private.data[1][];   

-- tableItemGetMainText: DoPart --
do private.data[1].copy->value[];   

-- tableItemSetIcon: DoPart --
do theIcon[]->private.theIcon[];   

-- tableItemGetIcon: DoPart --
do private.theIcon[]->theIcon[];   

-- tableItemSetColumnText: DoPart --
do value.copy->private.data[theColumn.private.index][];   

-- tableItemGetColumnText: DoPart --
do private.data[theColumn.private.index].copy->value[];   

-- tableItemSetTextIndent: DoPart --
do value->private.theTextIndent;   

-- tableItemGetTextIndent: DoPart --
do private.theTextIndent->value;   

-- tableItemSetIconIndent: DoPart --
do value->private.theIconIndent;   

-- tableItemGetIconIndent: DoPart --
do private.theIconIndent->value;   

-- tableItemSelected: DoPart --
do private.selected->value;   

-- tableItemOpen: DoPart --
do
   THIS(tableView).private.columns.size->private.data.new;
   defaultIconIndent->private.theIconIndent;
   defaultTextIndent->private.theTextIndent;
   INNER ;
     

-- tableItemClose: DoPart --
do INNER ;   

-- tableItemLib: Attributes --
toggle: (#  do updateMainView;  #);   

-- tableItemPrivate: Descriptor --
(#
   data: [1] ^text;
   theIcon: ^pixmap;
   selected: @boolean;
   theIconIndent: @integer;
   theTextIndent: @integer;
   pos: ^tableItemList.theCellType;
   
#)  

-- tableViewAppend: DoPart --
do
   theTableItem[]->private.tableItems.append->theTableItem.private.pos[];
   (if private.tableItems.size = 1 then
       theTableItem[]->private.topItem[]; 
   if);
   updateMainView;
   updateScrollBars;
     

-- tableViewPrepend: DoPart --
do
   theTableItem[]->private.tableItems.prepend->theTableItem.private.pos[];
   theTableItem[]->private.topItem[];
   updateMainView;
     

-- tableViewInsert: DoPart --
do
   (theTableItem[],after.private.pos[])->private.tableItems.insertAfter
     ->theTableItem.private.pos[];
   updateMainView;
     

-- tableViewScan: DoPart --
do private.tableitems.scan (#  do current[]->THIS(scan).current[];  #);   

-- tableViewSelectionSet: DoPart --
do
   (if theTableItem.selected then
       (if private.theSelection.size <> 1 then
           clear; theTableItem[]->add; 
       if);
       
    else
       clear; theTableItem[]->add; 
   if);
     

-- tableViewSelectionAdd: DoPart --
do
   (if not theTableItem.selected then
       theTableItem[]->private.theSelection.append;
       true->theTableItem.private.selected;
       updateMainView;
       
   if);
     

-- tableViewSelectionRemove: DoPart --
do
   (if theTableItem.selected then
       theTableItem[]->private.theSelection.at->private.theSelection.delete;
       theTableItem.theEventHandler.onDeSelect;
       false->theTableItem.private.selected;
       updateMainView;
       
   if);
     

-- tableViewSelectionClear: DoPart --
do
   (if not private.theSelection.empty then
       private.theSelection.scan
         (# 
         do
            current.theEventHandler.onDeSelect;
            false->current.private.selected;
            
         #);
       private.theSelection.clear;
       updateMainView;
       
   if);
     

-- tableViewSelectionScan: DoPart --
do
   private.theSelection.scan
     (#  do current[]->THIS(scan).current[]; INNER scan;  #);
     

-- tableViewSetTheScroll: DoPart --
do
   (if (x <> private.xScroll) or (y <> private.yScroll) then
       (x,y)->(private.xScroll,private.yScroll);
       updateScrollCache;
       updateMainView;
       updateTitleview;
       THIS(window).update;
       
   if);
     

-- tableViewGetTheScroll: DoPart --
do (private.xScroll,private.yScroll)->(x,y);   

-- tableViewScroll: DoPart --
do   

-- tableViewMaxScroll: DoPart --
do
     (# width,height: @integer; 
     do
        private.mainView.size->(width,height);
        private.columns.scan
          (#  do maxX+current.width->maxX;  #);
        maxX+50->maxX;
        maxX-width->maxX;
        (if maxX < 0 then 0->maxX;  if);
        private.tableItems.size*(rowHeight+rowDist)->maxY;
        (if horizontalScrollbarvisible then maxY+scrollbarHeight->maxY;  if);
        maxY-height->maxY;
        (if maxY < 0 then 0->maxY;  if);
        
     #);
     

-- tableViewSetSortByColumn: DoPart --
do theColumn[]->private.sortColumn[]; updateTitleView; sortTableView;   

-- tableViewGetSortByColumn: DoPart --
do private.sortColumn[]->theColumn[];   

-- tableViewOpen: DoPart --
do
   'Helvetica'->private.titleStyle.name;
   10->private.titleStyle.size;
   'Helvetica'->private.bodyStyle.name;
   10->private.bodyStyle.size;
   THIS(tableView)[]->private.titleView.open;
   THIS(tableView)[]->private.cornerView.open;
   THIS(tableView)[]->private.mainView.open;
   THIS(tableView)[]->private.verticalScrollbar.open;
   THIS(tableview)[]->private.horizontalScrollbar.open;
   private.tableItems.init;
   private.columns.init;
   private.theSelection.init;
   true->border.visible;
   borderStyles.shadowIn->border.style;
   layout;
   (0xFFFF,52428,26214)->private.hilitecolor;
   INNER ;
   updateScrollbars;
     

-- tableViewClose: DoPart --
do
   INNER ;
   private.tableItems.clear;
   private.columns.clear;
   private.titleView.close;
   private.mainView.close;
     

-- tableViewOnFrameChanged: DoPart --
do layout; updateScrollBars; INNER ;   

-- tableViewLib: Attributes --
titleHeight: (#  exit 20 #);
scrollbarWidth: (#  exit 16 #);
scrollbarHeight: (#  exit 16 #);
defaultTextIndent: (#  exit 45 #);
defaultIconIndent: (#  exit 25 #);
sortTableView:
  (#
     sorter: @sequence (# element:: tableItemType;  #);
     index: @integer;
     theLess: ^column.less;
     
  do
     sorter.init;
     private.sortColumn.private.index->index;
     &private.sortColumn.less[]->theLess[];
     private.tableItems.scan
       (#  do current[]->sorter.append;  #);
     sorter.sort
       (# less::  (#  do (left[],right[])->theLess->value;  #);  #);
     private.tableItems.clear;
     sorter.scan
       (# 
       do current[]->private.tableItems.append->current.private.pos[]; 
       #);
     updateScrollCache;
     updateMainView;
     
  #);
updateScrollCache:
  (# itemNumber: @integer; lineHeight: @integer; inx: @integer; 
  do
     rowHeight+rowDist->lineHeight;
     (private.yScroll+lineHeight) div lineHeight->itemNumber;
     private.yScroll-(itemNumber-1)*lineHeight->private.yOffset;
     private.xScroll->private.xOffset;
     search: private.tableItems.scan
       (# 
       do
          inx+1->inx;
          (if inx = itemNumber then
              current[]->private.topItem[]; leave search; 
          if);
          
       #);
     
  #);
updateMainView:
  (# 
  do
     (if not private.pendingUpdate then
         private.mainView.update; true->private.pendingUpdate; 
     if);
     
  #);
updateTitleView: (#  do private.titleView.update;  #);
updateScrollBars:
  (# maxX,maxY: @integer; x,y: @integer; width,height: @integer; 
  do
     true->private.updatingScrollbars;
     maxScroll->(maxX,maxY);
     theScroll->(x,y);
     maxY->private.verticalScrollBar.maxValue;
     y->private.verticalScrollBar.value;
     rowHeight+rowDist->private.verticalScrollBar.scrollAmount;
     private.mainView.size->(width,height);
     height-(rowHeight+rowDist)->private.verticalScrollBar.pageScrollAmount;
     maxX->private.horizontalScrollbar.maxValue;
     x->private.horizontalScrollbar.value;
     10->private.horizontalScrollbar.scrollAmount;
     50->private.horizontalScrollbar.pageScrollAmount;
     false->private.updatingScrollbars;
     
  #);
columnList: list
  (# theCellType::  (#  #); element:: abstractColumn;  #);
tableItemList: list
  (# theCellType::  (#  #); element:: tableItemType;  #);
layout:
  (# width,height: @integer; 
  do
     size->(width,height);
     ((2,2),(width-2,2+titleHeight))->private.titleView.frame;
     ((width-scrollbarWidth,2),(width-2,2+titleHeight))
       ->private.cornerView.frame;
     ((2,2+titleHeight),(width-1,height-2))->private.mainView.frame;
     ((1,height-scrollbarheight-1),(width-scrollbarwidth,height-1))
       ->private.horizontalScrollbar.frame;
     ((width-scrollbarWidth-1,titleHeight+1),(width-1,height-1))
       ->private.verticalScrollbar.frame;
     
  #);
tableItemClick:
  (#
     theEvent: ^windowItem.eventHandler.mouseDown;
     theTableItem: ^tableItemType;
     bounds: ^rectangle;
     getExpanderBox:
       (# bounds: ^rectangle; box: @rectangle; 
       enter bounds[]
       do
          bounds.left+5->box.left;
          bounds.top+(rowHeight-12) div 2->box.top;
          (12,12)->box.size;
          
       exit box
       #);
     box: @rectangle;
     
  enter (theEvent[],theTableItem[],bounds[])
  do
     bounds[]->getExpanderBox->box;
     theEvent.doubleClick->theTableItem.theEventHandler.onSelect;
     (if theEvent.localPosition->box.containsPoint then
         theTableItem.toggle; 
      else
         (if theTableItem.selected then
             (if theEvent.shiftKey then
                 theTableItem[]->selection.remove; 
             if);
             
          else
             (if theEvent.shiftKey then
                 theTableItem[]->selection.add; 
              else
                 theTableItem[]->selection.set; 
             if);
             
         if);
         
     if);
     
  #);
tableViewClick:
  (# theEvent: ^windowItem.eventHandler.mouseDown
  enter theEvent[]
  do (if not theEvent.shiftKey then selection.clear;  if); 
  #);
columnClick:
  (# theEvent: ^windowItem.eventHandler.mouseDown; theColumn: ^column; 
  enter (theEvent[],theColumn[])
  do theColumn[]->sortByColumn; 
  #);
rowheight: (#  exit 18 #);
rowdist: (#  exit 1 #);
  

-- tableViewPrivate: Descriptor --
(#
   topItem: ^tableItemType;
   xScroll,yScroll: @integer;
   xOffset,yOffset: @integer;
   pendingUpdate: @boolean;
   updatingScrollbars: @boolean;
   hiliteColor: @ (# theColor: @Color enter theColor exit theColor #);
   columns: @columnList;
   tableItems: @tableItemList;
   titleStyle,bodyStyle: @textStyle;
   sortColumn: ^abstractColumn;
   theSelection: @tableItemList;
   titleView: @windowItem
     (#
        eventHandler:: 
          (#
             onRefresh:: 
               (# 
               do
                  graphics
                    (# box: @rectangle; width,height: @integer; 
                    do
                       - xOffset->box.left;
                       0->box.top;
                       titleHeight->box.bottom;
                       Columns.scan
                         (# 
                         do
                            box.left+current.width->box.right;
                            (THIS(graphics)[],box[])
                              ->current.theEventHandler.onDrawTitle;
                            box.right->box.left;
                            
                         #);
                       size->(width,height);
                       (if box.left <= width then
                           width->box.right; (box,bevelSmallNormal)->drawBevel; 
                       if);
                       
                    #);
                  
               #);
             onMouseDown:: 
               (#
                  x,y: @integer;
                  left: @integer;
                  right: @integer;
                  theColumn: ^column;
                  
               do
                  localPosition->(x,y);
                  - xOffset->left;
                  search: columns.scan
                    (# 
                    do
                       left+current.width->right;
                       (if (x >= left) and (x < right) then
                           current[]->theColumn[]; leave search; 
                       if);
                       right->left;
                       
                    #);
                  (if theColumn[] <> none then
                      (THIS(onMouseDown)[],theColumn[])->columnClick; 
                  if);
                  
               #);
             
          #);
        
     #);
   mainView: @windowItem
     (#
        eventHandler:: 
          (#
             onRefresh:: 
               (# 
               do
                  graphics
                    (#
                       box: @rectangle;
                       width,height: @integer;
                       str: ^text;
                       sortBox: @integer;
                       theColumn: ^column;
                       
                    do
                       size->(width,height);
                       (if sortColumn[] <> none then
                           - xOffset->box.left;
                           &column[]->theColumn[];
                           search: columns.scan
                             (# 
                             do
                                (if current[] = sortColumn[] then
                                    box.left+sortColumn.width->box.Right;
                                    leave search;
                                    
                                 else
                                    box.left+current.width->box.left; 
                                if);
                                current[]->theColumn[];
                                
                             #);
                           0->box.top;
                           height->box.bottom;
                           gray3->pen.foregroundColor;
                           box->fillRect;
                           
                       if);
                       - yOffset->box.top;
                       (if topItem[] <> none then
                           updating:
                           topItem.private.pos[]
                             ->tableitems.scanFrom
                               (#
                                  item: ^tableItemType;
                                  hiliteBox: @rectangle;
                                  
                               do
                                  (if box.top < height then
                                      - xOffset->box.left;
                                      box.top+rowHeight->box.bottom;
                                      current[]->item[];
                                      box->hiliteBox;
                                      0->hiliteBox.left;
                                      width->hiliteBox.right;
                                      (if current.selected then
                                          hiliteColor->pen.foreGroundColor;
                                          hiliteBox->fillRect;
                                          
                                      if);
                                      columns.scan
                                        (# 
                                        do
                                           box.left+current.width->box.right;
                                           item.private.data
                                           [current.private.index][]->str[];
                                           (THIS(graphics)[],box[],item[],str[])
                                             ->
                                               current.theEventHandler.
                                                 onDrawItem;
                                           box.right->box.left;
                                           
                                        #);
                                      gray1->pen.foreGroundColor;
                                      (0,box.bottom)->moveTo;
                                      (width,box.bottom)->drawTo;
                                      box.bottom+1->box.top;
                                      
                                   else
                                      leave updating; 
                                  if);
                                  
                               #);
                           
                       if);
                       
                    #);
                  false->pendingUpdate;
                  
               #);
             onMouseDown:: 
               (#
                  r: @rectangle;
                  x,y: @integer;
                  item: ^tableItemType;
                  width,height: @integer;
                  
               do
                  - yOffset->r.top;
                  localPosition->(x,y);
                  (if topItem[] <> none then
                      search:
                      topItem.private.pos[]
                        ->tableitems.scanFrom
                          (# 
                          do
                             0->r.left;
                             r.top+rowHeight->r.bottom;
                             (if (y >= r.top) and (y < r.bottom) then
                                 current[]->item[]; leave search; 
                             if);
                             r.bottom+1->r.top;
                             
                          #)
                  if);
                  (if item[] <> none then
                      size->(width,height);
                      - xOffset->r.left;
                      r.left+width->r.right;
                      (THIS(onMouseDown)[],item[],r[])->tableItemClick;
                      
                   else
                      THIS(onMouseDown)[]->tableViewClick; 
                  if);
                  
               #);
             
          #);
        
     #);
   verticalScrollBar: @scrollbar
     (#
        vertical::  (#  do true->value #);
        eventHandler:: 
          (#
             onValueChanged:: 
               (# x,y: @integer; 
               do
                  (if not updatingScrollbars then
                      theScroll->(x,y); (x,value)->theScroll; 
                  if);
                  
               #);
             
          #);
        
     #);
   horizontalScrollbar: @scrollbar
     (#
        eventHandler:: 
          (#
             onValueChanged:: 
               (# x,y: @integer; 
               do
                  (if not updatingScrollbars then
                      theScroll->(x,y); (value,y)->theScroll
                  if)
               #);
             
          #)
     #);
   cornerView: @windowItem
     (#
        eventHandler:: 
          (#
             onRefresh:: 
               (# 
               do
                  (if verticalScrollbar.visible then
                      graphics
                        (# 
                        do (((0,0),size),bevelSmallNormal)->drawBevel; 
                        #)
                  if);
                  ;
                  
               #);
             
          #);
        
     #);
   
#)  

-- tableViewonMouseUp: DoPart --
do INNER  

-- tableViewdelete: DoPart --
do
   theTableItem[]->private.tableItems.at->private.tableItems.delete;
   updateScrollBars;
     

-- tableViewDeleteSelection: DoPart --
do
     (#
        aTableItem: ^tableItemType;
        topItemDeleted: @Boolean;
        firstElmAfterSelection:
          (#
             aTableItem: ^tableItem;
             lastElmSelected:
               (# 
               exit
               ((private.theSelection.last).elm[] =
                (private.tableItems.last).elm[])
               #);
             
          do
             (if not lastElmSelected then
                 (((private.theSelection.last).elm[]->private.tableItems.at).
                  succ.elm[])->aTableItem[];
                 
             if)
          exit aTableItem[]
          #)
     do
        (if not private.theSelection.empty then
            firstElmAfterSelection->aTableItem[];
            private.theSelection.scan
              (# 
              do
                 current.theEventhandler.onDeSelect;
                 FALSE->current.private.selected;
                 (if current[] = private.topItem[] then
                     TRUE->topItemDeleted; 
                 if);
                 current[]->delete;
                 
              #);
            private.theSelection.clear;
            (if (atableItem[] = none ) and (not private.tableItems.empty) then
                (private.tableItems.last).elm[]->atableItem[]
            if);
            (if topItemDeleted then
                (if private.tableItems.empty then
                    none ->private.topItem[]; 
                 else
                    (private.tableItems.head).elm[]->private.topItem[]
                if)
            if);
            (if aTableItem[] <> none then aTableItem[]->selection.set if);
            updateScrollBars;
            updateMainView;
            
        if)
     #)  

-- tableViewChangeHiliteColor: DoPart --
do aColor->private.hiliteColor; updateMainView;   

