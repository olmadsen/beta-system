ORIGIN '../framer';
INCLUDE '~beta/guienv/graphics';
LIB_ITEM 'frigg_framer';

-- framerLib: attributes --
maxComponent: (# exit 0xFFFF #);
lighter:
  (# c: @color;
     up: integerValue
       (# comp: @integer;
       enter comp
       do comp + (comp * 30) div 100 -> value;
          (if value > maxComponent then
              maxComponent -> value;
          if);
       #);
  enter c
  do c.red -> up -> c.red;
     c.green -> up -> c.green;
     c.blue -> up -> c.blue;
  exit c
  #);
darker:
  (# c: @color;
     down: integerValue
       (# comp: @integer;
       enter comp
       do comp - (comp * 30) div 100 -> value;
          (if value < 0 then
              0 -> value;
          if);
       #);
  enter c
  do c.red -> down -> c.red;
     c.green -> down -> c.green;
     c.blue -> down -> c.blue;
  exit c
  #);


bevelGraphics: graphics
  (# drawMotifBevel:
       (# box: @rectangle;
          top, bottom: @color;
          count: @integer;
       enter (box, top, bottom, count)
       do	top -> pen.foreGroundColor;
          (for inx: count repeat
               (box.left + inx - 1, box.top + inx - 1) -> moveTo;
               (box.right - inx, box.top + inx - 1) -> drawTo;
               (box.left + inx - 1, box.top + inx - 1) -> moveTo;
               (box.left + inx - 1, box.bottom - inx) -> drawTo;
          for);
          bottom -> pen.foreGroundColor;
          (for inx: count repeat
               (box.left 	+ inx	, box.bottom 	- inx) 	-> moveTo;
               (box.right 	- inx	, box.bottom	- inx) 	-> drawTo;
               (box.right 	- inx	, box.top 		+ inx) 	-> moveTo;
               (box.right 	- inx	, box.bottom 	- inx) 	-> drawTo;
          for);
       #);
     drawBevelOutline:
       (# box: @rectangle;
          top, bottom: @color;
          count: @integer;
       enter (box, top, bottom, count)
       do (0, 0, 0) -> pen.foreGroundColor;
          box -> drawRect;
          (1, 1) -> box.inset;
          (box, top, bottom, count - 1) -> drawMotifBevel;
       #);
     drawBevelRounded:
       (# box: @rectangle;
          top, bottom: @color;
          count: @integer;
       enter (box, top, bottom, count)
       do (0, 0, 0) -> pen.foreGroundColor;
          (box.left, box.top + 1) -> moveTo;
          (box.left, box.bottom - 2) -> drawTo;
          (box. left + 1, box.top) -> moveTo;
          (box.right - 2, box.top) -> drawTo;
          (box.right - 1, box.top + 1) -> moveTo;
          (box.right - 1, box.bottom - 2) -> drawTo;
          (box.left + 1, box.bottom-1) -> moveTo;
          (box.right - 2, box.bottom-1) -> drawTo;
          (1, 1) -> box.inset;
          (box, top, bottom, count - 1) -> drawMotifBevel;
       #);
     drawBevel:
       (# box: @rectangle;
          top, bottom: @color;
          count: @integer;
          kind: @integer;
       enter (box, top, bottom, count, kind)
       do (if kind
           //bevelkind then
              (box, top, bottom, count) -> drawMotifBevel;
           //bevelOutlineKind then
              (box, top, bottom, count) -> drawBevelOutline;
           //bevelRoundedOutlineKind then
              (box, top, bottom, count) -> drawBevelRounded;
          if);
       #);
  do INNER;
  #);

-- framerOpen: doPart --
do 2 -> theborderwidth;
   bevelKind -> theKind;
   true -> insetFlag;
   INNER;

-- framerOnRefresh: doPart --
do bevelGraphics
   (# box: @rectangle;
      top, bottom: @color;
   do size -> box.size;
      (if inset then
          backgroundColor -> lighter -> top;
          backgroundColor -> darker -> bottom;
       else
          backgroundColor -> darker -> top;
          backgroundColor -> lighter -> bottom;
      if);
      (box, top, bottom, borderwidth, kind) -> drawBevel;
   #);
   INNER;
