ORIGIN '../decorator';


-- decoratorOpen: doPart --
do borderStyles.shadowIn -> border.style;
   true -> border.visible;
   this(decorator)[] -> contents.open;
   layoutDecorator;
   inner;

-- decoratorOnFrameChanged: doPart --
do layoutContents;
   inner;

-- decoratorOnChildFrameChanged: doPart --
do layoutDecorator;
   inner;
   
-- decoratorPrivate: descriptor --
(# 
   doingLayout: @boolean;
#)

-- decoratorLib: attributes --

borderWidth: integerValue
  (#
  do (if border.visible then
         2 -> value;
      else
         0 -> value;
     if);
  #);

layoutDecorator:
  (# width, height: @integer;
  do (if not private.doingLayout then
         true -> private.doingLayout;
         contents.size -> (width, height);
         (width - 2 * borderWidth, height - 2 * borderWidth) -> size;
         false -> private.doingLayout;
     if);
  #);

layoutContents:
  (# width, height: @integer;
  do (if not private.doingLayout then
         true -> private.doingLayout;
         size -> (width, height);
         ((borderWidth, borderWidth), 
         (width - borderWidth, height - borderWidth))-> contents.frame;
         false -> private.doingLayout;
     if);
  #);

