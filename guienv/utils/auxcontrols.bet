ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls'
        '~beta/guienv/fields'
        '~beta/guienv/graphics'
        '~beta/guienv/utils/navigationkeys'
        '~beta/sysutils/objinterface';
LIB_DEF 'frigg_aux' '../../lib';
-- lib: Attributes --
downArrowKey: (#  exit downArrow #);   

-- windowLib: Attributes --
caption: staticText
  (#
     eventHandler::< 
       (# onLabelChanged::<  (#  do fitToContents; INNER ;  #);  #);
     
  #);
downArrow: windowitem
  (#
     arrowHeight:
       (# 
       enter
         (# value: @integer
         enter value
         do
            (if value <> theArrowHeight then value->theArrowHeight; update if)
         #)
       exit theArrowHeight
       #);
     popupMenu: (#  enter thePopupMenu[] exit thePopupMenu[] #);
     open::<  (#  do 5->theArrowHeight; INNER #);
     eventHandler::< 
       (#
          onRefresh::< 
            (# 
            do
               graphics
                 (# x,y: @integer; width,height: @integer
                 do
                    size->(width,height);
                    width div 2->x;
                    (height-arrowHeight) div 2+arrowHeight->y;
                    (0,arrowHeight-1)
                      ->forto
                        (#  do ((x-inx,y-inx),(x+inx,y-inx))->drawLine #)
                 #);
               INNER
            #);
          onMouseDown::< 
            (# 
            do
               (if popupMenu <> none then
                   (0,localposition,THIS(downArrow)[])->(popupmenu).popup
               if)
            #)
       #);
     theArrowHeight: @integer;
     thePopupMenu: ^menu
  #);
groupRadioButton: radioButton
  (#
     unToggleOthers:
       (# 
       do
          father.scan
            (# 
            do
               (if current[] <> THIS(groupRadioButton)[] then
                   (if current## <= groupRadioButton## then
                       current[]
                         ->qua
                           (# as:: groupRadioButton
                           do false->thisObj.state
                           #)
                   if)
               if)
            #)
       #);
     eventHandler::< 
       (#
          onStateChanged::< 
            (#  do (if state then unToggleOthers if); INNER #)
       #)
  #);
dialogField: textField
  (#
     filter:<
       (# ch: @char; allow,done: @boolean; 
       enter ch
       do
          true->allow;
          (if ch
           // ascii.ht (* tab *) then
              false->allow; 
           // ascii.cr // ascii.nl // 3 (* return *) then
              allowReturn->allow; 
           // leftArrow // rightArrow // upArrow // downArrowKey then
              true->allow; true->done
          if);
          (if (allow and (not done)) then INNER ;  if);
          
       exit allow
       #);
     next: (#  enter theNext[] exit theNext[] #);
     editable: (#  enter editableFlag exit editableFlag #);
     allowReturn: (#  enter allowReturnFlag exit allowReturnFlag #);
     integerContents:
       (# value: @integer; syntaxError:< notification; 
       enter
         (# st: @styledText; 
         enter value
         do
            value->st.putInt;
            true->settingContents;
            st[]->contents;
            false->settingContents;
            
         #)
       exit
         (# st: ^styledText; 
         do
            l:
              (# 
              do
                 contents->st[];
                 st.asInt
                   (#
                      syntaxError:: 
                        (# 
                        do THIS(integerContents).syntaxError; leave l; 
                        #);
                      
                   #)->value;
                 
              #);
            
         exit value
         #)
       #);
     textContents:
       (# value: ^text; 
       enter
         (# st: @styledText; 
         enter value[]
         do
            value->st;
            true->settingContents;
            st[]->contents;
            false->settingContents;
            
         #)
       exit
         (# st: ^styledText; 
         do contents->st[]; st.copy->value[]; 
         exit value[]
         #)
       #);
     selectAll: (#  do (0,length)->selection #);
     open::<  (#  do true->editable; (2,2)->margin; INNER ;  #);
     eventHandler::< 
       (#
          tab: event
            (# shift: @boolean; done: @boolean; 
            enter shift
            do INNER ; 
            exit done
            #);
          onTab:< tab;
          return: event (#  do INNER ;  #);
          onReturn:< return;
          onKeyDown::< 
            (# 
            do
               (if ch
                // ascii.ht then
                   (if not (shiftKey->onTab) then
                       (if next <> none then
                           next->target; (next).selectAll; 
                       if);
                       
                   if);
                   
                // ascii.cr // 3 then
                   true->justReturned; onReturn; 
               if);
               INNER ;
               
            #);
          onBeforeChange:: 
            (# str: ^text; 
            do
               (if editable then
                   (if justReturned then
                       allowReturn->allow; false->justReturned; 
                    else
                       (if controlModified then
                           false->allow
                        else
                           (if length > 0 then
                               theText->str[];
                               l: str.scanAll
                                 (# 
                                 do
                                    ch->filter->allow;
                                    (if not allow then leave l;  if);
                                    
                                 #);
                               
                           if)
                       if);
                       
                   if);
                   
                else
                   settingContents->allow; 
               if);
               
            #);
          
       #);
     fitToContents:: 
       (# st: ^textStyle; width,height: @integer
       do
          defaultStyle->st[];
          size->(width,height);
          st.lineHeight+8->height;
          (width,height)->size;
          true->doneInInner
       #);
     editableFlag,allowReturnFlag,settingContents,justReturned: @boolean;
     theNext: ^dialogField;
     
  #);
  

