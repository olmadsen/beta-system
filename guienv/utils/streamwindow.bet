ORIGIN '~beta/guienv/guienv';
BODY 'private/streamwindowbody';

--GuienvLib: attributes--
streamwindow: window
  (* A separate scrolled window, which displays the text put to the
   * stream 'theStream'.
   * theStream buffers output to prevent too frequent
   * updates of the text output.
   * Use flush to ensure everything is displayed in 
   * text output.
   * Should only be 'open'-ed once. Call 'show' and 'hide'
   * to make the window visible/invisible.
   *)
  (# theStream: @stream<<SLOT StreamWindowStreamBody: MainPart>>;
     close::<(# <<SLOT streamwindowClose: dopart>> #);
     eventHandler::<
       (# 
          onAboutToClose::< (# <<SLOT streamwindowOnAboutToClose: dopart>> #);
       #);
     open::< (# <<SLOT StreamWindowOpen: DoPart>> #);
     show: <<SLOT StreamWindowShow: Descriptor>>;
     flush: <<SLOT StreamWindowFlush: Descriptor>>;
     vanish: (* deletes logfile, if opened *)
       (# <<SLOT StreamWindowVanish: DoPart>> #);
     logfilename: ^text
       (* If specified, logs output to file with that name too *);
     hideOnCloseByUser:@Boolean;
       (* If true then window will be hide when a user "click" on close *);
     saveAs:
       (* Opens a standard file dialog which prompts for a filename.
        * If this dialog is confirmed, the text written to THIS(StreamWindow)
        * is written to the named file.
        *)
       (# dialogtitle: ^text;
       enter dialogtitle[]
       do <<SLOT StreamWindowSaveAs: Descriptor>>
       #);
     private: @<<SLOT StreamWindowPrivate: descriptor>>;
  exit theStream[]
  #);

