ORIGIN '../guienv';
INCLUDE 'row'
        '~beta/guienv/controls' '~beta/guienv/graphics' '~beta/basiclib/formatio';
INCLUDE '~beta/sysutils/errorscreen';
INCLUDE 'tooltip';
-- windowitemlib: Attributes --
toolbartool: canvas
  (# 
     onDraw:<(# do INNER #);
     onSelect:<(# do INNER #);
     onEnable:<(# enable:@boolean; do enabled->enable; inner; enable->enabled #);
     ContentsWidth:<IntegerValue;
     ContentsHeight:<IntegerValue;
     cw,ch:@integer;
     enabled:@boolean;
     tooltip:^text;
     open::<(# do ContentsWidth->cw; ContentsHeight->ch; (cw+4->cw,ch+4->ch)->size; true->enabled; inner; onDraw #);
     drawUp:@graphics
       (# do 
          father.backgroundColor->pen.foregroundColor;
          ((2,2),(cw-3,ch-3))->fillRect;
          (0,0,0)->pen.foregroundColor;
          ((0,0),(cw,ch))->drawRect;
          (0xe7e7,0xe7e7,0xe7e7)->pen.foregroundColor;
          ((1,1),(cw-2,1))->drawLine;
          ((1,2),(1,ch-2))->drawline;
          (0x6f6f,0x6f6f,0x6f6f)->pen.foregroundColor;
          ((2,ch-2),(cw-2,ch-2))->drawline;
          ((cw-2,2),(cw-2,ch-2))->drawline;
          onDraw
       #);
     drawDown:@graphics
       (# do 
          (0x6f6f,0x6f6f,0x6f6f)->pen.foregroundColor;
          ((1,1),(cw-2,1))->drawLine;
          ((1,2),(1,ch-2))->drawline;
          (0xe7e7,0xe7e7,0xe7e7)->pen.foregroundColor;          
          ((1,cw-1),(cw-1,ch-1))->drawline;
          ((cw-1,1),(cw-1,ch-1))->drawline;
          (0xa3a3,0xa3a3,0xa3a3)->pen.foregroundColor;
          ((2,2),(cw-2,cw-2))->fillRect;
          onDraw
       #);
     clear:@graphics
       (# 
       do father.backgroundColor->pen.foregroundColor;
          ((0,0),(cw,ch))->fillRect;
          ((2,2),(cw-3,ch-3))->fillRect;
          ((0,0),(cw,ch))->drawRect;
          ((1,1),(cw-1,ch-1))->drawRect;
          onDraw
       #);
     eventhandler::<
       (# 
          onMouseDown::(# do (if enabled then drawDown; inner if) #);
          onMouseUp::(# do (if enabled then drawUp if); inner; (if enabled then onSelect if) #);
          onMouseEnter::
            (# 
            do (if enabled then drawUp;inner if);
            #);
          onMouseLeave::
            (# 
            do clear;inner 
            #);
          onRefresh::(# do clear #)
       #)
  #);
pixmapTool:toolBarTool
  (# pm,disabledpm:@pixmap;
     ContentsWidth::(# do pm.width->value #);
     ContentsHeight::(# do pm.height->value #);
     init:
       (# pmname,disabledPmName:^text;
       enter (pmname[],disabledPmName[])
       do pmname[]->pm.read
          (# error::(# stderr:@errorscreen
                    do what[]->stderr.putline; terminate
                    #) 
          #);
          (if disabledPmName[]<>none then
              disabledPmName[]->disabledpm.read
              (# error::(# stderr:@errorscreen do what[]->stderr.putline; terminate #) #)
           else
              pmname[]->disabledPm.read
              (# error::(# stderr:@errorscreen do what[]->stderr.putline; terminate #) #)
          if)
       #);
     onDraw::
       (# 
       do graphics
          (# 
          do (if enabled then
                 (pm[],(0,0),(2,2),pm.width,pm.height)->drawRaster
              else
                 (disabledpm[],(0,0),(2,2),pm.width,pm.height)->drawRaster
             if)
          #)
       #)
  #);

-- windowlib: Attributes --
toolbar: row
  (#
     IconSpacing:<IntegerValue(# do 0->value #);
     tools:@list(# element::toolbartool #);
     statechanged:<
       (# 
       do tools.scan
          (# cs:@boolean;
          do current.enabled->cs;
             current.onEnable; 
             (if cs<>current.enabled then
                 current.clear
             if)
          #);
          inner
       #);
     AddTool:
       (# tbt:^toolbartool
       enter tbt[]
       do tbt[]->tools.append;
       #);
     open::< 
       (# w,h:@integer;
       do father.size->(w,h);
          IconSpacing->margin.Right;
          0->margin.Left;
          0->distance;
          INNER;
          MININT->w;MININT->h;
          tools.scan
          (# 
          do this(toolbar)[]->current.open;
             current.onEnable;
             (h,current.ch)->max->h;
             (if current.tooltip[]<>none then current.tooltip[]->current.addToolTip if)
          #);
          (w-2,h)->size;h->putint;
          (1,40)->position;
       #);
     close::(# do margin.left->putint; margin.right->putint; distance->putint #);
  #)  

