ORIGIN '../guienv';
INCLUDE '~beta/interfacebuilder/support/row'
        '~beta/guienv/controls' '~beta/guienv/graphics' '~beta/basiclib/formatio';
-- windowitemlib: Attributes --
toolbartool: canvas
  (# 
     onDraw:<(# do INNER #);
     onSelect:<(# do INNER #);
     thesize:(# exit 40 #);
     open::<(# do (thesize,thesize)->size; inner; onDraw #);
     drawUp:@graphics
       (# do 
          father.backgroundColor->pen.foregroundColor;
          ((2,2),(thesize-3,thesize-3))->fillRect;
          (0,0,0)->pen.foregroundColor;
          ((0,0),(thesize,thesize))->drawRect;
          (0xe7e7,0xe7e7,0xe7e7)->pen.foregroundColor;
          ((1,1),(thesize-2,1))->drawLine;
          ((1,2),(1,thesize-2))->drawline;
          (0x6f6f,0x6f6f,0x6f6f)->pen.foregroundColor;
          ((2,thesize-2),(thesize-2,thesize-2))->drawline;
          ((thesize-2,2),(thesize-2,thesize-2))->drawline;
          onDraw
       #);
     drawDown:@graphics
       (# do 
          (0x6f6f,0x6f6f,0x6f6f)->pen.foregroundColor;
          ((1,1),(thesize-2,1))->drawLine;
          ((1,2),(1,thesize-2))->drawline;
          (0xe7e7,0xe7e7,0xe7e7)->pen.foregroundColor;          
          ((1,thesize-1),(thesize-1,thesize-1))->drawline;
          ((thesize-1,1),(thesize-1,thesize-1))->drawline;
          (0xa3a3,0xa3a3,0xa3a3)->pen.foregroundColor;
          ((2,2),(thesize-2,thesize-2))->fillRect;
          onDraw
       #);
     clear:@graphics
       (# 
       do father.backgroundColor->pen.foregroundColor;
          ((0,0),(thesize,thesize))->fillRect;
          ((2,2),(thesize-3,thesize-3))->fillRect;
          ((0,0),(thesize,thesize))->drawRect;
          ((1,1),(thesize-1,thesize-1))->drawRect;
          onDraw
       #);
     eventhandler::
       (# 
          onMouseDown::(# do drawDown;inner #);
          onMouseUp::(# do drawUp;inner;onSelect #);
          onMouseEnter::
            (# 
            do drawUp;inner
            #);
          onMouseLeave::
            (# 
            do clear;inner
            #);
          onRefresh::(# do clear #)
       #)
  #);
pixmapTool:toolBarTool
  (# pm:@pixmap;
     init:
       (# pmname:^text;
       enter pmname[]
       do pmname[]->pm.read
       #);
     onDraw::
       (# 
       do graphics
          (# 
          do (pm[],(0,0),(0,0),pm.width,pm.height)->drawRaster
          #)
       #)
  #);

-- windowlib: Attributes --
toolbar: row
  (#
     tools:@list(# element::toolbartool #);
     AddTool:
       (# tbt:^toolbartool
       enter tbt[]
       do tbt[]->tools.append;
       #);
     open::< 
       (# w,h:@integer;
       do father.size->(w,h);
          (w-2,45)->size;
          (1,1)->position;
          INNER;
          tools.scan
          (# do this(toolbar)[]->current.open
          #)
       #)
  #)  

