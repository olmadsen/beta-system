ORIGIN '../guienv';
INCLUDE '~beta/interfacebuilder/support/row'
        '~beta/guienv/controls' '~beta/guienv/graphics' '~beta/basiclib/formatio';
-- windowitemlib: Attributes --
toolbartool: canvas
  (# 
     onDraw:<(# do INNER #);
     onSelect:<(# do INNER #);
     ContentsWidth:<IntegerValue;
     ContentsHeight:<IntegerValue;
     cw,ch:@integer;
     open::<(# do ContentsWidth->cw; ContentsHeight->ch; (cw+4->cw,ch+4->ch)->size; inner; onDraw #);
     drawUp:@graphics
       (# do 
          father.backgroundColor->pen.foregroundColor;
          ((2,2),(cw-3,ch-3))->fillRect;
          (0,0,0)->pen.foregroundColor;
          ((0,0),(cw,ch))->drawRect;
          (0xe7e7,0xe7e7,0xe7e7)->pen.foregroundColor;
          ((1,1),(cw-2,1))->drawLine;
          ((1,2),(1,ch-2))->drawline;
          (0x6f6f,0x6f6f,0x6f6f)->pen.foregroundColor;
          ((2,ch-2),(cw-2,ch-2))->drawline;
          ((cw-2,2),(cw-2,ch-2))->drawline;
          onDraw
       #);
     drawDown:@graphics
       (# do 
          (0x6f6f,0x6f6f,0x6f6f)->pen.foregroundColor;
          ((1,1),(cw-2,1))->drawLine;
          ((1,2),(1,ch-2))->drawline;
          (0xe7e7,0xe7e7,0xe7e7)->pen.foregroundColor;          
          ((1,cw-1),(cw-1,ch-1))->drawline;
          ((cw-1,1),(cw-1,ch-1))->drawline;
          (0xa3a3,0xa3a3,0xa3a3)->pen.foregroundColor;
          ((2,2),(cw-2,cw-2))->fillRect;
          onDraw
       #);
     clear:@graphics
       (# 
       do father.backgroundColor->pen.foregroundColor;
          ((0,0),(cw,ch))->fillRect;
          ((2,2),(cw-3,ch-3))->fillRect;
          ((0,0),(cw,ch))->drawRect;
          ((1,1),(cw-1,ch-1))->drawRect;
          onDraw
       #);
     eventhandler::
       (# 
          onMouseDown::(# do drawDown;inner #);
          onMouseUp::(# do drawUp;inner;onSelect #);
          onMouseEnter::
            (# 
            do drawUp;inner
            #);
          onMouseLeave::
            (# 
            do clear;inner 
            #);
          onRefresh::(# do clear #)
       #)
  #);
pixmapTool:toolBarTool
  (# pm:@pixmap;
     ContentsWidth::(# do pm.width->value #);
     ContentsHeight::(# do pm.height->value #);
     init:
       (# pmname:^text;
       enter pmname[]
       do pmname[]->pm.read
       #);
     onDraw::
       (# 
       do graphics
          (# 
          do (pm[],(0,0),(2,2),pm.width,pm.height)->drawRaster
          #)
       #)
  #);

-- windowlib: Attributes --
toolbar: row
  (#
     IconSpacing:<IntegerValue(# do 0->value #);
     tools:@list(# element::toolbartool #);
     AddTool:
       (# tbt:^toolbartool
       enter tbt[]
       do tbt[]->tools.append;
       #);
     open::< 
       (# w,h:@integer;
       do father.size->(w,h);
          IconSpacing->margin.Right;
          0->margin.Left;
          0->distance;
          INNER;
          MININT->w;MININT->h;
          tools.scan
          (# 
          do this(toolbar)[]->current.open;
             (h,current.ch)->max->h
          #);
          (w-2,h)->size;h->putint;
          (1,40)->position;
       #);
     close::(# do margin.left->putint; margin.right->putint; distance->putint #);
  #)  

