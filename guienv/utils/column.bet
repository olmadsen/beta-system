ORIGIN 'manager';
LIB_DEF 'frigg_col' '../../lib';

-- windowLib: Attributes --
column: manager
  (#
     open::<  (#  do INNER ;  #);
     computeLayout:: 
       (#
          width,height: @integer;
          y,needed,count: @integer;
          maxWidth: @integer;
          last: ^windowItem;
          
       do
          size->(width,height);
          iterate
            (# w,h: @integer
            do
               current[]->last[];
               current.size->(w,h);
               (w,maxWidth)->Max->maxWidth;
               needed+h->needed;
               count+1->count;
               
            #);
          (if count <> 0 then
              needed+margin.top+margin.bottom+(count-1)*distance->needed;
              (if vertical.wrap then
                  margin.top->y
               else
                  (if vertical.alignment
                   // alignTop then
                      margin.top->y
                   // alignBottom then
                      height-needed+margin.top->y
                   // alignCenter then
                      (height-needed) div 2+margin.top->y
                  if)
              if);
              (if horizontal.wrap then
                  maxWidth+margin.left+margin.right->width
              if);
              (if vertical.wrap then needed->height if);
              iterate
                (# w,h: @integer
                do
                   current.size->(w,h);
                   (if horizontal.alignment
                    // alignLeft then
                       (margin.left,y)->current.position
                    // alignCenter then
                       (margin.left+(width-margin.left-margin.right-w) div 2,y)
                         ->current.position
                    // alignRight then
                       ((width-margin.right-w),y)->current.position
                   if);
                   y+h+distance->y;
                   
                #);
              (width,height)->size
           else
              (if vertical.wrap then
                 margin.top+margin.bottom -> height;
              if);
              (if horizontal.wrap then
                 margin.left+margin.right -> width;
              if);
              (width, height)->size
          if);
          
       #)
  #)  

