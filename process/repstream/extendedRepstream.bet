ORIGIN '~beta/basiclib/v1.4/betaenv';
BODY 'private/extendedRepstreamBody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1993-94
 *       All rights reserved.
 *)
[[
--- lib:attributes ---

ExtendedRepStream:
  (# R: [1]@integer;
     lgth: @integer; (* Valid contents of this(ExtendedRepstream): [1..lgth] *)
     pos: @integer; (* Next read/write uses this(ExtendedRepstream)[pos..] *)
     
     (* exceptions *)
     repException: @repExceptionType;
     repExceptionType:< exception(# enter msg do INNER #);

     (* operations *)
     clear: @(# do 0->pos->lgth #);
     truncate: @(# enter setPos do pos->lgth #);
     reset: @(# do 0->pos #);
     length: @(# exit lgth #);
     getPos: @(# exit pos #);
     setPos:@(# p: @integer enter p do ((p,0)->max,lgth)->min->pos #);
     setPosEnd: @(# do lgth->pos #);
     eos: @(# exit pos>=lgth #);
     
     put: @
       (# ch: @char;
       enter ch
       do (if (pos>=r.range) // true then r.range -> r.extend if);
	  ch -> r[pos+1 -> pos];
          (if (pos>lgth)//true then pos->lgth if);
       #);

     putInt: @
       (# i: @integer;
       enter i
       do (if (pos>=r.range) // true then r.range -> r.extend if);
	  i -> r[pos+1 -> pos];
          (if (pos>lgth)//true then pos->lgth if);
       #);

     putText: @
       (# t: ^text;
          pt: @<<SLOT ExtendedRepStreamPutTextBody:descriptor>>
       enter t[]
       do pt
       #);

     get: @
       (#
       do (if (pos>=lgth)//true then
              'get beyond end of stream' -> repException;
          if)
       exit r[pos+1 -> pos]
       #);

     getInt: @
       (#
       do (if (pos>=lgth)//true then
              'getInt beyond end of stream' -> repException;
          if)
       exit r[pos+1 -> pos]
       #);

     extendTo: @
       (# n: @integer;
       enter n
       do
          (if (r.range<n) // true then
              (n-r.range) -> r.extend
          if);
       #);

     getText: @
       (# t: ^text;
          gt: @<<SLOT ExtendedRepStreamGetTextBody:descriptor>>
       do gt
       exit t[]
       #);
  #);

---]]
