ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/sysutils/v1.6/endian';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1993-95
 *       All rights reserved.
 *)

BODY 'private/extendedRepstreamBody';

--- lib:attributes ---

ExtendedRepStream:
  (# R: [1]@integer;
     lgth: @integer; (* Valid contents of this(ExtendedRepstream): [1..lgth] *)
     pos: @integer; (* Next read/write uses this(ExtendedRepstream)[pos..] *)
     
     (* exceptions *)
     repException: @repExceptionType;
     repExceptionType:< exception(# enter msg do INNER #);

     (* operations *)
     clear: @(# do 0->pos->lgth #);
     truncate: @(# enter setPos do pos->lgth #);
     reset: @(# do 0->pos #);
     length: @(# exit lgth #);
     getPos: @(# exit pos #);
     setPos:@(# p: @integer enter p do ((p,0)->max,lgth)->min->pos #);
     setPosEnd: @(# do lgth->pos #);
     eos: @(# exit pos>=lgth #);
     
     put: @
       (# ch: @char;
       enter ch
       do (if pos>=r.range then r.range -> r.extend if);
	  ch -> htonl -> r[pos+1 -> pos];
          (if pos>lgth then pos->lgth if);
       #);

     putInt: @
       (# i: @integer;
       enter i
       do (if pos>=r.range then r.range -> r.extend if);
	  i -> htonl -> r[pos+1 -> pos];
          (if pos>lgth then pos->lgth if);
       #);

     putText: @
       (# t: ^text;
       enter t[]
       <<SLOT ExtendedRepStreamPutTextBody:dopart>>
       #);

     get: @
       (#
       do (if pos>=lgth then
              'get beyond end of stream' -> repException;
          if)
       exit (r[pos+1 -> pos] -> ntohl)
       #);

     getInt: @
       (#
       do (if pos>=lgth then
              'getInt beyond end of stream' -> repException;
          if)
       exit (r[pos+1 -> pos] -> ntohl)
       #);

     extendTo: @
       (# n: @integer;
       enter n
       do
          (if r.range<n then
              (n-r.range) -> r.extend
          if);
       #);

     getText: @
       (# t: ^text;
       <<SLOT ExtendedRepStreamGetTextBody:dopart>>
       exit t[]
       #);

     (* private *)
     private: @<<SLOT ExtendedRepStreamPrivate:descriptor>>;
  #);


