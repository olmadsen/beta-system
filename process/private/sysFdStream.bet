ORIGIN '~beta/basiclib/basicsystemenv';
LIB_DEF 'processsysfdstream' '../../lib';

MDBODY nti     'sysFdStream_nt'
       mac     'sysFdStream_mac'
       ppcmac  'sysFdStream_mac'
       jvm     'sysFdStream_jvm'
       clr     'sysFDStream_clr'
       default 'sysFdStream_unix';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1993-97
 *       All rights reserved.
 *)

--- systemlib:attributes ---

(* fdStream creates a normal beta stream interface to a UNIX file descriptor.
 *)

fdStream: stream
  (#
     Put::< (# bfPut: @<<SLOT fdStreamPut:descriptor>> do INNER; bfPut #);
     Get::< (# bfGet: @<<SLOT fdStreamGet:descriptor>> do INNER; bfGet #);
     Peek::< (# bfPeek: @<<SLOT fdStreamPeek:descriptor>> do INNER; bfPeek #);
     eos::< (# bfEos: @<<SLOT fdStreamEos:descriptor>> do INNER; bfEos #);

     PutText::<
       (# bfPutText: @<<SLOT fdStreamPutText:descriptor>>;
       do bfPutText;
          INNER;
       #);
     GetLine::< (# do <<SLOT fdStreamGetLine:descriptor>> #);
     GetAtom::< (# do <<SLOT fdStreamGetAtom:descriptor>> #);

     GetRep:
       (# repAdr: @integer; (* @@rep[inx]: start address *)
          length: @integer; (* max. no. of elements to read *)
          doGet: @<<SLOT fdStreamGetRep:descriptor>>;
          (* must be static to prevent garbage collection! *)
       enter(repAdr,length)
       do doGet
       exit(length div 4)
       #);

     PutRep:
       (# repAdr (* @@rep[inx]: start address *),
          length (* no. of rep-elements to be and was written *): @integer;
          doPut: @<<SLOT fdStreamPutRep:descriptor>>;
          (* must be static to prevent garbage collection! *)
       enter(repAdr,length)
       do doPut
       exit(length div 4)
       #);

     OpenRead:< (# do INNER; <<SLOT fdStreamOpenRead:descriptor>> #);
     OpenWrite:< (# do INNER; <<SLOT fdStreamOpenWrite:descriptor>> #);
     OpenReadWrite:< (# do INNER;<<SLOT fdStreamOpenReadWrite:descriptor>>#);
     OpenExeWrite:< (# do INNER; <<SLOT fdStreamOpenExeWrite:descriptor>> #);

     Flush:< (* Flushes THIS(fdStream).
              * Has only an effect on streams opened for output.
              *)
       (# do <<SLOT fdStreamFlush:descriptor>>; INNER; #);

     Close:< (* Closes THIS(fdStream) *)
       (# do <<SLOT fdStreamClose:descriptor>>; INNER; #);

     fdStreamException: StreamException
       (# message: @Text;
       enter message
       do <<SLOT fdStreamException:descriptor>>;
   	  INNER;
       #);

     (* Raised when attempted to access an fdStream. Message: "Insufficient
      * access privileges".
      *)
     AccessError:< fdStreamException
        (#
	do 'Insufficient access privileges' -> msg.putline;
	   INNER AccessError;
	#);
     (* Raised from Get and Peek when attempted to read a non-existing
      * block. Message: "Read block error".
      *)
     ReadError:< fdStreamException
        (#
	do 'Read block error' -> msg.putline;
	   INNER ReadError;
	#);
     (* Raised from Put, PutText and Flush when attempted to write on a
      * non-existing block. Message: "Write block error".
      *)
     WriteError:< fdStreamException
        (#
	do 'Write block error' -> msg.putline;
	   INNER WriteError;
	#);
     (* Raised when the file system is full. Message:
      * "File system is full".
      *)
     NoSpaceError:< fdStreamException
        (#
	do 'File system is full' -> msg.putline;
	   INNER NoSpaceError;
	#);
     OtherError::< fdStreamException;

     private: @<<SLOT fdStreamPrivate:descriptor>>;
  #);

readChannel: fdStream
  (#
     openWrite::< (# do 'called openWrite on a readChannel' -> otherError #);

     put::< (# do 'Called put in a readChannel' -> OtherError #);

  #); (* read channel *)

writeChannel: fdStream
  (#
     openRead::< (# do 'called openRead on a WriteChannel' -> otherError #);

     get::< (# do 'Called get in a writeChannel' -> OtherError #);

  #); (* write channel *)

