ORIGIN '../idScheduler';
INCLUDE '~beta/basiclib/v1.4/timehandler';
INCLUDE '~beta/containers/v1.4/list';

(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1994
 *       All rights reserved.
 *)

--- idSchedulerPrivate:descriptor ---
(#
   elements: @list(# element::< idSchedElement #);
#)

--- idSchedulerInit:descriptor ---
(#
do private.elements.init;
   INNER init;
#)

--- idSchedulerSuspend:descriptor ---
(#
do
   &isElement[]->elm[];
   id->elm.id;
   elm[]->private.elements.append;
   elm.suspend_sem.P;
#)

--- idSchedulerResume:descriptor ---
(#
do
   L: (if 1//1 then
          private.elements.iterate (* !!! optimize alert *)
          (#
          do
             (if current.elm.id // id then 
                 current.elm[]->elm[];
                 current[]->private.elements.delete;
                 found;
                 elm.suspend_sem.V;
                 leave L;
             if);
          #);
          
          (* tell user no element had the right 'id' *)
          not_found;
      if);
#)

--- idTimeoutSchedulerPrivate:descriptor ---
(#
   timeHnd: @timeHandler;
#)

--- idTimeoutSchedulerInit:descriptor ---
(#
do private2.timeHnd.init;
   INNER init;
#)

--- idTimeoutSchedulerTOSuspend:descriptor ---
(#
   timedoutFlag: @boolean;
   timeHandlerID: @integer;
   onTimeOut: @(# do true->timedoutFlag; elm.suspend_sem.V; #);
do
   &isElement[]->elm[];
   id->elm.id;
   elm[]->private.elements.append;
   L: (if 1//1 then
          false->timedoutFlag;
          (onTimeOut[],timeoutValue)
            ->private2.timeHnd.register
            ->timeHandlerID;
          elm.suspend_sem.P;
          (if timedoutFlag // true then
              (if retry // true then
                  (* wait-and-see *)
                  restart L;
               else
                  (* failure *)
                  elm[]->private.elements.at->private.elements.delete;
                  onTimeout;
              if);
           else
              (* success *)
              timeHandlerID->private2.timeHnd.unregister;
              onSuccess;
          if);
      if);
#)
