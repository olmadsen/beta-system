ORIGIN '../processmanager';
MDBODY mac     'processmanager_macbody'
       nti     'processmanager_ntbody'
       default 'processmanager_unixbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1992,93,94
 *       All rights reserved.
 *)
[[
--- lib:attributes ---

argDecodeSeparator: (# exit 1 #);
   (* The arguments to a process are encoded into a text. The 
    * arguments are separated by "argDecodeSeparator" in the 
    * resulting text. 
    *)

--- putArg:descriptor ---
(# 
do T[] -> argument.puttext;
   argDecodeSeparator -> argument.put;
#)

--- ProcessScanArguments:descriptor ---
(# 
do current.clear;
   argument.scanAll 
   (# 
   do (if ch//argDecodeSeparator then
          INNER ScanArguments;
          current.clear;
       else
          ch -> current.put;
      if);
   #);
#)

--- ProcessInit:descriptor ---
(# 
do argument.argument.clear
#)

--- ProcessStart:descriptor ---
(#
do
   (if stillRunning 
    // false then 
       <<SLOT ProcessStartMDBody:descriptor>>;
       onStart;
    // true then 'start: two current' -> twoCurrent;
   if);
#)

--- ProcessStop:descriptor ---
(#
do
   <<SLOT ProcessStopMDBody:descriptor>>;
   wait: (if stillRunning // true then restart wait if);
   onStop;
#)


--- ProcessManagerException:descriptor ---
(#  
do	
   'ProcessManager exception. Error id: '-> msg.puttext;
   <<SLOT ProcessManagerExceptionMDBody:descriptor>>;
   msg.newline;
   message[] -> msg.puttext;
#)

---]]
