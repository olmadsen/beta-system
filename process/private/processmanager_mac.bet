ORIGIN 'processmanagerbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1992,93,94
 *       All rights reserved.
 *)
[[
--- include '~beta/basiclib/v1.5/private/unixinterface'

--- ProcessManagerPrivate:descriptor ---
(#
	pid: @Integer;
	inputIndex, outputIndex: @assignGuard(# rep: @Integer enter rep exit rep #);
#)

--- ProcessStartMDBody:descriptor---
(#
do	(if private.inputIndex.assigned // false then 0 -> private.inputIndex if);
	(if private.outputIndex.assigned // false then 1 -> private.outputIndex if);
	(name,argument,private.inputIndex,private.outputIndex) ->
		&startUnixProcess -> pid;
	(if pid // -1  then 'failed to start process ' -> error if);
	0 -> private.inputIndex;
	1 -> private.outputIndex;
#)

--- ProcessStopMDBody:descriptor ---
(#
do	(if ( pid -> &stopUnixProcess)
	 // -1 then 'could not stop the process' -> error
	if);
#)

--- ProcessAwaitStopped:descriptor ---
(#
do	(if stillRunning // true then
	    (if (pid -> &awaitNotExecuting)
		 // -1 then 'awaitStopped' -> error;
		 // 0  then (* ?????? *) 
         // 1  then (* exited *)
         // 2  then (* killed *)
        if)
	if);
#)

--- ProcessStillRunning:descriptor ---
(#
do	(if (pid -> &stillExecuting)
	 // -1 then 'stillRunning failed' -> error
	 // 0  then false -> result
	 // 1  then (pid > 0) -> result
	if);
#)

--- connectToProcess:descriptor ---
(* (THIS(Process),toProcess) => firstProc | toProcess
 * Note that more than 2 processes may be connected in that
 * way. This is obtained by several calls of THIS operation.
 *)
(#	aPipe: ^pipe;
do	&Pipe[] -> aPipe;
	aPipe.init;
	(THIS(Process)[],aPipe.writeEnd[]) -> redirectToOneWayChannel;
	(toProcess[],aPipe.readEnd[]) -> redirectFromOneWayChannel;
#)

--- redirectFromFile:descriptor ---
(* (THIS(Process),inputFile) => inputFile > THIS(Process) *)
(#
do	inputFile.openRead;
	inputFile.Index -> THIS(Process).private.inputIndex;
#)

--- redirectToFile:descriptor ---
(* (THIS(Process),outputFile) => THIS(Process) > outputFile *)
(#
do	outputFile.openWrite;
	outputFile.Index -> aProc.outputIndex;
#)

--- redirectFromOneWayChannel:descriptor ---
(* (THIS(Process),aChannel) => 
 * Redirect the input to THIS(Process) from inputChannel.
 *)
(# do inputChannel.Index -> THIS(Process).inputIndex #)

--- redirectToOneWayChannel:descriptor ---
(* (THIS(Process),outputChannel) => 
 * Redirect the output from aProc to outputChannel.
 *)
(# do outputChannel.Index -> THIS(Process).outputIndex #)


--- ProcessManagerExceptionMDBody:descriptor ---
(# getErrno: external (# value: @Integer exit value #);
do getErrno -> errorstring -> msg.puttext;
#)

---]]
