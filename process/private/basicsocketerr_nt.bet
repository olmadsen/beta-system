ORIGIN 'basicsocketbody';

INCLUDE '~beta/win32lib/v1.6/nterrno';

--- BasicSocketEndOfDataEC:dopart ---
do 
   (if err
    // cdef_WSAENOTSOCK then                  (* From ioctlsocket() *)
       &connBrokenError[]->chosen[];
       
    // cdef_WSAEFAULT
    // cdef_WSAEINVAL then
       &internalError[]->chosen[];
       
    else
       &unknownError[]->chosen[];
   if);


--- BasicSocketSendMemEC:dopart ---
do
   (if err 
    // cdef_WSAENOTCONN			      (* From send() *)
    // cdef_WSAESHUTDOWN
    // cdef_WSAENOTSOCK
       (* probably end-of-client *)
    // cdef_WSAECONNABORTED
    // cdef_WSAENETRESET
    // cdef_WSAECONNRESET then
       (* hangup, probably end-of-client *)
       &theIdle.connBrokenError[]->chosen[];
       
    // cdef_WSAEINTR 
    // cdef_WSAEFAULT
    // cdef_WSAEINVAL then 
       &theIdle.internalError[]->chosen[];
       
    // cdef_WSAENOBUFS then
       &theIdle.resourceError[]->chosen[];
       
    else
       &theIdle.unknownError[]->chosen[];
   if);


--- BasicSocketRecvMemEC:dopart ---
do (if err
    // cdef_WSAENOTSOCK                   (* From recv() *)
    // cdef_WSAENOTCONN 
    // cdef_WSAECONNRESET then

       (* hangup, probably end-of-client *)
       &theIdle.connBrokenError[]->chosen[];
       
    // cdef_WSAEFAULT
    // cdef_WSAEINVAL then 
       &theIdle.internalError[]->chosen[];
       
    // cdef_WSAENOBUFS then 
       &theIdle.resourceError[]->chosen[];
       
    else
       'Uknown error: ' -> screen.putText; 
       err -> screen.putInt; screen.newLine;
       &theIdle.unknownError[]->chosen[];
   if);
   
   
--- BasicSocketConnectEC:dopart ---
do
   (if err
       
    // cdef_WSAEMFILE
    // cdef_WSAENOBUFS then
       &resourceError[]->chosen[];
       
    // cdef_WSANOTINITIALISED
    // cdef_WSAEAFNOSUPPORT
    // cdef_WSAESOCKTNOSUPPORT
    // cdef_WSAEPROTONOSUPPORT
    // cdef_WSAEPROTOTYPE then
       &internalError[]->chosen[];
       
    // cdef_WSAEADDRINUSE                  (* From connect() *)
    // cdef_WSAEADDRNOTAVAIL
    // cdef_WSAENETUNREACH then 
       &addressError[]->chosen[];
       
    // cdef_WSAEAFNOSUPPORT
    // cdef_WSAEALREADY
    // cdef_WSAEBADF
    // cdef_WSAEFAULT
    // cdef_WSAEINPROGRESS
    // cdef_WSAEINVAL
    // cdef_WSAEISCONN
    // cdef_WSAENOTSOCK then 
       &internalError[]->chosen[];
       
    // cdef_WSAECONNREFUSED
    // cdef_WSAETIMEDOUT then
       &refusedError[]->chosen[];
       
    // cdef_WSAEINTR then 
       &intrError[]->chosen[];
       
    else
       &unknownError[]->chosen[];
   if);

