ORIGIN '~beta/process/streamsocket';
--- systemlib:attributes ---
rsh:
  (* On windows, this works just fine.
   * On unix, the program must be run as root or
   * setuid root.  This is not a good thing to do,
   * so on unix it is better to open a pipe to an external rsh command
   *)
  (# sockType:< StreamSocket
       (# error::
            (# 
            do errCB_abortOperation->value;
               true->failed;
            #)
       #);
     s: ^sockType;
     host, localuser, remoteuser, command: ^Text;
     result: @Char;
     failed: @Boolean;
  enter (host[], localuser[], remoteuser[], command[])
  do &sockType[]->s[];
     (Waitforever, host[], 514)->s.connect
     (# firstLocalPort::(# do 1023->value #);
        lastLocalPort::(# do 512->value #);
     #);
     (if not failed then
         '0'->s.put; 0->s.put;
         localuser[]->s.putText; 0->s.put;
         remoteuser[]->s.putText; 0->s.put;
         command[]->s.putText; 0->s.put;
         s.flush;
     if);
     (if not failed then
         s.get->result;
         (if result<>0 then true->failed if);
     if);
     (if failed then s.close; NONE->s[]; if);
  exit s[]
  #);
   
