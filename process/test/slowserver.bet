ORIGIN '~beta/guienv/v1.6/guienvsystemenv';
INCLUDE '~beta/process/v1.6/processmanager';
INCLUDE '~beta/process/v1.6/binarygenerator';
INCLUDE '~beta/guienv/v1.6/fields';
INCLUDE '~beta/guienv/v1.6/stddialogs';

--- program:descriptor ---
systemEnv
(# setWindowEnv::< (# do myWindowEnv[] -> theWindowEnv[] #);
   
   myWindowEnv: @guienv
     (#
     #);
   
   server: system
     (# clientSocket: ^BinarySocket;
        id, i: @integer;
     do (* Communicate with the clients *)
        1 -> i; 
        cycle
        (#
        do 'id ' -> putText; id -> putInt;
           ' Sending ' -> putText; i -> putInt; newLine;
           (2, i) -> clientSocket.putInt;
           i + 1 -> i;
           3 -> sleep;
        #);
     #);
   
   client1: @Process;
   sockGen: @socketGenerator;
   idbase: @integer;
   aServer: ^|server;
   aSocket: ^BinarySocket;
   
do (* communicate via socket using number 5000 *)
   5000 -> sockGen.bind;
   'Binding port.' -> putText; newLine;
   
   (* Start the program 'texteditclient', connect through 'asocket' *)
   'texteditclient' -> client1.init;
   client1.start;
   
   waitForever -> sockGen.getBinaryConnection -> aSocket[];
   &|server[] -> aServer[];
   idbase -> aServer.id;
   aSocket[] -> aServer.clientSocket[];
   aServer[] -> fork;
   
   cycle
   (#
   do idbase + 1 -> idbase;
      
      waitForever -> sockGen.getBinaryConnection -> asocket[];
      &|server[] -> aServer[];
      idbase -> aServer.id;
      aSocket[] -> aServer.clientSocket[];
      aServer[] -> fork;
   #)   
#)

