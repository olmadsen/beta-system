ORIGIN '../idTimeoutScheduler';
INCLUDE '~beta/basiclib/v1.5/timehandler';

(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1994
 *       All rights reserved.
 *)

--- idTimeoutSchedulerPrivate:descriptor ---
(#
   timeHnd: @timeHandler;
#)

--- idTimeoutSchedulerInit:descriptor ---
(#
do private2.timeHnd.init;
   INNER init;
#)

--- idTimeoutSchedulerTOSuspend:descriptor ---
(#
   timedoutFlag: @boolean;
   timeHandlerID: @integer;
   onTimeOut: @(# do true->timedoutFlag; elm.suspend_sem.V; #);
do
   &isElement[]->elm[];
   id->elm.id;
   elm[]->private.elements.append;
   L: (if 1//1 then
          false->timedoutFlag;
          (onTimeOut[],timeoutValue)
            ->private2.timeHnd.register
            ->timeHandlerID;
          elm.suspend_sem.P;
          (if timedoutFlag // true then
              (if retry // true then
                  (* wait-and-see *)
                  restart L;
               else
                  (* failure *)
                  elm[]->private.elements.at->private.elements.delete;
                  onFailure;
              if);
           else
              (* success *)
              timeHandlerID->private2.timeHnd.unregister;
              onSuccess;
          if);
      if);
#)
