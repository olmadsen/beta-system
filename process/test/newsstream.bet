ORIGIN '~beta/basiclib/v1.5/systemenv';
INCLUDE '~beta/process/v1.6/streamsocket';

--- program:descriptor ---
systemEnv
(#
   s: @StreamSocket
     (#
        getSome: @getLine
          (#
             idle:: (# do 'idle!'->screen.putLine #);
             blocking::(# do 'blocking!'->screen.putLine #);
          #);
     #);

   article: @Text;

do s.init;
   (20, 'news', 119) -> s.connect
   (#
   #);

   'Connected' -> putLine;
   s.getSome -> screen.putLine;

   (* 'xgtitle daim*' -> s.putLine;  *)
   (* 'body <4vuuv0$7b5@gjallar.daimi.aau.dk>' -> s.putLine;  *)
   'body <56te3i$lp5$1@gjallar.daimi.aau.dk>' -> s.putLine;

   (if s.peek='2' then (* 2xx means OK, result follows. *)
       s.getSome -> screen.putLine;
       L: (# t: ^Text;
          do s.getSome -> t[];
             (if (t.T[1]='.') and (t.T[2]<>'.') then leave L if);
             t[] -> article.putLine;
             restart L
          #)
    else
       s.getSome -> screen.putLine;
   if);

   article.length -> screen.putInt; screen.newLine; 

   'quit' -> s.putLine;

   L: (#
      do (if not s.eos then
             s.get -> screen.put;
             restart L
         if)
      #);

   s.close;
#)
