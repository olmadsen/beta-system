ORIGIN  '~beta/guienv/v1.4/guienvsystemenv';
INCLUDE '~beta/guienv/v1.4/fields';
INCLUDE '~beta/guienv/v1.4/stddialogs';
INCLUDE '~beta/basiclib/v1.5/file';
INCLUDE '~beta/process/v1.6/binarysocket';
-- program: descriptor --
systemEnv
(# setWindowEnv::< (# do myWindowEnv[] -> theWindowEnv[] #);
   
   myWindowEnv: @guienv
     (# textWindow: @window
          (# menubarType::
               (# fileMenu: @menu
                    (# textFile: @file;
                       openItem: @menuitem
                         (# eventHandler::
                              (# onSelect::
                                   (# tmpText: @styledText;
                                   do textWindow[]
                                        ->fileSelectionDialog
                                        ->textFile.name;
                                      textFile.openRead;
                                      textFile.scan
                                      (# while:: (# do true->value #)
                                      do ch->tmpText.put
                                      #);
                                      tmpText[]
                                        ->txtEdit.contents.contents;
                                      textFile.close
                              #) #);
                            open:: (# do 'Open'->name #)
                         #);
                       saveItem: @menuitem
                         (# eventHandler::
                              (# onSelect::
                                   (#
                                   do textFile.openWrite;
                                      txtEdit.contents.contents
                                        ->textFile.puttext;
                                      textFile.close
                              #) #);
                            open:: (# do 'Save'->name #)
                         #);
                       quitItem: @menuitem
                         (# eventHandler:: 
                              (# onSelect:: (# do terminate #) #);
                            open:: (# do 'Quit'->name #)
                         #);
                       open::
                         (# 
                         do 'File' -> name;
                            openItem.open; openItem[]->append;
                            saveItem.open; saveItem[]->append;
                            quitItem.open; quitItem[]->append
                    #) #);
                  open:: (# do fileMenu.open; fileMenu[] -> append #);
               #);
             txtEdit: @textEditor
               (# open::
                    (#
                    do textWindow.size->size;
                       true->bindBottom;
                       true->bindRight
               #) #);
             open::
               (#
               do 'textEditorFull'->title;
                  txtEdit.open
               #)
          #)
     do textWindow.open
     #);
   
   
   every: system
     (# i: @integer;
        aSock: @BinarySocket;
        texttmp: @styledText;
     do 
        'localhost' -> aSock.host; 5000 -> aSock.port;
        aSock.connect;
        
        cycle
        (# 
        do 5 -> aSock.getInt -> i;
           texttmp.clear;
           myWindowEnv.textWindow.txtEdit.contents.contents -> texttmp.puttext;
           'recieved: ' -> texttmp.putText; i -> texttmp.putInt; texttmp.newline;
           texttmp[] -> myWindowEnv.textWindow.txtEdit.contents.contents;
           0 -> sleep;
        #);
        aSock.close;
     #);
   
do conc (# do &|every[] -> start; #);
#)

