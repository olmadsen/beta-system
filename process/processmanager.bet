ORIGIN '~beta/basiclib/v1.5/basicsystemenv';

(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1992-95
 *       All rights reserved.
 *)

BODY 'private/processmanagerbody';
INCLUDE '~beta/basiclib/v1.5/file';

--- systemlib:attributes ---
BetaEnvStop: (# T: ^Text; I: @Integer;
             enter (I,T[])
             do (I,T[]) -> Stop;
             #);

Process:
  (* Notice, this(Process) can only be executed once.
   *
   * Two program executions of the same Process,
   * can be executed by instantiating and executing two different BETA 
   * objects from the same Process.
   *)
  (#
     <<SLOT ProcessLib:attributes>>;
     
     name: ^Text;
     init:< (# enter name[] <<SLOT ProcessInit:dopart>> #);
     
     argType:
       (# argument: @Text;
          putArg:
            (# t: ^Text;
            enter t[]
            <<SLOT putArg:dopart>>
            #);
          append: @putArg;
          scanArguments: (* calls inner for each argument *)
            (# current: @Text; 
            <<SLOT ProcessScanArguments:dopart>>
            #);
       #);
     argument: @argType; (* arguments to this(Process) *)
     
     
     (* operations *)
     
     start: (* starts this(Process)'s program execution *)
       (# error:< ProcessManagerException;
          twoCurrent:< ProcessManagerException;
       <<SLOT ProcessStart:dopart>>
       #);
     
     stop: (* stops this(Process)'s program execution *)
       (# error:< ProcessManagerException;
       <<SLOT ProcessStop:dopart>>
       #);
     
     awaitStopped: (* Returns when THIS(Process) stops *)
       (# error:< ProcessManagerException;
       <<SLOT ProcessAwaitStopped:dopart>>
       #);
     
     stillRunning: (* Returns true if THIS(Process) is still running *)
       (# error:< ProcessManagerException;
          value: @Boolean;
       <<SLOT ProcessStillRunning:dopart>>
       exit value
       #);
     
     (* input/output redirection *)
     
     connectToProcess:  (* connect output of this(process) to toProcess's input
                         * In Unix terms: this(Process) | toProcess
                         *)
       (# error:< ProcessManagerException;
          toProcess: ^Process;
       enter toProcess[]
       <<SLOT connectToProcess:dopart>>
       #);
     
     connectInPipe:  (* connect output of fromProcess to input of  this(process) 
                      * In Unix terms: fromProcess | this(Process)
                      *)
       (# error:< ProcessManagerException;
          fromProcess: ^Process;
       enter fromProcess[]
       <<SLOT connectInPipe:dopart>>
       #);
     
     redirectFromFile:  (* redirect input to this(process) from inputFile
                         * In Unix terms: this(Process) < inputFile
                         *)
       (# error:< ProcessManagerException;
          inputFile: ^File;
       enter inputFile[]
       <<SLOT redirectFromFile:dopart>>
       #);
     
     redirectToFile:  (* redirect output of this(process) to outputFile
                       * In Unix terms: this(Process) > outputFile
                       *)
       (# error:< ProcessManagerException;
          outputFile: ^File;
       enter outputFile[]
       <<SLOT redirectToFile:dopart>>
       #);
     
     redirectFromChannel:  (* redirect input to this(process) from inputChannel *)
       (# error:< ProcessManagerException;
          inputChannel: ^Stream;
       enter inputChannel[]
       <<SLOT redirectFromChannel:dopart>>
       #);
     
     redirectToChannel:  (* redirect output of this(process) to outputChannel *)
       (# error:< ProcessManagerException;
          outputChannel: ^Stream;
       enter outputChannel[]
       <<SLOT redirectToChannel:dopart>>
       #);
     
     (* Virtual callbacks: called when the proper action has occurred *)
     
     onStart:< (# do INNER #);
     onStop:< (# do INNER #);
     
     doDebug: @Boolean;
     private: @<<SLOT ProcessPrivate:descriptor>>;
  #);

ProcessManagerException: Exception
  (# message: ^Text;
  enter message[]
  <<SLOT ProcessManagerException:dopart>>
  #);

