ORIGIN '~beta/basiclib/v1.5/betaenv';
BODY   'private/processmanagerbody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1992,93,94
 *       All rights reserved.
 *)
[[
--- include '~beta/basiclib/v1.5/file'
--- include 'communication'
--- lib:attributes ---

(* The ProcessManager models the concepts of program executions and
 * communication between program executions.
 *
 * A program execution is modelled as a process.
 * A process can be started (executed) and stopped (terminated).
 * Processes can communicate to each other using either pipes or sockets.
 * Using pipes as communication model, processes can make simple communication
 * though redirection of standard input/output streams (screen and keyboard).
 * Using sockets as communication model, processes can communicate through
 * any specified stream.
 *)

(* Notice, this(Process) can only be executed once.
 *
 * Two program executions of the same Process,
 * can be executed by instantiating and executing two different BETA 
 * objects from the same Process.
 *)

Process:
  (#
     <<SLOT ProcessLib:attributes>>;
     
     name: ^Text;
     init:< (# enter name[] do <<SLOT ProcessInit:descriptor>>; INNER #);
     
     argType:
       (# argument: @Text;
          putArg:
            (# t: ^Text;
            enter t[]
            do <<SLOT putArg:descriptor>>
            #);
          append: @putArg;
          scanArguments: (* calls inner for each argument *)
            (# current: @Text; 
            do <<SLOT ProcessScanArguments:descriptor>>;
            #);
       #);
     argument: @argType; (* arguments to this(Process) *)
     
     
       (* operations *)
     
     start: (* starts this(Process)'s program execution *)
       (# error:< ProcessManagerException;
          twoCurrent:< ProcessManagerException;
       do <<SLOT ProcessStart:descriptor>>; INNER;
       #);
     
     stop: (* stops this(Process)'s program execution *)
       (# error:< ProcessManagerException;
       do <<SLOT ProcessStop:descriptor>>; INNER;
       #);
     
     awaitStopped: (* Returns when THIS(Process) stops *)
       (# error:< ProcessManagerException;
       do <<SLOT ProcessAwaitStopped:descriptor>>; INNER;
       #);
     
     stillRunning: (* Returns true if THIS(Process) is still running *)
       (# error:< ProcessManagerException;
          value: @Boolean;
       do <<SLOT ProcessStillRunning:descriptor>>; INNER;
       exit value
       #);
     
     (* input/output redirection *)
     
     connectToProcess:  (* connect output of this(process) to toProcess's input
                         * In Unix terms: this(Process) | toProcess
                         *)
       (# error:< ProcessManagerException;
          toProcess: ^Process;
       enter toProcess[]
       do <<SLOT connectToProcess:descriptor>>;
       #);
     
     connectInPipe:  (* connect output of fromProcess to input of  this(process) 
                      * In Unix terms: fromProcess | this(Process)
                      *)
       (# error:< ProcessManagerException;
          fromProcess: ^Process;
       enter fromProcess[]
       do <<SLOT connectInPipe:descriptor>>;
       #);
         
     redirectFromFile:  (* redirect input to this(process) from inputFile
                         * In Unix terms: this(Process) < inputFile
                         *)
       (# error:< ProcessManagerException;
          inputFile: ^File;
       enter inputFile[]
       do <<SLOT redirectFromFile:descriptor>>;
       #);
     
     redirectToFile:  (* redirect output of this(process) to outputFile
                       * In Unix terms: this(Process) > outputFile
                       *)
       (# error:< ProcessManagerException;
          outputFile: ^File;
       enter outputFile[]
       do <<SLOT redirectToFile:descriptor>>;
       #);
     
     redirectFromChannel:  (* redirect input to this(process) from inputChannel *)
       (# error:< ProcessManagerException;
          inputChannel: ^Stream;
       enter inputChannel[]
       do <<SLOT redirectFromChannel:descriptor>>; 
       #);
     
     redirectToChannel:  (* redirect output of this(process) to outputChannel *)
       (# error:< ProcessManagerException;
          outputChannel: ^Stream;
       enter outputChannel[]
       do <<SLOT redirectToChannel:descriptor>>; 
       #);
     
     (* Virtual callbacks: called when the proper action has occurred *)
     
     onStart:< (# do INNER #);
     onStop:< (# do INNER #);
     
     doDebug: @Boolean;
     private: @<<SLOT ProcessPrivate:descriptor>>;
  #);

ProcessManagerException: Exception
  (# message: ^Text;
  enter message[]
  do <<SLOT ProcessManagerException:descriptor>>;
     inner;
  #);

---]]
