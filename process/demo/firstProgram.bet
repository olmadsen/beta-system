ORIGIN '~beta/basiclib/v1.6/systemenv';
INCLUDE '~beta/process/v1.6/processmanager';
INCLUDE '~beta/process/v1.6/streamgenerator';

--- program:descriptor ---
systemEnv
(#	
   application: @process;
   inSocket: ^StreamSocket;
   Generator: @socketGenerator;
   aText: ^Text;
do	
   (* launch 'otherProgram' *)
   'otherProgram' -> application.init;
   application.start; 
   
   (* communicate via socket using number 5000 *)
   5000 -> Generator.bind; 
   
   (* establish connection *)
   waitForever -> Generator.getStreamConnection -> inSocket[];
   screen.newline;
   
   (* send a text via the socket *)
   'aText to the active program' -> aText[];
   'firstProgram sends: ' -> screen.puttext; aText[] -> screen.putline;
   aText[] -> inSocket.putText; inSocket.newline;
   
   (* inSocket.flush is done automatically by inSocket.newline *)
   
   (* close down *)
   application.awaitStopped; (* client first: avoids address-in-use error *)
   inSocket.close;
   Generator.close;
#)
