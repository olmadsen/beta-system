ORIGIN '~beta/basiclib/systemenv';
INCLUDE '~beta/process/processmanager';
INCLUDE '~beta/process/streamgenerator';

--- program:descriptor ---
systemEnv
(#	
   application: @process;
   inSocket: ^StreamSocket;
   Generator: @socketGenerator;
   aText: ^Text;
do	
   'Launching \'otherProgram\'' -> putline;
   'otherProgram' -> application.init;
   application.start; 
   
   'Communicate via socket using number 5123' -> putline;
   5123 -> Generator.bind; 
   
   'Establishing connection' -> putline;
   waitForever -> Generator.getStreamConnection -> inSocket[];
   screen.newline;
   
   'Sending a text via the socket' -> putline;
   'aText to the active program' -> aText[];
   'firstProgram sends: ' -> screen.puttext; aText[] -> screen.putline;
   aText[] -> inSocket.putText; inSocket.newline;
   
   (* inSocket.flush is done automatically by inSocket.newline *)
   
   'Closing down' -> putline;
   application.awaitStopped; (* client first: avoids address-in-use error *)
   inSocket.close;
   Generator.close;
#)
