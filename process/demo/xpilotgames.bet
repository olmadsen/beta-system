ORIGIN '~beta/basiclib/systemenv';
INCLUDE '~beta/process/streamsocket';

-- program: descriptor --
(*
 * This program demonstrates how to use the streamsocket pattern to
 * connect to the xpilot meta server and send a query about 
 * ongoing games.
 *)
systemenv
(# theSocket: @streamSocket(# error:: (# do 'Socket error'->screen.putline; stop #)#);
   buffer: @text;
   ch: @char;
do (waitForEver, 'xpilot.starpowder.de', 4400) -> theSocket.connect;
   'list' -> theSocket.putLine;
   'quit' -> theSocket.putLine;
   l: (if NOT theSocket.eos then
          theSocket.get -> ch;
          (if ch
           //10 then (* Ignore linefeed *)
           //13 then 
              buffer[] -> putLine; 
              buffer.clear
           else
              ch -> buffer.put;
          if);
          restart l;
      if);
#)
