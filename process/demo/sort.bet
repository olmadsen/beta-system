ORIGIN '~beta/process/v1.5/processmanager';
INCLUDE '~beta/process/v1.5/private/communication_unixbody';
(* This program illustrates how to invoke a program, how to give
 * program arguments, how to direct input to this program, and how to
 * read its output from within a BETA program.  In this example, the
 * program is the UNIX 'sort' program
 *)
-- program: descriptor --
(# program: @process; input,output: @pipe;
do '/bin/sort'->program.init;
   
   input.init;
   input.readEnd[]->program.redirectFromChannel;
   
   output.init;
   output.writeEnd[]->program.redirectToChannel;
   
   '-u'->program.argument.append;
   
   program.start;
   
   '4'->input.writeEnd.putLine;
   '7'->input.writeEnd.putLine;
   '3'->input.writeEnd.putLine;
   '5'->input.writeEnd.putLine;
   '4'->input.writeEnd.putLine;
   input.private.writeCh.close;
   
   getOutPut:
     (if not output.readend.eos then
       output.readend.get->screen.put;
       restart getOutput
   if);
#)
