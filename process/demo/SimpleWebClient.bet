ORIGIN '~beta/basiclib/systemenv';
INCLUDE '~beta/basiclib/textutils';
INCLUDE '~beta/basiclib/file';
INCLUDE '~beta/sysutils/pathhandler';
INCLUDE '~beta/process/streamgenerator';
(******************************************************************
 * A very simple Web client that retrieves files
 * from a SimpleWebServer - see SimpleWebServer.bet
 *)
---program:descriptor---
systemEnv
(# outSocket: @StreamSocket;
   aText: ^Text;
   no: @Integer;
do 6789 -> outSocket.port;
   'localhost' -> outSocket.host;
   cycle
   (# fn: ^text
   do doConnect: outSocket.connect
        (# error::< 
             (#
             do no+1->no; '.' -> screen.put;
                (if no<=10 then restart doConnect if);
             #);
        #);
      (* connection established *)
      
      'Type file name: ' -> puttext;
      keyboard.getLine->fn[];
      'GET /'->outSocket.putText;
      fn[]-> outSocket.puttext;
      '"'->put; fn[]->puttext; '"'->put; newline;
      ' HTTP/1.0'-> outSocket.putline;
      
      'Received: ' -> screen.putLine;
      read:
        cycle
        (#
        do outSocket.get-> screen.put;
           (if outSocket.eos then leave read if)
        #);
      outSocket.close;	(* close down the socket *)
   #)
#)
