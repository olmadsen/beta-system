ORIGIN '~beta/basiclib/systemenv';
INCLUDE '~beta/basiclib/formatio';
INCLUDE '~beta/process/streamsocket';

--- program:descriptor ---
systemEnv
(# 
   hostnm: ^Text;
   hostaddr: @Integer;
   i1: @Integer;
   i2: @Integer;
   i3: @Integer;
   i4: @Integer;

do
   (if noOfArguments<>2 then
       'Usage: ./reversedns nnn.nnn.nnn.nnn '->screen.putLine;
    else
       2->arguments->hostnm[];
       '\nThe address is ' -> screen.putText;
       hostnm[] -> screen.putLine;
       0 -> hostnm.pos;
       '%i.%i.%i.%i'->hostnm.getFormat(# do i->i1; i->i2; i->i3; i->i4; #);
       i1 * 256 * 256 * 256 + i2 * 256 * 256 + i3 * 256 + i4 -> hostaddr;
       hostaddr -> gethostbyaddr
       (#
           notfound::
	      (#
	      do 'gethostbyaddr failed!' ->screen.putLine;
	         true->continue;
	         ''->hostnm;
	      #)
       #) -> hostnm[];
       (if '' -> hostnm.equal then
	   'Reverse lookup failed! ' -> screen.putLine;
       else
	   'Reverse lookup succeeded!' -> screen.putLine;
	   'The reverse name is ' -> screen.putText;
	   hostnm[] -> screen.putLine;
       if);
   if);
#)
