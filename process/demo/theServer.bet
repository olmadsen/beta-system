ORIGIN '~beta/process/v1.4.1/processmanager';
[[
--- program:descriptor ---
(#
   client1,client2: @process;
   sockGen: @SocketGenerator;
   cl1socket,cl2socket: ^StreamSocket;
   msg1,msg2: ^Text;
do
   (* communicate via socket using number 5000 *)
   5000 -> sockGen.port;
   sockGen.bind;

   (* Start the program 'aClient', establish connection through 'cl1socket' *)
   'aClient' -> client1.init;
   client1.start;
   sockGen.getStreamConnection -> cl1socket[];

   (* Start another instance of 'aClient', connect through 'cl2socket' *)
   'aClient' -> client2.init;
   client2.start;
   sockGen.getStreamConnection -> cl2socket[];

   (* Communicate with the clients *)
   'a text for client1' -> cl1socket.putLine; cl1socket.flush;
   cl1socket.GetLine -> msg1[];
   'a text for client2' -> cl2socket.putLine; cl2socket.flush;
   cl2socket.GetLine -> msg2[];
   
   (* Show results *)
   'server, received from client1: ' -> screen.putText; 
   msg1[] -> screen.putLine;
   'server, received from client2: ' -> screen.putText;
   msg2[] -> screen.putLine;

   (* Close down *)
   client1.awaitStopped;
   cl1socket.close; (* close after client to avoid address-in-use-error *)
   client2.awaitStopped;
   cl2socket.close;
   sockGen.close;
#)
---]]
