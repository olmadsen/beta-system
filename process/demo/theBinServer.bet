ORIGIN '~beta/process/v1.4.1/processmanager';
[[
--- program:descriptor ---
(#
   client1,client2: @process;
   sockGen: @socketGenerator;
   cl1socket,cl2socket: ^binarySocket;
   i,j: @integer;
do
   (* communicate via socket using number 5000 *)
   5000 -> sockGen.port;
   sockGen.bind;

   (* Start the program 'aBinClient', connect through 'cl1socket' *)
   'aBinClient' -> client1.init;
   client1.start;
   sockGen.getBinaryConnection -> cl1socket[];

   (* Start another instance of 'aBinClient', connect through 'cl2socket' *)
   'aBinClient' -> client2.init;
   client2.start;
   sockGen.getBinaryConnection -> cl2socket[];

   (* Communicate with the clients *)
   1 -> i; 
   2 -> j;
   (@@i,4) -> cl1socket.writeData;
   (@@i,4) -> cl1socket.readData;
   (@@j,4) -> cl2socket.writeData;
   (@@j,4) -> cl2socket.readData;
   
   (* Show results *)
   'server, received from client1: ' -> screen.putText; 
   i -> screen.putInt; screen.newline;
   'server, received from client2: ' -> screen.putText;
   j -> screen.putInt; screen.newline;

   (* Close down *)
   client1.awaitStopped;
   cl1socket.close; (* close after client to avoid address-in-use-error *)
   client2.awaitStopped;
   cl2socket.close;
   sockGen.close;
#)
---]]
