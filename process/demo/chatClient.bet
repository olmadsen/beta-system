ORIGIN '~beta/process/v1.4.1/processmanager';
[[
--- include 'commandCategory'
--- program:descriptor ---
(# 
   getcmd:
     (#
        prompt:
          (#
             t: @text;
          do
             'client ' -> t.putText;
             idNumber -> t.putInt;
             ' > ' -> t.putText;
          exit t[]
          #);
        cmd: ^text;
     do
        prompt -> screen.puttext;
        keyboard.getline -> cmd[];
     exit cmd[]
     #);

   chat:
     (#
        cmd: ^text;
        answerNo,requestNo: @integer;
     do
        outSocket.getLine -> cmd[];	(* Regrettably 'getInt' stops at *)
        cmd.reset;			(*   'eos', so we "pipe" the data *)
        cmd.getInt -> idNumber;		(*   through 'cmd' to 'idNumber' *)
        L: cycle
          (#
          do
             (if (answerNo<requestNo)//true then
                 outSocket.nonBlockingScope
                 (#
                    received: ^text;
                 do
                    outSocket.getLine -> received[];
                    'Received answer to request no. ' -> screen.putText;
                    answerNo+1 -> answerNo -> screen.putInt;
                    ': ' -> screen.putText;
                    received[] -> screen.putLine;
                 #)
             if);
             getcmd -> cmd[] -> outSocket.putLine;
             outSocket.flush;
             (if true
              //(cmd[]->isQuitCommand) then
                 leave L
              //(cmd[]->isAnswerCommand) then
                 'Making request no. ' -> screen.putText;
                 requestNo+1 -> requestNo -> screen.putInt;
                 screen.newline;
              //(cmd[]->isAnswerWaitCommand) then
                 'Waiting for answer: ' -> screen.putText;
                 outSocket.getLine -> screen.putLine;
             if);
          #);
     #);

   outSocket: @activeStreamSocket;
   idNumber: @integer;
   no: @Integer;
do
   (* Initialize and establish connection *)
   5000 -> outSocket.port;
   'localhost' -> outSocket.host;
   doConnect: outSocket.connect
     (# error::<
          (#
          do no+1->no; '.' -> screen.put;
             (if no>10//false then restart doConnect if);
          #);
     #);

   (* Communicate with server *)
   chat;

   (* Close down *)
   outSocket.close;
#)
---]]
