ORIGIN '~beta/process/v1.4.1/processmanager';
[[
--- include 'showRep'
--- program:descriptor ---
(#
   outSocket: @activeBinarySocket;
   rep: @extendedRepStream;
   header: @integer;
   no: @integer;
do
   (* Initialize and establish connection *)
   5000 -> outSocket.port;
   'localhost' -> outSocket.host;
   doConnect: outSocket.connect
     (# error::<
          (#
          do no+1->no; '.' -> screen.put;
             (if no>10//false then restart doConnect if);
          #);
     #);

   (* Communicate *)
   rep[] -> outSocket.getRep -> header;
   'client, received from server: ' -> screen.putText;
   (rep[],header) -> showRep; screen.newline;

   rep.clear;
   (if header
    //1 then (* received integers; return text data *)
       2 -> header;
       'an answer' -> rep.putText;
    //2 then (* received text; return integer data *)
       1 -> header;
       10 -> rep.putInt;
    //3 then (* received mixed data; return mixed data *)
       3 -> header;
       2 -> rep.putInt; 'a not' -> rep.putText;
       1 -> rep.putInt; 2 -> rep.putInt;
       2 -> rep.putInt; 'mixed answer' -> rep.putText;
   if);
   (rep[],header) -> outSocket.putRep;

   (* Close down *)
   outSocket.close;
#)
---]]
