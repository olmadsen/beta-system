ORIGIN '~beta/basiclib/v1.6/systemenv';
INCLUDE '~beta/process/v1.6/commpipe';
INCLUDE '~beta/process/v1.6/processmanager';
--- program:descriptor ---
systemEnv
(# 
   execRead:
     (# name, args: ^text;
        output: ^stream;
        child: @process;
        channel: @pipe;
     enter (name[], args[])
     do
        (* initialize (an object to manage) the child process *)
        name[] -> child.init;
        args[] -> child.argument.append; 

        (* setup communication channel to retrieve output from child *)
        channel.init;
        channel.writeEnd[] -> child.redirectToChannel;
        channel.readEnd[] -> output[];
   
        (* run the child process *)
        child.start;
        
        INNER;
        channel.readEnd.close;
     #);
   
   execReadText:ExecRead
     (# T: ^Text;
     do &Text[]->T[];
        loop:
          (# 
          do (if not output.eos then
                 output.getline -> T.putline; 
                 restart loop
             if);
          #);
     exit T[]
     #);
   
   first, other: ^Text;
   
do 'output of "/bin/ls -l":'->screen.putLine;
   ('/bin/ls','-l') -> execReadText -> first[];
   first[]->screen.putText;
   
   'Repeating command 1000 times...' ->screen.putLine;
   (for manytimes:1000 repeat
        manytimes->screen.putInt; ','->screen.put;
        ('/bin/ls','-l') -> execReadText -> other[];
        (if (first.length<>other.length) or (not (other[]->first.equal)) then
            screen.newLine;
            'first and other are different during run '->screen.putText;
            manytimes->screen.putInt;'.  Other:'->screen.putLine;
            other[]->screen.putLine;
        if)
   for)     
#)
