(* Simple test of process IO redirection of subprocesses *)
(* Doesn't compile on Windows - use activatent instead *)

ORIGIN '~beta/basiclib/systemenv';
INCLUDE '~beta/process/commpipe';
INCLUDE '~beta/process/processmanager';
--- program:descriptor ---
systemEnv
(# 
   execReadWriteInit:
     (# name, args: ^text;
        child: @process;
        openRedirection:< Object;
        closeRedirection:< Object;
        
     enter (name[], args[])
     do
        (* initialize (an object to manage) the child process *)
        name[] -> child.init;
        (if args[]<> none then args[] -> child.argument.append if);
        &openRedirection;
        (* run the child process *)
        child.start;
        INNER;
        &closeRedirection;
        (* Await exit of child.
         * Otherwise it will turn into a zombie.
         *)
        child.awaitStopped;
     #);
   
   execRead: execReadWriteInit
     (# output: ^stream;
        outchannel: @pipe;
        openRedirection::<
          (# 
          do (* setup communication channel to retrieve output from child *)
             outchannel.init;
             outchannel.writeEnd[] -> child.redirectToChannel;
             outchannel.readEnd[] -> output[];
             INNER;
          #);
        closeRedirection::<
          (# 
          do (* Close channel *)
             INNER;
             outchannel.readEnd.close;
          #);
     do INNER;
     #);
   
   
   execReadWrite: execRead
     (# input: ^stream;
        inchannel: @pipe;
        openRedirection::<
          (# 
          do (* setup communication channel to give input to child *)
             inchannel.init;
             inchannel.readEnd[] -> child.redirectFromChannel;
             inchannel.writeEnd[] -> input[];
             INNER;
          #);
        closeRedirection::<
          (# 
          do (* Close channel *)
             INNER;
             inchannel.writeEnd.close;
          #);

     do INNER;
     #);
   execWrite: execReadWriteInit
     (# input: ^stream;
        inchannel: @pipe;
        openRedirection::<
          (# 
          do (* setup communication channel to give input to child *)
             inchannel.init;
             inchannel.readEnd[] -> child.redirectFromChannel;
             inchannel.writeEnd[] -> input[];
             INNER;
          #);
        closeRedirection::<
          (# 
          do (* Close channel *)
             INNER;
             inchannel.close;
          #);

     do INNER;
     #);
   
   execReadText:ExecRead
     (# T: ^Text;
     do &Text[]->T[];
        loop:
          (# 
          do 
             (if not output.eos then
                 output.getline -> T.putLine;
                 restart loop
             if)
          #);
     exit T[]
     #);
   
   execReadWriteText:ExecReadWrite
     (# T: ^Text;
     enter T[]
     do T[]->input.putText;
        inchannel.writeEnd.close;
        &Text[]->T[];
        loop:
          (# 
          do (if not output.eos then
                 output.getline -> T.putLine;
                 restart loop
             if)
          #);
     exit T[]
     #);
   
   first, other: ^Text;

   (*
    * The examples here fail rather inelegantly if the programs called
    * don't exist
    *)
   
do 'output of "/bin/ls -l":'->screen.putLine;
   ('/bin/ls','-l') -> execReadText -> first[];
   first[]->screen.putText;
   
   'Repeating command 100 times...' ->screen.putLine;
   (for manytimes:100 repeat
        manytimes->screen.putInt; 
        ('/bin/ls','-l') -> execReadText -> other[];
        (if (first.length<>other.length) or (not (other[]->first.equal)) then
            screen.newLine;
            'first and other are different during run '->screen.putText;
            manytimes->screen.putInt;'.  Other:'->screen.putLine;
            other[]->screen.putLine;
        if);
        ascii.CR->screen.put;
   for);
  
   'output of "./slowwriter -s":'->screen.putLine;
   ('./slowwriter','-s') -> execReadText -> first[];
   first[]->screen.putText;

(*
 * 'Sending a mail through sendmail'->screen.putLine;
 * ('/usr/lib/sendmail', '-t') -> execWrite
 * (# 
 * do 'To: corry@mjolner.dk' -> input.putLine;
 *    'Subject: activate.bet sent mail.' -> input.putLine;
 *    input.newLine;
 *    'Hi,' -> input.putLine;
 *    'This is a mail sent from the activate.bet testprogram' -> input.putLine;
 * #);
 *)
   
   'Writing this output to cat, NOT reading the output:'->screen.putLine;
   ('/bin/cat', NONE) -> execWrite
   (# 
   do first[]->input.putText;
   #);
   
   'Writing this output to wc, NOT reading the output:'->screen.putLine;
   ('/usr/bin/wc', NONE) -> execWrite
   (# 
   do first[]->input.putText;
   #);
   
   'Writing this output to wc, reading the output:'->screen.putLine;
   ('/usr/bin/wc','-l', first[])->execReadWriteText->screen.putLine;
   'done.'->screen.putLine;
   
   screen.newLine;
#)
