ORIGIN '~beta/process/v1.5/processmanager';
[[
--- include 'commandCategory'
--- program:descriptor ---
(# 
   getcmd:
     (#
        prompt:
          (#
             t: @text;
          do
             'client ' -> t.putText;
             idNumber -> t.putInt;
             ' > ' -> t.putText;
          exit t[]
          #);
        cmd: ^text;
     do
        prompt -> screen.puttext;
        keyboard.getline -> cmd[];
     exit cmd[]
     #);

   chat:
     (# rep: @extendedRepStream;
        cmd: ^text;
        answerNo,requestNo: @integer;
     do
	(@@idNumber,4) -> outSocket.readData;
        L: cycle
          (#
          do
             (if (answerNo<requestNo)//true then
                 outSocket.nonBlockingScope
                 (#
                    received: ^text;
                 do
                    rep[] -> outSocket.getRep;
                    rep.getText -> received[];
                    'Received answer to request no. ' -> screen.putText;
                    answerNo+1 -> answerNo -> screen.putInt;
                    ': ' -> screen.putText;
                    received[] -> screen.putLine;
                 #)
             if);
             rep.clear;
             getcmd -> cmd[] -> rep.putText;
             (rep[],packetCount+1->packetCount) -> outSocket.putRep;
             (if true
              //(cmd[]->isQuitCommand) then
                 leave L
              //(cmd[]->isAnswerCommand) then
                 'Making request no. ' -> screen.putText;
                 requestNo+1 -> requestNo -> screen.putInt;
                 screen.newline;
              //(cmd[]->isAnswerWaitCommand) then
                 'Waiting for answer (' -> screen.putText;
                 rep[] -> outSocket.getRep -> screen.putInt;
                 '): ' -> screen.putText;
                 rep.getText -> screen.putLine;
             if);
          #);
     #);

   outSocket: @activeBinarySocket;
   idNumber,packetCount: @integer;
   no: @Integer;
do
   (* Initialize and establish connection *)
   5000 -> outSocket.port;
   'localhost' -> outSocket.host;
   doConnect: outSocket.connect
     (# error::<
          (#
          do no+1->no; '.' -> screen.put;
             (if no>10//false then restart doConnect if);
          #);
     #);

   (* Communicate with server *)
   chat;

   (* Close down *)
   outSocket.close;
#)
---]]
