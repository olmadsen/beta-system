ORIGIN '~beta/basiclib/v1.5/betaenv';
[[
--- include '~beta/process/v1.4.1/repstream/extendedRepstream'
--- lib:attributes ---

(* Prints repetitionStream according to the (arbitrary)
 * conventions for their internal format used in 
 * 'repChatClient.bet' and 'repChatServer.bet'
 *)

showRep:
  (# rep: ^extendedRepStream;
     header: @integer;
  enter (rep[],header)
  do
     L: (if header
         //1 then (* integer data *)
            'Integer data: ' -> screen.putText;
            Loop(# until::< (# do rep.eos->value #);
                do
                   rep.getInt -> screen.putInt;
                   ' ' -> screen.putText;
                #);
         //2 then (* text data *)
            'Text data: ' -> screen.putText;
            Loop(# until::< (# do rep.eos->value #);
                do
                   '"' -> screen.putText;
                   rep.getText -> screen.putText;
                   '" ' -> screen.putText;
                #);
         //3 then (* mixed data *)
            'Mixed data: ' -> screen.putText;
            Loop(# until::< (# do rep.eos->value #);
                do
                   (if rep.getInt
                    //1 then (* next item an integer *)
                       rep.getInt -> screen.putInt;
                       ' ' -> screen.putText;
                    //2 then (* next item a text string *)
                       '"' -> screen.putText;
                       rep.getText -> screen.putText;
                       '" ' -> screen.putText;
                    else
                       'Unknown data item!' -> screen.putText;
                   if);
                #);
         else
            'Unknown kind of data!' -> screen.putText;
        if);
  #); (* showRep *)

---]]
