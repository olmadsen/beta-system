ORIGIN '~beta/process/v1.5/processmanager';
[[
--- include 'showRep'
--- program:descriptor ---
(#
   sleep: external
     (# duration: @integer;
     enter duration
     do callC
     #);

   client1,client2,client3: @process;
   sockGen: @socketGenerator;
   cl1socket,cl2socket,cl3socket: ^binarySocket;
   rep1,rep2,rep3: @extendedRepStream;
   header1,header2,header3: @integer;
do
   (* communicate via socket using number 5000 *)
   5000 -> sockGen.port;
   sockGen.bind;

   (* Start the program 'aRepClient', connect through 'cl1socket' *)
   'aRepClient' -> client1.init;
   client1.start;
   sockGen.getBinaryConnection -> cl1socket[];

   (* Start another instance of 'aRepClient', connect through 'cl2socket' *)
   'aRepClient' -> client2.init;
   client2.start;
   sockGen.getBinaryConnection -> cl2socket[];

   (* Start a third instance of 'aRepClient', connect through 'cl3socket' *)
   'aRepClient' -> client3.init;
   client3.start;
   sockGen.getBinaryConnection -> cl3socket[];

   (* Communicate with the clients *)
   1 -> header1; (* flag meaning "integer data" *)
   1 -> rep1.putInt;
   2 -> rep1.putInt;
   3 -> rep1.putInt;

   2 -> header2; (* flag meaning "text data" *)
   'one' -> rep2.putText;
   'two' -> rep2.putText;
   'three' -> rep2.putText;

   3 -> header3; (* flag meaning mixed data *)
   1 -> rep3.putInt; 45 -> rep3.putInt;
   2 -> rep3.putInt; 'sixty-seven' -> rep3.putText;
   1 -> rep3.putInt; 89 -> rep3.putInt;

   (rep1[],header1) -> cl1socket.putRep;
   rep1[] -> cl1socket.getRep -> header1;
   (rep2[],header2) -> cl2socket.putRep;
   rep2[] -> cl2socket.getRep -> header2;
   (rep3[],header3) -> cl3socket.putRep;
   rep3[] -> cl3socket.getRep -> header3;

   (* Show results *)
   'server, received from client1: ' -> screen.putText;
   (rep1[],header1) -> showRep; screen.newline;
   'server, received from client2: ' -> screen.putText;
   (rep2[],header2) -> showRep; screen.newline;
   'server, received from client3: ' -> screen.putText;
   (rep3[],header3) -> showRep; screen.newline;

   (* Close down *)
   client1.awaitStopped;
   cl1socket.close; (* close after client to avoid address-in-use-error *)
   client2.awaitStopped;
   cl2socket.close;
   client3.awaitStopped;
   cl3socket.close;
   sockGen.close;
#)
---]]
