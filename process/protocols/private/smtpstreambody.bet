ORIGIN '../smtpstream';
INCLUDE '~beta/basiclib/formatio';
INCLUDE 'protocolstreambody';

--- smtphello: dopart ---
do ('HELO',domain[])->cmd
   (#
   do (if '250'->status.equalNCS then
          result.copy->greeting[];
       else (status[],result[])->error
      if)
   #)
--- smtpmail: dopart ---
do ('MAIL FROM:',reverse_path[])->cmd
   (#
   do (if '250'->status.equalNCS then
          (* OK *)
       else (status[],result[])->error
      if)
   #)
--- smtprecipient: dopart ---
do ('RCPT TO:',forward_path[])->cmd
   (#
   do (if status.asInt//250//251 then
          (* OK *)
       else (status[],result[])->error
      if)
   #)
--- smtpdata: dopart ---
do ('DATA',none)->cmd
   (#
   do (if '354'->status.equalNCS then
          message[]->putline;
          '.'->putline;
          getReply
          (#
          do (if '250'->status.equalNCS then
                 (* OK *)
              else (status[],result[])->error
             if)
          #)
       else (status[],result[])->error
      if)
   #)
--- smtpreset: dopart ---
do ('RSET',none)->cmd
   (#
   do (if '250'->status.equalNCS then
          (* OK *)   
       else (status[],result[])->error
      if)
   #)
--- smtpsend: dopart ---
do ('SEND FROM:',reverse_path[])->cmd
   (#
   do (if '250'->status.equalNCS then
          (* OK *)
       else (status[],result[])->error
      if)
   #)
--- smtpsendormail: dopart ---
do ('SOML FROM:',reverse_path[])->cmd
   (#
   do (if '250'->status.equalNCS then
          (* OK *)
       else (status[],result[])->error
      if)
   #)
--- smtpsendandmail: dopart ---
do ('SAML FROM:',reverse_path[])->cmd
   (#
   do (if '250'->status.equalNCS then
          (* OK *)
       else (status[],result[])->error
      if)
   #)
--- smtpverify: dopart ---
do ('VRFY',username[])->cmd
   (#
   do (if status.asInt//250//251//252 then
          result.copy->expandedusername[];
       else (status[],result[])->error
      if)
   #)
--- smtpexpand: dopart ---
do ('EXPN',groupname[])->cmd
   (#
   do (if status.asInt//250//502 then
          result.copy->groupmember[];
          INNER expand
       else (status[],result[])->error
      if)
   #)
--- smtphelp: dopart ---
do &text[]->helptext[];
   ('HELP',commandname[])->cmd
   (#
   do (if status.asInt//211//214 then
          result[]->helptext.putline;
          INNER help
       else (status[],result[])->error
      if)
   #)
--- smtpnoop: dopart ---
do ('NOOP',none)->cmd
   (#
   do (if '250'->status.equalNCS then
          (* OK *)
       else (status[],result[])->error
      if)
   #)
--- smtpquit: dopart ---
do ('QUIT',none)->cmd
   (#
   do (if '221'->status.equalNCS then
          (* OK *)
       else (status[],result[])->error
      if)
   #)
--- smtpturn: dopart ---
do ('TURN',none)->cmd
   (#
   do (if '250'->status.equalNCS then
          (* OK *)
       else (status[],result[])->error
      if)
   #)
   
--- smtperror:dopart ---
do status.reset;
   (if status.asInt
    // 421 then (status[],result[])->(&protocolException[]).serviceDisconnected
    // 450 then (status[],result[])->(&protocolException[]).mailboxUnavailable
    // 451 then (status[],result[])->(&protocolException[]).localError
    // 452 then (status[],result[])->(&protocolException[]).storageLow
    // 500 then (status[],result[])->(&protocolException[]).commandUnknown
    // 501 then (status[],result[])->(&protocolException[]).parameterError
    // 502 then (status[],result[])->(&protocolException[]).commandNotImplemented
    // 503 then (status[],result[])->(&protocolException[]).commandSequenceWrong
    // 504 then (status[],result[])->(&protocolException[]).parameterNotImplemented
    // 550 then (status[],result[])->(&protocolException[]).mailboxUnavailable
    // 551 then (status[],result[])->(&protocolException[]).userNotLocal
    // 552 then (status[],result[])->(&protocolException[]).storageLow
    // 553 then (status[],result[])->(&protocolException[]).mailboxNameIllegal
    // 554 then (status[],result[])->(&protocolException[]).transactionFailed
    else (status[],result[])->(&protocolException[]).unknownError
   if)
