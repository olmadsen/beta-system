ORIGIN '../protocolstream';
INCLUDE '~beta/basiclib/regexp'

--- protocolerror: dopart ---
do INNER;
   name->msg.puttext; ' error message: '->msg.puttext;
   status[]->msg.puttext; ' '->msg.put; result[]->msg.putline;
   
--- protocolserver: dopart ---
do init;
   communicationPort->port; domain->host;
   connect
     (# error::< 
          (#
          do (domain[],port)
               ->(&protocolException[]).serverNotFound;
          #);
     #);
   (* connection established *)

   (* get the greating text *)
   getReply
   (#
   do (if okStatus->status.equalNCS then
          (* OK *)
       else (status[],result[])->(&protocolException[]).protocolError
      if)
   #)
   
--- protocolstreamcommandlib: attributes ---
cmd:
  (# name, params, status, result: ^text;
   enter (name[], params[])
  do name[]->puttext; ' '->put;
     (if params[]<>none then params[]->putline else newline if);
     flush;
     loop:
       (# sep: @char
       do getreply->(status[],sep,result[]);
          (if sep
           // '-' then (* multiline reply *)
              INNER cmd;
              restart loop
           // ' ' then (* last line *)
              INNER cmd;
          if)
       #)
  exit (status[], result[])
  #);

getReply:
  (# status: ^text; sep: @char; result, tmp: ^text;
  do getline->result[]; result.reset;
     (if private.replyPtn[]=none then
         result.getatom->status[]; result.get->sep; result.getline->result[];
      else
         private.replyPtn[]->result.regexp_match
         (#
         do ((1->regs.start)+1, 1->regs.end)->result.sub->status[];
            ((2->regs.start)+1, 2->regs.end)->result.sub->tmp[];
            tmp.reset; tmp.get->sep;
            ((3->regs.start)+1, result.length)->result.sub->result[];
         #)
     if);
     INNER getReply
  exit (status[],sep,result[])
  #);

getDotTerminatedMessage:
  (# result, r: ^text;
  do getline->result[];
     loop: (if (result.lgth=2) and (result.T[1]='.') then
               leave loop
            else
               INNER getDotTerminatedMessage;
               getline->result[];
               restart loop
           if);
  #);

--- protocolstreamprivate: descriptor ---
(# replyPtn: ^text #)
