ORIGIN '../popstream';
INCLUDE '~beta/basiclib/formatio';
INCLUDE 'protocolstreambody';
   
--- popuser: dopart ---
do ('USER',userName[])->cmd
   (#
   do (if '+OK'->status.equalNCS then
          (* OK *)
       else (status[],result[])->(&protocolException[]).POPerror
      if)
   #)

--- poppwd: dopart ---
do ('PASS',pwd[])->cmd
   (#
   do (if '+OK'->status.equalNCS then
          (* OK *)
       else (status[],result[])->(&protocolException[]).POPerror
      if)
   #)
   
--- popstat: dopart ---
do ('STAT',none)->cmd
   (#
   do (if '+OK'->status.equalNCS then
          result.reset;
          '%i %i'->result.getformat
          (# do i->noOfMessages; i->sizeOfMailbox #);
          INNER stat
       else (status[],result[])->(&protocolException[]).POPerror
      if)
   #)

--- poplist: dopart ---
do (# t: ^text
   do (if messageNo<>0 then messageNo->i2a->t[] if);
      ('LIST',t[])->cmd
      (#
      do (if '+OK'->status.equalNCS then
             (if messageNo=0 then
                 getDotTerminatedMessage
                 (#
                 do result.reset;
                    '%i %i'->result.getformat
                    (# do i->messageNo; i->sizeOfMessage #);
                    INNER list
                 #)
              else
                 result.reset;
                 '%i %i'->result.getformat
                 (# do i->messageNo; i->sizeOfMessage #);
                 INNER list
             if)
          else (status[],result[])->(&protocolException[]).POPerror
         if)
      #)
   #)
   
--- popretrieve: dopart ---
do ('RETR',messageNo->i2a)->cmd
   (#
   do (if '+OK'->status.equalNCS then
          &text[]->message[];
          getDotTerminatedMessage(# do result[]->message.putline #);
       else (status[],result[])->(&protocolException[]).POPerror
      if)
   #)

--- popdelete: dopart ---
do ('DELE',messageNo->i2a)->cmd
   (#
   do (if '+OK'->status.equalNCS then
          (* OK *)
       else (status[],result[])->(&protocolException[]).POPerror
      if)
   #)
   
--- popnoop: dopart ---
do ('NOOP',none)->cmd
   (#
   do (if '+OK'->status.equalNCS then
          (* OK *)
       else (status[],result[])->(&protocolException[]).POPerror
      if)
   #)

--- popreset: dopart ---
do ('RSET',none)->cmd
   (#
   do (if '+OK'->status.equalNCS then
          (* OK *)
       else (status[],result[])->(&protocolException[]).POPerror
      if)
   #)
   
--- poptop: dopart ---
do (# t: ^text
   do messageNo->i2a->t[]; ' '->t.put; noOfLines->t.putint;
      ('TOP',t[])->cmd
      (#
      do (if '+OK'->status.equalNCS then
             &text[]->message[];
             getDotTerminatedMessage(# do result[]->message.putline #);
          else (status[],result[])->(&protocolException[]).POPerror
         if)
      #)
   #)
      
--- popuidl: dopart ---
do (# t: ^text
   do (if messageNo<>0 then messageNo->i2a->t[] if);
      ('UIDL',t[])->cmd
      (#
      do (if '+OK'->status.equalNCS then
             (if messageNo<>0 then
                 getDotTerminatedMessage
                 (#
                 do result.reset;
                    '%i %s'->result.getformat
                    (# do i->messageNo; s->messageId[] #);
                 #)
              else
                 result.reset;
                 '%i %s'->result.getformat
                 (# do i->messageNo; s->messageId[] #)
             if)
          else (status[],result[])->(&protocolException[]).POPerror
         if)
      #)
   #)
   
--- popapop: dopart ---
do (# t: ^text
   do mailboxname.copy->t[]; ' '->t.put; digest[]->t.puttext;
      ('APOP',t[])->cmd
      (#
      do (if '+OK'->status.equalNCS then
             (* OK *)
          else (status[],result[])->(&protocolException[]).POPerror
         if)
      #)
   #)

--- popquit: dopart ---
do ('QUIT',none)->cmd
   (#
   do (if '+OK'->status.equalNCS then 
          (* OK *)
       else (status[],result[])->(&protocolException[]).POPerror
      if)
   #)
   
--- lib: attributes ---
i2a:
  (# i: @integer; a: ^text
  enter i
  do &text[]->a[]; i->a.putint;
  exit a[]
  #)
