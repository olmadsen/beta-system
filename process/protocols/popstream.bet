ORIGIN '~beta/basiclib/basicsystemenv';
INCLUDE 'protocolstream';
BODY 'private/popstreambody';
--- systemlib: attributes ---
popStream: protocolStream
  (* This stream implements the RFC 1725: Post Office Protocol
   * (POP3). For detailes on the usage of this protocol, please refer
   * to the RFC 1725 document, which can be found several places on
   * the WWW
   *)
  (# <<SLOT popstreamlib: attributes>>;
     name:: (# do 'POP'->value[] #);
     communicationPort:: (# do 110->value #);
     popCommand: command
       (# protocolException::<
            (# serviceDisconnected:< protocolError
                 (# do 'POP3 server disconnected'->msg.putline #);
               POPerror:< protocolError
                 (# do 'POP3 protocol error'->msg.putline #);
            do INNER protocolException
            #);
       do INNER popCommand
       #);
     server:: (# okStatus:: (# do '+OK'->value[] #) #);
     user: popCommand
       (# userName: ^text
       enter userName[]
       <<SLOT popuser: dopart>>
       #);
     password: popCommand
       (# pwd: ^text
       enter pwd[]
       <<SLOT poppwd: dopart>>
       #);
     stat: popCommand
       (# noOfMessages, sizeOfMailbox: @integer
       <<SLOT popstat: dopart>>
       exit (noOfMessages, sizeOfMailbox)
       #);
     list: popCommand
       (# messageNo, sizeOfMessage: @integer; 
       enter messageNo
       <<SLOT poplist: dopart>>
       #);
     retrieve: popCommand
       (# messageNo: @integer; message: ^text
       enter messageNo
       <<SLOT popretrieve: dopart>>
       exit message[]   
       #);
     delete: popCommand
       (# messageNo: @integer
       enter messageNo
         <<SLOT popdelete: dopart>>
       #);
     noop: popCommand
       (#
       <<SLOT popnoop: dopart>>
       #);
     reset: popCommand
       (#
       <<SLOT popreset: dopart>>
       #);
     top: popCommand
       (# messageNo, noOfLines: @integer; message: ^text
       enter (messageNo, noOfLines)
       <<SLOT poptop: dopart>>
       exit message[]
       #);
     uidl: popCommand
       (# messageNo: @integer; messageId: ^text;
       enter messageNo
       <<SLOT popuidl: dopart>>
       exit messageId[]
       #);
     apop: popCommand
       (# mailboxname, digest: ^text
       enter (mailboxname[], digest[])
       <<SLOT popapop: dopart>>
       #);
     quit: popCommand
       (# <<SLOT popquit: dopart>> #);
  #);
