ORIGIN '~beta/basiclib/basicsystemenv';
INCLUDE 'protocolstream';
BODY 'private/smtpstreambody';
--- systemlib: attributes ---

smtpStream: protocolStream
  (* This stream implements the RFC 821: Simple Mail Transfer
   * Protocol (SMTP).  For detailes on the usage of this protocol,
   * please refer to the RFC 821 document, which can be found several
   * places on the WWW
   *)
  (# <<SLOT smtpstreamlib: attributes>>;
     name:: (# do 'SMTP'->value #);
     communicationPort:: (# do 25->value #);
     smtpCommand: command
       (# protocolException::< 
            (# serviceDisconnected:< protocolError
                 (# do 'SMTP server disconnected'->msg.putline #);
               mailboxUnavailable:< protocolError
                 (# do 'Mailbox not available'->msg.putline #);
               localError:< protocolError
                 (# do 'Local error in processing'->msg.putline #);
               storageLow:< protocolError
                 (# do 'Insufficient system storage'->msg.putline #);
               commandUnknown:< protocolError
                 (# do 'Cammand not known'->msg.putline #);
               parameterError:< protocolError
                 (# do 'Syntax error in parameter'->msg.putline #);
               commandNotImplemented:< protocolError
                 (# do 'Command not implemented'->msg.putline #);
               commandSequenceWrong:< protocolError
                 (# do 'Wrong sequence of commands'->msg.putline #);
               parameterNotImplemented:< protocolError
                 (# do 'Parameter not implemented'->msg.putline #);
               userNotLocal:< protocolError
                 (# do 'User not local'->msg.putline #);
               mailboxNameIllegal:< protocolError
                 (# do 'Syntax error in mailbox name'->msg.putline #);
               transactionFailed:< protocolError
                 (# do 'Transaction failed'->msg.putline #);
               unknownError:< protocolError
                 (# do 'Unknown SMTP response'->msg.putline #);
            do INNER protocolException
            #);
          error:: (# <<SLOT smtperror:dopart>> #);
       do INNER smtpCommand
       #);
     server:: (# okStatus:: (# do '220'->value[] #) #);
     hello: smtpCommand
       (# domain, greeting: ^text
       enter domain[]
       <<SLOT smtphello:dopart>>
       exit greeting[]
       #);
     mail: smtpCommand
       (# reverse_path: ^text
       enter reverse_path[]
       <<SLOT smtpmail:dopart>>
       #);
     recipient: smtpCommand
       (# forward_path: ^text
       enter forward_path[]
       <<SLOT smtprecipient:dopart>>
       #);
     data: smtpCommand
       (# message: ^stream
       enter message[]
       <<SLOT smtpdata:dopart>>
       #);
     reset: smtpCommand
       (# <<SLOT smtpreset:dopart>> #);
     send: smtpCommand
       (# reverse_path: ^text
       enter reverse_path[]
       <<SLOT smtpsend:dopart>>
       #);
     send_or_mail: smtpCommand
       (# reverse_path: ^text
       enter reverse_path[]
       <<SLOT smtpsendormail:dopart>>
       #);
     send_and_mail: smtpCommand
       (# reverse_path: ^text
       enter reverse_path[]
       <<SLOT smtpsendandmail:dopart>>
       #);
     verify: smtpCommand
       (# username, expandedusername: ^text
       enter username[]
       <<SLOT smtpverify:dopart>>
       exit expandedusername[]
       #);
     expand: smtpCommand
       (# groupname, groupmember: ^text
       enter groupname[]
          (* executes INNER for each groupmember in the group *)
       <<SLOT smtpexpand:dopart>>
       #);
     help: smtpCommand
       (# commandname, helptext: ^text
       enter commandname[]
       <<SLOT smtphelp:dopart>>
       exit helptext[]   
       #);
     noop: smtpCommand
       (# <<SLOT smtpnoop:dopart>> #);
     quit: smtpCommand
       (# <<SLOT smtpquit:dopart>> #);
     turn: smtpCommand
       (# <<SLOT smtpturn:dopart>> #);
     init::<
       (# <<SLOT smtpinit:dopart>> #);
  #)

