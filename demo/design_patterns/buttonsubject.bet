ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls'
        '~beta/containers/sets'
	'buttonobserver';
-- windowLib: Attributes --
Subject: pushButton
  (# 
     eventHandler::< (#
	onMouseDown::< (# do notifyUpdate #);
     #);
     observerType:< Observer;
     init:< (#  do observers.init; INNER INIT;  #);
     attach:
       (# theObserver: ^observerType; 
       enter theObserver[]
       do theObserver[]->observers.insert; 
       #);
     detach:
       (# theObserver: ^observerType; 
       enter theObserver[]
       do theObserver[]->observers.delete
       #);
     notify:
       (# current: ^observerType; 
       do
          observers.scan
            (#  do current[]->THIS(notify).current[]; INNER notify #)
       #);
     notifyUpdate: notify (#  do current.update #);
     observers: @set (# element:: observerType #);
     
  #);
  

