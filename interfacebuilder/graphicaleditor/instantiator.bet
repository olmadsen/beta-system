ORIGIN 'graphicaleditor';
INCLUDE '~beta/guienv/v1.6/private/datastructures/association';

-- graphicalEditorEnvLib: attributes --

instantiator:
  (# theGraphicalEditor: ^graphicalEditor;
     type:< theGraphicalEditor.windowItemEditor;
     theWindowItemEditor: ^type
  enter theGraphicalEditor[]
  do &type[] -> theWindowItemEditor[];
     NONE -> theGraphicalEditor[];
  exit theWindowItemEditor[]
  #);

factory:
  (# instantiate:
       (# theGraphicalEditor: ^graphicalEditor;
          name: ^text;
          theWindowItemEditor: ^graphicalEditor.windowItemEditor;
       enter (theGraphicalEditor[], name[])
       do (# inst: ^instantiator;
             theName: ^text;
          do name.copy -> theName[];
             theName.makeLC;
             theName[] -> insts.lookup -> inst[];
             (if inst[] <> NONE then
                 theGraphicalEditor[] -> inst -> theWindowItemEditor[];
              else
                 exception
                 (# 
                 do 'The instantiator is missing for "' -> msg.putText;
                    name[] -> msg.putText;
                    '"' -> msg.putLine;
                 #);
             if);
          #);
       exit theWindowItemEditor[]
       #);
     defined: booleanValue
       (# name: ^text;
          theName: ^text;
       enter name[]
       do name.copy -> theName[];
          theName.makeLC;
          (theName[] -> insts.lookup) <> NONE -> value;
       #);
     init:
       (# 
       do insts.init;
          ('windowitem', &instantiator (# type:: theGraphicalEditor.windowItemEditor #)[]) -> insts.insert;
          ('separator', &instantiator (# type:: theGraphicalEditor.separatorEditor #)[]) -> insts.insert;
          ('separator-vertical', &instantiator (# type:: theGraphicalEditor.verSeparatorEditor #)[]) -> insts.insert;
          ('canvas', &instantiator (# type:: theGraphicalEditor.canvasEditor #)[]) -> insts.insert;
          ('scroller', &instantiator (# type:: theGraphicalEditor.scrollerEditor #)[]) -> insts.insert;
          ('texteditor', &instantiator (# type:: theGraphicalEditor.textEditorEditor #)[]) -> insts.insert;
          ('textfield', &instantiator (# type:: theGraphicalEditor.textFieldEditor #)[]) -> insts.insert;
          ('pushbutton', &instantiator (# type:: theGraphicalEditor.pushButtonEditor #)[]) -> insts.insert;
          ('iconbutton', &instantiator (# type:: theGraphicalEditor.iconButtonEditor #)[]) -> insts.insert;
          ('optionbutton', &instantiator (# type:: theGraphicalEditor.optionButtonEditor #)[]) -> insts.insert;
          ('radiobutton', &instantiator (# type:: theGraphicalEditor.radioButtonEditor #)[]) -> insts.insert;
          ('checkbox', &instantiator (# type:: theGraphicalEditor.checkBoxEditor #)[]) -> insts.insert;
          ('scrollbar', &instantiator (# type:: theGraphicalEditor.scrollbarEditor #)[]) -> insts.insert;
          ('scrollbar-vertical', &instantiator (# type:: theGraphicalEditor.verScrollbarEditor #)[]) -> insts.insert;
          ('edittext', &instantiator (# type:: theGraphicalEditor.editTextEditor #)[]) -> insts.insert;
          ('statictext', &instantiator (# type:: theGraphicalEditor.staticTextEditor #)[]) -> insts.insert;
          ('textscrolllist', &instantiator (# type:: theGraphicalEditor.textScrollListEditor #)[]) -> insts.insert;
          ('window', &instantiator (# type:: theGraphicalEditor.windowEditor #)[]) -> insts.insert;
       #);
     insts: @dictionary
       (# element:: instantiator;
       #);
  #);

