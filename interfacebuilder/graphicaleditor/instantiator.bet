ORIGIN 'graphicaleditor';
INCLUDE '~beta/guienv/v1.6/private/datastructures/association'
        '~beta/guienv/v1.6/private/datastructures/private/associationbody';
BODY 'private/instantiatorbody';
-- graphicalEditorEnvLib: Attributes --
instantiator:
  (#
     theGraphicalEditor: ^graphicalEditor;
     editorType:< theGraphicalEditor.windowItemEditor;
     theWindowItemEditor: ^editorType;
     index: @integer;
     name: ^text;
     path: ^text;
     icon: ^pixmap;
     scripts: ^scriptList;
     
  enter theGraphicalEditor[]
  do &editorType[]->theWindowItemEditor[]; none ->theGraphicalEditor[]; 
  exit theWindowItemEditor[]
  #);
scriptList: sequence (# element:: text #);
dictionary:
  (#
     element:< object;
     define:
       (# name: ^text; elm: ^element; 
       enter (name[],elm[])
       do (name[],elm[])->storage.element->storage.append
       #);
     lookup:
       (# name: ^text; elm: ^element; 
       enter name[]
       do
          l: storage.scan
            (# 
            do
               (if name[]->current.name.equalNCS then
                   current.elm[]->elm[]; leave l
               if)
            #)
       exit elm[]
       #);
     init:< (#  do storage.init; INNER #);
     dictionaryElement:
       (# name: ^text; elm: ^element
       enter (name[],elm[])
       exit THIS(dictionaryElement)[]
       #);
     storage: @sequence (# element:: dictionaryElement #)
  #);
factory:
  (#
     insert:
       (#
          index: @integer;
          name: ^text;
          inst: ^instantiator;
          icon: ^pixmap;
          scripts: ^scriptList;
          
       enter (icon[],name[],inst[])
       do
          name[]->inst.name[];
          icon[]->inst.icon[];
          &scriptList[]->scripts[];
          scripts.init;
          'onMouseUp'->scripts.append;
          'onMouseDown'->scripts.append;
          scripts[]->inst.scripts[];
          (name[],inst[])->insts.define;
          
       #);
     scan:
       (# current: ^instantiator
       do
          insts.storage.scan
            (#  do current.elm[]->THIS(scan).current[]; INNER scan #)
       #);
     instantiate:
       (#
          theGraphicalEditor: ^graphicalEditor;
          name: ^text;
          theWindowItemEditor: ^graphicalEditor.windowItemEditor;
          
       enter (theGraphicalEditor[],name[])
       <<SLOT factoryInstantiate:DoPart>>
       exit theWindowItemEditor[]
       #);
     defined: booleanValue
       (# name: ^text; 
       enter name[]
       <<SLOT factoryDefined:DoPart>>
       #);
     lookup:
       (# name: ^text; theInstantiator: ^instantiator; 
       enter name[]
       <<SLOT factoryLookup:DoPart>>
       exit theInstantiator[]
       #);
     init: (#  <<SLOT factoryInit:DoPart>> #);
     insts: @dictionary (# element:: instantiator;  #);
     
  #);
  

