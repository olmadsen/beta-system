ORIGIN '~beta/guienv/v1.6/guienvall';
BODY 'private/graphicaleditorprivate';


INCLUDE '../resourcesupport/parameters';
INCLUDE '../resourcesupport/easyinterface';
INCLUDE '~beta/mps/v5.2/astlevel';
INCLUDE '~beta/betaast/v5.2/betacfl';
INCLUDE '../asteditor/asteditor';
INCLUDE '../fragmenthandler/fragmenthandler';
INCLUDE '~beta/containers/v1.6/list';
INCLUDE '~beta/containers/v1.6/sets';
INCLUDE 'classinfo';

-- lib: attributes --

entryExists: booleanValue
  (# name: ^text;
  enter name[]
  <<SLOT entryExistsBody: doPart>>
  #);
entryReadable: booleanValue
  (# name: ^text;
  enter name[]
  <<SLOT entryReadableBody: doPart>>
  #);
entryWriteable: booleanValue
  (# name: ^text;
  enter name[]
  <<SLOT entryWriteAbleBody: doPart>>
  #);


-- guienvLib: attributes --

displayMessage:
  (# msg: ^text;
  enter msg[]
  do (none, msg[], 'Alert') -> noteUser;
  #);

graphicalEditorEnv:
  (# <<SLOT graphicalEditorEnvLib: attributes>>;
     
     platform: integerValue
       (#
       <<SLOT graphicalEditorPlatform: doPart>>
       #);
     winnt: (# exit 1 #);
     mac: (# exit 2 #);
     X11: (# exit 3 #);
     
     attributeList: list
       (# element:: betaGram.attributeDecl;
       #);
     imperativeList: list
       (# element:: betaGram.imp;
       #);
     
     checkIdentifier: booleanValue
       (# name: ^text;
          frag: ^astInterface.fragmentForm;
          nameDecl: ^astInterface.beta.nameDcl;
       enter name[]
       <<SLOT checkIdentifierDoPart: doPart>>
       #);

     MPS: ^astInterface;
     betaGram: ^astInterface.beta;
     pretty: ^astInterface.prettyPrinter;
     
     astFileExists: booleanValue
       (# name: ^text;
       enter name[]
       <<SLOT astFileExistsBody: doPart>>
       #);
     astFileReadable: booleanValue
       (# name: ^text;
       enter name[]
       <<SLOT astFileReadAbleBody: doPart>>
       #);
     astFileWriteAble: booleanValue
       (# name: ^text;
       enter name[]
       <<SLOT astFileWriteAbleBody: doPart>>
       #);
     
     showParseErrors:<
       (# msg: ^text;
       enter msg[]
       do inner;
       #);
     save:<
       object;
     autoSaveGroup:<
       (# fg: ^astInterface.fragmentGroup;
       enter fg[]
       do inner;
       #);
     
     theAstEditorEnv: @astEditorEnv
       (# showParseErrors::<
            (# 
            do msg[] -> this(graphicalEditorEnv).showParseErrors;
            #);
       #);
 
     openAstEditorMethod:
       (# node: ^astInterface.AST;
          title: ^text;
       enter (node[], title[])
       do inner;
       #);
     openSeparateAstEditorMethod: openAstEditorMethod
       (# 
       <<SLOT graphicalEditorEnvOpenSeparateAstEditorMethod: doPart>>
       #);
     openAstEditor:< openAstEditorMethod;
     
     onGroupOpen:<
       (# fg: ^astInterface.fragmentGroup;
       enter fg[]
       do inner;
       #);
     onGroupChanged:<
       (# fg: ^astInterface.fragmentGroup;
       enter fg[]
       do inner;
       #);
     onGroupSaved:<
       (# fg: ^astInterface.fragmentGroup;
       enter fg[]
       do inner;
       #);
     saveFragmentGroup:<
       (# fg: ^astInterface.fragmentGroup;
       enter fg[]
       do inner;
       #);
     theFragServer: ^fragmentServer;
     
     init:<
       (# 
       enter (theFragServer[], MPS[], betaGram[], pretty[])
       <<SLOT graphicalEditorEnvInit: doPart>> 
       #);
     
     find:
       (# node: ^astInterface.beta.attributeDecl;
          editor: ^graphicalEditor;
       enter node[]
       <<SLOT graphicalEditorEnvFind: doPart>>
       exit editor[]
       #);
          
     classData:
       (# theClass: ^classInfo.class;
          theAttributes: ^attributeList;
          theImperatives: ^imperativeList;
          node: ^astInterface.beta.attributeDecl;
          superClassData: ^classData;
          vertical: @boolean;
          init:
            (# 
               group: ^astInterface.fragmentGroup;
            enter (theClass[], node[], group[])
            <<SLOT classDataInit: doPart>>
            #);
       #);
     
     getClassData:
       (# node: ^astInterface.beta.attributeDecl;
          theClassData: ^classData;
       enter node[]
       <<SLOT getClassDataDoPart: doPart>>
       exit theClassData[]
       #);
     
     graphicalEditor: window
       (# <<SLOT graphicalEditorLib: attributes>>;
          
          readOnly: @boolean;
          menubarType::<
            (# open::<
                 (# <<SLOT InterfaceBuilderGraphicalEditorWindowMenubarOpen: doPart>> #);
            #);
          
          (*
           * theDocument: ^document;
           *)
          
          node: ^astInterface.beta.attributeDecl;
          
          privateAttributes:
            (# value: @boolean
            enter (# enter value <<SLOT GraphicalEditorEnterPrivateAttributes: doPart>> #)
            exit (# <<SLOT GraphicalEditorExitPrivateAttributes: doPart>> exit value #)
            #);
          privateDoParts:
            (# value: @boolean
            enter (# enter value <<SLOT GraphicalEditorEnterPrivateDoParts: doPart>> #)
            exit (# <<SLOT GraphicalEditorExitPrivateDoPart: doPart>> exit value #)
            #);
          
          (* astControl: @astController
           *             (# 
           *                onReplaced::<
           *                  (# <<SLOT GraphicalEditorAstControlReplaced: doPart>> #);
           *             #);
           *)
          
          windowItemEditorList: sequence
            (# element:: windowItemEditor;
               copy:
                 (# theCopy: ^windowItemEditorList;
                 <<SLOT windowItemEditorListCopy: doPart>>
                 exit theCopy[]
                 #);
               set:
                 (# theOther: ^windowItemEditorList;
                 enter theOther[]
                 <<SLOT windowItemEditorListSet: doPart>>
                 #);
            #);
          
          windowitemEditor: 
            (# <<SLOT windowitemEditorLib: attributes>>;
               
               node: ^astInterface.beta.attributeDecl;
               owner: ^canvasEditor;
               windowitemType:< windowitem;
               theWindowitem: ^windowitemType;
               defaultData, data: ^parameters;
               theClassData: ^classData;
               privateFrag: ^astInterface.fragmentForm;
               
               showInfoDialog:
                 (# <<SLOT GraphicalEditorWindowItemEditorShowInfoDialog: doPart>> #);
               
               editAttributes:
                 (#  <<SLOT GraphicalEditorWindowItemEditorEditAttributes: doPart>> #);
               editVirtual:
                 (# name: ^text;
                 enter name[]
                 <<SLOT GraphicalEditorWindowItemEditorEditVirtual: doPart>>
                 #);
               
               init:<
                 (# type: @integer;
                    inherited: @boolean;
                    initialize:<
                      (# done: @boolean;
                      <<SLOT windowItemEditorInitialize: doPart>>
                      #);
                 enter (owner[], node[], data[], type, inherited, theClassData[], privateFrag[])
                 <<SLOT GraphicalEditorWindowItemEditorInit: doPart>>
                 #);
               newData:<
                 (# type: @integer;
                    data: ^parameters;
                 enter type
                 <<SLOT GraphicalEditorWindowItemEditorNewData: doPart>> 
                 exit data[]
                 #);
               createInitImps:<
                 (# frag: ^astInterface.fragmentForm;
                    initImps: ^astInterface.beta.imperatives;
                    override:< booleanValue;
                 enter frag[]
                 <<SLOT GraphicalEditorWindowItemEditorCreateInitImps: doPart>>
                 exit initImps[]
                 #);
               createOpen:<
                 (# newImps: ^astInterface.beta.imperatives;
                 <<SLOT windowItemEditorCreateOpen: doPart>>
                 #);
               new:< 
                 (# type: @integer;
                    
                    newNode:<
                      (# <<SLOT windowItemEditorNewNode: doPart>> #);
                 enter (owner[], type, theClassData[])
                 <<SLOT windowitemEditornew: doPart>> 
                 #);
               close:<
                 (# <<SLOT GraphicalEditorWindowItemEditorClose: doPart>> #);
               mousedown:<
                 (# done: @boolean;
                    theEvent: ^windowItem.eventHandler.mouseDown;
                    pt: @point;
                 enter (theEvent[], pt)
                 <<SLOT windowitemEditorMousedown: doPart>>
                 #);
               select:
                 (# <<SLOT windowItemEditorSelect: doPart>> #);
               deSelect:
                 (# <<SLOT windowItemEditorDeSelect: doPart>> #);
               applyObjectData:<
                 (# 
                 <<SLOT IBwindowitemEditorapplyObjectData: doPart>>
                 #);
               findEditor:<
                 (# pt: @point;
                    ignoreEditor: ^windowItemEditor;
                    theEditor: ^windowItemEditor;
                 enter (pt, ignoreEditor[])
                 <<SLOT graphicalEditorWindowItemEditorFindEditor: doPart>>
                 exit theEditor[]
                 #);
               getPublicAttributes:<
                 (# theAttributes: ^astInterface.beta.attributes;
                 <<SLOT windowItemEditorGetPublicAttributes: doPart>>
                 exit theAttributes[]
                 #);
               getPrivateAttributes:<
                 (# theAttributes: ^astInterface.beta.attributes;
                 <<SLOT windowItemEditorGetPrivateAttributes: doPart>>
                 exit theAttributes[]
                 #);
               getMainAttributes:<
                 (# theAttributes: ^astInterface.beta.attributes;
                 <<SLOT windowItemEditorGetMainAttributes: doPart>>
                 exit theAttributes[]
                 #);
               getMainNode:<
                 (# theNode: ^astInterface.beta.attributeDecl;
                 do node[] -> theNode[];
                    inner;
                 exit theNode[]
                 #);
               createTheWindowItem:<
                 (# initial: @boolean;
                 enter initial
                 <<SLOT GraphicalEdioterWindowItemEditorCreateWindowItem: doPart>> 
                 #);
                
               makeDelegateMousedownAction:
                 (# aWindowItem: ^windowItem;
                    enclosing: ^windowItemEditor;
                 enter (aWindowItem[], enclosing[])
                 <<SLOT makeDelegateMousedownAction: doPart>>
                 #);
               private: @<<SLOT windowitemEditorPrivate: descriptor>>;
            #);
          canvasEditor: windowitemEditor
            (# <<SLOT canvasEditorLib: attributes>>;
               children: @windowItemEditorList;
               locked: @boolean;
               newEditor:
                 (# type: @integer;
                    theEditor: ^windowitemEditor;
                    theClassData: ^classData;
                 enter (type, theClassData[])
                 <<SLOT graphicalEditorNewEditor: doPart>>
                 exit theEditor[]
                 #);
               windowitemType::< canvas;
               getMainCanvas:<
                 (# theCanvas: ^canvas;
                 do theWindowItem[] -> theCanvas[];
                    inner;
                 exit theCanvas[]
                 #);
               newData::< (# <<SLOT GraphicalEditorCanvasEditorNewData: doPart>> #);
               new::< 
                 (# 
                 <<SLOT canvasEditornew: doPart>> 
                 #);
               init::< 
                 (# initialize::<
                      (# <<SLOT canvasEditorInitialize: doPart>> #);
                 <<SLOT canvasEditorInit: doPart>> 
                 #);
               close::<
                 (# <<SLOT GraphicalEditorCanvasEditorClose: doPart>> #);
               mousedown::< (# <<SLOT canvasEditorMousedown: doPart>> #);
               applyObjectData::<
                 (# 
                 <<SLOT IBcanvasEditorapplyObjectData: doPart>>
                 #);
               findEditor:: (# <<SLOT graphicalEditorCanvasEditorFindEditor: doPart>> #);
               private: @<<SLOT canvasEditorPrivate: descriptor>>;
               createTheWindowItem::<
                 (# <<SLOT GraphicalEditorCanvasEditorCreateTheWindowItem: doPart>> #);
               createOpen::<
                 (# <<SLOT CanvasEditorCreateOpen: doPart>> #)
            #);
          windowEditor: canvasEditor
            (# newData::<
                 (# <<SLOT windowEditorNewData: doPart>> #);
               
            #);
          
          (* 
           * Mode
           *)
          
          selectMode: (# exit 1 #);
          hierarkiMode: (# exit 2 #);
          resizeMode: (# exit 3 #);
          
          mode:
            (# value: @integer;
            enter (# enter value <<SLOT graphicalEditorEnterMode: doPart>> #)
            exit (# <<SLOT graphicalEditorExitMode: doPart>>  exit value #)
            #);
          (*
           * Init
           *)
          
          init:<
            (#
            enter node[]
            <<SLOT GraphicalEditorInit: doPart>>
            #);
          new:<
            (# 
            enter node[]
            <<SLOT GraphicalEditorNew: doPart>>
            #);
          
          (*
           * Selection
           *)
          
          
          canUndo: booleanValue
            (# <<SLOT graphicalEditorCanUndo: doPart>> #);
          undo:
            (# 
            <<SLOT graphicalEditorUndo: doPart>>
            #);
          undoText:
            (# value: ^text;
            <<SLOT graphicalEditorUndoText: doPart>>
            exit value[]
            #);
          canRedo: booleanValue
            (# <<SLOT graphicalEditorCanRedo: doPart>> #);
          redo:
            (# 
            <<SLOT graphicalEditorRedo: doPart>>
            #);
          redoText:
            (# value: ^text;
            <<SLOT graphicalEditorRedoText: doPart>>
            exit value[]
            #);
          canPaste: booleanValue
            (# 
            <<SLOT graphicalEditorCanPaste: doPart>>
            #);
          paste:
            (# 
            <<SLOT graphicalEditorPaste: doPart>>
            #);
          selection: @windowItemEditorList
            (# <<SLOT selectionLib: attributes>>;
               
               append::
                 (#
                 <<SLOT selectionAppend: doPart>>
                 #);
               prepend::
                 (# 
                 <<SLOT selectionPrepend: doPart>>
                 #);
               delete::
                 (#
                 <<SLOT selectionDelete: doPart>>
                 #);
               clear::
                 (# 
                 <<SLOT selectionClear: doPart>>
                 #);
               
               (* Actions *)
               deleteAll:
                 (# <<SLOT GraphicalEditorSelectionDelete: doPart>> #);
	       showInfo:
                 (# <<SLOT GraphicalEditorSelectionShowInfo: doPart>> #);
               fitToContents:
                 (# <<SLOT GraphicalEditorSelectionFitToContents: doPart>> #);
               alignLeft:
                 (# <<SLOT GraphicalEditorSelectionAlignLeft: doPart>> #);
               alignRight:
                 (# <<SLOT GraphicalEditorSelectionAlignRight: doPart>> #);
               alignTop:
                 (# <<SLOT GraphicalEditorSelectionAlignTop: doPart>> #);
               alignBottom:
                 (# <<SLOT GraphicalEditorSelectionAlignBottom: doPart>> #);
               alignHorCenter:
                 (# <<SLOT GraphicalEditorSelectionAlignHorCenter: doPart>> #);
               alignVerCenter:
                 (# <<SLOT GraphicalEditorSelectionAlignVerCenter: doPart>> #);
               showSpacingDialog:
                 (# <<SLOT GraphicalEditorSelectionShowSpacingDialog: doPart>> #);
               doCopy:
                 (# <<SLOT graphicalEditorSelectionDoCopy: doPart>> #);
               doCut:
                 (# <<SLOT graphicalEditorSelectionDoCut: doPart>> #);
            #);

          open::<
            (# width, height: @integer;
            enter (width, height)
            <<SLOT graphicalEditorOpen: doPart>> 
            #);
          close::<
            (# <<SLOT graphicalEditorClose: doPart>> #);
          eventHandler::<
            (# onMouseDown::<
                 (# <<SLOT graphicalEditorOnMouseDown: doPart>> #);
               onAboutToClose::<
                 (# <<SLOT graphicalEditorOnAboutToClose: doPart>> #);
               onActivate::<
                 (# 
                 <<SLOT graphicalEditorOnActivate: doPart>>
                 #);
               onDeactivate::<
                 (#
                 <<SLOT graphicalEditorOnDeactivate: doPart>>
                 #)
               
            #);
          private: @<<SLOT graphicalEditorPrivateAttributes: descriptor>>;
          

       #);
     
     trace:
       (# xT: 
            (# xT: ^text 
            enter xT[] 
            do xT[]->tracestream.putText 
            #);
          xC:
            (# c: @char;
            enter c
            do c->tracestream.put;
            #);
          xI: 
            (# n: @integer 
            enter n 
            do ' '->tracestream.put; n->tracestream.putint; 
               ' '->tracestream.put 
            #);
          xB: 
            (# b: @boolean
            enter b
            do (if b then ' true '->traceStream.puttext 
                else ' false '->traceStream.puttext 
               if)
            #);
          xA: 
            (# T: ^astInterface.AST;
            enter T[] 
            do (if T[] = NONE then
                   ' NONE ' -> traceStream.putText;
                else
                   (T[], traceStream[]) -> pretty.printNode;
               if);
               xN;
            #);
          xClass:
            (# class: ^classData;
            enter class[]
            do (if class[] = NONE then
                   ' NONE ' -> xT;
                else
                   (if class.theClass[] = NONE then
                       'class.theClass[] is NONE' -> xT;
                    else
                       class.theClass.name[] -> xT;
                       ' vertical =' -> xT;
                       class.vertical -> xB;
                   if);
               if);
            #);
          xN: (# do tracestream.newline #);
          no: @integer;
       enter no
       do (if switch[no] then
              Xn;
              '$' -> xC;
              no -> xI;
              ':' -> xC;
              INNER;
          if);
       #);
     traceStream: ^stream;
     switch: [255] @boolean;
     private: @<<SLOT graphicalEditorEnvPrivate: descriptor>>;
  #);



-- graphicalEditorLib: attributes --


textEditorPict: canvas
  (# <<SLOT textEditorPictLib: attributes>>;
     open::<
       (# <<SLOT textEditorPictOpen: doPart>> #);
     private: @<<SLOT textEditorPictPrivate: descriptor>>;
  #);
verScrollbar: scrollbar
  (# vertical:: (# do true -> value #);
  #);
verSeparator: separator
  (# vertical:: (# do true -> value #);
  #);

