ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/auxcontrols'
        '../dialogs/friggdialog'
        'graphicaleditor'
        '../code/match'
        '~beta/guienv/controls';
-- guienvLib: Attributes --
propagateInterfaceDialog: friggDialog
  (#
     type:: windowTypes.dialog;
     open::< 
       (# 
       do
          (435,145)->size;
          'Propagate Interface'->dialogTitle;
          'Propagate Interface'->title;
          propagateInterfaceView1.open;
          INNER
       #);
     propagateInterfaceView1: @propagateInterfaceView
       (# open::<  (#  do (340,110)->size; (0,30)->position; INNER #)
       #)
  #);
propagateInterface:
  (#
     dialog: @propagateInterfaceDialog
       (#
          propagate:
            (#
               name: ^text;
               attribute: ^text;
               fromEditor: ^graphicalEditorEnv.graphicalEditor.componentEditor;
               toEditor: ^graphicalEditorEnv.graphicalEditor.componentEditor;
               gram: @grammar;
               
            enter (fromEditor[],toEditor[],name[],attribute[])
            do
                 (#
                    path: ^text;
                    current:
                      ^graphicalEditorEnv.graphicalEditor.componentEditor;
                    txt: ^text;
                    decl: ^astInterface.beta.attributeDecl;
                    
                 do
                    fromEditor[]->current[];
                    &text[]->path[];
                    attribute[]->path.prepend;
                    '.'->path.prepend;
                    current.comp.name->path.prepend;
                    current.owner[]->current[];
                    l:
                    (if current[] <> toEditor[] then
                        '.'->path.prepend;
                        current.comp.name->path.prepend;
                        current.owner[]->current[];
                        restart l;
                        
                    if);
                    &text[]->txt[];
                    name[]->txt.putText;
                    ': (# enter '->txt.putText;
                    path[]->txt.putText;
                    ' exit '->txt.putText;
                    path[]->txt.putText;
                    ' #) '->txt.putText;
                    (toEditor.node.frag[],txt[],gram.attributeDecl)->parseText
                      ->decl[];
                    decl[]->toEditor.comp.append;
                    
                 #);
               
            #);
          onConfirm:: 
            (#
               name: ^text;
               toEditor,current:
                 ^graphicalEditorEnv.graphicalEditor.componentEditor;
               attribute: ^text;
               where: ^text;
               
            do
               propagateInterfaceView1.newAttributeFld.textContents->name[];
               propagateInterfaceView1.oldAttributeOpt.textContents
                 ->attribute[];
               propagateInterfaceView1.whereOpt.textContents->where[];
               editor.owner[]->current[];
               l:
               (if current.comp.name->where.equalNCS then
                   current[]->toEditor[]; 
                else
                   current.owner[]->current[]; restart l; 
               if);
               (editor[],toEditor[],name[],attribute[])->propagate;
               
            #);
          onCancel::  (#  do close;  #);
          pop1,pop2: @menu
            (#
               add:
                 (# name: ^text; item: ^menuItem; 
                 enter name[]
                 do
                    &menuItem[]->item[];
                    item.open;
                    name[]->item.name;
                    item[]->append;
                    
                 #);
               
            #);
          open:: 
            (# current: ^graphicalEditorEnv.graphicalEditor.componentEditor; 
            do
               pop1.open;
               editor.owner[]->current[];
               l:
               (if current[] <> none then
                   current.comp.name->pop1.add;
                   current.owner[]->current[];
                   restart l;
                   
               if);
               pop1[]->propagateInterfaceView1.whereOpt.popupMenu;
               pop2.open;
               editor.data.data.storage.scan
                 (#  do current.key[]->pop2.add;  #);
               pop2[]->propagateInterfaceView1.oldAttributeOpt.popupMenu;
               
            #);
          
       #);
     editor: ^graphicalEditorEnv.graphicalEditor.componentEditor;
     
  enter editor[]
  do dialog.open; 
  #)  

-- windowLib: Attributes --
propagateInterfaceView: canvas
  (#
     open::< 
       (# 
       do
          (335,95)->size;
          oldAttributeOpt.open;
          lab1.open;
          newAttributeFld.open;
          whereOpt.open;
          lab2.open;
          lab3.open;
          INNER
       #);
     oldAttributeOpt: @optionbutton
       (#
          textContents:
            (# value: ^text; 
            do (currentItem->(popupMenu).getMenuItemByNumber).name->value[]; 
            exit value[]
            #);
          open::< 
            (#  do ''->label; (225,25)->size; (100,5)->position; INNER #)
       #);
     lab1: @caption
       (#
          open::< 
            (# 
            do 'Attribute:'->label; (60,13)->size; (15,11)->position; INNER
            #)
       #);
     newAttributeFld: @dialogField
       (# open::<  (#  do (220,21)->size; (105,35)->position; INNER #)
       #);
     whereOpt: @optionbutton
       (#
          textContents:
            (# value: ^text; 
            do (currentItem->(popupMenu).getMenuItemByNumber).name->value[]; 
            exit value[]
            #);
          open::< 
            (#  do ''->label; (225,25)->size; (100,65)->position; INNER #)
       #);
     lab2: @caption
       (#
          open::< 
            (# 
            do 'Name:'->label; (36,13)->size; (15,39)->position; INNER
            #)
       #);
     lab3: @caption
       (#
          open::< 
            (# 
            do
               'Propagate To:'->label; (80,13)->size; (15,71)->position; INNER
            #)
       #)
  #)  

