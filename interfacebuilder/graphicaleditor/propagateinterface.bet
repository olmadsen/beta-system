ORIGIN '~beta/guienv/guienv';
INCLUDE '../support/auxcontrols'
        'graphicaleditor'
        '../code/match'
        '~beta/guienv/controls';
-- guienvLib: Attributes --
propagateInterfaceDialog: window
  (# type:: windowTypes.dialog;
     open::< 
       (#  do (340,190)->size; propagateInterfaceView1.open; INNER #);
     onPropagate:< 
       (# do INNER #);
     onCancel:< 
       (# do INNER #);
     propagateInterfaceView1: @propagateInterfaceView
       (#
          open::<  (#  do (340,190)->size; (0,0)->position; INNER #);
          onPropagate::
            (#  do THIS(propagateInterfaceDialog).onPropagate #);
          onCancel:: (#  do THIS(propagateInterfaceDialog).onCancel #)
       #)
  #);
propagateInterface: 
  (# dialog: @propagateInterfaceDialog
       (# propagate:
            (# name: ^text;
               attribute: ^text;
               fromEditor: ^graphicalEditorEnv.graphicalEditor.componentEditor;
               toEditor: ^graphicalEditorEnv.graphicalEditor.componentEditor;
               gram: @grammar;
            enter (fromEditor[], toEditor[], name[], attribute[])
            do (# path: ^text;
                  current: ^graphicalEditorEnv.graphicalEditor.componentEditor;
                  txt: ^text;
                  decl: ^astInterface.beta.attributeDecl;
               do fromEditor[] -> current[];
                  &text[] -> path[];
                  attribute[] -> path.prepend;
                  '.' -> path.prepend;
                  current.comp.name -> path.prepend;
                  current.owner[] -> current[];
                  l: (if current[] <> toEditor[] then
                         '.' -> path.prepend;
                         current.comp.name -> path.prepend;
                         current.owner[] -> current[];
                         restart l;
                     if);
                  &text[] -> txt[];
                  name[] -> txt.putText;
                  ': (# enter ' -> txt.putText;
                        path[] -> txt.putText;
                        ' exit ' -> txt.putText;
                        path[] -> txt.putText;
                        ' #) ' -> txt.putText;
                  (toEditor.node.frag[], txt[], gram.attributeDecl) -> parseText -> decl[];
                  decl[] -> toEditor.comp.append;
               #);
            #);
          onPropagate::
            (# name: ^text;
               toEditor, current: ^graphicalEditorEnv.graphicalEditor.componentEditor;
               attribute: ^text;
               where: ^text;
            do propagateInterfaceView1.newAttributeFld.textContents -> name[];
               propagateInterfaceView1.oldAttributeOpt.textContents -> attribute[];
               propagateInterfaceView1.whereOpt.textContents -> where[];
               editor.owner[] -> current[];
               l: (if current.comp.name -> where.equalNCS then
                      current[] -> toEditor[];
                   else
                      current.owner[] -> current[];
                      restart l;
                  if);
               (editor[], toEditor[], name[], attribute[]) -> propagate;
            #);
          onCancel::
            (#
            do close;
            #);
          pop1, pop2: @menu
            (# add:
                 (# name: ^text;
                    item: ^menuItem;
                 enter name[]
                 do &menuItem[] -> item[];
                    item.open;
                    name[] -> item.name;
                    item[] -> append;
                 #);
            #);
          open::
            (# current: ^graphicalEditorEnv.graphicalEditor.componentEditor;
            do pop1.open;
               editor.owner[] -> current[];
               l:
                 (if current[] <> NONE then
                     current.comp.name -> pop1.add;
                     current.owner[] -> current[];
                     restart l;
                 if);
               pop1[] -> propagateInterfaceView1.whereOpt.popupMenu;
               pop2.open;
               editor.data.data.storage.scan
               (#
               do current.key[] -> pop2.add;
               #);
               pop2[] -> propagateInterfaceView1.oldAttributeOpt.popupMenu;
               
            #);
       #);
     editor: ^graphicalEditorEnv.graphicalEditor.componentEditor;
  enter editor[]
  do dialog.open;
  #)  

-- windowLib: Attributes --
propagateInterfaceView: canvas
  (#
     onPropagate:< (#  do INNER #);
     onCancel:< (#  do INNER #);
     open::< 
       (# 
       do
          (340,190)->size;
          oldAttributeOpt.open;
          caption1.open;
          newAttributeFld.open;
          whereOpt.open;
          separator1.open;
          propagateBtn.open;
          cancelBtn.open;
          INNER
       #);
     oldAttributeOpt: @optionbutton
       (# textContents:
            (# value: ^text;
            do (currentItem -> (popupMenu).getMenuItemByNumber).name -> value[];
            exit value[]
            #);
          open::< 
            (# 
            do
               'Attribute To Propagate'->label;
               (310,35)->size;
               (15,15)->position;
               INNER
            #)
       #);
     caption1: @caption
       (#
          open::< 
            (# 
            do
               'Name Of New Attribute:'->label;
               (136,17)->size;
               (20,55)->position;
               INNER
            #)
       #);
     newAttributeFld: @dialogField
       (# open::<  (#  do (305,21)->size; (20,75)->position; INNER #)
       #);
     whereOpt: @optionbutton
       (# textContents:
            (# value: ^text;
            do (currentItem -> (popupMenu).getMenuItemByNumber).name -> value[];
            exit value[]
            #);
          open::< 
            (# 
            do
               'Propagate To:'->label;
               (300,35)->size;
               (20,105)->position;
               INNER
            #)
       #);
     separator1: @separator
       (# open::<  (#  do (305,4)->size; (20,145)->position; INNER #)
       #);
     propagateBtn: @pushbutton
       (#
          open::< 
            (# 
            do
               'Propagate'->label;
               (60,20)->size;
               (250,155)->position;
               THIS(pushButton)[]->defaultButton;
               INNER
            #);
          eventHandler::< (# onMouseUp::< (#  do onPropagate #) #)
       #);
     cancelBtn: @pushbutton
       (#
          open::< 
            (# 
            do 'Cancel'->label; (60,20)->size; (175,155)->position; INNER
            #);
          eventHandler::< (# onMouseUp::< (#  do onCancel #) #)
       #)
  #)  

