ORIGIN '../canvaseditor';
INCLUDE 'graphicaleditorprivate'
        'windowitemeditorbody';

-- canvasEditorLib: Attributes --


  
getMainFrag:
  (#
     theAttributes: ^astInterface.beta.attributes;
     frag: ^astInterface.fragmentForm;
     
  do
     getMainAttributes->theAttributes[];
     (if theAttributes[] <> none then theAttributes.frag[]->frag[];  if);
     
  exit frag[]
  #);

scanAttributes:
  (#
     current: ^astInterface.beta.attributeDecl;
     theAttributes: ^astInterface.beta.attributes;
     
  do
     getMainAttributes->theAttributes[];
     (if theAttributes[] <> none then
         theAttributes.iterate
           (# 
           do
              current[]->THIS(scanAttributes).current[]; INNER scanAttributes; 
           #);
         
     if);
     
  #);
  
  
-- canvasEditorNewData: DoPart --
do
   (if hasBorder then
       borderStyles.shadowIn->data.borderStyle; false->data.borderVisible; 
   if);
   INNER  

-- canvasEditorInitialize: DoPart --
do (#  initAttributes:
        (# aNode: ^astInterface.beta.attributeDecl;
           super: @boolean;
           aComp: @component;
           theAttributes: ^attributeList;
           theImperatives: ^imperativeList;
           prefixName: ^text;
        enter (aNode[], super)
        do (if aNode[] <> NONE then
               aNode[] -> aComp.node[];
               aComp.prefix -> prefixName[];
               (if prefixName[] <> NONE then
                   prefixName[] -> lookup -> inst[];
                   (if inst[] = NONE then
                       (aNode[] -> getSuperPattern, true) -> initAttributes;
                    else
                       (if not inst.basic then
                           (if not inst.compiled then
                               (inst.node[], true) -> initAttributes;
                           if);
                       if);
                   if);
               if);
               aNode[] -> getAttributes -> (theAttributes[], theImperatives[]);
               (data[],theImperatives[])->initData;
               applyObjectData;
               theAttributes.scan
               (# theEditor: ^windowItemEditor;
                  theClassName: ^text;
                  vertical: @boolean;
               do
                  (if current[]->isStaticItem then
                      current[] -> getClassName -> theClassName[];
                      current[] -> getVertical -> vertical;
                      (if theClassName[] <> none then
                          (theClassName[],vertical)
                            ->makeEditor->theEditor[];
                          (if theEditor[] <> none then
                              (THIS(canvasEditor)[],current[],none ,super,none )->theEditor.init
                          if);
                          
                      if);
                      
                  if);
                  
               #);
            else
               'NONE' -> putLine;
           if);
        #);
      proto: ^astInterface.AST;
      isStaticItem: booleanValue
        (# node: ^astInterface.beta.attributeDecl; 
        enter node[]
        do (mps[],node[],proto[])->match->value; 
        #);
      frag: ^astInterface.fragmentForm;
      inst: ^instantiator;
      
   do className[]->lookup->inst[];
      true->createTheWindowItem;
      betaGram[]->mps.newFragmentForm->frag[];
      (frag[],'<<nameDcl>>: @<<nameApl>> <<mainpart>>',betaGram.simpleDecl)
        ->parseText->proto[];
      (node[],inherited)->initAttributes;
      true->done;
      
   #);
     
	  
-- canvasEditorClose: DoPart --
do INNER ; children.scan (#  do current.close;  #);   

-- canvasEditorMousedown: DoPart --
do
   INNER mouseDown;
   (if not skip then
       (if not theEvent.doubleClick then
           (if (owner[] = none ) or (theEvent.controlKey) then
                 (#
                    r: @rectangle;
                    width,height: @integer;
                    intersectedObjects: @componentEditorList;
                    
                 do
                    intersectedObjects.init;
                    (theEvent.localPosition,theEvent.localPosition)->r;
                    (r,false,false,true,true)->theWindowItem.defineRect->r;
                    children.scan
                      (# itsFrame: ^rectangle; 
                      do
                         (if not current.inherited then
                             current.frame->itsFrame[];
                             (if (r,itsFrame)->itsFrame.intersection then
                                 current[]->intersectedObjects.append; 
                             if);
                             
                         if);
                         
                      #);
                    (if intersectedObjects.empty then
                        (if theEvent.shiftKey then
                            (if selected then deSelect;  else select;  if); 
                         else
                            selection.clear; select; 
                        if);
                        
                     else
                        (if theEvent.shiftKey then
                            intersectedObjects.scan
                              (# 
                              do
                                 (if current.selected then
                                     current.deSelect; 
                                  else
                                     current.select; 
                                 if);
                                 
                              #);
                            
                         else
                            selection.clear;
                            intersectedObjects.scan
                              (#  do current.select;  #);
                            
                        if);
                        
                    if);
                    true->skip;
                    
                 #);
               
           if)
       if);
       
   if);
     
-- canvasEditorAppendChild: doPart --
do (# theDoPart: ^astInterface.beta.doPart;
   do child[]->children.append;
   	  child.node[] -> comp.append;
   	  child.node.frag[] -> fragmentChanged;
      forOpenStatements -> findOpenDoPart -> theDoPart[];
	  (if theDoPart[] <> NONE then
         theDoPart.frag[]->child.generateOpenImp->appendOpenImp;
	  if);
   #);

-- canvasEditorDeleteChild: doPart --
do 
     
-- canvasEditorFindEditor: DoPart --
do
   (if not locked then
       search: children.scan
         (# theFrame: ^rectangle; 
         do
            (if current[] <> ignoreEditor[] then
                current.frame->theFrame[];
                (if pt->theFrame.containsPoint then
                    theFrame.topLeft->pt.subTract;
                    (pt,ignoreEditor[])->current.findEditor->theEditor[];
                    leave search;
                    
                if);
                
            if);
            
         #);
       (if theEditor[] = none then THIS(canvasEditor)[]->theEditor[];  if);
       
   if);
     
-- canvasEditorCreateTheWindowItem: DoPart --
do
   theWindowItem.scan
     (# 
     do
        (current[],THIS(canvasEditor)[])->makeDelegateMousedownAction;
        current.disableDefaultBehaviour;
        none ->current.theCursor;
        
     #);
   children.scan (#  do current.createTheWindowItem;  #); 
   INNER ;
	
-- recievingCanvasEditorAccept: DoPart --
do
   (if not locked then
       (if dragee.palette then
           TRUE->value; 
        else
           (if (THIS(graphicalEditor).selection.first).owner[] = THIS(
           canvasEditor)[] then
               TRUE->value; 
            else
               THIS(canvasEditor)[]->THIS(graphicalEditor).selection.moveAllowed
                 ->value;
               
           if);
           
       if);
       
   if);
     
-- recievingCanvasEditorRecieve: DoPart --
do (THIS(canvasEditor)[],bounds[])->dragee.moveTo;   

-- windowEditorNewData: DoPart --
do 'untitled'->data.title; INNER ;   

-- windowEditorApplyObjectData: DoPart --
do false->theWindowItem.border.visible; data.title->titleBar; INNER  
