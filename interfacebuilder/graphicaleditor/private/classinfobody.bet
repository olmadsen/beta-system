ORIGIN '../classinfo';
INCLUDE '~beta/guienv/private/datastructures/association';

-- lib: attributes --

interfaceObjectType: (# exit 1 #);
windowitemType: (# exit 2 #);
separatorType: (# exit 3 #);
canvasType: (# exit 4 #);
abstractScrollerType: (# exit 5 #);
scrollerType: (# exit 6 #);
textEditorType: (# exit 7 #);
controlType: (# exit 8 #);
buttonType: (# exit 9 #);
pushButtonType: (# exit 10 #);
iconButtonType: (# exit 11 #);
optionButtonType: (# exit 12 #);
staticTextType: (# exit 13 #);
toggleButtonType: (# exit 14 #);
radioButtonType: (# exit 15 #);
checkBoxType: (# exit 16 #);
scrollbarType: (# exit 17 #);
editTextType: (# exit 18 #);
textFieldType: (# exit 19 #);
scrollListType: (# exit 20 #);
textScrollListType: (# exit 21 #);
windowType: (# exit 22 #);
menuType: (# exit 23 #);
menubarType: (# exit 24 #);
menuitemType: (# exit 25 #);
dynamicMenuitemType: (# exit 26 #);
verScrollbarType: (# exit 27 #);
verSeparatorType: (# exit 28 #);
rectType: (# exit 29 #);


-- ClassInfoLookup: doPart --
do name.makeLC;
   name[] -> private.table.lookup -> theClass[];
   (* (if theClass[] = none then
    *        'windowitem' -> private.table.lookup -> theClass[];
    *    if);
    *)
   inner;
   
-- ClassInfoInit: doPart --
do private.table.init;
   ('interfaceObject', none, true) -> private.table.add
   (# 
   do ('onMouseUp', true) -> addMethod;
      ('onMouseDown', true) -> addMethod;
      ('onKeyDown', true) -> addMethod;
      ('onRefresh', true) -> addMethod;
      ('onActivate', true) -> addMethod;
      ('onDeactivate', true) -> addMethod;
      
      ('open', false) -> addMethod;
      ('close', false) -> addMethod;
      
      ('windowitem', theClass[], true) -> private.table.add 
      (# 
      do ('onVisibleChanged', true) -> addMethod;
         ('onFrameChanged', true) -> addMethod;
         ('onFatherFrameChanged', true) -> addMethod;
         ('onEnableChanged', true) -> addMethod;
         ('onEnableTarget', true) -> addMethod;
         ('onDisableTarget', true) -> addMethod;
         ('onBorderStyleChanged', true) -> addMethod;
         ('onBorderVisibleChanged', true) -> addMethod;
         ('onTheCursorChanged', true) -> addMethod;
         ('onHiliteChanged', true) -> addMethod;
         
         ('separator', theClass[], true) -> private.table.add;
         ('canvas', theClass[], true) -> private.table.add
         (# 
         do ('onChildFrameChanged', true) -> addMethod;
            ('abstractScroller', theClass[], true) -> private.table.add
            (# 
            do
               ('scroller', theClass[], true) -> private.table.add;
               ('textEditor', theClass[], true) -> private.table.add;
            #);
         #);
         ('control', theClass[], true) -> private.table.add
         (# 
         do ('button', theClass[], true) -> private.table.add
            (# 
            do ('onLabelChanged', true) -> addMethod;
               ('onStyleChanged', true) -> addMethod;
               
               ('pushButton', theClass[], true) -> private.table.add;
               ('iconButton', theClass[], true) -> private.table.add
               (# 
               do ('onShowLabelChanged', true) -> addMethod;
                  ('onIconChanged', true) -> addMethod;
               #);
               ('optionButton', theClass[], true) -> private.table.add
               (# 
               do ('onCurrentItemChanged', true) -> addMethod;
                  ('onPopupMenuChanged', true) -> addMethod;
               #);
               ('staticText', theClass[], true) -> private.table.add;
               ('toggleButton', theClass[], true) -> private.table.add
               (# 
               do ('onStateChanged', true) -> addMethod;
                  ('radioButton', theClass[], true) -> private.table.add;
                  ('checkBox', theClass[], true) -> private.table.add;
               #);
            #);
            ('scrollbar', theClass[], true) -> private.table.add
            (# 
            do ('onThumbMoved', true) -> addMethod;
               ('onPageDown', true) -> addMethod;
               ('onPageUp', true) -> addMethod;
               ('onButtonDown', true) -> addMethod;
               ('onButtonUp', true) -> addMethod;
               ('onPageScrollAmountChanged', true) -> addMethod;
               ('onScrollAmountChanged', true) -> addMethod;
               ('onMaxValueChanged', true) -> addMethod;
               ('onValueChanged', true) -> addMethod;
            #);
            ('editText', theClass[], true) -> private.table.add;
         #);
         ('textField', theClass[], true) -> private.table.add
         (# 
         do ('onTextChanged', true) -> addMethod;
            ('onBeforeChange', true) -> addMethod;
         #);
         ('scrollList', theClass[], true) -> private.table.add
         (# 
         do ('textScrollList', theClass[], true) -> private.table.add;
         #);
         ('rect', theClass[], false) -> private.table.add;
      #);
      ('window', theClass[], true) -> private.table.add
      (# 
      do ('onAboutToClose', true) -> addMethod;
      #);
      ('menu', theClass[], false) -> private.table.add;
      ('menubar', theClass[], false) -> private.table.add;
      ('menuitem', theClass[], false) -> private.table.add
      (# 
       do ('dynamicMenuitem', theClass[], true) -> private.table.add;
      #);
   #);
   
-- ClassInfoPrivate: descriptor --
(# table: @dictionary
     (# element:: class;
        
        add:
          (# theClass: ^element;
             name: ^text;
             addMethod:
               (# name: ^text;
                  event: @boolean;
                  method: ^element.methods.element;
               enter (name[], event)
               do &theClass.methods.element[] -> method[];
                  name[] -> method.name[];
                  event -> method.event;
                  method[] -> theClass.methods.append;
               #);
             superClass: ^element;
             supported: @boolean;
          enter (name[], superClass[], supported)
          do name.makeLC;
             &element[] -> theClass[];
             name[] -> theClass.name[];
             supported -> theClass.supported;
             theClass.methods.init;
             (if superClass[] <> None then
                 superClass.methods.iterate
                 (# 
                 do current.elm[] -> theClass.methods.append;
                 #);
             if);
             inner;
             (theClass.name[], theClass[]) -> insert;
          exit theClass[]
          #);
     #);
#)

