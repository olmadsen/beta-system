ORIGIN '../classinfo';
INCLUDE '../../datastructures/map';
INCLUDE '../../resourcesupport/types';

-- ClassInfoLookup: doPart --
do name[] -> private.table.lookup -> theClass[];
   (* (if theClass[] = none then
    *        'windowitem' -> private.table.lookup -> theClass[];
    *    if);
    *)
   inner;
   
-- ClassInfoInit: doPart --
do private.table.init;
   ('interfaceObject', none, interfaceObjectType) -> private.table.add
   (# 
   do ('onMouseUp', true) -> addMethod;
      ('onMouseDown', true) -> addMethod;
      ('onKeyDown', true) -> addMethod;
      ('onRefresh', true) -> addMethod;
      ('onActivate', true) -> addMethod;
      ('onDeactivate', true) -> addMethod;
      
      ('open', false) -> addMethod;
      ('close', false) -> addMethod;
      
      ('windowitem', theClass[], windowItemType) -> private.table.add 
      (# 
      do ('onVisibleChanged', true) -> addMethod;
         ('onFrameChanged', true) -> addMethod;
         ('onFatherFrameChanged', true) -> addMethod;
         ('onEnableChanged', true) -> addMethod;
         ('onEnableTarget', true) -> addMethod;
         ('onDisableTarget', true) -> addMethod;
         ('onBorderStyleChanged', true) -> addMethod;
         ('onBorderVisibleChanged', true) -> addMethod;
         ('onTheCursorChanged', true) -> addMethod;
         ('onHiliteChanged', true) -> addMethod;
         
         ('separator', theClass[], separatorType) -> private.table.add;
         ('canvas', theClass[], canvasType) -> private.table.add
         (# 
         do ('onChildFrameChanged', true) -> addMethod;
            ('abstractScroller', theClass[], abstractScrollerType) -> private.table.add
            (# 
            do
               ('scroller', theClass[], scrollerType) -> private.table.add;
               ('textEditor', theClass[], textEditorType) -> private.table.add;
            #);
         #);
         ('control', theClass[], controlType) -> private.table.add
         (# 
         do ('button', theClass[], buttonType) -> private.table.add
            (# 
            do ('onLabelChanged', true) -> addMethod;
               ('onStyleChanged', true) -> addMethod;
               
               ('pushButton', theClass[], pushButtonType) -> private.table.add;
               ('iconButton', theClass[], iconButtonType) -> private.table.add
               (# 
               do ('onShowLabelChanged', true) -> addMethod;
                  ('onIconChanged', true) -> addMethod;
               #);
               ('optionButton', theClass[], optionButtonType) -> private.table.add
               (# 
               do ('onCurrentItemChanged', true) -> addMethod;
                  ('onPopupMenuChanged', true) -> addMethod;
               #);
               ('staticText', theClass[], staticTextType) -> private.table.add;
               ('toggleButton', theClass[], toggleButtonType) -> private.table.add
               (# 
               do ('onStateChanged', true) -> addMethod;
                  ('radioButton', theClass[], radioButtonType) -> private.table.add;
                  ('checkBox', theClass[], checkBoxType) -> private.table.add;
               #);
            #);
            ('scrollbar', theClass[], scrollbarType) -> private.table.add
            (# 
            do ('onThumbMoved', true) -> addMethod;
               ('onPageDown', true) -> addMethod;
               ('onPageUp', true) -> addMethod;
               ('onButtonDown', true) -> addMethod;
               ('onButtonUp', true) -> addMethod;
               ('onPageScrollAmountChanged', true) -> addMethod;
               ('onScrollAmountChanged', true) -> addMethod;
               ('onMaxValueChanged', true) -> addMethod;
               ('onValueChanged', true) -> addMethod;
            #);
            ('editText', theClass[], editTextType) -> private.table.add;
         #);
         ('textField', theClass[], textFieldType) -> private.table.add
         (# 
         do ('onTextChanged', true) -> addMethod;
            ('onBeforeChange', true) -> addMethod;
         #);
         ('scrollList', theClass[], scrollListType) -> private.table.add
         (# 
         do ('textScrollList', theClass[], textScrollListType) -> private.table.add;
         #);
      #);
      ('window', theClass[], windowType) -> private.table.add
      (# 
      do ('onAboutToClose', true) -> addMethod;
      #);
      ('menu', theClass[], menuType) -> private.table.add;
      ('menubar', theClass[], menuBarType) -> private.table.add;
      ('menuitem', theClass[], menuItemType) -> private.table.add
      (# 
      do ('dynamicMenuitem', theClass[], dynamicMenuItemType) -> private.table.add;
      #);
   #);
   
-- ClassInfoPrivate: descriptor --
(# table: @textMap
     (# element:: class;
        
        add:
          (# theClass: ^element;
             name: ^text;
             addMethod:
               (# name: ^text;
                  event: @boolean;
                  method: ^element.methods.element;
               enter (name[], event)
               do &theClass.methods.element[] -> method[];
                  name[] -> method.name[];
                  event -> method.event;
                  method[] -> theClass.methods.append;
               #);
             superClass: ^element;
             type: @integer;
          enter (name[], superClass[], type)
          do &element[] -> theClass[];
             name[] -> theClass.name[];
             type -> theClass.type;
             theClass.methods.init;
             (if superClass[] <> None then
                 superClass.methods.iterate
                 (# 
                 do current.elm[] -> theClass.methods.append;
                 #);
             if);
             inner;
             (theClass.name[], theClass[]) -> insert;
          exit theClass[]
          #);
     #);
#)

