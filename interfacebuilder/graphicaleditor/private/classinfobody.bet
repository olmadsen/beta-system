ORIGIN '../classinfo';
INCLUDE '~beta/containers/v1.4/hashTable';

-- ClassInfoLookup: doPart --
do name[] -> private.table.textHash -> private.table.findIndexed
   (# predicate::
        (# 
        do current.name[] -> name.equal -> value;
        #);
   #) -> theClass[];
   inner;
   
-- ClassInfoInit: doPart --
do private.table.init;
   ('interfaceObject', none) -> private.table.add
   (# 
   do ('onMouseUp', true) -> addMethod;
      ('onMouseDown', true) -> addMethod;
      ('onKeyDown', true) -> addMethod;
      ('onRefresh', true) -> addMethod;
      ('onActivate', true) -> addMethod;
      ('onDeactivate', true) -> addMethod;
      
      ('open', false) -> addMethod;
      ('close', false) -> addMethod;
      
      ('windowitem', theClass[]) -> private.table.add 
      (# 
      do ('onVisibleChanged', true) -> addMethod;
         ('onFrameChanged', true) -> addMethod;
         ('onFatherFrameChanged', true) -> addMethod;
         ('onEnableChanged', true) -> addMethod;
         ('onEnableTarget', true) -> addMethod;
         ('onDisableTarget', true) -> addMethod;
         ('onBorderStyleChanged', true) -> addMethod;
         ('onBorderVisibleChanged', true) -> addMethod;
         ('onTheCursorChanged', true) -> addMethod;
         ('onHiliteChanged', true) -> addMethod;
         
         ('separator', theClass[]) -> private.table.add;
         ('canvas', theClass[]) -> private.table.add
         (# 
         do ('onChildFrameChanged', true) -> addMethod;
            ('abstractScroller', theClass[]) -> private.table.add
            (# 
            do
               ('scroller', theClass[]) -> private.table.add;
               ('textEditor', theClass[]) -> private.table.add;
            #);
         #);
         ('control', theClass[]) -> private.table.add
         (# 
         do ('button', theClass[]) -> private.table.add
            (# 
            do ('onLabelChanged', true) -> addMethod;
               ('onStyleChanged', true) -> addMethod;
               
               ('pushButton', theClass[]) -> private.table.add;
               ('iconButton', theClass[]) -> private.table.add
               (# 
               do ('onShowLabelChanged', true) -> addMethod;
                  ('onIconChanged', true) -> addMethod;
               #);
               ('optionButton', theClass[]) -> private.table.add
               (# 
               do ('onCurrentItemChanged', true) -> addMethod;
                  ('onPopupMenuChanged', true) -> addMethod;
               #);
               ('staticText', theClass[]) -> private.table.add;
               ('toggleButton', theClass[]) -> private.table.add
               (# 
               do ('onStateChanged', true) -> addMethod;
                  ('radioButton', theClass[]) -> private.table.add;
                  ('checkBox', theClass[]) -> private.table.add;
               #);
            #);
            ('scrollbar', theClass[]) -> private.table.add
            (# 
            do ('onThumbMoved', true) -> addMethod;
               ('onPageDown', true) -> addMethod;
               ('onPageUp', true) -> addMethod;
               ('onButtonDown', true) -> addMethod;
               ('onButtonUp', true) -> addMethod;
               ('onPageScrollAmountChanged', true) -> addMethod;
               ('onScrollAmountChanged', true) -> addMethod;
               ('onMaxValueChanged', true) -> addMethod;
               ('onValueChanged', true) -> addMethod;
            #);
            ('editText', theClass[]) -> private.table.add;
         #);
         ('textField', theClass[]) -> private.table.add
         (# 
         do ('onTextChanged', true) -> addMethod;
            ('onBeforeChange', true) -> addMethod;
         #);
         ('scrollList', theClass[]) -> private.table.add
         (# 
         do ('textScrollList', theClass[]) -> private.table.add;
         #);
      #);
      ('window', theClass[]) -> private.table.add
      (# 
      do ('onAboutToClose', true) -> addMethod;
      #);
      ('menu', theClass[]) -> private.table.add;
      ('menubar', theClass[]) -> private.table.add;
      ('menuitem', theClass[]) -> private.table.add
      (# 
      do ('dynamicMenuitem', theClass[]) -> private.table.add;
      #);
   #);
   
-- ClassInfoPrivate: descriptor --
(# table: @hashTable
     (# element:: class;
        textHash: integerValue
          (# t: ^text;
          enter t[]
          do t.scanAll
             (# 
             do value + ch -> value;
             #);
          #);
        hashFunction::
          (# 
          do e.name[] -> textHash -> value;
          #);
        add:
          (# theClass: ^element;
             name: ^text;
             addMethod:
               (# name: ^text;
                  event: @boolean;
                  method: ^element.methods.element;
               enter (name[], event)
               do &theClass.methods.element[] -> method[];
                  name[] -> method.name[];
                  event -> method.event;
                  method[] -> theClass.methods.append;
               #);
             superClass: ^element;
          enter (name[], superClass[])
          do &element[] -> theClass[];
             name[] -> theClass.name[];
             theClass.methods.init;
             (if superClass[] <> None then
                 superClass.methods.iterate
                 (# 
                 do current.elm[] -> theClass.methods.append;
                 #);
             if);
             inner;
             theClass[] -> insert;
          exit theClass[]
          #);
     #);
#)

