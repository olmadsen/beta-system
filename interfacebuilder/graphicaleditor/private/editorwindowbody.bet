ORIGIN '../editorwindow';
INCLUDE '~beta/guienv/v1.6/guienvall'
        '../dictionary'
        '../../support/tabcontrol'
        '../../guienvstuff/loadbitmap'
        '../../guienvstuff/killtranslations'
        '~beta/guienv/v1.6/utils/guienvadds'
        '../../resourcesupport/parameters'
        '~beta/guienv/v1.6/controls';
-- editorWindowprivate: Descriptor --
(#
   palette: canvas
     (#
        x,y: @integer;
        paletteItem: iconButton
          (#
             name: (#  enter theName[] do theName[]->label;  #);
             theName: ^text;
             open:: 
               (# 
               do disableDefaultBehaviour; (40,30)->size; false->showLabel; 
               #);
             eventHandler:: 
               (#
                  onMouseDown:: 
                    (# p: @point; 
                    do
                       (localPosition,THIS(paletteItem)[],THIS(palette)[])
                         ->translate->p;
                       (theName[],p,THIS(mouseDown)[])->mousedownInItem;
                       
                    #);
                  
               #);
             
          #);
        addPaletteItem:
          (# icon: ^pixmap; name: ^text; 
          enter (icon[],name[])
          do
               (# theItem: ^paletteItem; 
               do
                  &paletteItem[]->theItem[];
                  THIS(palette)[]->theItem.open;
                  icon[]->theItem.icon;
                  name[]->theItem.name;
                  (x,y)->theItem.position;
                  x+40->x;
                  
               #);
             
          #);
        open::< 
          (# 
          do
             (5,5)->(x,y);
             true->bindRight;
             5->border.style;
             true->border.visible;
             (552,40)->size;
             (39,90)->position;
             
          #)
     #);
   makePalette:
     (# aPalette: ^palette
     do &palette[]->aPalette[]; paletteTabber[]->aPalette.open
     exit aPalette[]
     #);
   itemDictionary: @patternDictionary;
   paletteTabber: @tabControl
     (#
        palettes: @dictionary (# element:: palette #);
        addPaletteItem:
          (# icon: ^pixmap; name,grouping: ^text; 
          enter (grouping[],icon[],name[])
          do
               (# thePalette: ^palette
               do
                  grouping[]->palettes.lookup->thePalette[];
                  (if thePalette[] = none then
                      (grouping[],makePalette->thePalette[])->addPalette
                  if);
                  (icon[],name[])->thePalette.addPaletteItem
               #);
             
          #);
        addPalette:
          (# name: ^text; thePalette: ^palette; 
          enter (name[],thePalette[])
          do
             (thePalette[],name[],none )->add;
             (name[],thePalette[])->palettes.define
          #);
        open::< 
          (# 
          do
             true->bindRight;
             false->border.visible;
             (640,60)->size;
             (0,0)->position;
             palettes.init;
             
          #)
     #)
#)  

-- editorWindowLib: Attributes --
registerStandardItems:
  (# 
  do
     ('windowItem',windowItem##)->registerItem;
     ('canvas',canvas##)->registerItem;
     ('control',control##)->registerItem;
     ('scrollbar',scrollbar##)->registerItem;
     ('button',button##)->registerItem;
     ('pushButton',pushButton##)->registerItem;
     ('staticText',staticText##)->registerItem;
     ('iconButton',iconButton##)->registerItem;
     ('optionButton',optionButton##)->registerItem;
     ('toggleButton',toggleButton##)->registerItem;
     ('radioButton',radioButton##)->registerItem;
     ('checkBox',checkBox##)->registerItem;
     ('editText',editText##)->registerItem;
     ('textField',textField##)->registerItem;
     ('abstractScroller',abstractScroller##)->registerItem;
     ('textEditor',textEditor##)->registerItem;
     ('scroller',scroller##)->registerItem;
     ('scrollList',scrollList##)->registerItem;
     ('textScrollList',textScrollList##)->registerItem
  #)  

-- editorWindowOpen: DoPart --
do
   (4,4)->position;
   (640,500)->size;
   registerStandardItems;
   private.paletteTabber.open;
   INNER  

-- editorWindowRegisterItem: DoPart --
do (name[],item##)->private.itemDictionary.define  

-- editorWindowLookupItem: DoPart --
do name[]->private.itemDictionary.lookup->item##;   

