ORIGIN '../editorwindow';
INCLUDE '~beta/guienv/v1.6/guienvall'
        '../../guienvstuff/loadbitmap'
        '../../guienvstuff/killtranslations'
        '~beta/guienv/v1.6/utils/guienvadds'
        '../../resourcesupport/parameters';
-- editorWindowprivate: Descriptor --
(#
   thePalette: @canvas
     (#
        x,y: @integer;
        paletteItem: iconButton
          (#
             name: (#  enter theName[] do theName[]->label;  #);
             index: @integer;
             theName: ^text;
             open:: 
               (# 
               do disableDefaultBehaviour; (40,30)->size; false->showLabel; 
               #);
             eventHandler:: 
               (#
                  onMouseDown:: 
                    (# p: @point; 
                    do
                       (localPosition,THIS(paletteItem)[],thePalette[])
                         ->translate->p;
                       (theName[],p,THIS(mouseDown)[],index)->mousedownInItem;
                       
                    #);
                  
               #);
             
          #);
        addPaletteItem:
          (# icon: ^pixmap; name: ^text; index: @integer; 
          enter (icon[],name[],index)
          do
               (# theItem: ^paletteItem; 
               do
                  &paletteItem[]->theItem[];
                  thePalette[]->theItem.open;
                  icon[]->theItem.icon;
                  name[]->theItem.name;
                  index->theItem.index;
                  (x,y)->theItem.position;
                  x+40->x;
                  
               #);
             
          #);
        open::< 
          (# 
          do
             (5,5)->(x,y);
             true->bindRight;
             5->border.style;
             true->border.visible;
             (640,40)->size;
             (0,0)->position;
             
          #)
     #);
   itemDictionary: @patternDictionary
#)  

-- editorWindowLib: Attributes --
registerStandardItems:
  (# 
  do
     ('windowItem',windowItem##)->registerItem;
     ('canvas',canvas##)->registerItem;
     ('control',control##)->registerItem;
     ('scrollbar',scrollbar##)->registerItem;
     ('button',button##)->registerItem;
     ('pushButton',pushButton##)->registerItem;
     ('staticText',staticText##)->registerItem;
     ('iconButton',iconButton##)->registerItem;
     ('optionButton',optionButton##)->registerItem;
     ('toggleButton',toggleButton##)->registerItem;
     ('radioButton',radioButton##)->registerItem;
     ('checkBox',checkBox##)->registerItem;
     ('editText',editText##)->registerItem;
     ('textField',textField##)->registerItem;
     ('abstractScroller',abstractScroller##)->registerItem;
     ('textEditor',textEditor##)->registerItem;
     ('scroller',scroller##)->registerItem;
     ('scrollList',scrollList##)->registerItem;
     ('textScrollList',textScrollList##)->registerItem
  #)  

-- editorWindowOpen: DoPart --
do
   (4,4)->position;
   (640,505)->size;
   private.thePalette.open;
   registerStandardItems;
   INNER  

-- editorWindowRegisterItem: DoPart --
do (name[],item##)->private.itemDictionary.define  

-- editorWindowLookupItem: DoPart --
do name[]->private.itemDictionary.lookup->item##;   

