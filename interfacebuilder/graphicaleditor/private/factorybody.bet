ORIGIN '../graphicaleditor';
INCLUDE '../standardeditors'
        '../../guienvstuff/loadbitmap'
        '../../code/generate'
        '../icons'
        '../../code/match';
-- factoryInstantiate: DoPart --
do
     (# inst: ^instantiator; theName: ^text; 
     do
        name.copy->theName[];
        theName.makeLC;
        theName[]->insts.lookup->inst[];
        (if inst[] <> none then
            theGraphicalEditor[]->inst->theWindowItemEditor[]; 
        if);
        
     #);
     

-- factoryLookup: DoPart --
do name[]->insts.lookup->theInstantiator[]  

-- factoryDefined: DoPart --
do
     (# theName: ^text; 
     do
        name.copy->theName[];
        theName.makeLC;
        (theName[]->insts.lookup) <> none ->value;
        
     #)  

-- instantiatorLib: Attributes --
makeScripts:
  (# scripts: ^textList
  do &textList[]->scripts[]; scripts.init; INNER
  exit scripts[]
  #);
makeInterfaceObjectScripts: makeScripts
  (# 
  do
     'onMouseUp'->scripts.append;
     'onMouseDown'->scripts.append;
     'onKeyDown'->scripts.append;
     'onRefresh'->scripts.append;
     'onActivate'->scripts.append;
     'onDeactivate'->scripts.append;
     INNER
  #);
makeWindowScripts: makeInterfaceObjectScripts
  (#  do 'onAboutToClose'->scripts.append; INNER #);
makeWindowItemScripts: makeInterfaceObjectScripts
  (# 
  do
     'onFrameChanged'->scripts.append;
     'onFatherFrameChanged'->scripts.append;
     'onEnableTarget'->scripts.append;
     'onDisableTarget'->scripts.append;
     'onHiliteChanged'->scripts.append;
     'onVisibleChanged'->scripts.append;
     'onEnableChanged'->scripts.append;
     INNER
  #);
makeCanvasScripts: makeWindowItemScripts
  (#  do 'onChildFrameChanged'->scripts.append; INNER #)  

-- factoryInit: DoPart --
do
   insts.init;
   ;
   (none ,'windowitem',&instantiator
      (# editorType:: theGraphicalEditor.windowItemEditor #)[],none ,none ,100,
    20)->insert;
   (('canvas', canvasIcon) -> loadBitmap,'canvas',&instantiator
      (#
         init::  (#  do makeCanvasScripts->scripts[] #);
         editorType:: theGraphicalEditor.canvasEditor;
         
      #)[],none ,'Standard',200,150)->insert;
   (('separatorh', separatorhIcon) -> loadBitmap,'separator',&instantiator
      (# editorType:: theGraphicalEditor.separatorEditor #)[],none ,'Standard',
    100,4)->insert;
   (('separatorv', separatorvIcon) -> loadBitmap,'separator-vertical',&instantiator
      (# editorType:: theGraphicalEditor.verSeparatorEditor #)[],none ,
    'Standard',4,100)->insert;
   (('pushbutton', pushbuttonIcon) -> loadBitmap,'pushbutton',&instantiator
      (# editorType:: theGraphicalEditor.pushButtonEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',60,20)->insert;
   (('radiobutton', radiobuttonIcon) -> loadBitmap,'radiobutton',&instantiator
      (# editorType:: theGraphicalEditor.radioButtonEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',60,16)->insert;
   (('checkbox', checkboxIcon) -> loadBitmap,'checkbox',&instantiator
      (# editorType:: theGraphicalEditor.checkBoxEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',60,16)->insert;
   (('statictext', statictextIcon) -> loadBitmap,'statictext',&instantiator
      (# editorType:: theGraphicalEditor.staticTextEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',60,16)->insert;
    
   (('edittext', edittextIcon) -> loadBitmap,'edittext',&instantiator
      (# editorType:: theGraphicalEditor.editTextEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',200,22)->insert;
    
   (('scrollbarh', scrollbarhIcon) -> loadBitmap,'scrollbar',&instantiator
      (# editorType:: theGraphicalEditor.scrollbarEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',100,16)->insert;
   (('scrollbarv', scrollbarvIcon) -> loadBitmap,'scrollbar-vertical',&instantiator
      (# editorType:: theGraphicalEditor.verScrollbarEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',16,100)->insert;
   
   
   (('iconbutton', iconbuttonIcon) -> loadBitmap,'iconbutton',&instantiator
      (# editorType:: theGraphicalEditor.iconButtonEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',40,40)->insert;
    
   (('optionbutton', optionbuttonIcon) -> loadBitmap,'optionbutton',&instantiator
      (# editorType:: theGraphicalEditor.optionButtonEditor #)[],
    '~beta/guienv/v1.6/controls','Standard',150,40)->insert;
      
    
   (('textfield', textfieldIcon) -> loadBitmap,'textfield',&instantiator
      (# editorType:: theGraphicalEditor.textFieldEditor #)[],
    '~beta/guienv/v1.6/fields','Standard',200,200)->insert;
   
   (('scroller', scrollerIcon) -> loadBitmap,'scroller',&instantiator
      (# editorType:: theGraphicalEditor.scrollerEditor #)[],
    '~beta/guienv/v1.6/fields','Standard',200,150)->insert;
   (('texteditor', texteditorIcon) -> loadBitmap,'texteditor',&instantiator
      (# editorType:: theGraphicalEditor.textEditorEditor #)[],
    '~beta/guienv/v1.6/fields','Standard',200,150)->insert;
  
   (('scrolllist', scrolllistIcon) -> loadBitmap,'textscrolllist',&instantiator
      (# editorType:: theGraphicalEditor.textScrollListEditor #)[],
    '~beta/guienv/v1.6/scrolllists','Standard',150,200)->insert;
   (none ,'window',&instantiator
      (#
         init::  (#  do makeWindowScripts->scripts[] #);
         editorType:: theGraphicalEditor.windowEditor
      #)[],none ,none ,300,200)->insert;     

-- instantiatorCopyName: DoPart --
do
     (# type: ##instantiator
     do
        THIS(instantiator)##->type##;
        &type[]->copy[];
        newName[]->copy.name[];
        none ->copy.path[];
        scripts[]->copy.scripts[];
        icon[]->copy.icon[];
        width->copy.width;
        height->copy.height
     #)  

-- instantiatorScanAttributes: DoPart --
do
     (#
        theAttributes: ^astInterface.beta.attributes;
        partObjectProto: ^astInterface.AST
     do
        5->trace (#  do 'instantiator.scanAttributes'->xT #);
        (if node[] <> none then
            node[]->betaGram.getAttributes->theAttributes[];
            (if theAttributes[] <> none then
                5->trace (#  do 'has attributes'->xT #);
                (makeFragmentForm,
                 '<<nameDcl>>: @ <<prefix>> (# <<attributes>> #)',
                 betaGram.simpleDecl)->parseText->partObjectProto[];
                theAttributes.newScan
                  (# 
                  do
                     5->trace (#  do '.'->xT #);
                     (if (mps[],current[],partObjectProto[])->match then
                         5->trace (#  do 'does match'->xT #);
                         current[]->whenPartObject
                     if)
                  #)
            if)
        if)
     #)  

-- instantiatorInit: DoPart --
do makeWindowItemScripts->scripts[]; INNER  

