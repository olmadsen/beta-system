ORIGIN '../graphicaleditor';
INCLUDE '~beta/guienv/v1.4/utils/dialogfield';
INCLUDE '../../dialogs/mygroup';
INCLUDE '~beta/guienv/v1.4/utils/guienvadds';
INCLUDE '../../code/generate';
INCLUDE '../../code/private/generatebody';
INCLUDE 'graphicaleditorbody';


-- GraphicalEditorCodeDialogTypeOpen: doPart --
do    
   (340, 200) -> Size;   
   private.NameField.Open;
   private.NameFieldLabel.Open;
   private.SuperPatternField.Open;
   private.SuperPatternFieldLabel.Open;
   private.scriptsGroup.open;
   private.updateBtn.open;
   private.closeButton.open;
   Inner Open;

-- GraphicalEditorCodeDialogTypePrivate: descriptor --
(# oldName: ^text;
   UpdateNames:
     (# theAttribute: ^betaGram.attributeDecl;
        newName: ^text;
     do NameField.contents -> newName[];
        (if not (newName[] -> oldName.equalNCS) then  
            editor.node[] -> theAttribute[];
            (# theNames: ^astInterface.beta.names;
               theNameDcl: ^astInterface.beta.nameDcl;
               theNameDecl: ^astInterface.nameDecl;
               oldNameDcl: ^astInterface.beta.nameDcl;
            do theAttribute[] -> betaGram.getNames -> theNames[];
               (if theNames[] <> none then
                   1 -> theNames.get -> oldNameDcl[];
                   (theNames.frag[], newName[]) -> betaGram.newNameDcl -> theNameDcl[];
                   (1, theNameDcl[]) -> theNames.put;
                   (true, theNames.frag[], oldNameDcl[], theNameDcl[])
                     -> theFragServer.notifyAstReplaced;
               if);
            #);
            (if editor.owner[] <> none then
                (if editor.isPrivateToOwner then
                    'private.' -> oldName.prepend;
                    'private.' -> newName.prepend;
                if);
                (oldName[], newName[]) -> editor.owner.updateOpen;
            if);
        if);
     #);
   NameField: @AlphaNumField
     (# Open::<
          (#
          do (36, 134) -> Position;
             (100,27) -> Size;
          #);
     #);

   NameFieldLabel: @StaticText
     (# Open::<
          (#
          do 'Name' -> Label;
             (7, 140) -> Position;
             (27,12) -> Size;
          #);
     #);

   SuperPatternField: @staticText
     (# Open::<
          (#
          do (250, 140) -> Position;
             (100, 12) -> Size;
          #);
     #);
   SuperPatternFieldLabel: @StaticText
     (# Open::<
          (#
          do 'Superpattern: ' -> Label;
             (145, 140) -> Position;
             (90,12) -> Size;
          #);
     #);
   
   scriptsGroup: @group
     (# 
        
        ScriptsOptionButton: @OptionButton
          (# EventHandler::<
               (# onActivate::<
                    (# theMenu: ^Menu;
                    do PopupMenu -> theMenu[];
                       (if theMenu[]//None
                           then
                        else theMenu.Enable;
                       if)
                    #);
                  
               #);
             setMenu:
               (# 
                  theMenu: ^menu;
               enter theMenu[]
               do theMenu[] -> popupMenu;
                  preferredSize -> size;
               #);
             Open::<
               (# 
               do 'Script' -> Label;
                  (80, 22) -> Position;
               #);
             Close::<
               (# theMenu: ^Menu;
               do PopupMenu -> theMenu[];
                  (if theMenu[]//None
                      then
                   else theMenu.Close;
                  if)
               #);
          #);
        editBtn: @pushButton
          (# 
             eventHandler::<
               (# 
                  onMouseUp::<
                    (# theMenu: ^Menu;
                       theItem: ^theMenu.menuItem;
                       t: ^Text;
                    do ScriptsOptionButton.PopupMenu -> theMenu[];
                       (if theMenu[]//None
                           then
                        else 
                           ScriptsOptionButton.CurrentItem -> theMenu.GetMenuItemByNumber -> theItem[];
                           (if theItem[]//None
                               then
                            else
                               theItem.Name -> editor.editVirtual;
                           if);
                       if)
                    #);
               #);
             open::<
               (# 
               do 'Edit' -> label;
                  ((20, 30), (80, 50)) -> frame;
               #);
          #);

        AttributesButton: @PushButton
          (# EventHandler::<
               (# onMouseUp::<
                    (# 
                    do editor.editAttributes;
                    #);
               #);
             Open::<
               (#
               do 'Attributes' -> Label;
                  ((10, 70), (90, 90)) -> frame;
               #);
          #);
        PrivateAttributesBtn: @PushButton
          (# Open::<
               (#
               do 'Private' -> Label;
                  ((120, 70), (200, 90)) -> frame;
               #);
             EventHandler::<
               (# onMouseUp::<
                    (# theAttributes: ^astInterface.beta.attributes;
                    do editor.getPrivateAttributes -> theAttributes[];
                       (if theAttributes[] <> none then
                           (theAttributes[], 'Private attributes') -> openAstEditor;
                       if);
                    #);
               #);
          #);
        open::<
          (# 
          do 'Scripts' -> label;
             ((10, 10), (330, 110)) -> frame;
             ScriptsOptionButton.Open;
             editBtn.open;
             AttributesButton.Open;
             PrivateAttributesBtn.Open;
             
          #);
     #);
   closeButton: @pushButton
     (# eventHandler::<
          (# onMouseUp::<
               (# 
               do this(codeDialogType).close;
                  none -> editor.codeDialog[];
               #);
          #);
        open::<
          (# 
          do ((240, 170), (300, 190)) -> frame;
             'Cancel' -> label;
          #);
     #);
   updateBtn: @pushButton
     (# eventHandler::<
          (# onMouseUp::<
               (# 
               do UpdateNames;
                  theDocument.autoSave;
                  this(codeDialogType).close;
                  none -> editor.codeDialog[];
               #);
          #);
        open::<
          (# 
          do ((160, 170), (220, 190)) -> frame;
             'OK' -> label;
          #);
     #);
#)

-- GraphicalEditorCodeDialogTypeUpdateFields: doPart --
do (# theAttribute: ^betaGram.attributeDecl;
      className: ^text;
      theMenu: ^menu;
      
      addItem:
        (# name: ^text;
           theItem: ^theMenu.menuItem;
        enter name[]
        do &theMenu.menuItem[] -> theItem[];
           theItem.open;
           name[] -> theItem.name;
           theItem[] -> theMenu.append;
        #);
      theTitle: ^text;
   do editor.node[] -> theAttribute[];
      theAttribute[] -> betaGram.getName -> private.oldName[] -> private.nameField.contents;
      theAttribute[] -> betaGram.getPrefix -> className[] -> private.superPatternField.label;
      &menu[] -> theMenu[];
      theMenu.open;
      (if editor.theClassData[] <> none then
          (if editor.theClassData.theClass[] <> none then
              editor.theClassData.theClass.methods.scan
              (# 
              do current.name[] -> addItem;
              #);
          if);
      if);
      (* className[] -> this(graphicalEditorEnv).private.theClassInfo.lookup
       *       (# 
       *       do (if theClass[] <> none then
       *              theClass.methods.scan
       *              (# 
       *              do current.name[] -> addItem;
       *              #);
       *          if);
       *       #);
       *)
      private.oldName.copy -> theTitle[];
      theMenu[] -> private.scriptsGroup.scriptsOptionButton.setMenu;
      ' : code' -> theTitle.append;
      theTitle[] -> title;
      (if editor.getPrivateAttributes = none then
          private.scriptsGroup.privateAttributesBtn.disable;
      if);
   #)
   

   
-- CodeDialogTypeClose: doPart --
do none -> editor.codeDialog[];
