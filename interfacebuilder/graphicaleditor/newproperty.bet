ORIGIN 'newpropertyview';
INCLUDE '../component/component'
        '../code/match'
        '~beta/betaast/betacfl'
        '~beta/betaast/gram';
-- guienvLib: Attributes --
newProperty: newPropertyView
  (#
     node: ^astInterface.beta.attributeDecl;
     onConfirm::< 
       (#
          str: ^text;
          name: ^text;
          upperName: ^text;
          type: ^text;
          prop: ^astInterface.beta.attributeDecl;
          comp: @component;
          gram: @grammar;
          
       do
          nameFld.textContents->name[];
          name.copy->upperName[];
          (1->upperName.inxGet->ascii.upCase,1)->upperName.inxPut;
          &text[]->str[];
          name[]->str.putText;
          ': (# enter (# enter the'->str.putText;
          upperName[]->str.putText;
          ' do update #) exit the'->str.putText;
          upperName[]->str.putText;
          ' #) '->str.putText;
          (node.frag[],str[],gram.attributeDecl)->parseText->prop[];
          node[]->comp.node[];
          prop[]->comp.append;
          &text[]->str[];
          typeFld.textContents->type[];
          'the'->str.putText;
          upperName[]->str.putText;
          ': @'->str.putText;
          type[]->str.putText;
          (node.frag[],str[],gram.attributeDecl)->parseText->prop[];
          prop[]->comp.append;
          INNER ;
          close;
          
       #);
     onCancel:: (#  do close #);
     open::<  (#  enter node[] do INNER ;  #);
     
  #);
  

