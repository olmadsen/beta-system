ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/guienv/v1.6/private/datastructures/sequence';
-- lib: Attributes --
constants:
  (#
     names: @sequence (# element:: text #);
     numToName:
       (# num: @integer; name: ^text
       enter num
       do (if num+1 <= names.size then num+1->names.get->name[] if)
       exit name[]
       #);
     nameToNum:
       (# num: @integer; name: ^text
       enter name[]
       do
          l: names.scan
            (# 
            do (if name[]->current.equalNCS then leave l if); num+1->num
            #)
       exit num
       #);
     add: (#  enter names.append #);
     init:< (#  do names.init; INNER #)
  #);
borderStyleConstants: constants
  (#
     init:: 
       (# 
       do
          'undefines'->add;
          'borderStyles.simple'->add;
          'borderStyles.etchedOut'->add;
          'borderStyles.etchedIn'->add;
          'borderStyles.shadowIn'->add;
          'borderStyles.shadowOut'->add
       #)
  #)  

