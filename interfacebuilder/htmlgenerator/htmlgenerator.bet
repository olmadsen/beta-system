ORIGIN '~beta/basiclib/betaenv';
-- lib: Attributes --
htmlDocument:
  (#
     output: ^stream;
     head:
       (# 
       do '<HEAD>'->output.putLine; INNER ; '</HEAD>'->output.putLine; 
       #);
     body:
       (# 
       do '<BODY>'->output.putLine; INNER ; '</BODY>'->output.putLine; 
       #);
     heading1:
       (#  do '<H1>'->output.putText; INNER ; '</H1>'->output.putLine;  #);
     heading2:
       (#  do '<H2>'->output.putText; INNER ; '</H2>'->output.putLine;  #);
     heading3:
       (#  do '<H3>'->output.putText; INNER ; '</H3>'->output.putLine;  #);
     heading4:
       (#  do '<H4>'->output.putText; INNER ; '</H4>'->output.putLine;  #);
     italic:
       (#  do '<I>'->output.putText; INNER ; '</I>'->output.putText;  #);
     center:
       (# 
       do '<CENTER>'->output.putText; INNER ; '</CENTER>'->output.putText; 
       #);
     bold:
       (#  do '<B>'->output.putText; INNER ; '</B>'->output.putText;  #);
     break: (#  do '<BR>'->output.putLine;  #);
     emit: (# str: ^text;  enter str[] do str[]->output.putText;  #);
     formated:
       (# 
       do '<PRE>'->output.putLine; INNER ; '</PRE>'->output.putLine; 
       #);
     cell:
       (#  do '<TD>'->output.putText; INNER ; '</TD>'->output.putLine;  #);
     row:
       (# 
       do '<TR VALIGN=TOP>'->output.putLine; INNER ; '</TR>'->output.putLine; 
       #);
     table:
       (# width: ^text; 
       enter width[]
       do
          '<TABLE BORDER WIDTH = "'->output.putText;
          width[]->output.putText;
          '">'->output.putLine;
          INNER ;
          '</TABLE>'->output.putLine;
          
       #);
     
  enter output[]
  do '<HTML>'->output.putLine; INNER ; '</HTML>'->output.putLine; 
  #);
  

