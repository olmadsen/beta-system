ORIGIN '~beta/basiclib/v1.6/basicsystemenv';
INCLUDE '~beta/guienv/v1.6/guienvsystemenv'
        'friggapplication'
        'additional/additional'
        'graphicaleditor/private/graphicaleditorprivate'
        '~beta/editor/v5.2/private/compiler'
        'dynenv';
BODY '~beta/editor/v5.2/private/nouser';
-- program: Descriptor --
systemenv
  (#
     dyn: @dynenv;
     frigg: @friggApplication
       (#
          ymerBrowserWindow::
            (#
               clearSemanticErrors:
                 (# 
                 do
                    (if theSemanticErrorViewer[] <> none then
                        (if theSemanticErrorViewer.opened then
                            none ->theSemanticErrorViewer.registerFragmentGroup
                        if)
                    if);
                    ''->writeInfoView;
                    formsWithSemanticErrors.clear
                 #);
               checkSemanticErrors:
                 (# exp: ^astInterface.expanded; node: ^astInterface.ast
                 do
                    (if formsWithSemanticErrors.size > 0 then
                        l: formsWithSemanticErrors.scan
                          (# 
                          do
                             none ->node[];
                             'Semantic Error(s)'->writeInfoView;
                             (if current.root.kind = mps.ast.kinds.interior then
                                 current.root[]->exp[];
                                 loop: exp.suffixWalk
                                   (# 
                                   do
                                      (if current.hasSemanticError then
                                      (*current.semanticError->semanticErrorText->t[];*)
                                          current[]->node[]; leave loop
                                      if)
                                   #);
                                 (if node[] <> none then
                                     node[]->findAndSelect
                                 if)
                             if);
                             current.father->showSemanticErrors;
                             leave l
                          #)
                    if);
                    
                 #)
            #);
          evaluateFile:: 
            (# theWindow: ^window; 
            do
               ymerBrowser.clearSemanticErrors;
               (name[],theOrigin[])->dyn.evaluateFile->theWindow[];
               ymerBrowser.checkSemanticErrors;
               (if theWindow[] <> none then theWindow.open if);
               
            #);
          init::  (#  do ymerBrowser.compilerPrivate.comp[]->dyn.init;  #);
          
       do
          ('groupbox',&theGraphicalEditorEnv.instantiator
             (# editorType:: theGraphicalEditor.groupBoxEditor #)[])
            ->theGraphicalEditorEnv.insertCustom
       #);
     setWindowEnv::  (#  do frigg[]->theWindowEnv[] #);
     
  #)  

