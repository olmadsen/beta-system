ORIGIN '~beta/basiclib/v1.6/basicsystemenv';
INCLUDE '~beta/guienv/v1.6/guienvsystemenv'
        'communication/communication'
        'friggapplication'
        'additional/additional'
        'graphicaleditor/private/graphicaleditorprivate'
        '~beta/editor/v5.2/private/compiler'
        'dynenv';
BODY '~beta/editor/v5.2/private/nouser';
-- program: Descriptor --
systemenv
  (#
     dyn: @dynenv;
     communicator: @communicationEnv
       (#
          openGroup: procedure
            (# 
            do args.scan (#  do current[]->frigg.processCommand #)
            #);
          init::  (#  do ('open',openGroup##)->install #);
          
       #);
     initDynenv: @|THIS(systemEnv).system
       (# 
       do
          frigg.ymerBrowser.compilerPrivate.comp[]
            ->dyn.init (# progress::  (#  do pause #) #);
          'DONE initializing compiler'->frigg.ymerBrowser.writeInfoView;
          ready.V
       #);
     ready: @semaphore;
     frigg: @friggApplication
       (#
          ymerBrowserWindow:: 
            (#
               clearSemanticErrors:
                 (# 
                 do
                    (if theSemanticErrorViewer[] <> none then
                        (if theSemanticErrorViewer.opened then
                            none ->theSemanticErrorViewer.registerFragmentGroup
                        if)
                    if);
                    ''->writeInfoView;
                    formsWithSemanticErrors.clear
                 #);
               checkSemanticErrors:
                 (# exp: ^astInterface.expanded; node: ^astInterface.ast
                 do
                    (if formsWithSemanticErrors.size > 0 then
                        l: formsWithSemanticErrors.scan
                          (# 
                          do
                             none ->node[];
                             'Semantic Error(s)'->writeInfoView;
                             (if current.root.kind = mps.ast.kinds.interior then
                                 current.root[]->exp[];
                                 loop: exp.suffixWalk
                                   (# 
                                   do
                                      (if current.hasSemanticError then
                                      (*current.semanticError->semanticErrorText->t[];*)
                                          current[]->node[]; leave loop
                                      if)
                                   #);
                                 (if node[] <> none then
                                     node[]->findAndSelect
                                 if)
                             if);
                             current.father->showSemanticErrors;
                             leave l
                          #)
                    if);
                    
                 #)
            #);
          evaluateFile:: 
            (# 
            do
               ready.P;
               ymerBrowser.clearSemanticErrors;
               (name[],theOrigin[])->dyn.evaluateFile->P##;
               ymerBrowser.checkSemanticErrors;
               ready.V;
               
            #);
          init::  (#  do initDynenv[]->fork #);
          
       do
          ('groupbox',&theGraphicalEditorEnv.instantiator
             (# editorType:: theGraphicalEditor.groupBoxEditor #)[],
           '~beta/interfacebuilder/v1.6/support/groupbox','Boxes')
            ->theGraphicalEditorEnv.insertCustom;
          false->theGraphicalEditorEnv.switch[11];
          communicator.init;
          
       #);
     setWindowEnv::  (#  do frigg[]->theWindowEnv[] #);
     
  #)  

