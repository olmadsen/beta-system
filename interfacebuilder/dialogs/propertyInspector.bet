ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '../support/row'
        '../support/column'
        '../support/auxcontrols'
        'integerfield'
        '~beta/guienv/v1.6/controls';
-- guienvlib: Attributes --
propertyInspector: window
  (#
     textChanged:<
       (# name: ^text; value: ^text enter (name[],value[]) do INNER #);
     intChanged:<
       (# name: ^text; value: @integer enter (name[],value) do INNER #);
     boolChanged:<
       (# name: ^text; value: @boolean enter (name[],value) do INNER #);
     open::< 
       (# 
       do
          (301,500)->size;
          inspectorPane.open;
          'Property Inspector'->title;
          INNER ;
          (getComputer).compute
       #);
     inspectorPane: @canvas
       (#
          eventhandler:: 
            (#
               onChildFrameChanged:: 
                 (# 
                 do propertyPane.size->size; size->THIS(propertyInspector).size
                 #)
            #);
          open::< 
            (# 
            do
               (0xDDDD,0x5555,0x5555)->backgroundColor;
               (301,500)->size;
               (0,0)->position;
               propertyPane.open;
               
            #);
          propertyPane: @column
            (#
               open::< 
                 (# 
                 do
                    (0xFFFF,0xFFFF,0xFFFF)->backgroundColor;
                    (301,500)->size;
                    alignTop->vertical.alignment;
                    alignLeft->horizontal.alignment;
                    true->vertical.wrap;
                    true->horizontal.wrap;
                    0->margin.left;
                    1->margin.top;
                    0->margin.right;
                    1->margin.bottom;
                    1->distance;
                    ('Position',100)->makeIntProperty;
                    ('Size',200)->makeIntProperty;
                    ('Label','Find...')->makeTextProperty;
                    ('backgroundColor',true)->makeBoolProperty;
                    
                 #);
               makePropertyView:
                 (#
                    name: ^text;
                    view: ^propertyViewDesc;
                    propertyViewDesc:< propertyView;
                    
                 enter name[]
                 do &propertyViewDesc[]->view[]; INNER ; 
                 #);
               makeBoolProperty: makePropertyView
                 (# value: @boolean; propertyViewDesc:: boolPropertyView
                 enter value
                 do (propertyPane[],name[],value)->view.open
                 #);
               makeIntProperty: makePropertyView
                 (# value: @integer; propertyViewDesc:: intPropertyView
                 enter value
                 do (propertyPane[],name[],value)->view.open
                 #);
               makeTextProperty: makePropertyView
                 (# value: ^text; propertyViewDesc:: textPropertyView
                 enter value[]
                 do (propertyPane[],name[],value[])->view.open
                 #);
               propertyView: row
                 (#
                    theName: ^text;
                    name:
                      (# 
                      enter
                        (# 
                        enter theName[]
                        do theName[]->nameView.caption1.label; 
                        #)
                      exit theName[]
                      #);
                    open::< 
                      (# theName: ^text; 
                      enter theName[]
                      do
                         (0xEEEE,0xEEEE,0xEEEE)->backgroundColor;
                         (291,40)->size;
                         alignLeft->horizontal.alignment;
                         alignTop->vertical.alignment;
                         true->horizontal.wrap;
                         true->vertical.wrap;
                         0->margin.left;
                         0->margin.top;
                         0->margin.right;
                         0->margin.bottom;
                         1->distance;
                         nameView.open;
                         valueView.open;
                         theName[]->name;
                         INNER ;
                         
                      #);
                    nameView: @row
                      (#
                         open::< 
                           (# 
                           do
                              alignLeft->horizontal.alignment;
                              alignCenter->vertical.alignment;
                              5->margin.left;
                              5->margin.top;
                              5->margin.right;
                              5->margin.bottom;
                              5->distance;
                              (140,30)->size;
                              caption1.open;
                              
                           #);
                         caption1: @caption
                           (#
                              open::< 
                                (# 
                                do
                                   'name:'->label;
                                   (34,17)->size;
                                   (37,- 3)->position;
                                   
                                #)
                           #)
                      #);
                    valueView: @valueViewDesc;
                    valueViewDesc:< row
                      (#
                         open::< 
                           (# 
                           do
                              alignLeft->horizontal.alignment;
                              alignCenter->vertical.alignment;
                              5->margin.left;
                              5->margin.top;
                              5->margin.right;
                              5->margin.bottom;
                              5->distance;
                              (140,30)->size;
                              INNER ;
                              
                           #)
                      #)
                 #);
               intPropertyView: propertyView
                 (#
                    valueViewDesc:: 
                      (#
                         theEditText: @integerField
                           (#
                              eventHandler:: 
                                (#
                                   onKeyDown:: 
                                     (# 
                                     do
                                        (if ch = 13 then
                                            (name,integerContents)->intChanged
                                        if)
                                     #)
                                #);
                              open:: 
                                (# w,h: @integer; 
                                do fitToContents; size->(w,h); (130,h)->size; 
                                #);
                              
                           #);
                         open::  (#  do theEditText.open;  #);
                         
                      #);
                    open:: 
                      (# value: @integer; t: @text
                      enter value
                      do value->t.putInt; t[]->valueView.theEditText.contents; 
                      #);
                    
                 #);
               textPropertyView: propertyView
                 (#
                    valueViewDesc:: 
                      (#
                         theEditText: @editText
                           (#
                              eventHandler:: 
                                (#
                                   onKeyDown:: 
                                     (# 
                                     do
                                        (if ch = 13 then
                                            (name,contents)->textChanged
                                        if)
                                     #)
                                #);
                              open:: 
                                (# w,h: @integer; 
                                do fitToContents; size->(w,h); (130,h)->size; 
                                #);
                              
                           #);
                         open::  (#  do theEditText.open;  #);
                         
                      #);
                    open:: 
                      (# value: ^text; 
                      enter value[]
                      do value[]->valueView.theEditText.contents; 
                      #);
                    
                 #);
               boolPropertyView: propertyView
                 (#
                    settingValue: @boolean;
                    valueViewDesc:: 
                      (#
                         theCheckBox: @checkBox
                           (#
                              eventHandler:: 
                                (#
                                   onStateChanged:: 
                                     (# 
                                     do
                                        (if not settingValue then
                                            (name,state)->boolChanged
                                        if)
                                     #)
                                #);
                              open::  (#  do ''->label; (16,16)->size;  #);
                              
                           #);
                         open::  (#  do theCheckBox.open;  #);
                         
                      #);
                    open:: 
                      (# value: @boolean; 
                      enter value
                      do
                         true->settingValue;
                         value->valueView.theCheckBox.state;
                         false->settingValue;
                         
                      #);
                    
                 #);
               
            #)
       #)
  #)  

