ORIGIN '~beta/guienv/guienv';
INCLUDE '../support/row'
        '../support/column'
        '../support/auxcontrols'
        '../graphicaleditor/constants'
        'integerfield'
        'textlist'
        '~beta/guienv/controls';
-- guienvlib: Attributes --
propertyInspector: window
  (# resizeable:: (# do false -> value #);
     theConstants: ^constantTable;
     propertyChanged:<
       (# name: ^text; value: ^text enter (name[],value[]) do INNER #);
     nameChanged:< (# name: ^text enter name[] do INNER #);
     editScript:< (# name: ^text enter name[] do INNER #);
     open::< 
       (# 
       enter theConstants[]
       do
          (301,500)->size;
          inspectorPane.open;
          'Object Inspector'->title;
          INNER ;
          (getComputer).compute
       #);
     makePropertyView:
       (#
          name: ^text;
          view: ^propertyViewDesc;
          propertyViewDesc:< propertyView;
          
       enter name[]
       do &propertyViewDesc[]->view[]; INNER ; 
       #);
     makeBoolProperty: makePropertyView
       (# value: @boolean; propertyViewDesc:: boolPropertyView
       enter value
       do (inspectorPane.propertyPane[],name[],value)->view.open
       #);
     makeIntProperty: makePropertyView
       (#
          value: @integer;
          str,const: ^text;
          propertyViewDesc:: textPropertyView;
          consts: ^constants
       enter value
       do
          (if theConstants[] <> none then
              name[]->theConstants.lookup->consts[]
          if);
          (if consts[] <> none then
              (name[],value,consts[])->makeConstantProperty
           else
              &text[]->str[];
              value->str.putint;
              (inspectorPane.propertyPane[],name[],str[])->view.open
          if)
       #);
     makeTextProperty: makePropertyView
       (# value: ^text; propertyViewDesc:: textPropertyView
       enter value[]
       do (inspectorPane.propertyPane[],name[],value[])->view.open
       #);
     makeConstantProperty: makePropertyView
       (#
          value: @integer;
          consts: ^constants;
          propertyViewDesc:: constantPropertyView
       enter (value,consts[])
       do (inspectorPane.propertyPane[],name[],value,consts[])->view.open
       #);
     propertyView: row
       (#
          theName: ^text;
          name:
            (# 
            enter
              (#  enter theName[] do theName[]->nameView.caption1.label;  #)
            exit theName[]
            #);
          open::< 
            (# theName: ^text; 
            enter theName[]
            do
               (0xEEEE,0xEEEE,0xEEEE)->backgroundColor;
               (291,40)->size;
               alignLeft->horizontal.alignment;
               alignTop->vertical.alignment;
               true->horizontal.wrap;
               true->vertical.wrap;
               0->margin.left;
               0->margin.top;
               0->margin.right;
               0->margin.bottom;
               1->distance;
               nameView.open;
               valueView.open;
               theName[]->name;
               INNER ;
               
            #);
          nameView: @row
            (#
               open::< 
                 (# 
                 do
                    alignLeft->horizontal.alignment;
                    alignCenter->vertical.alignment;
                    5->margin.left;
                    5->margin.top;
                    5->margin.right;
                    5->margin.bottom;
                    5->distance;
                    (140,26)->size;
                    caption1.open;
                    
                 #);
               caption1: @caption
                 (#
                    open::< 
                      (# 
                      do 'name:'->label; (34,17)->size;
                      #)
                 #)
            #);
          valueView: @valueViewDesc;
          valueViewDesc:< row
            (#
               open::< 
                 (# 
                 do
                    alignLeft->horizontal.alignment;
                    alignCenter->vertical.alignment;
                    5->margin.left;
                    5->margin.top;
                    5->margin.right;
                    5->margin.bottom;
                    5->distance;
                    (200,26)->size;
                    INNER ;
                    
                 #)
            #)
       #);
     textPropertyView: propertyView
       (#
          value:
            (# str: ^text
            enter str[]
            do str[]->valueView.theEditText.textContents
            #);
          intValue:
            (# str: ^text; int: @integer
            enter int
            do
               &text[]->str[];
               int->str.putInt;
               str[]->valueView.theEditText.textContents
            #);
          valueViewDesc:: 
            (#
               theEditText: @dialogField
                 (#
                    eventHandler:: 
                      (#
                         onReturn:: 
                           (# 
                           do (name,textContents)->propertyChanged; selectAll
                           #)
                      #);
                    open:: 
                      (# w,h: @integer; 
                      do fitToContents; size->(w,h); (190,h)->size; 
                      #);
                    
                 #);
               open::  (#  do theEditText.open;  #);
               
            #);
          open:: 
            (# value: ^text; 
            enter value[]
            do value[]->valueView.theEditText.textContents; 
            #);
          
       #);
     boolPropertyView: propertyView
       (#
          settingValue: @boolean;
          value:
            (# b: @boolean
            enter b
            do
               true->settingValue;
               b->valueView.theCheckBox.state;
               false->settingValue
            #);
          valueViewDesc:: 
            (#
               theCheckBox: @checkBox
                 (#
                    eventHandler:: 
                      (#
                         onStateChanged:: 
                           (#
                              asText:
                                (# b: @boolean; str: ^text
                                enter b
                                do
                                   (if b then
                                       'true'->str[]
                                    else
                                       'false'->str[]
                                   if)
                                exit str[]
                                #)
                           do
                              (if not settingValue then
                                  (name,state->asText)->propertyChanged
                              if)
                           #)
                      #);
                    open::  (#  do ''->label; (16,16)->size;  #);
                    
                 #);
               open::  (#  do theCheckBox.open;  #);
               
            #);
          open::  (# b: @boolean;  enter b do b->value;  #);
          
       #);
     constantPropertyView: propertyView
       (#
          consts: ^constants;
          value:
            (# val: @integer; str: ^text
            enter val
            do
               (if consts[] <> none then
                   val->consts.numToName->str[]
                else
                   'undefined'->str[]
               if);
               str[]->valueView.theCaption.label
            #);
          theMenu: @menu
            (#
               constItem: menuItem
                 (#
                    open::  (# str: ^text enter str[] do str[]->name #);
                    eventHandler:: 
                      (# onSelect::  (#  do name->constChanged #) #)
                 #);
               add:
                 (# str: ^text; item: ^constItem
                 enter str[]
                 do &constItem[]->item[]; str[]->item.open; item[]->append
                 #)
            #);
          constChanged:
            (# str: ^text; val: @integer; asText: ^text
            enter str[]
            do
               str[]->consts.nameToNum->val;
               str[]->valueView.theCaption.label;
               &text[]->asText[];
               val->asText.putInt;
               (name,asText[])->propertyChanged
            #);
          valueViewDesc:: 
            (#
               theCaption: @caption (#  #);
               thePopup: @downArrow
                 (#
                    open:: 
                      (# 
                      do
                         borderStyles.shadowOut->border.style;
                         true->border.visible;
                         (25,17)->size
                      #)
                 #);
               open::  (#  do thePopup.open; theCaption.open;  #);
               
            #);
          open:: 
            (# val: @integer; 
            enter (val,consts[])
            do
               val->value;
               theMenu.open;
               (if consts[] <> none then
                   consts.names.scan
                     (# 
                     do
                        (if not ('undefined'->current.equalNCS) then
                            current[]->theMenu.add
                        if)
                     #)
               if);
               theMenu[]->valueView.thePopup.popupMenu;
               
            #);
          
       #);
     inspectorPane: @column
       (#
          name:
            (# theName: ^text
            enter theName[]
            do
               (if theName[] <> none then
                   namePane.nameView.show;
                   namePane.lab.show;
                   theName[]->namePane.nameView.textContents
                else
                   ''->namePane.nameView.textContents;
                   namePane.nameView.hide;
                   namePane.lab.hide
               if)
            #);
          scripts:
            (# theScripts: ^textList
            enter theScripts[]
            do
               scriptMenu.clear;
               (if theScripts[] <> none then
                   theScripts.scan
                     (#  do current[]->scriptMenu.add #);
                   namePane.eventsLab.show;
                   namePane.pop.show
                else
                   namePane.eventsLab.hide; namePane.pop.hide
               if)
            #);
          scriptMenu: @menu
            (#
               add:
                 (# name: ^text; item: ^scriptItem
                 enter name[]
                 do &scriptItem[]->item[]; name[]->item.open
                 #);
               scriptItem: menuItem
                 (#
                    open:: 
                      (# str: ^text
                      enter str[]
                      do THIS(scriptItem)[]->append; str[]->name
                      #);
                    eventHandler:: 
                      (# onSelect::  (#  do name->editScript #) #)
                 #)
            #);
          eventhandler:: 
            (#
               onFrameChanged:: 
                 (#  
                 do size->THIS(propertyInspector).size 
                 #)
            #);
          open::< 
            (# 
            do
               (0xDDDD,0x5555,0x5555)->backgroundColor;
               true->vertical.theWrap;
               true->horizontal.theWrap;
               0->margin.theLeft;
               0->margin.theRight;
               0->margin.theBottom;
               0->margin.theTop;
               0->distance;
               (301,500)->size;
               (0,0)->position;
               namePane.open;
               propertyPane.open;
               
            #);
          namePane: @row
            (#
               lab: @caption (# open::  (#  do 'Name:'->label #) #);
               nameView: @dialogField
                 (#
                    open::  (#  do (150,22)->size #);
                    eventHandler:: 
                      (#
                         onReturn::< 
                           (#  do textContents->nameChanged; selectAll #)
                      #)
                 #);
               eventsLab: @caption
                 (# open::  (#  do 'Events:'->label #) #);
               pop: @downArrow
                 (#
                    open:: 
                      (# 
                      do
                         (27,13)->size;
                         scriptMenu.open;
                         scriptMenu[]->popupMenu;
                         true->border.visible;
                         borderStyles.shadowOut->border.style
                      #)
                 #);
               open:: 
                 (# 
                 do
                    (341,20)->size;
                    true->vertical.wrap;
                    true->border.visible;
                    borderStyles.shadowOut->border.style;
                    lab.open;
                    nameView.open;
                    eventsLab.open;
                    pop.open
                 #)
            #);
          propertyPane: @column
            (#
               find:
                 (# name: ^text; found: ^propertyView; 
                 enter name[]
                 do
                    search: scan
                      (# propView: ^propertyView
                      do
                         current[]->propView[];
                         (if propView.name->name.equalNCS then
                             propView[]->found[]; leave search
                         if)
                      #)
                 exit found[]
                 #);
               open::< 
                 (# 
                 do
                    (0xFFFF,0xFFFF,0xFFFF)->backgroundColor;
                    (301,500)->size;
                    alignTop->vertical.alignment;
                    alignLeft->horizontal.alignment;
                    true->vertical.wrap;
                    true->horizontal.wrap;
                    0->margin.left;
                    1->margin.top;
                    0->margin.right;
                    1->margin.bottom;
                    1->distance;
                    ('Position',100)->makeIntProperty;
                    ('Size',200)->makeIntProperty;
                    ('Label','Find...')->makeTextProperty;
                    ('backgroundColor',true)->makeBoolProperty;
                    
                 #);
               
            #)
       #)
  #)  

