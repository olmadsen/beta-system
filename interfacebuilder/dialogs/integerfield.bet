ORIGIN '~beta/guienv/v1.6/controls';

-- windowLib: attributes --

integerField: editText
  (# integerContents:
       (# int: @Integer;
       enter (# t: @text;
             enter int
             do int -> t.putInt;
                t[] -> contents;
             #)
       exit (# t: ^text;
            do contents -> t[];
               (if not t.empty then
                   t.reset;
                   l: t.getInt
                     (# syntaxError::<
                          (# 
                          do leave l;
                          #);
                     #) -> int;
               if);
            exit int 
            #)
       #);
  #);

centerOnScreen:
  (# screen: @rectangle;
     width, height: @integer;
     screenWidth, screenHeight: @integer;
     left, top: @integer;
  do size -> (width, height);
     system.screenRectangle -> screen;
     
     screen.size -> (screenWidth, screenHeight);
     
     screen.left + ((screenWidth - width) div 2) -> left;
     screen.top + ((screenHeight - height) div 2) -> top;
     (left, top) -> position;
  #);
