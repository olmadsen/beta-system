ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';
INCLUDE 'center';

-- guienvLib: Attributes --
createWindowDialog: window
  (#
     type:: windowTypes.dialog;
     onCreate:< (# name: ^text;  enter name[] do INNER #);
     onCancel:< (#  do INNER #);
     open::< 
       (# 
       do 'Create Window'->title;
          (353,98)->size;
          center;
          createBtn.open;
          lab.open;
          nameFld.open;
          cancelBtn.open;
          INNER
       #);
     createBtn: @pushbutton
       (#
          open::< 
            (# 
            do
               'Create'->label;
               (60,20)->size;
               (277,65)->position;
               THIS(pushButton)[]->defaultButton;
               
            #);
          eventHandler::< 
            (# onMouseUp::<  (#  do nameFld.contents->onCreate #) #)
       #);
     lab: @statictext
       (#
          open::< 
            (#  do 'Name:'->label; (34,17)->size; (16,17)->position;  #)
       #);
     nameFld: @edittext
       (# open::<  (#  do (285,21)->size; (59,17)->position; nameFld[] -> target;  #) #);
     cancelBtn: @pushbutton
       (#
          eventHandler::<  (# onMouseUp::<  (#  do onCancel #) #);
          open::< 
            (#  do 'Cancel'->label; (60,20)->size; (202,65)->position;  #)
       #)
  #)  

