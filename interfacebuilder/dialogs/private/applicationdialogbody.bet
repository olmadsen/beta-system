ORIGIN '../applicationdialog';
INCLUDE '~beta/guienv/v1.4/controls';
INCLUDE '~beta/guienv/v1.4/utils/guienvadds';

-- applicationDialogLib: attributes --

windowWidth: (# exit 300 #);

-- applicationDialogPrivate: descriptor --
(# dx, dy: @integer;
   
   applNameLbl: @staticText
     (# open::
          (# width, height: @integer;
          do 'Application name:' -> label;
             preferredSize -> (width, height) -> size;
             (dx + 10, dy + 10) -> position;
             10 + width -> dx;
             10 -> dy;
          #);
     #);
   applName: @staticText
     (# open::
          (# name: ^text;
             width, height: @integer;
          enter name[]
          do 
             name[] -> label;
             preferredSize -> (width, height) -> size;
             (dx + 5, dy) -> position;
             10 + height + dy -> dy;
             0 -> dx;
          #);
     #);
   resourceFileLbl: @staticText
     (# open::
          (# width, height: @integer;
          do 'Resource file:' -> label;
             preferredSize -> (width, height) -> size;
             (dx + 10, dy + 10) -> position;
             dx + 10 + width -> dx;
             dy + 10 -> dy;
          #);
     #);
   resourceFileFld: @editText
     (# open::
          (# name: ^text;
          enter name[]
          do (windowWidth - dx - 10 - 10, 25) -> size;
             (dx + 10, dy) -> position;
             0 -> dx;
             dy + 25 + 10 -> dy;
             name[] -> contents;
          #);
     #);
   buildBtn: @pushButton
     (# open::
          (# 
          do 'Build' -> label;
             (60, 20) -> size;
             (windowWidth - 60 - dx - 60 - 10 - 10, dy) -> position;
          #);
        eventHandler::
          (# onMouseUp::
               (# 
               do (resourceFileFld.contents, applName.label) -> onBuild;
               #);
          #);
     #);
   cancelBtn: @pushButton
     (# open::
          (# 
          do 'Cancel' -> label;
             (60, 20) -> size;
             (windowWidth - 60 - 10, dy) -> position;
             dy + 20 + 10 -> dy;
          #);
        eventHandler::
          (# onMouseUp::
               (# 
               do onCancel;
               #);
          #);
     #);
#)

-- applicationDialogRun: doPart --
do (resourceFileName[], applicationName[]) -> open;
   showModal;
   
-- applicationDialogOpen: doPart --
do 'Build application' -> title;
   contents -> private.applNameLbl.open;
   (contents, applicationName[]) -> private.applName.open;
   contents -> private.resourceFileLbl.open;
   (contents, resourceFileName[]) -> private.resourceFileFld.open;
   contents -> private.buildBtn.open;
   contents -> private.cancelBtn.open;
   (windowWidth, private.dy) -> size;
