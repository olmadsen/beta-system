ORIGIN '../spacingdialog';

INCLUDE '~beta/guienv/v1.4/stddialogs';
INCLUDE '~beta/guienv/v1.4/controls';


-- WindowLib: Attributes --
CheckButton: CheckBox
  (# EventHandler::<
       (# onMouseUp::< 
            (# 
            do 
               inner onMouseUp;   
            #);
       #);
     Open::< 
       (#
       do (150,15) -> Size;
          Inner Open;
       #);
  #);

NumberField: EditText
  (#
     EventHandler::<
       (# onMouseUp::< 
            (# 
            do 
               inner onMouseUp;   
            #);
       #);
     Open::< 
       (#
       do (100,25) -> Size;
          Inner Open;
       #);
     number:
       (# n: @integer;
       enter (# t: ^text;
             enter n 
             do &text[] -> t[];
                n -> t.putInt;
                t[] -> contents;
             #)
       exit (# t: ^text;
            do contents -> t[];
               t.reset;
               get: t.getInt
               (# 
                  syntaxError::
                    (# 
                    do leave get;
                    #);
               #) -> n;
            exit n
            #)
       #);
  #)


--SpacingDialogPrivate: Descriptor--

(# accept:
     (# 
     do VdisBtn.state -> values.doVerticalDist;
        HdisBtn.state -> values.doHorizontalDist;
        WidthBtn.state -> values.doWidth;
        HeightBtn.state -> values.doHeight;
        VdisField.number -> values.verticalDist;
        HdisField.number -> values.horizontalDist;
        WidthField.number -> values.width;
        HeightField.number -> values.height;
     #);
   open: (#
         do 'Spacing'->title;
            (300,190)->size;
            VdisBtn.open;
            (10,10) -> VdisBtn.Position;
            HdisBtn.open;
            (10,40) -> HdisBtn.Position;
            WidthBtn.open;
            (10,70) -> WidthBtn.Position;
            HeightBtn.open;
            (10,100) -> HeightBtn.Position;
            VdisField.open;
            (170,10) -> VdisField.Position;
            HdisField.open;
            (170,40) -> HdisField.Position;
            WidthField.open;
            (170,70) -> WidthField.Position;
            HeightField.open;
            (170,100) -> HeightField.Position;
            theseparator.open;
            (0,150)->theseparator.position;
            OkBtn.open;
            (210,160) -> OkBtn.Position;
            CancelBtn.open;
            (130,160) -> CancelBtn.Position;
         #);
   
   VdisBtn: @Checkbutton
     (# 
        open::<
          (#
          do 'Vertical distance:'->label;
             values.doVerticalDist -> state;
          #);   
     #);
   
   HdisBtn: @Checkbutton
     (#
        open::<
          (#
          do 'Horizontal distance:'->label;
             (162,25) -> Size;
             values.doHorizontalDist -> state;
          #);
     #);
   
   WidthBtn: @Checkbutton
     (#
        open::<
          (#
          do 'Width:'->label; 
             (80,25) -> Size;
             values.doWidth -> state;
          #);
        
     #);
   
   HeightBtn: @Checkbutton
     (#
        open::<
          (#
          do 'Height:'->label;
             (80,25) -> Size;
             values.doHeight -> state;
          #);
     #);
   
   VdisField: @NumberField
     (# 
        open::<
          (#
          do true -> bindRight;
             values.verticalDist -> number;
          #);   
     #);
   
   HdisField: @NumberField
     (#
        open::<
          (#
          do true -> bindRight;
             values.horizontalDist -> number;
          #);
     #);
   
   WidthField: @NumberField
     (#
        open::<
          (#
          do true -> bindRight;
             values.width -> number;
          #);
        
     #);
   
   HeightField: @NumberField
     (#
        open::<
          (#
          do true -> bindRight;
             values.height -> number;
          #);
     #);

   
   theSeparator: @separator
     (#
        open::<
          (# do (300,0)->size;
             true -> bindRight;
             true -> bindBottom;
             false -> bindTop;
          #);
     #);
   
   OkBtn: @PushButton
     (# EventHandler::<
          (# onMouseUp::< 
               (# 
               do this(SpacingDialog).Accept;
               #);
          #);
        Open::<
          (#
          do  'OK' -> Label;
             (60,20) -> Size;
             OkBtn[] -> DefaultButton;
             True -> BindBottom;
             False -> BindTop;
             true -> bindRight;
             false -> bindLeft;
          #);
     #);

   CancelBtn: @PushButton
     (# EventHandler::<
          (# onMouseUp::< 
               (# 
               do this(SpacingDialog).Cancel;
               #);
          #);
        Open::<
          (#
          do 'Cancel' -> Label;
             (187,294) -> Position;
             (60,20) -> Size;
             True -> BindBottom;
             False -> BindTop;
             true -> bindRight;
             false -> bindLeft;
          #);
     #);
#)

--SpacingDialogOpen: doPart--
do (if values[] = None then
       &spacingValues[] -> values[];
   if);
   Private.open;
   inner;
   
--SpacingDialogCancel: doPart--

do this(SpacingDialog).Close;
   INNER;
   
--SpacingDialogAccept: doPart--
   
do private.accept;
   INNER;
   


