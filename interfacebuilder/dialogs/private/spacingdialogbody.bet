ORIGIN '../spacingdialog';

INCLUDE '~beta/guienv/v1.4/stddialogs';
INCLUDE '~beta/guienv/v1.4/controls';
INCLUDE '../integerfield';


-- WindowLib: Attributes --
CheckButton: CheckBox
  (# EventHandler::<
       (# onMouseUp::< 
            (# 
            do 
               inner onMouseUp;   
            #);
       #);
     Open::< 
       (#
       do (150,15) -> Size;
          Inner Open;
       #);
  #);

numberField: integerField
  (# open::<
       (# 
       do (100,25) -> Size;
          INNER;
       #);
  #);
--SpacingDialogPrivate: Descriptor--

(# accept:
     (# 
     do VdisBtn.state -> values.doVerticalDist;
        HdisBtn.state -> values.doHorizontalDist;
        WidthBtn.state -> values.doWidth;
        HeightBtn.state -> values.doHeight;
        VdisField.integerContents -> values.verticalDist;
        HdisField.integerContents -> values.horizontalDist;
        WidthField.integerContents -> values.width;
        HeightField.integerContents -> values.height;
     #);
   open: (#
         do 'Spacing'->title;
            (300,190)->size;
            VdisBtn.open;
            (10,10) -> VdisBtn.Position;
            HdisBtn.open;
            (10,40) -> HdisBtn.Position;
            WidthBtn.open;
            (10,70) -> WidthBtn.Position;
            HeightBtn.open;
            (10,100) -> HeightBtn.Position;
            VdisField.open;
            (170,10) -> VdisField.Position;
            HdisField.open;
            (170,40) -> HdisField.Position;
            WidthField.open;
            (170,70) -> WidthField.Position;
            HeightField.open;
            (170,100) -> HeightField.Position;
            theseparator.open;
            (0,150)->theseparator.position;
            OkBtn.open;
            (210,160) -> OkBtn.Position;
            CancelBtn.open;
            (130,160) -> CancelBtn.Position;
         #);
   
   VdisBtn: @Checkbutton
     (# 
        open::<
          (#
          do 'Vertical distance:'->label;
             values.doVerticalDist -> state;
          #);   
     #);
   
   HdisBtn: @Checkbutton
     (#
        open::<
          (#
          do 'Horizontal distance:'->label;
             (162,25) -> Size;
             values.doHorizontalDist -> state;
          #);
     #);
   
   WidthBtn: @Checkbutton
     (#
        open::<
          (#
          do 'Width:'->label; 
             (80,25) -> Size;
             values.doWidth -> state;
          #);
        
     #);
   
   HeightBtn: @Checkbutton
     (#
        open::<
          (#
          do 'Height:'->label;
             (80,25) -> Size;
             values.doHeight -> state;
          #);
     #);
   
   VdisField: @numberField
     (# 
        open::<
          (#
          do true -> bindRight;
             values.verticalDist -> integerContents;
          #);   
     #);
   
   HdisField: @numberField
     (#
        open::<
          (#
          do true -> bindRight;
             values.horizontalDist -> integerContents;
          #);
     #);
   
   WidthField: @numberField
     (#
        open::<
          (#
          do true -> bindRight;
             values.width -> integerContents;
          #);
        
     #);
   
   HeightField: @numberField
     (#
        open::<
          (#
          do true -> bindRight;
             values.height -> integerContents;
          #);
     #);

   
   theSeparator: @separator
     (#
        open::<
          (# do (300,0)->size;
             true -> bindRight;
             true -> bindBottom;
             false -> bindTop;
          #);
     #);
   
   OkBtn: @PushButton
     (# EventHandler::<
          (# onMouseUp::< 
               (# 
               do this(SpacingDialog).Accept;
               #);
          #);
        Open::<
          (#
          do  'OK' -> Label;
             (60,20) -> Size;
             OkBtn[] -> DefaultButton;
             True -> BindBottom;
             False -> BindTop;
             true -> bindRight;
             false -> bindLeft;
          #);
     #);

   CancelBtn: @PushButton
     (# EventHandler::<
          (# onMouseUp::< 
               (# 
               do this(SpacingDialog).Cancel;
               #);
          #);
        Open::<
          (#
          do 'Cancel' -> Label;
             (187,294) -> Position;
             (60,20) -> Size;
             True -> BindBottom;
             False -> BindTop;
             true -> bindRight;
             false -> bindLeft;
          #);
     #);
#)

--SpacingDialogOpen: doPart--
do (if values[] = None then
       &spacingValues[] -> values[];
   if);
   Private.open;
   private.okbtn[] -> defaultButton;
   inner;
   
--SpacingDialogCancel: doPart--

do this(SpacingDialog).Close;
   INNER;
   
--SpacingDialogAccept: doPart--
   
do private.accept;
   INNER;
   


