ORIGIN '../promptfortextdialog';
INCLUDE '~beta/guienv/v1.2/controls';

INCLUDE '~beta/guienv/v1.2/fields';

-- PromptForTextAccept: doPart --
do (# st: ^styledText;
   do private.theTextFld.contents -> st[];
      st.copy -> values.theText[];
   #);
   inner;

-- PromptForTextDismiss: doPart --
do inner;

-- PromptForTextOpen: doPart --
do (if values[] = None then
       &promptForTextvalues[] -> values[];
   if);
   (if values.theText[] = None then
       '' -> values.theText[];
   if);
   (if values.prompt[] = None then
       'Enter text:' -> values.prompt[];
   if);
   (if values.acceptLabel[] = None then
       'OK' -> values.acceptLabel[];
   if);
   (if values.dismissLabel[] = None then
       'Cancel' -> values.dismissLabel[];
   if);
   
   (200, 82) -> size;
   this(promptForText)[] -> private.thePrompt.open;
   this(promptForText)[] -> private.theTextFld.open;
   this(promptForText)[] -> private.okBtn.open;
   this(promptForText)[] -> private.cancelBtn.open;
   inner;

-- PromptForTextPrivate: descriptor --
(#
   thePrompt: @staticText
     (# open::
          (# 
          do values.prompt[] -> label;
             ((4, 4), (196, 20)) -> frame;
             true -> bindRight;
          #);
     #);
   theTextFld: @textField
     (# open::
          (# st: ^styledText;
          do &styledText[] -> st[];
             values.theText -> st;
             st[] -> contents;
             ((8, 24), (196, 52)) -> frame;
             true -> bindRight -> bindBottom;
          #);
     #);
   okBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (# 
               do accept;
               #);
          #);
        open::
          (# 
          do values.acceptLabel[] -> label;
             ((136, 60), (196, 78)) -> frame;
             true -> bindRight;
             false -> bindLeft;
             true -> bindBottom;
             false -> bindTop;
          #);
     #);
   
   cancelBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (# 
               do dismiss;
               #);
          #);
        open::
          (# 
          do values.dismissLabel[] -> label;
             ((72, 60), (132, 78)) -> frame;
             true -> bindRight;
             false -> bindLeft;
             true -> bindBottom;
             false -> bindTop;
          #);
     #);
#)

