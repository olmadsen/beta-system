ORIGIN '../promptfortextdialog';
INCLUDE '~beta/guienv/v1.6/controls';

INCLUDE '~beta/guienv/v1.6/fields';

-- promptForTextViewLib: attributes --

theText:
  (# st: ^styledText;
     str: ^text;
   do private.theTextFld.contents -> st[];
     st.copy -> str[];
  exit str[]
  #);

-- promptForTextViewAccept: doPart --
do theText -> values.theText[];
   inner;

-- promptForTextViewDismiss: doPart --
do none -> values.theText[];
   inner;

-- promptForTextViewOpen: doPart --
do (if values[] = None then
       &promptForTextvalues[] -> values[];
   if);
   (if values.theText[] = None then
       '' -> values.theText[];
   if);
   (if values.prompt[] = None then
       'Enter text:' -> values.prompt[];
   if);
   (if values.acceptLabel[] = None then
       'OK' -> values.acceptLabel[];
   if);
   (if values.dismissLabel[] = None then
       'Cancel' -> values.dismissLabel[];
   if);
   
   (200, 82) -> size;
   this(promptForTextView)[] -> private.thePrompt.open;
   this(promptForTextView)[] -> private.theTextFld.open;
   this(promptForTextView)[] -> private.okBtn.open;
   this(promptForTextView)[] -> private.cancelBtn.open;
   inner;

-- promptForTextViewPrivate: descriptor --
(#
   thePrompt: @staticText
     (# open::
          (# 
          do values.prompt[] -> label;
             ((4, 4), (196, 20)) -> frame;
             true -> bindRight;
          #);
     #);
   theTextFld: @textField
     (# open::
          (# st: ^styledText;
          do &styledText[] -> st[];
             values.theText -> st;
             st[] -> contents;
             ((8, 24), (196, 52)) -> frame;
             true -> bindRight -> bindBottom;
          #);
     #);
   okBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (# 
               do accept;
               #);
          #);
        open::
          (# 
          do values.acceptLabel[] -> label;
             ((136, 60), (196, 78)) -> frame;
             true -> bindRight;
             false -> bindLeft;
             true -> bindBottom;
             false -> bindTop;
          #);
     #);
   
   cancelBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (# 
               do dismiss;
               #);
          #);
        open::
          (# 
          do values.dismissLabel[] -> label;
             ((72, 60), (132, 78)) -> frame;
             true -> bindRight;
             false -> bindLeft;
             true -> bindBottom;
             false -> bindTop;
          #);
     #);
#)

-- GUIenvPromptForText: doPart --
do (# theDialog: @window
        (# interior: @promptForTextView
             (# open::
                  (# 
                  do interior.size -> theDialog.size;
                     true -> bindRight -> bindBottom;
                  #);
                accept::
                  (# 
                  do (if verify then
                         this(dopromptForText).accept;
                         theDialog.close;
                     if);
                  #);
                dismiss::
                  (# 
                  do this(dopromptForText).dismiss;
                     theDialog.close;
                  #);
             #);
           open::
             (# 
             do hide;
                '' -> title;
                (contents, values[]) -> interior.open;
             #);
           eventHandler::
             (# onAboutToClose::
                  (# 
                  do interior.theText -> values.theText[];
                     verify -> okToClose;
                  #);
             #);
        #);
   do theDialog.open;
      theDialog.showModal;
   #)
