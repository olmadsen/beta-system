ORIGIN '../generate';
INCLUDE '~beta/betaast/v5.1/betasematt';
INCLUDE '~beta/betaast/v5.1/gram';
INCLUDE '../../mpsstuff/mpsutils';

-- makeOptional: doPart --
do (cat, frag[]) -> newOptional -> theOptional[];
   cat -> theOptional.nonTerminalSymbol;
   
-- MPSDoGenerate: doPart --
do inner;
   (if theAst[] = None then
       failure;
   if);
   
-- MPSnewObjectDescriptor: doPart --
do (# theObjectDescriptor: ^objectDescriptor;
      theMainPart: ^mainPart;
      theAttributes: ^attributes;
      theActionPart: ^actionPart;
      thePrefix: ^prefix;
      theEnterpart, theOpt: ^optional;
      gram: @grammar;
   do
      (objectDescriptor,frag[]) -> newAst -> theObjectDescriptor[];
      (mainPart,frag[]) -> newAst 
        -> theMainPart[] -> theObjectDescriptor.putMainPart;
      (if prefixName[] = None then
          (gram.prefixOpt,frag[]) -> makeOptional -> theObjectDescriptor.putPrefixOpt;
       else
          (gram.prefix,frag[]) -> newAst -> thePrefix[] -> theObjectDescriptor.putPrefixOpt;
          (frag[],prefixName[]) -> newNameApl -> thePrefix.putAttributeDenotation;
      if);
      (attributes,frag[]) -> newAst
        -> theAttributes[] -> theMainPart.putAttributes;
      (actionPart,frag[]) -> newAst
        -> theActionPart[] -> theMainPart.putActionPart;
      (1,(gram.attributeDeclOpt,frag[]) -> makeOptional) -> theAttributes.insert;
      (gram.enterPartOpt,frag[]) -> makeOptional -> theActionPart.putEnterPartOpt;
      (gram.doPartOpt,frag[]) -> makeOptional -> theActionPart.putDoPartOpt;
      (gram.exitPartOpt,frag[]) -> makeOptional -> theActionPart.putExitPartOpt;
      theObjectDescriptor[] -> theAst[];
   #);

-- MPSnewDescriptorForm: doPart --
do (# theDescriptorForm: ^descriptorForm;
   do (descriptorForm,frag[]) -> newAst -> theDescriptorForm[];
      (frag[],prefixName[]) -> newObjectDescriptor -> theDescriptorForm.putObjectDescriptor;
      theDescriptorForm[] -> theAst[];
   #);

-- MPSSetOriginProperty: doPart --
do 'ORIGIN' -> prop.addProp
   (# 
   do value.copy -> addString; 
   #)

-- MPSGetOriginProperty: doPart --
do 'ORIGIN' -> prop.getProp
   (# 
      doProp::<
        (# 
        do scanParameters
           (# 
              doString::<
                (# 
                do s.copy -> value[];
                #);
           #);
        #)
   #)

-- MPSnewStaticAttribute: doPart --
do (# theSimpleDecl: ^simpleDecl;
      theStaticItem: ^staticItem;
   do (simpleDecl,frag[]) -> newAst -> theSimpleDecl[] -> theAst[];
      (frag[],name[]) -> newNames -> theSimpleDecl.putNames;
      (staticItem,frag[]) -> newAst -> theStaticItem[] -> theSimpleDecl.putReferenceSpecification;
      (frag[],prefixName[]) -> newNameApl -> theStaticItem.putObjectSpecification;
      
   #);
-- MPSnewVirtualBinding: doPart --
do (# theBindingDecl: ^bindingDecl;
   do (bindingDecl,frag[]) -> newAst -> theBindingDecl[] -> theAst[];
      (frag[],name[]) -> newNames -> theBindingDecl.putNames;
      (frag[],prefixName[]) -> newObjectDescriptor -> theBindingDecl.putObjectSpecification;
   #);
   
-- MPSnewVirtualDefinition: doPart --
do (# theVirtualDecl: ^virtualDecl;
   do (virtualDecl,frag[]) -> newAst -> theVirtualDecl[] -> theAst[];
      (frag[],name[]) -> newNames -> theVirtualDecl.putNames;
      (frag[],prefixName[]) -> newObjectDescriptor -> theVirtualDecl.putObjectSpecification;
   #);

-- MPSnewPatternAttribute: doPart --
do (# thePatternDecl: ^patternDecl;
   do (patternDecl,frag[]) -> newAst -> thePatternDecl[] -> theAst[];
      (frag[],name[]) -> newNames -> thePatternDecl.putNames;
      (frag[],prefixName[]) -> newObjectDescriptor -> thePatternDecl.putObjectDescriptor;
   #);

-- MPSsetObjectDescriptor: doPart --
do (#
   do (if theAttributeDecl.symbol
       //patternDecl then
          (# thePatternDecl: ^patternDecl;
          do theAttributeDecl[] -> thePatternDecl[];
             theObjectDescriptor[] -> thePatternDecl.putObjectDescriptor;
          #);
       //simpleDecl then
          (# theSimpleDecl: ^simpleDecl;
             theReferenceSpecification: ^referenceSpecification;
          do theAttributeDecl[] -> theSimpleDecl[];
             theSimpleDecl.getReferenceSpecification -> theReferenceSpecification[];
             (if theReferenceSpecification.symbol
              //staticItem then
                 (# theStaticItem: ^staticItem;
                 do theReferenceSpecification[] ->  theStaticItem[];
                    theObjectDescriptor[] -> theStaticItem.putObjectSpecification;
                 #);
              else
                 failure;
             if);
          #);
       //virtualDecl then
          (# theVirtualDecl: ^virtualDecl;
          do theAttributeDecl[] -> theVirtualDecl[];
             theObjectDescriptor[] -> theVirtualDecl.putObjectSpecification;
          #);
       //bindingDecl then
          (# theBindingDecl: ^bindingDecl;
          do theAttributeDecl[] -> theBindingDecl[];
             theObjectDescriptor[] -> theBindingDecl.putObjectSpecification;
          #);
       //finalDecl then
          (# theFinalDecl: ^finalDecl;
          do theAttributeDecl[] -> theFinalDecl[];
             theObjectDescriptor[] -> theFinalDecl.putObjectSpecification;
          #);
       else
          failure;
      if);
   #);

-- MPSgetObjectDescriptor: doPart --
do (#
   do (if theAttributeDecl.symbol
       //patternDecl then
          (# thePatternDecl: ^patternDecl;
          do theAttributeDecl[] -> thePatternDecl[];
             thePatternDecl.getObjectDescriptor -> check -> theObjectDescriptor[];
          #);
       //simpleDecl then
          (# theSimpleDecl: ^simpleDecl;
             theReferenceSpecification: ^AST;
          do theAttributeDecl[] -> theSimpleDecl[];
             theSimpleDecl.getReferenceSpecification -> theReferenceSpecification[];
             (if theReferenceSpecification.symbol
              //staticItem then
                 (# theStaticItem: ^staticItem;
                    theObjectSpecification: ^AST;
                 do theReferenceSpecification[] ->  theStaticItem[];
                    theStaticItem.getObjectSpecification -> theObjectSpecification[];
                    (if theObjectSpecification.symbol
                     //objectDescriptor then
                        theObjectSpecification[] -> theObjectDescriptor[];
                     else
                        none -> theObjectDescriptor[];
                    if);
                 #);
              else
                 failure;
             if);
          #);
       //virtualDecl then
          (# theVirtualDecl: ^virtualDecl;
             theObjectSpecification: ^AST;
          do theAttributeDecl[] -> theVirtualDecl[];
             theVirtualDecl.getObjectSpecification -> theObjectSpecification[];
             (if theObjectSpecification.symbol
              //objectDescriptor then
                 theObjectSpecification[] -> theObjectDescriptor[];
              else
                 failure;
             if);
          #);
       //bindingDecl then
          (# theBindingDecl: ^bindingDecl;
             theObjectSpecification: ^AST;
          do theAttributeDecl[] -> theBindingDecl[];
             theBindingDecl.getObjectSpecification -> theObjectSpecification[];
             (if theObjectSpecification.symbol
              //objectDescriptor then
                 theObjectSpecification[] -> theObjectDescriptor[];
              else
                 failure;
             if);
          #);
       //finalDecl then
          (# theFinalDecl: ^finalDecl;
             theObjectSpecification: ^AST;
          do theAttributeDecl[] -> theFinalDecl[];
             theFinalDecl.getObjectSpecification -> theObjectSpecification[];
             (if theObjectSpecification.symbol
              //objectDescriptor then
                 theObjectSpecification[] -> theObjectDescriptor[];
              else
                 failure;
             if);
          #);
       else
          failure;
      if);
   #);

-- MPSAttributeSetName: doPart --
do (# theNames: ^names;
      theNameDcl: ^nameDcl;
      theNameDecl: ^nameDecl;
   do theAttributeDecl[] -> getNames -> theNames[];
      (if theNames[] <> none then
          (theNames.frag[], name[]) -> newNameDcl -> theNameDcl[];
          (1, theNameDcl[]) -> theNames.put;
          (* 1 -> theNames.get -> theNameDcl[];
           *           theNameDcl.getNameDecl -> theNameDecl[];
           *           name[] -> theNameDecl.putText;
           *)
          (* theNames.newScan
           *           (# 
           *              theNameDecl: ^lexemText;
           *           do current.getNameDecl -> theNameDecl[];
           *              name.copy -> theNameDecl.putText;
           *           #);
           *)
       else
          failure;
      if);
   #);

-- MPSAttributeGetName: doPart --
do (# theNames: ^names;
   do theAttributeDecl[] -> getNames -> theNames[];;
      (if theNames[] <> none then
          theNames.newScan
          (# 
             theNameDecl: ^nameDecl;
          do current.getNameDecl -> theNameDecl[];
             theNameDecl.getText -> name[];
          #);
       else
          failure;
      if);
   #)

-- MPSAttributeSetPrefix: doPart --
do (# theObjectDescriptor: ^objectDescriptor;
      gram: @grammar;
   do theAttributeDecl[] -> getObjectDescriptor -> theObjectDescriptor[];
      (if theObjectDescriptor[] <> none then
          (if prefixName[] <> none then
              (# thePrefix: ^prefix;
              do (prefix,theObjectDescriptor.frag[]) -> newAst -> thePrefix[] -> theObjectDescriptor.putPrefixOpt;
                 (theObjectDescriptor.frag[],prefixName[]) -> newNameApl -> thePrefix.putAttributeDenotation;
              #);
           else
              (gram.prefixOpt,theObjectDescriptor.frag[]) -> makeOptional -> theObjectDescriptor.putPrefixOpt;
          if);
       else
          failure;
      if);
   #);

-- MPSAttributeGetPrefix: doPart --
do (# theObjectDescriptor: ^objectDescriptor;
      theAst: ^AST;
   do theAttributeDecl[] -> getObjectDescriptor -> theObjectDescriptor[];
      (if theObjectDescriptor[] <> none then
          theObjectDescriptor.getPrefixOpt -> theAst[];
          (if theAst.kind = kinds.optional then
              none -> prefixName[];
           else
              
              (# thePrefix: ^prefix;
                 theAttributeDenotation: ^attributeDenotation;
              do theAst[] -> thePrefix[];
                 thePrefix.getAttributeDenotation -> theAttributeDenotation[];
                 (if theAttributeDenotation.symbol
                  //nameApl then
                     (# theNameApl: ^nameApl;
                     do theAttributeDenotation[] -> theNameApl[];
                        theNameApl.getText -> prefixName[];
                        (if prefixName.empty then
                            NONE -> prefixName[];
                        if);
                     #);
                  else
                     failure;
                 if);
              #);
          if);
       else
          failure;
      if);
   #)
-- MPSattributeGetPrefixNameApl: doPart --
do (# theObjectDescriptor: ^objectDescriptor;
      theAst: ^AST;
   do theAttributeDecl[] -> getObjectDescriptor -> theObjectDescriptor[];
      (if theObjectDescriptor[] <> none then
          theObjectDescriptor.getPrefixOpt -> theAst[];
          (if theAst.kind = kinds.optional then
           else
              
              (# thePrefix: ^prefix;
                 theAttributeDenotation: ^attributeDenotation;
              do theAst[] -> thePrefix[];
                 thePrefix.getAttributeDenotation -> theAttributeDenotation[];
                 (if theAttributeDenotation.symbol
                  //nameApl then
                     theAttributeDenotation[] -> prefixNameApl[];
                  else
                     failure;
                 if);
              #);
          if);
       else
          failure;
      if);
   #)
   
-- MPSAttributeFindAttribute: descriptor --
(# theObjectDescriptor: ^objectDescriptor;
      theAttributes: ^attributes;
      theMainPart: ^mainPart;
   do theAttributeDecl[] -> getObjectDescriptor 
      (# failure::< 
           (# do this(findAttribute).failure; #);
      #) -> theObjectDescriptor[];
      (if theObjectDescriptor[] <> None then
          theObjectDescriptor.getMainPart -> theMainPart[];
          theMainPart.getAttributes -> theAttributes[];
          (theAttributes[],name[]) -> find -> foundAttribute[];
       else
          failure;
      if);
   #)

-- MPSsetDopart: doPart --
do (# theActionPart: ^actionPart;
      theMainPart: ^mainPart;
   do theObjectDescriptor.getMainPart -> theMainPart[];
      theMainPart.getActionPart -> theActionPart[];
      theDoPart[] -> theActionPart.putDoPartOpt;
   #);

-- MPSgetDopart: doPart --
do (# theActionPart: ^actionPart;
      theMainPart: ^mainPart;
      theAst: ^AST;
   do theObjectDescriptor.getMainPart -> theMainPart[];
      theMainPart.getActionPart -> theActionPart[];
      theActionPart.getDoPartOpt -> theAst[];
      (if theAst.symbol//doPart then
          theAst[] -> theDoPart[];
       else
          failure;
      if);
   #)

-- MPSnewNameApl: doPart --
do (# theNameApl: ^nameApl;
      theNameAppl: ^nameAppl;
   do (nameApl,frag[]) -> newAst -> theNameApl[] -> theAst[];
      (nameAppl,frag[]) -> newAst -> theNameAppl[] -> theNameApl.putNameAppl;
      name[] -> theNameAppl.putText;
   #)

-- MPSnewNameDcl: doPart --
do (# theNameDcl: ^nameDcl;
      theNameDecl: ^nameDecl;
   do (nameDcl,frag[]) -> newAst -> theNameDcl[] -> theAst[];
      (nameDecl,frag[]) -> newAst -> theNameDecl[] -> theNameDcl.putNameDecl;
      name[] -> theNameDecl.putText;
   #)

-- MPSnewNames: doPart --
do (# theNames: ^names;
   do (names,frag[]) -> newAst ->  theNames[] -> theAst[];
      (frag[],name[]) -> newNameDcl -> theNames.append;
   #)

-- betaAttributes: attributes --
getNames: manipulateAttribute
  (# theNames: ^names;
  do (if theAttributeDecl.symbol
       //patternDecl then
          (# thePatternDecl: ^patternDecl;
          do theAttributeDecl[] -> thePatternDecl[];
             thePatternDecl.getNames -> theNames[];
          #);
       //simpleDecl then
          (# theSimpleDecl: ^simpleDecl;
          do theAttributeDecl[] -> theSimpleDecl[];
             theSimpleDecl.getNames -> theNames[];
          #);
       //virtualDecl then
          (# theVirtualDecl: ^virtualDecl;
          do theAttributeDecl[] -> theVirtualDecl[];
             theVirtualDecl.getNames -> theNames[];
          #);
       //bindingDecl then
          (# theBindingDecl: ^bindingDecl;
          do theAttributeDecl[] -> theBindingDecl[];
             theBindingDecl.getNames -> theNames[];
          #);
       //finalDecl then
          (# theFinalDecl: ^finalDecl;
          do theAttributeDecl[] -> theFinalDecl[];
             theFinalDecl.getNames -> theNames[];
          #);
      //repetitionDecl then
         (# theRepDecl: ^repetitionDecl;
         do theAttributeDecl[] -> theRepDecl[];
            theRepDecl.getNames -> theNames[];
         #);
      else
         failure;
     if);
  exit theNames[] 
  #);


-- MPSnewDoPart: doPart --
do (# theDoPart: ^doPart;
      theImperatives: ^imperatives;
      gram: @grammar;
   do (doPart,frag[]) -> newAst -> theDoPart[] -> theAst[];
      (imperatives,frag[]) -> newAst -> theImperatives[] -> theDoPart.putImperatives;
      (gram.impOpt,frag[]) -> makeOptional -> theImperatives.append;
   #);
   
-- MPSnewSlotDoPart: doPart --
do (# theUnExpanded: ^unExpanded;
      theSlotDesc: ^slotDesc;
   do (doPart, frag[]) -> newUnExpanded -> theUnExpanded[] -> theAst[];
      frag[] -> newSlot -> theSlotDesc[] -> theUnExpanded.theSlot;
      name[] -> theSlotDesc.name;
   #);

-- MPSnewOpenStatement: doPart --
do (# theObjectDenotation: ^objectDenotation;
      theRemote, theRemote2: ^remote;
   do (objectDenotation,frag[]) -> newAst -> theObjectDenotation[] -> theAst[];
      (remote,frag[]) -> newAst -> theRemote[] -> theObjectDenotation.putAttributeDenotation;
      (if isPrivate then
          (remote,frag[]) -> newAst -> theRemote2[] -> theRemote.putAttributeDenotation;
          (frag[],'private') -> newNameApl -> theRemote2.putAttributeDenotation;
          (frag[],objectName[]) -> newNameApl -> theRemote2.putNameApl;
       else
          (frag[],objectName[]) -> newNameApl -> theRemote.putAttributeDenotation;
      if);
      
      (frag[],'open') -> newNameApl -> theRemote.putNameApl;
   #);


-- MPSfind: doPart --
do 
   l: theAttributes.iterate
     (# currentAttribute: ^attributeDecl;
        theNames: ^names;
     do current[] -> currentAttribute[];
        current[] -> getNames -> theNames[];
        theNames.iterate
        (# 
        do (if current.getText -> name.equalNCS then
               currentAttribute[] -> theAttributeDecl[];
               leave l;
           if);
        #);
     #);
   
-- MPSappendAttribute: doPart --
do (# theObjectDescriptor: ^objectDescriptor;
      theMainPart: ^mainPart;
      theAttributes: ^attributes;
   do (if theAttributeDecl.symbol
       //patternDecl 
       //virtualDecl
       //bindingDecl
       //finalDecl then 
          theAttributeDecl[] -> getObjectDescriptor -> theObjectDescriptor[];
       //simpleDecl then
          (# theSimpleDecl: ^simpleDecl;
             theReferenceSpecification: ^ReferenceSpecification;
             theStaticItem: ^StaticItem;
             theObjectSpecification: ^ObjectSpecification;
             theNameApl: ^NameApl;
          do theAttributeDecl[] -> theSimpleDecl[];
             theSimpleDecl.getReferenceSpecification -> theReferenceSpecification[];
             (if theReferenceSpecification.symbol
              //staticItem then
                 theReferenceSpecification[] -> theStaticItem[];
                 theStaticItem.getObjectSpecification -> theObjectSpecification[];
                 (if theObjectSpecification.symbol
                  //objectDescriptor then
                     theObjectSpecification[] -> theObjectDescriptor[];
                  //nameApl then
                     theObjectSpecification[] -> theNameApl[];
                     (theAttributeDecl.frag[],theNameApl.getText) 
                       -> newObjectDescriptor -> theObjectDescriptor[] 
                       -> theStaticItem.putObjectSpecification;
                  else failure;
                 if);
              else
                 failure;
             if);
          #);
       else
          failure;
      if);
      (if theObjectDescriptor[] <> None then
          theObjectDescriptor.getMainPart -> theMainPart[];
          theMainPart.getAttributes -> theAttributes[];
          theAttribute[] -> theAttributes.smartAppend;
      if);
   #)

-- MPSnewComment: doPart --
do (comment, theText.length, frag[]) -> newLexemText -> theAst[];
   theText[] -> theAst.putText;
   1 -> theAst.commentType;
   
-- MPSnewSingularItem: doPart --
do (# theSimpleDecl: ^simpleDecl;
      theStaticItem: ^staticItem;
   do (simpleDecl,frag[]) -> newAst -> theSimpleDecl[] -> theAst[];
      (frag[],name[]) -> newNames -> theSimpleDecl.putNames;
      (staticItem,frag[]) -> newAst -> theStaticItem[] -> theSimpleDecl.putReferenceSpecification;
      (frag[],prefixName[]) -> newObjectDescriptor -> theStaticItem.putObjectSpecification;
      
   #)
   
-- MPSnewInitFromResourceStatement: doPart --
do (# theAssignmentEvaluation: ^assignmentEvaluation;
      theIntegerConst: ^integerConst;
      theConst: ^const;
      theObjectDenotation: ^objectDenotation;
      theEvalList: ^evalList;
      theEvaluations: ^evaluations;
      theTextConst: ^textConst;
      theString: ^string;
   do (assignmentEvaluation, frag[]) -> newAst -> theAssignmentEvaluation[] -> theAst[];
      (evalList, frag[]) -> newAst -> theEvalList[]
        -> theAssignmentEvaluation.putEvaluation;
      (evaluations, frag[]) -> newAst -> theEvaluations[]
        -> theEvalList.putEvaluations;
      (textConst, frag[]) -> newAst -> theTextConst[]
        -> theEvaluations.append;
      (string, frag[]) -> newAst -> theString[]
        -> theTextConst.putString;
      storeName[] -> theString.putText;
      
      (integerConst, frag[]) -> newAst -> theIntegerConst[]
        -> theEvaluations.append;
      frag[] -> newConst -> theConst[] -> theIntegerConst.putConst;
      objectID -> theConst.putValue;
      (objectDenotation, frag[] ) -> newAst -> theObjectDenotation[] 
        -> theAssignmentEvaluation.putTransaction;
      (frag[], 'initFromResource') -> newNameApl -> theObjectDenotation.putAttributeDenotation;
   #);
-- MPSattributeGetAttributes: doPart --
do (# theObjectDescriptor: ^objectDescriptor;
      theMainPart: ^mainPart;
   do theAttributeDecl[] -> getObjectDescriptor -> theObjectDescriptor[];
      (if theObjectDescriptor[]//none then
          failure;
       else
          theObjectDescriptor.getMainPart -> theMainPart[];
          theMainPart.getAttributes -> theAttributes[];
      if);
   #);
-- MPSFragmentGroupFindFragment: doPart --
do search: fragmentList.scan
   (# 
   do (if current.type = formType then
          (if current.name[] -> name.equalNCS then
              current.open -> frag[];
              leave search;
          if);
      if);
   #);
   
-- astInterfaceLib: attributes --
check:
  (# anAst: ^ast;
  enter anAst[]
  do (if anAst[] <> NONE then
         (if anAst.kind <> kinds.interior then
             NONE -> anAst[];
         if);
     if)
  exit anAst[]
  #);

-- AstInterfaceSafeOpen: doPart --
do l: (name[], screen[]) -> top.open
   (# 
      startingParsing::<
        (# 
        do 'parsing...' -> putLine;
        #);
      astOverflow::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      fragmentNotExisting::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      grammarNotFound::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      badFormat::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      parseErrors::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      fatalParseError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      doubleFormDeclaration::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      readAccessError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      writeAccessError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      writeAccessOnLstFileError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      EOSError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      noSuchFileError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      fileExistsError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      noSpaceLeftError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
      otherFileError::< 
        (#
        do none -> theCatcher[];
           leave l;
        #);
   #) -> group[];
   
-- MPSnewFragment: doPart --
do this(beta)[] -> newFragmentForm -> frag[];
   name.copy -> frag.name;
   inner;

-- MPSnewDescriptorFrag: doPart --
do (frag[], none) -> newDescriptorForm -> frag.root[];

-- MPSnewDoPartFrag: doPart --
do frag[] -> newDoPart -> frag.root[];

-- MPSnewAttributesFrag: doPart -- 
do (# theAttributesForm: ^attributesForm;
      theAttributes: ^attributes;
      gram: @grammar;
   do (attributesForm, frag[]) -> newAst -> theAttributesForm[] -> frag.root[];
      (attributes, frag[]) -> newAst -> theAttributes[] -> theAttributesForm.putAttributes;
      (1, (gram.attributeDeclOpt, frag[]) -> makeOptional) -> theAttributes.insert;
   #);

-- listEmpty: doPart --
do true -> value;
   l: scan
   (# 
   do (if current.kind <> kinds.optional then
          false -> value;
          leave l;
      if);
   #);
   
-- newAttributesDoPart: doPart --
do (attributes, frag[]) -> newAst -> theAst[];
   (1, theAttributeDecl[]) -> theAst.insert;
   
-- generateNewBooleanBinding: doPart --
do (# theFinalDecl: ^finalDecl;
      theDescriptor: ^objectDescriptor;
      theMainPart: ^mainPart;
      theActionPart: ^actionPart;
      theDoPart: ^doPart;
      theImperatives: ^imperatives;
      theBooleanAssignment: ^imp;
   do (finalDecl, frag[]) -> newAst -> theFinalDecl[] -> theAst[];
      (frag[], name[]) -> newNames -> theFinalDecl.putNames;
      (frag[], none) -> newObjectDescriptor -> theDescriptor[] -> theFinalDecl.putObjectSpecification;
      theDescriptor.getMainPart -> theMainPart[];
      theMainPart.getActionPart -> theActionPart[];
      (doPart, frag[]) -> newAst -> theDoPart[] -> theActionPart.putDoPartOpt;
      (imperatives, frag[]) -> newAst -> theImperatives[] -> theDoPart.putImperatives;
      (frag[], 'value', value) -> newBooleanAssignment -> theBooleanAssignment[];
      (1, theBooleanAssignment[]) -> theImperatives.insert;
   #);
   
-- generateNewBooleanAssignment: doPart --
do (# theAssignEval: ^assignmentEvaluation;
      theObjectDenotation1: ^objectDenotation;
      theObjectDenotation2: ^objectDenotation;
   do (assignmentEvaluation, frag[]) -> newAst -> theAssignEval[] -> theAst[];
      (objectDenotation, frag[]) -> newAst -> theObjectDenotation1[] -> theAssignEval.putEvaluation;
      (frag[], 'true') -> newNameApl -> theObjectDenotation1.putAttributeDenotation;
      (objectDenotation, frag[]) -> newAst -> theObjectDenotation2[] -> theAssignEval.putTransaction;
      (frag[], name[]) -> newNameApl -> theObjectDenotation2.putAttributeDenotation;
   #);
   
-- generateNewAssignment: doPart --
do (# theAssignEval: ^assignmentEvaluation;
      theObjectDenotation: ^objectDenotation;
   do (assignmentEvaluation, frag[]) -> newAst -> theAssignEval[] -> theAst[];
      value[] -> theAssignEval.putEvaluation;
      (objectDenotation, frag[]) -> newAst -> theObjectDenotation[] -> theAssignEval.putTransaction;
      (frag[], name[]) -> newNameApl -> theObjectDenotation.putAttributeDenotation;
   #)

-- generateNewTextEvaluation: doPart --
do (# theTextConst: ^textConst;
      theString: ^string;
   do (textConst, frag[]) -> newAst -> theTextConst[] -> theAst[];
      (string, frag[]) -> newAst -> theString[] -> theTextConst.putString;
      value[] -> theString.putText
   #);

-- generateNewIntEvaluation: doPart --
do (# theIntegerConst: ^integerConst;
      theConst: ^const;
   do (integerConst, frag[]) -> newAst -> theIntegerConst[] -> theAst[];
      frag[] -> newConst -> theConst[] -> theIntegerConst.putConst;
      value -> theConst.putValue;
   #);

-- generateNewPointEvaluation: doPart --
do (# theObjectDenotation: ^objectDenotation;
      theEvalList: ^evalList;
      theEvaluations: ^evaluations;
   do (evalList, frag[]) -> newAst -> theEvalList[] -> theAst[];
      (evaluations, frag[]) -> newAst -> theEvaluations[]
        -> theEvalList.putEvaluations;
      (frag[], x) -> newIntEvaluation -> theEvaluations.append;
      (frag[], y) -> newIntEvaluation -> theEvaluations.append;
     
   #);

-- generateNewBoolEvaluation: doPart --
do (# theObjectDenotation: ^objectDenotation;
   do (objectDenotation, frag[]) -> newAst -> theObjectDenotation[] -> theAst[];
      (if value
       //true then
          (frag[], 'true') -> newNameApl -> theObjectDenotation.putAttributeDenotation;
       //false then
          (frag[], 'false') -> newNameApl -> theObjectDenotation.putAttributeDenotation;
      if);
   #)

-- generateFindAssignment: doPart --
do search: theImperatives.iterate
   (# theTransaction: ^ast;
      theAssignEval: ^assignmentEvaluation;
      theObjectDenotation: ^objectDenotation;
      theAttributeDenotation: ^ast;
      theNameApl: ^nameApl;
      itsName: ^text;
   do (if current.symbol = assignmentEvaluation then
          current[] -> theAssignEval[];
          theAssignEval.getTransaction -> theTransaction[];
          (if theTransaction.symbol = objectDenotation then
              theTransaction[] -> theObjectDenotation[];
              theObjectDenotation.getAttributeDenotation -> theAttributeDenotation[];
              (if theAttributeDenotation.symbol = nameApl then
                  theAttributeDenotation[] -> theNameApl[];
                  theNameApl.getText -> itsName[];
                  (if name[] -> itsName.equalNCS then
                      current[] -> theAssignment[];
                      leave search;
                  if);
              if);
          if);
      if);
   #);
   
-- generateEvaluateAssignment: doPart --
do INNER;
   
-- generateEvaluateRect: doPart --
do (# theEvalList: ^evalList;
      theEvaluations: ^evaluations;
      anEvaluation: ^evaluation;
      theIntegerConst: ^integerConst;
      theConst: ^const;
   do (if theEvaluation.symbol = evalList then
          theEvaluation[] -> theEvalList[];
          theEvalList.getEvaluations -> theEvaluations[];
          (if theEvaluations.noOfsons = 2 then
              1 -> theEvaluations.get -> anEvaluation[];
              (if anEvaluation.symbol = evalList then
                  anEvaluation[] -> evaluatePoint -> (x, y);
                  2 -> theEvaluations.get -> anEvaluation[];
                  (if anEvaluation.symbol = evalList then
                      anEvaluation[] -> evaluatePoint -> (width, height);
                      width - x -> width;
                      height - y -> height;
                   else
                      failure;
                  if);
               else
                  failure;
              if);
           else
              failure;
          if);
       else
          failure;
      if);
   #)
   
-- generateEvalutePoint: doPart --
do (# theEvalList: ^evalList;
      theEvaluations: ^evaluations;
      anEvaluation: ^evaluation;
      theIntegerConst: ^integerConst;
      theConst: ^const;
   do (if theEvaluation.symbol = evalList then
          theEvaluation[] -> theEvalList[];
          theEvalList.getEvaluations -> theEvaluations[];
          (if theEvaluations.noOfsons = 2 then
              1 -> theEvaluations.get -> anEvaluation[];
              (if anEvaluation.symbol = integerConst then
                  anEvaluation[] -> theIntegerConst[];
                  theIntegerConst.getConst -> theConst[];
                  theConst.getValue -> x;
                  2 -> theEvaluations.get -> anEvaluation[];
                  (if anEvaluation.symbol = integerConst then
                      anEvaluation[] -> theIntegerConst[];
                      theIntegerConst.getConst -> theConst[];
                      theConst.getValue -> y;
                   else
                      failure;
                  if);
               else
                  failure;
              if);
           else
              failure;
          if);
       else
          failure;
      if);
   #);

-- generateEvaluteInt: doPart --
do (# theIntegerConst: ^integerConst;
      theConst: ^const;
   do (if theEvaluation.symbol = integerConst then
          theEvaluation[] -> theIntegerConst[];
          theIntegerConst.getConst -> theConst[];
          theConst.getValue -> value;
       else
          failure;
      if);
   #);

-- generateEvaluteBool: doPart --
do (# theAttributeDenotation: ^ast;
      theObjectDenotation: ^objectDenotation;
      theNameApl: ^nameApl;
      theText: ^text;
   do (if theEvaluation.symbol = objectDenotation then
          theEvaluation[] -> theObjectDenotation[];
          theObjectDenotation.getAttributeDenotation -> theAttributeDenotation[];
          (if theAttributeDenotation.symbol = nameApl then
              theAttributeDenotation[] -> theNameApl[];
              theNameApl.getText -> theText[];
              (if true
               //'true' -> theText.equalNCS then
                  true -> value;
               //'false' -> theText.equalNCS then
                  false -> value;
               else
                  failure;
              if);
           else
              failure;
          if);
       else
          failure;
      if);
   #);

-- generateEvaluteText: doPart --
do (# theTextConst: ^textConst;
      theString: ^string;
   do (if theEvaluation.symbol = textConst then
          theEvaluation[] -> theTextConst[];
          theTextConst.getString -> theString[];
          theString.getText -> value[];
       else
          failure;
      if);
   #);

   
-- MPSFragmentGroupFindBodyFragment: doPart --
do search: scanBodyGroups
     (# body: ^fragmentGroup;
     do currentFullPath[] -> safeOpen -> body[];
        (if body[] <> none then
            name[] -> body.findFragment -> frag[];
            (if frag[] <> NONE then
                leave search;
            if);
        if);
     #);
