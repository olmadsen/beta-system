ORIGIN '../createmps';

INCLUDE '~beta/betaast/v5.1/betacfl';
INCLUDE '~beta/betaast/v5.1/betasematt';
INCLUDE '~beta/mps/v5.1/metagrammarcfl';
INCLUDE '~beta/mps/v5.1/private/astparser';
INCLUDE '~beta/mps/v5.1/findgrammar';

-- lib: attributes --

betaAstinterface: astInterface
  (# 
     defaultGrammarFinder::< findGrammar;
  #);

-- CreateMPSDoPart: descriptor  --
(# do (#  InitBeta:
        (* Initialize the betagrammar. *)
        (# BetaGrammarName: 
             (# t: ^Text;
             do '~beta/grammars/beta/v2.4/beta' -> t[];
             exit t[]
             #);
           BetaParserName: 
             (# t: ^Text;
             do '~beta/grammars/beta/v2.4/beta' -> MPS.ExpandToFullPath -> t[];
                '-parser.btab' -> t.append;
             exit t[]
             #);
           MetaGrammarName: 
             (# t: ^Text;
             do '~beta/grammars/metagram/v4.4/metagrammar' -> t[];
             exit t[]
             #);
           theGram: ^theMetaGrammar.Agrammar;
           Error:< Exception
             (# t: ^Text;
             enter t[]
             do 'Failed to initialize the betagrammar ' -> msg.putText;
                BetaGrammarName -> msg.putLine;
                t[] -> msg.putText;
                inner;
             #);
           theMetaGrammar: ^MPS.MetaGrammar;
           theBetaGrammar: ^MPS.Beta;
           g: ^MPS.fragmentGroup;
        do &MPS.MetaGrammar[] -> theMetaGrammar[] -> MPS.grammarTable.insertMetaGrammar;
           (* theMetaGrammar.Init; *)
           (MetaGrammarName,screen[]) -> MPS.Top.Open -> g[];
           ('meta', screen[])  -> g.open -> g[];
           ('metagrammar', screen[]) -> g.open -> theMetaGrammar.grammarAst[];
           theMetaGrammar.Init;
           &MPS.Beta[] -> theBetaGrammar[];
           (* theBetaGrammar.init; *)
           theBetaGrammar[] -> MPS.GrammarTable.Beta[];
           (BetaGrammarName,screen[]) -> MPS.Top.Open -> g[];
           ('meta', screen[]) -> g.open -> g[];
           ('beta', screen[]) -> g.open -> theBetaGrammar.grammarAst[];
           BetaParserName -> theBetaGrammar.parser.Initialize;
           (if theBetaGrammar.grammarAst[]//None
               then
               (* 'grammarast missing' -> Error; *)
            else
               theBetaGrammar[] -> MPS.grammarTable.t[MPS.grammarTable.noOfKnownGrammars+1][];
               MPS.grammarTable.noOfKnownGrammars+1 -> MPS.grammarTable.noOfKnownGrammars;
               theBetaGrammar.init;
               theBetaGrammar.grammarAst.root[] -> theGram[];
               (if theGram[]//None
                   then
                   'grammar ast has no root' -> Error;
                else
                   BetaParserName -> theBetaGrammar.parser.Initialize;
                   ('objectDescriptor','descriptor') -> theBetaGrammar.parser.privatePart.b.defineNonTAlias;
                    ('objectDescriptor','descriptorForm') -> theBetaGrammar.parser.privatePart.b.defineNonTAlias;
                    ('attributeDecl','attributes') -> theBetaGrammar.parser.privatePart.b.defineNonTAlias;
                    ('attributeDecl','attributesForm') -> theBetaGrammar.parser.privatePart.b.defineNonTAlias;
               if);
           if);
           
        #);
   do
      &betaAstinterface[] -> MPS[];
      MPS.AstLevelInit;
     (* ('beta',screen[]) -> MPS.grammarTable.find -> MPS.grammarTable.beta[]; *)
      InitBeta;
   #)
#)
