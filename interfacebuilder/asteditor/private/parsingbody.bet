ORIGIN 'parsing';
INCLUDE '~beta/mps/v5.0.1/metagramsematt';
INCLUDE '~beta/mps/v5.0.1/metagrammarcfl';

-- ParsingSynCatNoDoPart: Descriptor --
(#
   fatherSynCatNo: @MPS.NonterminalSymbol;
   fatherNode: ^MPS.expanded;
do
   node[] -> GetSynCatNo -> synCatNo;
   
   Node.father -> fatherNode[];
   (if fatherNode[]//none 
       then (* root *); 
    else
       synCatNo -> FindSuper -> synCatNo;
       (* fatherNode[] -> GetSynCatNo -> fatherSynCatNo;
        *        (if fatherSynCatNo -> IsListCategory then 
        *            fatherSynCatNo -> synCatNo;
        *         else
        *            synCatNo -> FindSuper -> synCatNo
        *        if);
        *)
   if);
#)

-- GetSynCatNoDoPart: descriptor --
(#
   nontNode: ^MPS.unExpanded;
do
   (if ((node.kind = MPS.kinds.unexpanded) OR (node.kind = MPS.kinds.optional)) then
       Node[] -> nontNode[];
       nontNode.nonterminalSymbol -> synCatNo
    else
       node.symbol -> synCatNo;
   if)
#)

-- FindSuperDoPart: Descriptor --
UseMetaGrammar
(# 
   superSynCatNo,lastSuper: @MPS.NonterminalSymbol;
   prod: ^theMetaGrammar.prod;
   leftSide: ^theMetaGrammar.leftSide;
   foundSuper: @boolean;
do
   (if not synCatNo.isLexem  then 
       false -> foundSuper;
       synCatNo -> ProdList.get -> prod[];
       loop:
         (# 
         do
            Prod.leftSide -> leftSide[];
            1 -> leftSide.getAttribute -> superSynCatNo;
            (if superSynCatNo
             //MPS.super.cons then 
             //MPS.super.list then 
             //MPS.super.undefined then 
             else 
                true -> foundSuper;
                superSynCatNo -> ProdList.get -> prod[];
                superSynCatNo -> lastSuper;
                restart loop;
            if);
         #);
       (if foundSuper //true then lastSuper -> synCatNo if);
   if);
#)

-- IsListCategoryDoPart: Descriptor --
UseMetaGrammar
(#      
   Prod: ^theMetaGrammar.prod;
do
   SynCatNo -> ProdList.get -> Prod[];
   (Prod.symbol = theMetaGrammar.listOne) 
   or 
   (Prod.symbol = theMetaGrammar.listZero) 
     -> IsList;
#)

-- astEditorEnvLib: Attributes --
UseMetaGrammar:
  (# theMetaGrammar: ^MPS.MetaGrammar;
     ProdList: ^theMetaGrammar.ProductionList;
     theGram: ^theMetaGrammar.Agrammar;
  do MPS.GrammarTable.Meta[] -> theMetaGrammar[];
     MPS.GrammarTable.BETA.grammarAst.root[] -> theGram[];
     theGram.getProductionList -> ProdList[];
     inner UseMetaGrammar;
  #);


