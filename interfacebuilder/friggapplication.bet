ORIGIN '~beta/editor/v5.1/ymersiflib';

INCLUDE '~beta/editor/v5.1/private/editorenvbody';
INCLUDE '~beta/sourcebrowser/v1.1/private/ymerBody';

INCLUDE 'graphicaleditor/graphicaleditor';
INCLUDE '~beta/guienv/v1.4/utils/getfile';
INCLUDE 'mpsstuff/mpsutils';
INCLUDE 'dialogs/promptfortextdialog';
INCLUDE 'code/generate';
INCLUDE 'code/pattern';
INCLUDE 'code/followsemanticlink';
INCLUDE 'resourcesupport/parameters';
INCLUDE 'dialogs/createclassdialog';
(* INCLUDE 'preferences/preferences'; *)
INCLUDE 'guienvstuff/getpreferencefile';
INCLUDE 'prettyprinter/prettyprinter';
INCLUDE 'utils/catchallfile';
INCLUDE 'buildermenu';
INCLUDE '~beta/pretty/v5.1/pplib';
INCLUDE 'dialogs/applicationdialog';
INCLUDE 'classes/classes';

INCLUDE 'seticon';

-- lib: attributes --
friggApplication: fragmentBrowser
(# isBETAsif:: (# do true -> value #);
   
   (* 
    * Handling of notifications from Sif
    *)
   
   
   
   buildApplication:
     (# frag: ^astInterface.fragmentForm;
        theDialog: @applicationDialog
          (# onBuild::
               (# 
               do theDialog.close;
                  (if resourceFileName[] -> entryWriteable then
                      (frag.father, resourceFileName[]) -> compileResources;
                   else
                      'You do not have write permissions build the resource file' -> displayMessage;
                  if);
               #);
             onCancel::
               (# 
               do theDialog.close;
               #);
          #);
        defaultNames:
          (# frag: ^astInterface.fragmentForm;
             applicationName, resourceFileName: ^text;
             groupName: ^text;
             e: @diskEntry;
          enter frag[]
          do (frag.father).name -> groupName[];
             groupName[] -> e.path;
             e.path.name -> applicationName[];
             '.rc' -> (applicationName.copy).append -> resourceFileName[];
          exit (resourceFileName[], applicationName[])
          #);
     enter frag[]
     do 
        frag[] -> defaultNames -> theDialog.run;
     #);
   compileResources:
     (# root: ^astInterface.fragmentGroup;
        fileExists: booleanValue
          (# fileName: ^text;
             anEntry: @diskEntry;
          enter fileName[]
          do fileName[] -> anEntry.path;
             l: anEntry.exists
               (# error::
                    (# 
                    do leave l;
                    #);
               #) -> value;
          #);
        
        getResourceData:
          (# fg: ^astInterface.fragmentGroup;
             resourceData: ^atom;
          enter fg[]
          do fg[] -> theGraphicalEditorEnv.findResourceData -> resourceData[];
             getting:
               (if resourceData[] = none then
                   (# 
                      theFile: @catchAllFile
                        (# failure::
                             (# 
                             do
                                leave getting;
                             #);
                        #);
                      parser: @atomParser;
                      resourceFileName: ^text;
                      ignoreAtom: ^text;
                      version: @integer;
                   do '.res' -> ((fg.name).copy).Append -> resourceFileName[];
                      (if resourceFileName[] -> fileExists then
                          resourceFileName[] -> theFile.name;
                          theFile.openRead;
                          theFile.getAtom -> ignoreAtom[];
                          theFile.getInt -> version;
                          (if version = 2 then
                              theFile[] -> parser.init;
                              parser.readAtom -> resourceData[];
                          if);
                          theFile.close;
                      if);
                   #);
               if);
          exit resourceData[]
          #);
        addResourceFile:
          (# 
             fg: ^astInterface.fragmentGroup;
          enter fg[]
          do adding:
               (# 
                  parameterList: ^atom;
                  resourceEntry: ^tableAtom;
                  nameAtom: ^textAtom;
               do
                  fg[] -> getResourceData  -> parameterList[];
                  (if parameterList[] <> none then
                      &tableAtom[] -> resourceEntry[];
                      resourceEntry.storage.init;
                      &textAtom[] -> nameAtom[];
                      fg.shortName -> nameAtom;
                      ('name', nameAtom[]) -> resourceEntry.define;
                      ('value', parameterList[]) -> resourceEntry.define;
                      resourceEntry[] -> theListAtom.storage.append;
                  if);
               #);
          #);
        theListAtom: ^listAtom;
        printer: @atomPrinter;
        output: @file;
        fileName: ^text;
     enter (root[], fileName[])
     do 
        (if fileName[] <> none then
            'Compiling resources... ' -> ymerbrowser.browser.infoView.setText;
            ymerbrowser.browser.infoView.display;
            &listAtom[] -> theListAtom[];
            theListAtom.storage.init;
            machineType -> MPS.dg.targetMachine;
            machineType -> MPS.dg.targetDirectory;
            root.name -> MPS.dg.scanExtent
            (# resourceFileName: ^text;
            do current[] -> addResourceFile;
            #);
            fileName[]  -> output.name;
            output.openWrite;
            output[] -> printer.init;
            theListAtom[] -> printer.printAtom;
            output.close;
            'done' -> ymerbrowser.browser.infoView.append;
            ymerbrowser.browser.infoView.display;
        if);
     #);
   
   onGroupSaved::
     (# theDocument: ^graphicalEditorEnv.document;
     do 
        fg[] -> theGraphicalEditorEnv.findRelatedDocument -> theDocument[];
        
        (if theDocument[] <> none then
            (if theDocument.touched  then
                theDocument.save;
            if);
        if)
     #);
   onGroupNotSaved::
     (# theDocument: ^graphicalEditorEnv.document;
     do fg[] -> theGraphicalEditorEnv.findRelatedDocument -> theDocument[];
        
        (if theDocument[] <> none then
            theDocument.deleteAutosaveFiles;
        if)
     #);
   onGroupClosed::
     (# theDocument: ^graphicalEditorEnv.document;
     do fg[] -> theGraphicalEditorEnv.findRelatedDocument -> theDocument[];
        (if theDocument[] <> none then
            theDocument[] -> theGraphicalEditorEnv.closeGraphicalEditors;
        if)
     #);
   
   
   (* prefs: ^preferences.section; *)
   
   (*
    * Misc utilities that ought to be somewhere else...
    *)
   
   stripExtension:
     (# path: ^text;
        lastDotInx, lastSlashInx: @integer;
     enter path[]
     do '.' -> path.findAll (# do inx -> lastDotInx #);
        MPS.ast.thePathHandler.directoryChar -> path.findAll (# do inx -> lastSlashInx #);
        (if (lastDotInx > 0) and (lastDotInx > lastSlashInx) then
            (lastDotInx, path.length) -> path.delete;
        if);
     #);
   
   getName:
     (# path: ^text;
        name: ^text;
        lastSlashInx: @integer;
     enter path[]
     do MPS.ast.thePathHandler.directoryChar -> path.findAll (# do inx -> lastSlashInx #);
        (if (lastSlashInx > 0) and (lastSlashInx <> path.length) then
            (lastSlashInx + 1, path.length) -> path.sub -> name[];
         else
            path.copy -> name[];
        if);
     exit name[]
     #);
   
   createFile:
     (# fileName: ^text;
        msg: ^text;
     enter msg[]
     do ymerBrowser[] -> fileCreationDialog
        (# 
        do (* msg[] -> label[]; *)
        #) -> fileName[];
        (if fileName[] <> none then
            fileName[] -> stripExtension;
        if);
     exit fileName[]
     #);
   getIdentifier:
     (# identifier: ^text;
        prompt: ^text;
     enter prompt[]
     do (none, 'Create window', prompt[], '') -> promptForText
        (# 
           validate::
             (# str: ^text
             do false -> value;
                userText.copy -> str[];
                checking:
                  (# EOS: (# exit 1 #);
                     ch: @char;
                     next:
                       (# 
                       do (if str.eos then
                              EOS -> ch;
                           else
                              str.get -> ch;
                          if);
                       #);
                     
                  do str.reset;
                     next;
                     (if ch = EOS then
                         leave checking;
                      else
                         (if ch -> ASCII.isLetter then
                             next;
                             l:
                               (if ch = EOS then
                                   leave l;
                                else
                                   (if (ch -> ASCII.isLetter) or (ch -> ASCII.isDigit) then
                                       next;
                                       restart l;
                                    else
                                       leave checking;
                                   if);
                               if);
                          else
                             leave checking;
                         if);
                     if);
                     true -> value;
                  #);
                
                (if not value then
                    'There are ilegal characters the name' -> displayMessage;
                if);
             #);
           ok::
             (# 
             do userText.copy -> identifier[];
             #);
        #);
     exit identifier[]
     #);
   
   externalFragmentFormHandler: ymerBrowser.browser.edenv.externalFragmentFormHandler
     (# astReplacedEvent::
          (# 
          do (false, ff[], oldAst[], newAst[]) -> theFragServer.notifyAstReplaced;
          #);
        astReplacedSequenceEvent::
          (# 
          do (false, ff[], rootOfSequence[]) -> theFragServer.notifyAstReplacedSequence;
          #);
        listElementInsertedEvent::
          (#
          do (false, ff[], listNode[], position) -> theFragServer.notifylistElementInserted;
          #);
        makeAstList:
          (# thePartList: ^window.editorEnv.partList;
             theAstList: ^astInterface.astList;
             length: @integer;
          enter (length, thePartList[])
          do &mps.ast.astList[] -> theAstList[];
             theAstList.init;
             (for inx: length repeat
                  thePartList.elm[inx][] -> theAstList.insert;
             for);
          exit theAstList[]
          #);
        listElementsDeletedEvent::
          (#
          do (false, ff[], listNode[], position, length, (length, oldElements[]) -> makeAstList)
               -> theFragServer.notifyListElementsDeleted;
          #);
        listElementsReplacedEvent::
          (#
          enter (listNode[],position,length,oldElements[],newLength)
          do (false, ff[], listNode[], position, length, 
             (length, oldElements[]) 
               -> makeAstList, newLength)
               -> theFragServer.notifyListElementsReplaced;
          #);
     #);
   
   theFragServer: @fragmentServer
     (# subscribe::
          (# effh: ^externalFragmentFormHandler
          do &externalFragmentFormHandler[] -> effh[];
             fragHandler.frag[] -> effh.ff[];
             effh[] -> ymerBrowser.browser.edenv.ffhandler.externalSubscribe;
          #);
        
        findFFhandler:
          (# frag: ^astInterface.fragmentForm;
             ffh: ^window.editorEnv.fragmentFormHandler;
          enter frag[]
          do l: ymerBrowser.browser.edenv.ffhandler.scan
               (# 
               do 
                  (if current.ff[] = frag[] then
                      current[] -> ffh[];
                      leave l;
                  if);
               #);
          exit ffh[]
          #);
        withFFH:
          (# ffh: ^window.editorEnv.fragmentFormHandler;
             frag: ^astInterface.fragmentForm;
             fromInterfaceBuilder: @boolean;
          enter (frag[],  fromInterfaceBuilder)
          do (if fromInterfaceBuilder then
                 frag[] -> findFFhandler -> ffh[];
                 (if ffh[] <> none then
                     inner;
                 if);
             if);
          #);
        notifyAstReplaced::
          (# 
          do (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do (none, oldAst[], newAst[]) -> ffh.anAstReplacedEvent;
             #);
          #);
        notifyListElementInserted::
          (#
          do (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do (none, listNode[], position) -> ffh.aListElementInsertedEvent;
             #);
          #);
        notifyAstReplacedSequence::
          (# 
          do (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do (none, rootOfSequence[]) -> ffh.astReplacedSequenceEvent;
             #);
          #);
        makePartList:
          (# theAstList: ^astInterface.astList;
             thePartList: ^window.editorEnv.partList;
             inx: @integer;
          enter theAstList[]
          do &ymerBrowser.browser.edenv.partList[] -> thePartList[];
             theAstList.size -> thePartList.elm.new;
             0 -> inx;
             theAstList.scan
             (# 
             do inx + 1 -> inx;
                current[] -> thePartList.elm[inx][];
             #);
          exit thePartList[]
          #);
        notifyListElementsDeleted::
          (# 
          do 
             (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do 
                (none, listNode[], position, length, oldElements[] -> makePartList) 
                  -> ffh.aListElementsDeletedEvent;
             #);
          #);
        notifyListElementsReplaced::
          (# 
          do (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do 
                (none, listNode[], position, length, oldElements[] -> makePartList, newLength) 
                  -> ffh.aListElementsReplacedEvent;
             #);
          #);
     #);
   
   
   pretty: @MPS.AST.prettyPrinter;
   
   findGroupEditor:
     (# fg: ^astInterface.fragmentGroup;
        theGroupEditor: ^window.editorEnv.groupEditor;
     enter fg[]
     do (ymerBrowser.browser[], fg[]) 
          -> ymerBrowser.browser.edenv.groupEditorList.findGroupEditor -> theGroupEditor[];
        (if theGroupEditor[] = none then
            'The groupeditor for ' -> putText;
            fg.name -> putText;
            ' was not found' -> putLine;
        if);
     exit theGroupEditor[]
     #);
   
   theGraphicalEditorEnv: @graphicalEditorEnv
     (# save::
          (# 
          do 'Calling save all' -> putLine;
             ymerBrowser.saveAll;
          #);
        openAstEditor::
          (# 
          do node.frag[] -> ymerBrowser.findForm;
             (node[], node[]) -> ymerBrowser.browser.cfe.openSubEditor;
          #);
        autoSaveGroup::
          (# 
          do ('#', fg[]) -> ymerBrowser.browser.edenv.editorenvPrivate.extendedSaveBackup  
          #);
        onGroupOpen::
          (#
          do fg[] -> ymerBrowser.findGroup;
          #);
        saveFragmentGroup::
          (# theGroupEditor: ^window.editorEnv.groupEditor;
          do fg[] -> findGroupEditor -> theGroupEditor[];
             (if theGroupEditor[] = none then
                 'The groupeditor is NONE!' -> putLine;
              else
                 (true, false,true) -> theGroupEditor.save;
             if);
          #);
        onGroupChanged::
          (# theGroupEditor: ^window.editorEnv.groupEditor;
          do fg[] -> findGroupEditor -> theGroupEditor[];
             (if theGroupEditor[] = none then
                 'The groupeditor is NONE!' -> putLine;
              else
                 theGroupEditor.groupTouched;
             if);
          #);
        onGroupSaved::
          (# theGroupEditor: ^window.editorEnv.groupEditor;
          do fg[] -> findGroupEditor -> theGroupEditor[];
             (if theGroupEditor[] = none then
                 'The groupeditor is NONE!' -> putLine;
              else
                 theGroupEditor.resetGroupTouch;
             if);
          #);
     #);
   
   
   (* 
    * Application info
    *)
   
   
   (* End of application info *)
   
   ymerBrowserWindow::
     (# browserTitle::
          (# 
          do 'Frigg: Mjolner BETA Browser and Interfacebuilder' -> title;
          #);
        makeSavePrompt:
          (# fg: ^astInterface.fragmentGroup;
             str: ^text;
          enter fg[]
          do 'Save changes to ' -> str[];
             fg.name -> str.putText;
             '?' -> str.put;
          exit str[]
          #);
        deleteHistory:
          (# fg: ^astInterface.fragmentGroup;
             
          enter fg[]
          do '[ Scanning projects ]' -> putLine;
             browser.projects.theProjects.scan
             (# 
             do 'Project = ' -> putText;
                current.location[] -> putLine;
                current.groups.scan
                (# 
                do 'group = ' -> putText;
                   current.location[] -> putText;
                   (if current.fg[] = fg[] then
                       ' ***' -> putLine;
                       none -> current.fg[];
                    else
                       newLine;
                   if);
                #);
             #);
             '[ Done scanning projects ]' -> putLine;
             newLine;
             ymerBrowserPrivate.history.iterate
             (# 
             do 
                (if current.elm.theGroup.fg[] = fg[] then
                    current.elm[]->(theMenubar).historyMenu.closeHistoryElement;
                    current[]->ymerBrowserPrivate.history.delete;
                if);
             #);
             (if browser.currentProject[] <> none then
                none ->browser.currentProject.lastGroupSelected[]
             if);
             (if browser.currentGroup[] <> none then
                none ->browser.currentGroup.lastFormSelected[]
             if);
             (browser.currentProject[],none ) -> browser.setGroup;
             none -> browser.cge[];
             none -> browser.cfe[];
          #);
        closeFragmentGroup:
          (# fg: ^astInterface.fragmentGroup;
             okToClose: @boolean;
          enter fg[]
          do l:
               (# ge: ^window.editorEnv.groupEditor;
               do fg[] -> findGroupEditor -> ge[];
                  (if ge[] <> none then
                      (if ge.touched > 0 then
                          (none, 'Close group', fg[] -> makeSavePrompt) -> promptForBoolean
                          (# ok::
                               (# 
                               do (true, true,true) -> ge.save;
                                  true -> okToClose;
                               #);
                             notOK::
                               (# 
                               do true -> okToClose;
                               #);
                             cancel::
                               (# 
                               do false -> okToClose;
                               #);
                          #);
                       else
                          true -> okToClose;
                      if);
                      (if okToClose then
                          fg[] -> deleteHistory;
                      if);
                  if);
                  (if okToClose then
                      (* fg.close; *)
                  if);
               #);
          exit okToClose
          #);
        
       
        (*
         * Menubar
         *)
        
        menubarType::
          (# groupFileMenuType::
               (# friggCloseItem: @item
                    (# onStatus::
                         (# 
                         do browser.cge[] <> none -> value;
                         #);
                       onSelect::
                         (# 
                         do browser.cge.fg[] -> closeFragmentGroup;
                         #);
                    #);
                  
                  open::
                    (# 
                    do
                       'New...'->iNew.new;
                       'New BETA Program...'->iNewBetaProgram.new;
                       'New BETA Library...'->iNewBetaLibrary.new;
                       newSeparator;
                       'Close'->iClose.new;
                       'Save'->iSave.new;
                       (* 'Save As...'->iSaveAs.new; *)
                       'Save All...'->iSaveAll.new;
                       'Save Abstract...'->iSaveAbstract.new;
                       newSeparator;
                       'Recover'->iRecover.new;
                       'Revert'->iRevert.new;
                       newSeparator;
                       'Group Write Protection'->iSetIsReadOnly.new;
                       'Global Write Protection'
                         ->iSetGlobalWriteProtection.new
                    #);
                  
               #);
             groupPerformMenuType::
               (#                                         
                  open::< 
                    (# 
                    do
                       'Check Current'->iCheck.new;
                       'Recheck'->iReCheck.new;
                       'Compile Current'->iCompile.new;
                       'Recompile'->iRecompile.new;
                       'Execute Current'->iExecute.new;
                       'Reexecute'->iReExecute.new;
                       newSeparator;
                       'Debug'->iDebug.new;
                       newSeparator;
                       'Semantic Errors...'->iSemanticErrors.new;
                       newSeparator;
                       'Show Diagram'->iShowDiagram.new;
                       INNER
                    #)
               #);
             ibMenu: @builderMenu
               (# newWindowLibItem: @builderItem
                    (# eventHandler::
                         (# onStatus::
                              (# 
                              do true -> value;
                              #);
                            onSelect::
                              (# 
                              do 'guienvLib' -> createSourceFile;
                              #);
                         #);
                       open::
                         (# 
                         do 'New Window library...' -> name;
                         #);
                    #);
                  
                  newCanvasLibItem: @builderItem
                    (# eventHandler::
                         (# onStatus::
                              (# 
                              do true -> value;
                              #);
                            onSelect::
                              (# 
                              do 'windowLib' -> createSourceFile;
                              #);
                         #);
                       open::
                         (# 
                         do 'New Canvas library...' -> name;
                         #);
                    #);
                  
                  
                  openGraphicalEditor: @builderItem
                    (# eventHandler::
                         (# onStatus::
                              (# name: ^text;
                                 theClassData: ^graphicalEditorEnv.classData;
                              do false -> value;
                                 (if browser.cfe[] <> none then
                                     (if browser.cfe.cs[] <> none then
                                         (if browser.cfe.cs.node[] <> none then
                                             (if browser.cfe.cs.node.symbol
                                              //MPS.betacfl.patternDecl
                                              //MPS.betacfl.simpleDecl then
                                                 (browser.cfe.cs.node[], none) -> theGraphicalEditorEnv.getClassData -> theClassData[];
                                                 (if theClassData[] = NONE then
                                                     false -> value;
                                                     'Edit object' -> this(builderItem).name;
                                                  else
                                                     true -> value;
                                                     'Edit ' -> name[];
                                                     browser.cfe.cs.node[] -> MPS.betacfl.getName -> name.append;
                                                     name[] -> this(builderItem).name;
                                                 if)
                                             if);
                                         if);
                                     if);
                                 if);
                              #);
                            onSelect::
                              (# group: ^astInterface.fragmentGroup;
                                 result: @boolean;
                              do browser.cfe.cs.node.frag.father -> group[];
                                 browser.cfe.cs.node[] -> editWindow;
                              #);
                         #);
                       open::
                         (# 
                         do 'Edit object' -> name;
                         #);
                    #);
                  newGraphicalEditor: @builderItem
                    (# eventHandler::
                         (# onStatus::
                              (# frag: ^astInterface.fragmentForm;
                                 fragName: ^text;
                              do false -> value;
                                 (if browser.cfe[] <> none then
                                     (if browser.cfe.cs.node[] <> none then
                                         browser.cfe.cs.node.frag[] -> frag[];
                                         (if frag[] <> None then
                                             (if frag.category = mps.betaCfl.attributesForm then
                                                 frag.name -> fragName[];
                                                 (if true
                                                  //'guienvLib' -> fragName.equalNCS then
                                                     'Create window...' -> name;
                                                     true -> value;
                                                  //'windowLib' -> fragName.equalNCS then
                                                     'Create canvas...' -> name;
                                                     true -> value;
                                                  else
                                                     'Create...' -> name;
                                                     false -> value;
                                                 if);
                                             if);
                                         if);
                                     if);
                                 if);        
                              #);
                            onSelect::
                              (# frag: ^astInterface.fragmentForm;
                                 result: @boolean;
                                 fragName, className: ^text;
                              do (if browser.cfe[] <> none then
                                     (if browser.cfe.cs.node[] <> none then
                                         browser.cfe.cs.node.frag[] -> frag[];
                                         (if frag[] <> None then
                                             (if frag.category = mps.betaCfl.attributesForm then
                                                 frag.name -> fragName[];
                                                 (if true
                                                  //'guienvLib' -> fragName.equalNCS then
                                                     'window' -> className[];
                                                  //'windowLib' -> fragName.equalNCS then
                                                     'canvas' -> className[];
                                                 if);
                                                 (frag[], className[]) -> createWindow;
                                             if);
                                         if);
                                     if);
                                 if);
                              #);
                         #);
                       open::
                         (# 
                         do 'Create...' -> name;
                         #);
                    #);
                  
                  open::
                    (# 
                    do 'Interfacebuilder' -> name;
                      
                       newWindowLibItem.open;
                       newCanvasLibItem.open;
                       addSeparator;
                       openGraphicalEditor.open;
                       newGraphicalEditor.open;
                    #);
               #);
             open::
               (# 
               do SLOTs.new;
                  SLOTs[]->append;
                  'Fragment'->GroupEdit.new;
                  'Tools'->GroupPerform.new;
                  ibMenu.open;
                  ibMenu[] -> append;
               #);
          #);
        
        (* End of Menubar *)
        
        (*
         * Event handling
         *)
        
        eventHandler::
          (# 
          #);
        
        open::
          (# width, height: @integer;
          do setFriggIcon;
             size -> (width, height);
             (width + 150, height) -> size;
          #);
        
        (*
         * Handling of windows
         *)
        
        saveAll:
          (# putBool:
               (# b: @boolean;
               enter b
               do (if b then
                      'true' -> putText;
                   else
                      'false' -> putText;
                  if);
               #);
          do theGraphicalEditorEnv.scanDocuments
             (# 
             do current.save;
             #);
          #);
        
        saveGroup:
          (# fg: ^astInterface.fragmentGroup;
          enter fg[]
          do saving:
               (# 
                  theFile: @catchAllFile
                    (# failure::
                         (# 
                         do msg[] -> putLine;
                            leave saving;
                         #);
                    #);
                  fileName: ^text;
               do (fg.name).copy -> fileName[];
                  '.bet' -> fileName.append;
                  fileName[] -> theFile.name;
                  theFile.openWrite;
                  (mps.ast[], fg[], theFile[], none) -> prettyPrintFragment;
                  theFile.close;
                  fg.fragmentList.scan
                  (# frag: ^astInterface.fragmentForm;
                  do (if current.type = MPS.AST.formType then
                         current.open -> frag[];
                         frag.recomputeSlotChain;
                     if);
                  #);
                  'doneCheck' -> fg.prop.deleteProp;
                  fg.markAsChanged;
               #);
          #);
        
        findPrivateSlotName:
          (# slotName: ^text;
             node: ^astInterface.beta.attributeDecl;
             failure:<
               (# msg: ^text;
               enter msg[]
               do inner;
               #);
             betaGram: ^astInterface.beta;
          enter node[]
          do (# theAst: ^astInterface.AST;
                theSimpleDecl: ^astInterface.beta.simpleDecl;
                theReferenceSpecification: ^astInterface.beta.ReferenceSpecification;
                theStaticItem: ^astInterface.beta.staticItem;
                theUnExpanded: ^astInterface.unExpanded;
                theSlotDesc: ^astInterface.slotDesc;
             do MPS.betacfl[] -> betaGram[];
                (node[], 'private') -> betaGram.findAttribute -> theAst[];
                (if theAst[] <> none then
                    (if theAst.symbol = betaGram.simpleDecl then
                        theAst[] -> theSimpleDecl[];
                        theSimpleDecl.getReferenceSpecification -> theReferenceSpecification[];
                        (if theReferenceSpecification.symbol = betagram.staticItem then
                            theReferenceSpecification[] -> theStaticItem[];
                            theStaticItem.getObjectSpecification -> theAst[];
                            (if theAst.kind = MPS.AST.kinds.unExpanded then
                                theAst[] -> theUnExpanded[];
                                (if theUnExpanded.isSlot then
                                    theUnExpanded.theSlot -> theSlotDesc[];
                                    theSlotDesc.name -> slotName[];
                                 else
                                    'The private decl lacks a SLOT' -> failure;
                                if);
                             else
                                'The private decl lacks a SLOT' -> failure;
                            if);
                         else
                            'The private decl is not a static item' -> failure;
                        if);
                     else
                        'The private decl is not a static item' -> failure;
                    if);
                 else
                    'There is no private decl' -> failure;
                if);
             #);
          exit slotName[]
          #);
        editWindow:
          (# theAttributeDecl: ^astInterface.ast;
             theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
             theDocument: ^graphicalEditorEnv.document;
             fg: ^astInterface.fragmentGroup;
          enter theAttributeDecl[]
          do l: (# 
                do
                   theAttributeDecl.frag.father -> fg[];
                   fg[] -> theGraphicalEditorEnv.findDocument -> theDocument[];
                   (if theDocument[] = none then
                       &theGraphicalEditorEnv.document[] -> theDocument[];
                       fg.name -> theDocument.open
                       (# 
                          error::
                            (# 
                            do msg[] -> displayMessage;
                               leave l;
                            #);
                       #);
                   if);
                   
                   (if theDocument.readOnly then
                       'Warning! The window is read only' -> displayMessage;
                   if);
                   theAttributeDecl[] -> theGraphicalEditorEnv.find -> theGraphicalEditor[];
                   l: (if theGraphicalEditor[] = none then
                          (# slotName: ^text;
                             body: ^astInterface.fragmentGroup;
                             frag: ^astInterface.fragmentForm;
                             help: ^text;
                          do theAttributeDecl[] -> MPS.betaCfl.getPrivateFragment -> frag[];
                             
                             &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                             theDocument[] -> theGraphicalEditor.theDocument[];
                             theGraphicalEditor.open;
                             (theAttributeDecl[], frag[]) -> theGraphicalEditor.init;
                             theDocument.readOnly -> theGraphicalEditor.readOnly;
                             
                          #);
                       else
                          theGraphicalEditor.bringToFront;
                      if);
                #);
          #);
        
        createWindow:
          (# frag: ^astInterface.fragmentForm;
             className: ^text;
          enter (frag[], className[])
          do l:
               (# theAttributesForm: ^astInterface.beta.attributesForm;
                  theAttributes: ^astInterface.beta.attributes;
                  theAttribute: ^astInterface.beta.attributeDecl;
                  name: ^text;
                  theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
                  theDocument: ^graphicalEditorEnv.document;
                  fg: ^astInterface.fragmentGroup;
                  classes: ^patternList;
                  classNames: @textList;
               do classNames.init;
                  className[] -> classNames.append;
                  
                  frag.father -> fg[];
                  (MPS.AST[], className[], fg[]) -> getClasses -> classes[];
                  classes.scan
                  (# 
                  do current.node[] -> MPS.betaCFL.getName -> classNames.append;
                  #);
                  
                  fg[] -> theGraphicalEditorEnv.findDocument -> theDocument[];
                  (if theDocument[] = none then
                      &theGraphicalEditorEnv.document[] -> theDocument[];
                      fg.name -> theDocument.open
                      (# 
                         error::
                           (# 
                           do msg[] -> displayMessage;
                              leave l;
                           #);
                      #);
                  if);
                  (if theDocument.readOnly then
                      'You do not have write acces to one or all of the files' -> displayMessage;
                   else
                      frag.root[] -> theAttributesForm[];
                      theAttributesForm.getAttributes -> theAttributes[];
                      ('', classNames[]) -> userCreateClass
                      (# createClass::
                           (# isIdentifier: @boolean;
                           do name[] -> theGraphicalEditorEnv.checkIdentifier -> isIdentifier;
                              (if isIdentifier then
                                  (theAttributes[], name[]) ->mps.betacfl.find -> theAttribute[];
                                  (if theAttribute[] <> none then
                                      (this(ymerBrowserWindow)[],
                                      'The window could not be created because it already exists', 
                                      'Alert') -> noteUser;
                                   else
                                      (# son1: ^mps.ast.ast;
                                         doReplace: @boolean;
                                         super: ^astInterface.beta.patternDecl;
                                      do
                                         (frag[], name[], className[]) -> mps.betacfl.newPatternAttribute -> theAttribute[];
                                         className[] -> classes.findByName -> super[];
                                         (if super[] <> none then
                                             (theAttribute[], super[]) -> setSemanticLink;
                                         if);
                                         (if theAttributes.noOfsons = 1 then
                                             1 -> theAttributes.get -> son1[];
                                             (if son1.kind = mps.ast.kinds.optional then
                                                 true -> doReplace;
                                             if);
                                         if);
                                         (if doReplace then
                                             (1, theAttribute[]) -> theAttributes.put;
                                          else
                                             theAttribute[] -> theAttributes.append;
                                         if);
                                         &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                                         theDocument[] -> theGraphicalEditor.theDocument[];
                                         theGraphicalEditor.open;
                                         theAttribute[] -> theGraphicalEditor.new;
                                         (if doReplace then
                                             (true, frag[], son1[], theAttribute[])
                                               -> theFragServer.notifyAstReplaced; 
                                          else
                                             (true, frag[], theAttributes[], theAttributes.noOfSons - 1) 
                                               -> theFragServer.notifyListElementInserted;
                                         if);
                                      #);
                                  if);
                               else
                                  'That is not a valid beta name' -> displayMessage;
                              if);
                           #);
                      #);
                      
                  if);
               #);
          #);
        createWindow1:
          (# frag: ^astInterface.fragmentForm;  
          enter frag[]
          do l:
               (# theAttributesForm: ^astInterface.beta.attributesForm;
                  theAttributes: ^astInterface.beta.attributes;
                  theAttribute: ^astInterface.beta.attributeDecl;
                  name: ^text;
                  theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
                  theDocument: ^graphicalEditorEnv.document;
                  fg: ^astInterface.fragmentGroup;
               do frag.father -> fg[];
                  fg[] -> theGraphicalEditorEnv.findDocument -> theDocument[];
                  (if theDocument[] = none then
                      &theGraphicalEditorEnv.document[] -> theDocument[];
                      fg.name -> theDocument.open
                      (# 
                         error::
                           (# 
                           do msg[] -> displayMessage;
                              leave l;
                           #);
                      #);
                  if);
                  (if theDocument.readOnly then
                      'You do not have write acces to one or all of the files' -> displayMessage;
                   else
                      frag.root[] -> theAttributesForm[];
                      theAttributesForm.getAttributes -> theAttributes[];
                      'Name:' -> getIdentifier -> name[];
                      (if name[] <> none then
                          (theAttributes[], name[]) ->mps.betacfl.find -> theAttribute[];
                          (if theAttribute[] <> none then
                              (this(ymerBrowserWindow)[],
                              'The window could not be created because it already exists', 
                              'Alert') -> noteUser;
                           else
                              (# son1: ^mps.ast.ast;
                                 doReplace: @boolean;
                              do
                                 (frag[], name[], 'window') -> mps.betacfl.newPatternAttribute -> theAttribute[];
                                 (if theAttributes.noOfsons = 1 then
                                     1 -> theAttributes.get -> son1[];
                                     (if son1.kind = mps.ast.kinds.optional then
                                         true -> doReplace;
                                     if);
                                 if);
                                 (if doReplace then
                                     (1, theAttribute[]) -> theAttributes.put;
                                  else
                                     theAttribute[] -> theAttributes.append;
                                 if);
                                 &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                                 theDocument[] -> theGraphicalEditor.theDocument[];
                                 theGraphicalEditor.open;
                                 theAttribute[] -> theGraphicalEditor.new;
                                 (if doReplace then
                                     (true, frag[], son1[], theAttribute[])
                                       -> theFragServer.notifyAstReplaced; 
                                  else
                                     (true, frag[], theAttributes[], theAttributes.noOfSons - 1) 
                                       -> theFragServer.notifyListElementInserted;
                                 if);
                              #);
                          if);
                      if);
                      
                  if);
               #);
          #);
        createSourceFile:
          (# theDocument: ^graphicalEditorEnv.document;
             fileName: ^text;
             file1, file2: ^text;
             e: @diskEntry;
             fragName: ^text;
          enter fragName[]
          do 'File name: ' -> createFile -> fileName[];
             l:
               (if fileName[] <> none then
                   fileName[] -> stripExtension;
                   fileName[] -> e.path;
                   (if e.exists and e.isDirectory then
                       (this(ymerBrowserWindow)[],
                       'There is a directory with that name', 
                       'Alert') -> noteUser;
                    else
                       &theGraphicalEditorEnv.document[] -> theDocument[];
                       (fileName[], fragName[]) -> theDocument.new
                       (# 
                          error::
                            (# 
                            do msg[] -> displayMessage;
                               leave l;
                            #);
                       #);
                       theDocument.interfaceGroup.markAsChanged;
                       theDocument.bodyGroups.scan
                       (# 
                       do current.markAsChanged;
                          current.fullName -> ymerBrowser.addProject;
                       #);
                       (* theDocument.theParameterStore.save; *)
                       theDocument.interfaceGroup.fullName -> ymerBrowser.addProject; 
                       
                   if);
               if);
          #);
     #);
   init::
     (# arg: ^text;
     do ymerBrowser.initCompiler;
        ('Sif: Known Bugs and Inconveniences',
        '~beta/editor/v5.1/doc/BUGS')-> helpDocument;
        (* ('mjolner', 'Interfacebuilder') -> getPreferencesSection  -> prefs[]; *)
        theFragServer.init;
        (theFragServer[], MPS.AST[], MPS.betaCfl[], pretty[]) -> theGraphicalEditorEnv.init;
        MPS.betaCfl[] -> pretty.init;
        pretty[] -> objectPool.put;
        
        (if noOfarguments > 1 then
            scanArguments:
              (for i: noOfArguments-1 repeat
                   i+1->arguments->arg[];
                   arg[]->ymerbrowser.appendProject
              for);
            (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
            
        if)
     #);
   
#)
