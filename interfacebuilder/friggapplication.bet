ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'graphicaleditor/graphicaleditor'
        'graphicaleditor/standardeditors'
        'graphicaleditor/basiceditors'
        'graphicaleditor/icons'
        '~beta/toollibs/utils/mpsutils'
        'dialogs/promptfortextdialog'
        'code/generate'
        'code/pattern'
        'code/followsemanticlink'
        'resourcesupport/parameters'
        'guienvstuff/loadbitmap'
        'prettyprinter/prettyprinter'
        'utils/catchallfile'
        'buildermenu'
        'classes/classes'
        'additional/additional'
        '~beta/sourcebrowser/ymerInterface'
        'frigginterface';
BODY 'private/friggapplicationbody';
LIB_DEF 'frigg_application' '../lib';

-- lib: Attributes --
friggApplication: (*GRAM fragmentBrowser *)
  (#
     gui: ^guienv;
     mps: ^ymermps;
     ymer: ^ymerEnvInterface;
     ymerbrowser: ^ymerbrowserInterface;
     <<SLOT friggApplicationLib:Attributes>>;
     isBETAsif:< booleanValue (#  do true->value; INNER #);
     externalInterface: @friggInterface
       (#
          askEditUserInterface::
            (# name: ^text; className: ^text; 
            do
               false->value;
               (if node.symbol
                // MPS.betacfl.patternDecl // MPS.betacfl.simpleDecl then
                   node[]->theGraphicalEditorEnv.getClassName->className[];
                   (if className[] = none then
                       false->value; 
                    else
                       true->value; 
                   if)
               if)
            #);
          editUserInterface:: 
            (# theAttributeDecl: ^astInterface.ast;
               findWindow:
                 (# node: ^astInterface.AST;
                    isGUIDef: booleanValue
                      (# a: ^astInterface.AST;
                         className: ^Text;
                      enter a[]
                      do false -> value;
                         (if a.kind = mps.ast.kinds.interior then
                             (if a.symbol
                              // MPS.betacfl.patternDecl // MPS.betacfl.simpleDecl then
                                 a[]->theGraphicalEditorEnv.getClassName->className[];
                                 className[]<>NONE -> value;
                             if)
                         if);
                      #);
                    theAttributeDecl: ^astInterface.ast;
                 enter node[]
                 do loop:
                      (#
                      do (if node[] -> isGUIDef then
                             node[] -> theAttributeDecl[];
                             leave loop;
                         if);
                         node.father -> node[];
                         (if node[] <> NONE then
                             restart loop;
                         if);
                      #);
                 exit theAttributeDecl[]
                 #);
            do node[] -> findWindow ->  theAttributeDecl[];
               (if theAttributeDecl[] <> NONE then
                   theAttributeDecl[]->editWindow;
                else
                   'there is no window' -> putline;
               if);
               
            #);
          OKtoTextedit::
            (#  do node[]->theGraphicalEditorEnv.okToTextEdit->value #)
       #);
     editWindow:
       (#
          theAttributeDecl: ^astInterface.ast;
          theGraphicalEditor: ^gui.graphicalEditorEnv.graphicalEditor;
          
       enter theAttributeDecl[]
       do
          theAttributeDecl[]->theGraphicalEditorEnv.find->theGraphicalEditor[];
          (if theGraphicalEditor[] = none then
              &theGraphicalEditorEnv.graphicalEditor[]->theGraphicalEditor[];
              theGraphicalEditor.open;
              theAttributeDecl[]->theGraphicalEditor.init;
              
           else
              theGraphicalEditor.bringToFront; 
          if);
          
       #);
     processCommand:
       (# cmd: ^text
       enter cmd[]
       <<SLOT friggApplicationProcessCommand:DoPart>>
       #);
     initializeDynamicCompiler:< (#  do INNER #);
     dynamicCompilationAvailable:< booleanValue (#  do INNER ;  #);
     evaluateFile:<
       (# name: ^text; theOrigin: ^object; P: ##object; 
       enter (name[],theOrigin[])
       do INNER
       exit P##
       #);
     compilePattern:<
       (#
          node: ^astInterface.beta.patternDecl;
          theOrigin: ^object;
          structure: ##object;
          
       enter (node[],theOrigin[])
       do INNER
       exit structure##
       #);
     compileFile:<
       (# fname,pname: ^text; theOrigin: ^object; structure: ##object; 
       enter (fname[],pname[],theOrigin[])
       do INNER
       exit structure##
       #);
     pretty: ^MPS.AST.prettyPrinter;
     theGraphicalEditorEnv: ^frigggraphicaleditorenv;
     frigggraphicaleditorenv: gui.graphicalEditorEnv
       (#
          ymerBrowser: ^ymerBrowserInterface;
          findform:
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do
               (if ff[] <> none then
                   ((ff.father).fullName,ff.name)
                     ->ymerbrowser.selectFragmentForm
                else
                   'findForm: ff is none!'->putLine
               if)
            #);
          findGroup:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            do
               (if fg[] <> none then
                   fg.fullName->ymerbrowser.selectFragmentGroup
                else
                   'findGroup: fg is none!'->putLine
               if)
            #);
          commands:: 
            (#
               dynamicCompilationAvailable:: 
                 (# 
                 do THIS(friggApplication).dynamicCompilationAvailable->value; 
                 #);
               evaluateFile:: 
                 (# 
                 do
                    (name[],theOrigin[])->THIS(friggApplication).evaluateFile
                      ->P##;
                    
                 #);
               compilePattern:: 
                 (# 
                 do
                    (node[],theOrigin[])->THIS(friggApplication).compilePattern
                      ->structure##;
                    
                 #);
               compileFile:: 
                 (# 
                 do
                    (fname[],pname[],theOrigin[])
                      ->THIS(friggApplication).compileFile->structure##;
                    
                 #);
               openAstEditor:: 
                 (# 
                 <<SLOT theGraphicalEditorEnvOpenAstEditorDopart:DoPart>>
                 #);
               autoSaveGroup:: 
                 (# 
                 <<SLOT theGraphicalEditorEnvAutoSaveGroupDopart:DoPart>>
                 #);
               saveFragmentGroup:: 
                 (# 
                 <<SLOT theGraphicalEditorEnvSaveFragmentGroupDopart:DoPart>>
                 #);
               quitApplication:: 
                 (#  <<SLOT friggApplicationQuitApplication:DoPart>> #);
               prettyPrintGroup::
                 (#  <<SLOT prettyPrintGroup:DoPart>> #)
            #);
          onGroupOpen:: 
            (# 
            <<SLOT theGraphicalEditorEnvOnGroupOpenDopart:DoPart>>
            #);
          onGroupChanged:: 
            (#  <<SLOT theGraphicalEditorEnvOnGroupChangedDopart:DoPart>> #);
          onGroupSaved:: 
            (#  <<SLOT theGraphicalEditorEnvOnGroupSavedDopart:DoPart>> #);
          init::< 
            (# 
            do
               insertBasicEditors;
               insertStandardSuite;
               INNER ;
               
            #);
          
       #);
     setupMenus:
       (# 
       do
          &gui.MenubarAppendMenuAction
            (#
               friggmenu: gui.BuilderMenu
                 (#
                    browser: ^ymerBrowserInterface;
                    newWindowItem: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus::  (#  do true->value #);
                              onSelect:: 
                                (#  <<SLOT newWindowItemOnSelect:DoPart>> #);
                              
                           #);
                         open::  (#  do 'New Window'->name;  #);
                         
                      #);
                    newWindowLibItem: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# 
                                <<SLOT ibMenuNewWindowLibItemOnStatus:DoPart>>
                                #);
                              onSelect:: 
                                (# 
                                <<SLOT ibMenuNewWindowLibItemOnSelect:DoPart>>
                                #);
                              
                           #);
                         open::  (#  do 'New Window library...'->name;  #);
                         
                      #);
                    newCanvasLibItem: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# 
                                <<SLOT ibMenuNewCanvasLibItemOnStatus:DoPart>>
                                #);
                              onSelect:: 
                                (# 
                                <<SLOT ibMenuNewCanvasLibItemOnSelect:DoPart>>
                                #);
                              
                           #);
                         open::  (#  do 'New Canvas library...'->name;  #);
                         
                      #);
                    openGraphicalEditor: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# name: ^text; className: ^text; 
                                do
                                   false->value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs[] <> none then
                                           (if browser.cfe.cs.node[] <> none
                                            then
                                               browser.cfe.cs.node[]
                                                 ->
                                                   externalinterface.
                                                     askEditUserInterface->value
                                           if);
                                           
                                       if);
                                       
                                   if);
                                   
                                #);
                              onSelect:: 
                                (# 
                                do browser.cfe.cs.node[]->editWindow; 
                                #);
                              
                           #);
                         open::  (#  do 'Edit object'->name;  #);
                         
                      #);
                    newGraphicalEditor: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (#
                                   frag: ^astInterface.fragmentForm;
                                   fragName: ^text;
                                   
                                do
                                   false->value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs.node[] <> none then
                                           browser.cfe.cs.node.frag[]->frag[];
                                           (if frag[] <> none then
                                               (if frag.category =
                                               mps.betaCfl.attributesForm then
                                                   frag.name->fragName[];
                                                   (if true
                                                    //
                                                    'guienvLib'
                                                      ->fragName.equalNCS then
                                                       'Create window...'->name;
                                                       true->value;
                                                       
                                                    //
                                                    'windowLib'
                                                      ->fragName.equalNCS then
                                                       'Create canvas...'->name;
                                                       true->value;
                                                       
                                                    else
                                                       'Create...'->name;
                                                       false->value;
                                                       
                                                   if);
                                                   
                                               if);
                                               
                                           if);
                                           
                                       if);
                                       
                                   if);
                                   
                                #);
                              onSelect:: 
                                (#
                                   frag: ^astInterface.fragmentForm;
                                   result: @boolean;
                                   fragName,className: ^text;
                                   
                                <<SLOT newGraphicalEditorOnSelect:DoPart>>
                                #);
                              
                           #);
                         open:: 
                           (# 
                           do
                              'Create...'->name;
                              
                           #);
                         
                      #);
                    addToPalette: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# 
                                do
                                   false->value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs.node[] <> none then
                                           (if browser.cfe.cs.node.symbol =
                                           theGraphicalEditorEnv.gram.
                                             patternDecl then
                                               true->value; 
                                           if);
                                           
                                       if);
                                       
                                   if);
                                   
                                #);
                              onSelect:: 
                                (# 
                                do
                                   browser.cfe.cs.node[]
                                     ->
                                       theGraphicalEditorEnv.
                                         addPatternToPalette;
                                   
                                #)
                           #);
                         open::  (#  do 'Add to Palette'->name;  #);
                         
                      #);
                    open:: 
                      (# 
                      do
                         'GUI'->name;
                         newWindowLibItem.open;
                         newCanvasLibItem.open;
                         addSeparator;
                         openGraphicalEditor.open;
                         newGraphicalEditor.open
                      #);
                    
                 #);
               fm: ^friggmenu;
               
            do
               &friggmenu[]->fm[];
               theBrowser[]->fm.browser[];
               fm.open;
               fm[]->themenubar.append;
               
            #)[]->ymer.addMenuAction;
          
       #);
     friggBrowserWindowExt: (* ymerbrowserwindow::< *)
       (#
          init:
            (# 
            enter ymerBrowserInt[]
            do
               ymerBrowserInt.ymerBrowser[]->ymerBrowser[];
               ymerBrowserInt[]->theGraphicalEditorEnv.ymerBrowser[]
                 ->THIS(friggApplication).ymerBrowser[];
               
            #);
          ymerBrowserInt: ^ymerBrowserInterface;
          ymerBrowser: ^gui.window;
          browserTitle:
            (# title: ^text; 
            do
               'Frigg: Mjolner BETA Browser and Interfacebuilder'->title;
               INNER ;
               
            #);
          (*
           * Menubar
           *)
          
          (* End of Menubar *)
          (*
           * Handling of windows
           *)
          findPrivateSlotName:
            (#
               slotName: ^text;
               node: ^astInterface.beta.attributeDecl;
               failure:< (# msg: ^text;  enter msg[] do INNER ;  #);
               betaGram: ^astInterface.beta;
               
            enter node[]
            do
                 (#
                    theAst: ^astInterface.AST;
                    theSimpleDecl: ^astInterface.beta.simpleDecl;
                    theReferenceSpecification:
                      ^astInterface.beta.ReferenceSpecification;
                    theStaticItem: ^astInterface.beta.staticItem;
                    theUnExpanded: ^astInterface.unExpanded;
                    theSlotDesc: ^astInterface.slotDesc;
                    
                 do
                    MPS.betacfl[]->betaGram[];
                    (node[],'private')->betaGram.findAttribute->theAst[];
                    (if theAst[] <> none then
                        (if theAst.symbol = betaGram.simpleDecl then
                            theAst[]->theSimpleDecl[];
                            theSimpleDecl.getReferenceSpecification
                              ->theReferenceSpecification[];
                            (if theReferenceSpecification.symbol =
                            betagram.staticItem then
                                theReferenceSpecification[]->theStaticItem[];
                                theStaticItem.getObjectSpecification->theAst[];
                                (if theAst.kind = MPS.AST.kinds.unExpanded then
                                    theAst[]->theUnExpanded[];
                                    (if theUnExpanded.isSlot then
                                        theUnExpanded.theSlot->theSlotDesc[];
                                        theSlotDesc.name->slotName[];
                                        
                                     else
                                        'The private decl lacks a SLOT'
                                          ->failure;
                                        
                                    if);
                                    
                                 else
                                    'The private decl lacks a SLOT'->failure; 
                                if);
                                
                             else
                                'The private decl is not a static item'
                                  ->failure;
                                
                            if);
                            
                         else
                            'The private decl is not a static item'->failure; 
                        if);
                        
                     else
                        'There is no private decl'->failure; 
                    if);
                    
                 #);
               
            exit slotName[]
            #);
          
       #);
     init:<
       (# arg: ^text; 
       enter ymer[]
       do
          ymer.gui[]->gui[];
          ymer.mps[]->mps[];
          &frigggraphicaleditorenv[]
            ->theGraphicalEditorEnv[];
          &MPS.ast.prettyPrinter[]->pretty[];
          MPS.betaCfl[]->pretty.init;
          pretty[]->objectPool.put;
          initializeDynamicCompiler;
          (MPS.AST[],MPS.betaCfl[],pretty[])->theGraphicalEditorEnv.init;
          theGraphicalEditorEnv.insertAdditionalSuite;
          INNER ;
          
       #);
     
  do INNER ; 
  #)  

