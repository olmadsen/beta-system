ORIGIN 'sifapplication';
INCLUDE '~beta/editor/private/editorenvbody'
        '~beta/sourcebrowser/private/ymerBody'
        'graphicaleditor/graphicaleditor'
        'graphicaleditor/standardeditors'
        'graphicaleditor/icons'
        'mpsstuff/mpsutils'
        'dialogs/promptfortextdialog'
        'code/generate'
        'code/pattern'
        'code/followsemanticlink'
        'resourcesupport/parameters'
        'dialogs/createclassdialog'
        'guienvstuff/loadbitmap'
        'prettyprinter/prettyprinter'
        'utils/catchallfile'
        'buildermenu'
        'dialogs/applicationdialog'
        'classes/classes'
        'graphicaleditor/overview'
        'seticon';
BODY 'private/friggapplicationbody';
-- systemlib: Attributes --
friggApplication: sifApplication
  (#
     onQuit::  (#  do 'Quiting...'->putLine;  #);
     <<SLOT friggApplicationLib:Attributes>>;
     processCommand:
       (# cmd: ^text
       enter cmd[]
       <<SLOT friggApplicationProcessCommand:DoPart>>
       #);
     dynamicCompilationAvailable:<
      booleanValue (#  do INNER ;  #);
     evaluateFile:<
       (# name: ^text; theOrigin: ^object; P: ##object; 
       enter (name[],theOrigin[])
       do INNER
       exit P##
       #);
     onGroupSaved::  (#  <<SLOT friggApplicationOnGroupSaved:DoPart>> #);
     onGroupNotSaved:: 
       (# 
       <<SLOT friggApplicationOnGroupNotSaved:DoPart>>
       #);
     onGroupClosed::  (#  <<SLOT friggApplicationOnGroupClosed:DoPart>> #);
     theFragServer:
       @fragmentServer
       (#
          <<SLOT theFragServerLib:Attributes>>;
          subscribe:: 
            (# 
            <<SLOT theFragServerSubscribeDopart:DoPart>>
            #);
          notifyAstReplaced:: 
            (#  <<SLOT theFragServerNotifyAstReplacedDopart:DoPart>> #);
          notifyListElementInserted:: 
            (# 
            <<SLOT theFragServerNotifyListElementInsertedDopart:DoPart>>
            #);
          notifyAstReplacedSequence:: 
            (# 
            <<SLOT theFragServerNotifyAstReplacedSequenceDopart:DoPart>>
            #);
          notifyListElementsDeleted:: 
            (# 
            <<SLOT theFragServerNotifyListElementsDeletedDopart:DoPart>>
            #);
          notifyListElementsReplaced:: 
            (# 
            <<SLOT theFragServerNotifyListElementsReplacedDopart:DoPart>>
            #);
          changed::
            (#  <<SLOT theFragServerChanged:DoPart>> #);
          
       #);
     pretty: @MPS.AST.prettyPrinter;
     theGraphicalEditorEnv:
       @graphicalEditorEnv
       (#
          commands:: 
            (#
               dynamicCompilationAvailable:: 
                 (# 
                 do THIS(friggApplication).dynamicCompilationAvailable->value; 
                 #);
               evaluateFile:: 
                 (# 
                 do
                    (name[],theOrigin[])->THIS(friggApplication).evaluateFile
                      ->P##;
                    
                 #);
               createSourceFile:: 
                 (# 
                 do
                    group.name->ymerBrowser.appendProject;
                    ymerBrowser.appendDone
                 #);
               ;
               openAstEditor:: 
                 (# 
                 <<SLOT theGraphicalEditorEnvOpenAstEditorDopart:DoPart>>
                 #);
               autoSaveGroup:: 
                 (# 
                 <<SLOT theGraphicalEditorEnvAutoSaveGroupDopart:DoPart>>
                 #);
               saveFragmentGroup:: 
                 (# 
                 <<SLOT theGraphicalEditorEnvSaveFragmentGroupDopart:DoPart>>
                 #);
               quitApplication:: 
                 (#  <<SLOT friggApplicationQuitApplication:DoPart>> #);
               prettyPrintGroup::
                 (#  <<SLOT prettyPrintGroup:DoPart>> #)
            #);
          onGroupOpen:: 
            (# 
            <<SLOT theGraphicalEditorEnvOnGroupOpenDopart:DoPart>>
            #);
          onGroupChanged:: 
            (#  <<SLOT theGraphicalEditorEnvOnGroupChangedDopart:DoPart>> #);
          onGroupSaved:: 
            (#  <<SLOT theGraphicalEditorEnvOnGroupSavedDopart:DoPart>> #);
          init::< 
            (# 
            do insertStandardSuite;
               INNER;
            #);
          
       #);
     ymerBrowserWindow::< 
       (#
          browserTitle::<
            (# 
            do 'Frigg: Mjolner BETA Browser and Interfacebuilder'->title;
               INNER ;
            #);
          (*
           * Menubar
           *)
          menubarType::<
            (#
               ibMenu: @builderMenu
                 (#
                    newWindowItem: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus::  (#  do true->value #);
                              onSelect:: 
                                (#  <<SLOT newWindowItemOnSelect:DoPart>> #);
                              
                           #);
                         open::  (#  do 'New Window'->name;  #);
                         
                      #);
                    newWindowLibItem: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# 
                                <<SLOT ibMenuNewWindowLibItemOnStatus:DoPart>>
                                #);
                              onSelect:: 
                                (# 
                                <<SLOT ibMenuNewWindowLibItemOnSelect:DoPart>>
                                #);
                              
                           #);
                         open::  (#  do 'New Window library...'->name;  #);
                         
                      #);
                    newCanvasLibItem: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# 
                                <<SLOT ibMenuNewCanvasLibItemOnStatus:DoPart>>
                                #);
                              onSelect:: 
                                (# 
                                <<SLOT ibMenuNewCanvasLibItemOnSelect:DoPart>>
                                #);
                              
                           #);
                         open::  (#  do 'New Canvas library...'->name;  #);
                         
                      #);
                    openGraphicalEditor: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# name: ^text; className: ^text; 
                                do
                                   false->value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs[] <> none then
                                           (if browser.cfe.cs.node[] <> none
                                            then
                                               (if browser.cfe.cs.node.symbol
                                                // MPS.betacfl.patternDecl
                                                // MPS.betacfl.simpleDecl then
                                                   browser.cfe.cs.node[]
                                                     ->
                                                       theGraphicalEditorEnv.
                                                         getClassName
                                                     ->className[];
                                                   (if className[] = none then
                                                       false->value;
                                                       'Edit object'
                                                         ->
                                                           THIS(builderItem).
                                                             name;
                                                       
                                                    else
                                                       true->value;
                                                       'Edit '->name[];
                                                       browser.cfe.cs.node[]
                                                         ->MPS.betacfl.getName
                                                         ->name.append;
                                                       name[]
                                                         ->
                                                           THIS(builderItem).
                                                             name;
                                                       
                                                   if)
                                               if);
                                               
                                           if);
                                           
                                       if);
                                       
                                   if);
                                   
                                #);
                              onSelect:: 
                                (#
                                   group: ^astInterface.fragmentGroup;
                                   result: @boolean;
                                   
                                do
                                   browser.cfe.cs.node.frag.father->group[];
                                   browser.cfe.cs.node[]->editWindow;
                                   
                                #);
                              
                           #);
                         open::  (#  do 'Edit object'->name;  #);
                         
                      #);
                    newGraphicalEditor: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (#
                                   frag: ^astInterface.fragmentForm;
                                   fragName: ^text;
                                   
                                do
                                   false->value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs.node[] <> none then
                                           browser.cfe.cs.node.frag[]->frag[];
                                           (if frag[] <> none then
                                               (if frag.category =
                                               mps.betaCfl.attributesForm then
                                                   frag.name->fragName[];
                                                   (if true
                                                    //
                                                    'guienvLib'
                                                      ->fragName.equalNCS then
                                                       'Create window...'->name;
                                                       true->value;
                                                       
                                                    //
                                                    'windowLib'
                                                      ->fragName.equalNCS then
                                                       'Create canvas...'->name;
                                                       true->value;
                                                       
                                                    else
                                                       'Create...'->name;
                                                       false->value;
                                                       
                                                   if);
                                                   
                                               if);
                                               
                                           if);
                                           
                                       if);
                                       
                                   if);
                                   
                                #);
                              onSelect:: 
                                (#
                                   frag: ^astInterface.fragmentForm;
                                   result: @boolean;
                                   fragName,className: ^text;
                                   
                                <<SLOT newGraphicalEditorOnSelect:DoPart>>
                                #);
                              
                           #);
                         open:: 
                           (# 
                           do
                              'Create...'->name;
                              
                           #);
                         
                      #);
                    addToPalette: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# 
                                do
                                   false->value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs.node[] <> none then
                                           (if browser.cfe.cs.node.symbol =
                                           theGraphicalEditorEnv.gram.
                                             patternDecl then
                                               true->value; 
                                           if);
                                           
                                       if);
                                       
                                   if);
                                   
                                #);
                              onSelect:: 
                                (# 
                                do
                                   browser.cfe.cs.node[]
                                     ->
                                       theGraphicalEditorEnv.
                                         addPatternToPalette;
                                   
                                #)
                           #);
                         open::  (#  do 'Add to Palette'->name;  #);
                         
                      #);
                    showOverViewItem: @builderItem
                      (#
                         eventHandler:: 
                           (#
                              onStatus:: 
                                (# 
                                do
                                   false->value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs.node[] <> none then
                                           (if browser.cfe.cs.node.symbol =
                                           theGraphicalEditorEnv.gram.
                                             patternDecl then
                                               true->value; 
                                           if);
                                           
                                       if);
                                       
                                   if);
                                   
                                #);
                              onSelect:: 
                                (# theOverView: ^overView; 
                                do
                                   &overView[]->theOverView[];
                                   browser.cfe.cs.node[]->theOverView.open;
                                   ;
                                   
                                #)
                           #);
                         open::  (#  do 'Show Overview'->name;  #);
                         
                      #);
                    open:: 
                      (# 
                      do
                         'Interfacebuilder'->name;
                         newWindowLibItem.open;
                         newCanvasLibItem.open;
                         addSeparator;
                         openGraphicalEditor.open;
                         newGraphicalEditor.open;
                         addSeparator;
                         addToPalette.open;
                         showOverViewItem.open;
                         
                      #);
                    
                 #);
               open::< (#  do ibMenu.open; ibMenu[]->append; INNER #);
               
            #);
          (* End of Menubar *)
          open::<
            (# width,height: @integer; 
            do setFriggIcon; size->(width,height); (650,500)->size; INNER ; 
            #);
          (*
           * Handling of windows
           *)
          findPrivateSlotName:
            (#
               slotName: ^text;
               node: ^astInterface.beta.attributeDecl;
               failure:< (# msg: ^text;  enter msg[] do INNER ;  #);
               betaGram: ^astInterface.beta;
               
            enter node[]
            do
                 (#
                    theAst: ^astInterface.AST;
                    theSimpleDecl: ^astInterface.beta.simpleDecl;
                    theReferenceSpecification:
                      ^astInterface.beta.ReferenceSpecification;
                    theStaticItem: ^astInterface.beta.staticItem;
                    theUnExpanded: ^astInterface.unExpanded;
                    theSlotDesc: ^astInterface.slotDesc;
                    
                 do
                    MPS.betacfl[]->betaGram[];
                    (node[],'private')->betaGram.findAttribute->theAst[];
                    (if theAst[] <> none then
                        (if theAst.symbol = betaGram.simpleDecl then
                            theAst[]->theSimpleDecl[];
                            theSimpleDecl.getReferenceSpecification
                              ->theReferenceSpecification[];
                            (if theReferenceSpecification.symbol =
                            betagram.staticItem then
                                theReferenceSpecification[]->theStaticItem[];
                                theStaticItem.getObjectSpecification->theAst[];
                                (if theAst.kind = MPS.AST.kinds.unExpanded then
                                    theAst[]->theUnExpanded[];
                                    (if theUnExpanded.isSlot then
                                        theUnExpanded.theSlot->theSlotDesc[];
                                        theSlotDesc.name->slotName[];
                                        
                                     else
                                        'The private decl lacks a SLOT'
                                          ->failure;
                                        
                                    if);
                                    
                                 else
                                    'The private decl lacks a SLOT'->failure; 
                                if);
                                
                             else
                                'The private decl is not a static item'
                                  ->failure;
                                
                            if);
                            
                         else
                            'The private decl is not a static item'->failure; 
                        if);
                        
                     else
                        'There is no private decl'->failure; 
                    if);
                    
                 #);
               
            exit slotName[]
            #);
          editWindow:
            (#
               theAttributeDecl: ^astInterface.ast;
               theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
               
            enter theAttributeDecl[]
            do
               theAttributeDecl[]->theGraphicalEditorEnv.find
                 ->theGraphicalEditor[];
               (if theGraphicalEditor[] = none then
                   &theGraphicalEditorEnv.graphicalEditor[]
                     ->theGraphicalEditor[];
                   theGraphicalEditor.open;
                   theAttributeDecl[]->theGraphicalEditor.init;
                   
                else
                   theGraphicalEditor.bringToFront; 
               if);
               
            #);
          createSourceFile1:
            (# fragName: ^text; 
            enter fragName[]
            <<SLOT createSourceFile1:DoPart>>
            #);
          
       #);
     init::< 
       (# arg: ^text; 
       do
          ymerBrowser.initCompiler;
          theFragServer.init;
          MPS.betaCfl[]->pretty.init;
          pretty[]->objectPool.put;
          (theFragServer[],MPS.AST[],
           MPS.betaCfl[],pretty[])
            ->theGraphicalEditorEnv.init;
          (if noOfarguments > 1 then
              scanArguments:
              (for i: noOfArguments-1 repeat
                i+1->arguments->arg[];
                arg[]
                  ->ymerbrowser.appendProject
              for);
              (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
              
          if);
          INNER ;
          
       #);
     
  do
     'Frigg - Mjolner BETA Interfacebuilder version 1.6(0)'->putLine;
     '        using Mjolner BETA Browser and Editor version '->putText;
     editorVersion->putLine;
     '        integrated with the BETA Compiler version '->putText;
     compilerVersion->putLine;
     INNER ;
     
  #)  

