ORIGIN '~beta/editor/v5.1/ymersiflib';

INCLUDE '~beta/editor/v5.1/private/editorenvbody';
INCLUDE '~beta/sourcebrowser/v1.1/private/ymerBody';

INCLUDE 'graphicaleditor/graphicaleditor';
INCLUDE '~beta/guienv/v1.4/utils/getfile';
INCLUDE 'mpsstuff/mpsutils';
INCLUDE 'dialogs/promptfortextdialog';
INCLUDE 'code/generate';
INCLUDE 'code/pattern';
INCLUDE 'code/followsemanticlink';
INCLUDE 'resourcesupport/parameters';
INCLUDE 'dialogs/createclassdialog';
INCLUDE 'guienvstuff/getpreferencefile';
INCLUDE 'prettyprinter/prettyprinter';
INCLUDE 'utils/catchallfile';
INCLUDE 'buildermenu';
INCLUDE 'dialogs/applicationdialog';
INCLUDE 'classes/classes';

INCLUDE 'seticon';

BODY 'private/friggapplicationbody';


-- lib: attributes --
friggApplication: fragmentBrowser
  (# <<SLOT friggApplicationLib: attributes>>;
     
     isBETAsif:: (# do true -> value #);
     
     (* 
      * Handling of notifications from Sif
      *)
     
     
     onGroupSaved::
       (# <<SLOT friggApplicationOnGroupSaved: doPart>> #);
     onGroupNotSaved::
       (# <<SLOT friggApplicationOnGroupNotSaved: doPart>> #);
     onGroupClosed::
       (# <<SLOT friggApplicationOnGroupClosed: doPart>> #);
     
   
     
     theFragServer: @fragmentServer
       (# <<SLOT theFragServerLib: attributes>>;
          subscribe::
            (#
            <<SLOT theFragServerSubscribeDopart: dopart>>
            #);
          
          notifyAstReplaced::
            (# 
            <<SLOT theFragServerNotifyAstReplacedDopart: dopart>>
            #);
          notifyListElementInserted::
            (#
            <<SLOT theFragServerNotifyListElementInsertedDopart: dopart>>
            #);
          notifyAstReplacedSequence::
            (# 
            <<SLOT theFragServerNotifyAstReplacedSequenceDopart: dopart>>
            #);
          notifyListElementsDeleted::
            (# 
            <<SLOT theFragServerNotifyListElementsDeletedDopart: dopart>>
            #);
          notifyListElementsReplaced::
            (# 
            <<SLOT theFragServerNotifyListElementsReplacedDopart: dopart>>
            #);
       #);
     
     
     pretty: @MPS.AST.prettyPrinter;
     
     
     theGraphicalEditorEnv: @graphicalEditorEnv
       (# save::
            (# 
            <<SLOT theGraphicalEditorEnvSaveDopart: dopart>>
            #);
          openAstEditor::
            (# 
            <<SLOT theGraphicalEditorEnvOpenAstEditorDopart: dopart>>
            #);
          autoSaveGroup::
            (# 
            <<SLOT theGraphicalEditorEnvAutoSaveGroupDopart: dopart>>  
            #);
          onGroupOpen::
            (#
            <<SLOT theGraphicalEditorEnvOnGroupOpenDopart: dopart>>
            #);
          saveFragmentGroup::
            (#
            <<SLOT theGraphicalEditorEnvSaveFragmentGroupDopart: dopart>>
            #);
          onGroupChanged::
            (#
            <<SLOT theGraphicalEditorEnvOnGroupChangedDopart: dopart>>
            #);
          onGroupSaved::
            (#
            <<SLOT theGraphicalEditorEnvOnGroupSavedDopart: dopart>>
            #);
       #);
     
     
     ymerBrowserWindow::
       (# browserTitle::
            (# 
            do 'Frigg: Mjolner BETA Browser and Interfacebuilder' -> title;
            #);
                    
          (*
           * Menubar
           *)
          
          menubarType::
            (# groupFileMenuType::
                 (#  open::
                      (# 
                      do
                         'New...'->iNew.new;
                         'New BETA Program...'->iNewBetaProgram.new;
                         'New BETA Library...'->iNewBetaLibrary.new;
                         newSeparator;
                         'Close'->iClose.new;
                         'Save'->iSave.new;
                         (* 'Save As...'->iSaveAs.new; *)
                         'Save All...'->iSaveAll.new;
                         'Save Abstract...'->iSaveAbstract.new;
                         newSeparator;
                         'Recover'->iRecover.new;
                         'Revert'->iRevert.new;
                         newSeparator;
                         'Group Write Protection'->iSetIsReadOnly.new;
                         'Global Write Protection'
                           ->iSetGlobalWriteProtection.new
                      #);
                    
                 #);
               groupPerformMenuType::
                 (#                                         
                    open::< 
                      (# 
                      do
                         'Check Current'->iCheck.new;
                         'Recheck'->iReCheck.new;
                         'Compile Current'->iCompile.new;
                         'Recompile'->iRecompile.new;
                         'Execute Current'->iExecute.new;
                         'Reexecute'->iReExecute.new;
                         newSeparator;
                         'Debug'->iDebug.new;
                         newSeparator;
                         'Semantic Errors...'->iSemanticErrors.new;
                         newSeparator;
                         'Show Diagram'->iShowDiagram.new;
                         INNER
                      #)
                 #);
               ibMenu: @builderMenu
                 (# newWindowLibItem: @builderItem
                      (# eventHandler::
                           (# onStatus::
                                (# 
                                do true -> value;
                                #);
                              onSelect::
                                (# 
                                do 'guienvLib' -> createSourceFile;
                                #);
                           #);
                         open::
                           (# 
                           do 'New Window library...' -> name;
                           #);
                      #);
                    
                    newCanvasLibItem: @builderItem
                      (# eventHandler::
                           (# onStatus::
                                (# 
                                do true -> value;
                                #);
                              onSelect::
                                (# 
                                do 'windowLib' -> createSourceFile;
                                #);
                           #);
                         open::
                           (# 
                           do 'New Canvas library...' -> name;
                           #);
                      #);
                    
                    
                    openGraphicalEditor: @builderItem
                      (# eventHandler::
                           (# onStatus::
                                (# name: ^text;
                                   theClassData: ^graphicalEditorEnv.classData;
                                do false -> value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs[] <> none then
                                           (if browser.cfe.cs.node[] <> none then
                                               (if browser.cfe.cs.node.symbol
                                                //MPS.betacfl.patternDecl
                                                //MPS.betacfl.simpleDecl then
                                                   (browser.cfe.cs.node[], none) -> theGraphicalEditorEnv.getClassData -> theClassData[];
                                                   (if theClassData[] = NONE then
                                                       false -> value;
                                                       'Edit object' -> this(builderItem).name;
                                                    else
                                                       true -> value;
                                                       'Edit ' -> name[];
                                                       browser.cfe.cs.node[] -> MPS.betacfl.getName -> name.append;
                                                       name[] -> this(builderItem).name;
                                                   if)
                                               if);
                                           if);
                                       if);
                                   if);
                                #);
                              onSelect::
                                (# group: ^astInterface.fragmentGroup;
                                   result: @boolean;
                                do browser.cfe.cs.node.frag.father -> group[];
                                   browser.cfe.cs.node[] -> editWindow;
                                #);
                           #);
                         open::
                           (# 
                           do 'Edit object' -> name;
                           #);
                      #);
                    newGraphicalEditor: @builderItem
                      (# eventHandler::
                           (# onStatus::
                                (# frag: ^astInterface.fragmentForm;
                                   fragName: ^text;
                                do false -> value;
                                   (if browser.cfe[] <> none then
                                       (if browser.cfe.cs.node[] <> none then
                                           browser.cfe.cs.node.frag[] -> frag[];
                                           (if frag[] <> None then
                                               (if frag.category = mps.betaCfl.attributesForm then
                                                   frag.name -> fragName[];
                                                   (if true
                                                    //'guienvLib' -> fragName.equalNCS then
                                                       'Create window...' -> name;
                                                       true -> value;
                                                    //'windowLib' -> fragName.equalNCS then
                                                       'Create canvas...' -> name;
                                                       true -> value;
                                                    else
                                                       'Create...' -> name;
                                                       false -> value;
                                                   if);
                                               if);
                                           if);
                                       if);
                                   if);        
                                #);
                              onSelect::
                                (# frag: ^astInterface.fragmentForm;
                                   result: @boolean;
                                   fragName, className: ^text;
                                do (if browser.cfe[] <> none then
                                       (if browser.cfe.cs.node[] <> none then
                                           browser.cfe.cs.node.frag[] -> frag[];
                                           (if frag[] <> None then
                                               (if frag.category = mps.betaCfl.attributesForm then
                                                   frag.name -> fragName[];
                                                   (if true
                                                    //'guienvLib' -> fragName.equalNCS then
                                                       'window' -> className[];
                                                    //'windowLib' -> fragName.equalNCS then
                                                       'canvas' -> className[];
                                                   if);
                                                   (frag[], className[]) -> createWindow;
                                               if);
                                           if);
                                       if);
                                   if);
                                #);
                           #);
                         open::
                           (# 
                           do 'Create...' -> name;
                           #);
                      #);
                    
                    open::
                      (# 
                      do 'Interfacebuilder' -> name;
                         
                         newWindowLibItem.open;
                         newCanvasLibItem.open;
                         addSeparator;
                         openGraphicalEditor.open;
                         newGraphicalEditor.open;
                      #);
                 #);
               open::
                 (# 
                 do SLOTs.new;
                    SLOTs[]->append;
                    'Fragment'->GroupEdit.new;
                    'Tools'->GroupPerform.new;
                    ibMenu.open;
                    ibMenu[] -> append;
                 #);
            #);
          
          (* End of Menubar *)
          
          (*
           * Event handling
           *)
          
          eventHandler::
            (# 
            #);
          
          open::
            (# width, height: @integer;
            do setFriggIcon;
               size -> (width, height);
               (650, 500) -> size;
            #);
          
          (*
           * Handling of windows
           *)
          
          saveAll:
            (# putBool:
                 (# b: @boolean;
                 enter b
                 do (if b then
                        'true' -> putText;
                     else
                        'false' -> putText;
                    if);
                 #);
            do theGraphicalEditorEnv.scanDocuments
               (# 
               do current.save;
               #);
            #);
          
          saveGroup:
            (# fg: ^astInterface.fragmentGroup;
            enter fg[]
            do saving:
                 (# 
                    theFile: @catchAllFile
                      (# failure::
                           (# 
                           do msg[] -> putLine;
                              leave saving;
                           #);
                      #);
                    fileName: ^text;
                 do (fg.name).copy -> fileName[];
                    '.bet' -> fileName.append;
                    fileName[] -> theFile.name;
                    theFile.openWrite;
                    (mps.ast[], fg[], theFile[], none) -> prettyPrintFragment;
                    theFile.close;
                    fg.fragmentList.scan
                    (# frag: ^astInterface.fragmentForm;
                    do (if current.type = MPS.AST.formType then
                           current.open -> frag[];
                           frag.recomputeSlotChain;
                       if);
                    #);
                    'doneCheck' -> fg.prop.deleteProp;
                    fg.markAsChanged;
                 #);
            #);
          
          findPrivateSlotName:
            (# slotName: ^text;
               node: ^astInterface.beta.attributeDecl;
               failure:<
                 (# msg: ^text;
                 enter msg[]
                 do inner;
                 #);
               betaGram: ^astInterface.beta;
            enter node[]
            do (# theAst: ^astInterface.AST;
                  theSimpleDecl: ^astInterface.beta.simpleDecl;
                  theReferenceSpecification: ^astInterface.beta.ReferenceSpecification;
                  theStaticItem: ^astInterface.beta.staticItem;
                  theUnExpanded: ^astInterface.unExpanded;
                  theSlotDesc: ^astInterface.slotDesc;
               do MPS.betacfl[] -> betaGram[];
                  (node[], 'private') -> betaGram.findAttribute -> theAst[];
                  (if theAst[] <> none then
                      (if theAst.symbol = betaGram.simpleDecl then
                          theAst[] -> theSimpleDecl[];
                          theSimpleDecl.getReferenceSpecification -> theReferenceSpecification[];
                          (if theReferenceSpecification.symbol = betagram.staticItem then
                              theReferenceSpecification[] -> theStaticItem[];
                              theStaticItem.getObjectSpecification -> theAst[];
                              (if theAst.kind = MPS.AST.kinds.unExpanded then
                                  theAst[] -> theUnExpanded[];
                                  (if theUnExpanded.isSlot then
                                      theUnExpanded.theSlot -> theSlotDesc[];
                                      theSlotDesc.name -> slotName[];
                                   else
                                      'The private decl lacks a SLOT' -> failure;
                                  if);
                               else
                                  'The private decl lacks a SLOT' -> failure;
                              if);
                           else
                              'The private decl is not a static item' -> failure;
                          if);
                       else
                          'The private decl is not a static item' -> failure;
                      if);
                   else
                      'There is no private decl' -> failure;
                  if);
               #);
            exit slotName[]
            #);
          editWindow:
            (# theAttributeDecl: ^astInterface.ast;
               theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
               fg: ^astInterface.fragmentGroup;
            enter theAttributeDecl[]
            do l: (# 
                  do
                     theAttributeDecl.frag.father -> fg[];
                     
                     theAttributeDecl[] -> theGraphicalEditorEnv.find -> theGraphicalEditor[];
                     l: (if theGraphicalEditor[] = none then
                            (# slotName: ^text;
                               body: ^astInterface.fragmentGroup;
                               frag: ^astInterface.fragmentForm;
                               help: ^text;
                            do theAttributeDecl[] -> MPS.betaCfl.getPrivateFragment -> frag[];
                               
                               &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                               theGraphicalEditor.open;
                               (theAttributeDecl[], frag[]) -> theGraphicalEditor.init;
                               
                            #);
                         else
                            theGraphicalEditor.bringToFront;
                        if);
                  #);
            #);
          
          createWindow:
            (# frag: ^astInterface.fragmentForm;
               className: ^text;
            enter (frag[], className[])
            do l:
                 (# theAttributesForm: ^astInterface.beta.attributesForm;
                    theAttributes: ^astInterface.beta.attributes;
                    theAttribute: ^astInterface.beta.attributeDecl;
                    name: ^text;
                    theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
                    fg: ^astInterface.fragmentGroup;
                    classes: ^patternList;
                    classNames: @textList;
                 do classNames.init;
                    className[] -> classNames.append;
                    
                    frag.father -> fg[];
                    (MPS.AST[], className[], fg[]) -> getClasses -> classes[];
                    classes.scan
                    (# 
                    do current.node[] -> MPS.betaCFL.getName -> classNames.append;
                    #);
                    frag.root[] -> theAttributesForm[];
                    theAttributesForm.getAttributes -> theAttributes[];
                    ('', classNames[]) -> userCreateClass
                    (# createClass::
                         (# isIdentifier: @boolean;
                         do name[] -> theGraphicalEditorEnv.checkIdentifier -> isIdentifier;
                            (if isIdentifier then
                                (theAttributes[], name[]) ->mps.betacfl.find -> theAttribute[];
                                (if theAttribute[] <> none then
                                    (this(ymerBrowserWindow)[],
                                    'The window could not be created because it already exists', 
                                    'Alert') -> noteUser;
                                 else
                                    (# son1: ^mps.ast.ast;
                                       doReplace: @boolean;
                                       super: ^astInterface.beta.patternDecl;
                                    do
                                       (frag[], name[], className[]) -> mps.betacfl.newPatternAttribute -> theAttribute[];
                                       className[] -> classes.findByName -> super[];
                                       (if super[] <> none then
                                           (theAttribute[], super[]) -> setSemanticLink;
                                       if);
                                       (if theAttributes.noOfsons = 1 then
                                           1 -> theAttributes.get -> son1[];
                                           (if son1.kind = mps.ast.kinds.optional then
                                               true -> doReplace;
                                           if);
                                       if);
                                       (if doReplace then
                                           (1, theAttribute[]) -> theAttributes.put;
                                        else
                                           theAttribute[] -> theAttributes.append;
                                       if);
                                       &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                                       theGraphicalEditor.open;
                                       theAttribute[] -> theGraphicalEditor.new;
                                       (if doReplace then
                                           (true, frag[], son1[], theAttribute[])
                                             -> theFragServer.notifyAstReplaced; 
                                        else
                                           (true, frag[], theAttributes[], theAttributes.noOfSons - 1) 
                                             -> theFragServer.notifyListElementInserted;
                                       if);
                                    #);
                                if);
                             else
                                'That is not a valid beta name' -> displayMessage;
                            if);
                         #);
                    #);
                 #);
            #);
          createSourceFile:
            (# 
               fileName: ^text;
               file1, file2: ^text;
               e: @diskEntry;
               fragName: ^text;
            enter fragName[]
            do (* 'File name: ' -> createFile -> fileName[];
               l:
                 (if fileName[] <> none then
                     fileName[] -> stripExtension;
                     fileName[] -> e.path;
                if);
                *)
            #);
       #);
     init::
       (# arg: ^text;
       do ymerBrowser.initCompiler;
          ('Sif: Known Bugs and Inconveniences',
          '~beta/editor/v5.1/doc/BUGS')-> helpDocument;
          ('Frigg FAQ',
          '~beta/interfacebuilder/v1.5/doc/FAQ') -> helpDocument;
          (* ('mjolner', 'Interfacebuilder') -> getPreferencesSection  -> prefs[]; *)
          theFragServer.init;
          (theFragServer[], MPS.AST[], MPS.betaCfl[], pretty[]) -> theGraphicalEditorEnv.init;
          MPS.betaCfl[] -> pretty.init;
          pretty[] -> objectPool.put;
          
          (if noOfarguments > 1 then
              scanArguments:
                (for i: noOfArguments-1 repeat
                     i+1->arguments->arg[];
                     arg[]->ymerbrowser.appendProject
                for);
              (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
              
          if)
       #);
  do  'Frigg - Mjolner BETA Interfacebuilder version 1.5(2)'->putLine;
     '        using Mjolner BETA Browser and Editor version '->putText;
     editorVersion->putLine;
     '        integrated with the BETA Compiler version '->putText;
     compilerVersion->putLine  
  #)
