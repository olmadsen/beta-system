ORIGIN '~beta/basiclib/v1.4/betaenv';
BODY 'private/objectdatabody';

-- lib: attributes --

objectData:
  (* Superpattern for a set of dataobjects describing the 
   * graphical attributes of interface objects
   *)
  (# type:
       (* An integer constant specifying the kind of
        * interface object this(objectData) is describing
        *)
       @integer;
     ID:
       (* An unique ID for this(objectData) *)
       @integer;
     read:<
       (# theStream: ^stream;
          ignoreAtom: 
            (# a: ^text;
            do getAtom -> a[];
            #);
          getInt: @
            (# value: @integer;
            do l: theStream.getInt
               (# 
                  syntaxError::<
                    (# 
                    do -1 -> ID;
                       leave l;
                    #);
               #) -> value;
            exit value
            #);
          getAtom: ^theStream.getAtom;
       enter theStream[]
       do &theStream.getAtom[] -> getAtom[];
          inner;
       #);
     write:<
       (# theStream: ^stream;
          putInt: ^theStream.putInt;
          putText: ^theStream.putText;
          newLine: ^theStream.newLine;
       enter theStream[]
       do &theStream.putInt[] -> putInt[];
          &theStream.putText[] -> putText[];
          &theStream.newLine[] -> newLine[];
          inner;
       #);
     copy:<
       (# theCopy: ^scrapData;
       do &scrapData[] -> theCopy[];
          type -> theCopy.type;
          inner;
       exit theCopy[]
       #);
     set:<
       (# theCopy: ^scrapData;
       enter theCopy[]
       do (if theCopy.type = type then
              inner;
           else
              exception
              (# 
              do 'The scrap data has wrong type' -> msg.append;
              #);
          if);
       #);
  #);
scrapData:
  (# type: @integer;
     position: @pointData;
     size: @sizeData;
     bindLeft, bindRight, bindTop, bindBottom: @boolean;
     visible, hilite, enabled: @boolean;
     borderStyle: @integer;
     borderVisible: @boolean;
     cursor: ^text;
     
     style: @integer;
     vertical: @boolean;
     
     label: ^text;
     textStyle: ^textStyleData;
     
     state: @boolean;
     
     scrollAmount, pageScrollAmount, maxValue, value: @integer;
     
     leftMargin,rightMargin: @integer;
     
     numberOfItems: @integer;
     singleSelection: @boolean;
  #);

objectDataStore:
  (# <<SLOT objectDataStoreLib: attributes>>;
     init:
       (# <<SLOT IBobjectDataStoreInit: doPart>> #);
     read:
       (# theStream: ^stream;
          failure:< notification;
       enter theStream[]
       <<SLOT IBobjectDataStoreRead: doPart>>
       #);
     write:
       (# theStream: ^stream;
          failure:< notification;
       enter theStream[]
       <<SLOT IBobjectDataStoreWrite: doPart>>
       #);
     get:
       (# ID: @integer;
          theObjectData: ^objectData;
          failure:< notification;
       enter ID
       <<SLOT IBobjectDataStoreGet: doPart>>
       exit theObjectData[]
       #);
     new:
       (# type: @integer;
          theObjectData: ^objectData;
          failure:< notification;
       enter type
       <<SLOT IBobjectDataStoreNew: doPart>>
       exit theObjectData[]
       #);
     private: @<<SLOT IBobjectDataStorePrivate: descriptor>>;
  #);

(* There is a specialization hierarki of objectData 
 * corresponding to the interfaceobject hierarki.
 *)


textStyleData: 
  (# name: ^text;
     size: @boolean;
     face: @integer;
  #);
pointData:
  (# h,v: @integer;
  enter (h,v)
  exit (h,v)
  #);
sizeData:
  (# width,height: @integer;
  enter (width,height)
  exit (width,height)
  #);


interfaceObjectData: objectData
  (# read::<
       (# 
          getTextStyle:
            (# style: ^textStyleData;
            do &textStyleData[] -> style[];
               getAtom -> style.name[];
               getInt -> style.size;
               getInt -> style.face;
            exit style[]
            #);
          getPoint:
            (# h, v: @integer;
            do getInt -> h;
               getInt -> v;
            exit (h,v)
            #);
          getString:
            (# string: ^text;
            <<SLOT interfaceObjectDataGetString: doPart>>
            exit string[]
            #);
       do inner;
       #);
     write::<
       (# 
          putTextStyle:
            (# style: ^textStyleData;
            enter style[]
            do style.name[] -> putText;
               ' ' -> putText;
               style.size -> putInt;
               ' ' -> putText;
               style.face -> putInt;
            #);
          putPoint:
            (# h, v: @integer;
            enter (h, v)
            do h -> putInt;
               ' ' -> putText;
               v -> putInt;
            #);
          putString:
            (# string: ^text;
            enter string[]
            <<SLOT InterfaceObjectDataPutString: doPart>>
            #);
       do inner;
       #);
  #);
windowitemData: interfaceObjectData
  (# position: @pointData;
     size: @sizeData;
     bindLeft, bindRight, bindTop, bindBottom: @boolean;
     visible, hilite, enabled: @boolean;
     borderStyle: @integer;
     borderVisible: @boolean;
     cursor: ^text;
     read::<
       (# 
       <<SLOT windowitemDataRead: doPart>> 
       #);
     write::<
       (# 
       <<SLOT windowitemDataWrite: doPart>> 
       #);
     copy::<
       (# 
       do position -> theCopy.position;
          bindLeft -> theCopy.bindLeft;
          size -> theCopy.size;
          bindRight -> theCopy.bindRight;
          bindTop -> theCopy.bindTop;
          bindLeft -> theCopy.bindLeft;
          bindBottom -> theCopy.bindBottom;
          visible -> theCopy.visible;
          hilite -> theCopy.hilite;
          enabled -> theCopy.enabled;
          borderStyle -> theCopy.borderStyle;
          borderVisible -> theCopy.borderVisible;
          cursor[] -> theCopy.cursor[];
          inner;
       #);
     set::<
       (# 
       do theCopy.position -> position;
          theCopy.bindLeft -> bindLeft;
          theCopy.size -> size;
          theCopy.bindRight -> bindRight;
          theCopy.bindTop -> bindTop;
          theCopy.bindLeft -> bindLeft;
          theCopy.bindBottom -> bindBottom;
          theCopy.visible -> visible;
          theCopy.hilite -> hilite;
          theCopy.enabled -> enabled;
          theCopy.borderStyle -> borderStyle;
          theCopy.borderVisible -> borderVisible;
          theCopy.cursor[] -> cursor[]; 
          inner;
       #);
  #);
separatorData: windowitemData
  (# style: @integer;
     vertical: @boolean;
     read::<
       (# <<SLOT separatorDataRead: doPart>> #);
     write::<
       (# <<SLOT separatorDataWrite: doPart>> #);
     copy::<
       (# 
       do style -> theCopy.style;
          vertical -> theCopy.vertical;
          inner;
       #);
     set::<
       (# 
       do theCopy.style -> style;
          theCopy.vertical -> vertical;
          inner;
       #);
  #);
canvasData: windowitemData
  (# 
     
  #);
abstractScrollerData: canvasData
  (# 
     
  #);
scrollerData: abstractScrollerData
  (# 
     
  #);
textEditorData: abstractScrollerData
  (# 
     
  #);
controlData: windowitemData
  (# 
     
  #);
buttonData: controlData
  (# label: ^text;
     style: ^textStyleData;
     read::<
       (# <<SLOT buttonDataRead: doPart>> #);
     write::<
       (# <<SLOT buttonDataWrite: doPart>> #);
     copy::<
       (# 
       do label.copy -> theCopy.label[];
          style[] -> theCopy.textStyle[];
          inner;
       #);
     set::<
       (# 
       do theCopy.label.copy -> label[];
          theCopy.textStyle[] -> style[]; 
          inner;
       #);
  #);
pushbuttondata: buttonData
  (# 
  #);
iconButtonData: buttonData
  (# 
     theRaster: ^text;
     showLabel: @boolean;
     read::<
       (# <<SLOT iconButtonDataRead: doPart>> #);
     write::<
       (# <<SLOT iconButtonDataWrite: doPart>> #);
  #);
optionButtonData: buttonData
  (# 
  #);
staticTextData: buttonData
  (# 
     
  #);
toggleButtonData: buttonData
  (# 
     state: @boolean;
     read::<
       (# <<SLOT toggleButtonDataRead: doPart>> #);
     write::<
       (# <<SLOT toggleButtonDataWrite: doPart>> #);
     copy::<
       (#
       do state -> theCopy.state;
          inner;
       #);
     set::<
       (#
       do theCopy.state -> state;
          inner;
       #);
  #);
radioButtonData: toggleButtonData
  (# 
     
  #);
checkBoxData: toggleButtonData
  (# 
     
  #);
scrollbarData: controlData
  (# 
     vertical: @boolean;
     scrollAmount, pageScrollAmount, maxValue, value: @integer;
     read::<
       (# <<SLOT scrollbarDataRead: doPart>> #);
     write::<
       (# <<SLOT scrollbarDataWrite: doPart>> #);
     copy::<
       (# 
       do vertical -> theCopy.vertical;
          scrollAmount -> theCopy.scrollAmount;
          pageScrollAmount -> theCopy.pageScrollAmount;
          maxValue -> theCopy.maxValue;
          value -> theCopy.value;
          inner;
       #);
     set::<
       (# 
       do theCopy.vertical -> vertical;
          theCopy.scrollAmount -> scrollAmount;
          theCopy.pageScrollAmount -> pageScrollAmount;
          theCopy.maxValue -> maxValue;
          theCopy.value -> value;
          inner;
       #);
  #);
editTextData: controlData
  (# 
     style: ^textStyleData;
     read::<
       (# <<SLOT editTextDataRead: doPart>> #);
     write::<
       (# <<SLOT editTextDataWrite: doPart>> #);
     copy::<
       (# 
       do style[] -> theCopy.textStyle[];
          inner;
       #);
     set::<
       (# 
       do theCopy.textStyle[] -> style[];
          inner;
       #);
  #);
textFieldData: windowitemData
  (# 
     defaultStyle: ^textStyleData;
     leftMargin,rightMargin: @integer;
     read::<
       (# <<SLOT textFieldDataRead: doPart>> #);
     write::<
       (# <<SLOT textFieldDataWrite: doPart>> #);
     copy::<
       (# 
       do defaultStyle[] -> theCopy.textStyle[];
          inner;
       #);
     set::<
       (# 
       do theCopy.textStyle[] -> defaultStyle[];
          inner;
       #);
  #);
scrollListData: windowitemData
  (# 
     numberOfItems: @integer;
     singleSelection: @boolean;
     read::<
       (# <<SLOT scrollListDataRead: doPart>> #);
     write::<
       (# <<SLOT scrollListDataWrite: doPart>> #);
  #);
textScrollListData: scrollListData
  (# 
     style: ^textStyleData;
     read::<
       (# <<SLOT textScrollListDataRead: doPart>> #);
     write::<
       (# <<SLOT textScrollListDataWrite: doPart>> #);
     copy::<
       (# 
       do style[] -> theCopy.textStyle[];
          inner;
       #);
     set::<
       (# 
       do theCopy.textStyle[] -> style[];
          inner;
       #);
  #);
windowData: interfaceObjectData
  (# 
     position: @pointData;
     size: @sizeData;
     floating: @boolean;
     title: ^text;
     visible: @integer;
     maxSize: @sizeData;
     minSize: @sizeData;
     read::<
       (# <<SLOT windowDataRead: doPart>> #);
     write::<
       (# <<SLOT windowDataWrite: doPart>> #);
  #);
menuData: interfaceObjectData
  (# 
     name: ^text;
     read::<
       (# <<SLOT menuDataRead: doPart>> #);
     write::<
       (# <<SLOT menuDataWrite: doPart>> #);
  #);
menubarData: interfaceObjectData
  (# 
     
  #);
menuitemData: interfaceObjectData
  (# key: @char;
     name: ^text;
     checked: @boolean;
     position: @integer;
     read::<
       (# <<SLOT menuitemDataRead: doPart>> #);
     write::<
       (# <<SLOT menuitemDataWrite: doPart>> #);
  #);
dynamicMenuitemData: menuitemData
  (# 
     
  #);


(* Object types follows *)

interfaceObjectType: (# exit 1 #);
windowitemType: (# exit 2 #);
separatorType: (# exit 3 #);
canvasType: (# exit 4 #);
abstractScrollerType: (# exit 5 #);
scrollerType: (# exit 6 #);
textEditorType: (# exit 7 #);
controlType: (# exit 8 #);
buttonType: (# exit 9 #);
pushButtonType: (# exit 10 #);
iconButtonType: (# exit 11 #);
optionButtonType: (# exit 12 #);
staticTextType: (# exit 13 #);
toggleButtonType: (# exit 14 #);
radioButtonType: (# exit 15 #);
checkBoxType: (# exit 16 #);
scrollbarType: (# exit 17 #);
editTextType: (# exit 18 #);
textFieldType: (# exit 19 #);
scrollListType: (# exit 20 #);
textScrollListType: (# exit 21 #);
windowType: (# exit 22 #);
menuType: (# exit 23 #);
menubarType: (# exit 24 #);
menuitemType: (# exit 25 #);
dynamicMenuitemType: (# exit 26 #);

