ORIGIN '../initfromresource';
INCLUDE '../objectdata';
INCLUDE '~beta/basiclib/v1.4/file';
INCLUDE '~beta/guienv/v1.3/controls';
INCLUDE '~beta/sysutils/v1.4/cstring';

MDBODY 
mac 'macintosh/initfromresourcemdbody'
nti 'winnt/initfromresourcemdbody'
default 'X11/initfromresourcemdbody';

-- GUIenvLib: attributes --

getRaster:
  (# name: ^text;
     theRaster: ^raster;
  enter name[]
  <<SLOT GetRasterDoPart: doPart>>
  exit theRaster[]
  #);
getResourceStream:
  (# theStream: ^stream;
  <<SLOT GetResourceStreamDoPart: doPart>>
  exit theStream[]
  #);



GUIserver:
  (* Global object kept in objectpool *)
  (# inited: @boolean;
     store: @objectDataStore;
     init: @
       (# 
       do (if not inited then
              l: (# theStream: ^stream;
                 do store.init;
                    getResourceStream -> theStream[];
                    theStream[] -> store.read;
                    true -> inited;
                 #);
          if);
       #);
  #);

getGUIserver:
  (# server: ^GUIserver;
     failure:< object;
  do objectPool.get 
     (# type:: GUIserver;
        exact:: (# do true -> value #);
        init::
          (# 
          do obj.init;
          #);
     #) -> server[];
     (if server.inited then
         inner;
      else
         failure;
     if);
  #);

-- GUIenvWindowItemInitFromResource: doPart --
do read: getGUIserver
     (# failure::<
          (# 
          do 'failed to initialize the GUIserver' -> putLine;
          #);
        data: ^windowItemData;
        theButtonData: ^buttonData;
        theScrollBarData: ^scrollBarData;
        
        thisButton: ^button;
        thisScrollBar: ^scrollBar;
     do ID -> server.store.get
        (# failure::<
             (# 
             do 'No resource with id: ' -> screen.putText;
                ID -> screen.putInt;
                ' in the GUIsever' -> screen.putLine;
                leave read;
             #);
        #) -> data[];
        data.position -> position;
        data.size -> size;
        data.bindLeft -> bindLeft;
        data.bindRight -> bindRight;
        data.bindTop -> bindTop;
        data.bindBottom -> bindBottom;
        data.visible -> visible;
        data.enabled -> enabled;
        data.borderStyle -> border.style;
        data.borderVisible -> border.visible;
        (if data.type
         //ButtonType
         //PushButtonType
         //ToggleButtonType
         //RadioButtonType
         //CheckBoxType
         //OptionButtonType
         //IconButtonType
         //StaticTextType then
            data[] -> theButtonData[];
            this(windowItem)[] -> thisButton[];
            theButtonData.label[] -> thisButton.label;
         //ScrollBarType then
            data[] -> theScrollBarData[];
            this(windowItem)[] -> thisScrollBar[];
            theScrollBarData.maxValue -> thisScrollBar.maxValue;
            theScrollBarData.value -> thisScrollBar.value;
            theScrollBarData.pageScrollAmount -> thisScrollBar.pageScrollAmount;
            theScrollBarData.scrollAmount -> thisScrollBar.scrollAmount;
        if);
     #);
   
-- GUIenvWindowInitFromResource: doPart --
do read: getGUIserver
     (# failure::<
          (# 
          do 'failed to initialize the GUIserver' -> putLine;
          #);
        data: ^windowItemData;
     do ID -> server.store.get
        (# failure::<
             (# 
             do 'No resource with id: ' -> screen.putText;
                ID -> screen.putInt;
                ' in the GUIsever' -> screen.putLine;
                leave read;
             #);
        #) -> data[];
        data.position -> position;
        data.size -> size;
     #);
