ORIGIN '../getresourcefilebody';
INCLUDE '~beta/basiclib/v1.5/file';
INCLUDE '~beta/sysutils/v1.5/envstring';
INCLUDE '~beta/guienv/v1.4/utils/prompts';
INCLUDE '~beta/guienv/v1.4/utils/getfile';

-- getResourceFileDoPart: doPart --
do (# home: ^text;
      fileName: ^text;
      resourceFileName: ^text;
      f: @file;
   do '$(HOME)' -> expandEnvVar -> home[];
      (if home[] = none then
          exception
          (# 
          do 'the HOME environment variable is not set' -> msg.append;   
          #);
       else
          home.copy -> fileName[];
          '/.resource' -> fileName.append;
          filename[] -> f.name;
          (if f.entry.exists then
              f.openRead;
              f.getAtom -> name[];
              f.close;
           else
              f.openWrite;
              default[] -> f.putLine;
              f.close;
              default[] -> f.name;
              f.touch;
              default.copy -> name[];
          if);
      if);
   #)
   
-- lib: attributes --

catchAllFile: file
  (# 
     failure:<
       (# msg: ^text;
       enter msg[]
       do inner;
       #);
     AccessError::<
       (# 
       do msg[] -> failure;
       #);
     WriteError::<
       (# 
       do msg[] -> failure;
       #);
     ReadError::<
       (# 
       do msg[] -> failure;
       #);
     EOSerror::<
       (# 
       do msg[] -> failure;
       #);
     NoSuchFileError::<
       (# 
       do msg[] -> failure;
       #);
     FileExistsError::<
       (# 
       do msg[] -> failure;
       #);
     NoSpaceError::<
       (# 
       do msg[] -> failure;
       #);
     OtherError::<
       (# 
       do msg[] -> failure;
       #);
     safeGetInt: getInt
       (# syntaxError::
            (# 
            do 'syntax error in getint' -> failure;
            #);
       #);
  #);

getAppName:
  (# appName: ^text;
     slashPos: @integer;
  do 1 -> arguments -> appName[];
     appName.reset;
     '/' -> appName.findAll
      (# 
      do inx -> slashPos;
      #);
     (if (slashPos > 0) and (slashPos < appName.length) then
         (slashPos + 1, appName.length) -> appName.sub -> appName[];
     if);
  exit appName[]
  #);
getHome:
  (# home: ^text;
  do '$(HOME)' -> expandEnvVar -> home[];
  exit home[]
  #);
getPrefFileName:
  (# prefFileName: ^text;
  do getHome -> prefFileName[];
     '/.' -> prefFileName.append;
     getAppName -> prefFileName.append;
     'rc' -> prefFileName.append;
  exit prefFileName[]
  #);



-- interactiveGetResourceFileDoPart: doPart --
do (# prefFile: @catchAllFile;
      version: @integer;
      firstLine: ^text;
      prefFileName: ^text;
   do getPrefFileName -> prefFileName[];
      prefFileName[] -> prefFile.name;
      (if prefFile.entry.exists then
          prefFile.openRead;
          prefFile.getLine -> firstLine[];
          firstLine.reset;
          firstLine.getInt -> version;
          (if version = 1 then
              prefFile.getLine -> resourceFileName[];
           else
              stop;
          if);
       else
          (none, '', 'I can''t find the resource file. Do you want to look for it?') -> promptForBoolean
          (# 
             ok::
               (# 
               do 'res' -> getFile -> resourceFileName[];
                  (if resourceFileName[] = none then
                      stop;
                   else
                      prefFile.openWrite;
                      '1' -> prefFile.putLine;
                      resourceFileName[] -> prefFile.putLine;
                      prefFile.close;
                  if);
               #);
             notOk::
               (# 
               do Stop;
               #);
             cancel::
               (# 
               do Stop;
               #);
          #);
      if);
   #);


