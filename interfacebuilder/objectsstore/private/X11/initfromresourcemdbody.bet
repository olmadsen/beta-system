ORIGIN '../initfromresourcebody';

-- GUIenvLib: attributes --

getResourceDirectory:
  (# resourceDirectory: ^text;
     cstr: @cString;
  do 'RESOURCEDIRECTORY' -> getenv -> cstr;
     (if cstr <> 0 then
         cstr.get -> resourceDirectory[];
     if);
  exit resourceDirectory[]
  #);
catchAllFile: file
  (# 
     failure:<
       (# msg: ^text;
       enter msg[]
       do inner;
       #);
     AccessError::<
       (# 
       do msg[] -> failure;
       #);
     WriteError::<
       (# 
       do msg[] -> failure;
       #);
     ReadError::<
       (# 
       do msg[] -> failure;
       #);
     EOSerror::<
       (# 
       do msg[] -> failure;
       #);
     NoSuchFileError::<
       (# 
       do msg[] -> failure;
       #);
     FileExistsError::<
       (# 
       do msg[] -> failure;
       #);
     NoSpaceError::<
       (# 
       do msg[] -> failure;
       #);
     OtherError::<
       (# 
       do msg[] -> failure;
       #);
  #);

resourceStream: catchAllFile
  (# failure::<
       (#
       do msg[] -> screen.putLine;
       #);
  #);
getenv: external
  (# var: [1] @char;
     val: @integer;
  enter var
  exit val
  #);

-- GetRasterDoPart: doPart --
do
   
-- GetResourceStreamDoPart: doPart --
do (# theFile: ^resourceStream;
      fileName: ^text;
      slashPos: @integer;
      path: ^text;
   do &resourceStream[] -> theFile[];
      1 -> arguments -> fileName[];
      fileName.reset;
      '/' -> fileName.findCh
      (# 
      do inx -> slashPos;
      #);
      (if (slashPos > 0) and (slashPos < fileName.length) then
          (slashPos + 1, fileName.length) -> fileName.sub -> fileName[];
      if);
      '.res' -> fileName.append;
      getResourceDirectory -> path[];
      (if path[] <> none then
          '/' -> fileName.prepend;
          path[] -> fileName.prepend;
      if);
      filename[] -> theFile.name;
      theFile.openRead;
      theFile[] -> theStream[];
   #);
