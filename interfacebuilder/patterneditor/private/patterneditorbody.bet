ORIGIN '../patterneditor';
INCLUDE '~beta/guienv/v1.3.1/utils/tableview';
INCLUDE '~beta/guienv/v1.3.1/utils/loadraster';
INCLUDE '../../mpsstuff/mpsutils';
INCLUDE '~beta/betaast/v4.9.1/gram';
INCLUDE '../../dialogs/promptfortextdialog';
INCLUDE '../../code/generate';
INCLUDE  '~beta/guienv/v1.3.1/fields';


-- lib: attributes --

attributeEditorTextValues: promptForTextValues 
  (# init:
       (# 
       do 'Enter attribute decl' -> prompt[];
       #);
  #);

getTextvalues: objectPool.get
  (# type:: attributeEditorTextValues;
     exact:: (# do true -> value #);
     init::
       (# 
       do obj.init;
       #);
  #);

-- attributesEditorLib: attributes --

clear:
  (# 
  do private.theAttributesView.scan
     (#
     do current.delete;
     #);
  #);
updateView:
  (# 
  do clear;
     (if private.theAttributes[] <> none then
         private.theAttributes.iterate
         (# 
         do current[] -> private.theAttributesView.addAttributeItem;
         #);
     if);
     private.theAttributesView.fitToContents;
     l: private.theAttributesView.scan
     (# 
     do current.select;
        leave l;
     #);
     update;
  #);

printAttribute:
  (# theAttributeDecl: ^astInterface.beta.attributeDecl;
     gram: @grammar;
     theText: ^text;
  enter theAttributeDecl[]
  do &text[] -> theText[];
     (if theAttributeDecl.symbol
      //gram.patternDecl then
         (# thePatternDecl: ^astInterface.beta.patternDecl;
         do theAttributeDecl[] -> thePatternDecl[];
            thePatternDecl.getNames -> printNames -> theText.append;
            ': ' -> theText.append;
            thePatternDecl.getObjectDescriptor -> printObjectDescriptor -> theText.append;
         #);
      //gram.simpleDecl then
         (# theSimpleDecl: ^astInterface.beta.simpleDecl;
         do theAttributeDecl[] -> theSimpleDecl[];
            theSimpleDecl.getNames -> printNames -> theText.append;
            ': ' -> theText.append;
            theSimpleDecl.getReferenceSpecification -> printReferenceSpecification -> theText.append;
         #);
      //gram.virtualDecl then
         (# theVirtualDecl: ^astInterface.beta.virtualDecl;
         do theAttributeDecl[] -> theVirtualDecl[];
            theVirtualDecl.getNames -> printNames -> theText.append;
            ':< ' -> theText.append;
            theVirtualDecl.getObjectSpecification -> printObjectSpecification -> theText.append;
         #);
      //gram.bindingDecl then
         (# theBindingDecl: ^astInterface.beta.bindingDecl;
         do theAttributeDecl[] -> theBindingDecl[];
            theBindingDecl.getNames -> printNames -> theText.append;
            '::< ' -> theText.append;
            theBindingDecl.getObjectSpecification -> printObjectSpecification -> theText.append;
         #);
      //gram.finalDecl then
         (# theFinalDecl: ^astInterface.beta.finalDecl;
         do theAttributeDecl[] -> theFinalDecl[];
            theFinalDecl.getNames -> printNames -> theText.append;
            ':: ' -> theText.append;
            theFinalDecl.getObjectSpecification -> printObjectSpecification -> theText.append;
         #);
      else
         failure;
     if);
  exit theText[]
  #);
printNames:
  (# theNames: ^astInterface.beta.names;
     theText: ^text;
     first: @boolean;
  enter theNames[]
  do &text[] -> theText[];
     true -> first;
     theNames.iterate
     (# theNameDecl: ^astInterface.nameDecl;
     do current.getNameDecl -> theNameDecl[];
        (if not first then
            ', ' -> theText.append;
         else
            false -> first;
        if);
        theNameDecl.getText -> theText.append;
     #);
  exit theText[]
  #);
printReferenceSpecification:
  (# theRefSpec: ^astInterface.beta.referenceSpecification;
     theText: ^text;
     gram: @grammar;
  enter theRefSpec[]
  do &text[] -> theText[];
     (if theRefSpec.symbol 
      //gram.staticItem then
         (# theStaticItem: ^astInterface.beta.staticItem;
         do theRefSpec[] -> theStaticItem[];
            '@' -> theText.append;
            theStaticItem.getObjectSpecification -> printObjectSpecification -> theText.append;
         #);
      //gram.dynamicItem then
         (# theDynamicItem: ^astInterface.beta.dynamicItem;
         do theRefSpec[] -> theDynamicItem[];
            '^' -> theText.append;
            theDynamicItem.getAttributeDenotation -> printAttributeDenotation -> theText.append;
         #);
      //gram.staticComponent then
      //gram.dynamicComponent then
      //gram.variablePattern then
         
     if);
  exit theText[]
  #);
printAttributeDenotation:
  (# theAttrDen: ^astInterface.beta.attributeDenotation;
     theText: ^text;
     gram: @grammar;
  enter theAttrDen[]
  do &text[] -> theText[];
     (if theAttrDen.symbol
      //gram.nameApl then
         (# theNameApl: ^astInterface.beta.nameApl;
            theNameAppl: ^astInterface.nameAppl;
         do theAttrDen[] -> theNameApl[];
            theNameApl.getNameAppl -> theNameAppl[];
            theNameAppl.getText -> theText.append;
         #);
     if);
  exit theText[]
  #);
printObjectSpecification:
  (# theObjSpec: ^astInterface.beta.objectSpecification;
     theText: ^text;
     gram: @grammar;
  enter theObjSpec[]
  do &text[] -> theText[];
     (if theObjSpec.symbol 
      //gram.objectDescriptor then
         theObjSpec[] -> printObjectDescriptor -> theText.append;
      else
         theObjSpec[] -> printAttributeDenotation -> theText.append;
     if);
  exit theText[]
  #);
printObjectDescriptor:
  (# theObjDesc: ^astInterface.beta.objectDescriptor;
     theText: ^text;
     theAst: ^astInterface.ASt;
     failure:< object;
     gram: @grammar;
  enter theObjDesc[]
  do &text[] -> theText[];
     theObjDesc.getPrefixOpt -> theAst[];
     (if theAst.kind = 1 (* interior *) then
         (# thePrefix: ^astInterface.beta.prefix;
            theAttributeDenotation: ^astInterface.beta.attributeDenotation;
         do theAst[] -> thePrefix[];
            thePrefix.getAttributeDenotation -> theAttributeDenotation[];
            (if theAttributeDenotation.symbol
             //gram.nameApl then
                (# theNameApl: ^astInterface.beta.nameApl;
                   theNameAppl: ^astInterface.nameAppl;
                do theAttributeDenotation[] -> theNameApl[];
                   theNameApl.getNameAppl -> theNameAppl[];
                   theNameAppl.getText -> theText.append;
                   ' ' -> theText.append;
                #);
             else
                failure;
            if);
         #)
     if);
     '...' -> theText.append;
  exit theText[]
  #);

attributesView: tableView
  (# element:: attributeItem;
     theIcon: ^raster;
     
     attributeUpdated:
       (# theAttribute: ^astInterface.beta.attributeDecl;
          theAttributeItem: ^attributeItem;
       enter theAttribute[]
       do theAttribute[] -> findAttributeItem -> theAttributeItem[];
          (if theAttributeItem[] <> none then
              theAttributeItem.attributeUpdated;
          if);
       #);
     findAttributeItem:
       (# theAttribute: ^astInterface.beta.attributeDecl;
          theAttributeItem: ^attributeItem;
       enter theAttribute[]
       do l: scan
            (# 
            do (if theAttribute[] -> current.theAttribute.equal then
                   current[] -> theAttributeItem[];
                   leave l;
               if);
            #);
       exit theAttributeItem[]
       #);
     
     attributeItem: tableItem
       (# theAttribute: ^astInterface.beta.attributeDecl;
          (* astControl: @astController
           *             (# onDeleted::
           *                  (# 
           *                  do this(attributeItem).delete;
           *                  #);
           *                onAncestorReplaced::
           *                  (# 
           *                  do this(attributeItem).delete;
           *                  #);
           *                onReplaced::
           *                  (# 
           *                  do node[]-> theAttribute[];
           *                     theAttribute[] -> printAttribute -> name;
           *                     (if selected then
           *                         (theAttribute[], false) -> attributeSelected;
           *                     if);
           *                  #);
           *             #);
           *)
          init::<
            (# 
            enter theAttribute[]
            do theAttribute[] -> printAttribute -> name;
               (if theAttribute.hasGUIcomment then
                   theIcon[] -> icon;
               if);
               inner;
            #);
          onSelect::<
            (# 
            do (theAttribute[], false) -> attributeSelected;
            #);
          onMouseDown::<
            (# 
            do (if theEvent.doubleClick then
                   (theAttribute[], true) -> attributeSelected; 
               if);
            #);
          attributeUpdated:
            (# 
            do theAttribute[] -> printAttribute -> name;
               (if theAttribute.hasGUIcomment then
                   theIcon[] -> icon;
               if);
            #);
       #);
     addAttributeItem:
       (# theAttribute: ^astInterface.beta.attributeDecl;
          theItem: ^attributeItem
       enter theAttribute[]
       do &attributeItem[] -> theItem[];
          (none, theAttribute[]) -> theItem.init;
       #);
     open::<
       (# 
       do 'InterfaceBuilderGraph' -> loadRaster -> theIcon[];
          inner;
       #);
     eventHandler::<
       (# onKeyDown::<
            (# 
            do (if ch//13//10 then
                   (* interactiveCreateAttribute; *)
                   
               if);
            #);
       #);
  #);


-- AttributesEditorEnterTheAttributes: doPart --
do ;
   value[] -> private.theAttributes[];
   updateView;
   
-- AttributesEditorExitTheAttributes: doPart --
do private.theAttributes[] -> value[];
   
-- AttributesEditorOpen: doPart --
do this(attributesEditor)[] -> private.theScroller.open;
   (if initialAttributes[] <> None then
       initialAttributes[] -> theAttributes;
   if);
   inner;

-- AttributesEditorPrivate: descriptor --
(# theScroller: @scroller
     (# contentsType:: attributesView;
        open::
          (# width, height: @integer;
          do contents[] -> theAttributesView[];
             this(attributesEditor).size -> (width, height);
             ((0, 0), (width, height)) -> frame;
             true -> bindRight -> bindBottom;
          #);
     #);
   
   theAttributesView: ^attributesView;
   
   theAttributes: ^astInterface.beta.attributes;
   (* astControl: @astController
    *      (# onReplaced::
    *           (# 
    *           do node[] -> this(attributesEditor).theAttributes;
    *           #);
    *         onDeleted::
    *           (# 
    *           do none -> theAttributes[];
    *              none -> this(attributesEditor).theAttributes;
    *           #);
    *         onAncestorReplaced::
    *           (# 
    *           do none -> theAttributes[];
    *              none -> this(attributesEditor).theAttributes;
    *           #);
    *      #);
    *)
#)

-- attributesEditorInteractiveCreateAttribute: doPart --
do 

-- attributesEditorAttributeUpdated: doPart --
do theAttribute[] -> private.theAttributesView.attributeUpdated;

