ORIGIN '../patterneditor';
INCLUDE '~beta/guienv/v1.3.1/utils/tableview';
INCLUDE '~beta/guienv/v1.3.1/utils/loadraster';
INCLUDE '~beta/interfacebuilder/v1.0/mpsstuff/mpsutils';
INCLUDE '~beta/betaast/v4.9.1/gram';
INCLUDE '~beta/interfacebuilder/v1.0/dialogs/promptfortextdialog';
INCLUDE '~beta/interfacebuilder/v1.0/code/generate';


-- lib: attributes --

attributeEditorTextValues: promptForTextValues 
  (# init:
       (# 
       do 'Enter attribute decl' -> prompt[];
       #);
  #);

getTextvalues: objectPool.get
  (# type:: attributeEditorTextValues;
     exact:: (# do true -> value #);
     init::
       (# 
       do obj.init;
       #);
  #);

getMPS: objectPool.get
  (# type:: astInterface;
     init::
       (# 
       do exception
          (# 
          do 'No instance of astInterface found in objectPool' -> msg.append;
          #);
       #);
  #);

getBeta:
  (# betaGrammar: ^astInterface.beta;
     MPS: ^astInterface;
  do getMPS -> MPS[];
     MPS.grammarTable.BETA[] -> betaGrammar[];
     (if betaGrammar[] = None then
         exception
         (# 
         do 'The instance of astInterface in objectPool has no Beta grammar' -> msg.append;
         #);
     if);
  exit betaGrammar[]
  #);

-- attributesEditorLib: attributes --

clear:
  (# 
  do private.theAttributesView.scan
     (#
     do current.delete;
        current.astControl.release;
     #);
  #);
updateView:
  (# 
  do clear;
     (if private.theAttributes[] <> none then
         private.theAttributes.iterate
         (# 
         do current[] -> private.theAttributesView.addAttributeItem;
         #);
     if);
  #);

printAttribute:
  (# theAttributeDecl: ^astInterface.beta.attributeDecl;
     gram: @grammar;
     theText: ^text;
  enter theAttributeDecl[]
  do &text[] -> theText[];
     (if theAttributeDecl.symbol
      //gram.patternDecl then
         (# thePatternDecl: ^astInterface.beta.patternDecl;
         do theAttributeDecl[] -> thePatternDecl[];
            thePatternDecl.getNames -> printNames -> theText.append;
            ': ' -> theText.append;
            thePatternDecl.getObjectDescriptor -> printObjectDescriptor -> theText.append;
         #);
      //gram.simpleDecl then
         (# theSimpleDecl: ^astInterface.beta.simpleDecl;
         do theAttributeDecl[] -> theSimpleDecl[];
            theSimpleDecl.getNames -> printNames -> theText.append;
            ': ' -> theText.append;
            theSimpleDecl.getReferenceSpecification -> printReferenceSpecification -> theText.append;
         #);
      //gram.virtualDecl then
         (# theVirtualDecl: ^astInterface.beta.virtualDecl;
         do theAttributeDecl[] -> theVirtualDecl[];
            theVirtualDecl.getNames -> printNames -> theText.append;
            ':< ' -> theText.append;
            theVirtualDecl.getObjectSpecification -> printObjectSpecification -> theText.append;
         #);
      //gram.bindingDecl then
         (# theBindingDecl: ^astInterface.beta.bindingDecl;
         do theAttributeDecl[] -> theBindingDecl[];
            theBindingDecl.getNames -> printNames -> theText.append;
            '::< ' -> theText.append;
            theBindingDecl.getObjectSpecification -> printObjectSpecification -> theText.append;
         #);
      //gram.finalDecl then
         (# theFinalDecl: ^astInterface.beta.finalDecl;
         do theAttributeDecl[] -> theFinalDecl[];
            theFinalDecl.getNames -> printNames -> theText.append;
            ':: ' -> theText.append;
            theFinalDecl.getObjectSpecification -> printObjectSpecification -> theText.append;
         #);
      else
         failure;
     if);
  exit theText[]
  #);
printNames:
  (# theNames: ^astInterface.beta.names;
     theText: ^text;
     first: @boolean;
  enter theNames[]
  do &text[] -> theText[];
     true -> first;
     theNames.iterate
     (# theNameDecl: ^astInterface.nameDecl;
     do current.getNameDecl -> theNameDecl[];
        (if not first then
            ', ' -> theText.append;
         else
            false -> first;
        if);
        theNameDecl.getText -> theText.append;
     #);
  exit theText[]
  #);
printReferenceSpecification:
  (# theRefSpec: ^astInterface.beta.referenceSpecification;
     theText: ^text;
     gram: @grammar;
  enter theRefSpec[]
  do &text[] -> theText[];
     (if theRefSpec.symbol 
      //gram.staticItem then
         (# theStaticItem: ^astInterface.beta.staticItem;
         do theRefSpec[] -> theStaticItem[];
            '@' -> theText.append;
            theStaticItem.getObjectSpecification -> printObjectSpecification -> theText.append;
         #);
      //gram.dynamicItem then
         (# theDynamicItem: ^astInterface.beta.dynamicItem;
         do theRefSpec[] -> theDynamicItem[];
            '^' -> theText.append;
            theDynamicItem.getAttributeDenotation -> printAttributeDenotation -> theText.append;
         #);
      //gram.staticComponent then
      //gram.dynamicComponent then
      //gram.variablePattern then
         
     if);
  exit theText[]
  #);
printAttributeDenotation:
  (# theAttrDen: ^astInterface.beta.attributeDenotation;
     theText: ^text;
     gram: @grammar;
  enter theAttrDen[]
  do &text[] -> theText[];
     (if theAttrDen.symbol
      //gram.nameApl then
         (# theNameApl: ^astInterface.beta.nameApl;
            theNameAppl: ^astInterface.nameAppl;
         do theAttrDen[] -> theNameApl[];
            theNameApl.getNameAppl -> theNameAppl[];
            theNameAppl.getText -> theText.append;
         #);
     if);
  exit theText[]
  #);
printObjectSpecification:
  (# theObjSpec: ^astInterface.beta.objectSpecification;
     theText: ^text;
     gram: @grammar;
  enter theObjSpec[]
  do &text[] -> theText[];
     (if theObjSpec.symbol 
      //gram.objectDescriptor then
         theObjSpec[] -> printObjectDescriptor -> theText.append;
      else
         theObjSpec[] -> printAttributeDenotation -> theText.append;
     if);
  exit theText[]
  #);
printObjectDescriptor:
  (# theObjDesc: ^astInterface.beta.objectDescriptor;
     theText: ^text;
     theAst: ^astInterface.ASt;
     failure:< object;
     gram: @grammar;
  enter theObjDesc[]
  do &text[] -> theText[];
     theObjDesc.getPrefixOpt -> theAst[];
     (if theAst.kind = 1 (* interior *) then
         (# thePrefix: ^astInterface.beta.prefix;
            theAttributeDenotation: ^astInterface.beta.attributeDenotation;
         do theAst[] -> thePrefix[];
            thePrefix.getAttributeDenotation -> theAttributeDenotation[];
            (if theAttributeDenotation.symbol
             //gram.nameApl then
                (# theNameApl: ^astInterface.beta.nameApl;
                   theNameAppl: ^astInterface.nameAppl;
                do theAttributeDenotation[] -> theNameApl[];
                   theNameApl.getNameAppl -> theNameAppl[];
                   theNameAppl.getText -> theText.append;
                   ' ' -> theText.append;
                #);
             else
                failure;
            if);
         #)
     if);
     '...' -> theText.append;
  exit theText[]
  #);

attributesView: tableView
  (# element:: attributeItem;
     theIcon: ^raster;
     
     attributeUpdated:
       (# theAttribute: ^astInterface.beta.attributeDecl;
          theAttributeItem: ^attributeItem;
       enter theAttribute[]
       do theAttribute[] -> findAttributeItem -> theAttributeItem[];
          (if theAttributeItem[] <> none then
              theAttributeItem.attributeUpdated;
          if);
       #);
     findAttributeItem:
       (# theAttribute: ^astInterface.beta.attributeDecl;
          theAttributeItem: ^attributeItem;
       enter theAttribute[]
       do l: scan
            (# 
            do (if theAttribute[] -> current.theAttribute.equal then
                   current[] -> theAttributeItem[];
                   leave l;
               if);
            #);
       exit theAttributeItem[]
       #);
     insertAttribute:
       (# beforeAttribute: ^astInterface.beta.attributeDecl;
          thePromptForTextValues: ^promptForTextValues;
          theAttribute: ^astInterface.beta.attributeDecl;
          
          theDialog: @window
            (# theCanvas: @promptForTextView
                 (# open::
                      (# 
                      do theCanvas.size -> theDialog.size;
                         true -> bindRight -> bindBottom;
                      #);
                    accept::
                      (# betaGrammar: ^astInterface.beta;
                         frag: ^astInterface.fragmentForm;
                         root: ^astInterface.AST;
                         category: @integer;
                         str: ^text;
                         gram: @grammar;
                         errorText: @text;
                         fatal, parseOK: @boolean;
                         oldErrorReporter: ^astInterface.errorReporter;
                         MPS: ^astInterface;
                      do getMPS -> MPS[];
                         MPS.grammarTable.BETA[] -> betaGrammar[];
                         (if betaGrammar[] = None then
                             exception
                             (# 
                             do 'The instance of astInterface in objectPool has no Beta grammar' -> msg.append;
                             #);
                         if);
                         MPS.theErrorReporter[] -> oldErrorReporter[];
                         &MPS.interactiveErrorReporter[] -> MPS.theErrorReporter[];
                         (theAttributes).frag[] -> frag[];
                         frag.root[] -> root[];
                         thePromptForTextValues.theText[] -> str[];
                         ' ' -> str.append;
                         str.reset;
                         gram.attributeDecl -> category;
                         (category, str[], screen[], frag[]) -> betaGrammar.parser.doParse
                         (#
                            fatalParseError::<
                              (#
                              do
                                 'Parse errors: ' -> errorText.putText;
                                 msg[] -> errorText.putLine;
                                 true -> fatal;
                                 true -> continue;
                              #);
                         #) -> parseOK;
                         (if parseOK then
                             frag.root[] -> theAttribute[];
                             theDialog.close;
                          else
                             (if not fatal then
                                 str.reset;
                                 (str[],errorText[]) -> betaGrammar.parser.errorReport;
                             if);
                             errorText[] -> reportParseErrors;
                         if);
                         root[] -> frag.root[];
                         oldErrorReporter[] -> MPS.theErrorReporter[];
                      #);
                    dismiss::
                      (# 
                      do theDialog.close;
                         none -> theAttribute[];
                      #);
                 #);
               open::
                 (# 
                 do hide;
                    'New attribute' -> title;
                    (contents, thePromptForTextValues[]) -> theCanvas.open;
                 #);
            #);
       enter beforeAttribute[]
       do getTextvalues -> thePromptForTextValues[];
          theDialog.open;
          theDialog.showModal;
          (if theAttribute[] <> None then
              theAttribute[] -> this(attributesEditor).private.theAttributes.smartAppend;
              theAttribute[] -> addAttributeItem;
          if);
       #);
     
     attributeItem: tableItem
       (# theAttribute: ^astInterface.beta.attributeDecl;
          astControl: @astController
            (# deleted::
                 (# 
                 do this(attributeItem).delete;
                 #);
               ancestorReplaced::
                 (# 
                 do this(attributeItem).delete;
                 #);
               replaced::
                 (# 
                 do node[]-> theAttribute[];
                    theAttribute[] -> printAttribute -> name;
                    (if selected then
                        theAttribute[] -> attributeSelected;
                    if);
                 #);
            #);
          init::<
            (# 
            enter theAttribute[]
            do theAttribute[] -> printAttribute -> name;
               (if theAttribute.hasGUIcomment then
                   theIcon[] -> icon;
               if);
               theAttribute[] -> astControl.init;
               inner;
            #);
          onSelect::<
            (# 
            do theAttribute[] -> attributeSelected;
            #);
          attributeUpdated:
            (# 
            do theAttribute[] -> printAttribute -> name;
               (if theAttribute.hasGUIcomment then
                   theIcon[] -> icon;
               if);
            #);
       #);
     addAttributeItem:
       (# theAttribute: ^astInterface.beta.attributeDecl;
          theItem: ^attributeItem
       enter theAttribute[]
       do &attributeItem[] -> theItem[];
          (none, theAttribute[]) -> theItem.init;
       #);
     open::<
       (# 
       do 'InterfaceBuilderGraph' -> loadRaster -> theIcon[];
          true -> border.visible;
          inner;
       #);
     eventHandler::<
       (# onKeyDown::<
            (# 
            do (if ch//13//10 then
                   interactiveCreateAttribute;
               if);
            #);
       #);
  #);


-- AttributesEditorEnterTheAttributes: doPart --
do (if private.astControl.node[] <> none then
       private.astControl.release;
   if);
   value[] -> private.theAttributes[];
   updateView;
   
-- AttributesEditorExitTheAttributes: doPart --
do private.theAttributes[] -> value[];
   
-- AttributesEditorOpen: doPart --
do this(attributesEditor)[] -> private.theAttributesView.open;
   (if initialAttributes[] <> None then
       initialAttributes[] -> theAttributes;
   if);
   inner;

-- AttributesEditorPrivate: descriptor --
(# theAttributesView: @attributesView
     (# open::
          (# width, height: @integer;
          do this(attributesEditor).size -> (width, height);
             ((0, 0), (width, height)) -> frame;
             true -> bindRight -> bindBottom;
          #);
        
     #);
   theAttributes: ^astInterface.beta.attributes;
   astControl: @astController
     (# replaced::
          (# 
          do node[] -> this(attributesEditor).theAttributes;
          #);
        deleted::
          (# 
          do none -> theAttributes[];
             none -> this(attributesEditor).theAttributes;
          #);
        ancestorReplaced::
          (# 
          do none -> theAttributes[];
             none -> this(attributesEditor).theAttributes;
          #);
     #);
#)

-- attributesEditorInteractiveCreateAttribute: doPart --
do private.theAttributesView.insertAttribute;

-- attributesEditorAttributeUpdated: doPart --
do theAttribute[] -> private.theAttributesView.attributeUpdated;

