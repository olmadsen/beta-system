ORIGIN '~beta/guienv/v1.4/guienv';

INCLUDE '~beta/betaast/v5.1/gram';

INCLUDE '../view/view';
INCLUDE '../view/buttons';
INCLUDE '../view/metainspector';

INCLUDE '../code/generate';

INCLUDE '~beta/guienv/v1.4/utils/prompts';
INCLUDE '~beta/guienv/v1.4/utils/raster_xpmfile';

-- program: descriptor --
GUIenv
(# MPS: ^astInterface;
   betagram: ^MPS.beta;
   pretty: ^MPS.prettyPrinter;
   
   theMetaInspector: @metaInspector;
   
   gram: @grammar;
   times10: @textStyle;
   times14: @textStyle;
   times14Bold: @textStyle;
   times14Italic: @textStyle;
   times12: @textStyle;
   times24: @textStyle;
   times12Italic: @textStyle;
   times9Bold: @textStyle;
   helvetica12: @textStyle;
   
   arrowRight, arrowDown: ^bitmap;
   arrowRightHilite, arrowDownHilite: ^bitmap;
   
   readImage:
     (# name: ^text;
        theImage: ^bitmap;
     enter name[]
     do &bitmap[] -> theImage[];
        name[] -> theImage.readFromXPMfile;
     exit theImage[]
     #);
   
   BetaGrammarLocation: 
     (# 
     exit '~beta/grammars/beta/v2.5/beta'
     #);
   BetaGrammarName:
     (#
     exit 'beta'
     #);
   
   simpleMenu: menu
     (# simpleItem: menuItem
          (# 
             open::<
               (# t: ^text;
               enter t[]
               do t[] -> name;
                  this(simpleItem)[] -> append;
                  INNER;
               #);
          #);
     #);
     
   classBrowser: @window
     (# menubarType::
          (# fileMenu: @simpleMenu
               (# displayItem: @simpleItem
                    (# eventHandler::
                         (# onSelect::
                              (#
                              do classArea.userDisplayClass;
                              #);
                         #);
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       'Display...' -> displayItem.open;
                    #);
               #);
             open::
               (# 
               do fileMenu.open;
                  fileMenu[] -> append;
               #);
             
          #);
        
        classArea: @world
          (# 
             
             makeSpace: makeView
               (# type::< space;
               do INNER;
               #);
             space: view
               (# naturalWidth:: (# do 5 -> value #);
               #);
             makeFill: makeSpace
               (# type::< fill;
               do INNER;
               #);
             fill: space
               (# widthStretch:: (# do 1000 -> value #);
               #);
             
             expanderButton: imageToggler
               (# init::<
                    (# 
                    do arrowRight[] -> offImage[] -> (interior).image;
                       arrowDown[] -> onImage[];
                       arrowDownHilite[] -> onImageHilite[];
                       arrowRightHilite[] -> offImageHilite[];
                    #);
               #);
             
             
             userDisplayClass:
               (# 
               do (classBrowser[], 'Display Class', 'Name:', '') -> promptForText
                  (# OK::
                       (# 
                       do userText[] -> findClass -> displayClass;
                       #);
                     
                  #)
               #);
             
             group: ^astInterface.fragmentGroup;
             
             
             findClass:
               (# name: ^text;
                  thePattern: ^astInterface.beta.patternDecl;
               enter name[]
               do 'find class: ' -> putText;
                  name[] -> putLine;
                  (if group[] <> NONE then
                      'group: ' -> putText;
                      group.name -> putLine;
                      
                      group.fragmentList.scan
                      (# frag: ^astInterface.fragmentForm;
                         exp: ^astInterface.expanded;
                      do (if current.type = mps.formType then
                             current.open -> frag[];
                             (if frag.root[] <> none then
                                 (if frag.root.symbol = gram.attributesForm then
                                     'frag: ' -> putText;
                                     frag.name -> putLine;
                                     
                                     frag.root[] -> exp[];
                                     l: gram.patternDecl -> exp.suffixWalkForProd
                                     (# itsName: ^text;
                                     do 'BANG!' -> putLine;
                                        current[] -> betaGram.getName -> itsName[];
                                        (if name[] -> itsName.equalNCS then
                                            current[] -> thePattern[];
                                            leave l;
                                        if);
                                     #);
                                 if);
                             if);
                         if)
                      #);
                  if);
               exit thePattern[]
               #);
             
             classView: adorner
               (# thePattern: ^astInterface.beta.patternDecl;
                  static:: trueObject;
                  theColumn: ^column;
                  
                  theAttributesColumn: ^column;
                  
                  expand:
                    (# theAttributes: ^astInterface.beta.attributes;
                    do (if theAttributesColumn[] = NONE then
                           &column[] -> theAttributesColumn[];
                           theAttributesColumn.init;
                           thePattern[] -> betaGram.getAttributes -> theAttributes[];
                           theAttributes.newScan
                           (# anAttributeView: ^attributeView;
                           do &attributeView[] -> anAttributeView[];
                              current[] -> anAttributeView.init;
                              anAttributeView[] -> theAttributesColumn.append;
                           #);
                       if);
                       theAttributesColumn[] -> theColumn.append;
                    #);
                  colapse:
                    (# 
                    do 
                    #);
                  
                  attributeView: adorner
                    (# theRow: ^row;
                       
                       name: @stringButton
                         (# init::
                              (# n: ^text;
                              enter n[]
                              do times14Bold[] -> (interior).style;
                                 n[] -> (interior).string;
                              #);
                            static:: trueObject;
                         #);
                       decl: @stringView
                         (# init::
                              (# t: ^text;
                              enter t[]
                              do times12italic[] -> style;
                                 t[] -> string;
                              #);
                         #);
                       init::
                         (# theAttributeDecl: ^astInterface.beta.attributeDecl;
                         enter theAttributeDecl[]
                         do theAttributeDecl[] -> betaGram.getName -> name.init;
                            name[] -> theRow.append;
                            makeFill -> theRow.append;
                            theAttributeDecl[] -> printAttributeDecl -> decl.init;
                            decl[] -> makeStringViewEditor -> theRow.append;
                         #);
                       interiorType:: motifBorder
                         (# interiorType:: solidBackground
                              (# interiorType:: margin
                                   (# interiorType:: row;
                                      init::
                                        (# 
                                        do interior -> theRow[];
                                        #);
                                      static:: trueObject;
                                   #);
                                 init::
                                   (# 
                                   do pinkish -> paint;
                                   #);
                                 static:: trueObject;
                              #);
                            init::
                              (# 
                              do 1 -> borderWidth;
                                 pinkish -> paint;
                              #);
                            
                            static:: trueObject;
                         #);
                       static:: trueObject;
                    #);
                  
                  interiorType:: motifBorder
                    (# static:: trueObject;
                       init::
                         (# 
                         do 3 -> borderWidth;
                            shadowOut -> borderStyle;
                         #);
                       interiorType:: solidBackground
                         (# static:: trueObject;
                            interiorType:: column
                              (# 
                                 header: row
                                   (# expander: @expanderButton
                                        (# toggleOn::
                                             (# 
                                             do expand;
                                             #);
                                           toggleOff::
                                             (# 
                                             do colapse;
                                             #);
                                        #);
                                      name: @stringButton
                                        (# static:: trueObject;
                                           label:
                                             (#
                                             enter (interior).string
                                             #);
                                           init::
                                             (# 
                                             do times14Bold[] -> (interior).style;
                                             #);
                                        #);
                                      init::
                                        (# 
                                        do (* expander.init;
                                            * expander[] -> makeCenter -> append;
                                            *)
                                           name.init;
                                           thePattern[] -> betaGram.getName -> name.label;
                                           name[] -> makeCenter -> append;
                                        #);
                                   #);
                                 init::
                                   (# theHeader: ^header;
                                   do &header[] -> theHeader[];
                                      theHeader.init;
                                      theHeader[] -> append;
                                      this(column)[] -> theColumn[];
                                      expand;
                                   #);
                              #);
                         #);
                         
                    #);
                  
                  init::<
                    (# 
                    enter thePattern[]
                    do 
                    #);
               #);
             
             
             
             
             printAttributeDecl:
               (# theAttributeDecl: ^astInterface.beta.attributeDecl;
                  str: ^text;
               enter theAttributeDecl[]
               do theAttributeDecl.getSon2 -> printAst -> str[];
               exit str[]
               #);
             
             printAst:
               (# anAst: ^astInterface.AST;
                  str: ^text;
                  foundNewLine: @boolean;
               enter anAst[]
               do &text[] -> str[];
                  (anAst[], str[]) -> pretty.printNode;
                  str[] -> chopTrailingBlanks;
                  (if str.length > 30 then
                      '...' -> str[];
                   else
                      ascii.newLine -> str.findAll (# do true -> foundNewLine #);
                      (if foundNewLine then
                          '...' -> str[];
                      if);
                  if);
               exit str[]
               #);
             chopTrailingBlanks:
               (# str: ^text;
               enter str[]
               do l: (if str.length > 0 then
                         (if str.length -> str.inxGet -> ascii.isSpace then
                             str.lgth - 1 -> str.lgth;
                             restart l;
                         if);
                  if);
               #);
             displayClass:
               (# thePattern: ^astInterface.beta.patternDecl;
                  theClassView: ^classView;
               enter thePattern[]
               do (if thePattern[] <> NONE then
                      &classView[] -> theClassView[];
                      thePattern[] -> theClassView.init;
                      theClassView[] -> bulletinBoard.append;
                  if);
               #); 
          #);
        open::
          (# 
          do classArea.open;
             theMetaInspector[] -> classArea.theInspector[];
             (300, 200) -> classArea.size;
             true -> classArea.bindRight;
             true -> classArea.bindBottom;
          #);
     #);
   name: ^text;
do 'Times' -> times10.name;
   10 -> times10.size;
   
   'Times' -> times14Bold.name;
   14-> times14Bold.size;
   textFaces.bold -> times14Bold.face;
   
   'Times' -> times14Italic.name;
   14 -> times14Italic.size;
   textFaces.italic -> times14Italic.face;
   
   'Times' -> times14.name;
   14 -> times14.size;
   
   'Times' -> times24.name;
   24 -> times24.size;
   
   'Times' -> times12.name;
   12 -> times12.size;
   
   'Times' -> times12Italic.name;
   12 -> times12Italic.size;
   textFaces.italic -> times12Italic.face;
   
   'Helvetica' -> helvetica12.name;
   12 -> helvetica12.size;
   
   '/users/henryml/beta/interfacebuilder/v1.6/classbrowser/arrowright.xpm' -> readImage -> arrowRight[];
   '/users/henryml/beta/interfacebuilder/v1.6/classbrowser/arrowrighthilite.xpm' -> readImage -> arrowRightHilite[];
   '/users/henryml/beta/interfacebuilder/v1.6/classbrowser/arrowdown.xpm' -> readImage -> arrowDown[];
   '/users/henryml/beta/interfacebuilder/v1.6/classbrowser/arrowdownhilite.xpm' -> readImage -> arrowDownHilite[];
   
   theMetaInspector.init;
   theMetaInspector.MPS[] -> MPS[];
   theMetaInspector.betagram[] -> betagram[];
   theMetaInspector.pretty[] -> pretty[];
   
   '~beta/guienv/v1.4/guienv' ->  name[];
   (name[], screen[]) -> MPS.top.open -> classBrowser.classArea.group[];
   classBrowser.open;
#)
