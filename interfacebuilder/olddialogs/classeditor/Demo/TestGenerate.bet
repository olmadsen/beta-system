ORIGIN '~michael/ast/UseAstEnv'
[[
-- Include '~michael/InterfaceEditor/GenerateAtt'
-- Program: Descriptor --
UseAstEnv
	(# BetaGram: @Grammar;
		Indent: @
			(# i,width: @Integer;
				Up: (# do i+1 -> i #);
				Down: (# do i-1 -> i #);
				t: @Text;
			do t.Clear;
				(for i*width repeat
					' ' -> t.Put;
				for);
				t[] -> PutText;
			#);
		Name: ^Text;
		fg: ^MPS.FragmentGroup;
		DeepScan:
			(# theAst: ^MPS.Ast;
			enter theAst[]
			do Indent.Up;
				theAst.ScanAttributes
					(#
					do Indent;
						Current.GetDeclName -> PutLine;
						(if Current.Symbol
						//BetaGram.PatternDecl
						then
							Current[] -> DeepScan;
						if);
					#);
				theAst.ScanPrivateAtt
					(#
					do Indent;
						Current.GetDeclName -> PutLine;
						Current[] -> DeepScan;
					#);
				Indent.Down;
			#);
	do 2 -> Indent.Width;
		'Group name?' -> PutLine;
		GetLine -> name[];
		(name[],Screen[]) -> MPS.Top.Open -> fg[];
		(if fg[]//None
		then
		else
			fg.FragmentList.Scan
				(# f: ^MPS.FragmentForm;
				do (if Current.Type//MPS.FormType
					then
						Current.Open -> f[];
						f.root[] -> DeepScan;
					if);
				#);
			
		if);
	#)
--]]
