ORIGIN '~beta/sourcebrowser/v0.1/ymer';
INCLUDE 'graphicaleditor/graphicaleditor';
INCLUDE 'guienvstuff/getfile';
-- program: descriptor --
ymerApplication
(# 
   theGraphicalEditorEnv: @graphicalEditorEnv
     (#
        openAstEditor::
          (# 
          do
             node[] -> ymerBrowser.browser.selectNode;
          #);
     #);
   theResourceFile: @file;
   theObjectStore: @objectDataStore;
   active: @boolean;
   
   ymerBrowserWindow::
     (# editWindow:
          (# theAttributeDecl: ^astInterface.ast;
             theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
          enter theAttributeDecl[]
          do 
             theAttributeDecl[] -> theGraphicalEditorEnv.find -> theGraphicalEditor[];
             (if theGraphicalEditor[] = none then
                 &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                 theObjectStore[] -> theGraphicalEditor.theObjectStore[];
                 theGraphicalEditor.open;
                 theAttributeDecl[] -> theGraphicalEditor.init;
              else
                 theGraphicalEditor.bringToFront;
             if);
          #);
        
        menubarType::
          (# builderMenu: @menu
               (# openGraphicalEditor: @menuItem
                    (# eventHandler::
                         (# onStatus::
                              (# 
                              do active -> value;
                              #);
                            onSelect::
                              (# 
                              do (browser.getFragmentFormEditor).sifViewer.cs.node[] -> editWindow;
                              #);
                         #);
                       open::
                         (# 
                         do 'Graphicaleditor...' -> name;
                         #);
                    #);
                  open::
                    (# 
                    do 'Interfacebuilder' -> name;
                       openGraphicalEditor.open;
                       openGraphicalEditor[] -> append;
                    #);
               #);
             open::
               (# 
               do builderMenu.open;
                  builderMenu[] -> append;
               #);
          #);
     #);
   init::
     (# name: ^text;
     do mps.betacfl[] -> mps.ast.grammartable.beta[];
        mps.ast[] -> objectPool.put;
        theGraphicalEditorEnv.init;
        theObjectStore.init;
        'res' -> getFile -> name[];
        (if name[] <> none then
            true -> active;
            name[] -> theResourceFile.name;
            theResourceFile.openRead;
            theResourceFile[] -> theObjectStore.read;
            theResourceFile.close;
         else
            false -> active;
        if);
     #);
do 
   init;
#)
