ORIGIN '../graphicaleditor/graphicaleditor';
INCLUDE '../graphicaleditor/standardeditors'
'~beta/guienv/utils/groupbox'
'~beta/guienv/utils/column'
'~beta/guienv/utils/row'
'~beta/guienv/utils/auxcontrols'
'../support/panorama'
'~beta/guienv/utils/extracontrols'
'../support/framer'
'../support/statusview'
'../graphicaleditor/icons';

INCLUDE '../graphicaleditor/private/graphicaleditorprivate';

LIB_DEF 'frigg_additional' '../../lib'

-- graphicalEditorEnvLib: attributes --

insertAdditionalSuite:
  (# 
  do
     (('caption',captionIcon)->loadBitmap,'caption',&instantiator
        (# editorType:: theGraphicalEditor.captionEditor #)[],
      '~beta/guienv/utils/auxcontrols','AUX',40,16,NONE,true)->factory.insert;
     (('dialogfield',dialogfieldIcon)->loadBitmap,'dialogField',&instantiator
        (# editorType:: theGraphicalEditor.dialogFieldEditor #)[],
      '~beta/guienv/utils/auxcontrols','AUX',100,21,NONE,true)->factory.insert;
     (('downarrow',downarrowIcon)->loadBitmap,'downArrow',&instantiator
        (# editorType:: theGraphicalEditor.downArrowEditor #)[],
      '~beta/guienv/utils/auxcontrols','AUX',32,32,NONE,true)->factory.insert;
     (('groupradiobutton',groupRadioButtonIcon)->loadBitmap,'groupRadioButton',&instantiator
        (# editorType:: theGraphicalEditor.groupRadioButtonEditor #)[],
      '~beta/guienv/utils/auxcontrols','AUX',60,16,NONE,true)->factory.insert;
     (('groupbox',groupboxIcon)->loadBitmap,'groupbox',&instantiator
        (# editorType:: theGraphicalEditor.groupBoxEditor #)[],
      '~beta/guienv/utils/groupbox','AUX',150,100,NONE,true)->factory.insert;
     (('column',columnIcon)->loadBitmap,'column',&instantiator
        (# editorType:: theGraphicalEditor.columnEditor #)[],
      '~beta/interfacebuilder/support/column','AUX',100,150,NONE,true)->factory.insert;
     (('row',rowIcon)->loadBitmap,'row',&instantiator
        (# editorType:: theGraphicalEditor.rowEditor #)[],
      '~beta/interfacebuilder/support/row','AUX',150,30,NONE,true)->factory.insert;
     (('canvas',canvasIcon)->loadBitmap,'expanderButton',&instantiator
        (# editorType:: theGraphicalEditor.expanderButtonEditor #)[],
      '~beta/guienv/utils/extracontrols','AUX',12,12, NONE,true)
       ->factory.insert;
     (('canvas',canvasIcon)->loadBitmap,'bevelButton',&instantiator
        (# editorType:: theGraphicalEditor.bevelButtonEditor #)[],
      '~beta/guienv/utils/extracontrols','AUX',70,20, NONE,true) 
       ->factory.insert;
		 
     (('canvas', canvasIcon)->loadBitmap, 'framer',&instantiator
     (# editorType:: theGraphicalEditor.framerEditor #)[],
     '~beta/interfacebuilder/support/framer','AUX' , 70, 20, NONE, true)
       ->factory.insert;
     (('canvas', canvasIcon)->loadBitmap, 'statusView',&instantiator
     (# editorType:: theGraphicalEditor.statusViewEditor #)[],
     '~beta/interfacebuilder/support/statusview','AUX' , 70, 20, NONE, true)
       ->factory.insert;
  #);
  

-- graphicalEditorLib: Attributes --

statusViewEditor: framerEditor
  (# windowItemType::< statusView;
     newData::<
       (#
       do ('maxValue', 100) -> data.setInt;
          ('value', 0) -> data.setInt;
          INNER;
       #);
     applyObjectData::<
       (#
       do 'maxValue' -> data.getInt -> theWindowItem.theMaxValue;
          'value' -> data.getInt -> theWindowItem.theValue;
          INNER;
       #);
  #);

framerEditor: canvasEditor
  (# windowItemType::< framer;
     outline::< (# do false -> value; INNER #);
     newData::<
       (#
       do ('borderwidth', 2) -> data.setInt;
          ('kind', bevelKind) -> data.setInt;
          ('inset', true) -> data.setBool;
          INNER;
       #);
     applyObjectData::<
       (#
       do 'borderwidth' -> data.getInt -> theWindowItem.theBorderWidth;
          'kind' -> data.getInt -> theWindowItem.theKind;
          'inset' -> data.getBool -> theWindowItem.insetFlag;
          theWindowItem.update;
          INNER;
       #);
  #);
groupBoxEditor: canvasEditor
  (#
     windowitemType:: groupBox;
     newData::<  (#  do ('title','Title:')->data.setText #);
     applyObjectData::< 
       (#  do 'title'->data.getText->theWindowItem.title #);
     outline::  (#  do false->value #);
     
  #);
dialogFieldEditor: textFieldEditor
  (#
     windowitemType:: dialogField;
     newData::< 
       (# 
       do
          ('allowReturn',false)->data.setBool;
          ('editable',true)->data.setBool;
          ('textContents','')->data.setText
       #);
     applyObjectData::< 
       (# 
       do
          'allowReturn'->data.getBool->theWindowItem.allowReturn;
          'editable'->data.getBool->theWindowItem.editable;
          'textContents'->data.getText->theWindowItem.textContents
       #);
     
  #);
captionEditor: staticTextEditor (# windowitemType:: caption;  #);
(* editLabelEditor: captionEditor (#  #); *)
groupRadioButtonEditor: radioButtonEditor
  (# windowitemType:: groupRadioButton;  #);
downArrowEditor: windowItemEditor
  (#
     outline::  (#  do false->value #);
     windowitemType:: downArrow;
     newData::<  (#  do ('arrowHeight',5)->data.setInt #);
     applyObjectData::< 
       (#  do 'arrowHeight'->data.getInt->theWindowItem.arrowHeight #);
     
  #);
managerEditor: canvasEditor
  (#
     windowitemType::< manager;
     newData::< 
       (# 
       do
          ('vertical.alignment',alignTop)->data.setInt;
          ('horizontal.alignment',alignLeft)->data.setInt;
          ('horizontal.wrap',false)->data.setBool;
          ('vertical.wrap',false)->data.setBool;
          ('margin.left',5)->data.setInt;
          ('margin.top',5)->data.setInt;
          ('margin.right',5)->data.setInt;
          ('margin.bottom',5)->data.setInt;
          ('distance',5)->data.setInt
       #);
     applyObjectData::< 
       (# 
       do
          'vertical.alignment'->data.getInt->theWindowItem.vertical.alignment;
          'horizontal.alignment'->data.getInt
            ->theWindowItem.horizontal.alignment;
          'horizontal.wrap'->data.getBool->theWindowItem.horizontal.wrap;
          'vertical.wrap'->data.getBool->theWindowItem.vertical.wrap;
          'margin.left'->data.getInt->theWindowItem.margin.left;
          'margin.top'->data.getInt->theWindowItem.margin.top;
          'margin.right'->data.getInt->theWindowItem.margin.right;
          'margin.bottom'->data.getInt->theWindowItem.margin.bottom;
          'distance'->data.getInt->theWindowItem.distance
       #);
     
  #);
columnEditor: managerEditor (# windowitemType::< column;  #);
rowEditor: managerEditor (# windowitemType::< row;  #);
expanderButtonEditor: windowItemEditor
  (# outline:: (# do false -> value #);
     windowItemType::< expanderButton;
  #);
bevelButtonEditor: windowItemEditor
  (# outline:: (# do false -> value #);
     windowItemType::< bevelButton;
     newData:: 
       (# 
       do
          ('label','ABC')->data.setText;
          ('alignment',alignmentCenter)->data.setInt;
          ('fontName','Times')->data.setText;
          ('fontSize',12)->data.setInt;
          ('fontFace',0)->data.setInt;
          ('kind',medium)->data.setInt;
          ('behavior',pushbuttonBehavior)->data.setInt;
          ('state',FALSE)->data.setBool;
          
       #);
     applyObjectData:: 
       (# 
       do
          'label'->data.getText->theWindowItem.label;
          'alignment'->data.getInt->theWindowItem.alignment;
          'fontName'->data.getText->theWindowItem.fontName;
          'fontSize'->data.getInt->theWindowItem.fontSize;
          'fontFace'->data.getInt->theWindowItem.fontFace;
          'kind'->data.getInt->theWindowItem.kind;
          noneBehavior->theWindowItem.behavior;
          'state'->data.getBool->theWindowItem.state;
          
       #);
     
  #);
panoramaEditor: canvasEditor (#  #);
panoramaScrollbarEditor: verScrollbarEditor (#  #);
verticalScrollerEditor: canvasEditor
  (#
     windowitemType::< verticalScroller;
     new::< 
       (#
          theAttributes: ^astInterface.beta.attributes;
          contentsNode: ^astInterface.beta.attributeDecl;
          
       do
          node[]->betaGram.getAttributes->theAttributes[];
          (node.frag[],'contentsType',none )->betaGram.newVirtualBinding
            ->contentsNode[]->theAttributes.smartAppend;
          contentsNode[]->addOpen;
          
       #);
     getMainAttributes::< 
       (# theNode: ^astInterface.beta.attributeDecl; 
       do
          (node[],'contentsType')->betaGram.findAttribute->theNode[];
          (if theNode[] = none then (* Failure!!! *)
              
           else
              theNode[]->betaGram.getAttributes->theAttributes[]; 
          if);
          
       #);
     getMainNode::< 
       (# 
       do
          (theNode[],'contentsType')->betaGram.findAttribute->theNode[];
          INNER ;
          
       #);
     getMainCanvas::<  (#  do theWindowItem.contents[]->theCanvas[];  #);
     outline::<  (#  do false->value; INNER #);
     
  #);
  

