ORIGIN '../graphicaleditor/graphicaleditor';
INCLUDE '../graphicaleditor/standardeditors'
        '../support/groupbox'
        '../support/column'
        '../support/row'
        '../support/auxcontrols'
        (* '../support/editlabel' *);
-- graphicalEditorLib: Attributes --
groupBoxEditor: canvasEditor
  (#
     windowitemType:: groupBox;
     newData::<  (#  do ('caption','Caption:')->data.setText #);
     applyObjectData::< 
       (#  do 'caption'->data.getText->theWindowItem.caption #);
     outline:: (# do false -> value #);
  #);
dialogFieldEditor: textFieldEditor
  (#
     windowitemType:: dialogField;
     newData::< 
       (# 
       do
          ('allowReturn',false)->data.setBool;
          ('editable',true)->data.setBool;
          ('textContents','')->data.setText
       #);
     applyObjectData::< 
       (# 
       do
          'allowReturn'->data.getBool->theWindowItem.allowReturn;
          'editable'->data.getBool->theWindowItem.editable;
          'textContents'->data.getText->theWindowItem.textContents
       #);
     
  #);
captionEditor: staticTextEditor (# windowitemType:: caption;  #);
(* editLabelEditor: captionEditor (#  #); *)
groupRadioButtonEditor: radioButtonEditor
  (# windowitemType:: groupRadioButton;  #);
downArrowEditor: windowItemEditor
  (# outline:: (# do false -> value #);
     windowitemType:: downArrow;
     newData::<  (#  do ('arrowHeight',5)->data.setInt #);
     applyObjectData::< 
       (#  do 'arrowHeight'->data.getInt->theWindowItem.arrowHeight #);
     
  #);
managerEditor: canvasEditor
  (#
     windowitemType::< manager;
     newData::< 
       (# 
       do
          ('vertical.alignment',alignTop)->data.setInt;
          ('horizontal.alignment',alignLeft)->data.setInt;
          ('horizontal.wrap',false)->data.setBool;
          ('vertical.wrap',false)->data.setBool;
          ('margin.left',5)->data.setInt;
          ('margin.top',5)->data.setInt;
          ('margin.right',5)->data.setInt;
          ('margin.bottom',5)->data.setInt;
          ('distance',5)->data.setInt
       #);
     applyObjectData::< 
       (# 
       do
          'vertical.alignment'->data.getInt->theWindowItem.vertical.alignment;
          'horizontal.alignment'->data.getInt
            ->theWindowItem.horizontal.alignment;
          'horizontal.wrap'->data.getBool->theWindowItem.horizontal.wrap;
          'vertical.wrap'->data.getBool->theWindowItem.vertical.wrap;
          'margin.left'->data.getInt->theWindowItem.margin.left;
          'margin.top'->data.getInt->theWindowItem.margin.top;
          'margin.right'->data.getInt->theWindowItem.margin.right;
          'margin.bottom'->data.getInt->theWindowItem.margin.bottom;
          'distance'->data.getInt->theWindowItem.distance
       #);
     
  #);
columnEditor: managerEditor (# windowitemType::< column;  #);
rowEditor: managerEditor (# windowitemType::< row;  #);
  

