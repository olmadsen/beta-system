ORIGIN '~beta/basiclib/basicsystemenv';
INCLUDE 'friggapplication'
        '~beta/guienv/guienvsystemenv'
        '~beta/compiler/DYN/dynlib'
        'graphicaleditor/loaderview'
        'dialogs/progressdialog'
        'additional/additional';
BODY '~beta/editor/private/nocompiler'
     '~beta/editor/private/nouser';
-- program: Descriptor --
systemenv
  (#
     frigg: @friggApplication
       (#
          onStartApplication::  (#  do  #);
          theLoaderView: @loaderView;
          init:: 
            (# 
            do
               'frigg init'->putLine;
               (DC[],theGraphicalEditorEnv[])->theLoaderView.open;
               theGraphicalEditorEnv.insertAdditionalSuite;
               
            #);
          initializeDynamicCompiler:: 
            (#
               theProgressDialog: @progressDialog
                 (#
                    type:: windowTypes.dialog;
                    onStart::  (#  do 1->t.start;  #);
                    open:: 
                      (# 
                      do
                         ''->title;
                         'Initializing Compiler'->message;
                         537->MaxValue;
                         0->Value;
                         
                      #);
                    
                 #);
               t: @timer
                 (#
                    action:: 
                      (# 
                      do
                         stop;
                         DC.initDyn
                           (#
                              tick:: 
                                (# 
                                do
                                   theProgressDialog.value+1
                                     ->theProgressDialog.value->putInt;
                                   ' '->put;
                                   theProgressDialog.maxValue->putInt;
                                   newLine
                                #)
                           #);
                         theProgressDialog.close;
                         
                      #);
                    
                 #);
               
            do theProgressDialog.open; 
            #);
          dynamicCompilationAvailable::  (#  do true->value;  #);
          evaluateFile:: 
            (# ok: @boolean; imp: ^imagePair; pt: @integer; 
            do
               'compiling '->putText;
               name[]->putLine;
               name[]->DC.compileAndLoad->(ok,imp[]);
               (if OK then
                   'it was ok'->putLine;
                   (if imp[] <> none then
                       'foo'->imp.namedOff->pt;
                       (theOrigin[],pt)->DC.makeStruc->P##;
                       
                    else
                       'but the imp was none'->putLine; 
                   if);
                   
               if);
               
            #);
          compilePattern:: 
            (#
               ok: @boolean;
               imp: ^imagePair;
               pt: @integer;
               fname: ^text;
               pname: ^text;
               
            do
               (node.frag.father).fullname->fname[];
               node[]->frigg.MPS.betacfl.getName->pname[];
               fname[]->DC.compileAndLoad->(ok,imp[]);
               (if OK then
                   (if imp[] <> none then
                       pname[]->imp.namedOff->pt;
                       (theOrigin[],pt)->DC.makeStruc->structure##;
                       
                   if);
                   
               if);
               
            #);
          compileFile:: 
            (#
               ok: @boolean;
               imp: ^imagePair;
               pt: @integer;
               fname: ^text;
               pname: ^text;
               
            do
               fname[]->DC.compileAndLoad->(ok,imp[]);
               (if OK then
                   (if imp[] <> none then
                       pname[]->imp.namedOff->pt;
                       (theOrigin[],pt)->DC.makeStruc->structure##;
                       
                   if);
                   
               if);
               
            #);
          
       #);
     DC: @frigg.MPS.AST.dynamicCompiler
       (#
          initDyn:: 
            (# thisExecutable: ^text; tick:< (#  do INNER #); 
            do
               screen[]->info[];
               screen[]->traceStream[];
               (1->Arguments->thisExecutable[],screen[])->NT.dotsInit;
               (thisExecutable[],none )
                 ->ActivateCompiler
                   (#
                      addedFragment::  (#  do tick #);
                      addedToJobFile::  (#  do  #)
                   #);
               (* to initialize 
                * dependency graph *)
               
            #);
          
       #);
     setWindowEnv::  (#  do frigg[]->theWindowEnv[] #);
     
  do 
  #)  

