ORIGIN '../fragmenthandler';
INCLUDE '~beta/guienv/v1.6/private/datastructures/association';



-- fragmentServerSubscribe: doPart --
do (if fragHandler[] = none then
       'fragHandler' -> refNone;
    else
       (if fragHandler.frag[] = none then
           'fragHandler.frag' -> refNone;
        else
           fragHandler[] -> private.insert;
           inner;
       if);
   if);
   
-- fragmentServerUnsubscribe: doPart --
do (if fragHandler[] = none then
       'fragHandler' -> refNone;
    else
       (if fragHandler.frag[] = none then
           'fragHandler.frag' -> refNone;
        else
           fragHandler[] -> private.delete;
           inner;
       if);
   if);

-- fragmentServerNotify: doPart --
do (# theSet: ^private.fragHandlers.element;
   do true -> (frag.father).changed;
      'doneCheck' -> (frag.father).prop.deleteProp;
      frag[] -> private.fragHandlers.lookup -> theSet[];
      (if theSet[] <> none then
          theSet.scan
          (# 
          do current[] -> foreach;
          #);
      if);
   #);
   frag[] -> changed;
   inner;
   
-- fragmentServerNotifyAstReplaced: doPart --
do inner;

-- fragmentServerNotifyAstReplacedSequence: doPart --
do inner;

-- fragmentServerNotifyListElementInserted: doPart --
do inner;

-- fragmentServerNotifyListElementsDeleted: doPart --
do inner;

-- fragmentServerNotifyListElementsReplaced: doPart --
do inner;

-- fragmentServerInit: doPart --
do private.fragHandlers.init;

-- fragmentServerPrivate: descriptor --
(# fragHandlers: @association
     (# MPS: ^astInterface;
        
        key:: MPS.fragmentForm;
        hash::
          (# doHash: @textHash;
          do theKey.name -> doHash -> value;
          #);
        equal::
          (# 
          do (left[] = right[]) -> value;
          #);
        element:: set
          (# element:: fragmentHandler;
          #);
     #);
   insert:
     (# theSet: ^fragHandlers.element;
        fragHandler: ^fragmentHandler;
     enter fragHandler[]
     do fragHandler.frag[] -> fragHandlers.lookup -> theSet[];
        (if theSet[] = none then
            &fragHandlers.element[] -> theSet[];
            theSet.init;
            (fragHandler.frag[], theSet[]) -> fragHandlers.insert;
        if);
        fragHandler[] -> theSet.insert;
     #);
   delete:
     (# theSet: ^fragHandlers.element;
        fragHandler: ^fragmentHandler;
     enter fragHandler[]
     do fragHandler.frag[] -> fragHandlers.lookup -> theSet[];
        (if theSet[] <> none then
            fragHandler[] -> theSet.delete;
        if);
     #);
#)

-- astCompare: doPart --
do (# 
   do (if (left[], right[]) -> astEqual then
          equal;
       else
          (if (right[], left[]) -> isAncestor then
              rightAncestor;
           else
              (if (left[], right[]) -> isAncestor then
                  leftAncestor;
               else
                  unRelated;
              if);
          if);
      if);
   #);
-- astEqual: doPart --
do (left.index = right.index) and (left.frag[] = right.frag[]) -> value;
   
-- isAncestor: doPart --
do false -> value;
   l: (# 
      do child.father -> child[];
         (if child[] <> none then
             (if (anc[], child[]) -> astEqual then
                 true -> value;
              else
                 restart l;
             if);
         if);
      #);
