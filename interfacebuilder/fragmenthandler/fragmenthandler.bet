ORIGIN '~beta/mps/v5.2/astlevel';
BODY 'private/fragmenthandlerbody';

INCLUDE '~beta/containers/v1.6/sets';

-- astInterfaceLib: attributes --

myAstList: set
  (# element:: AST;
  #);

-- lib: attributes --

fragmentServer:
  (# <<SLOT fragmentServerLib: attributes>>;
     
     subscribe:<
       (# fragHandler: ^fragmentHandler;
          refNone:< exception
            (# what: ^text;
            enter what[]
            do what[] -> msg.append;
               ' is none in fragmentServer.subscribe' -> msg.append;
               inner;
            #);
       enter fragHandler[]
       <<SLOT fragmentServerSubscribe: doPart>>
       #);
     unsubscribe:<
       (# fragHandler: ^fragmentHandler;
          refNone:< exception
            (# what: ^text;
            enter what[]
            do what[] -> msg.append;
               ' is none in fragmentServer.unsubscribe' -> msg.append;
               inner;
            #);
       enter fragHandler[]
       <<SLOT fragmentServerUnsubscribe: doPart>>
       #);
     
     notify:
       (# frag: ^astInterface.fragmentForm;
          forEach:<
            (# current: ^fragmentHandler;
            enter current[]
            do inner;
            #);
          fromInterfaceBuilder: @boolean;
       enter (fromInterfaceBuilder, frag[])
       <<SLOT fragmentServerNotify: doPart>>
       #);
     notifyAstReplaced:< notify
       (# oldAst, newAst: ^astInterface.ast;
          forEach::< (# do (oldAst[], newAst[]) -> current.astReplaced; inner #);
       enter (oldAst[], newAst[])
       <<SLOT fragmentServerNotifyAstReplaced: doPart>>
       #);
     notifyAstReplacedSequence:< notify
       (# rootOfSequence: ^astInterface.ast;
          forEach::< (# do rootOfSequence[] -> current.astReplacedSequence; inner #);
       enter rootOfSequence[]
       <<SLOT fragmentServerNotifyAstReplacedSequence: doPart>>
       #);
     notifyListElementInserted:< notify
       (# position: @integer; listNode: ^astInterface.expanded;
          forEach::< (# do (listNode[], position) -> current.listElementInserted; inner  #);
       enter (listNode[], position)
       <<SLOT fragmentServerNotifyListElementInserted: doPart>>
       #);
     notifyListElementsDeleted:< notify
       (# oldElements: ^astInterface.myAstList;
          listNode: ^astInterface.expanded;
          position, length: @integer;
          forEach::< 
            (# 
            do (listNode[], position, length, oldElements[]) 
                 -> current.listElementsDeleted; 
               inner 
            #);
       enter (listNode[], position, length, oldElements[])
       <<SLOT fragmentServerNotifyListElementsDeleted: doPart>>
       #);
     notifyListElementsReplaced:< notify
       (# oldElements: ^astInterface.myAstList;
          listNode: ^astInterface.expanded;
          position, length, newLength: @integer;
          forEach::< 
            (# 
            do (listNode[], position, length, oldElements[], newLength) 
                 -> current.listElementsReplaced;
               inner 
            #);
       enter (listNode[], position, length, oldElements[], newLength)
       <<SLOT fragmentServerNotifyListElementsReplaced: doPart>>
       #);
     
     init:
       (# <<SLOT fragmentServerInit: doPart>> #);
     private: @<<SLOT fragmentServerPrivate: descriptor>>;
  #);

fragmentHandler:
  (# <<SLOT fragmentHandlerLib: attributes>>;
     
     frag: ^astInterface.fragmentForm;
     
     astReplaced:<
       (# oldAst, newAst: ^astInterface.ast; 
       enter (oldAst[], newAst[])
       do inner;
       #);
     astReplacedSequence:<
       (# rootOfSequence: ^astInterface.ast
       enter rootOfSequence[]
       do inner;
       #);
     listElementInserted:<
       (# position: @integer; listNode: ^astInterface.expanded; 
       enter (listNode[], position)
       do inner;
       #);
     listElementsDeleted:<
       (# oldElements: ^astInterface.myAstList;
          listNode: ^astInterface.expanded;
          position, length: @integer
       enter (listNode[], position, length, oldElements[])
       do inner;
       #);
     listElementsReplaced:<
       (# oldElements: ^astInterface.myAstList;
          listNode: ^astInterface.expanded;
          position, length, newLength: @integer
       enter (listNode[], position, length, oldElements[], newLength)
       do inner;
       #);
  #);

astCompare:
  (# leftAncestor:<
       (# do inner #);
     rightAncestor:<
       (# do inner #);
     equal:<
       (# do inner #);
     unRelated:<
       (# do inner #);
     left, right: ^astInterface.AST;
  enter (left[], right[])
  <<SLOT astCompare: doPart>>
  #);
astEqual: booleanValue
  (# left, right: ^astInterface.AST;
  enter (left[], right[])
  <<SLOT astEqual: doPart>>
  #);
isAncestor: booleanValue
  (# anc, child: ^astInterface.AST;
     father: ^astInterface.AST;
  enter (anc[], child[])
  <<SLOT isAncestor: doPart>>
  #);
