ORIGIN 'component';
INCLUDE '~beta/betaast/betasematt';
-- betaPatternLib: Attributes --
check:
  (# anAst: ^astInterface.ast; 
  enter anAst[]
  do
     (if anAst[] <> none then
         (if anAst.kind <> 1 (* Interior *) then none ->anAst[];  if); 
     if);
     
  exit anAst[]
  #);
parseText:
  (#
     frag: ^astInterface.fragmentForm;
     txt: ^text;
     category: @integer;
     node: ^astInterface.AST;
     
  enter (frag[],txt[],category)
  do
       (#
          root: ^astInterface.AST;
          fatal: @boolean;
          errorText: @text;
          parseOK: @boolean;
          
       do
          frag.root[]->root[];
          txt.reset;
          (category,txt[],screen[],frag[])
            ->frag.grammar.parser.doParse
              (#
                 fatalParseError::< 
                   (# 
                   do
                      'Parse errors: '->errorText.putText;
                      msg[]->errorText.putLine;
                      true->fatal;
                      true->continue;
                      
                   #);
                 
              #)->parseOK;
          (if parseOK then
              frag.root[]->node[]; 
           else
              (if not fatal then
                  txt.Reset;
                  (txt[],errorText[])->frag.grammar.parser.errorReport;
                  
              if);
              txt[]->screen.putLine;
              
          if);
          root[]->frag.root[];
          
       #);
     
  exit node[]
  #);
findObjectDescriptor:
  (#
     theObjectDescriptor: ^astInterface.beta.objectDescriptor;
     theReferenceSpecification: ^astInterface.beta.referenceSpecification;
     theStaticItem: ^astInterface.beta.staticItem;
     thePatternDecl: ^astInterface.beta.patternDecl;
     theObjectSpecification: ^astInterface.beta.objectSpecification;
     theSimpleDecl: ^astInterface.beta.simpleDecl;
     theBindingDecl: ^astInterface.beta.bindingDecl;
     
  do
     (if node.symbol
      // gram.patternDecl then
         node[]->thePatternDecl[];
         thePatternDecl.getObjectDescriptor->check->theObjectDescriptor[];
         
      // gram.simpleDecl then
         node[]->theSimpleDecl[];
         theSimpleDecl.getReferenceSpecification->check
           ->theReferenceSpecification[];
         (if theReferenceSpecification[] <> none then
             (if theReferenceSpecification.symbol
              // gram.staticItem then
                 theReferenceSpecification[]->theStaticItem[];
                 theStaticItem.getObjectSpecification->check
                   ->theObjectSpecification[];
                 (if theObjectSpecification[] <> none then
                     (if theObjectSpecification.symbol
                      // gram.objectDescriptor then
                         theObjectSpecification[]->theObjectDescriptor[]; 
                     if);
                     
                 if);
                 
             if);
             
         if);
         
      // gram.bindingDecl then
         node[]->theBindingDecl[];
         theBindingDecl.getObjectSpecification->check->theObjectSpecification[];
         (if theObjectSpecification[] <> none then
             (if theObjectSpecification.symbol
              // gram.objectDescriptor then
                 theObjectSpecification[]->theObjectDescriptor[]; 
             if);
             
         if);
         
     if);
     
  exit theObjectDescriptor[]
  #);
findAttributes:
  (#
     theObjectDescriptor: ^astInterface.beta.objectDescriptor;
     theMainPart: ^astInterface.beta.mainPart;
     theAttributes: ^astInterface.beta.attributes;
     
  do
     findObjectDescriptor->theObjectDescriptor[];
     (if theObjectDescriptor[] <> none then
         theObjectDescriptor.getMainPart->check->theMainPart[];
         (if theMainPart[] <> none then
             theMainPart.getAttributes->check->theAttributes[]; 
         if);
         
     if);
     
  exit theAttributes[]
  #);
  

-- betaPatternGetKind: DoPart --
do
     (#
        theReferenceSpecification: ^astInterface.beta.referenceSpecification;
        theStaticItem: ^astInterface.beta.staticItem;
        theObjectSpecification: ^astInterface.beta.objectSpecification;
        theSimpleDecl: ^astInterface.beta.simpleDecl;
        
     do
        illegalKind->theKind;
        (* Default to illegalKind *)
        (if node.symbol
         // gram.patternDecl then
            patternKind->theKind; 
         // gram.simpleDecl then
            node[]->theSimpleDecl[];
            theSimpleDecl.getReferenceSpecification->check
              ->theReferenceSpecification[];
            (if theReferenceSpecification[] <> none then
                (if theReferenceSpecification.symbol
                 // gram.staticItem then
                    theReferenceSpecification[]->theStaticItem[];
                    theStaticItem.getObjectSpecification->check
                      ->theObjectSpecification[];
                    (if theObjectSpecification[] <> none then
                        (if theObjectSpecification.symbol
                         // gram.objectDescriptor then
                            singularItemKind->theKind; 
                         else
                            itemKind->theKind; 
                        if);
                        
                    if);
                    
                if);
                
            if);
            
        if);
        
     #);
     

-- betaPatternSetName: DoPart --
do
     (#
        theNames: ^astInterface.beta.names;
        old: ^astInterface.ast;
        new: ^astInterface.beta.nameDcl;
        theNameDecl: ^astInterface.nameDecl;
        
     do
        node.getson1->check->theNames[];
        (if theNames[] <> none then
            1->theNames.get->check->old[];
            (theNames.frag[],theName[],gram.nameDcl)->parseText->new[];
            (1,new[])->theNames.put;
            (* AST-REPLACED *)
            
        if);
        
     #);
     

-- betaPatternGetName: DoPart --
do
     (#
        theNames: ^astInterface.beta.names;
        theNameDcl: ^astInterface.beta.nameDcl;
        theNameDecl: ^astInterface.nameDecl;
        
     do
        node.getson1->check->theNames[];
        (if theNames[] <> none then
            1->theNames.get->check->theNameDcl[];
            (if theNameDcl[] <> none then
                theNameDcl.getNameDecl->theNameDecl[];
                (if theNameDecl[] <> none then
                    theNameDecl.getText->theName[]; 
                if);
                
            if);
            
        if);
        
     #);
     

-- betaPatternSetPrefix: DoPart --
do
     (#
        theObjectDescriptor: ^astInterface.beta.objectDescriptor;
        old,new: ^astInterface.ast;
        theReferenceSpecification: ^astInterface.beta.referenceSpecification;
        theStaticItem: ^astInterface.beta.staticItem;
        thePatternDecl: ^astInterface.beta.patternDecl;
        theObjectSpecification: ^astInterface.beta.objectSpecification;
        theSimpleDecl: ^astInterface.beta.simpleDecl;
        
     do
        (if node.symbol
         // gram.patternDecl then
            node[]->thePatternDecl[];
            thePatternDecl.getObjectDescriptor->check->theObjectDescriptor[];
            (if theObjectDescriptor[] <> none then
                theObjectDescriptor.getPrefixOpt->old[];
                (theObjectDescriptor.frag[],thePrefix[],gram.prefix)->parseText
                  ->new[];
                new[]->theObjectDescriptor.putPrefixOpt;
                (* AST-REPLACED *)
                
            if);
            
         // gram.simpleDecl then
            node[]->theSimpleDecl[];
            theSimpleDecl.getReferenceSpecification->check
              ->theReferenceSpecification[];
            (if theReferenceSpecification[] <> none then
                (if theReferenceSpecification.symbol
                 // gram.staticItem then
                    theReferenceSpecification[]->theStaticItem[];
                    theStaticItem.getObjectSpecification->check
                      ->theObjectSpecification[];
                    (if theObjectSpecification[] <> none then
                        (if theObjectSpecification.symbol
                         // gram.objectDescriptor then
                            theObjectDescriptor.getPrefixOpt->old[];
                            (theObjectDescriptor.frag[],thePrefix[],gram.prefix)
                              ->parseText->new[];
                            new[]->theObjectDescriptor.putPrefixOpt;
                            (* AST-REPLACED *)
                            
                         else
                            (theStaticItem.frag[],thePrefix[],gram.nameApl)
                              ->parseText->theStaticItem.putObjectSpecification;
                            (* AST-REPLACED *)
                            
                        if);
                        
                    if);
                    
                if);
                
            if);
            
        if);
        
     #);
     

-- betaPatternGetPrefix: DoPart --
do
     (#
        theObjectDescriptor: ^astInterface.beta.objectDescriptor;
        old,new: ^astInterface.ast;
        theReferenceSpecification: ^astInterface.beta.referenceSpecification;
        theStaticItem: ^astInterface.beta.staticItem;
        thePatternDecl: ^astInterface.beta.patternDecl;
        theObjectSpecification: ^astInterface.beta.objectSpecification;
        theSimpleDecl: ^astInterface.beta.simpleDecl;
        theAttributeDenotation: ^astInterface.beta.attributeDenotation;
        thePrefixNode: ^astInterface.beta.prefix;
        theNameApl: ^astInterface.beta.nameApl;
        
     do
        (if node.symbol
         // gram.patternDecl then
            node[]->thePatternDecl[];
            thePatternDecl.getObjectDescriptor->check->theObjectDescriptor[];
            (if theObjectDescriptor[] <> none then
                theObjectDescriptor.getPrefixOpt->check->thePrefixNode[];
                (if thePrefixNode[] <> none then
                    thePrefixNode.getAttributeDenotation->check
                      ->theAttributeDenotation[];
                    (if theAttributeDenotation[] <> none then
                        (if theAttributeDenotation.symbol = gram.nameApl then
                            theAttributeDenotation[]->theNameApl[];
                            theNameApl.getText->thePrefix[];
                            (if thePrefix.empty then none ->thePrefix[];  if);
                            
                        if);
                        
                    if);
                    
                if);
                
            if);
            
         // gram.simpleDecl then
            node[]->theSimpleDecl[];
            theSimpleDecl.getReferenceSpecification->check
              ->theReferenceSpecification[];
            (if theReferenceSpecification[] <> none then
                (if theReferenceSpecification.symbol
                 // gram.staticItem then
                    theReferenceSpecification[]->theStaticItem[];
                    theStaticItem.getObjectSpecification->check
                      ->theObjectSpecification[];
                    (if theObjectSpecification[] <> none then
                        (if theObjectSpecification.symbol
                         // gram.objectDescriptor then
                            theObjectSpecification[]->theObjectDescriptor[];
                            theObjectDescriptor.getPrefixOpt->check
                              ->thePrefixNode[];
                            (if thePrefixNode[] <> none then
                                thePrefixNode.getAttributeDenotation->check
                                  ->theAttributeDenotation[];
                                (if theAttributeDenotation[] <> none then
                                    (if theAttributeDenotation.symbol =
                                    gram.nameApl then
                                        theAttributeDenotation[]->theNameApl[];
                                        theNameApl.getText->thePrefix[];
                                        (if thePrefix.empty then
                                            none ->thePrefix[]; 
                                        if);
                                        
                                    if);
                                    
                                if);
                                
                            if);
                            
                         else
                            (if theObjectSpecification.symbol = gram.nameApl
                             then
                                theObjectSpecification[]->theNameApl[];
                                theNameApl.getText->thePrefix[];
                                (if thePrefix.empty then
                                    none ->thePrefix[]; 
                                if);
                                
                            if);
                            
                        if);
                        
                    if);
                    
                if);
                
            if);
            
        if);
        
     #);
     

-- betaPatternSetPrefixNode: DoPart --
do   

-- betaPatternGetPrefixNode: DoPart --
do   

-- betaPatternScan: DoPart --
do
     (# theAttributes: ^astInterface.beta.attributes; 
     do
        findAttributes->theAttributes[];
        (if theAttributes[] <> none then
            theAttributes.scan
              (# 
              do
                 (if current.kind = 1 then
                     current[]->THIS(scan).current[]; INNER scan; 
                 if);
                 
              #);
            
        if);
        
     #);
     

-- betaPatternGetAttributes: DoPart --
do findAttributes->theAttributes[];   

-- betaPatternAppend: DoPart --
do
     (#
        theAttributes: ^astInterface.beta.attributes;
        son1: ^astInterface.ast;
        doReplace: @boolean;
        insertInx: @integer;
        
     do
        findAttributes->theAttributes[];
        (if theAttributes[] <> none then
            (if theAttributes.noOfsons = 1 then
                1->theAttributes.get->son1[];
                (if son1.kind = 3 (* optional *) then true->doReplace;  if);
                
            if);
            (if doReplace then
                (1,child[])->theAttributes.put;
                (son1[],child[])->theAttributes.frag.notifyAstReplaced;
                (* DANGER!!! *)
                
             else
                theAttributes.noOfSons+1->insertInx;
                child[]->theAttributes.append;
                (theAttributes[],insertInx)
                  ->theAttributes.frag.notifyListElementInserted;
                (* DANGER!!! *)
                
            if);
            
        if);
        
     #);
     

-- betaPatternFind: DoPart --
do   

-- betaPatternGetComment: DoPart --
do
     (#
        clone: ^astInterface.expanded;
        type: ##astInterface.AST;
        removeStar:
          (# in,out: ^text; 
          enter in[]
          do
             &text[]->out[];
             in.scanAll
               (#  do (if ch <> '*' then ch->out.put;  if);  #);
             
          exit out[]
          #);
        
     do
        node##->type##;
        &type[]->clone[];
        node.index->clone.index;
        node.frag[]->clone.frag[];
        &text[]->comment[];
        clone.suffixWalk
          (# cutIf::  (#  do prod = gram.mainPart->toCut #)
          do
             current.scanComments
               (# 
               do
                  (if current[] <> none then
                      current[]->removeStar->comment.append; 
                  if);
                  
               #)
          #);
        
     #);
     

