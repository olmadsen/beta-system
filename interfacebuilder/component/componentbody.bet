ORIGIN 'component';
INCLUDE '~beta/betaast/v5.2/betasematt';

-- componentLib: attributes --
check:
	(# anAst: ^astInterface.ast;
	enter anAst[]
	do (if anAst[] <> NONE then
			(if anAst.kind <> 1 (* Interior *) then
				NONE -> anAst[];
			if);
		if);
	exit anAst[]
	#);
parseText:
  (# frag: ^astInterface.fragmentForm;
     txt: ^text;
     category: @integer;
     node: ^astInterface.AST;
  enter (frag[], txt[], category)
  do (# root: ^astInterface.AST;
			fatal: @boolean;
			errorText: @text;
			parseOK: @boolean;
		do frag.root[] -> root[];
			txt.reset;
			(category,txt[],screen[],frag[]) -> frag.grammar.parser.doParse
			(#
				fatalParseError::<
				  (#
				  do
					  'Parse errors: ' -> errorText.putText;
					  msg[] -> errorText.putLine;
					  true -> fatal;
					  true -> continue;
				  #);
			#) -> parseOK;
			(if parseOK then
				 frag.root[] -> node[];
			 else
				 (if not fatal then
					  txt.Reset;
					  (txt[],errorText[]) -> frag.grammar.parser.errorReport;
				 if);
				 txt[] -> screen.putLine;
			if);
			root[] -> frag.root[];
		#);
  exit node[]
  #);
findObjectDescriptor:
	(# theObjectDescriptor: ^astInterface.beta.objectDescriptor;
		theReferenceSpecification: ^astInterface.beta.referenceSpecification;
		theStaticItem: ^astInterface.beta.staticItem;
		thePatternDecl: ^astInterface.beta.patternDecl;
		theObjectSpecification: ^astInterface.beta.objectSpecification;
		theSimpleDecl: ^astInterface.beta.simpleDecl;
	do (if node.symbol
			//gram.patternDecl then
				node[] -> thePatternDecl[];
				thePatternDecl.getObjectDescriptor -> check -> theObjectDescriptor[];
			//gram.simpleDecl then
				node[] -> theSimpleDecl[];
				theSimpleDecl.getReferenceSpecification -> check -> theReferenceSpecification[];
				(if theReferenceSpecification[] <> NONE then
					(if theReferenceSpecification.symbol
						//gram.staticItem then
							theReferenceSpecification[] -> theStaticItem[];
							theStaticItem.getObjectSpecification -> check -> theObjectSpecification[];
							(if theObjectSpecification[] <> NONE then
								(if theObjectSpecification.symbol
									//gram.objectDescriptor then
										theObjectSpecification[] -> theObjectDescriptor[];
								if);
							if);
					if);
				if);
		if);
	exit theObjectDescriptor[]
	#);
findAttributes:
	(# theObjectDescriptor: ^astInterface.beta.objectDescriptor;
		theMainPart: ^astInterface.beta.mainPart;
		theAttributes: ^astInterface.beta.attributes;
	do findObjectDescriptor -> theObjectDescriptor[];
		(if theObjectDescriptor[] <> NONE then
			theObjectDescriptor.getMainPart -> check -> theMainPart[];
			(if theMainPart[] <> NONE then
				theMainPart.getAttributes -> check -> theAttributes[];
			if);
		if);
	exit theAttributes[]
	#);
-- componentGetKind: doPart --
do (# theReferenceSpecification: ^astInterface.beta.referenceSpecification;
		theStaticItem: ^astInterface.beta.staticItem;
		theObjectSpecification: ^astInterface.beta.objectSpecification;
		theSimpleDecl: ^astInterface.beta.simpleDecl;
	do illegalKind -> theKind; (* Default to illegalKind *)
		(if node.symbol
			//gram.patternDecl then
				patternKind -> theKind;
			//gram.simpleDecl then
				node[] -> theSimpleDecl[];
				theSimpleDecl.getReferenceSpecification -> check -> theReferenceSpecification[];
				(if theReferenceSpecification[] <> NONE then
					(if theReferenceSpecification.symbol
						//gram.staticItem then
							theReferenceSpecification[] -> theStaticItem[];
							theStaticItem.getObjectSpecification -> check -> theObjectSpecification[];
							(if theObjectSpecification[] <> NONE then
								(if theObjectSpecification.symbol
									//gram.objectDescriptor then
										singularItemKind -> theKind;
								else
										itemKind -> theKind;
								if);
							if);
					if);
				if);
		if);
	#);

-- componentSetName: doPart --
do (# theNames: ^astInterface.beta.names;
		old: ^astInterface.ast;
		new: ^astInterface.beta.nameDcl;
		theNameDecl: ^astInterface.nameDecl;
	do node.getson1 -> check -> theNames[];
		(if theNames[] <> NONE then
			1 -> theNames.get -> check -> old[];
			(theNames.frag[], theName[], gram.nameDcl) -> parseText -> new[];
			(1, new[]) -> theNames.put;
			(* AST-REPLACED *)
		if);
	#);

-- componentGetName: doPart --
do (# theNames: ^astInterface.beta.names;
		theNameDcl: ^astInterface.beta.nameDcl;
		theNameDecl: ^astInterface.nameDecl;
	do node.getson1 -> check -> theNames[];
		(if theNames[] <> NONE then
			1 -> theNames.get -> check -> theNameDcl[];
			(if theNameDcl[] <> NONE then
				theNameDcl.getNameDecl -> theNameDecl[];
				(if theNameDecl[] <> NONE then
					theNameDecl.getText -> theName[];
				if);
			if);
		if);
	#);

-- componentSetPrefix: doPart --
do (# theObjectDescriptor: ^astInterface.beta.objectDescriptor;
	 	old, new: ^astInterface.ast;
		theReferenceSpecification: ^astInterface.beta.referenceSpecification;
		theStaticItem: ^astInterface.beta.staticItem;
		thePatternDecl: ^astInterface.beta.patternDecl;
		theObjectSpecification: ^astInterface.beta.objectSpecification;
		theSimpleDecl: ^astInterface.beta.simpleDecl;
	do (if node.symbol
			//gram.patternDecl then
				node[] -> thePatternDecl[];
				thePatternDecl.getObjectDescriptor -> check -> theObjectDescriptor[];
				(if theObjectDescriptor[] <> NONE then
					theObjectDescriptor.getPrefixOpt -> old[];
					(theObjectDescriptor.frag[], thePrefix[], gram.prefix) 
						-> parseText -> new[];
					new[] -> theObjectDescriptor.putPrefixOpt;
					(* AST-REPLACED *)
				if);
			//gram.simpleDecl then
				node[] -> theSimpleDecl[];
				theSimpleDecl.getReferenceSpecification -> check -> theReferenceSpecification[];
				(if theReferenceSpecification[] <> NONE then
					(if theReferenceSpecification.symbol
						//gram.staticItem then
							theReferenceSpecification[] -> theStaticItem[];
							theStaticItem.getObjectSpecification -> check -> theObjectSpecification[];
							(if theObjectSpecification[] <> NONE then
								(if theObjectSpecification.symbol
									//gram.objectDescriptor then
										theObjectDescriptor.getPrefixOpt -> old[];
										(theObjectDescriptor.frag[], thePrefix[], gram.prefix) 
											-> parseText -> new[];
										new[] -> theObjectDescriptor.putPrefixOpt;
										(* AST-REPLACED *)
								else
									(theStaticItem.frag[], thePrefix[], gram.nameApl)
										-> parseText
										-> theStaticItem.putObjectSpecification;
									(* AST-REPLACED *)
								if);
							if);
					if);
				if);
		if);
	#);

-- componentGetPrefix: doPart --
do (# theObjectDescriptor: ^astInterface.beta.objectDescriptor;
	 	old, new: ^astInterface.ast;
		theReferenceSpecification: ^astInterface.beta.referenceSpecification;
		theStaticItem: ^astInterface.beta.staticItem;
		thePatternDecl: ^astInterface.beta.patternDecl;
		theObjectSpecification: ^astInterface.beta.objectSpecification;
		theSimpleDecl: ^astInterface.beta.simpleDecl;
		theAttributeDenotation: ^astInterface.beta.attributeDenotation;
		thePrefixNode: ^astInterface.beta.prefix;
		theNameApl: ^astInterface.beta.nameApl;
	do (if node.symbol
			//gram.patternDecl then
				node[] -> thePatternDecl[];
				thePatternDecl.getObjectDescriptor -> check -> theObjectDescriptor[];
				(if theObjectDescriptor[] <> NONE then
					theObjectDescriptor.getPrefixOpt -> check -> thePrefixNode[];
					(if thePrefixNode[] <> NONE then
						thePrefixNode.getAttributeDenotation->check->theAttributeDenotation[];
						 (if theAttributeDenotation[] <> NONE then
							 (if theAttributeDenotation.symbol = gram.nameApl then
								theAttributeDenotation[]->theNameApl[];
								theNameApl.getText->thePrefix[];
								(if thePrefix.empty then
									 none ->thePrefix[]; 
								if);
							 if);
						 if);
					if);
				if);
			//gram.simpleDecl then
				node[] -> theSimpleDecl[];
				theSimpleDecl.getReferenceSpecification -> check -> theReferenceSpecification[];
				(if theReferenceSpecification[] <> NONE then
					(if theReferenceSpecification.symbol
						//gram.staticItem then
							theReferenceSpecification[] -> theStaticItem[];
							theStaticItem.getObjectSpecification -> check -> theObjectSpecification[];
							(if theObjectSpecification[] <> NONE then
								(if theObjectSpecification.symbol
									//gram.objectDescriptor then
										theObjectDescriptor.getPrefixOpt -> check -> thePrefixNode[];
										(if thePrefixNode[] <> NONE then
											thePrefixNode.getAttributeDenotation->check->theAttributeDenotation[];
											 (if theAttributeDenotation[] <> NONE then
												 (if theAttributeDenotation.symbol = gram.nameApl then
													theAttributeDenotation[]->theNameApl[];
													theNameApl.getText->thePrefix[];
													(if thePrefix.empty then
														 none ->thePrefix[]; 
													if);
												 if);
											 if);
										if);
								else
									(if theObjectSpecification.symbol = gram.nameApl then
										theObjectSpecification[]->theNameApl[];
										theNameApl.getText->thePrefix[];
										(if thePrefix.empty then
											 none ->thePrefix[]; 
										if);
									 if);
								if);
							if);
					if);
				if);
		if);
	#);

	

-- componentSetPrefixNode: doPart --
do 

-- componentGetPrefixNode: doPart --
do

-- componentScan: doPart --
do (# theAttributes: ^astInterface.beta.attributes;
   do findAttributes -> theAttributes[];
		(if theAttributes[] <> NONE then
			theAttributes.scan
				(# 
				do (if current.kind = 1 then
						  current[] -> this(scan).current[];
						  inner scan;
					if);
				#);
		if);
	#);

-- componentGetAttributes: doPart --
do findAttributes -> theAttributes[];

-- componentFind: doPart --
do 
