ORIGIN 'editorenvprivate';
INCLUDE '../../code/createmps';
INCLUDE '../../dialogs/parseerrorsdialog';

-- editorEnvLib: attributes --
parseErrorsDialogWindow: window
  (# errors: ^text;
     theParseErrorsDialog: @parseErrorsDialog
       (# 
          open::
            (# 
            do this(parseErrorsDialogWindow).size -> size;
               true -> bindRight -> bindBottom;
            #);
       #);
     open::<
       (# errors: ^text;
       enter errors[]
       do 'Parse errors' -> title;
          (contents, errors[]) -> theParseErrorsDialog.open;
       #);
     close::<
       (# 
       do none -> this(editorEnv).private.theWindow[];
       #);
  #);

-- IBeditorEnvInit: doPart --
do (if not private.initialized then
       true -> private.initialized;
       (if MPS[]//None then
           createMPS -> MPS[];
           
       if);
       MPS[] -> objectPool.put;
       (if pretty[]//none then
           &MPS.prettyPrinter[] -> pretty[];
           pretty.init;
       if);
       pretty[] -> objectPool.put;
       (if betaGram[]//none then
           MPS.grammarTable.beta[] -> betaGram[];
       if);
       thePathHandler.init;
   if);

-- IBeditorEnvDoPart: doPart --
do init;
   inner;

-- editorEnvThePathHandlerInit: doPart --
do currentDirectory -> private.basis[] -> makePretty;
   

-- editorEnvthePathHandlerlocalize: doPart --
do 
   l: (path[], private.basis[]) -> localPath
   (# 
      localPathException::<
        (# 
        do path[] -> local[];
           failure;
           leave l;
        #);
   #) -> local[];

-- editorEnvthePathHandlerprivate: descriptor --
(# basis: ^text;
#)

-- thePathHandlerLib: attributes --
makePretty:
  (# path: ^text;
     homeIndex, machineIndex: @integer;
  enter path[]
  do 
     (if (1 -> path.inxGet) = '/' then
         (if (2 -> path.inxGet) = 'a' then
             (if (3 -> path.inxGet) = '/' then
                 4 -> homeIndex;
             if);
          else
             2 -> homeIndex;
         if);
         (if homeIndex > 1 then
             (if 'home' -> ((homeIndex, homeIndex + 3) -> path.sub).equal then
                 homeIndex + 5 -> machineIndex;
                 machineIndex -> path.setPos;
                 path.scan
                 (# while::<
                      (# 
                      do ch <> '/' -> value;
                         machineIndex + 1 -> machineIndex;
                      #);
                 #);
                 (1, machineIndex) -> path.delete;
                 '/users/' -> path.prepend;
             if);
         if);
     if);
     (if (path.length -> path.inxGet) = '/' then
         (path.length, path.length) -> path.delete;
     if);
  #)
-- editorEnvthePathHandlerworkingDirectory: doPart --
do private.basis[] -> path[];

-- EditorEnvShowParseErrors: doPart --
do (# theWindow: ^parseErrorsDialogWindow;
   do (if private.theWindow[] <> none then
          private.theWindow.close;
      if);
      &parseErrorsDialogWindow[] -> theWindow[];
      errors[] -> theWindow.open;
      theWindow[] -> private.theWindow[];
   #);
-- editorEnvSaveGroup: doPart --
do (# f: @file;
      fileName: ^text;
   do (group.name).copy -> fileName[];
      '.bet' -> fileName.append;
      fileName[] -> f.name;
      f.openWrite;
      (group[], f[]) -> pretty.printGroup;
      f.close;
      group.fragmentList.scan
      (# frag: ^astInterface.fragmentForm;
      do (if current.type = MPS.formType then
             current.open -> frag[];
             frag.recomputeSlotChain;
         if);
      #);
      'doneCheck' -> group.prop.deleteProp;
      group.markAsChanged;
   #);
