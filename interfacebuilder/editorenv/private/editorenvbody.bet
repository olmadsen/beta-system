ORIGIN 'editorenvprivate';
INCLUDE '~beta/interfacebuilder/v1.0/code/createmps';
INCLUDE '~beta/interfacebuilder/v1.0/dialogs/parseerrorsdialog';

-- editorEnvLib: attributes --
parseErrorsDialogWindow: window
  (# errors: ^text;
     theParseErrorsDialog: @parseErrorsDialog
       (# 
          open::
            (# 
            do this(parseErrorsDialogWindow).size -> size;
               true -> bindRight -> bindBottom;
            #);
       #);
     open::<
       (# errors: ^text;
       enter errors[]
       do 'Parse errors' -> title;
          (contents, errors[]) -> theParseErrorsDialog.open;
       #);
  #);

-- IBeditorEnvInit: doPart --
do (if not private.initialized then
       true -> private.initialized;
       (if MPS[]//None then
           createMPS -> MPS[];
           
       if);
       MPS[] -> objectPool.put;
       (if pretty[]//none then
           &MPS.prettyPrinter[] -> pretty[];
           pretty.init;
       if);
       pretty[] -> objectPool.put;
       (if betaGram[]//none then
           MPS.grammarTable.beta[] -> betaGram[];
       if);
       thePathHandler.init;
       private.classInfo.init;
       ('interfaceObject', none) -> private.classInfo.add
       (# 
       do ('onMouseUp', true) -> addMethod;
          ('onMouseDown', true) -> addMethod;
          ('onKeyDown', true) -> addMethod;
          ('onRefresh', true) -> addMethod;
          ('onActivate', true) -> addMethod;
          ('onDeactivate', true) -> addMethod;
          
          ('open', false) -> addMethod;
          ('close', false) -> addMethod;
          
          ('windowitem', class[]) -> private.classInfo.add 
          (# 
          do ('onVisibleChanged', true) -> addMethod;
             ('onFrameChanged', true) -> addMethod;
             ('onFatherFrameChanged', true) -> addMethod;
             ('onEnableChanged', true) -> addMethod;
             ('onEnableTarget', true) -> addMethod;
             ('onDisableTarget', true) -> addMethod;
             ('onBorderStyleChanged', true) -> addMethod;
             ('onBorderVisibleChanged', true) -> addMethod;
             ('onTheCursorChanged', true) -> addMethod;
             ('onHiliteChanged', true) -> addMethod;
             
             ('separator', class[]) -> private.classInfo.add;
             ('canvas', class[]) -> private.classInfo.add
             (# 
             do ('onChildFrameChanged', true) -> addMethod;
                ('abstractScroller', class[]) -> private.classInfo.add
                (# 
                do
                   ('scroller', class[]) -> private.classInfo.add;
                   ('textEditor', class[]) -> private.classInfo.add;
                #);
             #);
             ('control', class[]) -> private.classInfo.add
             (# 
             do ('button', class[]) -> private.classInfo.add
                (# 
                do ('onLabelChanged', true) -> addMethod;
                   ('onStyleChanged', true) -> addMethod;
                   
                   ('pushButton', class[]) -> private.classInfo.add;
                   ('iconButton', class[]) -> private.classInfo.add
                   (# 
                   do ('onShowLabelChanged', true) -> addMethod;
                      ('onIconChanged', true) -> addMethod;
                   #);
                   ('optionButton', class[]) -> private.classInfo.add
                   (# 
                   do ('onCurrentItemChanged', true) -> addMethod;
                      ('onPopupMenuChanged', true) -> addMethod;
                   #);
                   ('staticText', class[]) -> private.classInfo.add;
                   ('toggleButton', class[]) -> private.classInfo.add
                   (# 
                   do ('onStateChanged', true) -> addMethod;
                      ('radioButton', class[]) -> private.classInfo.add;
                      ('checkBox', class[]) -> private.classInfo.add;
                   #);
                #);
                ('scrollbar', class[]) -> private.classInfo.add
                (# 
                do ('onThumbMoved', true) -> addMethod;
                   ('onPageDown', true) -> addMethod;
                   ('onPageUp', true) -> addMethod;
                   ('onButtonDown', true) -> addMethod;
                   ('onButtonUp', true) -> addMethod;
                   ('onPageScrollAmountChanged', true) -> addMethod;
                   ('onScrollAmountChanged', true) -> addMethod;
                   ('onMaxValueChanged', true) -> addMethod;
                   ('onValueChanged', true) -> addMethod;
                #);
                ('editText', class[]) -> private.classInfo.add;
             #);
             ('textField', class[]) -> private.classInfo.add
             (# 
             do ('onTextChanged', true) -> addMethod;
                ('onBeforeChange', true) -> addMethod;
             #);
             ('scrollList', class[]) -> private.classInfo.add
             (# 
             do ('textScrollList', class[]) -> private.classInfo.add;
             #);
          #);
          ('window', class[]) -> private.classInfo.add
          (# 
          do ('onAboutToClose', true) -> addMethod;
          #);
          ('menu', class[]) -> private.classInfo.add;
          ('menubar', class[]) -> private.classInfo.add;
          ('menuitem', class[]) -> private.classInfo.add
          (# 
          do ('dynamicMenuitem', class[]) -> private.classInfo.add;
          #);
       #);
   if);

-- IBeditorEnvDoPart: doPart --
do init;
   
   inner;

-- EDscanMatchingAstcontrollers: doPart --
do private.astControllers.scan
   (# ancestor: ^MPS.AST;
      ignore: @integer;
   do (if theAst[] -> current.theAst.equal then
          current[] -> doSame;
       else
          current.theAst[] -> theAst.nearestCommonAncestor 
            -> (ancestor[],ignore,ignore);
          (if ancestor[]<>None then
              (if true
               //ancestor[] -> theAst.equal then
                  current[] -> doChild;
               //ancestor[] -> current.theAst.equal then
                  current[] -> doAncestor;
              if);
          if);
      if);
   #);
   
-- EDastControllerInit: doPart --
do this(astController)[] -> addAstController;
   
   inner init;
   
   
-- EDaddAstController: doPart --
do (if astControl[] -> private.astControllers.has//false then
       astControl[] -> private.astControllers.append;
   if);
   
   
-- EEastControllerChange: doPart --
do newAst[] -> theAst[];
   private.family.scan
   (# 
   do theAst[] -> current.notify;
   #);
   
-- EEastControllerPrepareChange: doPart --
do theAst[] -> scanMatchingAstControllers
   (# member: ^familyMember;
      doSame::<
        (# 
        do (if current[]<>this(astController)[] then
               &sameMember[] -> member[];
               current[] -> member.controller[];
               member[] -> private.family.append;
           if);
        #);
      doAncestor::<
        (# 
        do (if current[]<>this(astController)[] then
               &ancestorMember[] -> member[];
               current[] -> member.controller[];
               member[] -> private.family.append;
           if)
        #);
      doChild::<
        (# 
        do (if current[]<>this(astController)[] then
               &childMember[] -> member[];
               current[] -> member.controller[];
               member[] -> private.family.append;
           if);
        #);
   #);
   
-- astControllerLib: attributes --

familyMember:
  (# controller: ^astController;
     notify:<
       (# theAst: ^MPS.AST;
       enter theAst[]
       do inner;
       #);
  #);
sameMember: familyMember
  (# 
     notify::<
       (# 
       do theAst[] -> controller.replaced;
       #);
  #);
childMember: familyMember
  (# 
     notify::<
       (# 
       do (* No notification Yet *)
       #);
  #);
ancestorMember: familyMember
  (# 
     notify::<
       (# 
       do controller.changed
       #);
  #);
-- astControllerPrivate: descriptor --
(# 
   family: @list
     (# element::< familyMember;
     #);
#)


-- EDastControllerRelease: doPart --
do inner release;
   this(astController)[] -> removeAstController;

-- EDRemoveAstController: doPart --
do (if astControl[] -> private.astControllers.has then
       astControl[] -> private.astControllers.at 
         -> private.astControllers.delete;
   if);

-- editorEnvThePathHandlerInit: doPart --
do currentDirectory -> private.basis[] -> makePretty;
   

-- editorEnvthePathHandlerlocalize: doPart --
do 
   l: (path[], private.basis[]) -> localPath
   (# 
      localPathException::<
        (# 
        do path[] -> local[];
           failure;
           leave l;
        #);
   #) -> local[];

-- editorEnvthePathHandlerprivate: descriptor --
(# basis: ^text;
#)

-- thePathHandlerLib: attributes --
makePretty:
  (# path: ^text;
     homeIndex, machineIndex: @integer;
  enter path[]
  do 
     (if (1 -> path.inxGet) = '/' then
         (if (2 -> path.inxGet) = 'a' then
             (if (3 -> path.inxGet) = '/' then
                 4 -> homeIndex;
             if);
          else
             2 -> homeIndex;
         if);
         (if homeIndex > 1 then
             (if 'home' -> ((homeIndex, homeIndex + 3) -> path.sub).equal then
                 homeIndex + 5 -> machineIndex;
                 machineIndex -> path.setPos;
                 path.scan
                 (# while::<
                      (# 
                      do ch <> '/' -> value;
                         machineIndex + 1 -> machineIndex;
                      #);
                 #);
                 (1, machineIndex) -> path.delete;
                 '/users/' -> path.prepend;
             if);
         if);
     if);
     (if (path.length -> path.inxGet) = '/' then
         (path.length, path.length) -> path.delete;
     if);
  #)
-- editorEnvthePathHandlerworkingDirectory: doPart --
do private.basis[] -> path[];

-- EditorEnvClassInfoTextHash: doPart --
do t.scanAll
   (# 
   do value + ch -> value;
   #);
-- EditorEnvClassInfoAdd: doPart --
do &element[] -> class[];
   name[] -> class.name[];
   class.methods.init;
   (if superClass[] <> None then
       superClass.methods.iterate
       (# 
       do current.elm[] -> class.methods.append;
       #);
   if);
   inner;
   class[] -> insert;
   
   
-- EditorEnvClassInfoAddMethod: doPart --
do &class.methods.element[] -> method[];
   name[] -> method.name[];
   event -> method.event;
   method[] -> class.methods.append;
   
-- EditorEnvClassInfoLookup: doPart --
do name[] -> textHash -> findIndexed
   (# predicate::
        (# 
        do current.name[] -> name.equal -> value;
        #);
   #) -> class[];
   inner;
   
--  EditorEnvShowParseErrors: doPart --
do (# theWindow: ^parseErrorsDialogWindow;
   do &parseErrorsDialogWindow[] -> theWindow[];
      errors[] -> theWindow.open;
   #);
