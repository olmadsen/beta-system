ORIGIN '~beta/guienv/v1.3.1/guienv';
INCLUDE '~beta/mps/v4.9.1/astlevel';
INCLUDE '~beta/betaast/v4.9.1/betacfl';
INCLUDE 'prettyprinter';
INCLUDE '~beta/sysutils/v1.4/pathhandler';
BODY 'private/editorenvbody';
-- lib: attributes --

editorEnv: GUIenv
  (# <<SLOT editorEnvLib: attributes>>;
     MPS: ^astInterface;
     betaGram: ^astInterface.beta;
     pretty: ^astInterface.prettyPrinter;
     
     saveGroup:
       (# group: ^astInterface.fragmentGroup;
       enter group[]
       <<SLOT editorEnvSaveGroup: doPart>>
       #);
     thePathHandler: @pathHandler
       (# <<SLOT thePathHandlerLib: attributes>>;
          init::<
            (# <<SLOT editorEnvThePathHandlerInit: doPart>> #);
          workingDirectory:
            (# path: ^text;
            <<SLOT editorEnvthePathHandlerworkingDirectory: doPart>>
               exit path[]
            #);
          localize:
            (* If possible the path will be expressed local to the
             *  workingDirectory - otherwise it will be a full path.
             *)
            (# path: ^text;
               local: ^text;
               failure:< object;
            enter path[]
            <<SLOT editorEnvthePathHandlerlocalize: doPart>>
            exit local[]
            #);
          private: @<<SLOT editorEnvthePathHandlerprivate: descriptor>>;
       #);
     private: @<<SLOT IBeditorEnvPrivate: descriptor>>;
     
     init:<
       (* Init is called in the doPart of this(editorEnv), if it is
        * not already called.
        *)
       (# <<SLOT IBeditorEnvInit: doPart>> #);
     showParseErrors:
       (# errors: ^text;
       enter errors[]
       <<SLOT EditorEnvShowParseErrors: doPart>>
       #);
  <<SLOT IBeditorEnvDoPart: doPart>>
  #);
