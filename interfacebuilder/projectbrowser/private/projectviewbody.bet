ORIGIN '../projectview';
INCLUDE '~beta/interfacebuilder/v1.0/guienvstuff/tableview';
INCLUDE '~beta/guienv/v1.2/fields';
INCLUDE '~beta/basiclib/v1.4/file';

-- projectViewLib: attributes --


loadRaster:
  (# name: ^text;
     theBitMap: ^bitmap;
  enter name[]
  do &bitmap[] -> theBitmap[];
     '/users/beta/bitmap/interfacebuilder/' -> name.copyPrepend -> theBitMap.readFromX11File;
  exit theBitMap[]
  #);
projectTableView: tableView
  (# updateView:
       (# 
       do disableUpdate;
          clear;
          (if theProject <> none then
              (theProject).fragmentGroups.iterate
              (# 
              do current.elm[] -> addFragmentGroup;
              #);
          if);
          enableUpdate;
          fitToContents;
       #);
     clear:
       (# 
       do scan
          (# 
          do current.delete;
          #);
       #);

     addFragmentGroup:
       (# name: ^text;
          theItem: ^fragmentGroupItem;
       enter name[]
       do &fragmentGroupItem[] -> theItem[];
          (none, name[]) -> theItem.init;
       #);
     fragmentGroupItem: tableItem
       (# path: ^text;
          onSelect::<
            (# 
            do path[] -> fileSelected;
            #);
          init::<
            (# 
            enter path[]
            do makeName -> name;
               groupIcon[] -> icon;
               inner;
            #);
          makeName:
            (# theName: ^text;
               entry: @diskEntry;
            do path[] -> entry.path;
               entry.path.name -> theName[];
            exit theName[]
            #);
       #);
     open::<
       (# 
       do 'smallgroupicon.icon' -> loadRaster -> groupIcon[];
       #);
     groupIcon: ^raster;
  #);
-- ProjectViewEnterTheProject: doPart --
do value[] -> private.theProject[];
   private.theScroller.contents.updateView;
       

-- ProjectViewExitTheProject: doPart --
do private.theProject[] -> value[];

-- ProjectViewOpen: doPart --
do 
   this(projectView)[] -> private.theScroller.open;
   'Project' -> label;
   inner open;

-- ProjectViewPrivate: descriptor --
(# theProject: ^project;
   theScroller: @scroller
     (# contentsType:: projectTableView;
        open::<
          (# width, height: @integer;
          do this(projectView).size -> (width, height);
             ((3, 16), (width - 3, height - 3)) -> frame;
             true -> bindRight;
             true -> bindBottom;
          #);
     #);
#)

