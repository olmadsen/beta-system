ORIGIN '../projectview';
INCLUDE '~beta/guienv/v1.6/utils/tableview';
INCLUDE '../../guienvstuff/loadbitmap';
INCLUDE '~beta/guienv/v1.6/fields';
INCLUDE '~beta/basiclib/v1.6/file';

-- projectViewLib: attributes --

projectTableView: tableView
  (# updateView:
       (# 
       do disableUpdate;
          clear;
          (if theProject <> none then
              (theProject).fragmentGroups.iterate
              (# 
              do current.elm[] -> addFragmentGroup;
              #);
          if);
          enableUpdate;
          fitToContents;
          l: scan
            (# 
            do current.select;
               leave l;
            #);
          update;
       #);
     clear:
       (# 
       do scan
          (# 
          do current.delete;
          #);
       #);

     addFragmentGroup:
       (# name: ^text;
          theItem: ^fragmentGroupItem;
       enter name[]
       do &fragmentGroupItem[] -> theItem[];
          (none, name[]) -> theItem.init;
       #);
     fragmentGroupItem: tableItem
       (# path: ^text;
          onSelect::<
            (# 
            do path[] -> fileSelected;
            #);
          init::<
            (# 
            enter path[]
            do makeName -> name;
               groupIcon[] -> icon;
               inner;
            #);
          makeName:
            (# theName: ^text;
               entry: @diskEntry;
            do path[] -> entry.path;
               entry.path.name -> theName[];
            exit theName[]
            #);
       #);
     open::<
       (# 
       do 'smallgroupicon' -> loadbitmap -> groupIcon[];
       #);
     groupIcon: ^raster;
  #);
-- ProjectViewEnterTheProject: doPart --
do value[] -> private.theProject[];
   private.theScroller.contents.updateView;
   (if value[] <> none then
       'Project' -> label;
    else
       'No project' -> label;
   if);
       

-- ProjectViewExitTheProject: doPart --
do private.theProject[] -> value[];

-- ProjectViewOpen: doPart --
do 
   this(projectView)[] -> private.theScroller.open;
   private.theScroller.contents.fitToContents;
   'No project' -> label;
   inner open;

-- ProjectViewPrivate: descriptor --
(# theProject: ^project;
   theScroller: @scroller
     (# contentsType:: projectTableView;
        open::<
          (# width, height: @integer;
          do this(projectView).size -> (width, height);
             ((3, 16), (width - 3, height - 3)) -> frame;
             true -> bindRight;
             true -> bindBottom;
          #);
     #);
#)

