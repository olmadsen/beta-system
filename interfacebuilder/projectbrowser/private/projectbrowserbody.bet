ORIGIN '../projectbrowser';

INCLUDE '../groupview';
INCLUDE '../projectview';
INCLUDE '../../patterneditor/libeditor';
INCLUDE '../../mpsstuff/mpsutils';
INCLUDE '../../code/generate';

INCLUDE '~beta/guienv/v1.6/utils/pane';
INCLUDE '~beta/guienv/v1.6/fields';

INCLUDE '~beta/betaast/v5.2/gram';

-- ProjectBrowserEnterTheProject: doPart --
do value[] -> private.theProject[];
   private.updateView;

-- ProjectBrowserExitTheProject: doPart --
do private.theProject[] -> value[];

-- ProjectBrowserOpen: doPart --
do private.open;
   inner;
   
-- ProjectBrowserTheGroup: doPart --
do private.theGroupView.theGroup -> group[];

-- ProjectBrowserTheProperty: doPart --
do (private.currentProp[],private.currentValue[]) -> (prop[], value[])

-- ProjectBrowserTheFragment: doPart --
do private.currentFragment[] -> frag[];

-- ProjectBrowserTheAttribute: doPart --
do private.currentAttributeDecl[] -> theAttributeDecl[];


   
-- ProjectBrowserPrivate: descriptor --
(# initialWidth: (# exit 250 #);
   initialHeight: (# exit 150 #);
   currentAttributeDecl: ^astInterface.beta.attributeDecl;
   currentProp, currentValue: ^text;
   currentFragment: ^astInterface.fragmentForm;
   
   theProject: ^project;
   open:
     (# 
     do (this(projectBrowser)[], true, 20, 4)  -> verPane.open;
        verPane.size -> size;
     #);
   updateView:
     (# 
     do none -> theGroupView.theGroup;
        none -> theLibEditor.theFragment;
        none -> currentAttributeDecl[];
        'No fragment selected' -> fragmentLabel.label;
        theProject[] -> theProjectView.theProject;
     #);
   fragmentLabel: @group
     (# open::
          (# 
          do (2 * initialWidth + 4, initialHeight) -> size;
             (fragmentLabel[], none) -> theLibEditor.open;
             true -> theLibEditor.bindRight;
             true -> theLibEditor.bindBottom;
             'No fragment selected' -> label;
          #);
     #);
   theLibEditor: @libEditor
     (# attributeSelected::
          (# 
          do theAttribute[] -> currentAttributeDecl[];
             doubleClick -> attributeChanged;
          #);
        open::
          (# width, height: @integer;
          do fragmentLabel.size -> (width, height);
             ((3, 20), (width - 3, height - 3)) -> frame;
          #);
     #);
   theProjectView: @projectView
     (# fileSelected::
          (# 
             group: ^astInterface.fragmentGroup;
             fullPath: ^text;
          do 
             name[] -> (theProject).convertToFullPath -> fullPath[];
             fullpath[] -> MPS.safeOpen -> group[];
             (if group[] <> none then
                 group[] -> theGroupView.theGroup;
                 groupChanged;
             if);
          #);
        open::
          (# 
          do (initialWidth, initialHeight) -> size;
          #);
     #);
   theGroupView: @groupView
     (# fragmentSelected::
          (# gram: @grammar;
             str: ^text;
             catName: ^text;
          do frag[] -> currentFragment[];
             (if frag.category 
              //gram.attributesForm then
                 frag[] -> theLibEditor.theFragment;
                 frag.name -> fragmentLabel.label;
              else
                 (if frag.category 
                  //gram.descriptorForm then
                     'descriptor' -> catname[];
                  //gram.doPart then
                     'do-part' -> catname[];
                  else
                     'unknown' -> catname[];
                 if);
                 none -> theLibEditor.theFragment;
                 none -> currentAttributeDecl[];
                 (frag.name).copy -> str[];
                 ': ' -> str.append;
                 catname[] -> str.append;
                 ' fragments are not supported yet' -> str.append;
                 str[] -> fragmentLabel.label;
             if);
             fragmentChanged;
          #);
        propertySelected::
          (# 
          do prop[] -> currentProp[];
             value[] -> currentValue[];
             propertyChanged;
          #);
        open::
          (# 
          do (initialWidth, initialHeight) -> size;
          #);
     #);
   
   verPane: @pane
     (# 
        horPane: @pane
          (# open::
               (# 
               do horPane[] -> theProjectView.open;
                  theProjectView[] -> appendMember;
                  horPane[] -> theGroupView.open;
                  theGroupView[] -> appendMember;
                  appendsDone;
               #);
          #);
        open::
          (#
          do 
             (verPane[], false, 20, 4) -> horPane.open;
             horPane[] -> appendMember;
             verPane[] -> fragmentLabel.open;
             fragmentLabel[] -> appendMember;
             appendsDone;
             size -> this(projectBrowser).size;
             true -> bindRight;
             true -> bindBottom;
             (* true -> horPane.bindRight; 
              *              true -> theLibEditor.bindRight;
              *              true -> theLibEditor.bindBottom;
              *              true -> fragmentLabel.bindRight;
              *              true -> fragmentLabel.bindBottom;
              *              true -> theProjectView.bindBottom;
              *              true -> theGroupView.bindRight;
              *              true -> theGroupView.bindBottom;
              *)
          #);
     #);
#)

