ORIGIN '../projectbrowser';

INCLUDE '../groupview';
INCLUDE '../projectview';
INCLUDE '~beta/interfacebuilder/v1.0/patterneditor/libeditor';
INCLUDE '~beta/interfacebuilder/v1.0/mpsstuff/mpsutils';
INCLUDE '~beta/interfacebuilder/v1.0/code/generate';

INCLUDE '~beta/guienv/v1.3/utils/pane';
INCLUDE '~beta/guienv/v1.3/fields';

INCLUDE '~beta/betaast/v4.9.1/gram';

-- ProjectBrowserEnterTheProject: doPart --
do value[] -> private.theProject[];
   private.updateView;

-- ProjectBrowserExitTheProject: doPart --
do private.theProject[] -> value[];

-- ProjectBrowserOpen: doPart --
do private.open;
   inner;
   
-- ProjectBrowserTheGroup: doPart --
do private.theGroupView.theGroup -> group[];

-- ProjectBrowserTheProperty: doPart --
do (private.currentProp[],private.currentValue[]) -> (prop[], value[])

-- ProjectBrowserTheFragment: doPart --
do private.currentFragment[] -> frag[];

-- ProjectBrowserTheAttribute: doPart --
do private.currentAttributeDecl[] -> theAttributeDecl[];


   
-- ProjectBrowserPrivate: descriptor --
(# initialWidth: (# exit 250 #);
   initialHeight: (# exit 150 #);
   currentAttributeDecl: ^astInterface.beta.attributeDecl;
   currentProp, currentValue: ^text;
   currentFragment: ^astInterface.fragmentForm;
   
   theProject: ^project;
   open:
     (# 
     do this(projectBrowser)[] -> verPane.open;
        verPane.size -> size;
     #);
   updateView:
     (# 
     do none -> theGroupView.theGroup;
        none -> theLibEditor.theFragment;
        none -> currentAttributeDecl[];
        'No fragment selected' -> fragmentLabel.label;
        theProject[] -> theProjectView.theProject;
     #);
   fragmentLabel: @group
     (# open::
          (# 
          do (2 * initialWidth + 4, initialHeight) -> size;
             (fragmentLabel[], none) -> theLibEditor.open;
             'No fragment selected' -> label;
          #);
     #);
   theLibEditor: @libEditor
     (# attributeSelected::
          (# 
          do theAttribute[] -> currentAttributeDecl[];
             attributeChanged;
          #);
        open::
          (# width, height: @integer;
          do fragmentLabel.size -> (width, height);
             ((3, 20), (width - 3, height - 3)) -> frame;
          #);
     #);
   theProjectView: @projectView
     (# fileSelected::
          (# MPS: ^astInterface;
             group: ^astInterface.fragmentGroup;
          do getMPS -> MPS[];
             name[] -> MPS.safeOpen -> group[];
             (if group[] <> none then
                 group[] -> theGroupView.theGroup;
                 groupChanged;
             if);
          #);
        open::
          (# 
          do (initialWidth, initialHeight) -> size;
          #);
     #);
   theGroupView: @groupView
     (# fragmentSelected::
          (# gram: @grammar;
             str: ^text;
             catName: ^text;
          do frag[] -> currentFragment[];
             (if frag.category 
              //gram.attributesForm then
                 frag[] -> theLibEditor.theFragment;
                 frag.name -> fragmentLabel.label;
              else
                 (if frag.category 
                  //gram.descriptorForm then
                     'descriptor' -> catname[];
                  //gram.doPart then
                     'do-part' -> catname[];
                  else
                     'unknown' -> catname[];
                 if);
                 none -> theLibEditor.theFragment;
                 none -> currentAttributeDecl[];
                 (frag.name).copy -> str[];
                 ': ' -> str.append;
                 catname[] -> str.append;
                 ' fragments are not supported yet' -> str.append;
                 str[] -> fragmentLabel.label;
             if);
             fragmentChanged;
          #);
        propertySelected::
          (# 
          do prop[] -> currentProp[];
             value[] -> currentValue[];
             propertyChanged;
          #);
        open::
          (# 
          do (initialWidth, initialHeight) -> size;
          #);
     #);
   
   verPane: @pane
     (# 
        horPane: @pane
          (# open::
               (# 
               do false -> verticalStacking;
                  20 -> minSize;
                  4 -> paneWidth;
                  horPane[] -> theProjectView.open;
                  (theProjectView[], false) -> appendMember;
                  horPane[] -> theGroupView.open;
                  (theGroupView[], true) -> appendMember;
               #);
          #);
        open::
          (# onGroupSizeComputed::
               (# 
               do size -> this(projectBrowser).size;
                  true -> bindRight;
                  true -> bindBottom;
                  true -> horPane.bindRight;
                  true -> theLibEditor.bindRight;
                  true -> theLibEditor.bindBottom;
                  true -> fragmentLabel.bindRight;
                  true -> fragmentLabel.bindBottom;
                  true -> theProjectView.bindBottom;
                  true -> theGroupView.bindRight;
                  true -> theGroupView.bindBottom;
               #);
          do true -> verticalStacking;
             20 -> minSize;
             4 -> paneWidth;
             verPane[] -> horPane.open;
             (horPane[], false) -> appendMember;
             verPane[] -> fragmentLabel.open;
             (fragmentLabel[], true) -> appendMember;
          #);
     #);
#)

