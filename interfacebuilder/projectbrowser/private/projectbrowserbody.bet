ORIGIN '../projectbrowser';

INCLUDE '../groupview';
INCLUDE '../projectview';
INCLUDE '~beta/interfacebuilder/v1.0/patterneditor/libeditor';
INCLUDE '~beta/interfacebuilder/v1.0/mpsstuff/mpsutils';

INCLUDE '~beta/guienv/v1.2/utils/pane';
INCLUDE '~beta/guienv/v1.2/fields';

INCLUDE '~beta/betaast/v4.9.1/gram';

-- ProjectBrowserEnterTheProject: doPart --
do value[] -> private.theProject[];
   private.updateView;

-- ProjectBrowserExitTheProject: doPart --
do private.theProject[] -> value[];

-- ProjectBrowserOpen: doPart --
do private.open;
   inner;
   
-- ProjectBrowserPrivate: descriptor --
(# initialWidth: (# exit 250 #);
   initialHeight: (# exit 150 #);
   
   theProject: ^project;
   open:
     (# 
     do this(projectBrowser)[] -> verPane.open;
        verPane.size -> size;
     #);
   updateView:
     (# 
     do none -> theGroupView.theGroup;
        none -> theLibEditor.theFragment;
        'No fragment selected' -> fragmentLabel.label;
        theProject[] -> theProjectView.theProject;
     #);
   fragmentLabel: @group
     (# open::
          (# 
          do (2 * initialWidth + 4, initialHeight) -> size;
             (fragmentLabel[], none) -> theLibEditor.open;
          #);
     #);
   theLibEditor: @libEditor
     (# 
        open::
          (# width, height: @integer;
          do fragmentLabel.size -> (width, height);
             ((3, 20), (width - 3, height - 3)) -> frame;
          #);
     #);
   theProjectView: @projectView
     (# fileSelected::
          (# MPS: ^astInterface;
             group: ^astInterface.fragmentGroup;
          do getMPS -> MPS[];
             (name[], screen[]) -> MPS.top.open -> group[];
             (if group[] <> none then
                 group[] -> theGroupView.theGroup;
             if);
          #);
        open::
          (# 
          do (initialWidth, initialHeight) -> size;
          #);
     #);
   theGroupView: @groupView
     (# fragmentSelected::
          (# gram: @grammar;
          do (if frag.category = gram.attributesForm then
                 frag[] -> theLibEditor.theFragment;
                 frag.name -> fragmentLabel.label;
             if);
          #);
        open::
          (# 
          do (initialWidth, initialHeight) -> size;
          #);
     #);
   
   verPane: @pane
     (# 
        horPane: @pane
          (# open::
               (# 
               do false -> verticalStacking;
                  20 -> minSize;
                  4 -> paneWidth;
                  horPane[] -> theProjectView.open;
                  (theProjectView[], false) -> appendMember;
                  horPane[] -> theGroupView.open;
                  (theGroupView[], true) -> appendMember;
               #);
          #);
        open::
          (# onGroupSizeComputed::
               (# 
               do size -> this(projectBrowser).size;
                  true -> bindRight;
                  true -> bindBottom;
                  true -> horPane.bindRight;
                  true -> theLibEditor.bindRight;
                  true -> theLibEditor.bindBottom;
                  true -> fragmentLabel.bindRight;
                  true -> fragmentLabel.bindBottom;
                  true -> theProjectView.bindBottom;
                  true -> theGroupView.bindRight;
                  true -> theGroupView.bindBottom;
               #);
          do true -> verticalStacking;
             20 -> minSize;
             4 -> paneWidth;
             verPane[] -> horPane.open;
             (horPane[], false) -> appendMember;
             verPane[] -> fragmentLabel.open;
             (fragmentLabel[], true) -> appendMember;
          #);
     #);
#)

