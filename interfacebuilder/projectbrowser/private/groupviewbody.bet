ORIGIN '../groupview';
INCLUDE '~beta/interfacebuilder/v1.0/mpsstuff/mpsutils';
INCLUDE '~beta/interfacebuilder/v1.0/guienvstuff/tableview';
INCLUDE '~beta/guienv/v1.3/fields';

-- groupViewLib: attributes --
groupTableView: tableView
  (# 
     clear:
       (# 
       do scan
          (# 
          do current.delete;
          #);
       #);
     updateView:
       (#
       do disableUpdate;
          clear;
          (if theGroup <> none then
              (theGroup).prop.scanProp
              (# 
                 doProp::
                   (# 
                   do (if true 
                       //'donecheck' -> prop.equalNCS
                       //'unique_group_id' -> prop.equalNCS
                       //'comment' -> prop.equalNCS then (* Nothing *)
                       else
                          scanParameters
                          (# doString::
                               (# 
                               do (prop.copy, s.copy) -> addPropertyItem;
                               #);
                          #);
                      if);
                   #);
              #);
              (theGroup).fragmentList.scan
              (# 
              do (if current.type = 0 (* FormType *) then
                     current[] -> addFragmentItem;
                 if);
              #);
              
          if);
          enableUpdate;
          fitToContents;
       #);

     addPropertyItem:
       (# prop: ^text;
          value: ^text;
          theItem: ^propertyItem;
       enter (prop[], value[])
       do &propertyItem[] -> theItem[];
          (none, prop[], value[]) -> theItem.init;
       #);
     propertyItem: tableItem
       (# prop: ^text;
          value: ^text;
          makeName:
            (# theName: ^text;
            do &text[] -> theName[];
               prop[] -> theName.append;
               ' ''' -> theName.append;
               value[] -> theName.append;
               '''' -> theName.append;
            exit theName[]
            #);
          onSelect::<
            (# 
            do (prop.copy, value.copy) -> propertySelected;
            #);
          init::<
            (# 
            enter (prop[], value[])
            do prop.makeUC;
               makeName -> name;
               inner;
            #);
       #);
     addFragmentItem:
       (# fragElm: ^astInterface.fragmentGroup.fragmentListElement; 
          theItem: ^fragmentItem;
       enter fragElm[]
       do &fragmentItem[] -> theItem[];
          (none, fragElm[]) -> theItem.init;
       #);
     fragmentItem: tableItem
       (# fragElm: ^astInterface.fragmentGroup.fragmentListElement;
          makeName:
            (# theName: ^text;
               frag: ^astInterface.fragmentForm;
               catName: ^text;
            do &text[] -> theName[];
               fragElm.open -> frag[];
               (frag.name).copy -> theName[];
               ': ' -> theName.append;
               frag.category -> frag.Grammar.symbolToName -> catName[];
               (if catName[] <> none then
                   (if true
                    // ('DescriptorForm' -> catName.equal)
                    // ('ObjectDescriptor' -> catName.equal) then
                       'Descriptor' -> theName.append; 
                    // ('AttributesForm' -> catName.equal)
                    // ('AttributeDecl' -> catName.equal) then
                       'Attributes' -> theName.append; 
                    else
                       (if catName.empty
                        // true then 'Unexpanded' -> theName.append; 
                        else
                           catName[] -> theName.append; 
                       if);
                       
                   if);
                   
               if);
            exit theName[]
            #);
          onSelect::<
            (# 
            do fragElm.open -> fragmentSelected;
            #);
          init::<
            (# 
            enter fragElm[]
            do makeName -> name;
               inner;
            #);
       #);
     open::<
       (# 
       do
       #);
  #);
-- GroupViewEnterTheGroup: doPart --
do value[] -> private.group[];
   private.theScroller.contents.updateView;
   (if private.group[] = none then
       'No fragment group selected' -> label;
    else
       private.group.name -> label;
   if);

-- GroupViewExitTheGroup: doPart --
do private.group[] -> value[];

-- GroupViewOpen: doPart --
do this(groupView)[] -> private.theScroller.open;
   private.theScroller.contents.fitToContents;
   'No fragment group selected' -> label;
   inner;

-- GroupViewPrivate: descriptor --
(# group: ^astInterface.fragmentGroup;
   theScroller: @scroller
     (# contentsType:: groupTableView;
        open::<
          (# width, height: @integer;
          do this(groupView).size -> (width, height);
             ((3, 16), (width - 3, height - 3)) -> frame;
             true -> bindRight;
             true -> bindBottom;
          #);
     #);
#)

