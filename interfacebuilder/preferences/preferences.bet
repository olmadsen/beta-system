ORIGIN '~beta/basiclib/v1.4/betaenv';
BODY 'private/preferencesbody';


-- lib: attributes --

getPreferences: objectPool.get
  (# type:: preferences;
     init::
       (# 
       do getPreferencesFileName -> obj.init
          (# 
             failure::
               (# 
               do msg[] -> putLine;
               #);
          #);
       #);
  #);
getPreferencesSection:
  (# name: ^text;
     thePreferences: ^preferences;
     theSection: ^preferences.section;
  enter name[]
  do getPreferences -> thePreferences[];
     name[] -> thePreferences.getSection -> theSection[];
  exit theSection[]
  #);

getPreferencesFileName:
  (# baseName: ^text;
     fileName: ^text;
  enter baseName[]
  <<SLOT getPreferencesFileName: doPart>>
  exit fileName[]
  #);

preferences:
  (# <<SLOT preferencesLib: attributes>>;
     
     init:
       (# fileName: ^text;
          failure:<
            (# msg: ^text;
            enter msg[]
            do inner;
            #);
       enter fileName[]
       <<SLOT preferencesInit: doPart>>
       #);
     save:
       (# failure:<
            (# msg: ^text;
            enter msg[]
            do inner;
            #);
       <<SLOT preferencesSave: doPart>> 
       #);
     
     getSection:
       (# name: ^text;
          theSection: ^section;
          newCreated:< object;
       enter name[]
       <<SLOT preferencesGetSection: doPart>>
       exit theSection[]
       #);
     setSection:
       (# name: ^text;
          theSection: ^section;
       enter (name[], theSection[])
       <<SLOT preferencesSetSection: doPart>>
       #);
     section:
       (# <<SLOT sectionLib: attributes>>;
          name: ^text;
          setOption:
            (# key, value: ^text;
            enter (key[], value[])
            <<SLOT preferencesSectionSetOption: doPart>>
            #);
          getOption:
            (# default:< object;
               key, value: ^text;
            enter key[]
            <<SLOT preferencesSectionGetOption: doPart>>
            exit value[]
            #);
          save:
            (# <<SLOT preferencesSectionSave: doPart>> #);
          private: @<<SLOT sectionPrivate: descriptor>>;
       #);
     private: @<<SLOT preferencesPrivate: descriptor>>;
  #);
