ORIGIN '~beta/basiclib/v1.6/betaenv';
BODY 'private/preferencesbody';


-- lib: attributes --

getPreferences: objectPool.get
  (# type:: preferences;
     init::
       (# 
       do baseName[] -> getPreferencesFileName -> obj.init
          (# 
             failure::
               (# 
               do msg[] -> putLine;
               #);
          #);
       #);
     baseName: ^text;
  enter baseName[]
  #);
getPreferencesSection:
  (# baseName, sectionName: ^text;
     thePreferences: ^preferences;
     theSection: ^preferences.section;
  enter (baseName[], sectionName[])
  do baseName[] -> getPreferences -> thePreferences[];
     sectionName[] -> thePreferences.getSection 
     (# notFound::
          (# 
          do sectionName[] -> thePreferences.createSection -> theSection[];
          #);
     #) -> theSection[];
  exit theSection[]
  #);

getPreferencesFileName:
  (# baseName: ^text;
     fileName: ^text;
  enter baseName[]
  <<SLOT getPreferencesFileName: doPart>>
  exit fileName[]
  #);

preferences:
  (# <<SLOT preferencesLib: attributes>>;
     
     init:
       (# fileName: ^text;
          failure:<
            (# msg: ^text;
            enter msg[]
            do inner;
            #);
       enter fileName[]
       <<SLOT preferencesInit: doPart>>
       #);
     save:
       (# failure:<
            (# msg: ^text;
            enter msg[]
            do inner;
            #);
       <<SLOT preferencesSave: doPart>> 
       #);
     
     createSection:
       (# name: ^text;
          theSection: ^section;
          alreadyCreated:< notification;
       enter name[]
       <<SLOT preferencesCreateSection: doPart>>
       exit theSection[]
       #);
     getSection:
       (# name: ^text;
          theSection: ^section;
          notFound:< notification;
       enter name[]
       <<SLOT preferencesGetSection: doPart>>
       exit theSection[]
       #);
     section:
       (# <<SLOT sectionLib: attributes>>;
          name: ^text;
          defineOption:
            (# key, value: ^text;
               alreadyDefined:< notification;
            enter (key[], value[])
            <<SLOT preferencesSectionDefineOption: doPart>>
            #);
          setOption:
            (# notFound:< notification;
               key, value: ^text;
            enter (key[], value[])
            <<SLOT preferencesSectionSetOption: doPart>>
            #);
          getOption:
            (# notFound:< notification;
               key, value: ^text;
            enter key[]
            <<SLOT preferencesSectionGetOption: doPart>>
            exit value[]
            #);
          scan:
            (# currentKey, currentValue: ^text;
            <<SLOT sectionScan: doPart>>
            #);
          save:
            (# <<SLOT preferencesSectionSave: doPart>> #);
          private: @<<SLOT sectionPrivate: descriptor>>;
       #);
     scan:
       (# current: ^section;
       <<SLOT preferencesScan: doPart>>
       #);
     private: @<<SLOT preferencesPrivate: descriptor>>;
  #);
