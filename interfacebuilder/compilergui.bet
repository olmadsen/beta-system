ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls'
        '~beta/guienv/v1.6/fields';
-- guienvLib: Attributes --
red: (#  exit (0xFFFF,0xAFFF,0xAFFF) #);
white: (#  exit (0xFFFF,0xFFFF,0xFFFF) #);
light_gray: (#  exit (0xEEEE,0xEEEE,0xEEEE) #);
compilerGUI: window
  (#
     run:< (# name: ^text;  enter name[] do INNER ;  #);
     add: (# name: ^text;  enter name[] do name[]->filesPanel.append;  #);
     commandPanel: @canvas
       (#
          open::< 
            (# 
            do
               true->bindRight;
               5->border.style;
               true->border.visible;
               (400,40)->size;
               runBtn.open;
               (0,0)->position;
               
            #);
          runBtn: @pushbutton
            (#
               eventHandler:: 
                 (#
                    onMouseUp:: 
                      (# name: ^text; 
                      do
                         filesPanel.selection->name[];
                         (if name[] <> none then name[]->run;  if);
                         
                      #);
                    
                 #);
               open::< 
                 (# 
                 do 'Compile'->label; (75,20)->size; (13,10)->position; 
                 #)
            #)
       #);
     filesPanel: @canvas
       (#
          selection:
            (# name: ^text; 
            do
               (if selected[] <> none then
                   selected.nameLab.label->name[]; 
               if);
               
            exit name[]
            #);
          append:
            (# name: ^text; 
            enter name[]
            do
                 (# item: ^fileItem; 
                 do
                    &fileItem[]->item[];
                    (filesPanel[],name[])->item.open;
                    computeLayout;
                    
                 #);
               
            #);
          filler: @windowItem
            (#
               open::  (#  do light_gray->backgroundColor;  #);
               eventHandler:: 
                 (#
                    onMouseDown:: 
                      (# 
                      do
                         (if selected[] <> none then
                             light_gray->selected.nameLab.backgroundColor; 
                         if);
                         none ->selected[];
                         
                      #);
                    
                 #);
               
            #);
          fileItem: canvas
            (#
               nameLab: @staticText
                 (#
                    open:: 
                      (# name: ^text; 
                      enter name[]
                      do
                         (10,2)->position;
                         light_gray->backgroundColor;
                         name[]->label;
                         fitToContents;
                         
                      #);
                    eventHandler:: 
                      (#
                         onMouseDown:: 
                           (# 
                           do
                              (if selected[] <> none then
                                  light_gray->selected.nameLab.backgroundColor; 
                              if);
                              THIS(fileItem)[]->selected[];
                              red->nameLab.backgroundColor;
                              
                           #);
                         
                      #);
                    
                 #);
               eventHandler:: 
                 (#
                    onMouseDown:: 
                      (# 
                      do
                         (if selected[] <> none then
                             light_gray->selected.nameLab.backgroundColor; 
                         if);
                         THIS(fileItem)[]->selected[];
                         red->nameLab.backgroundColor;
                         
                      #);
                    
                 #);
               open:: 
                 (# name: ^text; 
                 enter name[]
                 do
                    light_gray->backgroundColor;
                    (THIS(fileItem)[],name[])->nameLab.open;
                    
                 #);
               
            #);
          eventHandler:: 
            (# onFrameChanged::  (#  do computeLayout;  #);  #);
          open::< 
            (# 
            do
               filler.open;
               white->backgroundColor;
               true->bindBottom;
               true->bindRight;
               (400,200)->size;
               (0,40)->position;
               
            #);
          computeLayout:
            (# width,height: @integer; y: @integer; 
            do
               size->(width,height);
               2->y;
               scan
                 (# 
                 do
                    (if current[] <> filler[] then
                        (0,y)->current.position;
                        (width,16)->current.size;
                        y+18->y;
                        
                    if);
                    
                 #);
               (0,y)->filler.position;
               (width,height-y)->filler.size;
               
            #);
          selected: ^fileItem;
          
       #);
     open::< 
       (# 
       do
          (13,50)->position;
          (400,260)->size;
          commandPanel.open;
          filesPanel.open;
          statusPanel.open;
          INNER
       #);
     statusPanel: @canvas
       (#
          statusStream: stream
            (#
               doClear: @boolean;
               listeners: @list (# element:: stream;  #);
               put:: 
                 (# t: ^text; 
                 do
                    (if doClear then clear; false->doClear;  if);
                    (if ch = ascii.newline then
                        true->doClear; 
                     else
                        &text[]->t[]; ch->t.put; t[]->append; 
                    if);
                    listeners.scan
                      (#  do ch->current.put;  #);
                    
                 #);
               puttext:: 
                 (# 
                 do
                    (if doClear then clear; false->doClear;  if);
                    txt[]->append;
                    listeners.scan
                      (#  do txt[]->current.putText;  #);
                    
                 #);
               
            #);
          makeStream:
            (# s: ^statusStream; 
            do &statusStream[]->s[]; s.listeners.init; 
            exit s[]
            #);
          clear: (#  do ''->status.lab.label; true->status.lab.update;  #);
          append:
            (# what: ^text; 
            enter what[]
            do
               status.lab.label->(what.copy).prepend->status.lab.label;
               true->status.update;
               
            #);
          set:
            (# what: ^text; 
            enter what[]
            do what[]->status.lab.label; true->status.lab.update; 
            #);
          open::< 
            (# 
            do
               true->bindBottom;
               false->bindTop;
               true->bindRight;
               5->border.style;
               true->border.visible;
               (400,20)->size;
               status.open;
               (0,240)->position;
               
            #);
          status: @canvas
            (# 
               open::<
                 (# 
                 do
                    true->bindRight;
                    (384,12)->size;
                    lab.open;
                    (14,4)->position;
                    
                 #);
               lab: @statictext
                 (#
                    open::< 
                      (# 
                      do 'Idle'->label; (2209,12)->size; (0,0)->position; 
                      #)
                 #)
            #)
       #)
  #);
outputWindow: window
  (#
     info:
       (# buffer: ^text; st: @styledText
       enter buffer[]
       do buffer->st; st[]->texteditor1.contents.contents
       #);
     open::< 
       (# 
       do
          (404,504)->size;
          (40,60)->position;
          'Errors and Warnings'->title;
          texteditor1.open;
          INNER
       #);
     texteditor1: @texteditor
       (#
          open::< 
            (# 
            do
               true->bindBottom;
               true->bindRight;
               (400,500)->size;
               (2,2)->position;
               
            #)
       #)
  #)  

