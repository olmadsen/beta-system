ORIGIN '../palette';
INCLUDE '~beta/guienv/v1.3.1/utils/tableview';
INCLUDE '../../guienvstuff/loadbitmap';
INCLUDE '../../resourcesupport/parameters';
INCLUDE '~beta/guienv/v1.3.1/utils/guienvadds';

-- GUIENVpaletteOpen: doPart --
do true -> border.visible;
   borderStyles.shadowOut -> border.style;
   this(palette)[] -> private.theTableView.open;
   inner;

-- GUIENVpaletteAddPaletteItem: doPart --
do (# theItem: ^private.theTableView.paletteItem;
   do &private.theTableView.paletteItem[] -> theItem[];
      theItem.init;
      icon[] -> theItem.icon;
      name[] -> theItem.name;
      type -> theItem.type;
   #);
   
-- GUIENVpaletteEventHandlerOnMouseDown: doPart --
do (* (if doubleClick then
    *        private.toggleShrinkState;
    *     else
    *        drag;
    *    if);
    *)
   drag;
   
-- GUIENVpalettePrivate: descriptor --
(# 
   width,  height: @integer;
   fitToContents:
     (#
     do theTableView.fitToContents;
        (4, 10) -> theTableView.position;
        theTableView.size -> (width, height);
        (* (36 + 4 + 6 -> width, height) -> theTableView.size; *)
        width + 2*4 -> width;
        height + 4 + 10 -> height;
        (width, height) -> size;
        true -> theTableView.bindRight;
        true -> theTableView.bindBottom;
     #);
   shrinked: @boolean;
   toggleShrinkState:
     (# 
     do (if shrinked then
            (width,  height) -> size;
            false -> shrinked;
         else
            (2*4 + 36 + 6 + 4, height) -> size;
            true -> shrinked;
        if);
     #);
   theTableView: @tableView
     (# paletteItem: tableItem
          (# type: @integer;
             onMouseDown::<
               (# p: @point;
               do (theEvent.localPosition, theTableView[], this(palette)[]) 
                    -> translate -> p; 
                  (type, name, p, theEvent[]) -> mouseDownInItem;
               #);
          #);
        open::
          (# 
          do true -> border.visible;
             borderStyles.shadowIn -> border.style;
          #);
     #);
#)



-- GUIENVControlpaletteOpen: doPart --
do ('button' -> loadbitmap, 'PushButton', pushButtonType) -> addPaletteItem;
   ('checkbox' -> loadbitmap, 'CheckBox',  checkBoxType) -> addPaletteItem;
   ('radiobutton' -> loadbitmap, 'RadioButton', radioButtonType) -> addPaletteItem;
   ('statictext' -> loadbitmap, 'StaticText', staticTextType) -> addPaletteItem;
   ('edittext' -> loadbitmap, 'EditText', editTextType) -> addPaletteItem;
   ('iconbutton' -> loadbitmap, 'IconButton', iconButtonType) -> addPaletteItem;
   ('optionbutton' -> loadbitmap, 'OptionButton', optionButtonType) -> addPaletteItem;
   ('scrollbar' -> loadbitmap, 'ScrollBar', scrollBarType) -> addPaletteItem;
   ('scrollbarv' -> loadbitmap, 'ScrollBar', verScrollBarType) -> addPaletteItem;
   ('separatorh' -> loadbitmap, 'Separator', separatorType) -> addPaletteItem;
   ('separatorv' -> loadbitmap, 'Separator', verSeparatorType) -> addPaletteItem;
   private.fitToContents;
   private.toggleShrinkState;
   inner;
   
-- GUIENVViewPaletteOpen: doPart --
do (none, 'Canvas', canvasType ) -> addPaletteItem;
   (none, 'Scroller', scrollerType) -> addPaletteItem;
   (none, 'TextEditor', textEditorType) -> addPaletteItem;
   (none, 'ScrollList', textScrollListType) -> addPaletteItem;
   (none, 'TextField', textFieldType) -> addPaletteItem;
   private.fitToContents;
   inner;
   
-- toolPaletteOpen: doPart --
do ('select' -> loadbitmap, 'Select', 1) -> addPaletteItem;
   ('hierarki' -> loadbitmap, 'Hierarki', 2) -> addPaletteItem;
   private.fitToContents;
   private.toggleShrinkState;
   inner open;
