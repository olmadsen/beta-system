ORIGIN '../palette';
INCLUDE '~beta/guienv/v1.6/utils/tableview';
INCLUDE '../../guienvstuff/loadbitmap';
INCLUDE '../../resourcesupport/parameters';
INCLUDE '~beta/guienv/v1.6/utils/guienvadds';

-- GUIENVpaletteOpen: doPart --
do true -> border.visible;
   borderStyles.shadowOut -> border.style;
   this(palette)[] -> private.theTableView.open;
   inner;

-- GUIENVpaletteAddPaletteItem: doPart --
do (# theItem: ^private.theTableView.paletteItem;
   do &private.theTableView.paletteItem[] -> theItem[];
      theItem.init;
      icon[] -> theItem.icon;
      name[] -> theItem.name;
      type -> theItem.type;
      index -> theItem.index;
   #);
   
-- GUIENVpaletteEventHandlerOnMouseDown: doPart --
do (* (if doubleClick then
    *        private.toggleShrinkState;
    *     else
    *        drag;
    *    if);
    *)
   drag;
   
-- GUIENVpalettePrivate: descriptor --
(# 
   width,  height: @integer;
   fitToContents:
     (#
     do theTableView.fitToContents;
        (4, 10) -> theTableView.position;
        theTableView.size -> (width, height);
        (* (36 + 4 + 6 -> width, height) -> theTableView.size; *)
        width + 2*4 -> width;
        height + 4 + 10 -> height;
        (width, height) -> size;
        true -> theTableView.bindRight;
        true -> theTableView.bindBottom;
     #);
   shrinked: @boolean;
   toggleShrinkState:
     (# 
     do (if shrinked then
            (width,  height) -> size;
            false -> shrinked;
         else
            (2*4 + 36 + 6 + 4, height) -> size;
            true -> shrinked;
        if);
     #);
   theTableView: @tableView
     (# paletteItem: tableItem
          (# type: @integer;
             index: @integer;
             onMouseDown::<
               (# p: @point;
               do (theEvent.localPosition, theTableView[], this(palette)[]) 
                    -> translate -> p; 
                  (type, name, p, theEvent[], index) -> mouseDownInItem;
               #);
          #);
        open::
          (# 
          do true -> border.visible;
             borderStyles.shadowIn -> border.style;
          #);
     #);
#)



-- GUIENVControlpaletteOpen: doPart --
do ('button' -> loadbitmap, 'PushButton', pushButtonType, 0) -> addPaletteItem;
   ('checkbox' -> loadbitmap, 'CheckBox',  checkBoxType, 0) -> addPaletteItem;
   ('radiobutton' -> loadbitmap, 'RadioButton', radioButtonType, 0) -> addPaletteItem;
   ('statictext' -> loadbitmap, 'StaticText', staticTextType, 0) -> addPaletteItem;
   ('edittext' -> loadbitmap, 'EditText', editTextType, 0) -> addPaletteItem;
   ('iconbutton' -> loadbitmap, 'IconButton', iconButtonType, 0) -> addPaletteItem;
   ('optionbutton' -> loadbitmap, 'OptionButton', optionButtonType, 0) -> addPaletteItem;
   ('scrollbar' -> loadbitmap, 'ScrollBar', scrollBarType, 0) -> addPaletteItem;
   ('scrollbarv' -> loadbitmap, 'ScrollBar', verScrollBarType, 0) -> addPaletteItem;
   ('separatorh' -> loadbitmap, 'Separator', separatorType, 0) -> addPaletteItem;
   ('separatorv' -> loadbitmap, 'Separator', verSeparatorType, 0) -> addPaletteItem;
   
   ('canvas' -> loadbitmap, 'Canvas', canvasType, 0) -> addPaletteItem;
   ('scroller' -> loadbitmap, 'Scroller', scrollerType, 0) -> addPaletteItem;
   ('texteditor' -> loadbitmap, 'TextEditor', textEditorType, 0) -> addPaletteItem;
   ('scrolllist' -> loadbitmap, 'TextScrollList', textScrollListType, 0) -> addPaletteItem;
   ('textfield' -> loadbitmap, 'TextField', textFieldType, 0) -> addPaletteItem;

  (*  ('rectangle' -> loadbitmap, 'Rect', rectType, 0) -> addPaletteItem; *)
   private.fitToContents;
   private.toggleShrinkState;
   inner;
   
-- GUIENVViewPaletteOpen: doPart --
do (none, 'Canvas', canvasType , 0) -> addPaletteItem;
   (none, 'Scroller', scrollerType, 0) -> addPaletteItem;
   (none, 'TextEditor', textEditorType, 0) -> addPaletteItem;
   (none, 'ScrollList', textScrollListType, 0) -> addPaletteItem;
   (none, 'TextField', textFieldType, 0) -> addPaletteItem;
   private.fitToContents;
   inner;
   
-- toolPaletteOpen: doPart --
do ('select' -> loadbitmap, 'Select', 1, 0) -> addPaletteItem;
   ('hierarki' -> loadbitmap, 'Hierarki', 2, 0) -> addPaletteItem;
   private.fitToContents;
   private.toggleShrinkState;
   inner open;
   
-- paletteFit: doPart --
do private.fitToContents;
