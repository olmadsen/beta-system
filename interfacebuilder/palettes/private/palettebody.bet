ORIGIN '../palette';
INCLUDE '~beta/interfacebuilder/v1.0/guienvstuff/tableview';
INCLUDE '~beta/interfacebuilder/v1.0/objectsstore/objectdata';
INCLUDE '~beta/guienv/v1.2/utils/guienvadds';

-- GUIENVpaletteOpen: doPart --
do true -> border.visible;
   borderStyles.shadowOut -> border.style;
   this(palette)[] -> private.theTableView.open;
   inner;
   private.fitToContents;

-- GUIENVpaletteAddPaletteItem: doPart --
do (# theItem: ^private.theTableView.paletteItem;
   do &private.theTableView.paletteItem[] -> theItem[];
      theItem.init;
      icon[] -> theItem.icon;
      name[] -> theItem.name;
      type -> theItem.type;
   #);
   
-- GUIENVpaletteEventHandlerOnMouseDown: doPart --
do (if doubleClick then
       private.toggleShrinkState;
    else
       drag;
   if);
   
-- GUIENVpalettePrivate: descriptor --
(# 
   width,  height: @integer;
   fitToContents:
     (#
     do theTableView.fitToContents;
        (4, 10) -> theTableView.position;
        theTableView.size -> (width, height);
        (* (36 + 4 + 6 -> width, height) -> theTableView.size; *)
        width + 2*4 -> width;
        height + 4 + 10 -> height;
        (width, height) -> size;
        true -> theTableView.bindRight;
        true -> theTableView.bindBottom;
     #);
   shrinked: @boolean;
   toggleShrinkState:
     (# 
     do (if shrinked then
            (width,  height) -> size;
            false -> shrinked;
         else
            (2*4 + 36 + 6 + 4, height) -> size;
            true -> shrinked;
        if);
     #);
   theTableView: @tableView
     (# paletteItem: tableItem
          (# type: @integer;
             onMouseDown::<
               (# p: @point;
               do (theEvent.localPosition, theTableView[], this(palette)[]) 
                    -> translate -> p; 
                  (type, name, p) -> mouseDownInItem;
               #);
          #);
        open::
          (# 
          do true -> border.visible;
             borderStyles.shadowIn -> border.style;
          #);
     #);
#)

-- guienvLib: attributes --
loadIcon:
  (# name: ^text;
     icon: ^bitmap;
  enter name[]
  do '/users/henryml/bitmaps/' -> name.prepend;
     &bitmap[] -> icon[];
     name[] -> icon.readFromX11File;
  exit icon[]
  #);

-- GUIENVControlpaletteOpen: doPart --
do ('button.xbm' -> loadIcon, 'PushButton', pushButtonType) -> addPaletteItem;
   ('checkbox.xbm' -> loadIcon, 'CheckBox',  checkBoxType) -> addPaletteItem;
   ('radiobutton.xbm' -> loadIcon, 'RadioButton', radioButtonType) -> addPaletteItem;
   ('statictext.xbm' -> loadIcon, 'StaticText', staticTextType) -> addPaletteItem;
   ('edittext.xbm' -> loadIcon, 'EditText', editTextType) -> addPaletteItem;
   ('iconbutton.xbm' -> loadIcon, 'IconButton', iconButtonType) -> addPaletteItem;
   ('optionbutton.xbm' -> loadIcon, 'OptionButton', menuButtonType) -> addPaletteItem;
   ('scrollbar.xbm' -> loadIcon, 'ScrollBar', scrollBarType) -> addPaletteItem;
   inner;
   
-- GUIENVViewPaletteOpen: doPart --
do (none, 'Canvas', canvasType ) -> addPaletteItem;
   (none, 'Scroller', scrollerType) -> addPaletteItem;
   (none, 'TextEditor', textEditorType) -> addPaletteItem;
   (none, 'ScrollList', textScrollListType) -> addPaletteItem;
   (none, 'TextField', textFieldType) -> addPaletteItem;
   (none, 'Separator', separatorType) -> addPaletteItem;
