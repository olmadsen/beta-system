ORIGIN '../palette';
INCLUDE '~beta/interfacebuilder/v1.0/guienvstuff/tableview';

-- GUIENVpaletteOpen: doPart --
do true -> border.visible;
   borderStyles.shadowOut -> border.style;
   this(palette)[] -> private.theTableView.open;
   inner;
   private.fitToContents;

-- GUIENVpaletteAddPaletteItem: doPart --
do (# theItem: ^private.theTableView.paletteItem;
   do &private.theTableView.paletteItem[] -> theItem[];
      theItem.init;
      icon[] -> theItem.icon;
      name[] -> theItem.name;
   #);
   
-- GUIENVpaletteEventHandlerOnMouseDown: doPart --
do (if doubleClick then
       private.toggleShrinkState;
    else
       drag;
   if);
   
-- GUIENVpalettePrivate: descriptor --
(# 
   width,  height: @integer;
   fitToContents:
     (#
     do theTableView.fitToContents;
        (4, 10) -> theTableView.position;
        theTableView.size -> (width, height);
        (* (36 + 4 + 6 -> width, height) -> theTableView.size; *)
        width + 2*4 -> width;
        height + 4 + 10 -> height;
        (width, height) -> size;
        true -> theTableView.bindRight;
        true -> theTableView.bindBottom;
     #);
   shrinked: @boolean;
   toggleShrinkState:
     (# 
     do (if shrinked then
            (width,  height) -> size;
            false -> shrinked;
         else
            (2*4 + 36 + 6 + 4, height) -> size;
            true -> shrinked;
        if);
     #);
   theTableView: @tableView
     (# paletteItem: tableItem
          (# 
             
          #);
        open::
          (# 
          do true -> border.visible;
             borderStyles.shadowIn -> border.style;
          #);
     #);
#)

-- guienvLib: attributes --
loadIcon:
  (# name: ^text;
     icon: ^bitmap;
  enter name[]
  do '/users/henryml/bitmaps/' -> name.prepend;
     &bitmap[] -> icon[];
     name[] -> icon.readFromX11File;
  exit icon[]
  #);

-- GUIENVControlpaletteOpen: doPart --
do ('button.xbm' -> loadIcon, 'PushButton') -> addPaletteItem;
   ('checkbox.xbm' -> loadIcon, 'CheckBox') -> addPaletteItem;
   ('radiobutton.xbm' -> loadIcon, 'RadioButton') -> addPaletteItem;
   ('statictext.xbm' -> loadIcon, 'StaticText') -> addPaletteItem;
   ('edittext.xbm' -> loadIcon, 'EditText') -> addPaletteItem;
   ('iconbutton.xbm' -> loadIcon, 'IconButton') -> addPaletteItem;
   ('optionbutton.xbm' -> loadIcon, 'OptionButton') -> addPaletteItem;
   ('scrollbar.xbm' -> loadIcon, 'ScrollBar') -> addPaletteItem;
   inner;
