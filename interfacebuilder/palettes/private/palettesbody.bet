ORIGIN '../palettes';

INCLUDE '~beta/guienv/v1.3.1/stddialogs';
INCLUDE '~beta/guienv/v1.3.1/controls';
INCLUDE '~beta/guienv/v1.3.1/fields';
INCLUDE '~beta/guienv/v1.3.1/scrolllists';
INCLUDE '~beta/guienv/v1.3.1/styledtext';
INCLUDE '~beta/interfacebuilder/v1.1/objectsstore/objectdata';

[[
-- windowLib: attributes --

(* Deminish *)
-- PaletteWindowDeminish: Descriptor --
(# currentWidth,currentHeight: @Integer;
do Size -> (currentWidth,currentHeight);
   (currentWidth,currentHeight-Height) -> Size;
#)

(* Grow *)
-- PaletteWindowGrow: Descriptor --
(# currentWidth,currentHeight: @Integer;
do Size -> (currentWidth,currentHeight);
   (currentWidth,currentHeight+Height) -> Size;
#)

(* ControlsPalette -Open -Close -Private *)
(*************************************)

-- ControlsPaletteOpen: Descriptor --
(# pos: @integer;
do 'Controls' -> title;
   (pushButtonType, 'PushButton', 'push3') -> addEntry;
   (radioButtonType, 'RadioButton', 'radio') -> addEntry;
   (checkBoxType, 'CheckBox', 'check') -> addEntry;
   (optionButtonType, 'OptionButton', 'option6') -> addEntry;
   (staticTextType, 'StaticText', 'static') -> addEntry;
   (iconButtonType, 'IconButton', 'icon') -> addEntry;
   (scrollBarType, 'ScrollBar', 'scrollbar') -> addEntry;
   (editTextType, 'EditText', 'edit') -> addEntry;
   Inner Open;
#)


-- ControlsPaletteClose: Descriptor --
(# pos: @integer;
do 
   NextCanvasPosition->pos;
   (pos-275)->NextCanvasPosition.set;
   Inner Close;
#)

-- ControlsPalettePrivate: Descriptor --
(#
#)



(* FigureitemsPalette -Open -Close -Private *)
(*************************************)

-- FigureitemsPaletteOpen: Descriptor --
(# pos: @integer;
do 'Draw' -> title;
   (0, 'RoundRect', 'roundrect') -> addEntry;
   (0, 'Wedge', 'wedge') -> addEntry;
   (0, 'Rect', 'rect') -> addEntry;
   (0, 'Polygon', 'poly') -> addEntry;
   (0, 'Line', 'line') -> addEntry;
   (0, 'Oval', 'oval') -> addEntry;
   Inner Open;
#)


-- FigureitemsPaletteClose: Descriptor --
(#
do  Inner Close;
#)

-- FigureitemsPalettePrivate: Descriptor --
(#
#)




(* ViewsPalette -Open -Close -Private *)
(*************************************)

-- ViewsPaletteOpen: Descriptor --
(# pos: @integer;
do 'Views' -> title;
   (canvasType, 'Canvas') -> addEntry;
   (scrollerType, 'Scroller') -> addEntry;
   (textEditorType, 'TextEditor') -> addEntry;
   (textScrollListType, 'ScrollList') -> addEntry;
   (textFieldType, 'TextField') -> addEntry;
   (separatorType, 'Separator') -> addEntry;
#)


-- ViewsPaletteClose: Descriptor --
(# pos: @integer;
do 
   NextCanvasPosition->pos;
   (pos-215)->NextCanvasPosition.set;
   Inner Close;
#)

-- ViewsPalettePrivate: Descriptor --
(#
#)
-- paletteEnvpaletteMenuItemInit: doPart --
do open;
   theName[] -> name;
   this(paletteMenuItem)[] -> this(paletteMenu).append;
   inner;

-- paletteEnvPaletteMenuItemOnSelect: doPart --
do  (name, type) -> itemSelected;
   
   
-- paletteEnvToggleItemOnSelect: doPart --
do  not checked -> checked;
   
-- paletteEnvControlsMenuOpen: doPart --
do 'Controls' -> name;
   (pushButtonType, 'PushButton') -> addPaletteMenuItem;
   (optionButtonType, 'OptionButton') -> addPaletteMenuItem;
   (staticTextType, 'StaticText') -> addPaletteMenuItem;
   (iconButtonType, 'IconButton') -> addPaletteMenuItem;
   (radioButtonType, 'RadioButton') -> addPaletteMenuItem;
   (checkBoxType, 'CheckBox') -> addPaletteMenuItem;
   (scrollBarType, 'ScrollBar') -> addPaletteMenuItem;
   (editTextType, 'EditText') -> addPaletteMenuItem;
   (* MakeMenuItem.open;
    MakemenuItem[] -> append; *)
   
   
-- paletteEnvPaletteMenuPrivate: descriptor --
(#
   installItem: @menuItem
     (# eventHandler::<
          (# onSelect::<
               (# 
               do (if thePalette[]<>none then
                      thePalette.show;
                   else
                      &paletteType[] -> thePalette[];
                      thePalette.open;
                  if);
               #);
          #);
        open::<
          (# 
          do 'Show Palette' -> name;
             installItem[] -> this(paletteMenu).append;
          #);
     #);
#)

-- paletteEnvPaletteMenuOpen: doPart --
do private.installItem.open;
   addSeparator;
   inner;

-- paletteEnvFigureItemsMenuOpen: doPart --
do 'FigureItems' -> name;
   (0, 'Rectangle') -> addPaletteMenuItem;
   (0, 'RoundRect') -> addPaletteMenuItem;
   (0, 'Polygon') -> addPaletteMenuItem;
   (0, 'Wedge') -> addPaletteMenuItem;
   (0, 'Line') -> addPaletteMenuItem;
   (0, 'Oval') -> addPaletteMenuItem;
   
-- paletteEnvViewsMenuOpen: doPart --
do 'Views' -> name;
   (canvasType, 'Canvas') -> addPaletteMenuItem;
   (scrollerType, 'Scroller') -> addPaletteMenuItem;
   (textEditorType, 'TextEditor') -> addPaletteMenuItem;
   (textScrollListType, 'ScrollList') -> addPaletteMenuItem;
   (textFieldType, 'TextField') -> addPaletteMenuItem;
   (separatorType, 'Separator') -> addPaletteMenuItem;
   
-- paletteMenuLib: attributes --
addPaletteMenuItem:
  (# name: ^text;
     type: @integer;
     thePaletteMenuItem: ^paletteMenuItem;
  enter (type, name[])
  do &paletteMenuItem[] -> thePaletteMenuItem[];
     (type, name[]) -> thePaletteMenuItem.init;
  #);
addSeparator:
  (# theSeparator: ^separator;
  do &separator[] -> theSeparator[];
     theSeparator.open;
     theSeparator[] -> append;
  #);

-- paletteEnvPaletteOpen: doPart --
do hide;
   true -> border.visible;
   borderStyles.shadowIn -> border.style;
   private.head.open;
   inner;
   show;

-- paletteEnvPalettePrivate: descriptor --
(# head: @canvas
     (# title: @staticText
          (# open::<
               (#
               do (35,3)->position;
                  (40 ,17)->size;
                  inner OPEN;
               #);
             eventHandler::<
               (# onMouseDown::<(# do this(palette).drag ;#);
               #);
          #);
        closeWindow: @CheckBox
          (#
             open::<
               (# do (2,5)->position;
                  (12,12)->size;
                  ''->label;
               #);
             eventHandler::<
               (# onMouseUp::<
                    (# 
                    do this(palette).hide; 
                    #);
               #);
          #);
        eventHandler::<
          (#
             onMouseDown::<(# do this(palette).drag ;#);
          #);
        open::<
          (#
          do title.open;
             closeWindow.open;
             (paletteWidth + 4, 22) -> size;
             true -> border.visible;
             borderStyles.shadowOut ->border.style;
             inner;
          #);
     #);
#)

-- paletteEnvPaletteSetTitle: doPart --
do theTitle[] -> private.head.title.label;
   

-- paletteEnvPaletteGetTitle: doPart --
do private.head.title.label -> theTitle[];

-- paletteEnvMainMenuOpen: doPart --
do private.controlsItem.init;
   private.figureItemsItem.init;
   private.viewItem.init;

-- paletteEnvMainMenuPrivate: descriptor --
(# controlsItem: @subMenuItem
     (# 
        paletteMenuType::< controlsMenu;
     #);
   figureItemsItem: @subMenuItem
     (# 
        paletteMenuType::< figureItemsMenu;
     #);
   viewItem: @subMenuItem
     (# 
        paletteMenuType::< viewsMenu;
     #);
#)

-- paletteEnvMainMenuSubMenuItemInit: doPart --
do open;
   this(subMenuItem)[] -> append;
   &paletteMenuType[] -> thePaletteMenu[];
   thePaletteMenu.open;
   thePaletteMenu[] -> subMenu;
   thePaletteMenu.name -> name;
   inner;
   
-- paletteEnvTextPaletteAddEntry: doPart --
do (# theEntry: ^entry;
   do &entry[] -> theEntry[];
      private.body[] -> theEntry.open;
      name[] -> theEntry.label;
      type -> theEntry.type;
      private.numberOfEntries + 1 -> private.numberOfEntries;
      private.numberOfEntries -> entryRect -> theEntry.frame;;
   #);

-- paletteEnvTextPaletteOpen: doPart --
do private.body.open;
   inner;
   (paletteWidth, paletteHeight) -> paletteSize;
   
-- textPaletteLib: attributes --
entry: staticText
  (# type: @integer;
     eventHandler::<
       (# onMouseDown::<
            (# 
            do true -> border.visible;
            #);
          onMouseUp::<
            (# 
            do false -> border.visible;
               (label, type) -> this(paletteEnv).itemSelected;
            #);
       #);
     open::<
       (# 
       do borderStyles.etchedIn -> border.style;
       #);
  #);
entryHeight: (# exit 22 #);
entryRect:
  (# entryNumber: @integer;
     left, top, right, bottom: @integer;
  enter entryNumber
  do 3 -> left;
     paletteWidth - 3 -> right;
     ((entryNumber - 1) * (entryHeight + 2)) + 3 -> top;
     top + entryHeight  -> bottom;
  exit ((left, top), (right, bottom))
  #);
paletteHeight: integerValue
  (#
  do private.numberOfEntries * (entryHeight + 2) + 6 -> value;
  #);
paletteSize:
  (# width, height: @integer;
  enter (width, height)
  do (width + 4, height + 26) -> size;
     (width, height) -> private.body.size;
  #);

-- paletteLib: attributes --
paletteWidth: (# exit 100 #);

-- paletteEnvTextPalettePrivate: descriptor --
(# numberOfEntries: @integer;
   body: @canvas
     (# 
        open::<
          (# 
          do (2, 22) -> position;
          #);
     #);
#)

-- iconPaletteLib: attributes --

rasterHome:
  (# path: ^text;
  do '/users/beta/interfacebuilder/v1.1/palettes/bitmaps/' -> path[];
  exit path[]
  #);
loadRaster:
  (# name: ^text;
     path: ^text;
     theBitMap: ^bitMap;
  enter name[]
  do rasterHome -> path[];
     name[] -> path.append;
     '.x11' -> path.append;
     &bitMap[] -> theBitMap[];
     path[] -> theBitMap.readFromX11File;
  exit theBitMap[]
  #);
iconEntry: iconButton
  (# name: ^text;
     type: @integer;
     eventHandler::<
       (# onMouseDown::<
            (# 
            do true -> border.visible;
            #);
          onMouseUp::<
            (# 
            do false -> border.visible;
               (name[], type) -> itemSelected;
            #);
       #);
     init:
       (# rasterName: ^text;
       enter (name[], rasterName[])
       do this(iconPalette).private.body[] -> open;
          rasterName[] -> loadRaster -> icon;
          borderStyles.etchedIn -> border.style;
          false -> showLabel;
       #);
  #);

iconWidth: integerValue
  (# 
  do paletteWidth div numberOfCols  -> value;
  #);
iconHeight: (# exit 32 #);
iconRect:
  (# entryNumber: @integer;
     left, top, right, bottom: @integer;
     row, col: @integer;
     side: @integer;
  enter entryNumber
  do (entryNumber - 1) div numberOfCols -> row;
     (entryNumber - 1) mod numberOfCols -> col;
     iconWidth * col -> left;
     iconHeight * row -> top;
     left + iconWidth -> right;
     top + iconHeight -> bottom;
  exit ((left, top), (right, bottom))
  #);
paletteHeight: 
  (# height: @integer;
  enter height
  do (paletteWidth + 4, height + 26) -> size;
     (paletteWidth, height) -> private.body.size;
  #);
-- paletteEnviconPaletteAddEntry: doPart --
do (# theEntry: ^iconEntry;
   do &iconEntry[] -> theEntry[];
      (name[], rasterName[]) -> theEntry.init;
      type -> theEntry.type;
      private.numberOfEntries + 1 -> private.numberOfEntries;
      private.numberOfEntries -> iconRect -> theEntry.frame;
   #);

-- paletteEnviconPaletteOpen: doPart --
do private.body.open;
   inner;
   (private.numberOfEntries div numberOfCols) * iconHeight -> paletteHeight;

-- paletteEnviconPalettePrivate: descriptor --
(# body: @canvas
     (# 
        open::<
          (# 
          do (2, 22) -> position;
          #);
     #);
   numberOfEntries: @integer;
#)


--]]


