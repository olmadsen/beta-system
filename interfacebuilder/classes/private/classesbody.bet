ORIGIN '../classes';
INCLUDE '~beta/betaast/v5.1/gram';
INCLUDE '~beta/betaast/v5.1/betasematt';
INCLUDE '../../code/followsemanticlink';

-- getClassesDoPart: doPart --
do (# 
      inheritsFrom:
        (# prefix: ^text;
           node: ^astInterface.beta.patternDecl;
           result: @boolean;
        enter (node[], prefix[])
        do (# theObjectDescriptor: ^astInterface.beta.objectDescriptor;
              thePrefixOpt: ^astInterface.AST;
              thePrefix: ^astInterface.beta.prefix;
              theAttributeDenotation: ^astInterface.AST;
              theNameApl: ^astInterface.beta.nameApl;
              thisPrefix: ^text;
              dest: ^astInterface.AST;
              theNameDcl: ^astInterface.beta.nameDcl;
              theNames: ^astInterface.beta.names;
              superNode: ^astInterface.AST;
           do node.getObjectDescriptor -> theObjectDescriptor[];
              theObjectDescriptor.getPrefixOpt -> thePrefixOpt[];
              (if thePrefixOpt.symbol = gram.prefix then
                  thePrefixOpt[] -> thePrefix[];
                  thePrefix.getAttributeDenotation -> theAttributeDenotation[];
                  (if theAttributeDenotation.symbol = gram.nameApl then
                      theAttributeDenotation[] -> theNameApl[];
                      theNameApl.getText -> thisPrefix[];
                      (if prefix[] -> thisPrefix.equalNCS then
                          true -> result
                       else
                          (mps[], theNameApl[]) -> followSemanticLink -> dest[];
                          (if (dest[] <> none) and (dest.symbol = gram.nameDcl) then
                              dest[] -> theNameDcl[];
                              theNameDcl.father -> theNames[];
                              theNames.father -> superNode[];
                              (if superNode.symbol = gram.patternDecl then
                                  (superNode[], prefix[]) -> inheritsFrom -> result;
                              if);
                          if);
                      if);
                  if);
              if);
           #);
        exit result
        #);
      
      collectClasses:
        (# theAttributes: ^astInterface.beta.attributes;
        enter theAttributes[]
        do theAttributes.newScan
           (# thePattern: ^pattern;
           do (if current.symbol = gram.patternDecl then
                  (if (current[], prefix[]) -> inheritsFrom then
                      &pattern[] -> thePattern[];
                      current[] -> thePattern;
                      thePattern[] -> classes.append;
                  if);
              if);
           #);
        #);
      theAttributesForm: ^astInterface.beta.attributesForm;
      gram: @grammar;
   do &patternList[] -> classes[];
      classes.init;
      group.fragmentList.scan
      (# frag: ^astInterface.fragmentForm;
      do (if current.type = mps.formType then
             current.open -> frag[];
             (if frag.category = gram.attributesForm then
                 frag.root[] -> theAttributesForm[];
                 theAttributesForm.getAttributes -> collectClasses;
             if);
         if);
      #);
   #);
-- patternListFindByName: doPart --
do search: scan
     (# theNames: ^astInterface.beta.names;
        theNameDcl: ^astInterface.beta.nameDcl;
     do current.node.getNames -> theNames[];
        1 -> theNames.get -> theNameDcl[];
        (if theNameDcl.getText -> name.equalNCS then
            current.node[] -> node[];
            leave search;
        if);
     #);

