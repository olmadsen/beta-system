ORIGIN 'view';

-- worldLib: attributes --

stringButton: adorner
  (# thePaint: ^color;
     interiorType:: stringView;
     init::<
       (# 
       do red -> thePaint[];
          INNER;
       #);
     onMouseDown::
       (# savedPaint: ^color;
          override:: (# do true -> value #);
       do track
          (# press::
               (#
               do (interior).paint -> savedPaint[];
                  thePaint[] -> (interior).paint;
                  redraw;
               #);
             exitView::
               (#
               do savedPaint[] -> (interior).paint;
                  redraw;
               #);
             enterView::
               (# 
               do thePaint[] -> (interior).paint;
                  redraw;
               #);
             release::
               (# inside::
                    (# do (interior).string -> putLine; #);
               do savedPaint[] -> (interior).paint;
                  redraw;
               #);
          #);
       #);
  #);
imageToggler: adorner
  (# offImage, onImage: ^bitmap;
     onImageHilite: ^bitmap;
     offImageHilite: ^bitmap;
     
     on: @boolean;
     interiorType:: imageView;
     static:: trueObject;
     toggleOn:<
       (# do INNER; #);
     toggleOff:<
       (# do INNER; #);
     
     onMouseDown::
       (# override:: (# do true -> value #);
       do track
          (# press::
               (#
               do (if on then
                      onImageHilite[] -> theInterior.image;
                   else
                      offImageHilite[] -> theInterior.image;
                  if);
                   (if background[] <> NONE then
                       background -> g.pen.foreGroundColor;
                       bounds.asRectangle -> g.fillRect;
                   if);
                  redraw;
               #);
             exitView::
               (#
               do (if on then
                      onImage[] -> theInterior.image;
                   else
                      offImage[] -> theInterior.image;
                  if);
                  (if background[] <> NONE then
                      background -> g.pen.foreGroundColor;
                      bounds.asRectangle -> g.fillRect;
                  if);
                  redraw;
               #);
             enterView::
               (# 
               do (if on then
                      onImageHilite[] -> theInterior.image;
                   else
                      offImageHilite[] -> theInterior.image;
                  if);
                  (if background[] <> NONE then
                      background -> g.pen.foreGroundColor;
                      bounds.asRectangle -> g.fillRect;
                  if);
                  redraw;
               #);
             release::
               (# inside::
                    (# 
                    do not on -> on;
                       (if on then
                           onImage[] -> theInterior.image;
                        else
                           offImage[] -> theInterior.image;
                       if);
                       (if background[] <> NONE then
                           background -> g.pen.foreGroundColor;
                           bounds.asRectangle -> g.fillRect;
                       if);
                       redraw;
                       (if on then
                           toggleOn;
                        else
                           toggleOff;
                       if);
                    #);
               do 
               #);
          #);
       #);
  #);

makeStringButton: makeAdorner
  (# type:: stringButton;
  do INNER;
  #);
