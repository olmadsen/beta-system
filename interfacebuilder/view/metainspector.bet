ORIGIN 'view';
INCLUDE '~beta/mps/v5.1/astlevel';
INCLUDE '~beta/betaast/v5.1/betacfl';
INCLUDE '../prettyprinter/prettyprinter';
INCLUDE '~beta/objectbrowser/v2.1/mpsinterface';

-- guienvLib: attributes --

metaInspector: inspector
  (# interface: @mpsInterface
       (# onNewGroup::
            (# 
            do (* This should never be called *)
               true -> continue;
            #);
          onOldGroup::
            (# 
            do true -> continue;
            #);
          onUncheckedGroup::
            (# 
            do true -> continue;
            #);
          onGroupNotFound::
            (# 
            do true -> continue;
               'Could not open "' -> putText;
               groupName[] -> putText;
               '"' -> putLine;
            #);
          onFatalParseError::
            (# 
            do true -> continue;
            #);
          onParseErrors::
            (# 
            do true -> continue;
            #);
          onOtherError::
            (# 
            do true -> continue;
            #);
       #);
     mps: ^astInterface;
     betagram: ^astInterface.beta;
     pretty: ^astInterface.prettyPrinter;
     
     
     getGroup:
       (# name: ^text;
          group: ^astInterface.fragmentGroup;
       enter name[]
       do name.copy -> name[];
          (if name.length > 3 then
              (if (1 -> name.inxGet) = '/' then 
                  (if (2 -> name.inxGet) = 'a' then
                      (if (3 -> name.inxGet) = '/' then
                          (1, 2) -> name.delete;
                      if);
                  if);
              if);
          if);
          name[] -> interface.fragmentGroupTable.getFragmentGroup -> group[];
       exit group[]
       #);
     
     init:
       (# 
       do interface.init;
          interface.AST[] -> mps[];
          interface.BETACFL[] -> betagram[];
          &mps.prettyprinter[] -> pretty[];
          pretty.init;
       #);
     
     inspect::
       (# proto: @prototype;
          group: ^astInterface.fragmentGroup;
          node: ^astInterface.ast;
       do (if obj[] <> NONE then
              obj[] -> getProtoType -> proto;
              (if proto <> 0 then
                  proto.groupID -> getGroup -> group[];
                  (if group[] <> NONE then
                      (group[], proto.formIndex, proto.astIndex * 2) -> interface.astRefToAst -> node[];
                      (if node[] <> NONE then
                          (node[], screen[]) -> pretty.printNode;
                      if);
                  if);
              if);
          if);
       #);
  #);
