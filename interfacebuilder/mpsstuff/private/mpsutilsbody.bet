ORIGIN '../mpsutils';
INCLUDE '~beta/pretty/v4.9.1/astviewer';
INCLUDE '~beta/betaast/v4.9.1/gram';

-- astInterfaceLib: attributes --
getPretty: objectPool.get
  (# type:: astViewer;
     init::
       (# 
       do exception
          (# 
          do 'No instance of AstViewer found in the objectPool' -> msg.append;
          #);
       #);
  #);

-- lib: attributes --
getMPS: objectPool.get
  (# type:: astInterface;
  #);
  

-- MPSlistIterate: doPart --
do scan
   (# 
   do (if current.kind
       //kinds.interior then
          current[] -> this(iterate).current[];
          inner iterate;
      if);
   #);
-- MPSinteractiveErrorReporterbeforeFirstError: doPart --
do inner
   
-- MPSinteractiveErrorReporterafterLastError: doPart --
do inner

-- MPSinteractiveErrorReporterforEachError: doPart --
do errorStream.newLine;
   errorLines.reset;
   l: (if not errorlines.eos then
          '# ' -> errorStream.putText;
          errorlines.getLine -> errorStream.putLine;
          restart l;
      if);
   'Expected symbol' -> errorStream.putText;
   (if legalSymbols.terminals.range = 1 then 
       ': '->errorStream.putText;
    else
       's: '->errorStream.putText
   if);
   (for i: legalSymbols.terminals.range repeat
        legalSymbols.terminals[i][]->errorStream.putText;
        ' '->errorStream.put
   for);
   errorStream.newLine;
   inner;

-- MPSAsthasGUIcomment: doPart --
do false -> value;
   (if hasComment then
       l: scanComments
         (# 
         do 
            (if current[]<>none then
                (if not current.empty then
                    (if (1 -> current.inxGet) = '$' then 
                        true -> value;
                    if);
                if);
            if);
         #);
   if);

-- MPSAstEnterGUIinfo: doPart --
do (# pretty: ^astInterface.astViewer;
      comment: @text;
      gram: @grammar;
      position: @integer;
   do getPretty -> pretty[];
      '$ ' -> comment.putText;
      value -> comment.putInt;
      frag[] -> pretty.frag[];
      (if symbol
       //gram.patternDecl then
          4 -> position;
       else
          2 -> position;
      if);
      (this(AST)[], position, 1, comment[], false) -> pretty.pp.ppInsertSubComment;
      1 -> typeOfComment;      
   #);
 
-- MPSASTExitGUIinfo: doPart --
do (if hasComment then
       l: scanComments
         (# 
         do (if current[] <> None then
                (if not current.empty then
                    (if (1 -> current.inxGet) = '$' then 
                        1 -> current.setPos;
                        get: current.getInt
                          (# 
                             syntaxError::
                               (# 
                               do leave get;
                               #);
                          #) -> value;
                    if);
                if);
            if);
         #);
   if);
   
