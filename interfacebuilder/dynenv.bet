ORIGIN '~beta/basiclib/v1.6/basicsystemenv';
INCLUDE '~beta/basiclib/v1.6/file'
        '~beta/compiler/CONTROL/system'
        '~beta/compiler/GENERATOR/SPARC/SPARCmachine'
        '~beta/compiler/DYN/linkobject'
        '~beta/compiler/DYN/nameTable';
BODY '~beta/compiler/SYNTHESIZER/synthesizer'
     '~beta/compiler/GENERATOR/SUN4S/SUN4SBasmlink';
-- systemLib: Attributes --
dynenv:
  (#
     comp: ^compiler;
     evaluateFile:
       (# name: ^text; theOrigin,theObject: ^object; 
       enter (name[],theOrigin[])
       do
          (if name[] <> none then
              name[]->comp.rootFragment[];
              'Compiling '->info.putText;
              name[]->info.putLine;
              (if not activateCompiler then
                  theOrigin[]->executeObject->theObject[]; 
              if)
          if);
          
       exit theObject[]
       #);
     NT: @nameTable;
     activateCompiler:
       (# error: @boolean
       do
          L: comp.translate
            (#
               Report:
                 (# msg: ^text
                 enter msg[]
                 do msg[]->comp.messagestream.putline; true->error; leave L
                 #);
               MsgException::<  (#  do msg[]->Report #);
               TransAccessException::<  (#  do msg[]->Report #);
               TransCreateDirException::<  (#  do msg[]->Report #);
               NoSpaceException::<  (#  do msg[]->Report #);
               MPSException::<  (#  do msg[]->Report #);
               notExistingException::<  (#  do msg[]->Report #);
               parseException::<  (#  do msg[]->Report #);
               betaRunException::<  (#  do msg[]->Report #);
               RshException::<  (#  do msg[]->Report #);
               jobFileException::<  (#  do msg[]->Report #);
               multipleMachException::<  (#  do msg[]->Report #);
               unknownMachException::<  (#  do msg[]->Report #);
               doubleFormException::<  (#  do msg[]->Report #);
               fragmentException::
               (* Note that the group is closed here.
                * Sif and other tools may NOT want to do this!
                *)  (#  do CloseGroup; msg[]->Report #);
               propertyException:: 
                 (# 
                 do
                    (if warning then
                        msg[]->comp.messagestream.putLine; true->continue
                     else
                        CloseGroup; msg[]->Report
                    if)
                 #);
               SystemException::  (#  do msg[]->Report #)
            #)
       exit error
       #);
     executeObject:
       (# theOrigin: ^object; F: ##object; R: ^object
       enter theOrigin[]
       do
          (if comp.theMachine[] <> none then
              'Linking new descriptor!'->info.putline;
              (NT[],theOrigin[],trace)->comp.theMachine.linkObject->F##;
              &F[]->R[];
              
          if)
       exit R[]
       #);
     init:
       (# T: ^text; 
       enter comp[]
       do
          screen[]->comp.infostream[];
          screen[]->comp.tracestream[];
          screen[]->comp.messagestream[];
          screen[]->comp.bugstream[];
          1->Arguments->NT.init;
          (true,6)->comp.setSwitch;
          (true,33)->comp.setSwitch;
          &stream[]->info[];
          comp.verboseLevel.nothing->comp.verboseLevel;
          (true,19)->comp.setSwitch;
          (true,90)->comp.setSwitch;
          (for i: NoOfArguments-1 repeat
            i+1->Arguments->T[];
            (if true
             // '--noexe'->T.equalNCS then
                true->noExe
             // '--trace'->T.equalNCS then
                true->trace; screen[]->msg[]
             // '--mute'->T.equalNCS then
                &stream[]->info[];
                comp.verboseLevel.nothing->comp.verboseLevel;
                (true,19)->comp.setSwitch;
                (true,90)->comp.setSwitch;
                
             // '--verbose'->T.equalNCS then
                screen[]->info[];
                comp.verboseLevel.nothing->comp.verboseLevel;
                (false,19)->comp.setSwitch;
                (false,90)->comp.setSwitch;
                
            if)
          for);
          (if msg[] = none then &stream[]->msg[] if);
          'Dynamic Mjolner Environment ...'->info.puttext;
          info.newline;
          
       #);
     info,msg: ^stream;
     noExe,trace: @boolean;
     
  do INNER ; 
  #)  

