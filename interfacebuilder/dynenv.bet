ORIGIN '~beta/basiclib/v1.6/basicsystemenv';
INCLUDE '~beta/basiclib/v1.6/file'
        '~beta/compiler/CONTROL/system'
        '~beta/compiler/GENERATOR/SPARC/SPARCmachine'
        '~beta/compiler/DYN/linkobject'
        '~beta/compiler/DYN/nameTable';
BODY '~beta/compiler/SYNTHESIZER/synthesizer'
     '~beta/compiler/GENERATOR/SUN4S/SUN4SBasmlink';
-- systemLib: Attributes --
dynenv:
  (#
     comp: ^compiler;
     evaluateFile:
       (# name: ^text; theOrigin: ^object; P: ##object; 
       enter (name[],theOrigin[])
       do
          (if name[] <> none then
              name[]->comp.rootFragment[];
              'Compiling '->info.putText;
              name[]->info.putLine;
              (if not activateCompiler then
                  theOrigin[]->linkPattern->P##; 
              if)
          if);
          
       exit P##
       #);
     NT: @nameTable;
     activateCompiler:
       (# error: @boolean
       do
          (true,33)->comp.setSwitch;
          (true,6)->comp.setSwitch;
          L: comp.translate
            (#
               Report:
                 (# msg: ^text
                 enter msg[]
                 do msg[]->comp.messagestream.putline; true->error; leave L
                 #);
               MsgException::<  (#  do msg[]->Report #);
               TransAccessException::<  (#  do msg[]->Report #);
               TransCreateDirException::<  (#  do msg[]->Report #);
               NoSpaceException::<  (#  do msg[]->Report #);
               MPSException::<  (#  do msg[]->Report #);
               notExistingException::<  (#  do msg[]->Report #);
               parseException::<  (#  do msg[]->Report #);
               betaRunException::<  (#  do msg[]->Report #);
               RshException::<  (#  do msg[]->Report #);
               jobFileException::<  (#  do msg[]->Report #);
               multipleMachException::<  (#  do msg[]->Report #);
               unknownMachException::<  (#  do msg[]->Report #);
               doubleFormException::<  (#  do msg[]->Report #);
               fragmentException::
               (* Note that the group is closed here.
                * Sif and other tools may NOT want to do this!
                *)  (#  do CloseGroup; msg[]->Report #);
               propertyException:: 
                 (# 
                 do
                    (if warning then
                        msg[]->comp.messagestream.putLine; true->continue
                     else
                        CloseGroup; msg[]->Report
                    if)
                 #);
               SystemException::  (#  do msg[]->Report #)
            #);
          (false,33)->comp.setSwitch;
          (false,6)->comp.setSwitch
       exit error
       #);
     linkPattern:
       (# theOrigin: ^object; F: ##object
       enter theOrigin[]
       do
          (if comp.theMachine[] <> none then
              'Linking new descriptor!'->info.putline;
              (NT[],theOrigin[],trace)->comp.theMachine.linkObject->F##;
              
          if)
       exit F##
       #);
     init:
       (# T: ^text; progress:< (#  do INNER #); 
       enter comp[]
       do
          1->Arguments->NT.init (# prog: @progress;  do prog #);
          &stream[]->info[];
          (true,19)->comp.setSwitch;
          (true,90)->comp.setSwitch;
          (if msg[] = none then &stream[]->msg[] if);
          'Dynamic Mjolner Environment ...'->info.puttext;
          info.newline;
          
       #);
     info,msg: ^stream;
     noExe,trace: @boolean;
     
  do INNER ; 
  #)  

