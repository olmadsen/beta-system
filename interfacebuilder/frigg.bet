ORIGIN 'friggapplication';
INCLUDE '~beta/guienv/guienvsystemenv'
        '~beta/compiler/DYN/dynlib'
        '~beta/sysutils/time'
        'seticon';
BODY '~beta/editor/private/compiler';
BODY '~beta/editor/private/nouser';
-- program: Descriptor --
systemenv
(# gui: @guienv
     (#
     #);
     setWindowEnv::  (#  do gui[]->theWindowEnv[] #);
     DC: ^myCompiler;
     ensureCompiler:
       (# 
       do (if DC[] = none then &myCompiler[]->DC[]; DC.initDyn if)
       #);
     myCompiler: frigg.MPS.AST.dynamicCompiler
       (#
          initDyn::< 
            (# thisExecutable: ^text; 
               clockBefore, clockAfter: @integer;
            do
               screen[]->info[];
               (if msg[] = none then &stream[]->msg[] if);
               'Dynamic Mjolner Environment ...'->info.puttext;
               (1->Arguments->thisExecutable[],false)->NT.init;
               '...'->info.puttext;
               (* On nti, calling dynenv.exe is normal and the compiler
                * cannot handle any extension but .bet *)
               exeFileExtension->thisExecutable.stripExtension;
               systemTime -> clockBefore;
               (thisExecutable[],none )
                 ->ActivateCompiler
                   (#
                      addedFragment:: 
                        (# count: @integer;
                        do (if (count mod 10) = 0 then
                               fullFn[] -> putline;
                           if);
                           count + 1 -> count;
                        #);
                      addedTranslate:: 
                        (#  do 'Translate: '->putText; fullFn[]->putLine #);
                      checkedFragment:: 
                        (#  do 'Checked: '->putText; fullFn[]->putLine #);
                      codegenFragment:: 
                        (# 
                        do 'Code generated: '->putText; fullFn[]->putLine
                        #)
                   #);
               systemTime -> clockAfter;
               
               'Time to INIT: [' -> putText;
               clockAfter - clockBefore -> putInt;
               ']' -> putLine;
               
            #);
          compile:
            (# fname: ^text; OK,err: @boolean; fNameIm: ^ImagePair
            enter fname[]
            do
            (* ConvertFilePath stuff added to prevent
             * problems with /users/datpete/... and
             * /a/home/ariel1/datpete/... being
             * different. The pathhandler checks for
             * aliases to the same file.
             *)
               (fname[],none )->frigg.MPS.AST.thePathhandler.convertFilePath
                 ->StripBetaExtension->fname[];
               L:
               (fname[],none )
                 ->ActivateCompiler
                   (# Report:: (#  do true->err; leave l #) #)->err;
               (if not (err) then
                   true->OK; fname[]->LoadFiles->fNameIm[]; 
               if)
            exit (OK,fNameIm[])
            #)
       #);
     frigg: @friggApplication
       (#
          compilePattern:: 
            (#
               ok: @boolean;
               imp: ^imagePair;
               pt: @integer;
               fname: ^text;
               pname: ^text;
               
            do
               ensureCompiler;
               (node.frag.father).fullname->fname[];
               node[]->frigg.MPS.betacfl.getName->pname[];
               fname[]->DC.compile->(ok,imp[]);
               (if OK then
                   (if imp[] <> none then
                       pname[]->imp.namedOff->pt;
                       (theOrigin[],pt)->DC.makeStruc->structure##;
                       
                   if);
                   
               if);
               
            #);
          init::  
            (#  
            do 
               
            #);
          
       #);
     isInteger:
       (# str: ^text;
          b: @boolean;
       enter str[]
       do true -> b;
          l: str.scanAll
          (#
          do (if NOT (ch -> ascii.isDigit) then
                 false -> b;
                 leave l;
             if);
          #);
       exit b
       #);
  do &expandWildcardsArgumentHandler[]->argumentHandler[];
     (# inx: @integer;
        arg: ^text;
        n: @integer;
     do 2 -> inx;
        l: (if inx <= noOfArguments then
               inx -> arguments -> arg[];
               (If '-s' -> arg.equal then
                   inx + 1 -> inx;
                   l2: (if inx <= noOfArguments then
                           inx -> arguments -> arg[];
                           arg.reset;
                           (if arg[] -> isInteger then
                               arg.asInt -> n;
                               (if (n > 0) and (n <= 256) then
                                   true -> frigg.theGraphicalEditorEnv.switch[n];
                               if);
                               inx + 1 -> inx;
                               restart l2;
                           if);
                       if);
               if);
               inx  + 1 -> inx;
               restart l;
           if);
     #);
  #)  

