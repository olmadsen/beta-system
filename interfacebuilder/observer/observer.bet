ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/containers/sets';
-- lib: Attributes --
Subject:
  (* Subject knows its obersers and provides methods for attaching
   * observers and sending notifications when subject is modified.
   *)
  (# observers: @Set (# element:: Observer #);
     Init:< 
       (* Initializes the subject. This method must be called
        * before using any other operation on the subject.
        *)
       (#  do observers.init; INNER #);
     Attach:
       (* Register an observer. The observer will recieve
        * notifications when the subject is modified.
        *)
       (# theObserver: ^Observer
       enter theObserver[]
       do theObserver[]->observers.insert
       #);
     Detach:
       (* Unregister an observer. The observer will no longer
        * recieve notifications when the subject is modified.
        *)
       (# theObserver: ^Observer
       enter theObserver[]
       do theObserver[]->observers.delete
       #);
     Notify: 
       (* Notify all registered observers that the subject has been
        * modified. INNER is called before the notification is send.
        * This makes notify useful as a superpattern for modify
        * operations.
        *)
       (#  do INNER ; observers.scan (#  do current.Update #) #)
  #);
Observer: 
  (* The observer updates its display or state when the update
   * notification is recieved from a subject. The observer is
   * allowed to modify a subject using the Notify interface.
   * It does not know the other Observers directly.
   *)
  (# Update:< 
       (* Virtual procedure that represents the Notification
        * recieved from subject. Subpatterns will bind this
        * to update state or display.
        *)
       (#  do INNER #) 
  #)  

