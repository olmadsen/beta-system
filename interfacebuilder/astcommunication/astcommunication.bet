ORIGIN '~beta/basiclib/v1.5/betaenv';
BODY 'private/astcommunicationbody';

INCLUDE '~beta/mps/v5.1/astlevel';

-- lib: attributes --

notify:
  (# node: ^astInterface.AST;
     private: @<<SLOT libNotifyPrivate: descriptor>>;
  enter node[]
  <<SLOT libNotify: doPart>>
  #);
notifyChange: notify
  (# <<SLOT libNotifyChange: doPart>> #);
notifyDelete: notify
  (# <<SLOT libNotifyDelete: doPart>> #);
notifyReplace: notify
  (# newNode: ^astInterface.AST;
  <<SLOT libNotifyReplace: doPart>>
  #);


astController:
  (# <<SLOT astControllerLib: attributes>>;
     node: ^astInterface.AST;
     
     init:<
       (# 
       enter node[]
       <<SLOT astControllerInit: doPart>>
       #);
     release:<
       (# <<SLOT astControllerRelease: doPart>> #);
     
     (*
      * Sending notifications
      *)
     
     notify:
       (# private: @<<SLOT notifyPrivate: descriptor>>;
       <<SLOT astControllerNotify: doPart>>
       #);
     notifyChange: notify
       (#  <<SLOT astControllerChange: doPart>> #);
     notifyDelete: notify
       (# <<SLOT astControllerDelete: doPart>> #);
     notifyReplace: notify
       (# newNode: ^astInterface.AST;
       <<SLOT astControllerReplace: doPart>>
       #);
     
     (*
      * Notifications
      *)
     
     onAncestorReplaced:<
       (# do inner #);
     onDeleted:<
       (# do inner #);
     onReplaced:<
       (# 
       enter node[]
       do inner
       #);
     onChanged:<
       (# do inner  #);
     
     present:<
       (# do inner #);
     
     dirty:
       (# value: @boolean
       enter (# enter value <<SLOT astControllerEnterDirty: doPart>> #)
       exit (# <<SLOT astControllerExitDirty: doPart>> exit value #)
       #);
     beeingEdited: booleanValue
       (# <<SLOT astControllerBeeingEdited: doPart>> #);
     
     private: @<<SLOT AstControllerPrivate: descriptor>>;
  exit node[]
  #);
