ORIGIN '~beta/editor/v5.1/ymersiflib';
BODY '~beta/editor/v5.1/private/compiler';
BODY '~beta/editor/v5.1/private/nouser';

INCLUDE '~beta/editor/v5.1/private/editorenvbody';
INCLUDE '~beta/sourcebrowser/v1.1/private/ymerBody';

INCLUDE 'graphicaleditor/graphicaleditor';
INCLUDE '~beta/guienv/v1.4/utils/getfile';
INCLUDE 'mpsstuff/mpsutils';
INCLUDE 'dialogs/promptfortextdialog';
INCLUDE 'code/generate';
INCLUDE 'resourcesupport/parameters';
(* INCLUDE 'preferences/preferences'; *)
INCLUDE 'guienvstuff/getpreferencefile';
INCLUDE 'editorenv/prettyprinter';
INCLUDE 'utils/catchallfile';
INCLUDE 'buildermenu';
INCLUDE '~beta/pretty/v5.1/pplib';
INCLUDE 'dialogs/applicationdialog';



-- program: descriptor --
fragmentBrowser
(# isBETAsif:: (# do true -> value #);
   
   (* 
    * Handling of notifications from Sif
    *)
   
   (* onAboutToCloseGroup::
    *      (# theDocument: ^graphicalEditorEnv.document;
    *      do 'about to close group: ' -> putText;
    *         fg.name -> putLine;
    *         fg[] -> theGraphicalEditorEnv.findRelatedDocument -> theDocument[];
    *         (if theDocument[] <> none then
    *             (if theDocument.touched  then
    *                 (none,'', 'Save changes to window before closing?') -> promptForBoolean
    *                 (# 
    *                    ok::
    *                      (# 
    *                      do theDocument.save;
    *                         true -> okToClose;
    *                         false -> askSave;
    *                      #);
    *                    notOk::
    *                      (# 
    *                      do true -> okToClose;
    *                      #);
    *                    cancel::
    *                      (# 
    *                      do false -> okToClose;
    *                      #);
    *                 #);
    *             if)
    *         if);
    *      #);
    *)
   
   buildApplication:
     (# frag: ^astInterface.fragmentForm;
        theDialog: @applicationDialog
          (# onBuild::
               (# 
               do theDialog.close;
                  (if resourceFileName[] -> entryWriteable then
                      (frag.father, resourceFileName[]) -> compileResources;
                   else
                      'You do not have write permissions build the resource file' -> displayMessage;
                  if);
               #);
             onCancel::
               (# 
               do theDialog.close;
               #);
          #);
        defaultNames:
          (# frag: ^astInterface.fragmentForm;
             applicationName, resourceFileName: ^text;
             groupName: ^text;
             e: @diskEntry;
          enter frag[]
          do (frag.father).name -> groupName[];
             groupName[] -> e.path;
             e.path.name -> applicationName[];
             '.rc' -> (applicationName.copy).append -> resourceFileName[];
          exit (resourceFileName[], applicationName[])
          #);
     enter frag[]
     do 
        frag[] -> defaultNames -> theDialog.run;
     #);
   compileResources:
     (# root: ^astInterface.fragmentGroup;
        fileExists: booleanValue
          (# fileName: ^text;
             anEntry: @diskEntry;
          enter fileName[]
          do fileName[] -> anEntry.path;
             l: anEntry.exists
               (# error::
                    (# 
                    do leave l;
                    #);
               #) -> value;
          #);
        
        getResourceData:
          (# fg: ^astInterface.fragmentGroup;
             resourceData: ^atom;
          enter fg[]
          do fg[] -> theGraphicalEditorEnv.findResourceData -> resourceData[];
             getting:
               (if resourceData[] = none then
                   (# 
                      theFile: @catchAllFile
                        (# failure::
                             (# 
                             do
                                leave getting;
                             #);
                        #);
                      parser: @atomParser;
                      resourceFileName: ^text;
                      ignoreAtom: ^text;
                      version: @integer;
                   do '.res' -> ((fg.name).copy).Append -> resourceFileName[];
                      (if resourceFileName[] -> fileExists then
                          resourceFileName[] -> theFile.name;
                          theFile.openRead;
                          theFile.getAtom -> ignoreAtom[];
                          theFile.getInt -> version;
                          (if version = 2 then
                              theFile[] -> parser.init;
                              parser.readAtom -> resourceData[];
                          if);
                          theFile.close;
                      if);
                   #);
               if);
          exit resourceData[]
          #);
        addResourceFile:
          (# 
             fg: ^astInterface.fragmentGroup;
          enter fg[]
          do adding:
               (# 
                  parameterList: ^atom;
                  resourceEntry: ^tableAtom;
                  nameAtom: ^textAtom;
               do
                  fg[] -> getResourceData  -> parameterList[];
                  (if parameterList[] <> none then
                      &tableAtom[] -> resourceEntry[];
                      resourceEntry.storage.init;
                      &textAtom[] -> nameAtom[];
                      fg.shortName -> nameAtom;
                      ('name', nameAtom[]) -> resourceEntry.define;
                      ('value', parameterList[]) -> resourceEntry.define;
                      resourceEntry[] -> theListAtom.storage.append;
                  if);
               #);
          #);
        theListAtom: ^listAtom;
        printer: @atomPrinter;
        output: @file;
        fileName: ^text;
     enter (root[], fileName[])
     do 
        (if fileName[] <> none then
            'Compiling resources... ' -> ymerbrowser.browser.infoView.setText;
            ymerbrowser.browser.infoView.display;
            &listAtom[] -> theListAtom[];
            theListAtom.storage.init;
            machineType -> MPS.dg.targetMachine;
            machineType -> MPS.dg.targetDirectory;
            root.name -> MPS.dg.scanExtent
            (# resourceFileName: ^text;
            do current[] -> addResourceFile;
            #);
            fileName[]  -> output.name;
            output.openWrite;
            output[] -> printer.init;
            theListAtom[] -> printer.printAtom;
            output.close;
            'done' -> ymerbrowser.browser.infoView.append;
            ymerbrowser.browser.infoView.display;
        if);
     #);
   
   onGroupSaved::
     (# theDocument: ^graphicalEditorEnv.document;
     do 
        fg[] -> theGraphicalEditorEnv.findRelatedDocument -> theDocument[];
        
        (if theDocument[] <> none then
            (if theDocument.touched  then
                theDocument.save;
            if);
        if)
     #);
   onGroupNotSaved::
     (# theDocument: ^graphicalEditorEnv.document;
     do fg[] -> theGraphicalEditorEnv.findRelatedDocument -> theDocument[];
        
        (if theDocument[] <> none then
            theDocument.deleteAutosaveFiles;
        if)
     #);
   onGroupClosed::
     (# theDocument: ^graphicalEditorEnv.document;
     do fg[] -> theGraphicalEditorEnv.findRelatedDocument -> theDocument[];
        (if theDocument[] <> none then
            theDocument[] -> theGraphicalEditorEnv.closeGraphicalEditors;
        if)
     #);
   onGroupCompiled::
     (# frag: ^astInterface.fragmentForm;
     do (if ok then
            'program' -> fg.findFragment -> frag[];
            (if frag[] <> none then
                frag[] -> buildApplication;
            if);
        if);
     #);
   
   (* prefs: ^preferences.section; *)
   
   (*
    * Misc utilities that ought to be somewhere else...
    *)
   
   stripExtension:
     (# path: ^text;
        lastDotInx, lastSlashInx: @integer;
     enter path[]
     do '.' -> path.findAll (# do inx -> lastDotInx #);
        MPS.ast.thePathHandler.directoryChar -> path.findAll (# do inx -> lastSlashInx #);
        (if (lastDotInx > 0) and (lastDotInx > lastSlashInx) then
            (lastDotInx, path.length) -> path.delete;
        if);
     #);
   
   getName:
     (# path: ^text;
        name: ^text;
        lastSlashInx: @integer;
     enter path[]
     do MPS.ast.thePathHandler.directoryChar -> path.findAll (# do inx -> lastSlashInx #);
        (if (lastSlashInx > 0) and (lastSlashInx <> path.length) then
            (lastSlashInx + 1, path.length) -> path.sub -> name[];
         else
            path.copy -> name[];
        if);
     exit name[]
     #);
   
   createFile:
     (# fileName: ^text;
        msg: ^text;
     enter msg[]
     do ymerBrowser[] -> fileCreationDialog
        (# 
        do (* msg[] -> label[]; *)
        #) -> fileName[];
        (if fileName[] <> none then
            fileName[] -> stripExtension;
        if);
     exit fileName[]
     #);
   getIdentifier:
     (# identifier: ^text;
        prompt: ^text;
     enter prompt[]
     do (none, 'Create window', prompt[], '') -> promptForText
        (# 
           validate::
             (# str: ^text
             do false -> value;
                userText.copy -> str[];
                checking:
                  (# EOS: (# exit 1 #);
                     ch: @char;
                     next:
                       (# 
                       do (if str.eos then
                              EOS -> ch;
                           else
                              str.get -> ch;
                          if);
                       #);
                     
                  do str.reset;
                     next;
                     (if ch = EOS then
                         leave checking;
                      else
                         (if ch -> ASCII.isLetter then
                             next;
                             l:
                               (if ch = EOS then
                                   leave l;
                                else
                                   (if (ch -> ASCII.isLetter) or (ch -> ASCII.isDigit) then
                                       next;
                                       restart l;
                                    else
                                       leave checking;
                                   if);
                               if);
                          else
                             leave checking;
                         if);
                     if);
                     true -> value;
                  #);
                
                (if not value then
                    'There are ilegal characters the name' -> displayMessage;
                if);
             #);
           ok::
             (# 
             do userText.copy -> identifier[];
             #);
        #);
     exit identifier[]
     #);
   (* getIdentifier:
    *      (# prompt: ^text;
    *         identifier: ^text;
    *         values: @promptForTextValues;
    *      enter prompt[]
    *      do prompt[] -> values.prompt[];
    *         values[] -> dopromptForText
    *         (# verify::
    *              (# str: ^text;
    *              do false -> okToClose;
    *                 values.theText[] -> str[];
    *                 checking:
    *                   (# EOS: (# exit 1 #);
    *                      ch: @char;
    *                      next:
    *                        (# 
    *                        do (if str.eos then
    *                               EOS -> ch;
    *                            else
    *                               str.get -> ch;
    *                           if);
    *                        #);
    *                   do str.reset;
    *                      next;
    *                      (if ch = EOS then
    *                          leave checking;
    *                       else
    *                          (if ch -> ASCII.isLetter then
    *                              next;
    *                              l:
    *                                (if ch = EOS then
    *                                    leave l;
    *                                 else
    *                                    (if (ch -> ASCII.isLetter) or (ch -> ASCII.isDigit) then
    *                                        next;
    *                                        restart l;
    *                                     else
    *                                        leave checking;
    *                                    if);
    *                                if);
    *                           else
    *                              leave checking;
    *                          if);
    *                      if);
    *                      true -> okToClose;
    *                   #);
    *                 (if not okToClose then
    *                     system.beep;
    *                 if);
    *              #);
    *            accept::
    *              (# 
    *              do values.theText.copy -> identifier[];
    *              #);
    *         #);
    *      exit identifier[]
    *      #);
    *)
   externalFragmentFormHandler: ymerBrowser.browser.edenv.externalFragmentFormHandler
     (# astReplacedEvent::
          (# 
          do (false, ff[], oldAst[], newAst[]) -> theFragServer.notifyAstReplaced;
          #);
        astReplacedSequenceEvent::
          (# 
          do (false, ff[], rootOfSequence[]) -> theFragServer.notifyAstReplacedSequence;
          #);
        listElementInsertedEvent::
          (#
          do (false, ff[], listNode[], position) -> theFragServer.notifylistElementInserted;
          #);
        makeAstList:
          (# thePartList: ^window.editorEnv.partList;
             theAstList: ^astInterface.astList;
             length: @integer;
          enter (length, thePartList[])
          do &mps.ast.astList[] -> theAstList[];
             theAstList.init;
             (for inx: length repeat
                  thePartList.elm[inx][] -> theAstList.insert;
             for);
          exit theAstList[]
          #);
        listElementsDeletedEvent::
          (#
          do (false, ff[], listNode[], position, length, (length, oldElements[]) -> makeAstList)
               -> theFragServer.notifyListElementsDeleted;
          #);
        listElementsReplacedEvent::
          (#
          enter (listNode[],position,length,oldElements[],newLength)
          do (false, ff[], listNode[], position, length, 
             (length, oldElements[]) 
               -> makeAstList, newLength)
               -> theFragServer.notifyListElementsReplaced;
          #);
     #);
   
   theFragServer: @fragmentServer
     (# subscribe::
          (# effh: ^externalFragmentFormHandler
          do &externalFragmentFormHandler[] -> effh[];
             fragHandler.frag[] -> effh.ff[];
             effh[] -> ymerBrowser.browser.edenv.ffhandler.externalSubscribe;
          #);
        
        findFFhandler:
          (# frag: ^astInterface.fragmentForm;
             ffh: ^window.editorEnv.fragmentFormHandler;
          enter frag[]
          do l: ymerBrowser.browser.edenv.ffhandler.scan
               (# 
               do 
                  (if current.ff[] = frag[] then
                      current[] -> ffh[];
                      leave l;
                  if);
               #);
          exit ffh[]
          #);
        withFFH:
          (# ffh: ^window.editorEnv.fragmentFormHandler;
             frag: ^astInterface.fragmentForm;
             fromInterfaceBuilder: @boolean;
          enter (frag[],  fromInterfaceBuilder)
          do (if fromInterfaceBuilder then
                 frag[] -> findFFhandler -> ffh[];
                 (if ffh[] <> none then
                     inner;
                 if);
             if);
          #);
        notifyAstReplaced::
          (# 
          do (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do (none, oldAst[], newAst[]) -> ffh.anAstReplacedEvent;
             #);
          #);
        notifyListElementInserted::
          (#
          do (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do (none, listNode[], position) -> ffh.aListElementInsertedEvent;
             #);
          #);
        notifyAstReplacedSequence::
          (# 
          do (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do (none, rootOfSequence[]) -> ffh.astReplacedSequenceEvent;
             #);
          #);
        makePartList:
          (# theAstList: ^astInterface.astList;
             thePartList: ^window.editorEnv.partList;
             inx: @integer;
          enter theAstList[]
          do &ymerBrowser.browser.edenv.partList[] -> thePartList[];
             theAstList.size -> thePartList.elm.new;
             0 -> inx;
             theAstList.scan
             (# 
             do inx + 1 -> inx;
                current[] -> thePartList.elm[inx][];
             #);
          exit thePartList[]
          #);
        notifyListElementsDeleted::
          (# 
          do 
             (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do 
                (none, listNode[], position, length, oldElements[] -> makePartList) 
                  -> ffh.aListElementsDeletedEvent;
             #);
          #);
        notifyListElementsReplaced::
          (# 
          do (frag[], fromInterfaceBuilder) -> withFFH
             (# 
             do 
                (none, listNode[], position, length, oldElements[] -> makePartList, newLength) 
                  -> ffh.aListElementsReplacedEvent;
             #);
          #);
     #);
   
   
   pretty: @MPS.AST.prettyPrinter;
   
   findGroupEditor:
     (# fg: ^astInterface.fragmentGroup;
        theGroupEditor: ^window.editorEnv.groupEditor;
     enter fg[]
     do (ymerBrowser.browser[], fg[]) 
          -> ymerBrowser.browser.edenv.groupEditorList.findGroupEditor -> theGroupEditor[];
        (if theGroupEditor[] = none then
            'The groupeditor for ' -> putText;
            fg.name -> putText;
            ' was not found' -> putLine;
        if);
     exit theGroupEditor[]
     #);
   
   theGraphicalEditorEnv: @graphicalEditorEnv
     (# save::
          (# 
          do 'Calling save all' -> putLine;
             ymerBrowser.saveAll;
          #);
        openAstEditor::
          (# 
          do node.frag[] -> ymerBrowser.findForm;
             (node[], node[]) -> ymerBrowser.browser.cfe.openSubEditor;
          #);
        autoSaveGroup::
          (# 
          do ('#', fg[]) -> ymerBrowser.browser.edenv.editorenvPrivate.extendedSaveBackup  
          #);
        onGroupOpen::
          (#
          do fg[] -> ymerBrowser.findGroup;
          #);
        saveFragmentGroup::
          (# theGroupEditor: ^window.editorEnv.groupEditor;
          do fg[] -> findGroupEditor -> theGroupEditor[];
             (if theGroupEditor[] = none then
                 'The groupeditor is NONE!' -> putLine;
              else
                 (true, false) -> theGroupEditor.save;
             if);
          #);
        onGroupChanged::
          (# theGroupEditor: ^window.editorEnv.groupEditor;
          do fg[] -> findGroupEditor -> theGroupEditor[];
             (if theGroupEditor[] = none then
                 'The groupeditor is NONE!' -> putLine;
              else
                 theGroupEditor.groupTouched;
             if);
          #);
        onGroupSaved::
          (# theGroupEditor: ^window.editorEnv.groupEditor;
          do fg[] -> findGroupEditor -> theGroupEditor[];
             (if theGroupEditor[] = none then
                 'The groupeditor is NONE!' -> putLine;
              else
                 theGroupEditor.resetGroupTouch;
             if);
          #);
     #);
   
   
   (* 
    * Application info
    *)
   
   
   (* End of application info *)
   
   ymerBrowserWindow::
     (# makeSavePrompt:
          (# fg: ^astInterface.fragmentGroup;
             str: ^text;
          enter fg[]
          do 'Save changes to ' -> str[];
             fg.name -> str.putText;
             '?' -> str.put;
          exit str[]
          #);
        deleteHistory:
          (# fg: ^astInterface.fragmentGroup;
             
          enter fg[]
          do '[ Scanning projects ]' -> putLine;
             browser.projects.theProjects.scan
             (# 
             do 'Project = ' -> putText;
                current.location[] -> putLine;
                current.groups.scan
                (# 
                do 'group = ' -> putText;
                   current.location[] -> putText;
                   (if current.fg[] = fg[] then
                       ' ***' -> putLine;
                       none -> current.fg[];
                    else
                       newLine;
                   if);
                #);
             #);
             '[ Done scanning projects ]' -> putLine;
             newLine;
             ymerBrowserPrivate.history.iterate
             (# 
             do 
                (if current.elm.theGroup.fg[] = fg[] then
                    current.elm[]->(theMenubar).historyMenu.closeHistoryElement;
                    current[]->ymerBrowserPrivate.history.delete;
                if);
             #);
             (if browser.currentProject[] <> none then
                none ->browser.currentProject.lastGroupSelected[]
             if);
             (if browser.currentGroup[] <> none then
                none ->browser.currentGroup.lastFormSelected[]
             if);
             (browser.currentProject[],none ) -> browser.setGroup;
             none -> browser.cge[];
             none -> browser.cfe[];
          #);
        closeFragmentGroup:
          (# fg: ^astInterface.fragmentGroup;
             okToClose: @boolean;
          enter fg[]
          do l:
               (# ge: ^window.editorEnv.groupEditor;
               do fg[] -> findGroupEditor -> ge[];
                  (if ge[] <> none then
                      (if ge.touched > 0 then
                          (none, 'Close group', fg[] -> makeSavePrompt) -> promptForBoolean
                          (# ok::
                               (# 
                               do (true, true) -> ge.save;
                                  true -> okToClose;
                               #);
                             notOK::
                               (# 
                               do true -> okToClose;
                               #);
                             cancel::
                               (# 
                               do false -> okToClose;
                               #);
                          #);
                       else
                          true -> okToClose;
                      if);
                      (if okToClose then
                          fg[] -> deleteHistory;
                      if);
                  if);
                  (if okToClose then
                      (* fg.close; *)
                  if);
               #);
          exit okToClose
          #);
        
        (* onTerminateApplication::
         *           (# 
         *           do theGraphicalEditorEnv.scanDocuments
         *              (# help: ^text;
         *              do (if current.touched then
         *                     'Save changes to the windows in ' -> help[];
         *                     current.interfaceGroup.name -> getName -> help.append;
         *                     '?' -> help.append;
         *                     (this(ymerBrowserWindow)[], '', help[]) -> promptForBoolean
         *                     (# 
         *                        ok::
         *                          (# 
         *                          do current.save;
         *                             current.close;
         *                             true -> okToTerminate;
         *                          #);
         *                        notOk::
         *                          (# 
         *                          do current.deleteAutosaveFiles;
         *                             current.close;
         *                             true -> okToTerminate;
         *                          #);
         *                        cancel::
         *                          (# 
         *                          do false -> okToTerminate;
         *                          #);
         *                     #);
         *                 if);
         *              #);
         *           #);
         *)
        
        
        
        (*
         * Menubar
         *)
        
        menubarType::
          (# groupFileMenuType::
               (# friggCloseItem: @item
                    (# onStatus::
                         (# 
                         do browser.cge[] <> none -> value;
                         #);
                       onSelect::
                         (# 
                         do browser.cge.fg[] -> closeFragmentGroup;
                         #);
                    #);
                  
                  open::
                    (# 
                    do
                       'New...'->iNew.new;
                       'New BETA Program...'->iNewBetaProgram.new;
                       'New BETA Library...'->iNewBetaLibrary.new;
                       newSeparator;
                       'Close'->iClose.new;
                       'Save'->iSave.new;
                       (* 'Save As...'->iSaveAs.new; *)
                       'Save All...'->iSaveAll.new;
                       'Save Abstract...'->iSaveAbstract.new;
                       newSeparator;
                       'Recover'->iRecover.new;
                       'Revert'->iRevert.new;
                       newSeparator;
                       'Group Write Protection'->iSetIsReadOnly.new;
                       'Global Write Protection'
                         ->iSetGlobalWriteProtection.new
                    #);
                  
               #);
             groupPerformMenuType::
               (#                                         
                  open::< 
                    (# 
                    do
                       'Check Current'->iCheck.new;
                       'Recheck'->iReCheck.new;
                       'Compile Current'->iCompile.new;
                       'Recompile'->iRecompile.new;
                       'Execute Current'->iExecute.new;
                       'Reexecute'->iReExecute.new;
                       newSeparator;
                       'Debug'->iDebug.new;
                       newSeparator;
                       'Semantic Errors...'->iSemanticErrors.new;
                       newSeparator;
                       'Show Diagram'->iShowDiagram.new;
                       INNER
                    #)
               #);
             ibMenu: @builderMenu
               (# newSourceFileItem: @builderItem
                    (# eventHandler::
                         (# onStatus::
                              (# 
                              do true -> value;
                              #);
                            onSelect::
                              (# 
                              do createSourceFile;
                              #);
                         #);
                       open::
                         (# 
                         do 'New GUI library...' -> name;
                         #);
                    #);
                  saveItem: @builderItem
                    (#  eventHandler::
                         (# onStatus::
                              (# 
                              do true -> value;
                              #);
                            onSelect::
                              (# 
                              do 
                                 l: edenv.groupEditorList.scan
                                   (# help: ^text; done: @boolean
                                   do
                                      (if (current.touched > 0) and not current.isAfrejaEditor then
                                          'Save changes to '->help[];
                                          current.fg.name->help.append;
                                          '?'->help.append;
                                          (THIS(ymerBrowserWindow)[],
                                          'Save group',help[])
                                            -> promptForBoolean
                                          (#
                                             ok::< 
                                               (# 
                                               do
                                                  (true,false)->current.save
                                               #);
                                             notOK::<  (#  do  #);
                                             cancel::< 
                                               (#  do true->done #);
                                             
                                          #);
                                       else
                                          doCommand
                                          (# 
                                          do
                                             (if current.needToUpdateTextFile and
                                                 not current.isAFrejaEditor then
                                                 current.fg[]
                                                   ->edenv.makeNewTextFile
                                             if)
                                          #)
                                      if);
                                      (if done then leave l if)
                                   #)
                              #);
                         #);
                       open::
                         (# 
                         do 'Save all' -> name;
                         #);
                    #);
                  openGraphicalEditor: @builderItem
                    (# eventHandler::
                         (# onStatus::
                              (# name: ^text;
                              do false -> value;
                                 (if browser.cfe[] <> none then
                                     (if browser.cfe.cs[] <> none then
                                         (if browser.cfe.cs.node[] <> none then
                                             (if browser.cfe.cs.node.hasGUIcomment then
                                                 (if browser.cfe.cs.node.symbol = MPS.betacfl.patternDecl then
                                                     browser.cfe.cs.node[] -> MPS.betacfl.getPrefix -> name[];
                                                     (if name[] <> none then
                                                         (if 'window' -> name.equalNCS then
                                                             true -> value;
                                                         if);
                                                     if);
                                                 if);
                                             if);
                                         if);
                                     if);
                                 if);
                              #);
                            onSelect::
                              (# group: ^astInterface.fragmentGroup;
                                 result: @boolean;
                              do browser.cfe.cs.node.frag.father -> group[];
                                 browser.cfe.cs.node[] -> editWindow;
                              #);
                         #);
                       open::
                         (# 
                         do 'Edit window...' -> name;
                         #);
                    #);
                  newGraphicalEditor: @builderItem
                    (# eventHandler::
                         (# onStatus::
                              (# frag: ^astInterface.fragmentForm;
                              do false -> value;
                                 (if browser.cfe[] <> none then
                                     (if browser.cfe.cs.node[] <> none then
                                         browser.cfe.cs.node.frag[] -> frag[];
                                         (if frag[] <> None then
                                             (if frag.category = mps.betaCfl.attributesForm then
                                                 true -> value;
                                             if);
                                         if);
                                     if);
                                 if);        
                              #);
                            onSelect::
                              (# frag: ^astInterface.fragmentForm;
                                 group: ^astInterface.fragmentGroup;
                                 result: @boolean;
                              do (if browser.cfe[] <> none then
                                     (if browser.cfe.cs.node[] <> none then
                                         browser.cfe.cs.node.frag[] -> frag[];
                                         (if frag[] <> None then
                                             (if frag.category = mps.betaCfl.attributesForm then
                                                 frag.father -> group[];
                                                 frag[] -> createWindow;
                                             if);
                                         if);
                                     if);
                                 if);
                              #);
                         #);
                       open::
                         (# 
                         do 'Create window...' -> name;
                         #);
                    #);
                  buildApplicationItem: @builderItem
                    (# eventHandler::
                         (# onStatus::
                              (# 
                              do false -> value;
                                 (if browser.cfe[] <> none then
                                     (if browser.cfe.frag[]<>none then
                                         (if 'program' -> (browser.cfe.frag.name).equalNCS then
                                             true -> value;
                                         if);
                                     if);
                                 if);
                              #);
                            onSelect::
                              (# 
                              do browser.cfe.frag.father -> lastCompiledFg[];
                                 browser.cfe.frag.father -> compile;
                              #);
                            
                         #);
                       open::
                         (# 
                         do 'Build Application' -> name;
                         #);
                    #);
                  open::
                    (# 
                    do 'Interfacebuilder' -> name;
                      
                       newSourceFileItem.open;
                       saveItem.open;
                       addSeparator;
                       openGraphicalEditor.open;
                       newGraphicalEditor.open;
                       addSeparator;
                       buildApplicationItem.open;
                    #);
               #);
             open::
               (# 
               do SLOTs.new;
                  SLOTs[]->append;
                  'Fragment'->GroupEdit.new;
                  'Tools'->GroupPerform.new;
                  ibMenu.open;
                  ibMenu[] -> append;
               #);
          #);
        
        (* End of Menubar *)
        
        (*
         * Event handling
         *)
        
        eventHandler::
          (# 
          #);
        
        open::
          (# width, height: @integer;
          do size -> (width, height);
             (width + 150, height) -> size;
          #);
        
        (*
         * Handling of windows
         *)
        
        saveAll:
          (# putBool:
               (# b: @boolean;
               enter b
               do (if b then
                      'true' -> putText;
                   else
                      'false' -> putText;
                  if);
               #);
          do theGraphicalEditorEnv.scanDocuments
             (# 
             do current.save;
             #);
          #);
        
        saveGroup:
          (# fg: ^astInterface.fragmentGroup;
          enter fg[]
          do saving:
               (# 
                  theFile: @catchAllFile
                    (# failure::
                         (# 
                         do msg[] -> putLine;
                            leave saving;
                         #);
                    #);
                  fileName: ^text;
               do (fg.name).copy -> fileName[];
                  '.bet' -> fileName.append;
                  fileName[] -> theFile.name;
                  theFile.openWrite;
                  (mps.ast[], fg[], theFile[], none) -> prettyPrintFragment;
                  theFile.close;
                  fg.fragmentList.scan
                  (# frag: ^astInterface.fragmentForm;
                  do (if current.type = MPS.AST.formType then
                         current.open -> frag[];
                         frag.recomputeSlotChain;
                     if);
                  #);
                  'doneCheck' -> fg.prop.deleteProp;
                  fg.markAsChanged;
               #);
          #);
        
        findPrivateSlotName:
          (# slotName: ^text;
             node: ^astInterface.beta.attributeDecl;
             failure:<
               (# msg: ^text;
               enter msg[]
               do inner;
               #);
             betaGram: ^astInterface.beta;
          enter node[]
          do (# theAst: ^astInterface.AST;
                theSimpleDecl: ^astInterface.beta.simpleDecl;
                theReferenceSpecification: ^astInterface.beta.ReferenceSpecification;
                theStaticItem: ^astInterface.beta.staticItem;
                theUnExpanded: ^astInterface.unExpanded;
                theSlotDesc: ^astInterface.slotDesc;
             do MPS.betacfl[] -> betaGram[];
                (node[], 'private') -> betaGram.findAttribute -> theAst[];
                (if theAst[] <> none then
                    (if theAst.symbol = betaGram.simpleDecl then
                        theAst[] -> theSimpleDecl[];
                        theSimpleDecl.getReferenceSpecification -> theReferenceSpecification[];
                        (if theReferenceSpecification.symbol = betagram.staticItem then
                            theReferenceSpecification[] -> theStaticItem[];
                            theStaticItem.getObjectSpecification -> theAst[];
                            (if theAst.kind = MPS.AST.kinds.unExpanded then
                                theAst[] -> theUnExpanded[];
                                (if theUnExpanded.isSlot then
                                    theUnExpanded.theSlot -> theSlotDesc[];
                                    theSlotDesc.name -> slotName[];
                                 else
                                    'The private decl lacks a SLOT' -> failure;
                                if);
                             else
                                'The private decl lacks a SLOT' -> failure;
                            if);
                         else
                            'The private decl is not a static item' -> failure;
                        if);
                     else
                        'The private decl is not a static item' -> failure;
                    if);
                 else
                    'There is no private decl' -> failure;
                if);
             #);
          exit slotName[]
          #);
        editWindow:
          (# theAttributeDecl: ^astInterface.ast;
             theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
             theDocument: ^graphicalEditorEnv.document;
             fg: ^astInterface.fragmentGroup;
          enter theAttributeDecl[]
          do l: (# 
                do
                   theAttributeDecl.frag.father -> fg[];
                   fg[] -> theGraphicalEditorEnv.findDocument -> theDocument[];
                   (if theDocument[] = none then
                       &theGraphicalEditorEnv.document[] -> theDocument[];
                       fg.name -> theDocument.open
                       (# 
                          error::
                            (# 
                            do msg[] -> displayMessage;
                               leave l;
                            #);
                       #);
                   if);
                   
                   (if theDocument.readOnly then
                       'Warning! The window is read only' -> displayMessage;
                   if);
                   theAttributeDecl[] -> theGraphicalEditorEnv.find -> theGraphicalEditor[];
                   l: (if theGraphicalEditor[] = none then
                          (# slotName: ^text;
                             body: ^astInterface.fragmentGroup;
                             frag: ^astInterface.fragmentForm;
                             help: ^text;
                          do theAttributeDecl[] -> findPrivateSlotName
                             (# 
                                failure::
                                  (# 
                                  do msg[] -> displayMessage;
                                     leave l;
                                  #);
                             #) -> slotName[];
                             (if slotName[] = none then
                                 'Error! Failed to find the private attributes' -> displayMessage;
                              else
                                 theDocument.defaultBodyGroup -> body[];
                                 (if body[] = none then
                                     'Error! There is no body group' -> displayMessage;
                                  else
                                     slotName[] -> body.findFragment -> frag[];
                                     (if frag[] <> none then 
                                         (if frag.category = MPS.betaCfl.descriptorForm then
                                             &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                                             theDocument[] -> theGraphicalEditor.theDocument[];
                                             theGraphicalEditor.open;
                                             (theAttributeDecl[], frag[]) -> theGraphicalEditor.init;
                                             theDocument.readOnly -> theGraphicalEditor.readOnly;
                                          else
                                             'Error! The private fragment has wrong category' -> displayMessage;
                                         if);
                                      else
                                         'Error! The fragment ' -> help[];
                                         slotName[] -> help.append;
                                         ' was not found in ' -> help.append;
                                         body.name -> help.append;
                                         help[] -> displayMessage;
                                     if);
                                 if);
                             if);
                          #);
                       else
                          theGraphicalEditor.bringToFront;
                      if);
                #);
          #);
        (* createGUI:
         *           (# theAttribute: ^astInterface.beta.attributeDecl;
         *              theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
         *              theDocument: ^graphicalEditorEnv.document;
         *              fg: ^astInterface.fragmentGroup;
         *           enter theAttribute[]
         *           do l: (#
         *                 do theAttribute.frag.father -> fg[];
         *                    fg.name -> theGraphicalEditorEnv.findDocument -> theDocument[];
         *                    (if theDocument[] = none then
         *                        &theGraphicalEditorEnv.document[] -> theDocument[];
         *                        fg.name -> theDocument.open
         *                        (# 
         *                           error::
         *                             (# 
         *                             do msg[] -> displayMessage;
         *                                leave l;
         *                             #);
         *                        #);
         *                    if);
         *                 #);
         *              (if theDocument.readOnly then
         *                  'You do not have write acces to one or all of the files' -> displayMessage;
         *               else
         *                  &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
         *                  theDocument[] -> theGraphicalEditor.theDocument[];
         *                  theGraphicalEditor.open;
         *                  theAttribute[] -> theGraphicalEditor.new;
         *              if);
         *           #);
         *)
        createWindow:
          (# frag: ^astInterface.fragmentForm;  
          enter frag[]
          do l:
               (# theAttributesForm: ^astInterface.beta.attributesForm;
                  theAttributes: ^astInterface.beta.attributes;
                  theAttribute: ^astInterface.beta.attributeDecl;
                  name: ^text;
                  theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
                  theDocument: ^graphicalEditorEnv.document;
                  fg: ^astInterface.fragmentGroup;
               do frag.father -> fg[];
                  fg[] -> theGraphicalEditorEnv.findDocument -> theDocument[];
                  (if theDocument[] = none then
                      &theGraphicalEditorEnv.document[] -> theDocument[];
                      fg.name -> theDocument.open
                      (# 
                         error::
                           (# 
                           do msg[] -> displayMessage;
                              leave l;
                           #);
                      #);
                  if);
                  (if theDocument.readOnly then
                      'You do not have write acces to one or all of the files' -> displayMessage;
                   else
                      frag.root[] -> theAttributesForm[];
                      theAttributesForm.getAttributes -> theAttributes[];
                      'Name:' -> getIdentifier -> name[];
                      (if name[] <> none then
                          (theAttributes[], name[]) ->mps.betacfl.find -> theAttribute[];
                          (if theAttribute[] <> none then
                              (this(ymerBrowserWindow)[],
                              'The window could not be created because it already exists', 
                              'Alert') -> noteUser;
                           else
                              (frag[], name[], 'window') -> mps.betacfl.newPatternAttribute -> theAttribute[];
                              theAttribute[] -> theAttributes.append;
                              &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                              theDocument[] -> theGraphicalEditor.theDocument[];
                              theGraphicalEditor.open;
                              theAttribute[] -> theGraphicalEditor.new;
                              (true, frag[], theAttributes[], theAttributes.noOfSons - 1) 
                                -> theFragServer.notifyListElementInserted
                          if);
                      if);
                      
                  if);
               #);
          #);
        createSourceFile:
          (# theDocument: ^graphicalEditorEnv.document;
             fileName: ^text;
             file1, file2: ^text;
             e: @diskEntry;
          do 'File name: ' -> createFile -> fileName[];
             l:
               (if fileName[] <> none then
                   fileName[] -> stripExtension;
                   fileName[] -> e.path;
                   (if e.exists and e.isDirectory then
                       (this(ymerBrowserWindow)[],
                       'There is a directory with that name', 
                       'Alert') -> noteUser;
                    else
                       &theGraphicalEditorEnv.document[] -> theDocument[];
                       fileName[] -> theDocument.new
                       (# 
                          error::
                            (# 
                            do msg[] -> displayMessage;
                               leave l;
                            #);
                       #);
                       theDocument.interfaceGroup.markAsChanged;
                       theDocument.bodyGroups.scan
                       (# 
                       do current.markAsChanged;
                          current.fullName -> ymerBrowser.addProject;
                       #);
                       theDocument.theParameterStore.save;
                       theDocument.interfaceGroup.fullName -> ymerBrowser.addProject; 
                       
                   if);
               if);
          #);
     #);
   init::
     (# 
     do ymerBrowser.initCompiler;
        ('Sif: Known Bugs and Inconveniences',
        '~beta/editor/v5.0.1/doc/TODO')-> helpDocument;
        (* ('mjolner', 'Interfacebuilder') -> getPreferencesSection  -> prefs[]; *)
        theFragServer.init;
        (theFragServer[], MPS.AST[], MPS.betaCfl[], pretty[]) -> theGraphicalEditorEnv.init;
        MPS.betaCfl[] -> pretty.init;
        pretty[] -> objectPool.put;
     #);
#)
