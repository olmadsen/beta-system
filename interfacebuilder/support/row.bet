ORIGIN '~beta/guienv/v1.6/guienv';
-- windowLib: Attributes --
fill: (#  exit 1 #);
alignLeft: (#  exit 2 #);
alignTop: (#  exit 2 #);
alignRight: (#  exit 3 #);
alignBottom: (#  exit 3 #);
alignCenter: (#  exit 4 #);
wrap: (#  exit 5 #);
row: canvas
  (#
     margin: @
       (#
          left:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theLeft then value->theLeft; computeLayout if)
              #)
            exit theLeft
            #);
          top:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theTop then value->theTop; computeLayout if)
              #)
            exit theTop
            #);
          right:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theRight then value->theRight; computeLayout if)
              #)
            exit theRight
            #);
          bottom:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theBottom then
                     value->theBottom; computeLayout
                 if)
              #)
            exit theBottom
            #);
          theLeft,theTop,theRight,theBottom: @integer
       #);
     alignment: @
       (#
          vertical:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theVertical then
                     value->theVertical; computeLayout
                 if)
              #)
            exit theVertical
            #);
          horizontal:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theHorizontal then
                     value->theHorizontal; computeLayout
                 if)
              #)
            exit theHorizontal
            #);
          theVertical,theHorizontal: @integer
       #);
     distance:
       (# value: @integer
       enter
         (# 
         enter value
         do
            (if value <> theDistance then
                value->theDistance; computeLayout
            if)
         #)
       exit theDistance
       #);
     eventHandler::< 
       (#
          onFrameChanged::<  (#  do computeLayout; INNER ;  #);
          onChildFrameChanged::< 
            (# 
            do (if not computingLayout then computeLayout if); INNER ; 
            #);
          
       #);
     open::< 
       (# 
       do
          5->theDistance;
          5->margin.theLeft;
          5->margin.theTop;
          5->margin.theRight;
          5->margin.theBottom;
          alignLeft->alignment.theHorizontal;
          alignCenter->alignment.theVertical;
          INNER ;
          
       #);
     computeLayout:
       (#
          width,height: @integer;
          x,needed,count: @integer;
          last: ^windowItem;
          
       do
          (if not computingLayout then
              true->computingLayout;
              size->(width,height);
              scan
                (# w,h: @integer
                do
                   current[]->last[];
                   current.size->(w,h);
                   needed+w->needed;
                   count+1->count;
                   
                #);
              (if count <> 0 then
                  needed+margin.left+margin.right+(count-1)*distance->needed;
                  (if alignment.horizontal
                   // wrap // alignLeft // fill then
                      margin.left->x
                   // alignRight then
                      width-needed+margin.left->x
                   // alignCenter then
                      (width-needed) div 2+margin.left->x
                  if);
                  scan
                    (# w,h: @integer
                    do
                       current.size->(w,h);
                       (if alignment.vertical
                        // alignTop // fill // wrap then
                           (x,margin.top)->current.position
                        // alignCenter then
                           (x,margin.top+(height-margin.top-margin.bottom-h) div
                            2)->current.position
                        // alignBottom then
                           (x,(height-margin.bottom-h))->current.position
                       if);
                       (if alignment.vertical
                        // fill then
                           (w,height-margin.top-margin.bottom)->current.size
                       if);
                       x+w+distance->x;
                       
                    #);
                  (if alignment.horizontal = wrap then
                      (needed,height)->size
                  if)
              if);
              false->computingLayout
          if);
          
       #);
     computingLayout: @boolean;
     theDistance: @integer
  #)  

