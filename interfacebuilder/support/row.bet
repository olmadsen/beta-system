ORIGIN '~beta/guienv/v1.6/guienv';
-- windowLib: Attributes --
row: canvas
  (#
     margin: @
       (#
          left:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theLeft then value->theLeft; computeLayout if)
              #)
            exit theLeft
            #);
          top:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theTop then value->theTop; computeLayout if)
              #)
            exit theTop
            #);
          right:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theRight then value->theRight; computeLayout if)
              #)
            exit theRight
            #);
          bottom:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theBottom then
                     value->theBottom; computeLayout
                 if)
              #)
            exit theBottom
            #);
          theLeft,theTop,theRight,theBottom: @integer
       #);
     distance:
       (# value: @integer
       enter
         (# 
         enter value
         do
            (if value <> theDistance then
                value->theDistance; computeLayout
            if)
         #)
       exit theDistance
       #);
     filler: @windowItem (#  #);
     eventHandler::< 
       (#
          onFrameChanged::<  (#  do computeLayout; INNER ;  #);
          onChildFrameChanged::< 
            (# 
            do (if not computingLayout then computeLayout if); INNER ; 
            #);
          
       #);
     open::< 
       (# 
       do
          5->theDistance;
          5->margin.theLeft;
          5->margin.theTop;
          5->margin.theRight;
          5->margin.theBottom;
          filler.open;
          INNER ;
          
       #);
     computeLayout:
       (# width,height: @integer; x: @integer; 
       do
          true->computingLayout;
          size->(width,height);
          margin.left->x;
          scan
            (# w,h: @integer
            do
               (if current[] <> filler[] then
                   (x,margin.top)->current.position;
                   current.size->(w,h);
                   (w,height-margin.top-margin.bottom)->current.size;
                   x+w+distance->x;
                   
               if);
               
            #);
          (x,margin.top)->filler.position;
          (width-x-margin.right,height-margin.top-margin.bottom)->filler.size;
          false->computingLayout;
          
       #);
     computingLayout: @boolean;
     theDistance: @integer
  #)  

