ORIGIN '~beta/guienv/v1.6/guienv';
-- windowLib: Attributes --
column: canvas
  (#
     margin: @
       (#
          left:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theLeft then value->theLeft; computeLayout if)
              #)
            exit theLeft
            #);
          top:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theTop then value->theTop; computeLayout if)
              #)
            exit theTop
            #);
          right:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theRight then value->theRight; computeLayout if)
              #)
            exit theRight
            #);
          bottom:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theBottom then
                     value->theBottom; computeLayout
                 if)
              #)
            exit theBottom
            #);
          theLeft,theTop,theRight,theBottom: @integer
       #);
     distance:
       (# value: @integer
       enter
         (# 
         enter value
         do
            (if value <> theDistance then
                value->theDistance; computeLayout
            if)
         #)
       exit theDistance
       #);
     filler: @windowItem (#  #);
     eventHandler::< 
       (#
          onFrameChanged::<  (#  do computeLayout; INNER ;  #);
          onChildFrameChanged::< 
            (# 
            do (if not computingLayout then computeLayout if); INNER ; 
            #);
          
       #);
     open::< 
       (# 
       do
          10->theDistance;
          5->margin.theLeft;
          5->margin.theTop;
          5->margin.theRight;
          5->margin.theBottom;
          filler.open;
          INNER ;
          
       #);
     computeLayout:
       (# width,height: @integer; y: @integer; 
       do
          true->computingLayout;
          size->(width,height);
          margin.top->y;
          scan
            (# w,h: @integer
            do
               (if current[] <> filler[] then
                   (margin.left,y)->current.position;
                   current.size->(w,h);
                   (width-margin.left-margin.right,h)->current.size;
                   y+h+distance->y;
                   
               if);
               
            #);
          (margin.left,y)->filler.position;
          (width-margin.left-margin.right,height-y-margin.bottom)->filler.size;
          false->computingLayout;
          
       #);
     computingLayout: @boolean;
     theDistance: @integer
  #)  

