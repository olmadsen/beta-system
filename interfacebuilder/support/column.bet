ORIGIN '~beta/guienv/v1.6/guienv';
-- windowLib: Attributes --
fill: (#  exit 1 #);
alignLeft: (#  exit 2 #);
alignTop: (#  exit 3 #);
alignRight: (#  exit 4 #);
alignBottom: (#  exit 5 #);
alignCenter: (#  exit 6 #);
column: canvas
  (#
     margin: @
       (#
          left:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theLeft then value->theLeft; computeLayout if)
              #)
            exit theLeft
            #);
          top:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theTop then value->theTop; computeLayout if)
              #)
            exit theTop
            #);
          right:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theRight then value->theRight; computeLayout if)
              #)
            exit theRight
            #);
          bottom:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theBottom then
                     value->theBottom; computeLayout
                 if)
              #)
            exit theBottom
            #);
          theLeft,theTop,theRight,theBottom: @integer
       #);
     alignment: @
       (#
          vertical:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theVertical then
                     value->theVertical; computeLayout
                 if)
              #)
            exit theVertical
            #);
          horizontal:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theHorizontal then
                     value->theHorizontal; computeLayout
                 if)
              #)
            exit theHorizontal
            #);
          theVertical,theHorizontal: @integer
       #);
     wrap:
       (# value: @boolean
       enter
         (# 
         enter value
         do (if value <> theWrap then value->theWrap; computeLayout if)
         #)
       exit theWrap
       #);
     distance:
       (# value: @integer
       enter
         (# 
         enter value
         do
            (if value <> theDistance then
                value->theDistance; computeLayout
            if)
         #)
       exit theDistance
       #);
     eventHandler::< 
       (#
          onFrameChanged::<  (#  do computeLayout; INNER ;  #);
          onChildFrameChanged::<  (#  do computeLayout; INNER ;  #);
          
       #);
     open::< 
       (# 
       do
          5->theDistance;
          5->margin.theLeft;
          5->margin.theTop;
          5->margin.theRight;
          5->margin.theBottom;
          fill->alignment.theHorizontal;
          alignTop->alignment.theVertical;
          INNER ;
          
       #);
     computeLayout:
       (#
          width,height: @integer;
          y,needed,count: @integer;
          maxWidth: @integer;
          last: ^windowItem;
          
       do
          (if not computingLayout then
              true->computingLayout;
              size->(width,height);
              scan
                (# w,h: @integer
                do
                   current[]->last[];
                   current.size->(w,h);
                   (w, maxWidth) -> Max -> maxWidth;
                   needed+h->needed;
                   count+1->count;
                   
                #);
              (if count <> 0 then
                  needed+margin.top+margin.bottom+(count-1)*distance->needed;
                  (if wrap then
                      margin.top->y
                   else
                      (if alignment.vertical
                       // alignTop // fill then
                          margin.top->y
                       // alignBottom then
                          height-needed+margin.top->y
                       // alignCenter then
                          (height-needed) div 2+margin.top->y
                      if)
                  if);
                  scan
                    (# w,h: @integer
                    do
                       current.size->(w,h);
                       (if alignment.horizontal
                        // alignLeft // fill then
                           (margin.left,y)->current.position
                        // alignCenter then
                           (margin.left+(width-margin.left-margin.right-w) div
                            2,y)->current.position
                        // alignRight then
                           ((width-margin.right-w),y)->current.position
                       if);
                       (if alignment.horizontal
                        // fill then
                           (width-margin.left-margin.right,h)->current.size
                       if);
                       y+h+distance->y;
                       
                    #);
                  (if wrap then (maxWidth + margin.left + margin.right, needed)->size if)
              if);
              false->computingLayout
          if);
          
       #);
     computingLayout: @boolean;
     theWrap: @boolean;
     theDistance: @integer
  #)  

