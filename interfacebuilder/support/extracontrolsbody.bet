ORIGIN 'extracontrols';

-- expanderButtonToggle: DoPart --
do
   not expanded->expanded;
   (if expanded then onExpand;  else onCollapse;  if);
   update;
     

-- expanderButtonOpen: DoPart --
do (12,12)->size; INNER ;   

-- expanderButtonOnRefresh: DoPart --
do
   graphics
     (# 
     do
        (if expanded then
            (0,0,expanderEnabledDown)->drawExpander; 
         else
            (0,0,expanderEnabledRight)->drawExpander; 
        if);
        
     #);
     

-- expanderButtonOnMouseDown: DoPart --
do (# box: @rectangle; 
   do ((0,0),size)->box;
	   buttonBehavior
		 (#
			drawPressed::
			  (# 
			  do
				 graphics
				   (# 
				   do
					  (if expanded then
						  (0,0,expanderPressedDown)->drawExpander; 
					   else
						  (0,0,expanderPressedRight)->drawExpander; 
					  if);
					  
				   #);
				 
			  #);
			drawNormal::
			  (# 
			  do
				 graphics
				   (# 
				   do
					  (if expanded then
						  (0,0,expanderEnabledDown)->drawExpander; 
					   else
						  (0,0,expanderEnabledRight)->drawExpander; 
					  if);
					  
				   #);
				 
			  #);
			action::
			  (# 
			  do
				 graphics
				   (# 
				   do
					  box->eraseRectangle;
					  (0,0,expanderIntermediateRight)->drawExpander;
					  3->guienvWait;
					  box->eraseRectangle;
					  (if expanded then
						  (0,0,expanderPressedRight)->drawExpander; 
					   else
						  (0,0,expanderPressedDown)->drawExpander; 
					  if);
					  3->guienvWait;
					  toggle;
					  
				   #);
				 
			  #);
			
		 #);
     #);

-- imageItemFitToContents: DoPart --
do
   (if theIcon[] <> none then (theIcon.width,theIcon.height)->size;  if);
   true->doneInInner;
     

-- imageItemOnRefresh: DoPart --
do
   graphics
     (# 
     do
        (if theIcon[] <> none then
            (theIcon[],((0,0),size),alignmentCenter)->drawCenteredIcon; 
        if);
        
     #);
   INNER ;
     

-- textItemOnRefresh: DoPart --
do
   size->box.size;
   (if border.visible then (2,2)->box.inset;  if);
   graphics
     (# 
     do
        (if theIcon[] <> none then
            (if theLabel[] <> none then
                (theIcon[],theLabel[],theStyle[],box,alignment,4)->drawIconAndText; 
             else
                (theIcon[],box,alignment)->drawCenteredIcon; 
            if);
            
         else
            (if theLabel[] <> none then
                (theStyle[],theLabel[],box,alignment)->drawCenteredText; 
            if);
            
        if);
        
     #);
   INNER ;
     


-- bevelButtonLib: attributes --

bevelGraphics: graphics
	(# box: @rectangle;
	  drawContent:
		(# 
		do
		   (if theIcon[] <> none then
			   (if theLabel[] <> none then
				   (theIcon[],theLabel[],
					THIS(bevelButton).
					  theStyle[],box,
					alignment,2)->drawIconAndText;
				   
				else
				   (theIcon[],box,alignment)->drawCenteredIcon; 
			   if);
			   
			else
			   (THIS(bevelButton).theStyle[],theLabel[],box,alignment)
				 ->drawCenteredText;
		   if);
		#);
	  drawNormal:
		(# 
		do
		   (if kind
			// small then
			   (box,bevelSmallNormal)->drawBevel; (4,2)->box.inset; 
			// medium then
			   (box,bevelMediumNormal)->drawBevel; (5,3)->box.inset; 
			// large then
			   (box,bevelLargeNormal)->drawBevel; (6,4)->box.inset; 
		   if);
		   black->pen.foreGroundColor;
		   drawContent;
		   
		#);
	  drawPressed:
		(# 
		do
		   (if kind
			// small then
			   (box,bevelSmallPressed)->drawBevel; (4,2)->box.inset; 
			// medium then
			   (box,bevelMediumPressed)->drawBevel; (5,3)->box.inset; 
			// large then
			   (box,bevelLargePressed)->drawBevel; (6,4)->box.inset; 
		   if);
		   white->pen.foreGroundColor;
		   drawContent;
		   
		#);
	  drawOn:
		(# 
		do
		   (if kind
			// small then
			   (box,bevelSmallPressed)->drawBevel; (4,2)->box.inset; 
			// medium then
			   (box,bevelMediumPressed)->drawBevel; (5,3)->box.inset; 
			// large then
			   (box,bevelLargePressed)->drawBevel; (6,4)->box.inset; 
		   if);
		   black->pen.foreGroundColor;
		   drawContent;
		   
		#);
	  
	do size->box.size; INNER ; 
	#);
	   
-- bevelButtonOpen: DoPart --
do
   &textStyle[]->theStyle[];
   'Charcoal'->theStyle.name;
   12->theStyle.size;
   alignmentCenter->theAlignment;
   medium->theKind;
   pushbuttonBehavior -> theBehavior;
   INNER ;
     

-- bevelButtonOnRefresh: DoPart --
do
   (if state then
       bevelGraphics (#  do drawOn;  #); 
    else
       bevelGraphics (#  do drawNormal;  #); 
   if);
   INNER ;
     

-- bevelButtonOnMouseDown: DoPart --
do
   size->box.size;
   buttonBehavior
     (#
        drawPressed::
          (#  do bevelGraphics (#  do drawPressed;  #);  #);
        drawNormal::
          (# 
          do
             (if state then
                 bevelGraphics (#  do drawOn;  #); 
              else
                 bevelGraphics (#  do drawNormal;  #); 
             if);
             
          #);
        action:: 
		  (#
		  do (if behavior
		       //pushbuttonBehavior then
			     update;
				 onAction;
			   //radioButtonBehavior then
			   	 (if not state then
				 	true -> state;
				 if);
			   //checkBoxBehavior then
			   	 not state -> state;
		  	 if);
		  
		  #);
        
     #);
     

