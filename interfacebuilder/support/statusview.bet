ORIGIN 'framer';
INCLUDE '~beta/guienv/graphics';

-- windowLib: Attributes --
statusView: framer
  (#
     open::<  (#  do 1->borderwidth; false->inset; INNER #);
     eventHandler::< 
       (# onRefresh::<  
            (# bluish: (# exit (0x4444, 0x4444, 0xDDDD) #);
            do graphics 
               (# box: @rectangle;
                  width: @integer;
               do size -> box.size;
                  (borderWidth, borderWidth) -> box.inset;
                  box.right - box.left -> width;
                  (if maxValue > 0 then
                      (value * width) div maxValue -> width;
                  if);
                  box.left + width -> box.right;
                  bluish -> pen.foreGroundColor;
                  box -> fillRect;
               #) 
            #) 
       #);
     maxValue:
       (# 
       enter (#  enter theMaxValue do update #)
       exit theMaxValue
       #);
     theMaxValue: @integer;
     value:
       (#  enter (#  enter theValue do update #) exit theValue #);
     theValue: @integer
  #);
  

