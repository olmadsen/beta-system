ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';

BODY 'panoramabody';


-- windowLib: attributes --


panorama: canvas
  (# <<SLOT panoramaLib: attributes>>;
     theScroll:
       (# vertical, horizontal: @integer;
       enter (# enter (horizontal, vertical) <<SLOT panoramaSetTheScroll: doPart>> #)	
       exit 	(# <<SLOT panoramaGetTheScroll: doPart>> exit (horizontal, vertical) #)
       #);
     maxScroll:
       (# vertical, horizontal: @integer;
       <<SLOT panoramaMaxScroll: doPart>>
       exit (horizontal, vertical)
       #);
     scrollbars: @list
       (# element:: panoramaScrollbar;
       #);
     eventHandler::<
       (# onFrameChanged::<
            (# <<SLOT panoramOnFrameChanged: doPart>> #);
          onChildFrameChanged::<
            (# <<SLOT panoramOnChildFrameChanged: doPart>> #);
       #);
     open::<
       (# <<SLOT panoramOpen: doPart>> #);
     private: @<<SLOT panoramePrivate: descriptor>>;
  #);



panoramaScrollbar: scrollbar
  (# <<SLOT panoramaScrollbarLib: attributes>>;
     panoramas: @list
       (# element:: panorama;
       #);
     attach:
       (# thePanorama: ^panorama;
       enter thePanorama[]
       <<SLOT panoramaScrollbarAttach: doPart>>
       #);
     eventHandler::<
       (# onValueChanged::<
            (# <<SLOT panoramaScrollbarOnValueChanged: doPart>>
            #);
       #);
     flag: (* private *) @boolean;
  #);


verticalScroller: canvas
  (# contentsType:< panorama
       (# open::<
            (# 
            do true->bindBottom;
               true->bindRight;
               (2,2)->position;
               (133,196)->size;
               INNER;
            #);
       #);
     contents: @contentsType;
     
     verticalScrollbar: @panoramaScrollbar
       (# vertical::  (#  do true->value #);
          open::
            (# 
            do true->bindRight;
               false->bindLeft;
               true->bindBottom;
               (133,1)->position;
               (16,198)->size;
            #)
       #);
     open::< 
       (# 
       do borderStyles.shadowIn->border.style;
          true->border.visible;
          (150,200)->size;
          contents.open;
          verticalScrollbar.open;
          contents[]->verticalScrollbar.attach;
          INNER;
       #);
  #);
