ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';

-- windowLib: attributes --

panorama: canvas
	(# firstChild:
			(# child: ^windowItem;
			do l: scan (# do current[] -> child[]; leave l #);
			exit child[]
			#);
		theScroll:
			(# vertical, horizontal: @integer;
			enter	(# child: ^windowItem;
				   enter (horizontal, vertical)
					do true -> duringScroll;
						firstChild -> child[];
						(if child[] <> NONE then
							(-horizontal,-vertical) -> child.position;
							this(window).update;
						if);
						false -> duringScroll;
					#)
			exit 	(# child: ^windowItem;
					do firstChild -> child[];
						(if child[] <> NONE then
							child.position -> (horizontal, vertical);
							-vertical -> vertical;
							-horizontal -> horizontal;
						if);
					exit (horizontal, vertical)
					#)
			#);
		maxScroll:
			(# vertical, horizontal: @integer;
				child: ^windowItem;
				width, height: @integer;
				childWidth, childHeight: @integer;
			do l: scan (# do current[] -> child[]; leave l #);
				(if child[] <> NONE then
					size -> (width, height);
					child.size -> (childWidth, childHeight);
					(childWidth - width, 0) -> max -> horizontal;
					(childHeight - height, 0) -> max -> vertical;
				if);
			exit (horizontal, vertical)
			#);
		eventHandler::<
			(# onFrameChanged::<
					(#
					do adjustScrollvalues;
						INNER;
					#);
				onChildFrameChanged::<
					(#
					do (if not duringScroll then
							adjustScrollvalues;
						if);
						INNER;
					#);
			#);
		open::<
			(#
			do scrollbars.init;
				INNER;
			#);
		adjustScrollvalues:
			(#
			do scrollbars.scan
					(#
					do current.adjustScrollvalues;
					#);
			#);
		scrollbars: @list
			(# element:: panoramaScrollbar;
			#);
		duringScroll: @boolean;
	#);
  
panoramaScrollbar: scrollbar
	(# thePanorama: ^panorama;
		
		setPanorama:
			(# 
			enter thePanorama[]
			do this(scrollbar)[] -> thePanorama.scrollbars.append;
				adjustScrollvalues;
			#);
		adjustScrollvalues:
			(# horizontal, vertical: @integer;
				pageWidth, pageHeight: @integer;
				oldValue: @integer;
			do (if NOT duringAdjustScrollValues then
					true -> duringAdjustScrollValues;
					thePanorama.maxScroll -> (horizontal, vertical);
					thePanorama.size -> (pageWidth, pageHeight);
					value -> oldValue;
					(if this(scrollbar).vertical then
						vertical -> maxValue;
						pageHeight -> pageScrollAmount;
					else
						horizontal -> maxValue;
						pageWidth -> pageScrollAmount;
					if);
					(if oldValue > maxValue then
						adjustPanorama;
					if);
					false -> duringAdjustScrollValues;
				if);
			#);
		adjustPanorama:
			(# vertical, horizontal: @integer;
			do (if thePanorama[] <> NONE then
					thePanorama.theScroll -> (horizontal, vertical);
					(if this(scrollbar).vertical then
						value -> vertical;
					else
						value -> horizontal;
					if);
					(horizontal, vertical) -> thePanorama.theScroll;
				if);
			#);
		eventHandler::
			(# onValueChanged::
					(# 
					do adjustPanorama;
					#);
			#);
		duringAdjustScrollValues: @boolean;
	#);
  