ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/controls';

-- windowLib: attributes --

panorama: canvas
	(# theScroll:
			(# vertical, horizontal: @integer;
			enter	(# child: ^windowItem;
				   enter (horizontal, vertical)
					do l: scan (# do current[] -> child[]; leave l #);
						(if child[] <> NONE then
							(-horizontal,-vertical) -> child.position;
						if);
					#)
			exit 	(# child: ^windowItem;
					do l: scan (# do current[] -> child[]; leave l #);
						(if child[] <> NONE then
							child.position -> (horizontal, vertical);
							-vertical -> vertical;
							-horizontal -> horizontal;
						if);
					exit (horizontal, vertical)
					#)
			#);
		maxScroll:
			(# vertical, horizontal: @integer;
				child: ^windowItem;
				width, height: @integer;
				childWidth, childHeight: @integer;
			do l: scan (# do current[] -> child[]; leave l #);
				(if child[] <> NONE then
					size -> (width, height);
					child.size -> (childWidth, childHeight);
					(childWidth - width, 0) -> max -> horizontal;
					(childHeight - height, 0) -> max -> vertical;
				if);
			exit (horizontal, vertical)
			#);
	#);
  
panoramaScrollbar: scrollbar
	(# thePanorama: ^panorama;
		
		setPanorama:
			(# horizontal, vertical: @integer
			enter thePanorama[]
			do thePanorama.maxScroll -> (horizontal, vertical);
				(if this(scrollbar).vertical then
					vertical -> maxValue;
				else
					horizontal -> maxValue;
				if);
			#);
		eventHandler::
			(# onValueChanged::
					(# vertical, horizontal: @integer;
					do (if thePanorama[] <> NONE then
							thePanorama.theScroll -> (horizontal, vertical);
							(if this(scrollbar).vertical then
								value -> vertical;
							else
								value -> horizontal;
							if);
							(horizontal, vertical) -> thePanorama.theScroll;
						if);
					#);
			#);
	#);
  