ORIGIN '~beta/guienv/v1.6/guienv'
-- windowLib: Attributes --
spread: (#  exit 1 #);
alignLeft: (#  exit 2 #);
alignTop: (#  exit 3 #);
alignRight: (#  exit 4 #);
alignBottom: (#  exit 5 #);
alignCenter: (#  exit 6 #);
stretch: (#  exit 6 #);
manager: canvas
  (#
     margin: @
       (#
          left:
            (# value: @integer
            enter 
              (# 
              enter value
              do (if value <> theLeft then value->theLeft; changed if)
              #)
            exit theLeft
            #);
          top:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theTop then value->theTop; changed if)
              #)
            exit theTop
            #);
          right:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theRight then value->theRight; changed if)
              #)
            exit theRight
            #);
          bottom:
            (# value: @integer
            enter
              (# 
              enter value
              do (if value <> theBottom then value->theBottom; changed if)
              #)
            exit theBottom
            #);
          theLeft,theTop,theRight,theBottom: @integer
       #);
     horizontal: @
       (#
          alignment:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theAlignment then
                     value->theAlignment; changed
                 if)
              #)
            exit theAlignment
            #);
          wrap:
            (# value: @boolean
            enter
              (# 
              enter value
              do (if value <> theWrap then value->theWrap; changed if)
              #)
            exit theWrap
            #);
          theWrap: @boolean;
          theAlignment: @integer
       #);
     vertical: @
       (#
          alignment:
            (# value: @integer
            enter
              (# 
              enter value
              do
                 (if value <> theAlignment then
                     value->theAlignment; changed
                 if)
              #)
            exit theAlignment
            #);
          wrap:
            (# value: @boolean
            enter
              (# 
              enter value
              do (if value <> theWrap then value->theWrap; changed if)
              #)
            exit theWrap
            #);
          theWrap: @boolean;
          theAlignment: @integer
       #);
     distance:
       (# value: @integer
       enter
         (# 
         enter value
         do (if value <> theDistance then value->theDistance; changed if)
         #)
       exit theDistance
       #);
     eventHandler::< 
       (#
          onFrameChanged::<  (#  do changed; INNER ;  #);
          onChildFrameChanged::< 
            (# 
            do (if not computingLayout then changed if); INNER ; 
            #);
          
       #);
     open::< 
       (# 
       do
          5->theDistance;
          5->margin.theLeft;
          5->margin.theTop;
          5->margin.theRight;
          5->margin.theBottom;
          alignLeft->horizontal.theAlignment;
          alignCenter->vertical.theAlignment;
          INNER ;
          
       #);
     computeLayout:<
       (# 
       do
          (if not computingLayout then
              true->computingLayout; INNER ; false->computingLayout
          if);
          
       #);
     changed:
       (# theComputer: ^computer
       do getComputer->theComputer[]; theComputer.activate
       #);
     computingLayout: @boolean;
     theDistance: @integer
  #);
getComputer: objectPool.get (# type:: computer #);
computer:
  (#
     activate:
       (#  do (if not active then true->active; 1->t.start if) #);
     compute:
       (#
          computeLayout:
            (# theCanvas: ^canvas; 
            enter theCanvas[]
            do
               theCanvas.scan
                 (# 
                 do
                    (if current## <= manager## then
                        current[]->&computeLayout;
                        current[]
                          ->qua (# as:: manager do thisObj.computeLayout #)
                     else
                        (if current## <= canvas## then
                            current[]->&computeLayout
                        if)
                    if)
                 #)
            #)
       do (if active then t.stop; (contents).hide;
              contents->computeLayout;
              (contents).show;
              false->active;
          if)
       #);
     t: @timer (# action::  (#  do compute #) #);
     active: @boolean
  #)  



