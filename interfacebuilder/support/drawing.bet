ORIGIN '~beta/guienv/v1.6/graphics';

-- graphicsLib: attributes --


expanderEnabledRight: (# exit 1 #);
expanderPressedRight: (# exit 2 #);
expanderIntermediateRight: (# exit 3 #);
expanderPressedDown: (# exit 4 #);
expanderEnabledDown: (# exit 5 #);
expanderDisabledRight: (# exit 6 #);
expanderDisabledDown: (# exit 7 #);

bluish1: (# exit (52428, 52428, 65535) #);
bluish2: (# exit (39321, 39321, 65535) #);
bluish3: (# exit (26214, 26214, 52428) #);
bluish4: (# exit (13107, 13107, 39321) #);

black: (# exit (0, 0, 0) #);
white: (# exit (0xFFFF, 0xFFFF, 0xFFFF) #);

gray1: (# exit (0xEEEE, 0xEEEE, 0xEEEE) #);
gray2: (# exit (0xDDDD, 0xDDDD, 0xDDDD) #);
gray3: (# exit (0xCCCC, 0xCCCC, 0xCCCC) #);
gray4: (# exit (0xBBBB, 0xBBBB, 0xBBBB) #);
gray5: (# exit (0xAAAA, 0xAAAA, 0xAAAA) #);
gray6: (# exit (0x9999, 0x9999, 0x9999) #);
gray7: (# exit (0x8888, 0x8888, 0x8888) #);
gray8: (# exit (0x7777, 0x7777, 0x7777) #);
gray9: (# exit (0x6666, 0x6666, 0x6666) #);
gray10: (# exit (0x5555, 0x5555, 0x5555) #);
gray11: (# exit (0x4444, 0x4444, 0x4444) #);
gray12: (# exit (0x2222, 0x2222, 0x2222) #);

A1: (# exit (13107, 13107, 13107) #);
A2: (# exit (4369, 4369, 4369) #);

eraseRectangle:
	(# r: @rectangle;
	enter r
	do backgroundColor -> pen.foreGroundColor;
		r -> fillRect;
	#);

drawExpander:
	(# left, top: @integer;
		kind: @integer;
		swap: @boolean; (* IF true, reverse horizontal and vertical
		 					  * coordinates
							  *)
		plot:
			(# x, y: @integer;
			enter (x, y)
			do (if swap then
					(top + y, left + x) -> drawSpot;
				else
					(left + x, top + y) -> drawSpot;
				if);
			#);
		drawRightContour:
			(#
			do black -> pen.foreGroundColor;
				(5, 1) -> plot; (5, 2) -> plot; (5, 3) -> plot; (5, 4) -> plot; 
				(5, 5) -> plot; (5, 6) -> plot; (5, 7) -> plot; (5, 8) -> plot; 
				(5, 9) -> plot; (5, 10) -> plot; (5, 11) -> plot; (6, 2) -> plot; 
				(7, 3) -> plot; (8, 4) -> plot; (9, 5) -> plot; (10, 6) -> plot; 
				(9, 7) -> plot; (8, 8) -> plot; (7, 9) -> plot; (6, 10) -> plot; 
				INNER;
			#);
		drawEnabledRight: drawRightContour
			(#
			do bluish1 -> pen.foreGroundColor;
				(6, 3) -> plot; (6, 4) -> plot; (6, 5) -> plot; 
				(6, 6) -> plot; (6, 7) -> plot; (6, 8) -> plot; 
				
				bluish2 -> pen.foreGroundColor;
				(7, 4) -> plot; (7, 5) -> plot; (7, 6) -> plot; (7, 7) -> plot; 
				(8, 5) -> plot; (8, 6) -> plot; (6, 9) -> plot; 
				
				bluish3 -> pen.foreGroundColor;
				(7, 8) -> plot; (8, 7) -> plot; (9, 6) -> plot; 
				
				gray7 -> pen.foreGroundColor;
				(6, 11) -> plot; (7, 10) -> plot; (8, 9) -> plot; 
				(9, 8) -> plot; (10, 7) -> plot; 
				
				gray4 -> pen.foreGroundColor;
				(6, 12) -> plot; (7, 11) -> plot; (8, 10) -> plot; 
				(9, 9) -> plot; (10, 8) -> plot; (11, 7) -> plot; 
			#);
		drawPressedRight: drawRightContour
			(#
			do bluish4 -> pen.foreGroundColor;
				(6, 3) -> plot; (6, 4) -> plot; (6, 5) -> plot; 
				(6, 6) -> plot; (6, 7) -> plot; (6, 8) -> plot; 
				(7, 4) -> plot; (7, 5) -> plot; (7, 6) -> plot; (7, 7) -> plot; 
				(8, 5) -> plot; (8, 6) -> plot; (6, 9) -> plot; 
				(7, 8) -> plot; (8, 7) -> plot; (9, 6) -> plot; 
			#);
		drawIntermediateRight:
			(#
			do black -> pen.foreGroundColor;
				(2, 10) -> plot; (3, 10) -> plot; (4, 10) -> plot; (5, 10) -> plot;
				(6, 10) -> plot; (7, 10) -> plot; (8, 10) -> plot; (9, 10) -> plot; 
				(10, 10) -> plot; (10, 2) -> plot; (10, 3) -> plot; (10, 4) -> plot; 
				(10, 5) -> plot; (10, 6) -> plot; (10, 7) -> plot; (10, 8) -> plot; 
				(10, 9) -> plot; (3, 9) -> plot; (4, 8) -> plot; (5, 7) -> plot; 
				(6, 6) -> plot; (7, 5) -> plot; (8, 4) -> plot; (9, 3) -> plot; 
				
				bluish4 -> pen.foreGroundColor;
				(4, 9) -> plot; (5, 9) -> plot; (6, 9) -> plot;  (7, 9) -> plot; (8, 9) -> plot; (9, 9) -> plot; 
				(5, 8) -> plot;  (6, 8) -> plot; (7, 8) -> plot; (8, 8) -> plot; (9, 8) -> plot;
				(6, 7) -> plot; (7, 7) -> plot; (8, 7) -> plot; (9, 7) -> plot; 
				(7, 6) -> plot; (8, 6) -> plot; (9, 6) -> plot; 
				(8, 5) -> plot; (9, 5) -> plot;  
				(9, 4) -> plot; 
			#);
		drawPressedDown:
			(#
			do true -> swap;
				drawPressedRight;
			#);
		drawEnabledDown:
			(#
			do true -> swap;
				drawEnabledRight;
			#);
		drawDisabledRight:
			(#
			do 
			#);
		drawDisabledDown:
			(#
			do 
			#);
	enter (left, top, kind)
	do (if kind
			//expanderEnabledRight then drawEnabledRight;
			//expanderPressedRight then drawPressedRight;
			//expanderIntermediateRight then drawIntermediateRight;
			//expanderPressedDown then drawPressedDown;
			//expanderEnabledDown then drawEnabledDown;
			//expanderDisabledRight then drawDisabledRight;
			//expanderDisabledDown then drawDisabledDown;
		if);
	#);
	
alignmentLeft: (# exit 1 #);
alignmentRight: (# exit 2 #);
alignmentCenter: (# exit 3 #);
	
drawCenteredText:
	(# theStyle: ^textStyle;
		theText: ^text;
		box: @rectangle;
		alignment: @integer;
		
		textWidth, textHeight: @integer;
		boxWidth, boxHeight: @integer;
		baseline: @integer;
	enter (theStyle[], theText[], box, alignment)
	do (if theText[] <> NONE then
			theStyle[] -> style;
			box.size -> (boxWidth, boxHeight);
			theStyle.ascent + theStyle.descent -> textHeight;
			(boxHeight - textHeight) div 2 + theStyle.ascent -> baseline;
			(if alignment
				//alignmentLeft then
					(box.left, baseLine) -> moveTo;
				//alignmentRight then
					theText[] -> theStyle.widthOfText -> textWidth;
					(box.right - textWidth, baseLine) -> moveTo;
				//alignmentCenter then
					theText[] -> theStyle.widthOfText -> textWidth;
					((boxWidth - textWidth) div 2 + box.left, baseLine) -> moveTo;
			if);
			theText[] -> drawText;
		if);
	#);
drawCenteredIcon:
	(# icon: ^pixmap;
		box: @rectangle;
		alignment: @integer;
		
		iconWidth, iconHeight: @integer;
		boxWidth, boxHeight: @integer;
		iconLeft, iconTop: @integer;
	enter (icon[], box, alignment)
	do (if icon[] <> NONE then
			icon.width -> iconWidth;
			icon.height -> iconHeight;
			box.size -> (boxWidth, boxHeight);
			box.top + (boxHeight - iconHeight) div 2 -> iconTop;
			(if alignment
				//alignmentLeft then
					box.left -> iconLeft;
				//alignmentRight then
					box.right - iconWidth -> iconLeft;
				//alignmentCenter then
					box.left + (boxWidth - iconWidth) div 2 -> iconLeft;
			if);
			(icon[], (0, 0), (iconLeft, iconTop), iconWidth, iconHeight) -> drawRaster;
		if);
	#);
drawIconAndText:
	(# icon: ^pixmap;
		theText: ^text;
		theStyle: ^textStyle;
		box: @rectangle;
		alignment: @integer;
		space: @integer;
		
		iconWidth, iconHeight: @integer;
		boxWidth, boxHeight: @integer;
		iconLeft, iconTop: @integer;
		textLeft: @integer;
	   textWidth, textHeight: @integer;
		baseline: @integer;
		
	enter (icon[], theText[], theStyle[], box, alignment, space)
	do theStyle[] -> style;
		box.size -> (boxWidth, boxHeight);
		theStyle.ascent + theStyle.descent -> textHeight;
		(boxHeight - textHeight) div 2 + theStyle.ascent -> baseline;
		icon.width -> iconWidth;
		icon.height -> iconHeight;
		box.top + (boxHeight - iconHeight) div 2 -> iconTop;
		(if alignment
			//alignmentLeft then
				box.left -> iconLeft;
				iconLeft + iconWidth + space -> textLeft;
			//alignmentRight then
				theText[] -> theStyle.widthOfText -> textWidth;
				box.right - textWidth -> textLeft;
				textLeft - iconWidth - space -> iconLeft;
			//alignmentCenter then
				box.left + (boxWidth - (iconWidth + textWidth + space)) div 2 -> iconLeft;
				iconLeft + space + iconWidth -> textLeft;
		if);
		(icon[], (0, 0), (iconLeft, iconTop), iconWidth, iconHeight) -> drawRaster;
		(textLeft, baseline) -> moveTo;
		theText[] -> drawText;
	#);
	
	
	
bevelMediumNormal: (# exit 1 #);
bevelMediumOn: (# exit 2 #);
bevelMediumPressed: (# exit 3 #);
bevelMediumDisabled: (# exit 4 #);



drawBevel:
	(# kind: @integer;
		box: @rectangle;
		
		northEast: (# exit 1 #);
		northWest: (# exit 2 #);
		southEast: (# exit 3 #);
		southWest: (# exit 4 #);
		
		drawCorner:
			(# corner: @integer;
				colors: [0]@color;
			enter (corner, colors)
			do (if corner
					//northEast then
						(for inx: colors.range repeat
							colors[inx] -> pen.foreGroundColor;
							(box.right - inx, box.top + inx - 1) -> drawSpot;
						for);
					//northWest then
						(for inx: colors.range repeat
							colors[inx] -> pen.foreGroundColor;
							(box.left + inx - 1, box.top + inx - 1) -> drawSpot;
						for);
					//southEast then
						(for inx: colors.range repeat
							colors[inx] -> pen.foreGroundColor;
							(box.right - inx, box.bottom - inx) -> drawSpot;
						for);
					//southWest then
						(for inx: colors.range repeat
							colors[inx] -> pen.foreGroundColor;
							(box.left + inx - 1, box.bottom - inx) -> drawSpot;
						for);
				if);
			#);
			
		drawEdge:
			(# corner: @integer;
				colors: [0]@color;
			enter (corner, colors)
			do (if corner
					//northWest then
						(for inx: colors.range repeat
							colors[inx] -> pen.foreGroundColor;
							(box.left + inx - 1, box.top + inx - 1) -> moveTo;
							(box.right - inx - 1, box.top + inx - 1) -> drawTo;
							(box.left + inx - 1, box.top + inx - 1) -> moveTo;
							(box.left + inx -1, box.bottom - inx - 1) -> drawTo;
						for);
					//southEast then
						(for inx: colors.range repeat
							colors[inx] -> pen.foreGroundColor;
							(box.left + inx, box.bottom - inx) -> moveTo;
							(box.right - inx, box.bottom - inx) -> drawTo;
							(box.right - inx, box.top + inx) -> moveTo;
							(box.right - inx, box.bottom -inx) -> drawTo;
						for);
				if);
			#);
		drawTheThing:
			(# cornerPixels: [0] @color;
				northWestEdgePixels: [0] @color;
				southEeastEdgePixels: [0] @color;
				background: @color;
			do	INNER;
				background -> pen.foreGroundColor;
				box -> fillRect;
				(northEast, cornerPixels) -> drawCorner;
				(southWest, cornerPixels) -> drawCorner;
				(northWest, northWestEdgePixels) -> drawEdge;
				(southEast, southEeastEdgePixels) -> drawEdge;
			#);
		drawMedium: drawTheThing
			(#
			do 3 -> cornerPixels.new;
				3 -> northWestEdgePixels.new;
				3 -> southEeastEdgePixels.new;
				INNER;
			#);
		drawMediumNormal: drawMedium
			(#
			do gray3 -> background;
				gray9 -> cornerPixels[1];
				gray5 -> cornerPixels[2];
				gray3 -> cornerPixels[3];
				gray9 -> northWestEdgePixels[1];
				gray3 -> northWestEdgePixels[2];
				white -> northWestEdgePixels[3];
				A1 -> southEeastEdgePixels[1];
				gray8 -> southEeastEdgePixels[2];
				gray6 -> southEeastEdgePixels[3];
			#);
	enter box
	do drawMediumNormal;
	#);
	