ORIGIN '../editlabel';
INCLUDE '~beta/sysutils/objinterface'
        '~beta/guienv/utils/guienvadds'
        '~beta/guienv/private/X11/guienv_unixprivate';
MDBODY nti 'winnt/editlabelbody'
       macintosh 'macintosh/editlabelbody'
       default 'X11/editlabelbody';
-- editlabelPriv: Descriptor --
(# theEditor: ^labelEditor;  #)  

-- windowLib: Attributes --
labelEditor: dialogField
  (#
     current: ^editLabel;
     open:: 
       (# 
       do
          (2,2)->margin;
          (XmNshadowThickNess, 1)->setIntegerResource;
          hide;
          
       #);
     endEdit:
       (# ok: @boolean; value: ^text; 
       do
          (if current[] <> none then
              textContents->value[];
              value[]->current.confirm->ok;
              (if ok then
                  value[]->current.label;
                  hide;
                  none ->current.private.theEditor[];
                  current.theEventHandler.onEnd;
                  
              if);
              
           else
              true->ok; 
          if);
          
       exit ok
       #);
     startEdit:
       (# new: ^editLabel; ok: @boolean; 
       enter new[]
       do
          endEdit->ok;
          (if ok then
              new[]->current[];
              new.label->textContents;
              new.style->defaultStyle;
              place;
              show;
              this(labelEditor)[] -> target;
              current.theEventHandler.onStart;
              
          if);
          
       exit ok
       #);
     place:
       (# bounds: @rectangle; 
       do
          current.frame->bounds;
          bounds.left-left->bounds.left;
          bounds.top-top->bounds.top;
          bounds.right+right->bounds.right;
          bounds.bottom+bottom->bounds.bottom;
          (bounds.topLeft,current.father[],THIS(window).contents)->translate
            ->bounds.topLeft;
          (bounds.bottomRight,current.father[],THIS(window).contents)->translate
            ->bounds.bottomRight;
          bounds->frame;
          
       #);
     filter::  (#  do ch->current.filter->allow;  #);
     eventHandler::  
       (# onReturn::  
            (#  
            do endEdit;  
            #);
         
       #);
     
  #);
getEditor: objectPool.get
  (# type:: labelEditor; init::  (#  do contents->obj.open;  #);  #);
left: integerValue (#  do 1->value;  #);
right: integerValue (#  do 1->value;  #);
top: integerValue (#  do 1->value;  #);
bottom: integerValue (#  do 1->value;  #);
  

-- editLabelStartEdit: DoPart --
do
   getEditor->private.theEditor[];
   THIS(editLabel)[]->private.theEditor.startEdit->ok;
     

-- editLabelEndEdit: DoPart --
do
   (if private.theEditor[] <> none then private.theEditor.endEdit->ok;  if);   

-- endCurrentEditingDoPart: DoPart --
do
     (# theEditor: ^labelEditor; 
     do getEditor->theEditor[]; theEditor.endEdit->ok; 
     #);
     

