ORIGIN '../editlabel';
MDBODY nti 'winnt/editlabelbody'
       macintosh 'macintosh/editlabelbody'
default 'X11/editlabelbody';
INCLUDE '~beta/sysutils/v1.6/objinterface';

-- editlabelPriv: Descriptor --
(# theEditor: ^labelEditor;  #)  

-- windowLib: Attributes --
labelEditor: dialogField
  (#
     current: ^editLabel;
     open::  
       (#  do hide;  #);
     endEdit:
       (# ok: @boolean;
          value: ^text;
       do
          (if current[] <> none then
              textContents -> value[];
              value[] -> current.confirm -> ok;
              (if ok then
                  value[]->current.label; hide;
                  none -> current.private.theEditor[];
                  current.theEventHandler.onEnd;
              if);
           else
              true -> ok;
          if);
       exit ok
       #);
     startEdit:
       (# new: ^editLabel;
          ok: @boolean;
       enter new[]
       do endEdit -> ok;
          (if ok then
              new[]->current[]; 
              new.label->textContents;
              new.style -> defaultStyle;
              place; 
              show;
              current.theEventHandler.onStart;
          if);
       exit ok
       #);
     place:
       (# bounds: @rectangle; 
       do
          current.frame->bounds;
          bounds.left-left->bounds.left;
          bounds.top-top->bounds.top;
          bounds.right+right->bounds.right;
          bounds.bottom+bottom->bounds.bottom;
          bounds->frame;
          
       #);
     filter::
       (#
       do ch -> current.filter -> allow;
       #);
     eventHandler::  (# onReturn::  (#  do endEdit;  #);  #);
     
  #);
getEditor: objectPool.get
  (# type:: labelEditor; 
     init::  
       (# 
       do contents -> obj.open; 
       #); 
  #);
left: integerValue (#  do 5->value;  #);
right: integerValue (#  do 5->value;  #);
top: integerValue (#  do 5->value;  #);
bottom: integerValue (#  do 5->value;  #);
  

-- editLabelStartEdit: DoPart --
do
   getEditor->private.theEditor[]; THIS(editLabel)[]->private.theEditor.startEdit -> ok;   

-- editLabelEndEdit: DoPart --
do (if private.theEditor[] <> NONE then
       private.theEditor.endEdit -> ok;
   if);

-- endCurrentEditingDoPart: doPart --
do (# theEditor: ^labelEditor;
   do getEditor -> theEditor[];
      theEditor.endEdit -> ok;
   #);
