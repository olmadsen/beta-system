ORIGIN '../tableview';
INCLUDE '~beta/containers/list';
INCLUDE '../drawing';
INCLUDE '~beta/guienv/controls';

-- columnSetTitle: doPart --
do value.copy -> private.theTitle[];

-- columnGetTitle: doPart --
do private.theTitle.copy -> value[];

-- columnSetWidth: doPart --
do value -> private.theWidth;

-- columnGetWidth: doPart --
do private.theWidth -> value;

-- columnOpen: doPart --
do 'Column' -> private.theTitle[];
	150 -> private.theWidth;
	0 -> private.index;
	INNER;

-- columnClose: doPart --
do INNER;
	0 -> private.theWidth;
	NONE -> private.theTitle[];


	
-- columnPrivate: descriptor --
(# theTitle: ^text;
	theWidth: @integer;
	pos: ^columnList.theCellType;
	index: @integer;
#)

-- tableViewAppendColumn: doPart --
do theColumn[] -> private.columns.append 
					-> theColumn.private.pos[];
	private.columns.size -> theColumn.private.index;

-- tableViewInsertColumn: doPart --
do	after.private.index -> theColumn.private.index;
	(theColumn[], after.private.pos[]) 
		-> private.columns.insertAfter
		-> theColumn.private.pos[];
	theColumn.private.pos[] -> private.columns.scanFrom
		(#
		do current.private.index + 1 -> current.private.index;
		#);

-- tableViewDeleteColumn: doPart --
do theColumn.private.pos[] -> private.columns.scanFrom
		(#
		do current.private.index - 1 -> current.private.index;
		#);
	theColumn.private.pos[] -> private.columns.delete;
	NONE -> theColumn.private.pos[];
	0 -> theColumn.private.index;

-- mainColumnTypeOnDrawTitle: doPart --
do (# box: @rectangle;
		mainColumnIndent: (# exit 25 #);
	do bounds -> box;
		(box, g.bevelSmallNormal) -> g.drawBevel;
		g.black -> g.pen.foreGroundColor;
		box.left + mainColumnIndent -> box.left;
		(this(tableView).private.titleStyle[],
	 	private.theTitle[], box, g.alignmentLeft) -> g.drawCenteredText;
	#);

-- mainColumnTypeOnDrawData: doPart --
do (# box: @rectangle;
		mainColumnIndent: (# exit 25 #);
	do bounds -> box;
		(if selected then
			hiliteColor -> g.pen.foreGroundColor;
			box -> g.fillRect;
		if);
		g.black -> g.pen.foreGroundColor;
		box.left + mainColumnIndent -> box.left;
		(this(tableView).private.bodyStyle[],
	 	data[], box, g.alignmentLeft) -> g.drawCenteredText;
	#);
 


-- simpleColumnSetAlignment: doPart --
do value -> private.theAlignment;

-- simpleColumnGetAlignment: doPart --
do private.theAlignment -> value;

-- simpleColumnOpen: doPart --
do alignLeft -> alignment;
	INNER;
	
-- simpleColumnClose: doPart --
do INNER;


-- simpleColumnOnDrawTitle: doPart --
do (# box: @rectangle;
		inset: (# exit 5 #);
		align: @integer;
	do bounds -> box;
		(box, g.bevelSmallNormal) -> g.drawBevel;
		g.black -> g.pen.foreGroundColor;
		box.left + inset -> box.left;
		box.right - inset -> box.right;
		(if alignment = alignLeft then
			g.alignmentLeft -> align;
		else
			g.alignmentRight -> align;
		if);
		(this(tableView).private.titleStyle[],
	 	this(column).private.theTitle[], box, align) -> g.drawCenteredText;
	#);

-- simpleColumnOnDrawData: doPart --
do (# box: @rectangle;
		inset: (# exit 5 #);
		align: @integer;
	do bounds -> box;
		g.black -> g.pen.foreGroundColor;
		box.left + inset -> box.left;
		box.right - inset -> box.right;
		(if alignment = alignLeft then
			g.alignmentLeft -> align;
		else
			g.alignmentRight -> align;
		if);
		(this(tableView).private.bodyStyle[],
	 	data[], box, align) -> g.drawCenteredText;
	#); 

-- simpleColumnPrivate: descriptor --
(#
	theAlignment: @integer;
#)

-- tableItemSetMainText: doPart --
do value.copy -> private.data[1][];

-- tableItemGetMainText: doPart --
do private.data[1].copy -> value[];

-- tableItemSetColumnText: doPart --
do value.copy -> private.data[theColumn.private.index][];

-- tableItemGetColumnText: doPart --
do private.data[theColumn.private.index].copy -> value[];

-- tableItemSelected: doPart --
do private.selected -> value;

-- tableItemOpen: doPart --
do this(tableView).private.columns.size -> private.data.new;
	INNER;

-- tableItemClose: doPart --
do INNER;

-- tableItemPrivate: descriptor --
(# data: [1]^text;
	selected: @boolean;
	pos: ^tableItemList.theCellType;
#)

-- tableViewAppend: doPart --
do theTableItem[] 
		-> private.tableItems.append
		-> theTableItem.private.pos[];
	(if private.tableItems.size = 1 then
		theTableItem[] -> private.topItem[];
	if);
	updateMainView;

-- tableViewPrepend: doPart --
do theTableItem[] 
		-> private.tableItems.prepend
		-> theTableItem.private.pos[];
	theTableItem[] -> private.topItem[];
	updateMainView;

-- tableViewInsert: doPart --
do (theTableItem[], after.private.pos[]) 
		-> private.tableItems.insertAfter
		-> theTableItem.private.pos[];
	updateMainView;

-- tableViewScan: doPart --
do private.tableitems.scan
		(#
		do current[] -> this(scan).current[];
		#);
		
-- tableViewSelectionSet: doPart --
do (if theTableItem.selected then
		(if private.theSelection.size <> 1 then
			clear;
			theTableItem[] -> add;
		if);
	else
		clear;
		theTableItem[] -> add;
	if);

-- tableViewSelectionAdd: doPart --
do (if not theTableItem.selected then
		theTableItem[] -> private.theSelection.append;
		theTableItem.theEventHandler.onSelect;
		true -> theTableItem.private.selected;
		updateMainView;
	if);
		

-- tableViewSelectionRemove: doPart --
do (if theTableItem.selected then
		theTableItem[] -> private.theSelection.at
							-> private.theSelection.delete;
		theTableItem.theEventHandler.onDeSelect;
		false -> theTableItem.private.selected;
		updateMainView;
	if);

-- tableViewSelectionClear: doPart --
do (if NOT private.theSelection.empty then
			private.theSelection.scan
				(#
				do current.theEventHandler.onDeSelect;
					false -> current.private.selected;
				#);
			private.theSelection.clear;
			updateMainView;
	if);

-- tableViewSelectionScan: doPart --
do  private.theSelection.scan
		(#
		do current[] -> this(scan).current[];
			INNER scan;
		#);
		

-- tableViewSetTheScroll: doPart --
do (x, y) -> (private.xScroll, private.yScroll);
	private.yScroll -> private.yOffset;
	updateMainView;

-- tableViewGetTheScroll: doPart --
do (private.xScroll, private.yScroll) -> (x, y);

-- tableViewScroll: doPart --
do

-- tableViewMaxScroll: doPart --
do (# width, height: @integer;
	do private.mainView.size -> (width, height);
		private.columns.scan
			(#
			do maxX + current.width -> maxX;
			#);
		maxX + 50 -> maxX;
		maxX - width -> maxX;
		(if maxX < 0 then
			0 -> maxX;
		if);
		
		private.tableItems.size * (rowHeight +  rowDist) -> maxY;
		maxY - height -> maxY;
		(if maxY < 0 then
			0 -> maxY;
		if);
	#);



-- tableViewOpen: doPart --
do 'Geneva' -> private.titleStyle.name;
	10 -> private.titleStyle.size;
	'Geneva' -> private.bodyStyle.name;
	10 -> private.bodyStyle.size;
	
	this(tableView)[] -> private.titleView.open;
	this(tableView)[] -> private.mainView.open;
	this(tableView)[] -> private.verticalScrollbar.open;
	private.tableItems.init;
	private.columns.init;
	private.theSelection.init;
	mainColumn.open;
	mainColumn[] -> appendColumn;
	layout;
	INNER;
	

-- tableViewClose: doPart --
do INNER;
	private.tableItems.clear;
	private.columns.clear;
	private.titleView.close;
	private.mainView.close;

-- tableViewOnFrameChanged: doPart --
do layout;
	updateScrollBars;
	INNER;

-- tableViewLib: attributes --

updateMainView:
	(#
	do (if not private.pendingUpdate then
			private.mainView.update;
			true -> private.pendingUpdate;
		if);
	#);
	
updateScrollBars:
	(# maxX, maxY: @integer;
		x, y: @integer;
	do true -> private.updatingScrollbars;
		maxScroll -> (maxX, maxY);
		theScroll -> (x, y);
		maxY -> private.verticalScrollBar.maxValue;
		y -> private.verticalScrollBar.value;
		false -> private.updatingScrollbars;
	#);

hiliteColor:
	(# exit (0xFFFF, 52428, 26214) #);
	

columnList: list
	(# theCellType:: (# #);
		element:: column;
	#);
tableItemList: list
	(# theCellType:: (# #);
		element:: tableItem;
	#);
titleHeight: (# exit 20 #);
scrollbarWidth: (# exit 16 #);
layout:
	(# width, height: @integer;
	do size -> (width, height);
		((0, 0), (width - scrollbarWidth, titleHeight)) 
			-> private.titleView.frame;
		((0, titleHeight), (width - scrollbarWidth, height)) 
			-> private.mainView.frame;
		((width - scrollbarWidth, titleHeight), (width, height)) 
			-> private.verticalScrollbar.frame;
	#);
	
tableItemClick:
	(# theEvent: ^windowItem.eventHandler.mouseDown;
		theTableItem: ^tableItem;
	enter (theEvent[], theTableItem[])
	do (if theTableItem.selected then
			(if theEvent.shiftKey then
				theTableItem[] -> selection.remove;
			if);
		else
			(if theEvent.shiftKey then
				theTableItem[] -> selection.add;
			else
				theTableItem[] -> selection.set;
			if);
		if);
	#);
tableViewClick:
	(# theEvent: ^windowItem.eventHandler.mouseDown
	enter theEvent[]
	do selection.clear;
	#);


rowheight: (# exit 18 #);
rowdist: (# exit 1 #);


-- tableViewPrivate: descriptor --
(# topItem: ^tableItem;
	xScroll, yScroll: @integer;
	xOffset, yOffset: @integer;
	
	pendingUpdate: @boolean;
	updatingScrollbars: @boolean;
	columns: @columnList;
	tableItems: @tableItemList;
	titleStyle, bodyStyle: @textStyle;
	
	theSelection: @tableItemList;
	
	titleView: @windowItem
		(# eventHandler::
				(# onRefresh::
						(#
						do	graphics
								(# r: @rectangle;
									width, height: @integer;
								do 0 -> r.left;
									0 -> r.top;
									titleHeight -> r.bottom;
									
									columns.scan
										(#
										do r.left + current.width -> r.right; 
											(this(graphics)[], r[], false) -> current.theEventHandler.onDrawTitle;
											r.right -> r.left;
										#);
									size -> (width, height);
									(if r.left < width then
										width -> r.right;
										(r, bevelSmallNormal) -> drawBevel;
									if);
								#);
						#);
				#);
		#);
	mainView: @windowItem
		(# eventHandler::
				(# onRefresh::
						(#
						do	graphics
								(# r: @rectangle;
									width, height: @integer;
									str: ^text;
								do size -> (width, height);
									-yOffset -> r.top;
									
									(if topItem[] <> NONE then
										updating: topItem.private.pos[] -> tableitems.scanFrom
											(# item: ^tableItem;
											do (if r.top < height then
													0 -> r.left;
													r.top + rowHeight -> r.bottom;
													current[] -> item[];
													columns.scan
													(#
													do r.left + current.width -> r.right;
														item.private.data[current.private.index][] -> str[];
														(if str[] <> NONE then
															(this(graphics)[], r[], item.private.selected, str[]) 
																-> current.theEventHandler.onDrawData;
														if);
														r.right -> r.left;
													#);
													gray1 -> pen.foreGroundColor;
													(0, r.bottom) -> moveTo;
													(width, r.bottom) -> drawTo;
													r.bottom + 1 -> r.top;
												else
													leave updating;
												if);
											#);
									if);
								#);
							false -> pendingUpdate;
						#);
					onMouseDown::
						(# r: @rectangle;
							x, y: @integer;
							item: ^tableItem;
						do - yOffset -> r.top;
							localPosition -> (x, y);
							search: tableitems.scan
							(# 
							do 0 -> r.left;
								r.top + rowHeight -> r.bottom;
								(if (y >= r.top) and (y < r.bottom) then
									current[] -> item[];
									leave search;
								if);
								r.bottom + 1 -> r.top;
							#);
							(if item[] <> NONE then
								(this(onMouseDown)[], item[]) -> tableItemClick;
							else
								this(onMouseDown)[] -> tableViewClick;
							if);
						#);
				#);
		#);
	verticalScrollBar: @scrollbar
		(# eventHandler::
				(# onValueChanged::
						(# x, y: @integer;
						do (if NOT updatingScrollbars then
								theScroll -> (x, y);
								(x, value) -> theScroll;
							if);
						#);
				#);
		#);
#)

