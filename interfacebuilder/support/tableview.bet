ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/graphics';
INCLUDE 'panorama';

-- windowLib: attributes --

alignLeft: (#  exit 1 #);
alignRight: (#  exit 2 #);
alignCenter: (#  exit 3 #);

tableView: canvas
	(# rowHeight: @integer;
		style: ^textStyle;
		sizeTimer: @timer
			(# action::
					(#
					do stop;
						computeSize;
					#);
			#);
		changedSize:
			(#
			do (if not changedSizeFlag then
					true -> changedSizeFlag;
					1 -> sizeTimer.start;
				if);
			#);
		changedSizeFlag: @boolean;
	   computeSize:
			(# width, height: @integer;
				pWidth, pHeight: @integer;
				count: @integer;
			do false -> changedSizeFlag;
				columnIndent -> width;
				(for inx: columns.range repeat
					 width + columns[inx].width -> width;
				for);
				pane.scan (# do count + 1 -> count #);
				(rowHeight + 1) * count -> height;
				tablePanorama.size -> (pWidth, pHeight);
				(width, pWidth) -> max -> width;
				(width, height) -> pane.size;
			#);
		
		columnInfo:
			(# title: ^text;
				width: @integer;
				alignment: @integer;
			#);
		columns: [0] ^columnInfo;
		
		numberOfColumns:
			(# n: @integer;
			enter n
			do n -> columns.new;
				(for inx: columns.range repeat
					&columnInfo[] -> columns[inx][];
					50 -> columns[inx].width;
					'Title' -> columns[inx].title[];
					alignLeft -> columns[inx].alignment;
				for);
				changedSize;
			#);
			
			
		columnIndent: (# exit 150 #);
		headerHeight: (# exit 20 #);
		pane: @canvas
			(# open::
					(#
					do (0xFFFF, 0xFFFF, 0xFFFF) -> backGroundColor;
					#);
			#);
		tablePanorama: @panorama
			(# open::
					(#
					do (0xDDDD, 0xDDDD, 0xDDDD) -> backgroundColor;
						(2, 1 + headerHeight) -> position;
						(300 - 4 - 15, 200 - 3 - headerHeight) -> size;
						true -> bindRight;
						true -> bindBottom;
					#);
			#);
		eventHandler::<
			(# onFrameChanged::<
					(#
					do changedSize; INNER;
					#);
			#);
		tableScrollbar: @panoramaScrollbar
			(# vertical:: (# do true -> value #);
				open::
					(#
					do (300 - 2 - 15, headerHeight) -> position;
						(16, 200 - 1 - headerHeight) -> size;
						false -> bindLeft;
						true -> bindRight;
						true -> bindBottom;
					#);
			#);
		header: @canvas
			(# titleItem: windowItem
					(# title: ^text;
						alignment: @integer;
						open::
							(#
							do true -> border.visible;
								borderStyles.shadowOut -> border.style;
							#);
						eventHandler::
							(# onRefresh::
									(#
									do graphics
											(# left: @integer;
												top: @integer;
												titleWidth, titleHeight: @integer;
												width, height: @integer;
											do 
												(if title[] <> NONE then
													size -> (width, height);
													nameIndent -> left;
													this(tableView).style.lineHeight -> titleHeight;
													(height - titleHeight) div 2 + this(tableView).style.ascent
														-> top;
													(if alignment
														//alignLeft then
															(left, top) -> moveTo;
														//alignRight then
															title[] -> this(tableView).style.widthOfText -> titleWidth;
															(left + (width - titleWidth), top) -> moveTo;
														//alignCenter then
															title[] -> this(tableView).style.widthOfText -> titleWidth;
															(left + (width - titleWidth) div 2, top) -> moveTo;
													if);
													this(tableView).style[] -> style;
													title[] -> drawText;
												if);
											#);
									#);
							#);
					#);
				build:
					(# left: @integer;
						anItem: ^titleItem;
					do &titleItem[] -> anItem[];
						anItem.open;
						(columnIndent, headerHeight) -> anItem.size;
						columnIndent -> left;
						'Name' -> anItem.title[];
						alignLeft -> anItem.alignment;
						(for inx: columns.range repeat
							&titleItem[] -> anItem[];
							anItem.open;
							columns[inx].title.copy -> anItem.title[];
							columns[inx].alignment -> anItem.alignment;
							((left, 0), (left + columns[inx].width, headerHeight)) -> anItem.frame;
							left + columns[inx].width -> left;
							
						for);
						&titleItem[] -> anItem[];
						anItem.open;
						((left, 0), (left+2000, headerHeight)) -> anItem.frame;
					#);
				open::
					(#
					do (1, 1) -> position;
						(300 - 3 - 15, headerHeight) -> size;
						true -> bindRight;
					#);
			#);
		corner: @windowItem
			(# open::
					(#
					do (300 - 2 - 15, 1) -> position;
						(16, headerHeight) -> size;
						true -> border.visible;
						borderStyles.shadowOut -> border.style;
						false -> bindLeft;
						true -> bindRight;
					#);
			#);
		nameIndent: (# exit 25 #);
		tableItem: windowItem
			(# icon: ^pixmap;
				name: ^text;
				subText: [0] ^text;
				
				iconIndent: (# exit 5 #);
				

				open::<
					(# tableWidth, tableHeight: @integer;
						create:: (# do pane[] -> father[] #);
					do (0xDDDD, 0xDDDD, 0xDDDD) -> backgroundColor;
						THIS(tableView).size -> (tableWidth, tableHeight);
						((0, currentY), (tableWidth, currentY + rowHeight)) -> frame;
						currentY + rowHeight + 1 -> currentY;
						columns.range -> subText.new;
						true -> bindRight;
						changedSize;
						INNER;
					#);
					
				eventHandler::<
					(# onRefresh::<
							(#
							do graphics
									(# iconWidth, iconHeight: @integer;
										iconLeft, iconTop: @integer;
										nameHeight: @integer;
										nameLeft, nameTop: @integer;
										sub: ^text;
										subLeft: @integer;
										subWidth: @integer;
									do (if icon[] <> NONE then
											icon.width -> iconWidth;
											icon.height -> iconHeight;
											(rowHeight - iconHeight) div 2 -> iconTop;
											iconIndent -> iconLeft;
											(icon[], (0, 0), (iconLeft, iconTop), iconWidth, iconHeight) -> drawRaster;
										if);
										(if name[] <> NONE then
											this(tableView).style[] -> style;
											this(tableView).style.lineHeight -> nameHeight;
											(rowHeight - nameHeight) div 2 + this(tableView).style.ascent -> nameTop;
											nameIndent -> nameLeft;
											(nameLeft, nameTop) -> moveTo;
											name[] -> drawText;
										if);
										columnIndent -> subLeft;
										(for inx: columns.range repeat
											subText[inx][] -> sub[];
											(if sub[] <> NONE then
												(if columns[inx].alignment
													//alignLeft then
														(subLeft, nameTop) -> moveTo;
													//alignRight then
														sub[] -> this(tableView).style.widthOfText -> subWidth;
														(subLeft + columns[inx].width - subWidth, nameTop) -> moveTo;
													//alignCenter then
														sub[] -> this(tableView).style.widthOfText -> subWidth;
														(subLeft + (columns[inx].width - subWidth) div 2, nameTop) -> moveTo;
												if);
												sub[] -> drawText;
											if);
											subLeft + columns[inx].width -> subLeft; 
										for);
									#);
								INNER;
							#);
					#);
			#);
			
		open::<
			(#
			do (300, 200) -> size;
				(0xDDDD, 0xDDDD, 0xDDDD) -> backgroundColor;
				&textStyle[] -> style[];
				'Times' -> style.name;
				12 -> style.size;
				18 -> rowHeight;
				true -> border.visible;
				borderStyles.shadowIn -> border.style;
				header.open;
				corner.open;
				tablePanorama.open;
				tablePanorama[] -> pane.open;
				tableScrollbar.open;
				tablePanorama[] -> tableScrollbar.attach;
				changedSize;
				INNER;
				header.build;
			#);
		currentY: @integer;
	#);
	