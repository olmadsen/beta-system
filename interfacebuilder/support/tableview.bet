ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/graphics';

-- windowLib: attributes --

tableView: canvas
	(# rowHeight: @integer;
		style: ^textStyle;
		
		columnWidth: [0] @integer;
		
		numberOfColumns:
			(# n: @integer;
			enter n
			do n -> columnWidth.new;
				(for inx: columnWidth.range repeat
					30 -> columnWidth[inx];
				for);
			#);
		tableItem: windowItem
			(# icon: ^pixmap;
				name: ^text;
				subText: [0] ^text;
				
				iconIndent: (# exit 5 #);
				nameIndent: (# exit 25 #);
				columnIndent: (# exit 150 #);
				open::<
					(# tableWidth, tableHeight: @integer;
					do (0xDDDD, 0xDDDD, 0xDDDD) -> backgroundColor;
						THIS(tableView).size -> (tableWidth, tableHeight);
						((0, currentY), (tableWidth, currentY + rowHeight)) -> frame;
						currentY + rowHeight + 1 -> currentY;
						columnWidth.range -> subText.new;
						INNER;
					#);
					
				eventHandler::<
					(# onRefresh::<
							(#
							do graphics
									(# iconWidth, iconHeight: @integer;
										iconLeft, iconTop: @integer;
										nameHeight: @integer;
										nameLeft, nameTop: @integer;
										sub: ^text;
										subLeft: @integer;
									do (if icon[] <> NONE then
											icon.width -> iconWidth;
											icon.height -> iconHeight;
											(rowHeight - iconHeight) div 2 -> iconTop;
											iconIndent -> iconLeft;
											(icon[], (0, 0), (iconLeft, iconTop), iconWidth, iconHeight) -> drawRaster;
										if);
										(if name[] <> NONE then
											this(tableView).style[] -> style;
											this(tableView).style.lineHeight -> nameHeight;
											(rowHeight - nameHeight) div 2 + this(tableView).style.ascent -> nameTop;
											nameIndent -> nameLeft;
											(nameLeft, nameTop) -> moveTo;
											name[] -> drawText;
										if);
										columnIndent -> subLeft;
										(for inx: columnWidth.range repeat
											subText[inx][] -> sub[];
											(if sub[] <> NONE then
												(subLeft, nameTop) -> moveTo;
												sub[] -> drawText;
											if);
											subLeft + columnWidth[inx] -> subLeft; 
										for);
									#);
								INNER;
							#);
					#);
			#);
			
		open::<
			(#
			do (0xFFFF, 0xFFFF, 0xFFFF) -> backGroundColor;
				&textStyle[] -> style[];
				'Times' -> style.name;
				12 -> style.size;
				18 -> rowHeight;
				INNER;
			#);
		currentY: @integer;
	#);
	