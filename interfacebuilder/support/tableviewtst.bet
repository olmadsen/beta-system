ORIGIN 'tableview';
INCLUDE '~beta/guienv/utils/drawing';

-- lib: attributes --


user:
  (# name: ^text;
     ID: @integer;
     kind: @integer;
  #);

guestKind: (# exit 1 #);
ownerKind: (# exit 2 #);
networkKind: (# exit 3 #);

userManager:
  (# lastID: @integer;
     
     
     addUser:
       (# name: ^text;
          kind: @integer;
          theUser: ^user;
       enter (name[], kind)
       do lastID + 1 -> lastID;
          &user[] -> theUser[];
          lastID -> theUser.ID;
          name[] -> theUser.name[];
          kind -> theUser.kind;
          theUser[] -> users.append;
       #);
     
     make:
       (# name: ^text;
       do ('Michael Lassen', ownerKind) -> addUser;
          ('Guest', guestKind) -> addUser;
          ('Ole Lehrmann', networkKind) -> addUser;
          ('J¿gen Lindskov', networkKind) -> addUser;
          ('Flemming Gram', networkKind) -> addUser;
          ('Elmer Sandvad', networkKind) -> addUser;
          ('Peter Andersen', networkKind) -> addUser;
          (for inx: 0 repeat
               &text[] -> name[];
               'User ' -> name.putText;
               inx -> name.putInt;
               (name[], networkKind) -> addUser;
          for);
       #);
     
     users: @list
       (# element:: user;
       #);
  #);

-- windowLib: attributes --

userTable: tableView
  (# userIcon: @pixmap;
     theUserManager: ^userManager;
     kindColumn: @simpleColumn
       (# open::
            (#
            do 'Kind' -> title;
               100 -> width;
            #);
       #);
     idColumn: @simpleColumn
       (# open::
            (#
            do 'ID' -> title;
               50 -> width;
               alignRight -> alignment;
            #);
          less::
            (#
            do (left.theUser.ID < right.theUser.ID) -> value;
               true -> doneInInner;
            #);
       #);
     accessColumn: @column
       (# eventHandler::
            (# onDrawTitle::
                 (#
                 do (if this(column)[] = sortByColumn then
                        (bounds, g.bevelSmallPressed) -> g.drawBevel;
                     else
                        (bounds, g.bevelSmallNormal) -> g.drawBevel;
                    if);
                    (userIcon[], bounds, g.alignmentCenter) -> g.drawCenteredIcon;
                 #);
               onDrawItem::
                 (# access: @boolean;
                    green: (# exit (0, 0xFFFF, 0) #);
                 do (if (1 -> theTableItem.theUser.name.inxGet) <> 'U' then
                        true -> access;
                    if);
                    (if access then
                        green -> g.pen.foreGroundColor;
                        ((bounds.left + 10 + 4, bounds.top + 8), (bounds.left + 10 + 6, bounds.top + 14)) -> g.drawLine;
                        ((bounds.left + 10 + 6,bounds.top + 14),(bounds.left + 10 + 9,bounds.top + 4)) -> g.drawLine;
                    if);
                 #);
            #);
          open::
            (#
            do 30 -> width;
            #);
       #);
     
     tableItemType::
       (# theUser: ^user;
          open::
            (# txt: ^text;
            enter theUser[]
            do theUser.name[] -> mainText;
               true -> expander;
               &text[] -> txt[];
               theUser.ID -> txt.putInt;
               (idColumn[], txt[]) -> setColumnText;
               (if theUser.kind
                //ownerKind then
                   (kindColumn[], 'Owner') -> setColumnText;
                //guestKind then
                   (kindColumn[], 'Guest') -> setColumnText;
                //networkKind then
                   (kindColumn[], 'Network') -> setColumnText;
                   userIcon[] -> icon;
               if);
            #);
       #);
     init:
       (# item: ^tableItemType;
       enter theUserManager[]
       do theUserManager.users.scan
          (#
          do &tableItemType[] -> item[];
             current[] -> item.open;
             item[] -> append;
          #);
       #);
     open::
       (#
       do 'user' -> userIcon.read;
          'Name' -> mainColumn.title;
          200 -> mainColumn.width;
          kindColumn.open;
          kindColumn[] -> appendColumn;
          idColumn.open;
          idColumn[] -> appendColumn;
          accessColumn.open;
          accessColumn[] -> appendColumn;
       #);
  #);
-- program: descriptor --
GUIenv
(# 

   w: @window
     (# theUserTable: @userTable
          (#
          #);
        theUserManager: @userManager;
        open::
          (#
          do theUserManager.make;
             theUserTable.open;
             theUserManager[] -> theUserTable.init;
             (10, 10) -> theUserTable.position;
             (600, 300) -> theUserTable.size;
             (620, 340) -> size;
             true -> theUserTable.bindRight;
             true -> theUserTable.bindBottom;
          #);
     #);	
do 
   w.open;
#)
