ORIGIN '~beta/guienv/guienv';
INCLUDE 'drawing' 'behaviour';

-- windowLib: attributes --

expanderButton: windowItem
  (# onExpand:<
       (#
       do inner;
       #);
     onCollapse:<
       (#
       do INNER;
       #);
     expanded: @boolean;
	  toggle:
	    (#
		 do not expanded -> expanded;
		 	 (if expanded then
			 	onExpand;
			 else
			 	onCollapse;
			 if);
			 update;
		 #);
		 
	  open::<
	    (#
		 do (12, 12) -> size;
		    INNER;
		 #);
     eventHandler::<
       (# onRefresh::
            (#
            do graphics
                 (#
                 do (if expanded then
					  			(0, 0, expanderEnabledDown) -> drawExpander;
							else
								(0, 0, expanderEnabledRight) -> drawExpander;
							if);
                 #);
            #);
			  onMouseDown::
			    (#  box: @rectangle;
				 do ((0, 0), size) -> box;
				 	 buttonBehavior
				 		(# drawPressed::
								(#
								do graphics
										(#
										do (if expanded then
												(0, 0, expanderPressedDown) -> drawExpander;
											else
												(0, 0, expanderPressedRight) -> drawExpander;
											if);
										#);
								#);
							drawNormal::
								(#
								do graphics
									  (#
									  do (if expanded then
												(0, 0, expanderEnabledDown) -> drawExpander;
											else
												(0, 0, expanderEnabledRight) -> drawExpander;
											if);
									  #);
								#);
							action::
								(#
								do graphics
										(#
										do box -> eraseRectangle;
											(0, 0, expanderIntermediateRight) -> drawExpander;
											3 -> guienvWait;
											box -> eraseRectangle;
											(if expanded then
												(0, 0, expanderPressedRight) -> drawExpander;
											else
												(0, 0, expanderPressedDown) -> drawExpander;
											if);
											3 -> guienvWait;
											toggle;
										#);
								#);
						#); 
				 #);
       #);
  #);

imageItem: windowItem
	(# icon: ^pixmap;
		fitToContents::<
			(#
			do (if icon[] <> NONE then
					(icon.width, icon.height) -> size;
				if);
				true -> doneInInner;
			#);
		eventHandler::<
			(# onRefresh::<
					(#
					do graphics
							(#
							do (if icon[] <> NONE then
									(icon[], ((0, 0), size), alignmentCenter) -> drawCenteredIcon;
								if);
							#);
						INNER;
					#);
			#);
	#);
	
textItem: windowItem
	(# icon: ^pixmap;
		theText: ^text;
		theStyle: ^textStyle;
		alignment: @integer;
		
		eventHandler::<
			(# onRefresh::<
					(# box: @rectangle;
					do size -> box.size;
						(if border.visible then
							(2, 2) -> box.inset;
						if);
						graphics
							(#
							do (if icon[] <> NONE then
									(if theText[] <> NONE then
										(icon[], theText[], theStyle[], box, alignment, 4) -> drawIconAndText;
									else
										(icon[], box, alignment) -> drawCenteredIcon;
									if);
								else
									(if theText[] <> NONE then
										(theStyle[], theText[], box, alignment) -> drawCenteredText;
									if);
								if);
							#);
						INNER;
					#);
			#);
	#);
	
small: (# exit 1 #);
medium: (# exit 2 #);
large: (# exit 3 #);

alignmentLeft: (# exit 1 #);
alignmentRight: (# exit 2 #);
alignmentCenter: (# exit 3 #);

bevelButton: windowItem
	(#	label: ^text;
		alignment: @integer;
		style: ^textStyle;
		kind: @integer;
		drawNormal:
			(# box: @rectangle;
			do	size -> box.size;
				graphics
					(#
					do (if kind 
							//small then
								(box, bevelSmallNormal) -> drawBevel;
								(4, 2) -> box.inset;
							//medium then
								(box, bevelMediumNormal) -> drawBevel;
								(5, 3) -> box.inset;
							//large then
								(box, bevelLargeNormal) -> drawBevel;
								(6, 4) -> box.inset;
						if);
						(if label[] <> NONE then
							black -> pen.foreGroundColor;
							(this(bevelButton).style[], label[], box, alignment) -> drawCenteredText;
						if);
					#);
			#);
		drawPressed:
			(# box: @rectangle;
			do	size -> box.size;
				graphics
					(#
					do (if kind 
							//small then
								(box, bevelSmallPressed) -> drawBevel;
								(4, 2) -> box.inset;
							//medium then
								(box, bevelMediumPressed) -> drawBevel;
								(5, 3) -> box.inset;
							//large then
								(box, bevelLargePressed) -> drawBevel;
								(6, 4) -> box.inset;
						if);
						(if label[] <> NONE then
							black -> pen.foreGroundColor;
							(this(bevelButton).style[], label[], box, alignment) -> drawCenteredText;
						if);
					#);
			#);
		open::<
			(#
			do &textStyle[] -> style[];
				'Times' -> style.name;
				12 -> style.size;
				alignmentCenter -> alignment;
				medium -> kind;
				INNER;
			#);
		eventHandler::<
			(# onRefresh::<
					(#
					do drawNormal;
						INNER;
					#);
				onMouseDown::<
					(# box: @rectangle;
					do size -> box.size;
						buttonBehavior
							(# drawPressed::
									(#
									do this(bevelButton).drawPressed;
									#);
								drawNormal::
									(#
									do this(bevelButton).drawNormal;
									#);
								action::
									(#
									do update;
									#);
							#); 
					#);
			#);
	#);