ORIGIN '../friggapplication';
INCLUDE '../code/match'
        '../code/generate'
        '~beta/betaast/v5.2/gram'
        '~beta/sourcebrowser/v1.2/private/ymerBody'
        '../dialogs/createwindow';
-- friggApplicationLib: Attributes --
externalFragmentFormHandler:
 ymerBrowser.browser.edenv.externalFragmentFormHandler
  (#
     astReplacedEvent:: 
       (# 
       do (false,ff[],oldAst[],newAst[])->theFragServer.notifyAstReplaced; 
       #);
     astReplacedSequenceEvent:: 
       (# 
       do
          (false,ff[],rootOfSequence[])
            ->theFragServer.notifyAstReplacedSequence;
          
       #);
     listElementInsertedEvent:: 
       (# 
       do
          (false,ff[],listNode[],position)
            ->theFragServer.notifylistElementInserted;
          
       #);
     makeAstList:
       (#
          thePartList: ^window.editorEnv.partList;
          theAstList: ^astInterface.myAstList;
          length: @integer;
          
       enter (length,thePartList[])
       do
          &mps.ast.myAstList[]->theAstList[];
          theAstList.init;
          (for inx: length repeat
            thePartList.elm[inx][]->theAstList.insert; 
          for);
          
       exit theAstList[]
       #);
     listElementsDeletedEvent:: 
       (# 
       do
          (false,ff[],listNode[],position,length,
           (length,oldElements[])->makeAstList)
            ->theFragServer.notifyListElementsDeleted;
          
       #);
     listElementsReplacedEvent:: 
       (# 
       do
          (false,ff[],listNode[],position,length,
           (length,oldElements[])->makeAstList,newLength)
            ->theFragServer.notifyListElementsReplaced;
          
       #);
     
  #);
findGroupEditor:
  (#
     fg: ^astInterface.fragmentGroup;
     theGroupEditor: ^window.editorEnv.groupEditor;
     
  enter fg[]
  do
     fg[]->ymerBrowser.ymerBrowserPrivate.geList.findOrCreateGroupEditor
       ->theGroupEditor[];
     
  exit theGroupEditor[]
  #);
makeSavePrompt:
  (# fg: ^astInterface.fragmentGroup; str: ^text; 
  enter fg[]
  do 'Save changes to '->str[]; fg.name->str.putText; '?'->str.put; 
  exit str[]
  #);
  

-- friggApplicationOnGroupSaved: DoPart --
do   

-- friggApplicationOnGroupNotSaved: DoPart --
do   

-- friggApplicationOnGroupClosed: DoPart --
do   

-- theFragServerLib: Attributes --
findFFhandler:
  (#
     frag: ^astInterface.fragmentForm;
     ffh: ^window.editorEnv.fragmentFormHandler;
     
  enter frag[]
  do
     l: ymerBrowser.browser.edenv.ffhandler.scan
       (# 
       do (if current.ff[] = frag[] then current[]->ffh[]; leave l;  if); 
       #);
     
  exit ffh[]
  #);
withFFH:
  (#
     ffh: ^window.editorEnv.fragmentFormHandler;
     frag: ^astInterface.fragmentForm;
     fromInterfaceBuilder: @boolean;
     
  enter (frag[],fromInterfaceBuilder)
  do
     (if fromInterfaceBuilder then
         frag[]->findFFhandler->ffh[]; (if ffh[] <> none then INNER ;  if); 
     if);
     
  #);
makePartList:
  (#
     theAstList: ^astInterface.myAstList;
     thePartList: ^window.editorEnv.partList;
     inx: @integer;
     
  enter theAstList[]
  do
     &ymerBrowser.browser.edenv.partList[]->thePartList[];
     theAstList.size->thePartList.elm.new;
     0->inx;
     theAstList.scan
       (#  do inx+1->inx; current[]->thePartList.elm[inx][];  #);
     
  exit thePartList[]
  #);
  

-- theFragServerSubscribeDopart: DoPart --
do
     (# effh: ^externalFragmentFormHandler
     do
        &externalFragmentFormHandler[]->effh[];
        fragHandler.frag[]->effh.ff[];
        effh[]->ymerBrowser.browser.edenv.ffhandler.externalSubscribe;
        
     #);
     

-- theFragServerNotifyAstReplacedDopart: DoPart --
do
   (frag[],fromInterfaceBuilder)
     ->withFFH
       (#  do (none ,oldAst[],newAst[])->ffh.anAstReplacedEvent;  #);
     

-- theFragServerNotifyListElementInsertedDopart: DoPart --
do
   (frag[],fromInterfaceBuilder)
     ->withFFH
       (# 
       do (none ,listNode[],position)->ffh.aListElementInsertedEvent; 
       #);
     

-- theFragServerNotifyAstReplacedSequenceDopart: DoPart --
do
   (frag[],fromInterfaceBuilder)
     ->withFFH
       (#  do (none ,rootOfSequence[])->ffh.astReplacedSequenceEvent;  #);
     

-- theFragServerNotifyListElementsDeletedDopart: DoPart --
do
   (frag[],fromInterfaceBuilder)
     ->withFFH
       (# 
       do
          (none ,listNode[],position,length,oldElements[]->makePartList)
            ->ffh.aListElementsDeletedEvent;
          
       #);
     

-- theFragServerNotifyListElementsReplacedDopart: DoPart --
do
   (frag[],fromInterfaceBuilder)
     ->withFFH
       (# 
       do
          (none ,listNode[],position,length,oldElements[]->makePartList,
           newLength)->ffh.aListElementsReplacedEvent;
          
       #);
     

-- theFragServerChanged: DoPart --
do frag.father->theGraphicalEditorenv.onGroupChanged;   

-- theGraphicalEditorEnvSaveDopart: DoPart --
do ymerBrowser.saveAll;   

-- theGraphicalEditorEnvOpenAstEditorDopart: DoPart --
do
     (#
        theGroupEditor: ^window.editorEnv.groupEditor;
        theFormEditor: ^window.codeEditor;
        
     do
        node.frag.father->findGroupEditor->theGroupEditor[];
        (if theGroupEditor[] <> none then
            node.frag[]->theGroupEditor.formEditorList.findOrCreateEditor
              ->theFormEditor[];
            (if theFormEditor[] = none then
                node.frag[]->ymerBrowser.findForm;
                (node[],node[])->ymerBrowser.browser.cfe.openSubEditor;
                
             else
                (node[],node[])->theFormEditor.openSubEditor; 
            if);
            
        if);
        
     #);
     

-- theGraphicalEditorEnvAutoSaveGroupDopart: DoPart --
do ('#',fg[])->ymerBrowser.browser.edenv.editorenvPrivate.extendedSaveBackup  

-- theGraphicalEditorEnvOnGroupOpenDopart: DoPart --
do fg[]->ymerBrowser.findGroup;   

-- theGraphicalEditorEnvSaveFragmentGroupDopart: DoPart --
do
     (# theGroupEditor: ^window.editorEnv.groupEditor; 
     do
        fg[]->findGroupEditor->theGroupEditor[];
        (if theGroupEditor[] = none then
            
         else
            (true,false,true)->theGroupEditor.save; 
        if);
        
     #);
     

-- theGraphicalEditorEnvOnGroupChangedDopart: DoPart --
do
     (# theGroupEditor: ^window.editorEnv.groupEditor; 
     do
        fg[]->findGroupEditor->theGroupEditor[];
        (if theGroupEditor[] <> none then theGroupEditor.groupTouched;  if);
        
     #);
     

-- theGraphicalEditorEnvOnGroupSavedDopart: DoPart --
do   

-- ibMenuNewWindowLibItemOnStatus: DoPart --
do not checkOngoingTextEditing->value  

-- ibMenuNewWindowLibItemOnSelect: DoPart --
do
     (# fileName: ^text
     do
        fileCreationDialog->fileName[];
        (if fileName[] <> none then
            (fileName[],'guienvLib')->theGraphicalEditorEnv.createSourceFile
        if)
     #);
     

-- ibMenuNewCanvasLibItemOnStatus: DoPart --
do true->value;   

-- ibMenuNewCanvasLibItemOnSelect: DoPart --
do
     (# fileName: ^text; 
     do
        fileCreationDialog->fileName[];
        (if fileName[] <> none then
            (fileName[],'windowLib')->theGraphicalEditorEnv.createSourceFile
        if)
     #);
     

-- createSourceFile1: DoPart --
do
     (#
        fileName: ^text;
        group: ^astInterface.fragmentGroup;
        frag: ^astInterface.fragmentForm;
        stripExtension:
          (# path: ^text; lastDotInx,lastSlashInx: @integer; 
          enter path[]
          do
             '.'->path.findAll (#  do inx->lastDotInx #);
             MPS.ast.thePathHandler.directoryChar
               ->path.findAll (#  do inx->lastSlashInx #);
             (if (lastDotInx > 0) and (lastDotInx > lastSlashInx) then
                 (lastDotInx,path.length)->path.delete; 
             if);
             
          #);
        createFragmentGroup:
          (# name: ^text; group: ^astInterface.fragmentGroup; 
          enter name[]
          do name[]->MPS.AST.top.newGroup->group[]; 
          exit group[]
          #);
        createFragmentForm:
          (# name: ^text; frag: ^astInterface.fragmentForm; 
          enter name[]
          do
             MPS.betaCFL[]->MPS.AST.newFragmentForm->frag[]; name[]->frag.name; 
          exit frag[]
          #);
        bodyGroup: ^astInterface.fragmentGroup;
        astFileName: ^text;
        betFileName: ^text;
        bodyFileName: ^text;
        bodyAstFileName: ^text;
        bodyBetFileName: ^text;
        gram: @grammar;
        guienvLocation: (#  exit '~beta/guienv/v1.6/guienvall' #);
        iconNameLocation: (#  exit '~beta/guienv/v1.6/utils/iconname' #);
        getShortName:
          (# name: ^text; shortName: ^text; lastDirCharPos: @integer; 
          enter name[]
          do
             name.reset;
             MPS.AST.thePathHandler.directoryChar
               ->name.find (#  do inx->lastDirCharPos;  #);
             (if (lastDirCharPos > 0) and (lastDirCharPos < name.length) then
                 (lastDirCharPos+1,name.length)->name.sub->shortName[]; 
              else
                 name.copy->shortName[]; 
             if);
             
          exit shortName[]
          #);
        
     do
        fileCreationDialog->fileName[];
        (if fileName[] <> none then
            fileName[]->stripExtension;
            '.ast'->(fileName.copy).append->astFileName[];
            '.bet'->(fileName.copy).append->betFileName[];
            'body'->(fileName.copy).append->bodyFileName[];
            '.ast'->(bodyFileName.copy).append->bodyAstFileName[];
            '.bet'->(bodyFileName.copy).append->bodyBetFileName[];
            (if (astFileName[]->entryExists) or (betFileName[]->entryExists) or
            (bodyAstFileName[]->entryExists) or
            (bodyBetFileName[]->entryExists) then
                'does already exist'->putLine; 
             else
                fileName[]->createFragmentGroup->group[];
                (if group[] <> none then
                    'ORIGIN'
                      ->group.prop.addProp
                        (#  do guienvLocation->addString;  #);
                    'INCLUDE'
                      ->group.prop.addProp
                        (#  do iconNameLocation->addString;  #);
                    'BODY'
                      ->group.prop.addProp
                        (#  do bodyFileName[]->getShortName->addString;  #);
                    group.loadIncludes;
                    fragName[]->MPS.betaCFL.newAttributesFrag->frag[];
                    frag[]->group.fragmentList.addFragment;
                    group.markAsChanged;
                    (MPS.AST[],group[],none ,betFileName[])
                      ->prettyprintFragment;
                    
                if);
                bodyFileName[]->createFragmentGroup->bodyGroup[];
                (if bodyGroup[] <> none then
                    'ORIGIN'
                      ->bodyGroup.prop.addProp
                        (#  do fileName[]->getShortName->addString;  #);
                    bodyGroup.markAsChanged;
                    (MPS.AST[],bodyGroup[],none ,bodyBetFileName[])
                      ->prettyprintFragment;
                    
                if);
                bodyBetFileName[]->ymerbrowser.appendProject;
                fileName[]->ymerbrowser.appendProject;
                ymerbrowser.appendDone;
                
            if);
            
        if);
        
     #)  

-- newWindowItemOnSelect: DoPart --
do
     (#
        theCreateWindow: @createWindowDialog
          (#
             onCancel::  (#  do theCreateWindow.close #);
             onCreate:: 
               (# 
               do
                  (if name[]->theGraphicalEditorEnv.checkIdentifier then
                      theCreateWindow.close;
                      name[]->theGraphicalEditorEnv.createNewWindow
                  if)
               #)
          #)
     do theCreateWindow.open
     #)  

-- newGraphicalEditorOnSelect: DoPart --
do
   (if browser.cfe[] <> none then
       (if browser.cfe.cs.node[] <> none then
           browser.cfe.cs.node.frag[]->frag[];
           (if frag[] <> none then
               (if frag.category = mps.betaCfl.attributesForm then
                   frag.name->fragName[];
                   (if true
                    // 'guienvLib'->fragName.equalNCS then
                       'window'->className[]; 
                    // 'windowLib'->fragName.equalNCS then
                       'canvas'->className[]; 
                   if);
                     (#
                        theCreateWindow: @createWindowDialog
                          (#
                             onCancel::  (#  do theCreateWindow.close #);
                             onCreate:: 
                               (# 
                               do
                                  (if
                                  name[]->theGraphicalEditorEnv.checkIdentifier
                                   then
                                      theCreateWindow.close;
                                      (frag[],name[],className[])
                                        ->theGraphicalEditorEnv.createWindow
                                  if)
                               #);
                             
                          #)
                     do theCreateWindow.open; 
                     #)
               if);
               
           if);
           
       if);
       
   if);
     

-- friggApplicationProcessCommand: DoPart --
do
   cmd[]->ymerBrowser.appendProject;
   ymerBrowser.appendDone;
   ymerBrowser.bringToFront  

