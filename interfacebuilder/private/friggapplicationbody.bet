ORIGIN '../friggapplication';
INCLUDE '../code/match';
INCLUDE '../code/generate';
INCLUDE '~beta/betaast/v5.2/gram';

-- friggApplicationLib: attributes --


externalFragmentFormHandler: ymerBrowser.browser.edenv.externalFragmentFormHandler
  (# astReplacedEvent::
       (# 
       do (false, ff[], oldAst[], newAst[]) -> theFragServer.notifyAstReplaced;
       #);
     astReplacedSequenceEvent::
       (# 
       do (false, ff[], rootOfSequence[]) -> theFragServer.notifyAstReplacedSequence;
       #);
     listElementInsertedEvent::
       (#
       do (false, ff[], listNode[], position) -> theFragServer.notifylistElementInserted;
       #);
     makeAstList:
       (# thePartList: ^window.editorEnv.partList;
          theAstList: ^astInterface.myAstList;
          length: @integer;
       enter (length, thePartList[])
       do &mps.ast.myAstList[] -> theAstList[];
          theAstList.init;
          (for inx: length repeat
               thePartList.elm[inx][] -> theAstList.insert;
          for);
       exit theAstList[]
       #);
     listElementsDeletedEvent::
       (#
       do (false, ff[], listNode[], position, length, (length, oldElements[]) -> makeAstList)
            -> theFragServer.notifyListElementsDeleted;
       #);
     listElementsReplacedEvent::
       (#
       do (false, ff[], listNode[], position, length, 
          (length, oldElements[]) 
            -> makeAstList, newLength)
            -> theFragServer.notifyListElementsReplaced;
       #);
  #);

findGroupEditor:
  (# fg: ^astInterface.fragmentGroup;
     theGroupEditor: ^window.editorEnv.groupEditor;
  enter fg[]
  do (ymerBrowser.browser[], fg[]) 
       -> ymerBrowser.browser.edenv.groupEditorList.findGroupEditor -> theGroupEditor[];
     (if theGroupEditor[] = none then
         (ymerBrowser.browser[], fg[], false) 
           -> ymerBrowser.browser.edenv.groupEditorList.CreateGroupEditor 
           -> theGroupEditor[];
         (if theGroupEditor[] = NONE then
             'the groupeditor for ' -> putText;
             fg.name -> putText;
             ' was not found' -> putLine;
         if);
     if);
  exit theGroupEditor[]
  #);

makeSavePrompt:
  (# fg: ^astInterface.fragmentGroup;
     str: ^text;
  enter fg[]
  do 'Save changes to ' -> str[];
     fg.name -> str.putText;
     '?' -> str.put;
  exit str[]
  #);

-- friggApplicationOnGroupSaved: doPart --
do

-- friggApplicationOnGroupNotSaved: doPart --
do

-- friggApplicationOnGroupClosed: doPart --
do
   
-- theFragServerLib: attributes --

findFFhandler:
  (# frag: ^astInterface.fragmentForm;
     ffh: ^window.editorEnv.fragmentFormHandler;
  enter frag[]
  do l: ymerBrowser.browser.edenv.ffhandler.scan
       (# 
       do 
          (if current.ff[] = frag[] then
              current[] -> ffh[];
              leave l;
          if);
       #);
  exit ffh[]
  #);
withFFH:
  (# ffh: ^window.editorEnv.fragmentFormHandler;
     frag: ^astInterface.fragmentForm;
     fromInterfaceBuilder: @boolean;
  enter (frag[],  fromInterfaceBuilder)
  do (if fromInterfaceBuilder then
         frag[] -> findFFhandler -> ffh[];
         (if ffh[] <> none then
             inner;
         if);
     if);
  #);

makePartList:
  (# theAstList: ^astInterface.myAstList;
     thePartList: ^window.editorEnv.partList;
     inx: @integer;
  enter theAstList[]
  do &ymerBrowser.browser.edenv.partList[] -> thePartList[];
     theAstList.size -> thePartList.elm.new;
     0 -> inx;
     theAstList.scan
     (# 
     do inx + 1 -> inx;
        current[] -> thePartList.elm[inx][];
     #);
  exit thePartList[]
  #);

--- theFragServerSubscribeDopart: dopart ---
do (# effh: ^externalFragmentFormHandler
   do &externalFragmentFormHandler[] -> effh[];
      fragHandler.frag[] -> effh.ff[];
      effh[] -> ymerBrowser.browser.edenv.ffhandler.externalSubscribe;
   #);
   
--- theFragServerNotifyAstReplacedDopart: dopart ---
do (frag[], fromInterfaceBuilder) -> withFFH
   (# 
   do (none, oldAst[], newAst[]) -> ffh.anAstReplacedEvent;
   #);
   
--- theFragServerNotifyListElementInsertedDopart: dopart ---
do (frag[], fromInterfaceBuilder) -> withFFH
   (# 
   do (none, listNode[], position) -> ffh.aListElementInsertedEvent;
   #);
--- theFragServerNotifyAstReplacedSequenceDopart: dopart ---
do (frag[], fromInterfaceBuilder) -> withFFH
   (# 
   do (none, rootOfSequence[]) -> ffh.astReplacedSequenceEvent;
   #);
   
   
--- theFragServerNotifyListElementsDeletedDopart: dopart ---
do 
   (frag[], fromInterfaceBuilder) -> withFFH
   (# 
   do 
      (none, listNode[], position, length, oldElements[] -> makePartList) 
        -> ffh.aListElementsDeletedEvent;
   #);
--- theFragServerNotifyListElementsReplacedDopart: dopart ---
do (frag[], fromInterfaceBuilder) -> withFFH
   (# 
   do 
      (none, listNode[], position, length, oldElements[] -> makePartList, newLength) 
        -> ffh.aListElementsReplacedEvent;
   #);
   
-- theFragServerChanged: doPart --
do 'theFragServerChanged: doPart' -> putLine;
   
--- theGraphicalEditorEnvSaveDopart: dopart ---
do 'Calling save all' -> putLine;
   ymerBrowser.saveAll;
   
--- theGraphicalEditorEnvOpenAstEditorDopart: dopart ---
do (# theGroupEditor: ^window.editorEnv.groupEditor;
      theFormEditor: ^window.codeEditor;
   do node.frag.father -> findGroupEditor -> theGroupEditor[];
      (if theGroupEditor[] <> NONE then
          node.frag[] -> theGroupEditor.formEditorList.findOrCreateEditor -> theFormEditor[];
          
          (if theFormEditor[] = NONE then
              node.frag[] -> ymerBrowser.findForm;
              (node[], node[]) -> ymerBrowser.browser.cfe.openSubEditor;
           else
              (node[], node[]) -> theFormEditor.openSubEditor;
          if);
       else
          'theGroupEditor was not found!' -> putLine;
      if);
   #);
   
--- theGraphicalEditorEnvAutoSaveGroupDopart: dopart ---
do ('#', fg[]) -> ymerBrowser.browser.edenv.editorenvPrivate.extendedSaveBackup
   
--- theGraphicalEditorEnvOnGroupOpenDopart: dopart ---
do fg[] -> ymerBrowser.findGroup;
   
--- theGraphicalEditorEnvSaveFragmentGroupDopart: dopart ---
do  (# theGroupEditor: ^window.editorEnv.groupEditor;
    do fg[] -> findGroupEditor -> theGroupEditor[];
       (if theGroupEditor[] = none then
           'The groupeditor is NONE!' -> putLine;
        else
           (true, false,true) -> theGroupEditor.save;
       if);
    #);

--- theGraphicalEditorEnvOnGroupChangedDopart: dopart ---
do (# theGroupEditor: ^window.editorEnv.groupEditor;
   do fg[] -> findGroupEditor -> theGroupEditor[];
      (if theGroupEditor[] = none then
          'The groupeditor is NONE!' -> putLine;
       else
          theGroupEditor.groupTouched;
      if);
   #);
   
--- theGraphicalEditorEnvOnGroupSavedDopart: dopart ---
do (# theGroupEditor: ^window.editorEnv.groupEditor;
   do fg[] -> findGroupEditor -> theGroupEditor[];
      (if theGroupEditor[] = none then
          'The groupeditor is NONE!' -> putLine;
       else
          theGroupEditor.resetGroupTouch;
      if);
   #);
   
-- ibMenuNewWindowLibItemOnStatus: doPart --
do not checkOngoingTextEditing -> value
   
-- ibMenuNewWindowLibItemOnSelect: doPart --
do 'guienvLib' -> createSourceFile;
   
-- ibMenuNewCanvasLibItemOnStatus: doPart --
do true -> value;
   
-- ibMenuNewCanvasLibItemOnSelect: doPart --
do 'windowLib' -> createSourceFile;
   
-- createSourceFile: doPart --
do (# fileName: ^text;
      
      group: ^astInterface.fragmentGroup;
      frag: ^astInterface.fragmentForm;
      stripExtension:
        (# path: ^text;
           lastDotInx, lastSlashInx: @integer;
        enter path[]
        do '.' -> path.findAll (# do inx -> lastDotInx #);
           MPS.ast.thePathHandler.directoryChar -> path.findAll (# do inx -> lastSlashInx #);
           (if (lastDotInx > 0) and (lastDotInx > lastSlashInx) then
               (lastDotInx, path.length) -> path.delete;
           if);
        #);
      
      createFragmentGroup:
        (# name: ^text;
           group: ^astInterface.fragmentGroup;
        enter name[]
        do name[] -> MPS.AST.top.newGroup -> group[];
        exit group[]
        #);
      createFragmentForm:
        (# name: ^text;
           frag: ^astInterface.fragmentForm;
        enter name[]
        do MPS.betaCFL[] -> MPS.AST.newFragmentForm -> frag[];
           name[] -> frag.name;
        exit frag[]
        #);
      
      bodyGroup: ^astInterface.fragmentGroup;
      astFileName: ^text;
      betFileName: ^text;
      bodyFileName: ^text;
      bodyAstFileName: ^text;
      bodyBetFileName: ^text;
      gram: @grammar;
      guienvLocation: (# exit '~beta/guienv/v1.6/guienvall' #);
      iconNameLocation: (# exit '~beta/guienv/v1.6/utils/iconname' #);
      
      getShortName:
        (# name: ^text;
           shortName: ^text;
           lastDirCharPos: @integer;
        enter name[]
        do name.reset;
           MPS.AST.thePathHandler.directoryChar -> name.find
           (# 
           do inx -> lastDirCharPos;
           #);
           (if (lastDirCharPos > 0) AND (lastDirCharPos < name.length) then
               (lastDirCharPos + 1, name.length) -> name.sub
                 -> shortName[];
            else
               name.copy -> shortName[];
           if);
        exit shortName[]
        #);
   do fileCreationDialog -> fileName[];
      (if fileName[] <> NONE then
          fileName[] -> stripExtension;
          '.ast' -> (fileName.copy).append -> astFileName[];
          '.bet' -> (fileName.copy).append -> betFileName[];
          'body' -> (fileName.copy).append -> bodyFileName[];
          '.ast' -> (bodyFileName.copy).append -> bodyAstFileName[];
          '.bet' -> (bodyFileName.copy).append -> bodyBetFileName[];
          (if (astFileName[] -> entryExists) OR (betFileName[] -> entryExists) OR
              (bodyAstFileName[] -> entryExists) OR (bodyBetFileName[] -> entryExists) then
              'does already exist' -> putLine;
           else
              fileName[] -> createFragmentGroup -> group[];
              (if group[] <> NONE then
                  'ORIGIN' -> group.prop.addProp
                  (# 
                  do guienvLocation -> addString;
                  #);
                  'INCLUDE' -> group.prop.addProp
                  (# 
                  do iconNameLocation -> addString;
                  #);
                  'BODY' -> group.prop.addProp
                  (# 
                  do bodyFileName[] -> getShortName -> addString;
                  #);
                  group.loadIncludes;
                  fragName[] -> MPS.betaCFL.newAttributesFrag -> frag[];
                  frag[] -> group.fragmentList.addFragment;
                  group.markAsChanged;
                  (MPS.AST[], group[], NONE, betFileName[]) -> prettyprintFragment;
              if);
              bodyFileName[] -> createFragmentGroup ->  bodyGroup[];
              (if bodyGroup[] <> NONE then
                  'ORIGIN' -> bodyGroup.prop.addProp
                  (# 
                  do fileName[] -> getShortName -> addString;
                  #);
                  bodyGroup.markAsChanged;
                  (MPS.AST[], bodyGroup[], NONE, bodyBetFileName[]) -> prettyprintFragment;
              if);
              bodyBetFileName[] -> ymerbrowser.appendProject;
              fileName[] -> ymerbrowser.appendProject;
              ymerbrowser.appendDone;
          if);
      if);
   #)
   


-- friggApplicationCreateWindow: doPart --
do l:
     (# theAttributesForm: ^astInterface.beta.attributesForm;
        theAttributes: ^astInterface.beta.attributes;
        theAttribute: ^astInterface.beta.attributeDecl;
        name: ^text;
        theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
        fg: ^astInterface.fragmentGroup;
        classes: ^patternList;
        classNames: @textList;
        gram: @grammar;
     do classNames.init;
        className[] -> classNames.append;
        
        frag.father -> fg[];
        (MPS.AST[], className[], fg[]) -> getClasses -> classes[];
        classes.scan
        (# 
        do current.node[] -> MPS.betaCFL.getName -> classNames.append;
        #);
        
        frag.root[] -> theAttributesForm[];
        theAttributesForm.getAttributes -> theAttributes[];
        ('', classNames[]) -> userCreateClass
        (# createClass::
             (# isIdentifier: @boolean;
             do name[] -> theGraphicalEditorEnv.checkIdentifier -> isIdentifier;
                (if isIdentifier then
                    (theAttributes[], name[]) ->mps.betacfl.find -> theAttribute[];
                    (if theAttribute[] <> none then
                        (this(ymerBrowserWindow)[],
                        'The window could not be created because it already exists', 
                        'Alert') -> noteUser;
                     else
                        (# son1: ^mps.ast.ast;
                           doReplace: @boolean;
                           super: ^astInterface.beta.patternDecl;
                        do
                           (frag[], name[], className[]) -> mps.betacfl.newPatternAttribute -> theAttribute[];
                           (# 
                              group, bodyGroup: ^astInterface.fragmentGroup;
                              
                              bodyName: ^text;
                              t: @text;
                              decl: ^astInterface.beta.simpleDecl;
                              privateFrag, theFragment: ^astinterface.fragmentForm;
                              privateName: ^text;
                           do frag.father -> group[];
                              (if group[] <> NONE then
                                  l: group.scanBodyGroups
                                  (# 
                                  do currentFullPath[] -> bodyName[];
                                     leave l;
                                     
                                  #);
                                  (if bodyName[] <> NONE then
                                      bodyName[] -> MPS.AST.safeOpen -> bodyGroup[];
                                      (if bodyGroup[] <> NONE then
                                          'private' -> (name.copy).append -> privateName[];
                                          'private: @<<SLOT ' -> t.append;
                                          privateName[] -> t.append;
                                          ': descriptor>>' -> t.append;
                                          (frag[],  t[], gram.simpleDecl) -> parseText -> decl[];
                                          (theAttribute[], decl[]) -> MPS.betaCFL.appendAttribute;
                                          MPS.betaCFL[] -> MPS.AST.newFragmentForm -> privateFrag[];
                                          privateName[] -> privateFrag.name;
                                          privateName[] -> bodyGroup.findFragment -> theFragment[];
                                          (if theFragment[] <> none then
                                              theFragment[] -> bodyGroup.deleteFragment;
                                          if);
                                          privateFrag[] -> bodyGroup.fragmentList.addFragment;
                                          (privateFrag[], none) 
                                            -> MPS.betaCFL.newDescriptorForm
                                            -> privateFrag.root[];
                                      if);
                                  if);
                              if);
                           #);
                           
                           
                           
                           className[] -> classes.findByName -> super[];
                           (if super[] <> none then
                               (theAttribute[], super[]) -> setSemanticLink;
                           if);
                           
                           
                           (if theAttributes.noOfsons = 1 then
                               1 -> theAttributes.get -> son1[];
                               (if son1.kind = mps.ast.kinds.optional then
                                   true -> doReplace;
                               if);
                           if);
                           (if doReplace then
                               (1, theAttribute[]) -> theAttributes.put;
                            else
                               theAttribute[] -> theAttributes.append;
                           if);
                           &theGraphicalEditorEnv.graphicalEditor[] -> theGraphicalEditor[];
                           theGraphicalEditor.open;
                           theAttribute[] -> theGraphicalEditor.new;
                           (if doReplace then
                               (true, frag[], son1[], theAttribute[])
                                 -> theFragServer.notifyAstReplaced; 
                            else
                               (true, frag[], theAttributes[], theAttributes.noOfSons - 1) 
                                 -> theFragServer.notifyListElementInserted;
                           if);
                        #);
                    if);
                 else
                    'That is not a valid beta name' -> displayMessage;
                if);
             #);
        #);
     #);
