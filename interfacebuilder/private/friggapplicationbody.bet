ORIGIN '../friggapplication';
INCLUDE '../code/match'
        '../code/generate'
        '~beta/betaast/gram'
        '~beta/sourcebrowser/private/ymerBody'
        '../dialogs/createwindow';
LIB_ITEM 'frigg_application';

-- friggApplicationLib: Attributes --
findGroupEditor:
  (#
     fg: ^astInterface.fragmentGroup;
     theGroupEditor: ^gui.window.editorEnv.groupEditor;
     
  enter fg[]
  do
     (*
      fg[]->ymerBrowser.ymerBrowserPrivate.geList.findOrCreateGroupEditor
      ->theGroupEditor[]; 
      *)
     fg[]->ymerBrowser.findOrCreateGroupEditor->theGroupEditor[];
  exit theGroupEditor[]
  #);
makeSavePrompt:
  (# fg: ^astInterface.fragmentGroup; str: ^text; 
  enter fg[]
  do 'Save changes to '->str[]; fg.name->str.putText; '?'->str.put; 
  exit str[]
  #);
  

-- theGraphicalEditorEnvOpenAstEditorDopart: DoPart --
do
     (#
        theGroupEditor: ^gui.window.editorEnv.groupEditor;
        theFormEditor: ^gui.window.codeEditor;
        
     do
        node.frag.father->findGroupEditor->theGroupEditor[];
        (if theGroupEditor[] <> none then
            node.frag[]->theGroupEditor.formEditorList.findOrCreateEditor
              ->theFormEditor[];
            (if theFormEditor[] = none then
                node.frag[]->findForm;
                (node[],node[])->ymerBrowser.cfe.openSubEditor;
                
             else
                (node[],node[])->theFormEditor.openSubEditor; 
            if);
            
        if);
        
     #);
     

-- theGraphicalEditorEnvAutoSaveGroupDopart: DoPart --
do
   13->trace (#  do 'group autosave, name = "'->xT; fg.name->xT; xn #);
     (# theGroupEditor: ^gui.window.editorEnv.groupEditor; 
     do
        fg[]->findGroupEditor->theGroupEditor[];
        (if theGroupEditor[] = none then  else theGroupEditor.autoSave;  if);
        
     #)  

-- theGraphicalEditorEnvOnGroupOpenDopart: DoPart --
do
   13->trace (#  do 'group open, name = "'->xT; fg.name->xT; xn #);
   fg[]->findGroup;
     

-- theGraphicalEditorEnvSaveFragmentGroupDopart: DoPart --
do
   13->trace (#  do 'group save, name = "'->xT; fg.name->xT; xn #);
     (# theGroupEditor: ^gui.window.editorEnv.groupEditor; 
     do
        fg[]->findGroupEditor->theGroupEditor[];
        (if theGroupEditor[] = none then
            
         else
            (true,false,true)->theGroupEditor.save; 
        if);
        
     #);
     

-- friggApplicationQuitApplication: DoPart --
do
(*
 (if ymerBrowser.ymerBrowserPrivate.geList.closeGroups and
 ymerBrowser.browser.sbprivate.down.aeList.closeAsciiEditors then
 ymerBrowser.quit
 if) *)   

-- theGraphicalEditorEnvOnGroupChangedDopart: DoPart --
do
   13
     ->trace
       (#  do 'group changed, name = "'->xT; fg.name->xT; '"'->xT; xN #);
     (# theGroupEditor: ^gui.window.editorEnv.groupEditor; 
     do
        fg[]->findGroupEditor->theGroupEditor[];
        (if theGroupEditor[] <> none then
            13
              ->trace
                (# 
                do 'group touched = '->xT; theGroupEditor.touched->xI; xN
                #);
            theGroupEditor.groupTouched;
            
         else
            'thegroupeditor is none'->screen.putline
        if);
        
     #);
     

-- theGraphicalEditorEnvOnGroupSavedDopart: DoPart --
do   

-- ibMenuNewWindowLibItemOnStatus: DoPart --
do true -> value;

-- ibMenuNewWindowLibItemOnSelect: DoPart --
do
     (# fileName: ^text
     do
        gui.fileCreationDialog->fileName[];
        (if fileName[] <> none then
            (fileName[],'guienvLib')->theGraphicalEditorEnv.com.createSourceFile
        if)
     #);
     

-- ibMenuNewCanvasLibItemOnStatus: DoPart --
do true->value;   

-- ibMenuNewCanvasLibItemOnSelect: DoPart --
do
     (# fileName: ^text; 
     do
        gui.fileCreationDialog->fileName[];
        (if fileName[] <> none then
            (fileName[],'windowLib')->theGraphicalEditorEnv.com.createSourceFile
        if)
     #);
     

-- createSourceFile1: DoPart --
do
     (#
        fileName: ^text;
        group: ^astInterface.fragmentGroup;
        frag: ^astInterface.fragmentForm;
        stripExtension:
          (# path: ^text; lastDotInx,lastSlashInx: @integer; 
          enter path[]
          do
             '.'->path.findAll (#  do inx->lastDotInx #);
             MPS.ast.thePathHandler.directoryChar
               ->path.findAll (#  do inx->lastSlashInx #);
             (if (lastDotInx > 0) and (lastDotInx > lastSlashInx) then
                 (lastDotInx,path.length)->path.delete; 
             if);
             
          #);
        createFragmentGroup:
          (# name: ^text; group: ^astInterface.fragmentGroup; 
          enter name[]
          do name[]->MPS.AST.top.newGroup->group[]; 
          exit group[]
          #);
        createFragmentForm:
          (# name: ^text; frag: ^astInterface.fragmentForm; 
          enter name[]
          do
             MPS.betaCFL[]->MPS.AST.newFragmentForm->frag[]; name[]->frag.name; 
          exit frag[]
          #);
        bodyGroup: ^astInterface.fragmentGroup;
        astFileName: ^text;
        betFileName: ^text;
        bodyFileName: ^text;
        bodyAstFileName: ^text;
        bodyBetFileName: ^text;
        gram: @grammar;
        guienvLocation: (#  exit '~beta/guienv/guienvall' #);
        iconNameLocation: (#  exit '~beta/guienv/utils/iconname' #);
        getShortName:
          (# name: ^text; shortName: ^text; lastDirCharPos: @integer; 
          enter name[]
          do
             name.reset;
             MPS.AST.thePathHandler.directoryChar
               ->name.find (#  do inx->lastDirCharPos;  #);
             (if (lastDirCharPos > 0) and (lastDirCharPos < name.length) then
                 (lastDirCharPos+1,name.length)->name.sub->shortName[]; 
              else
                 name.copy->shortName[]; 
             if);
             
          exit shortName[]
          #);
        
     do
        gui.fileCreationDialog->fileName[];
        (if fileName[] <> none then
            fileName[]->stripExtension;
            '.ast'->(fileName.copy).append->astFileName[];
            '.bet'->(fileName.copy).append->betFileName[];
            'body'->(fileName.copy).append->bodyFileName[];
            '.ast'->(bodyFileName.copy).append->bodyAstFileName[];
            '.bet'->(bodyFileName.copy).append->bodyBetFileName[];
            (if (astFileName[]->entryExists) or (betFileName[]->entryExists) or
            (bodyAstFileName[]->entryExists) or
            (bodyBetFileName[]->entryExists) then
                'does already exist'->putLine; 
             else
                fileName[]->createFragmentGroup->group[];
                (if group[] <> none then
                    'ORIGIN'
                      ->group.prop.addProp
                        (#  do guienvLocation->addString;  #);
                    'INCLUDE'
                      ->group.prop.addProp
                        (#  do iconNameLocation->addString;  #);
                    'BODY'
                      ->group.prop.addProp
                        (#  do bodyFileName[]->getShortName->addString;  #);
                    group.loadIncludes;
                    fragName[]->MPS.betaCFL.newAttributesFrag->frag[];
                    frag[]->group.fragmentList.addFragment;
                    group.markAsChanged;
                    (MPS.AST[],group[],none ,betFileName[])
                      ->prettyprintFragment;
                    
                if);
                bodyFileName[]->createFragmentGroup->bodyGroup[];
                (if bodyGroup[] <> none then
                    'ORIGIN'
                      ->bodyGroup.prop.addProp
                        (#  do fileName[]->getShortName->addString;  #);
                    bodyGroup.markAsChanged;
                    (MPS.AST[],bodyGroup[],none ,bodyBetFileName[])
                      ->prettyprintFragment;
                    
                if);
                bodyBetFileName[]->ymerbrowserInt.appendProject;
                fileName[]->ymerbrowserInt.appendProject;
                ymerbrowserInt.appendDone;
                
            if);
            
        if);
        
     #)  

-- newWindowItemOnSelect: DoPart --
do
     (#
        theCreateWindow: @gui.createWindowDialog
          (#
             onCancel::  (#  do theCreateWindow.close #);
             onCreate:: 
               (# 
               do
                  (if name[]->theGraphicalEditorEnv.checkIdentifier then
                      theCreateWindow.close;
                      name[]->theGraphicalEditorEnv.createNewWindow;
                      
                   else
                      message:
                        (# msg: ^text; 
                        do
                           &text[]->msg[];
                           '"'->msg.put;
                           name[]->msg.putText;
                           '"'->msg.put;
                           ' is not a legal name.'->msg.putText;
                           msg[]->gui.displayMessage;
                           
                        #);
                      
                  if)
               #)
          #)
     do theCreateWindow.open
     #)  

-- newGraphicalEditorOnSelect: DoPart --
do
   (if browser.cfe[] <> none then
       (if browser.cfe.cs.node[] <> none then
           browser.cfe.cs.node.frag[]->frag[];
           (if frag[] <> none then
               (if frag.category = mps.betaCfl.attributesForm then
                   frag.name->fragName[];
                   (if true
                    // 'guienvLib'->fragName.equalNCS then
                       'window'->className[]; 
                    // 'windowLib'->fragName.equalNCS then
                       'canvas'->className[]; 
                   if);
                     (#
                        theCreateWindow: @gui.createWindowDialog
                          (#
                             onCancel::  (#  do theCreateWindow.close #);
                             onCreate:: 
                               (# 
                               do
                                  (if
                                  name[]->theGraphicalEditorEnv.checkIdentifier
                                   then
                                      theCreateWindow.close;
                                      (frag[],name[],className[])
                                        ->theGraphicalEditorEnv.createWindow
                                   else
                                      message:
                                        (# msg: ^text; 
                                        do
                                           &text[]->msg[];
                                           '"'->msg.put;
                                           name[]->msg.putText;
                                           '"'->msg.put;
                                           ' is not a legal name.'->msg.putText;
                                           msg[]->gui.displayMessage;
                                           
                                        #);
                                      
                                  if)
                               #);
                             
                          #)
                     do theCreateWindow.open; 
                     #)
               if);
               
           if);
           
       if);
       
   if);
     

-- friggApplicationProcessCommand: DoPart --
do
   'ProcessCommandfrigg'->screen.putline;
   (*
    cmd[]->ymerBrowserInt.appendProject;
    ymerBrowser.appendDone;
    ymerBrowser.bringToFront *)
     

-- prettyPrintGroup: DoPart --
do
     (# f: @file; 
     do
        13
          ->trace
            (#  do 'prettyprint group, name = '->xT; group.name->xT; xN #);
        (mps[],group[],none ,name[])->prettyPrintFragment;
        name[]->f.name;
        group.modTime-200->f.entry.modtime;
        
     #);
     

     

