ORIGIN '../interfacebuilder';
INCLUDE '~beta/basiclib/v1.4/betaenv';
INCLUDE '../dialogs/askingdialog';
INCLUDE '../mpsstuff/mpsutils';
INCLUDE '../code/generate';
INCLUDE '../projectbrowser/private/projectbrowserbody';
INCLUDE '../utils/path';
INCLUDE '../dialogs/promptfortextdialog';
INCLUDE '~beta/guienv/v1.3.1/utils/timer';
INCLUDE '~beta/betaast/v4.9.1/gram';
INCLUDE '../guienvstuff/getfile';
INCLUDE '../utils/catchallfile';
INCLUDE '../fragmenthandler/fragmenthandler';
INCLUDE '../buildermenu';

-- interfaceBuilderLib: attributes --

interfaceBuilderProject: project
  (# convertToFullPath::
       (# 
       do (path[], home) -> thePathHandler.convertFilePath -> fullPath[];
       #);
  #);

pathExists: booleanValue
  (# thePath: ^text;
  enter thePath[]
  do (# anEntry: @diskEntry; 
     do thePath[] -> anEntry.path;
        anEntry.exists
        (# error:: (# do true -> continue #);
        #) -> value;
        (if value then
            anEntry.isFile -> value;
        if);
     #);
  #);



getFileName:
  (# prompt: ^text;
     filename: ^text;
     values: @promptForTextValues;
  enter prompt[]
  do prompt[] -> values.prompt[];
     values[] -> doPromptForText
     (# verify::
          (# str: ^text;
          do false -> okToClose;
             values.theText[] -> path
             (# failure::
                  (# 
                  do system.beep;
                  #);
             do l:
                  (# 
                  do (if basis[] <> none then
                         (if not (basis[] -> pathExists) then
                             system.beep;
                             leave l;
                          else
                             basis.copy -> str[];
                             '/' -> str.append;
                             name[] -> str.append;
                         if);
                      else
                         name.copy -> str[];
                     if);
                     true -> okToClose;
                  #);
             #);
          #);
        accept::
          (# 
          do values.theText.copy -> fileName[];
          #);
     #);
  exit fileName[]
  #);
getIdentifier:
  (# prompt: ^text;
     identifier: ^text;
     values: @promptForTextValues;
  enter prompt[]
  do prompt[] -> values.prompt[];
     values[] -> dopromptForText
     (# verify::
          (# str: ^text;
          do false -> okToClose;
             values.theText[] -> str[];
             checking:
               (# EOS: (# exit 1 #);
                  ch: @char;
                  next:
                    (# 
                    do (if str.eos then
                           EOS -> ch;
                        else
                           str.get -> ch;
                       if);
                    #);
               do str.reset;
                  next;
                  (if ch = EOS then
                      leave checking;
                   else
                      (if ch -> ASCII.isLetter then
                          next;
                          l:
                            (if ch = EOS then
                                leave l;
                             else
                                (if (ch -> ASCII.isLetter) or (ch -> ASCII.isDigit) then
                                    next;
                                    restart l;
                                 else
                                    leave checking;
                                if);
                            if);
                       else
                          leave checking;
                      if);
                  if);
                  true -> okToClose;
               #);
             (if not okToClose then
                 system.beep;
             if);
          #);
        accept::
          (# 
          do values.theText.copy -> identifier[];
          #);
     #);
  exit identifier[]
  #);

(* getFileName:
 *   (# prompt: ^text;
 *      filename: ^text;
 *   enter prompt[]
 *   do (# theDialog: @window
 *           (# theCanvas: @promptForTextView
 *                (# open::
 *                     (# 
 *                     do theCanvas.size -> theDialog.size;
 *                        true -> bindRight -> bindBottom;
 *                     #);
 *                   accept::
 *                     (# 
 *                     do thePromptForTextValues.theText[] -> path
 *                        (# failure::
 *                             (# 
 *                             do system.beep;
 *                             #);
 *                        do l:
 *                             (# 
 *                             do (if basis[] <> none then
 *                                    (if not (basis[] -> pathExists) then
 *                                        system.beep;
 *                                        leave l;
 *                                     else
 *                                        basis.copy -> fileName[];
 *                                        '/' -> fileName.append;
 *                                        name[] -> fileName.append;
 *                                    if);
 *                                 else
 *                                    name.copy -> fileName[];
 *                                if);
 *                                theDialog.close;
 *                             #);
 *                        #);
 *                     #);
 *                   dismiss::
 *                     (# 
 *                     do theDialog.close;
 *                     #);
 *                #);
 *              open::
 *                (# 
 *                do hide;
 *                   '' -> title;
 *                   (contents, thePromptForTextValues[]) -> theCanvas.open;
 *                #);
 *           #);
 *         thePromptForTextValues: @promptForTextValues;
 *      do prompt[] -> thePromptForTextValues.prompt[];
 *         theDialog.open;
 *         theDialog.showModal;
 *      #);
 *   exit filename[]
 *   #);
 *)

theObjectStore:
  (# 
  exit private.theObjectStore[]
  #);
interactiveNewProject:
  (# path: ^text;
  do
     (* fileCreationDialog
      *      (# 
      *      do 'Project name:' -> label[];
      *      #) -> path[];
      *)
     'Project name:' -> getFileName -> path[];
     (if path[] <> none then
         path[] -> newProject;
     if);
  #);
newProject:
  (# path: ^text;
     projectFileName: ^text;
     resourceFileName: ^text;
     anEntry: @diskEntry;
  enter path[]
  do '.pro' -> path.copyAppend -> projectFileName[];
     '.res' -> path.copyAppend -> resourceFileName[];
     resourceFileName[] -> private.theResourceFile.name;
     
     (projectFileName[], thePathHandler.currentDirectory)
       -> thePathHandler.convertFilePath -> projectFileName[];
     private.theObjectStore.init;
     saveObjectStore;
     &interfaceBuilderProject[] -> currentProject[];
     currentProject.init;
     resourceFileName[] -> currentProject.resFile;
     projectFileName[] -> currentProject.useFile;
     saveCurrentProject;
     currentProject[] -> mainProjectBrowserWindow.private.theBrowser.theProject;
  #);

saveObjectStore:
  (# 
  do private.theResourceFile.openWrite;
     private.theResourceFile[] -> private.theObjectStore.write;
     private.theResourceFile.close;
  #);
saveCurrentProject:
  (# 
  do currentProject.save
     (# 
        failure::
          (# 
          do exception;
          #);
     #);
  #);

interactiveOpenProject:
  (# fileName: ^text;
     anEntry: @diskEntry;
     projectFileName: ^text;
  do 'pro' -> getFile -> fileName[];
     l: (if fileName[] <> none then
            (if fileName[] -> pathExists then
                fileName[] ->  projectFileName[];
                (if projectFileName[] -> pathExists then
                    projectFileName[] -> openProject;
                 else
                    system.beep
                if);
             else
                system.beep;
            if);
        if);
  #);
openProject:
  (# path: ^text;
     resourceFileName: ^text;
     anEntry: @diskEntry;
  enter path[]
  do opening:
       (# theProject: ^project;
          theResourceFile: @catchAllFile
            (# failure::
                 (# 
                 do leave opening;
                 #);
            #);
       do &interfaceBuilderProject[] -> theProject[];
          path[] -> theProject.readFile
          (# 
             failure::
               (# 
               do leave opening;
               #);
          #);
          theProject[] -> currentProject[];
          currentProject.resFile -> resourceFileName[];
          (resourceFileName[], currentProject.home) 
            -> thePathHandler.convertFilePath
            -> resourceFileName[];
          resourceFileName[] -> theResourceFile.name;
          private.theObjectStore.init;
          theResourceFile.openRead;
          theResourceFile[] -> private.theObjectStore.read;
          theResourceFile.close;
          currentProject[] -> mainProjectBrowserWindow.private.theBrowser.theProject;
          resourceFileName[] -> private.theResourceFile.name;
       #);
  #);

saveAll:
  (# 
  do saveObjectStore;
     MPS.top.groupTable.scan
     (# 
     do (if current.g[] <> none then
            (if current.g.changed then
                current.g[] -> saveGroup;
            if);
        if);
     #);
  #);
quit:
  (# okToQuit: @boolean;
     theAskingDialog: @askingDialog
       (# yes::
            (# 
            do true -> doSave;
               true -> okToQuit;
            #);
          no::
            (# 
            do false -> doSave;
               true -> okToQuit;
            #);
          cancel::
            (# 
            do false -> doSave;
               false -> okToQuit;
            #);
       #);
     doSave: @boolean;
  do true -> okToQuit;
     (if currentProject[] <> None then
         ('Save', 'Save changes before quiting?') -> theAskingDialog.init;
         theAskingDialog.showModal;
     if);
     (if doSave then
         saveAll;
     if);
     (if okToQuit then
         terminate;
     if);
  #);

-- InterfaceBuilderProjectBrowserWindowMenubarOpen: doPart --
do this(projectBrowserWindow).private.theFileMenu.open;
   this(projectBrowserWindow).private.theFileMenu[] -> append;
   this(projectBrowserWindow).private.theEditMenu.open;
   this(projectBrowserWindow).private.theEditMenu[] -> append;

-- InterfaceBuilderProjectBrowserWindowOpen: doPart --
do 
   private.open;
   'Interfacebuilder: project browser' -> title;

-- InterfaceBuilderProjectBrowserPrivate: descriptor --
(# 
   
   
   theTimer: @timer
     (# action:: setupButtons
     #);
   newFragmentGroup:
     (# path, fullPath: ^text;
        group: ^astInterface.fragmentGroup;
        theLink: ^astInterface.fragmentLink;
        frag: ^astInterface.fragmentForm;
     do 'Fragment group name' -> getFileName -> path[];
        (if path[] <> none then
            
            path[] -> currentProject.convertToFullpath -> fullPath[];
            fullPath[] -> MPS.top.newGroup -> group[];
            '~beta/guienv/v1.3.1/guienvall' -> group.originProperty;
            'INCLUDE' -> group.prop.addProp
            (# 
            do '~beta/interfacebuilder/v1.2/objectsstore/initfromresource' -> addString;
            #);
            MPS.newfragmentLink -> theLink[];
            '~beta/interfacebuilder/v1.2/objectsstore/initfromresource' -> theLink.name;
            'initfromresource' -> theLink.localName[];
            theLink[] -> group.fragmentList.addfragment;
            'guienvLib' -> betaGram.newAttributesFrag -> frag[];
            frag[] -> group.fragmentList.addFragment;
            group[] -> saveGroup;
            path[] -> currentProject.fragmentGroups.append;
            saveCurrentProject;
            currentProject[] -> theBrowser.theProject;
        if);
     #);
   addFragmentGroup:
     (# 
     #);
   removeFragmentGroup:
     (# 
     #);
   
   setupButtons:
     (# 
     do (* openSeparateBtn.disable; *)
        (if theBrowser.private.currentAttributeDecl[] = none then
            editGUIBtn.disable;
         else
            (if theBrowser.private.currentAttributeDecl.hasGUIcomment then
                editGUIBtn.enable;
             else
                editGUIBtn.disable
            if);
        if);
        (if theBrowser.private.theLibEditor.theFragment = none then
            (* newAttributeBtn.disable; *)
            newGUIBtn.disable;
         else
            newGUIBtn.enable;
            (* newAttributeBtn.enable; *)
        if);
     #);
   
   editGUI:
     (# theAttributeDecl: ^astInterface.beta.attributeDecl;
        theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
     enter theAttributeDecl[]
     do (# 
        do 
           theAttributeDecl[] -> theGraphicalEditorEnv.find -> theGraphicalEditor[];
           (if theGraphicalEditor[] = none then
               &theGraphicalEditorEnv.graphicalEditorWindow[] -> theGraphicalEditor[];
               theObjectStore -> theGraphicalEditor.theObjectStore[];
               theGraphicalEditor.open;
               theAttributeDecl[] -> theGraphicalEditor.init;
            else
               theGraphicalEditor.bringToFront;
           if);
        #);
     #);
   createGUI:
     (# theAttribute: ^astInterface.beta.attributeDecl;
        theGraphicalEditor: ^graphicalEditorEnv.graphicalEditor;
     enter theAttribute[]
     do 
        &theGraphicalEditorEnv.graphicalEditorWindow[] -> theGraphicalEditor[];
        theObjectStore -> theGraphicalEditor.theObjectStore[];
        theGraphicalEditor.open;
        theAttribute[] -> theGraphicalEditor.new;
     #);
   createWindow:
     (# frag: ^astInterface.fragmentForm;
     enter frag[]
     do (# theAttributesForm: ^astInterface.beta.attributesForm;
           theAttributes: ^astInterface.beta.attributes;
           theAttribute: ^astInterface.beta.attributeDecl;
           name: ^text;
        do frag.root[] -> theAttributesForm[];
           theAttributesForm.getAttributes -> theAttributes[];
           'Name:' -> getIdentifier -> name[];
           (if name[] <> none then
               (frag[], name[], 'window') -> betaGram.newPatternAttribute -> theAttribute[];
               theAttribute[] -> theAttributes.smartAppend;
               theAttribute[] -> createGUI;
               frag[] -> theBrowser.private.theLibEditor.theFragment;
           if);
        #);
     #);
   
   theFileMenu: @builderMenu
     (# newProjectItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do interactiveNewProject;
                    #);
                  onStatus::
                    (#
                    do (currentProject[] = none) -> value;
                    #);
               #);
             open::
               (# 
               do 'New project...' -> name;
               #);
          #);
        openProjectItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do interactiveOpenProject;
                    #);
                  onStatus::
                    (#
                    do (currentProject[] = none) -> value;
                    #);
               #);
             open::
               (# 
               do 'Open project...' -> name;
               #);
          #);
        closeProjectItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do ;
                    #);
                  onStatus::
                    (#
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Close project' -> name;
               #);
          #);
        
        saveItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do (* nothing *)
                    #);
                  onStatus::
                    (#
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Save' -> name;
               #);
          #);
        saveAllItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do saveAll;
                    #);
                  onStatus::
                    (#
                    do (currentProject[] <> none) -> value;
                    #);
               #);
             open::
               (# 
               do 'Save all' -> name;
               #);
          #);
        newGroupItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do newFragmentGroup;
                    #);
                  onStatus::
                    (#
                    do (currentProject[] <> none) -> value;
                    #);
               #);
             open::
               (# 
               do 'New fragment group...' -> name;
               #);
          #);
        addGroupItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do addFragmentGroup;
                    #);
                  onStatus::
                    (#
                    do (* (currentProject[] <> none) -> value; *)
                       false -> value;
                    #);
               #);
             open::
               (# 
               do 'Add fragment group' -> name;
               #);
          #);
        removeGroupItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do removeFragmentGroup;
                    #);
                  onStatus::
                    (#
                    do (currentProject[] <> none) -> value;
                       false -> value;
                    #);
               #);
             open::
               (# 
               do 'Remove fragment group' -> name;
                  
               #);
          #);
        quitItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    do quit;
                    #);
                  onStatus::
                    (#
                    do true -> value;
                    #);
               #);
             open::
               (# 
               do 'Quit' -> name;
               #);
          #);
        open::
          (# 
          do 
             'File' -> name;
             newProjectItem.open;
             openProjectItem.open;
             closeProjectItem.open;
             saveAllItem.open;
             addSeparator;
             newGroupItem.open;
             addGroupItem.open;
             removeGroupItem.open;
             saveItem.open;
             addSeparator;
             quitItem.open;
          #);
     #);
   
   theEditMenu: @builderMenu
     (# undoItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    #);
                  onStatus::
                    (# 
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Undo' -> name;
                  'Z' -> key;
               #);
          #);
        redoItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    #);
                  onStatus::
                    (# 
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Redo' -> name;
                  'R' -> key;
               #);
          #);
        cutItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    #);
                  onStatus::
                    (# 
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Cut' -> name;
                  'X' -> key;
               #);
          #);
        copyItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    #);
                  onStatus::
                    (# 
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Copy' -> name;
                  'C' -> key;
               #);
          #);
        pasteItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    #);
                  onStatus::
                    (# 
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Paste' -> name;
                  'V' -> key;
               #);
          #);
        deleteItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    #);
                  onStatus::
                    (# 
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Delete' -> name;
               #);
          #);
        findItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    #);
                  onStatus::
                    (# 
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Find...' -> name;
               #);
          #);
        findAgainItem: @builderItem
          (# eventHandler::
               (# onSelect::
                    (# 
                    #);
                  onStatus::
                    (# 
                    do false -> value;
                    #);
               #);
             open::
               (# 
               do 'Find again' -> name;
               #);
          #);
        open::
          (# 
          do 'Edit' -> name;
             undoItem.open;
             redoItem.open;
             addSeparator;
             cutItem.open;
             copyItem.open;
             pasteItem.open;
             deleteItem.open;
             addSeparator;
             findItem.open;
             findAgainItem.open;
          #);
     #);
   
   open:
     (# width, height: @integer;
     do (contents, none) -> theBrowser.open;
        theBrowser.size -> (width, height);
        (2, 2) -> theBrowser.position;
        (width + 4, height + 2 + 26) -> size;
        true -> theBrowser.bindBottom;
        true -> theBrowser.bindRight;
        (* (contents, 5, height + 6) -> newAttributeBtn.open; *)
        (contents, 10, height + 6) -> editGUIBtn.open;
        (contents, 120, height + 6) -> newGUIBtn.open;
        (* (contents, 305, height + 6) -> openSeparateBtn.open; *)
        false -> newAttributeBtn.bindTop;
        true -> newAttributeBtn.bindBottom;
        false -> editGUIBtn.bindTop;
        true -> editGUIBtn.bindBottom;
        false -> newGUIBtn.bindTop;
        true -> newGUIBtn.bindBottom;
        false -> openSeparateBtn.bindTop;
        true -> openSeparateBtn.bindBottom;
        16 -> theTimer.activate;
     #);
   theBrowser: @projectBrowser
     (# attributeChanged::
          (# 
          do (if doubleClick then
                 (if private.currentAttributeDecl[] <> none then
                     private.currentAttributeDecl[] -> editGUI;
                 if);
             if);
          #);
     #);
   
   
   newAttributeBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (# 
               do (if theBrowser.private.theLibEditor.theFragment <> None then
                      theBrowser.private.theLibEditor.interactiveCreateAttribute;
                  if);
               #);
          #);
        open::
          (# x, y: @integer;
          enter (x, y)
          do ((x, y), (x + 90, y + 18)) -> frame;
             'New attribute' -> label;
             disable
          #);
     #);
   editGUIBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (# 
               do (if theBrowser.private.currentAttributeDecl[] <> none then
                      theBrowser.private.currentAttributeDecl[] -> editGUI;
                  if);
               #);
          #);
        open::
          (# x, y: @integer;
          enter (x, y)
          do ((x, y), (x + 100, y + 18)) -> frame;
             'Edit window' -> label;
             disable;
          #);
     #);
   newGUIBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (# frag: ^astInterface.fragmentForm;
                  gram: @grammar;
               do theBrowser.theFragment -> frag[];
                  (if frag[] <> None then
                      (if frag.category = gram.attributesForm then
                          frag[] -> createWindow;
                      if);
                  if);
               #);
          #);
        open::
          (# x, y: @integer;
          enter (x, y)
          do ((x, y), (x + 100, y + 18)) -> frame;
             'New window...' -> label;
             disable
          #);
     #);
   
   openSeparateBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (# 
               do
               #);
          #);
        open::
          (# x, y: @integer;
          enter (x, y)
          do ((x, y), (x + 90, y + 18)) -> frame;
             'Open separate' -> label;
             disable
          #);
     #);
   
#)

-- graphicalEditorWindowLib: attributes --
interactiveClose:
  (# 
  do (if theEventHandler.onAboutToClose then
         this(graphicalEditorWindow).close;
     if);
  #);


-- InterfaceBuilderGraphicalEditorWindowOpen: doPart --
do inner;

-- InterfaceBuilderGraphicalEditorPrivate: descriptor --
(# 

#)

-- InterfaceBuilderPrivate: descriptor --
(# theFragServer: @fragmentServer;
   theResourceFile: @file;
   theObjectStore: @objectDataStore;
#)

-- InterfaceBuilderDoPart: doPart --
do private.theFragServer.init;
   private.theFragServer[] -> theGraphicalEditorEnv.init;
   mainProjectBrowserWindow.open;
   (if noOfArguments = 2 then
       2 -> arguments -> path
       (# 
          fileName: ^text;
       do (if basis[] = none then
              name.copy -> fileName[]
           else
              basis.copy -> fileName[];
              '/' -> fileName.append;
              name[] -> fileName.append;
          if);
          '.pro' -> fileName.append;
          (if fileName[] -> pathExists then
              fileName[] -> openProject;
          if);
       #);
   if);
-- theGraphicalEditorEnvSave: doPart --
do saveAll;
