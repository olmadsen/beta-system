ORIGIN '../interfacebuilder';
INCLUDE '~beta/basiclib/v1.4/directory';
INCLUDE '../graphicaleditor/graphicaleditor';
INCLUDE '../fragmenteditor/fragmenteditor';
INCLUDE '../asteditor/asteditor';
INCLUDE '../code/generate';
INCLUDE 'guiview';
MDBODY default 'X11/interfacebuilderXbody';

-- interfaceBuilderLib: attributes --
interfaceBuilderFileMenu: menu
  (# 
     saveItem: @menuItem
       (# eventHandler::<
            (# onSelect::<
                 (# 
                 do 
                 #);
            #);
          open::<
            (# 
            do 'Save' -> name;
            #);
       #);
     quitItem: @menuitem
       (# 
          eventHandler::<
            (# onSelect::<
                 (# 
                 do terminate;
                 #);
            #);
          open::<
            (# 
            do 'Quit' -> name;
            #);
       #);
     open::<
       (# 
       do 'File' -> name;
          saveItem.open;
          saveItem[] -> append;
          quitItem.open;
          quitItem[] -> append;
       #);
  #);
fragmentEditor: fragmentEditorWindow
  (# 
     openFragment::<
       (# theAstEditor: ^astEditor;
          theGuiView: ^guiView;
       do (if theFragment[]//none then
              'Fragment is none' -> putLine;
           else
              (if theFragment.category//betaGram.attributesForm then
                  &guiView[] -> theGuiView[];
                  theFragment[] -> theGuiView.init;
               else
                  &astEditor[] -> theAstEditor[];              
                  theFragment.root[] -> theAstEditor.init;
              if);
          if);
       #);
  #);

loadProjectInfo:
  (# fileName: ^text;
     thisDir: @Directory;
     dummyFile: @file;
     projectName: ^text;
  do 
     thePathHandler.workingDirectory -> thisDir.name;
     l: thisDir.scanEntries
     (# 
     do 
        (if '.pro' -> (found.path.name.suffix).equal then
            found.path.name -> fileName[];
        if);
     #);
     (if fileName[]//none then
         thisDir.entry.path.name.prefix -> projectName[];
         '.pro' -> projectName.copyAppend -> fileName[];
         'guifile.gui' -> theProjectInfo.GUIfileName;
         thePathHandler.workingDirectory -> theProjectInfo.home;
         fileName[] -> theProjectInfo.fileName[];
         theProjectInfo.save;
      else
         fileName[] -> theProjectInfo.fileName[];
         theProjectInfo.load;
         
     if);
  #);
loadObjectStore:
  (# theFile: @file
  do (* theProjectInfo.theObjectStore.init; *)
     (* 'guifile.gui' -> theFile.name; *)
     (* theFile.openRead; *)
     (* theFile[] -> theProjectInfo.theObjectStore.read; *)
     (* theFile.close; *)
     
  #);

catchAllFile: file
  (# 
     failure:<
       (# msg: ^text;
       enter msg[]
       do inner;
       #);
     AccessError::<
       (# 
       do msg[] -> failure;
       #);
     WriteError::<
       (# 
       do msg[] -> failure;
       #);
     ReadError::<
       (# 
       do msg[] -> failure;
       #);
     EOSerror::<
       (# 
       do msg[] -> failure;
       #);
     NoSuchFileError::<
       (# 
       do msg[] -> failure;
       #);
     FileExistsError::<
       (# 
       do msg[] -> failure;
       #);
     NoSpaceError::<
       (# 
       do msg[] -> failure;
       #);
     OtherError::<
       (# 
       do msg[] -> failure;
       #);
  #);

-- interfaceBuilderDoPart: doPart --
do (# theFile: @file;
   do theProjectInfo.init;
      loadProjectInfo;
      'guifile.gui' -> theFile.name;
      theFile.openRead;
      theObjectStore.init;
      theFile[] -> theObjectStore.read;
      theProjectInfo.showEditor;
   #);
-- editorEnvtheProjectInfofragmentGroupAdded: doPart --
do save;

-- editorEnvtheProjectInfofragmentGroupDeleted: doPart --
do save;

-- editorEnvtheProjectInfoopenFragmentGroup: doPart --
do (# theGroup: ^MPS.fragmentGroup;
      theFragmentEditor: ^fragmentEditor;
   do name[] -> MPS.safeOpen -> theGroup[];
      (if theGroup[]<>none then
          &fragmentEditor[] -> theFragmentEditor[];
          theGroup[] -> theFragmentEditor.init;
      if);
   #);

-- theProjectInfoLib: attributes --

save:
  (# 
  do saving:
       (# theFile: @catchAllFile
            (# 
               failure::<
                 (# 
                 do 'error in saving projectfile. ' -> msg.prepend;
                    msg[] -> screen.putLine;
                    leave saving;
                 #);
            #);
       do fileName[] -> theFile.name;
          theFile.openWrite;
          theFile[] -> write;
          theFile.close;
       #);
  #);

load:
  (# 
  do loading:
       (# theFile: @catchAllFile
            (# 
               failure::<
                 (# 
                 do 'error in loading projectfile. ' -> msg.prepend;
                    msg[] -> screen.putLine;
                    leave loading;
                 #);
            #);
       do fileName[] -> theFile.name;
          theFile.openRead;
          theFile[] -> read;
          theFile.close;
       #);
  #);

(* -- interfaceBuilderMenubarTypeOpen: doPart --
do (# theFileMenu: ^menu;
   do &interfaceBuilderFileMenu[] -> theFileMenu[];
      theFileMenu.open;
      theFileMenu[] -> append;
   #);
*)
