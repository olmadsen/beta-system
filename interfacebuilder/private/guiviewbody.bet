ORIGIN 'guiview';
INCLUDE '~beta/guienv/v1.2/scrolllists';
INCLUDE '~beta/guienv/v1.2/controls';
INCLUDE '~beta/guienv/v1.2/utils/listview';
INCLUDE '../code/generate';
INCLUDE '../graphicaleditor/graphicaleditor';
INCLUDE '../patterneditor/libeditor';
INCLUDE '../mpsstuff/mpsutils';

-- guiViewLib: attributes --

editGUI:
  (# theAttributeDecl: ^astInterface.beta.attributeDecl;
     theGraphicalEditor: ^graphicalEditor;
  enter theAttributeDecl[]
  do &graphicalEditor[] -> theGraphicalEditor[];
     theObjectStore[] -> theGraphicalEditor.theObjectStore[];
     theGraphicalEditor.open;
     theAttributeDecl[] -> theGraphicalEditor.init;
  #);
createGUI:
  (# theAttribute: ^astInterface.beta.attributeDecl;
     theGraphicalEditor: ^graphicalEditor;
  enter theAttribute[]
  do 
     &graphicalEditor[] -> theGraphicalEditor[];
     theObjectStore[] -> theGraphicalEditor.theObjectStore[];
     theGraphicalEditor.open;
     theAttribute[] -> theGraphicalEditor.new;
     theAttribute[] -> private.theLibEditor.attributeUpdated;
  #);
createAttribute:
  (# 
  do private.theLibEditor.interactiveCreateAttribute;
  #);

-- guiViewInit: doPart --
do open;
   inner;

-- guiViewOpen: doPart --
do (# name: ^text;
   do (300, 200) -> size;
      'Fragment: ' -> name[];
      frag.name -> name.append;
      name[] -> title;
      (contents, frag[]) -> private.theLibEditor.open;
      contents -> private.createBtn.open;
      contents -> private.editBtn.open;
      contents -> private.newBtn.open;
      private.setupButtons;
      inner;
   #);
   
-- guiViewPrivate: descriptor --
(# selected: ^astInterface.beta.attributeDecl;
   
   setupButtons:
     (# 
     do (if selected[]=none then
            createBtn.disable;
            editBtn.disable;
         else
            (if selected.hasGUIcomment then
                editBtn.enable;
                createBtn.disable;
             else
                editBtn.disable;
                createBtn.enable;
            if);
        if);
     #);
   
   theLibEditor: @libEditor
     (# 
        open::
          (#
          do 
             ((2, 2), (298, 174)) -> frame;
             true -> bindRight -> bindBottom;
          #);
        attributeSelected::
          (# 
          do theAttribute[] -> selected[];
             setupButtons;
          #);
        reportParseErrors::
          (# 
          do errors[] -> showParseErrors;
          #);
     #);
   
   createBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (#
               do (if selected[] <> None then
                      selected[] -> createGUI;
                      setupButtons;
                  if);
               #);
          #);
        open::
          (# 
          do 'Create GUI' -> label;
             ((4, 178), (74, 196)) -> frame;
             false -> bindTop;
             true -> bindBottom;
          #);
     #);
   editBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (#
               do (if selected[] <> None then
                      selected[] -> editGUI;
                  if)
               #);
          #);
        open::
          (# 
          do 'Edit GUI' -> label;
             ((82, 178), (152, 196)) -> frame;
             false -> bindTop;
             true -> bindBottom;
          #);
     #);
   newBtn: @pushButton
     (# eventHandler::
          (# onMouseUp::
               (#
               do createAttribute;
               #);
          #);
        open::
          (# 
          do 'New Attribute' -> label;
             ((164, 178), (254, 196)) -> frame;
             false -> bindTop;
             true -> bindBottom;
          #);
     #);
#)


