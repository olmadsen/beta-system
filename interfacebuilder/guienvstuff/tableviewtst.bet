ORIGIN '~beta/guienv/v1.2/guienv';
INCLUDE '~beta/interfacebuilder/v1.0/guienvstuff/tableview';
INCLUDE '~beta/guienv/v1.2/fields';
INCLUDE '~beta/basiclib/v1.4/directory';

-- windowLib: attributes --

directoryTableView: tableView
  (# fileIcon, dirIcon: @bitmap;
     directoryTableItem: tableItem
       (# onSelect::<
            (# 
            do name -> putLine;
            #);
       #);
     addItem:
       (# name: ^text;
          icon: ^raster;
          theItem: ^tableItem;
       enter (name[], icon[])
       do &directoryTableItem[] -> theItem[];
          theItem.init;
          name[] -> theItem.name;
          icon[] -> theItem.icon;
       #);
     open::
       (# currentDir: @directory;
       do 'file.icon' -> fileIcon.readFromX11File;
          'dir.icon' -> dirIcon.readFromX11File;
          './' -> currentDir.name;
          currentDir.scanEntries
          (# 
          do (if true 
              //found.isFile then
                 (found.path.name, fileIcon[]) -> addItem;
              //found.isDirectory then
                 (found.path.name, dirIcon[]) -> addItem;
              else
                 (found.path.name, none) -> addItem;
             if);
          #);
          l: scan
            (# 
            do current.select;
               leave l;
            #);
       #);
  #);
-- program: descriptor --
GUIenv
(# 
   theWindow: @window
     (# theScroller: @scroller
          (# contentsType:: directoryTableView;
             open::<
               (# 
               do this(window).size -> size;
                  true -> bindRight -> bindBottom;
                  contents.fitToContents;
               #);
          #);
        open::
          (#
          do 'Browser' -> title;
             theScroller.open;
          #)
     #);
do 
   theWindow.open;
#)
