ORIGIN '~beta/guienv/v1.4/guienv';
BODY 'private/mywalkingantsbody';

-- GUIenvLib: attributes --

oneWay: (# exit 1 #);
twoWay: (# exit 2 #);

walkingAnts:
  (* WalkingsAnts provides a way to highlight an area inside a windowItem.
   * 
   * The walking ants are an animated stippled rectangle.
   * 
   * Interval is the pause in microseconds between each step.
   * 
   * More than one area can be animated at once.
   *  
   *)
  (# <<SLOT walkingAntsLib: attributes>>;
     
     (* Interval is the pause in microseconds 
      * between each step.
      *)
     interval: @integer;
     
     (* Style is oneWay or twoWay. 
      *   oneWay: the ants walks clockwise around the edge 
      *           of the area.
      *   twoWay: the ants alternates between clock 
      *           and counterclock wise.
      *)
     style: @integer;
     
     init:<
       (# <<SLOT GUIenvWalkingAntsInit: doPart>> #);
     
     add:
       (* Add an area. *)
       (# theRectangle: ^rectangle;
          theWindowItem: ^window.windowItem;
       enter (theWindowItem[], theRectangle[])
       <<SLOT walkingAntsAdd: doPart>>
       #);
     delete:
       (* Delete an area. *)
       (# theRectangle: ^rectangle;
          theWindowItem: ^window.windowItem;
       enter (theWindowItem[], theRectangle[])
       <<SLOT walkingAntsDelete: doPart>>
       #);
     clear:
       (* Delete all areas *)
       (# <<SLOT walkingAntsClear: doPart>> #);
     
     start:
       (* Start the animation *)
       (#
       <<SLOT GUIenvWalkingAntsStart: doPart>>
       #);
     stop:
       (* Stop the animation *)
       (# <<SLOT GUIenvWalkingAntsStop: doPart>> #);
     
     private: @<<SLOT GUIenvWalkingAntsPrivate: descriptor>>;
  #);
