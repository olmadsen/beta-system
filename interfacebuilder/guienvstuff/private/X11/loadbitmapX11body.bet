ORIGIN '../loadbitmapbody';
INCLUDE '~beta/unixlib/v1.4/unixinterface';
INCLUDE '~beta/sysutils/v1.4.2/cstring';
INCLUDE '~beta/basiclib/v1.4/file';
-- lib: attributes --

pathExists:
  (# value: @boolean;
     path: ^text;
     theEntry: @diskEntry;
  enter path[]
  do path[] -> theEntry.path;
     l: theEntry.exists
     (# 
        error::
          (#
          do leave l;
          #);
     #) -> value;
  exit value
  #);
-- GUIENVloadbitmap: doPart --
do (# betaLibEnv: @cString;
      path: ^text;
      fileName: ^text;
      theBitMap: ^bitMap;
   do 'BETALIB' -> getenv -> betaLibEnv;
      (if betaLibEnv = 0 then
          '/usr/local/lib/beta' -> path[];
       else
          betaLibEnv.get -> path[];
      if);
          
      '/bitmap/interfacebuilders/' -> path.append;
      path[] -> name.copyPrepend -> fileName[];
      '.xbm' -> fileName.append;
      (if fileName[] -> pathExists then
          &bitmap[] -> theBitMap[] -> theRaster[];
          fileName[] -> theBitMap.readFromX11File;
       else
          exception
          (# 
          do 'Not able to load ' -> msg.append;
             filename[] -> msg.append;
          #);
      if);
   #)
