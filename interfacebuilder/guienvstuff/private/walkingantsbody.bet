ORIGIN '../walkingants';
INCLUDE '../timer';
INCLUDE '~beta/guienv/v1.3/graphics';
INCLUDE '~beta/guienv/v1.3/utils/graphicsadds';
-- GUIenvLib: attributes --

loadRaster:
  (# name: ^text;
     theRaster: ^bitmap;
  enter name[]
  do '/users/beta/bitmap/interfacebuilder/' -> name.copyPrepend -> name[];
     &bitmap[] -> theRaster[];
     name[] -> theRaster.readFromX11File;
  exit theRaster[]
  #);
-- GUIenvWalkingAntsStart: doPart --
do 1 -> private.current;
   private.drawCurrent;
   (if interval = 0 then
       100 -> interval
   if);
   (if theCanvas[] = None then
       exception
       (# 
       do 'The Canvas is None in start of walkingants' -> msg.append;
       #);
   if);
   interval -> private.onTimer.activate;

-- GUIenvWalkingAntsStop: doPart --
do private.onTimer.pause;
   true -> theCanvas.update;

-- GUIenvWalkingAntsInit: doPart --
do 'stripes1.x11' -> loadRaster -> private.stripes[1][];
   'stripes2.x11' -> loadRaster -> private.stripes[2][];
   'stripes3.x11' -> loadRaster -> private.stripes[3][];
   'stripes4.x11' -> loadRaster -> private.stripes[4][];
   'stripes5.x11' -> loadRaster -> private.stripes[5][];
   'stripes6.x11' -> loadRaster -> private.stripes[6][];
   'stripes7.x11' -> loadRaster -> private.stripes[7][];
   'stripes8.x11' -> loadRaster -> private.stripes[8][];
   oneWay -> style;
   inner;

-- GUIenvWalkingAntsPrivate: descriptor --
(# stripes: [8] ^raster;
   current: @integer;
   step: @integer;
   
   drawCurrent: @
     (# 
     do theCanvas.graphics
        (# width,height: @integer;
        do stripes[current][] ->  pen.stipple;
           (* false -> clipChildren; *)
           theRectangle -> drawRect;
        #);
     #);
   
   onTimer: @timer
     (# action::
          (# 
          do (if style
              //oneWay then
                 (if current = 8 then
                     1 -> current;
                  else
                     1 + current -> current;
                 if);
              else
                 (if current 
                  //4 then
                     -1 -> step;
                  //1 then
                     1 -> step;
                 if);
                 current + step -> current;
             if);
             drawCurrent;
          #);
     #);
   
#)

