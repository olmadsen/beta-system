ORIGIN '../tableview';
INCLUDE '~beta/guienv/v1.2/graphics';
INCLUDE '../defaultstyle';

-- GUIENVtableViewTableItemEnterName: doPart --
do value[] -> private.name[];
   update;

-- GUIENVtableViewTableItemExitName: doPart --
do private.name[] -> value[];

-- GUIENVtableViewTableItemEnterIcon: doPart --
do value[] -> private.icon[];
   this(tableView).private.computeItemHeight;
   update;

-- GUIENVtableViewTableItemExitIcon: doPart --
do private.icon[] -> value[];

-- GUIENVTableViewTableItemPosition: doPart --
do private.position -> value;

-- GUIENVtableViewTableItemInit: doPart --
do (this(tableItem)[], beforeItem[]) -> this(tableView).private.insert;
   inner;
   update;

-- GUIENVtableViewTableItemDelete: doPart --
do this(tableItem)[] -> this(tableView).private.delete;
   inner;
   update;

-- GUIENVtableViewTableItemSelect: doPart --
do true -> selected;

-- GUIENVtableViewTableItemDeSelect: doPart --
do false -> selected;

-- GUIENVtableViewTableItemPrivate: descriptor --
(# name: ^text;
   icon: ^raster;
   position: @integer;
   selected: @boolean;
#)

-- GUIENVtableViewEnterStyle: doPart --
do value[] -> private.theStyle[];
   private.computeItemHeight;
   update;

-- GUIENVtableViewExitStyle: doPart --
do private.theStyle[] -> value[];

-- GUIENVtableViewItemHeight: doPart --
do private.itemHeight -> value;
   
-- GUIENVtableViewFitToContents: doPart --
do private.preferredSize -> size;

-- GUIENVtableViewScan: doPart --
do private.items.iterate
   (# 
   do current.elm[] -> this(scan).current[];
      inner scan;
   #);

-- GUIENVtableViewOpen: doPart --
do private.items.init;
   defaultStyle -> private.theStyle[];
   private.computeItemHeight;
   inner;

-- GUIENVtableViewClose: doPart --
do inner;
   private.items.clear;
   none -> private.theStyle[];
   0 -> private.itemHeight;

-- GUIENVtableViewOnRefresh: doPart --
do private.refresh;
   inner;


-- GUIENVtableViewOnMouseDown: doPart --
do (# theItem: ^tableItem;
   do localPosition -> private.findTableItem -> theItem[];
      (if theItem[] <> None then
          (if not theItem.selected then
              scan
              (# 
              do current.deSelect;
              #);
              theItem.select;
          if);
      if);
   #);
   inner;

-- GUIENVtableViewOnMouseUp: doPart --
do inner;

-- GUIENVtableViewOnKeyDown: doPart --
do inner;

-- GUIENVtableViewPrivate: descriptor --
(# borderSpacing: (# exit 3 #);
   iconSpacing: (# exit 4 #);
   spacing: (# exit 2 #);
   margin:
     (# value: @integer;
     do (if border.visible then
            2 + borderSpacing -> value;
         else
            borderSpacing -> value;
        if);
     exit value
     #);
   
   theStyle: ^textStyle;
   itemHeight: @integer;
   items: @list
     (# element:: tableItem;
     #);
   maxIconSize:
     (# width, height: @integer;
     do scan
        (# icon: ^raster;
        do current.icon -> icon[];
           (if icon[] <> None then
               (icon.width, width) -> Max -> width;
               (icon.height, height) -> Max -> height;
           if);
        #);
     exit (width, height)
     #);
   computeItemHeight:
     (# width, height: @integer;
     do maxIconSize -> (width, height);
        (height + 2 * spacing, theStyle.lineHeight) -> Max -> itemHeight;
     #);
   insert:
     (# item, before: ^tableItem;
     enter (item[], before[])
     do (if before[] = None then
            item[] -> items.append;
         else
            (item[], before[] -> items.at) -> items.insertBefore;
        if);
     #);
   delete:
     (# item: ^tableItem;
     enter item[]
     do item[] -> items.at -> items.delete;
     #);
   
   refresh:
     (# width, ignoreheight: @integer;
        textWidth: @integer;
        icon: ^raster;
        black: (# exit (0, 0, 0) #);
        white: (# exit (maxInt, maxInt, maxInt) #);
     do 
        maxIconSize -> (width, ignoreheight);
        size -> (textWidth, ignoreheight);
        textWidth - 2*margin - width - iconSpacing -> textWidth;
        graphics
        (# tx,ty: @integer;
           ix, iy: @integer;
        do theStyle[] -> style;
           margin + width + iconSpacing -> tx;
           (itemHeight - theStyle.lineHeight) div 2 + theStyle.ascent + margin -> ty;
           margin -> ix;
           margin -> iy;
           scan
           (# ih, iw: @integer;
           do current.icon -> icon[];
              (if icon[] <> None then
                  icon.height -> ih;
                  icon.width -> iw;
                  (icon[], (0, 0), (ix, iy + (itemHeight - ih) div 2), iw, ih) -> drawRaster;
              if);
              (if current.selected then
                  ((tx, iy), (tx + textWidth, iy + itemHeight)) -> fillRect;
                  (tx, ty) -> moveTo;
                  white -> pen.foreGroundColor;
                  current.name -> drawText;
                  black -> pen.foreGroundColor;
               else
                  (tx, ty) -> moveTo;
                  current.name -> drawText;
              if);
              ty + itemHeight -> ty;
              iy + itemHeight -> iy;
           #);
        #);
     #);
   preferredSize:
     (# width, height: @integer;
        maxTextWidth: @integer;
        iw, ih: @integer;
     do items.size * itemHeight + 2 * margin -> height;
        scan
        (# tw: @integer;
        do current.name -> theStyle.widthOfText -> tw;
           (tw, maxTextWidth) -> Max -> maxTextWidth;
        #);
        maxIconSize -> (iw, ih);
        2*margin + iw + iconSpacing + maxTextWidth -> width;
     exit (width, height)
     #);
   findTableItem:
     (# x, y: @integer;
        inx: @integer;
        theItem: ^tableItem;
        position: @integer;
     enter (x, y)
     do ((y - margin) div itemHeight) + 1 -> position;
        l: scan
          (# 
          do inx + 1 -> inx;
             (if inx = position then
                 current[] -> theItem[];
                 leave l;
             if);
          #);
     exit theItem[]
     #);
#)

-- GUIENVtableViewTableItemEnterSelected: doPart --
do (if value <> private.selected then
       value -> private.selected;
       true -> update;
       (if private.selected then
           onSelect;
       if);
   if);

-- GUIENVtableViewTableItemExitSelected: doPart --
do private.selected -> value;

