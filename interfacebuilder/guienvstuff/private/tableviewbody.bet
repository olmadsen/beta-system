ORIGIN '../tableview';
INCLUDE '~beta/guienv/v1.3/graphics';
INCLUDE '~beta/guienv/v1.3/fields';
INCLUDE '~beta/guienv/v1.3/utils/graphicsadds';
INCLUDE '../defaultstyle';
INCLUDE '../movabletextfield';
INCLUDE '../colors';

-- GUIENVtableViewTableItemEnterName: doPart --
do value[] -> private.name[];
   this(tableView).private.updateTable;

-- GUIENVtableViewTableItemExitName: doPart --
do private.name[] -> value[];

-- GUIENVtableViewTableItemEnterIcon: doPart --
do value[] -> private.icon[];
   (if not this(tableView).private.updateDisabled then
       this(tableView).private.computeItemHeight;
   if);
   this(tableView).private.updateTable;

-- GUIENVtableViewTableItemExitIcon: doPart --
do private.icon[] -> value[];

-- GUIENVTableViewTableItemPosition: doPart --
do private.position -> value;

-- GUIENVtableViewTableItemInit: doPart --
do (this(tableItem)[], beforeItem[]) -> this(tableView).private.insert;
   inner;
   this(tableView).private.updateTable;

-- GUIENVtableViewTableItemDelete: doPart --
do this(tableItem)[] -> this(tableView).private.delete;
   inner;
   this(tableView).private.updateTable;

-- GUIENVtableViewTableItemSelect: doPart --
do true -> selected;

-- GUIENVtableViewTableItemDeSelect: doPart --
do false -> selected;
   
-- GUIENVtableViewTableItemActivateEdit: doPart --
do (# x, y: @integer;
   do deactivateEdit;
      position 
        -> this(tableView).private.computeItemPosition -> (x, y);
      (name, style, (x, y)) 
        -> this(tableView).private.editFld.activate;
      this(tableItem)[] -> this(tableView).private.currentEdit[];
   #);
   
-- GUIENVtableViewDeactivateEdit: doPart --
do (if private.currentEdit[] <> none then
       private.editFld.deactivate;
   if);
   
   
-- GUIENVtableViewDisableUpdate: doPart --
do true -> private.updateDisabled;
   
-- GUIENVtableViewEnableUpdate: doPart --
do (if private.updateDisabled then
       false -> private.updateDisabled;
       private.computeItemHeight;
       update;
   if);

-- GUIENVtableViewTableItemPrivate: descriptor --
(# name: ^text;
   icon: ^raster;
   position: @integer;
   selected: @boolean;
   drawOne: this(tableView).private.tableGraphics
     (# x, y: @integer;
     do this(tableView).private.margin -> x;
        (this(tableView).private.margin
        + this(tableView).private.itemHeight * (position - 1))
          -> y;
        (this(tableItem)[], x, y) -> drawItem;
     #);
#)

-- GUIENVtableViewEnterStyle: doPart --
do value[] -> private.theStyle[];
   private.computeItemHeight;
   this(tableView).private.updateTable;

-- GUIENVtableViewExitStyle: doPart --
do private.theStyle[] -> value[];

-- GUIENVtableViewItemHeight: doPart --
do private.itemHeight -> value;
   
-- GUIENVtableViewFitToContents: doPart --
do private.preferredSize -> size;

-- GUIENVtableViewScan: doPart --
do private.items.iterate
   (# 
   do current.elm[] -> this(scan).current[];
      inner scan;
   #);

-- GUIENVtableViewOpen: doPart --
do private.items.init;
   defaultStyle -> private.theStyle[];
   private.computeItemHeight;
   private.editFld.init;
   inner;

-- GUIENVtableViewClose: doPart --
do inner;
   private.items.clear;
   none -> private.theStyle[];
   0 -> private.itemHeight;

-- GUIENVtableViewOnRefresh: doPart --
do private.refresh;
   inner;


-- GUIENVtableViewOnMouseDown: doPart --
do (# theItem: ^tableItem;
   do deactivateEdit;
      localPosition -> private.findTableItem -> theItem[];
      (if theItem[] <> None then
          (if not theItem.selected then
              scan
              (# 
              do current.deSelect;
              #);
              theItem.select;
          if);
          this(onMouseDown)[] -> theItem.onMouseDown;
       else
          scan
          (# 
          do current.deSelect;
          #);
      if);
   #);
   inner;

-- GUIENVtableViewOnMouseUp: doPart --
do scan
   (# 
   do (if current.selected then 
          this(onMouseUp)[] -> current.onMouseUp;
          (if metaKey then
              current.activateEdit;
          if);
      if);
   #);
   inner;

-- GUIENVtableViewOnKeyDown: doPart --
do ch -> private.speedSearch;
   inner;

-- GUIENVtableViewPrivate: descriptor --
(# theStyle: ^textStyle;
   itemHeight: @integer;
   iconWidth: @integer;
   updateDisabled: @boolean;
   
   borderSpacing: (# exit 3 #);
   iconSpacing: (# exit 4 #);
   spacing: (# exit 2 #);
   margin:
     (# value: @integer;
     do (if border.visible then
            2 + borderSpacing -> value;
         else
            borderSpacing -> value;
        if);
     exit value
     #);
   
   tableGraphics: graphics
     (# drawItem:
          (# theItem: ^tableItem;
             x, y: @integer;
          enter (theItem[], x, y)
          do (# tx, ty: @integer;
                tw: @integer;
                ix, iy: @integer;
                ih, iw: @integer;
                
                icon: ^raster;
             do x + iconWidth + iconSpacing -> tx;
                (y 
                + (itemHeight - th) div 2 
                + theStyle.ascent)
                  -> ty;
                
                theItem.icon -> icon[];
                (if icon[] <> none then
                    icon.height -> ih;
                    icon.width -> iw;
                    x -> ix;
                    y + (itemHeight - ih) div 2 -> iy;
                    (icon[], (0, 0), (ix, iy), iw, ih) -> drawRaster;
                if);
                theItem.name -> theStyle.widthOfText -> tw;
                (if theItem.selected then
                    ((tx, y), (tx + tw, y + itemHeight)) -> fillRect;
                    (tx, ty) -> moveTo;
                    theBGcolor -> pen.foreGroundColor;
                    theItem.name -> drawText;
                    black -> pen.foreGroundColor;
                 else
                    theBGcolor -> pen.foreGroundColor;
                    ((tx, y), (tx + tw, y + itemHeight)) -> fillRect;
                    black -> pen.foreGroundColor;
                    (tx, ty) -> moveTo;
                    theItem.name -> drawText;
                if);
             #);
          #);
        black: (# exit (0, 0, 0) #);
        white: (# exit (maxInt, maxInt, maxInt) #);
        theBGcolor: ^color;

        th: @integer;
     do theStyle[] -> style;
        theStyle.lineHeight -> th;
        backgroundColor -> theBGcolor[];
        (if border.visible then
            (# width, height: @integer;
            do size -> (width, height);
               (if (width >= 4) and (height >= 4) then
                   ((2, 2), (width - 2, height - 2)) -> clipRectangle;
               if);
            #);
        if);
        inner;
     #);
   
   
   updateTable:
     (# 
     do (if not updateDisabled then
            update;
        if);
     #);
   items: @list
     (# element:: tableItem;
        assignPositions: scan
          (# inx: @integer;
          do inx + 1 -> inx;
             inx -> current.private.position;
          #);
     #);
   maxIconSize:
     (# width, height: @integer;
     do scan
        (# icon: ^raster;
        do current.icon -> icon[];
           (if icon[] <> None then
               (icon.width, width) -> Max -> width;
               (icon.height, height) -> Max -> height;
           if);
        #);
     exit (width, height)
     #);
   computeItemHeight:
     (# width, height: @integer;
     do maxIconSize -> (width, height);
        (height + 2 * spacing, theStyle.lineHeight) -> Max -> itemHeight;
     #);
   computeItemPosition:
     (# position: @integer;
        x, y: @integer;
        width, ignoreheight: @integer;
     enter position
     do maxIconSize -> (width, ignoreheight);
        width + margin + iconSpacing -> x;
        (position - 1) * itemHeight
        + (itemHeight - theStyle.lineHeight) div 2 
        + theStyle.ascent + margin 
          -> y;
     exit (x, y)
     #);
   insert:
     (# item, before: ^tableItem;
     enter (item[], before[])
     do (if before[] = None then
            item[] -> items.append;
         else
            (item[], before[] -> items.at) -> items.insertBefore;
        if);
        items.assignPositions;
     #);
   delete:
     (# item: ^tableItem;
     enter item[]
     do item[] -> items.at -> items.delete;
        items.assignPositions;
     #);
   
   refresh: tableGraphics
     (# x, y: @integer;
     do maxIconSize -> (iconWidth, integerObject);
        margin -> x;
        margin -> y;
        scan
        (# 
        do (current[], x, y) -> drawItem;
           y + itemHeight -> y;
        #);
     #);

   preferredSize:
     (# width, height: @integer;
        maxTextWidth: @integer;
        iw, ih: @integer;
     do items.size * itemHeight + 2 * margin -> height;
        scan
        (# tw: @integer;
        do current.name -> theStyle.widthOfText -> tw;
           (tw, maxTextWidth) -> Max -> maxTextWidth;
        #);
        maxIconSize -> (iw, ih);
        2*margin + iw + iconSpacing + maxTextWidth -> width;
     exit (width, height)
     #);
   findTableItem:
     (# x, y: @integer;
        inx: @integer;
        theItem: ^tableItem;
        position: @integer;
     enter (x, y)
     do ((y - margin) div itemHeight) + 1 -> position;
        l: scan
          (# 
          do inx + 1 -> inx;
             (if inx = position then
                 current[] -> theItem[];
                 leave l;
             if);
          #);
     exit theItem[]
     #);
   editFld: @movableTextField
     (# 
        verify::
          (# 
          do (if currentEdit[] <> none then
                 (if theText -> currentEdit.verify then
                     theText -> currentEdit.name;
                 if);
                 none -> currentEdit[];
             if);
          #);
     #);
   currentEdit: ^tableItem;
   
   simpleFind:
     (# ch: @char;
        found: ^tableItem;
     enter ch
     do l: scan
        (# theName: ^text;
        do current.name -> theName[];
           (if theName.length >= 1 then
               (if ch = (1 -> theName.inxGet) then
                   current[] -> found[];
                   leave l;
               if);
           if);
        #);
     exit found[]
     #);
   speedSearch:
     (# ch: @char;
        foundItem: ^tableItem;
     enter ch
     do ch -> simpleFind -> foundItem[];
        (if foundItem[] <> none then
            scan
            (# 
            do current.deSelect;    
            #);
            foundItem.select;
        if);
     #);
#)

-- GUIENVtableViewTableItemEnterSelected: doPart --
do (if value <> private.selected then
       value -> private.selected;
       (* true -> update;*)
       private.drawOne;
       (if private.selected then
           onSelect;
       if);
   if);

-- GUIENVtableViewTableItemExitSelected: doPart --
do private.selected -> value;

