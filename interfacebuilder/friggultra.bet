ORIGIN '~beta/guienv/guienv';
INCLUDE 'graphicaleditor/graphicaleditor';
INCLUDE '~beta/objectbrowser/mpsinterface';
INCLUDE 'prettyprinter/prettyprinter';
INCLUDE 'dialogs/createwindow';
INCLUDE 'code/generate';
INCLUDE 'asteditor/asteditor';

-- program: Descriptor --
GUIenv
(# theAstEditorEnv: @astEditorEnv
		(# showParseErrors::
				(#
				do msg[] -> putLine;
				#);
		#);

	scriptEditor: theAstEditorEnv.astEditor
		(# 
		#);
		
	mainWindow: @window
     (#
        open:: 
          (# 
          do 
             'Create' -> title;
             (280,40)->size;
             newButton.open;
             openBtn.open;
             quitButton.open;
             'Frigg: Mjolner BETA Interfacebuilder'->Title;
             
          #);
        close::  (#  do Terminate;  #);
        newButton: @pushButton
          (# eventHandler::
               (# onMouseUp::
                    (#
							  theCreateWindow: @createWindowDialog
								 (#
									 onCancel::  (#  do theCreateWindow.close #);
									 onCreate:: 
										(# 
										do
											(if name[]->theGraphicalEditorEnv.checkIdentifier then
												 theCreateWindow.close;
												 name[]->theGraphicalEditorEnv.createNewWindow;
											else
												message:
												  (# msg: ^text;
												  do &text[] -> msg[];
													  '"' -> msg.put;
														name[] -> msg.putText;
													  '"' -> msg.put;
													  ' is not a legal name.' -> msg.putText;
													  msg[] -> displayMessage;
												  #);
											if)
										#)
								 #)
						  do theCreateWindow.open
						  #);
               #);
             open::<
               (# 
               do
                  (80,20)->size;
                  (10,10)->position;
                  'New...'->label;
                  
               #)
          #);
        openBtn: @pushButton
          (#
             open::<
               (# 
               do
                  (80,20)->size;
                  (100,10)->position;
                  'Open...'->label;
                  
               #)
          #);
        quitButton: @pushButton
          (#
             open::<
               (# 
               do
                  (80,20)->size;
                  (190,10)->position;
                  'Quit'->label;
                  
               #)
          #)
     #);
   MPS: @mpsInterface (#  #);
   theFragServer: @fragmentServer
     (#
        subscribe::  (#  #);
        notifyAstReplaced::  (#  #);
        notifyListElementInserted::  (#  #);
        notifyAstReplacedSequence::  (#  #);
        notifyListElementsDeleted::  (#  #);
        notifyListElementsReplaced::  (#  #);
        
     #);
   pretty: @MPS.AST.prettyPrinter;
   theGraphicalEditorEnv: @graphicalEditorEnv
     (#
        commands::
			(#
			  openAstEditor::
				 (# theEditor: ^scriptEditor; 
				  do
					  node[]->theAstEditorEnv.find->theEditor[];
					  (if theEditor[] = none then
							&scriptEditor[]->theEditor[];
							node[]->theEditor.init;
							title[]->theEditor.title;
							
						else
							theEditor.bringToFront; 
					  if);
					  
				  #);
			  autoSaveGroup::  (#  #);
			  saveFragmentGroup::  (# #);
        #);
     #);
   
do
   MPS.init;
   theFragServer.init;
   MPS.betaCfl[]->pretty.init;
   pretty[]->objectPool.put;
   (theFragServer[],MPS.AST[],MPS.betaCfl[],pretty[])
     ->theGraphicalEditorEnv.init;
	(theFragServer[], MPS.AST[], pretty[]) -> theAstEditorEnv.init;
   mainWindow.open;
#)  

