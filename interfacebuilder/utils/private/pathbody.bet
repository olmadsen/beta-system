ORIGIN '../path';

-- UtilsPath: doPart --
do (# EOS: (# exit 1 #);
      ch: @char;
      next:
        (# 
        do (if str.eos then
               EOS -> ch;
            else
               str.get -> ch;
           if);
        #);
   do str.reset;
      next;
      getting:
        (# failure:
             (# 
             do this(path).failure;
                leave getting;
             #);
           dotPos, slashPos: @integer;
        do -1 -> dotPos -> slashPos;
           (if ch
            //'.'
            //'/'
            //EOS then
               failure;
            else
               l:
                 (if ch
                  //'.' then
                     str.getPos -> dotPos;
                     next;
                     (if ch = '.' then
                         failure;
                      else
                         restart l
                     if);
                  //'/' then
                     str.getPos -> slashPos;
                     next;
                     (if ch
                      //'/'
                      //'.' then
                         failure;
                      else
                         restart l;
                     if);
                  //EOS then 
                     leave l;
                  else
                     (if true
                      //ch -> ASCII.isLetter
                      //ch -> ASCII.isDigit then
                         next;
                         restart l;
                      else
                         failure;
                     if);
                 if);
           if);
           (if slashPos = -1 then
               (if dotPos = -1 then
                   str.copy -> name[];
                else
                   (1, dotPos - 1) -> str.sub -> name[];
                   (if dotPos < str.length then
                       (dotPos + 1, str.length) -> str.sub -> ext[];
                   if);
               if);
            else
               (if slashPos > 1 then
                   (1, slashPos - 1) -> str.sub -> basis[];
               if);
               (if slashPos < dotPos then
                   (slashPos + 1, dotPos -1) -> str.sub -> name[];
                   (if dotPos < str.length then
                       (dotPos + 1, str.length) -> str.sub -> ext[];
                   if);
                else
                   (if slashPos < str.length then
                       (slashPos + 1, str.length) -> str.sub -> name[];
                   if);
               if);
           if);
           inner path;
        #);
   #)
