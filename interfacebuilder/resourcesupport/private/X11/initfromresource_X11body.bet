ORIGIN '../initfromresourcebody';
INCLUDE '~beta/basiclib/v1.4/file';
INCLUDE '~beta/sysutils/v1.4.2/envstring';
INCLUDE '~beta/guienv/v1.3.1/utils/prompts';
INCLUDE '~beta/guienv/v1.3.1/utils/getfile';

-- getResourceDBInit: doPart --
do l: (# prefFile: @catchAllFile
           (# 
              failure::
                (# 
                do stop;
                #);
           #);
         version: @integer;
         firstLine: ^text;
         prefFileName: ^text;
         resourceFileName: ^text;
         resourceFile: @catchAllFile
           (# failure::
                (# 
                do 
                   (normal, 'Syntax-error in resource-file') -> stop;
                #);
           #);
      do getLocalPrefFileName -> prefFile.name;
         (if prefFile.entry.exists then
             prefFile.openRead;
             prefFile.getLine -> resourceFileName[];
             prefFile.close;
          else
             (none, '', 'Specify the resource-file') -> promptForBoolean
             (# 
                ok::
                  (# 
                  do 'rc' -> getFile -> resourceFileName[];
                     prefFile.openWrite;
                     resourceFileName[] -> prefFile.putLine;
                     prefFile.close;
                  #);
                notOk::
                  (# 
                  do Stop;
                  #);
                cancel::
                  (# 
                  do Stop;
                  #);
             #);
         if);
         
         (if resourceFileName[] = none then
             stop;
          else
             resourceFileName[] -> resourceFile.name;
             (if resourceFile.entry.exists then
                 resourceFile.openRead;
                 resourceFile[] -> obj.init;
                 resourceFile.close;
              else
                 stop;
             if);
         if);
      #);  
   
-- lib: attributes --

catchAllFile: file
  (# 
     failure:<
       (# msg: ^text;
       enter msg[]
       do inner;
       #);
     AccessError::<
       (# 
       do msg[] -> failure;
       #);
     WriteError::<
       (# 
       do msg[] -> failure;
       #);
     ReadError::<
       (# 
       do msg[] -> failure;
       #);
     EOSerror::<
       (# 
       do msg[] -> failure;
       #);
     NoSuchFileError::<
       (# 
       do msg[] -> failure;
       #);
     FileExistsError::<
       (# 
       do msg[] -> failure;
       #);
     NoSpaceError::<
       (# 
       do msg[] -> failure;
       #);
     OtherError::<
       (# 
       do msg[] -> failure;
       #);
     safeGetInt: getInt
       (# syntaxError::
            (# 
            do 'syntax error in getint' -> failure;
            #);
       #);
  #);

getAppName:
  (# appName: ^text;
     slashPos: @integer;
  do 1 -> arguments -> appName[];
     appName.reset;
     '/' -> appName.findCh
      (# 
      do inx -> slashPos;
      #);
     (if (slashPos > 0) and (slashPos < appName.length) then
         (slashPos + 1, appName.length) -> appName.sub -> appName[];
     if);
  exit appName[]
  #);
getHome:
  (# home: ^text;
  do '$(HOME)' -> expandEnvVar -> home[];
  exit home[]
  #);
getPrefFileName:
  (# prefFileName: ^text;
  do getHome -> prefFileName[];
     '/.' -> prefFileName.append;
     getAppName -> prefFileName.append;
     'rc' -> prefFileName.append;
  exit prefFileName[]
  #);
getLocalPrefFileName:
  (# prefFileName: ^text;
  do '.' -> prefFileName[];
     getAppName -> prefFileName.append;
     'rc' -> prefFileName.append;
  exit prefFileName[]
  #);
