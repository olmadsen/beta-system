ORIGIN '../initfromresourcebody';
INCLUDE '~beta/basiclib/v1.4/file';
INCLUDE '~beta/sysutils/v1.4.2/envstring';
INCLUDE '~beta/guienv/v1.3.1/utils/prompts';
INCLUDE '~beta/guienv/v1.3.1/utils/getfile';

-- getResourceDBInit: doPart --
do l: (# prefFile: @catchAllFile
           (# 
              failure::
                (# 
                do stop;
                #);
           #);
         version: @integer;
         firstLine: ^text;
         prefFileName: ^text;
         resourceFileName: ^text;
         resourceFile: @catchAllFile
           (# failure::
                (# 
                do 
                   (normal, 'Syntax-error in resource-file') -> stop;
                #);
           #);
      do (none, '', 'Specify the resource-file') -> promptForBoolean
         (# 
            ok::
              (# 
              do 'NONE' -> getFile -> resourceFileName[];
                 (if resourceFileName[] = none then
                     stop;
                  else
                     resourceFileName[] -> resourceFile.name;
                     resourceFile.openRead;
                     resourceFile[] -> obj.init;
                     resourceFile.close;
                 if);
              #);
            notOk::
              (# 
              do Stop;
              #);
            cancel::
              (# 
              do Stop;
              #);
         #);
#);  
   
-- lib: attributes --

catchAllFile: file
  (# 
     failure:<
       (# msg: ^text;
       enter msg[]
       do inner;
       #);
     AccessError::<
       (# 
       do msg[] -> failure;
       #);
     WriteError::<
       (# 
       do msg[] -> failure;
       #);
     ReadError::<
       (# 
       do msg[] -> failure;
       #);
     EOSerror::<
       (# 
       do msg[] -> failure;
       #);
     NoSuchFileError::<
       (# 
       do msg[] -> failure;
       #);
     FileExistsError::<
       (# 
       do msg[] -> failure;
       #);
     NoSpaceError::<
       (# 
       do msg[] -> failure;
       #);
     OtherError::<
       (# 
       do msg[] -> failure;
       #);
     safeGetInt: getInt
       (# syntaxError::
            (# 
            do 'syntax error in getint' -> failure;
            #);
       #);
  #);

getAppName:
  (# appName: ^text;
     slashPos: @integer;
  do 1 -> arguments -> appName[];
     appName.reset;
     '/' -> appName.findCh
      (# 
      do inx -> slashPos;
      #);
     (if (slashPos > 0) and (slashPos < appName.length) then
         (slashPos + 1, appName.length) -> appName.sub -> appName[];
     if);
  exit appName[]
  #);
getHome:
  (# home: ^text;
  do '$(HOME)' -> expandEnvVar -> home[];
  exit home[]
  #);
getPrefFileName:
  (# prefFileName: ^text;
  do getHome -> prefFileName[];
     '/.' -> prefFileName.append;
     getAppName -> prefFileName.append;
     'rc' -> prefFileName.append;
  exit prefFileName[]
  #);
