ORIGIN '../initfromresourcebody';
INCLUDE '~beta/basiclib/v1.5/file';
INCLUDE '~beta/sysutils/v1.5/envstring';
INCLUDE '~beta/guienv/v1.4/utils/prompts';
INCLUDE '~beta/guienv/v1.4/stddialogs';
INCLUDE '~beta/guienv/v1.4/utils/getfile';

-- getResourceDBInit: doPart --
do l: (# prefFile: @catchAllFile
           (# 
              failure::
                (# 
                do stop;
                #);
           #);
         version: @integer;
         firstLine: ^text;
         prefFileName: ^text;
         resourceFileName: ^text;
         resourceFile: @catchAllFile
           (# failure::
                (# 
                do 
                   (normal, 'Syntax-error in resource-file') -> stop;
                #);
           #);
         e: @diskEntry;
      do getLocalPrefFileName -> prefFile.name;
         (if prefFile.entry.exists then
             prefFile.openRead;
             prefFile.getLine -> resourceFileName[];
             prefFile.close;
             resourceFileName[] -> e.path;
             (if not e.exists then
                 none -> resourceFileName[];
             if);
         if);
         (if resourceFileName[] = none then
             (none, 'Specify the resource-file', 'Message') -> noteUser;
             'rc' -> getFile -> resourceFileName[];
             (if resourceFileName[] <> none then
                 prefFile.openWrite;
                 resourceFileName[] -> prefFile.putLine;
                 prefFile.close;
              else
                 stop;
             if);
         if);
         
         (if resourceFileName[] = none then
             stop;
          else
             resourceFileName[] -> resourceFile.name;
             (if resourceFile.entry.exists then
                 resourceFile.openRead;
                 resourceFile[] -> obj.init;
                 resourceFile.close;
              else
                 stop;
             if);
         if);
      #);  
   
-- lib: attributes --

catchAllFile: file
  (# 
     failure:<
       (# msg: ^text;
       enter msg[]
       do inner;
       #);
     AccessError::<
       (# 
       do msg[] -> failure;
       #);
     WriteError::<
       (# 
       do msg[] -> failure;
       #);
     ReadError::<
       (# 
       do msg[] -> failure;
       #);
     EOSerror::<
       (# 
       do msg[] -> failure;
       #);
     NoSuchFileError::<
       (# 
       do msg[] -> failure;
       #);
     FileExistsError::<
       (# 
       do msg[] -> failure;
       #);
     NoSpaceError::<
       (# 
       do msg[] -> failure;
       #);
     OtherError::<
       (# 
       do msg[] -> failure;
       #);
     safeGetInt: getInt
       (# syntaxError::
            (# 
            do 'syntax error in getint' -> failure;
            #);
       #);
  #);

getAppName:
  (# appName: ^text;
     slashPos: @integer;
  do 1 -> arguments -> appName[];
     appName.reset;
     '/' -> appName.findAll
      (# 
      do inx -> slashPos;
      #);
     (if (slashPos > 0) and (slashPos < appName.length) then
         (slashPos + 1, appName.length) -> appName.sub -> appName[];
     if);
  exit appName[]
  #);
getHome:
  (# home: ^text;
  do '$(HOME)' -> expandEnvVar -> home[];
  exit home[]
  #);
getPrefFileName:
  (# prefFileName: ^text;
  do getHome -> prefFileName[];
     '/.' -> prefFileName.append;
     getAppName -> prefFileName.append;
     'rc' -> prefFileName.append;
  exit prefFileName[]
  #);
getLocalPrefFileName:
  (# prefFileName: ^text;
  do '.' -> prefFileName[];
     getAppName -> prefFileName.append;
     'rc' -> prefFileName.append;
  exit prefFileName[]
  #);
