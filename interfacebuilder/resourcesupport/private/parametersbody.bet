ORIGIN '../parameters';
INCLUDE '../../utils/catchallfile';

-- parametersLib: attributes --

lookup:
  (# key: ^text;
     theAtom: ^atom;
     undefined:<
       (# do INNER #);
  enter key[]
  do key[] -> data.lookup -> theAtom[];
     (if theAtom[] = none then
         undefined;
      else
         inner;
     if);
  #);

typeError:
  (# key, type: ^text;
  enter (key[], type[])
  do exception
     (# 
     do 'parameter ' -> msg.putText;
        key[] -> msg.putText;
        ' has wrong type - ' -> msg.putText;
        type[] -> msg.putText;
        ' expected' -> msg.putText;
     #);
  #);

-- parametersType: doPart --
do 'type' -> getInt -> value;


-- parametersGetBool: doPart --
do key[] -> lookup
   (# theBoolAtom: ^boolAtom;
   do theAtom.asBoolAtom -> theBoolAtom[];
      (if theBoolAtom[] = none then
          (key[], 'bool') -> typeError;
       else
          theBoolAtom -> value;
      if);
   #);

-- parametersSetBool: doPart --
do (# theBoolAtom: ^boolAtom;
   do &boolAtom[] -> theBoolAtom[];
      value -> theBoolAtom;
      (key[], theBoolAtom[]) -> data.define;
   #);

-- parametersGetInt: doPart --
do key[] -> lookup
   (# theIntAtom: ^intAtom;
   do theAtom.asIntAtom -> theIntAtom[];
      (if theIntAtom[] = none then
          (key[], 'int') -> typeError;
       else
          theIntAtom -> value;
      if);
   #);

-- parametersSetInt: doPart --
do (# theIntAtom: ^intAtom;
   do &intAtom[] -> theIntAtom[];
      value -> theIntAtom;
      (key[], theIntAtom[]) -> data.define;
   #)

-- parametersGetText: doPart --
do key[] -> lookup
   (# theTextAtom: ^textAtom;
      undefined::
        (# 
        do '?' -> value[];
        #);
   do theAtom.asTextAtom -> theTextAtom[];
      (if theTextAtom[] = none then
          (key[], 'text') -> typeError;
       else
          theTextAtom -> value[];
      if);
   #);
-- parametersSetText: doPart --
do (# theTextAtom: ^textAtom;
   do &textAtom[] -> theTextAtom[];
      value[] -> theTextAtom;
      (key[], theTextAtom[]) -> data.define;
   #)

-- parametersCopy: doPart --
do &parameters[] -> theCopy[];
   &tableAtom[] -> theCopy.data[];
   theCopy.data.storage.init;
   data.storage.scan
   (# 
   do (current.key[], current.value[]) -> theCopy.data.define;
   #);

-- parametersSet: doPart --
do (# 
   do data.storage.clear;
      theCopy.data.storage.scan
      (# 
      do (current.key[], current.value[]) -> data.define;
      #);
   #);

-- parameterStoreLoad: doPart --
do reading:
     (# theFile: @catchAllFile
          (# failure::
               (# 
               do leave reading;
               #);
          #);
        ignoreAtom: ^text;
        parser: @atomParser;
        version: @integer;
     do fileName.copy -> private.fileName[];
        fileName[] -> theFile.name;
        theFile.openRead;
        theFile.getAtom -> ignoreAtom[];
        theFile.getInt -> version;
        (if version
         //1 then
            'older version - needs conversion' -> putLine;
         //2 then
            theFile[] -> parser.init;
            parser.readAtom -> private.data[];
            private.data.storage.scan
            (# theTableAtom: ^tableAtom;
               params: ^parameters;
            do current.asTableAtom -> theTableAtom[];
               (if theTableAtom[] <> none then
                   &parameters[] -> params[];
                   theTableAtom[] -> params.data[];
                   params[] -> private.parametersList.append;
                else
                   leave reading;
               if);
            #);
         else
            'newer version - cannot handle' -> putLine;
        if);
        theFile.close;
        theFile.entry.path.name.prefix -> private.shortName[];
     #);
   
-- parameterStoreCreate: doPart --
do creating:
     (# theFile: @file;
     do fileName.copy -> private.fileName[];
        &listAtom[] -> private.data[];
        private.data.storage.init;
        private.parametersList.init;
        fileName[] -> theFile.name;
        theFile.entry.path.name.prefix -> private.shortName[];
     #);


-- parameterStoreSave: doPart --
do writing:
     (# theFile: @catchAllFile
          (# failure::
               (# 
               do leave writing;
               #);
          #);
        printer: @atomPrinter;
     do private.fileName[] -> theFile.name;
        theFile.openWrite;
        'version: ' -> theFile.putText;
        2 -> theFile.putInt;
        theFile.newLine;
        theFile[] -> printer.init;
        private.data[] -> printer.printAtom;
        theFile.close;
     #);
   
-- paramterStoreAutoSave: doPart --
do writing:
     (# theFile: @catchAllFile
          (# failure::
               (# 
               do leave writing;
               #);
          #);
        printer: @atomPrinter;
     do '.' -> (private.fileName.copy).Append -> theFile.name;
        theFile.openWrite;
        'version: ' -> theFile.putText;
        2 -> theFile.putInt;
        theFile.newLine;
        theFile[] -> printer.init;
        private.data[] -> printer.printAtom;
        theFile.close;
        (if theFile.entry.exists 
            (# 
               error::
                 (# 
                 do leave writing;
                 #);
            #) then
            '#' -> (private.fileName.copy).Append -> theFile.entry.rename
            (# 
               error::
                 (# 
                 do leave writing;
                 #);
            #);
        if);
     #);
   
-- parameterStoreDeleteAutoSave: doPart --
do deleting:
     (# f: @catchAllFile
          (# 
             failure::
               (# 
               do leave deleting;
               #);
          #);;
        exists: @boolean;
     do '#' -> (private.fileName.copy).Append -> f.name;
        f.entry.exists
          (# error::
               (# 
               do leave deleting;
               #);
          #) -> exists;
        (if exists then
            f.delete;
        if);
     #);
   
-- parameterStoreGet: doPart --
do


-- parameterStoreNew: doPart --
do &parameters[] -> params[];
   &tableAtom[] -> params.data[];
   params.data.storage.init;
   ('type', type) -> params.setInt;
   params[] -> private.parametersList.append;
   params.data[] -> private.data.storage.append;
   
-- parameterStoreShortName: doPart --
do private.shortName.copy -> value[];

-- parameterStorePrivate: descriptor --
(# data: ^listAtom;
   fileName: ^text;
   parametersList: @sequence
     (# element:: parameters;
     #);
   shortName: ^text;
#)
