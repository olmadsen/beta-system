ORIGIN '~beta/guienv/v1.3.1/guienv';
BODY 'private/dragmanagerbody';

-- guienvLib: attributes --

rectangleValue:
  (# value: ^rectangle;
  do inner;
  exit value[]
  #);

dragManager:
  (* The dragmanager supports drag and drop between windowitems.
   * 
   * There is a virutal dragger class for draggin items and a reciever
   * class (not virtual) for recieving items.
   * 
   * The dragger class is virtual, so it can be extended 
   * in a type-safe way.
   * 
   *)
  (# <<SLOT dragmanagerLib: attributes>>;
     init:<
       (* The world of the dragmanager is the windowitem that limits
        * the dragging.
        * 
        * World = none means that desktop is the world. That is,
        * objekts can be dragged between windows.
        * 
        * All reciever items must be inside the world.
        *)
       (# world: ^window.windowItem;
       enter world[]
       <<SLOT dragmanagerInit: doPart>>
       #);
     dragger:<
       (* The dragger class models dragging of items 
        * in the dragmanager world. The item can also be reciever.
        * 
        * A dragger objekt can be used for dragging more 
        * than one item.
        * 
        * The item may not be none when dragging.
        *)
       (# <<SLOT draggerLib: attributes>>;
          item: ^window.windowItem;
          init:<
            (# 
            <<SLOT dragManagerDraggerInit: doPart>> 
            #);
          drag:
            (* When drag is invoked, the system tracks the mouse and
             * provides feedback.
             * 
             * Default the frame of the item is used, but this overridden by
             * furtherbinding "frame".
             * 
             * Drag must be invoked within a onMouseDown event.
             *)
            (# frame:< rectangleValue;
               event: ^window.windowItem.eventHandler.mouseDown;
               
               badItemError:< exception
                 (# 
                 do 'Error in dragger.drag: the item is not inside the dragmanager world' -> msg.append;
                    inner;
                 #);
               noItemError:< exception
                 (# 
                 do 'Error in dragger.drag: the item is NONE' -> msg.append;
                    inner;
                 #);
               noEventError:< exception
                 (# 
                 do 'Error in dragger.drag: the event is NONE' -> msg.append;
                 #);
               exclude: ^window.windowItem;
            enter (event[], exclude[])
            <<SLOT dragManagerDraggerDrag: doPart>>
            #);
       #);
     reciever:
       (# <<SLOT recieverLib: attributes>>;
          
          init:<
            (# item: ^window.windowItem;
               badItemError:< exception
                 (# 
                 do 'Error in reciever.init: the item is not inside the dragmanager world' -> msg.append;
                    inner;
                 #);
               noItemError:< exception
                 (# 
                 do 'Error in reciever.init: the item is NONE' -> msg.append;
                    inner;
                 #);
            enter item[]
            <<SLOT dragManagerRecieverInit: doPart>> 
            #);
          delete:<
            (#  <<SLOT dragManagerRecieverDelete: doPart>> #);
          recieve:<
            (# theDragger: ^dragger;
               frame: ^rectangle;
            enter (theDragger[], frame[])
            do inner;
            #);
          
          onHilite:<
            object;
          onLowlite:<
            object;
          private: @<<SLOT dragManagerRecieverPrivate: descriptor>>;
       #);
     private: @<<SLOT dragManagerPrivate: descriptor>>;
  #);

