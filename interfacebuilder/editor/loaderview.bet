ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/scrolllists'
        '~beta/guienv/controls'
        '~beta/guienv/stddialogs'
        '../support/row'
        '~beta/compiler/DYN/dynlib';

-- guienvLib: Attributes --
loaderView: window
  (# origin: ^object;
     
     comp: ^astInterface.dynamicCompiler;
     fileEntry:
       (#
          name: ^text;
          init: (#  enter name[] do entryPoints.init; INNER ;  #);
          compile:
            (# ok: @boolean; 
            do
               name[]->comp.compileAndLoad->(ok,imp[]);
               (if ok then
                   (if imp[] <> none then
                       imp.scanBetaNames
                         (#  do current[]->entryPoints.append;  #);
                       THIS(fileEntry)[]->entryPointsView.showFileEntry;
                       
                   if);
                   
               if);
               
            #);
          imp: ^imagePair;
          entrypoints: @list (# element:: text;  #);
          execute:
            (# entryPoint: ^text;
               structure: ##object;
               off: @integer;
            enter entryPoint[]
            do (if imp[] <> NONE then
                   entryPoint[] -> imp.namedOff -> off;
                   (origin[], off) -> comp.makeStruc -> structure##;
                   (if structure## <> NONE then
                       &structure;
                   if);
               if);
            #);
       #);
     fileEntries: @list
       (#
          element:: fileEntry;
          findByName: find
            (#
               name: ^text;
               predicate:: (#  do name[]->current.name.equal->value;  #);
               
            enter name[]
            #);
          
       #);
     openFile:
       (# name: ^text; entry: ^fileEntry; 
       do
          fileSelectionDialog->name[];
          (if name[] <> none then
              &fileEntry[]->entry[];
              name[]->entry.init;
              entry[]->fileEntries.append;
              entry[]->fileView.addEntry;
              
          if);
          
       #);
     compile:
       (# name: ^text; entry: ^fileEntry; 
       do
          (if fileView.selection.first <> 0 then
              fileView.selection.first->fileView.getText->name[];
              name[]->fileEntries.findByName->entry[];
              entry.compile;
              
          if);
          
       #);
     execute:
       (#
       do (if entryPointsView.theFileEntry[] <> NONE then
              (if entryPointsView.selection.first <> 0 then
                  entryPointsView.selection.first 
                    -> entryPointsView.getText
                    -> entryPointsView.theFileEntry.execute;
              if);
          if);
       #);
     open::<
       (# 
       enter (comp[], origin[])
       do
          (280,253)->size;
          fileView.open;
          entryPointsView.open;
          buttonPanel.open;
          fileEntries.init;
          INNER
       #);
     fileView: @textscrolllist
       (#
          addEntry:
            (# entry: ^fileEntry; 
            enter entry[]
            do 1->append; (numberOfItems,entry.name[])->setText; 
            #);
          open::<
            (# 
            do
               true->bindRight;
               borderStyles.shadowOut->border.style;
               (280,92)->size;
               (0,42)->position;
               INNER
            #)
       #);
     entryPointsView: @textscrolllist
       (#
          theFileEntry: ^fileEntry;
          showFileEntry:
            (# 
            enter theFileEntry[]
            do
               (1,numberOfItems)->delete;
               theFileEntry.entryPoints.size->append;
               theFileEntry.entryPoints.scan
                 (# inx: @integer; 
                 do inx+1->inx; (inx,current[])->setText; 
                 #);
               
            #);
          open::<
            (# 
            do
               true->bindBottom;
               true->bindRight;
               borderStyles.shadowOut->border.style;
               (280,92)->size;
               (0,136)->position;
               INNER
            #)
       #);
     buttonPanel: @row
       (#
          open::<
            (# 
            do
               true->bindRight;
               true->border.visible;
               13->distance;
               10->margin.bottom;
               10->margin.right;
               10->margin.top;
               10->margin.left;
               true->vertical.wrap;
               false->horizontal.wrap;
               alignLeft->horizontal.alignment;
               alignTop->vertical.alignment;
               borderStyles.shadowOut->border.style;
               (280,40)->size;
               (0,0)->position;
               openBtn.open;
               compileBtn.open;
               executeBtn.open;
               INNER
            #);
          openBtn: @pushbutton
            (#
               open::<
                 (# 
                 do
                    'Open'->label;
                    borderStyles.shadowOut->border.style;
                    (60,20)->size;
                    (10,10)->position;
                    INNER
                 #);
               eventHandler::<
                 (# onMouseUp::< (#  do openFile;  #) #)
            #);
          compileBtn: @pushbutton
            (#
               open::<
                 (# 
                 do
                    'Compile'->label;
                    borderStyles.shadowOut->border.style;
                    (60,20)->size;
                    (83,10)->position;
                    INNER
                 #);
               eventHandler::< (# onMouseUp::< (#  do compile #) #)
            #);
          executeBtn: @pushbutton
            (#
               open::<
                 (# 
                 do
                    'Execute'->label;
                    borderStyles.shadowOut->border.style;
                    (60,20)->size;
                    (156,10)->position;
                    INNER
                 #);
               eventHandler::< (# onMouseUp::< (#  do execute  #) #)
            #)
       #)
  #)  

