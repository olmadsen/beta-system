ORIGIN '~beta/xcompiler/compiler';
INCLUDE '~beta/guienv/v1.6/guienvsystemenv';
INCLUDE '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/stddialogs';
INCLUDE 'compilergui';

RESOURCE ppcmac 'private/standalonebeta.rsrc';

-----program: descriptor---
systemEnv
(# comp: @compiler;
	compilerFacade: @guienv
		(# onOpenDocument::
				(#
				do fileName[] -> theCompilerGUI.add;
				#);
			theCompilerGUI: @compilerGUI
				(# menuBarType::
						(# myFileMenu: @menu
								(# openItem: @menuItem
										(# eventHandler::
												(# onSelect::
														(# name: ^text;
														do fileSelectionDialog -> name[];
															(if name[] <> NONE then
																name[] -> theCompilerGUI.add;
															if);
														#);
												#);
											open::
												(#
												do 'Open' -> name;
													'O' -> key;
												#);
										#);
									quitItem: @menuItem
										(# eventHandler::
												(# onSelect::
														(#
														do Terminate;
														#);
												#);
											open::
												(#
												do 'Quit' -> name;
													'Q' -> key;
												#);
										#);
									open::
										(#
										do 'File' -> name;
											openItem.open;
											openItem[] -> append;
											quitItem.open;
											quitItem[] -> append;
										#);
								#);
							open::
								(#
								do myFileMenu.open;
									myFileMenu[] -> append;
								#);
						#);
					run::
						(# s: ^statusPanel.statusStream;
							buffer: ^text;
							error: @boolean;
						do &text[] -> buffer[];
							statusPanel.makeStream -> s[];
						   buffer[] -> s.listeners.append;
							s[] -> comp.infoStream[];
							buffer[] -> comp.messagestream[];
							buffer[]->comp.bugstream[];
							name[] -> comp.rootFragment[];
							'' -> theOutputWindow.info;
							L: comp.translate
								(#
									  Report:
										 (# msg: ^text
										 enter msg[]
										 do msg[]->comp.messagestream.putline; true->error; leave L
										 #);
									  MsgException::<  (#  do msg[]->Report #);
									  TransAccessException::<  (#  do msg[]->Report #);
									  TransCreateDirException::<  (#  do msg[]->Report #);
									  NoSpaceException::<  (#  do msg[]->Report #);
									  MPSException::<  (#  do msg[]->Report #);
									  notExistingException::<  (#  do msg[]->Report #);
									  parseException::<  (#  do msg[]->Report #);
									  betaRunException::<  (#  do msg[]->Report #);
									  RshException::<  (#  do msg[]->Report #);
									  jobFileException::<  (#  do msg[]->Report #);
									  multipleMachException::<  (#  do msg[]->Report #);
									  unknownMachException::<  (#  do msg[]->Report #);
									  doubleFormException::<  (#  do msg[]->Report #);
									  fragmentException::
									  (* Note that the group is closed here.
										* Sif and other tools may NOT want to do this!
										*)  (#  do CloseGroup; msg[]->Report #);
									  propertyException:: 
										 (# 
										 do
											 (if warning then
												  msg[]->comp.messagestream.putLine; true->continue
											  else
												  CloseGroup; msg[]->Report
											 if)
										 #);
									  SystemException::  (#  do msg[]->Report #)
								  #);
						 	buffer[] -> theOutputWindow.info;
							(if error then
								system.beep;
								theOutputWindow.show;
								theOutputWindow.bringToFront;
							if);
						#);
					open::
						(#
						do 'Beta Compiler' -> Title;
						#);
				#);
			theOutputWindow: @outputWindow
				(# open::
						(#
						do hide;
						#);
					eventHandler::
						(# onAboutToClose::
								(#
								do hide;
									false -> okToClose;
								#);
						#);
				#);
		do theCompilerGUI.open;
			theOutputWindow.open;
		#);
	setWindowEnv::  (#  do compilerFacade[]->theWindowEnv[] #)
do screen[]->comp.infostream[];
  	screen[]->comp.tracestream[];
  	screen[]->comp.messagestream[];
  	screen[]->comp.bugstream[];
  	comp.init;
	61 -> comp.switchOn;
	70 -> comp.switchOn;
#)
