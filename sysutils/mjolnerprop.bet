ORIGIN '~beta/basiclib/betaenv';
LIB_DEF 'mjolnerprop' '../lib';
INCLUDE 'pathhandler';
INCLUDE 'envstring';
INCLUDE '~beta/basiclib/file';
BODY 'private/mjolnerpropbody';
---LIB: ATTRIBUTES---
GlobalOnly:     (# exit 2 #);    (* Which properties to scan. *)
LocalOnly:      (# exit 1 #);
GlobalAndLocal: (# exit 0 #);

mjolnerrcfile: (# t: ^text do '~beta/configuration/mjolnerrc'->t[] exit t[] #);

ScanMjolnerProps:
(* This pattern scans the file .mjolnerrc (: def. of local user-specific 
   properties) and Mjolnerrc (: def of global properties).  The scanning is 
   done in that order, that is all global properties are scan before the
   local.  Each property on these files are read and inner is executed. If 
   a format error is encountered on one of the files, the scanning of the file
   is terminated.  The files consist of a sequence of property definitions. A 
   property-definition has the following form:
          <ApplName>.<PropName>: <PropVal>nl
          <ApplName> is a sequence of chars, not containing '.'.
          <PropName> is a sequence of chars, not containing ':'.
          <PropVal> is a sequence of chars, not containing nl.
   The forbidden chars can be included, by escaping them. E.g. 'a\.b' can be 
   used as an <ApplName>.
   Comments are allowed in .mjolnerrc and Mjolnerrc. They should be enclosed 
   in the usual brackets.
   Not all properties are scanned. The pattern ScanMjolnerProps enters three
   arguments: GlobalLocal, targetApplName and targetPropName. The first
   determines whether to scan global and/or local properties. The second
   gives an application name that must be matched in order to execute INNER on
   a property. Similarly the third argument must match the actual property name
   in order to have INNER executed. TargetApplName and targetPropName can be
   given the value '' which matches any application name or property name.
 *)
  (# GlobalLocal: @integer; 
     targetApplName, targetPropName: @text;
     (* ----------------------- Iterator variables ------------------------- *)
     Global:   @boolean;   (* True iff actual property is a global property. *)
     ApplName: @text;      (* Name of application, this prop. is ass. with.  *)
     PropName: @text;      (* Name of this property.                         *)
     PropVal:  @tokenText; (* Value of this property.                        *)
     (* -------------------------------------------------------------------- *)
  enter(GlobalLocal,targetApplName,targetPropName)
  <<SLOT sysutils_mjolnerprop:doPart>>
  #);

TokenText:  text
(* Properties have values which are instanses of this pattern. *)
(#
   ScanTokens:         (* For each token Token (: sequence of non-blanks) 
                          in this text do inner.
                       *)
   (# Token:    @text; (* This one iterates over the tokens! *)
      ch:       @char;
      i,len:    @integer;
   do
      Length -> len;
      1      -> i;
      sykkel:
      (if (i<=len) then
          skipb: 
            (if (i<=len) then
                (if ((i->InxGet)<=' ') then
                    i+1 -> i;
                    restart skipb;
                if)
            if);
         Token.clear;
         collect:
            (if (i<=len) then
                (if ((i->InxGet->ch)>' ') then
                    ch  -> Token.put;
                    i+1 -> i;
                    restart collect;
                if)
            if);
          (if (Token.Length>0) then inner if);
         restart sykkel
      if);
   #);
#);

