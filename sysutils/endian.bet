ORIGIN '~beta/basiclib/v1.5/betaenv';
(* Operations for testing whether or not the host system uses
 * big endian or little endian data representation.
 * In addition there are operations for converting a long (4bytes)
 * and a short (2 bytes) memory location from big/little to
 * little/big endian.
 *)
---lib:attributes---
isBigEndian:
  (# X: @integer; b: @boolean
  do 0x01000000->X; 
     @@X->TOS'%adrGetByte[0]'->b
  exit b
  #);
isLittleEndian:
  (# X: @integer; b: @boolean
  do 0x0001->X;
     @@X->TOS'%adrGetByte[0]'->b
  exit b
  #);
(* WARNING: byteSwapShort and byteSwapLong are NOT safe operations,
 * since they take a memory argument as a parameter. This require
 * use of the low-level primitive @@. 
 * Ex:
 *    X: @integer; R: [100] @shortInt
 *    @@XX->byteSwapLong;
 *    (for i: R.range repeat
 *         @@R[i]->byteSwapShort
 *    for)
 * After @@X/@@R[i] has been computed, a grabage collection MUTS NOT happen,
 * since this invalidates the computed addresses. It is therefore
 * recommedn to use statis versions like
 * 
 *     BSL: @ byteSwapLong
 *     @@X->BSL
 * 
 * Another alternative is to use the TOS operations directly.
 * The TOS operations are currently only implemented for Linux
 *)
byteSwapShort:
  (# adr: @integer
  enter adr
  do adr->TOS'%byteSwapShort'
  #);
byteSwapLong:
  (# adr: @integer
  enter adr
  do adr->TOS'%byteSwapLong'
  #);


