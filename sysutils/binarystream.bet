ORIGIN '~beta/basiclib/betaenv';
LIB_DEF 'sysutilsbinstream' '../lib';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-98
 *   All rights reserved.
 * 
 * For constructing an in-memory byte stream in a repetition
 * that can be saved and/or restored on a file.
 * Used by the compiler and valhalla for generating
 * and reading '.db' files.
 *)
BODY 'private/binarystreambody';

---LIB:attributes---

BinaryStream:
  (# init:< (# <<SLOT BinaryStreamInit:doPart>> #);
     put: @
       (# ch: @char 
       enter ch 
       <<SLOT BinaryStreamPut:dopart>> 
       #);
     putShort: @
       (# V: @integer 
       enter V 
       <<SLOT BinaryStreamPutShort:dopart>> 
       #);
     putLong: @
       (# V: @integer 
       enter V 
       <<SLOT BinaryStreamPutLong:dopart>> 
       #);
     putText: @
       (# T: ^text
       enter T[] 
       <<SLOT BinaryStreamPutText:dopart>> 
       #);
     get: @ 
       (# ch: @char 
       <<SLOT BinaryStreamGet:dopart>> 
       exit ch 
       #);
     getShort:  @
       (# V: @integer 
       <<SLOT BinaryStreamGetShort:dopart>> 
       exit V 
       #);
     getLong: @
       (# V: @integer 
       <<SLOT BinaryStreamGetLong:dopart>> 
       exit V 
       #);
     getText: @
       (# T: ^text
       <<SLOt BinaryStreamGetText:dopart>> 
       exit T[] 
       #);
     save:
       (# openFailed:< Exception;
          FN: ^text
       enter FN[] 
       do <<SLOT BinaryStreamSave:descriptor>>
       #);
     restore:
       (# notFound:< Exception;
          FN: ^text 
       enter FN[]
       do <<SLOT BinaryStreamRestore:descriptor>>
       #);
     fragRestore:
       (# notFound:< Exception;
          FN: ^text 
       enter FN[]
       do <<SLOT BinaryStreamFragRestore:descriptor>>
       #);
     (* The folowing exceptions may not be sufficient;
      * the exception model is clumsy since the current version 
      * is a quick merge of the two binarystream versions used by
      * the compiler and valhalla. The compiler uses the exceptions below
      * but they are not used by valhalla; continue is therefore 
      * set to true. Valhalla uses the openFailed/notFound execptions
      * in save,restore and fragRestore
      *)
     AccessError:< 
       Exception(# FN: ^text enter FN[] do true -> continue; INNER #);
     NoSpaceError:< 
       Exception(# FN: ^text enter FN[] do true -> continue; INNER #);
     WriteError:< 
       Exception(# FN: ^text enter FN[] do true -> continue; INNER #);

     rep: @ <<SLOT BinaryStreamRep:descriptor>>             
  #)
