ORIGIN '~beta/basiclib/v1.6/betaenv';

INCLUDE '~beta/sysutils/v1.6/objinterface';
INCLUDE '~beta/sysutils/v1.6/scanobjects';

--- program:descriptor ---
(#
   marker:
     (# msg: ^text;
     enter msg[]
     do
        '-------------------------'->screen.puttext;
        msg[]->screen.puttext;
        '-------------------------'->screen.putline;
     #);
   names: @
     (# obj: ^object;
        pl: @screen.putline;
        pn: @getPatternName;
     enter obj[]
     do obj[]->pn->pl;
     #);
   objects: @
     (# po: @printObject;
        nl: @screen.newline;
        obj: ^object
     enter obj[]
     do (obj[],screen[])->po; nl;
     #);
   t: @text;
   namesAndOrigins: @
     (# obj,org: ^object;
        go: @getOrigin;
        pt: @screen.puttext;
        pl: @screen.putline;
        pn: @getPatternName;
     enter obj[]
     do obj[]->go->org[];
        obj[]->pn->pt;
        ', origin='->pt;
        org[]->pn->pl;
     #);
do
   'print visited'->marker;
   scanIOA(# do true->printVisited #);
   
   'print visited, with origin and size'->marker;
   scanIOA(# do true->printVisited->printOrigin->printSize #);
   
   'names of patterns'->marker;
   scanIOA(# callback::< (# do obj[]->names #)#);
   
   'objects'->marker;
   scanIOA(# callback::< (# do obj[]->objects #)#);
   
   'names and origins, in BETA'->marker;
   scanIOA(# callback::< (# do obj[]->namesAndOrigins #)#);
   
   'stream objects'->marker;
   scanIOA
   (# callback::< (# do obj[]->objects #);
   do
      true->printVisited->printOrigin;
      stream##->root##;
   #);
#)
