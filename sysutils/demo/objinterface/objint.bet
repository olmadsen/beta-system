ORIGIN '~beta/sysutils/v1.6/objinterface';
-- PROGRAM: descriptor --

(* Example showing some of the uses of the BETA interface to
 * BETA objects. Here some characteristics of 'foo', which
 * is an instance of 'bar', are printed.
 *)

(#
   bar: IntegerValue
     (# i: @integer;
        f: @text;
        g: ^object;
     do 1 -> value;
        2 -> i;
        'Hello' -> f;
        f[] -> g[];
     #);
   foo: @bar;
   
   proto: @prototype;
   pref: @prototype;
   
do foo;
   '\nPrint of foo: ' -> puttext;
   (foo[], screen[]) -> printobject;
   '\nFound using the prototype of foo:' -> putline;
   foo[] -> getPrototype -> proto;
   '\tPattern name:     ' -> puttext; foo[] -> getPatternName -> putline;
   '\tGC table:         ' -> puttext; proto.GCtab -> putint; newline;
   '\tOrigin Offset:    ' -> puttext; proto.OriginOff -> putint; newline;
   '\tGeneration Part:  ' -> puttext; proto.Gpart -> putint; newline;
   '\tSize (longs):     ' -> puttext; proto.size -> putint; newline;
   '\tPrefix labId:     ' -> puttext; 
   proto.prefix -> pref.address; pref.LabId -> putline;
   '\tLocated in file:  ' -> puttext; proto.groupId -> putline;
   
   '\tLabId:            ' -> puttext; proto.labId -> putline;
   newline;
   proto.scanRefs
   (# 
   do (if isStatic
       // true  then '\tStatic reference at offset  ' -> puttext;
       // false then '\tDynamic reference at offset ' -> puttext;
      if);
      thisOffset -> putint; newline;
   #);
#)
