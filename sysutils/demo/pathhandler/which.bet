ORIGIN '~beta/sysutils/pathhandler';
INCLUDE '~beta/basiclib/file';
INCLUDE '~beta/toollibs/utils/fileExtensions';
(* Simple demo of pathhandler.scanSearchPath, here used to
 * implement a variant of the 'which' program that displays
 * all files in the search path of a given name.
 *)
--PROGRAM: descriptor--
(# exename, batname: ^text;
do (if NoOfArguments<>2 then
       'Usage: which <programname>' -> screen.putline;
       stop;
   if);
   2 -> Arguments -> exename[];
   exeFileExtension -> exename.appendExtension;
   2 -> Arguments -> batname[];
   batFileExtension -> batname.appendExtension;
   scanSearchPath
   (# e: @diskentry;
   do path.length->path.setpos;
      DirectoryChar -> path.put;
      exename[] -> (path.copy).append -> e.path;
      (if e.exists then e.path -> screen.putline if);
      (if not (exeFileExtension -> (batFileExtension).equal) then
          batname[] -> (path.copy).append -> e.path;
          (if e.exists then e.path -> screen.putline if);
      if)
   #);
#)
