ORIGIN '~beta/sysutils/v1.5/pathhandler'

(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992
 *       All rights reserved.
 *)

--PROGRAM:descriptor--

(* An example of using PathHandler.
 * The results of ConvertFilePath on various combinations of paths and bases
 * are demonstrated.
 *)

(# handler: @pathhandler
     (# ConvertFilePath::< 
          (# NoFather::< 
               (# (* Print exception message, but continue *)
               do '-------------------------'->putline;
                  '**** Exception processing'->putline;
                  msg[] -> puttext;
                  '-------------------------'->putline;
                  true -> continue;
               #)
          #)
     #);
   
   converter:
     (# basis: ^text;
        convert:
          (# t: ^text;
          enter t[]
          do '\t' ->puttext; t[] -> puttext; ' is converted to:\n' -> puttext;
             (t[], basis[])  -> handler.localpath -> t[];
             '\t\t' -> puttext; t[] -> putline;
          #);
        
     do INNER;
        newline;
        'Paths converted relative to \n\t\t'->puttext;
        basis[] -> putline;
        
        '/a/home/amparo1/beta/basiclib/v1.5/fisk' -> convert;
        '/usr/local/lib/beta/basiclib/v1.5/fisk' -> convert;

     #)        
   
do handler.init;
   '\nTHIS(PathHandler).BetaLib is:\n\t\t' -> puttext; 
   handler.betalib -> putline;
   'Current directory is:\n\t\t' -> puttext; 
   handler.currentdirectory -> putline;
   
   converter(# do '.' -> basis[] #);
   converter(# do handler.currentdirectory -> basis[] #);
   converter(# do '' -> basis[] #);
  
#)
