ORIGIN '~beta/sysutils/v1.5/pathhandler';
INCLUDE '~beta/unixlib/v1.5/unixdirectory';
-- PROGRAM: descriptor--

(* An example of using FilenameConverter.
 * A directory ('~/bar') and a file ('~/bar/foo') is created, and two 
 * symbolic links to 'bar' ('~/foo1' and '~/foo2') are also created.
 * Then it is demonstrated that three paths to the same file 'foo' yield 
 * the same result when converted.
 *)

(# handler: @FilenameConverter;
   home: ^text;
   dir: @UnixDirectory
     (# EntryDesc::<
          (# myLinkFrom: linkFrom(# exists::< (# do true->continue #)#)#);
        myCreateFile: createFile(# exists::< (# do true->continue #)#);
     #);
do (* First let us find the home directory of the user,
    * since it is likely that he has write permission there.
    *)
   handler.init;
   ('~', '.') -> handler.convertFilePath -> home[];
   
   'Creating the following disk entries: ' -> putline;
   '\tdirectory ~/bar/' -> putline;
   '\tlink      ~/foo1 -> ~/bar' -> putline;
   '\tlink      ~/foo2 -> ~/bar' -> putline;
   '\tfile      ~/bar/foo' -> putline;
   '/bar' -> (home.copy).append -> dir.name;
   (if dir.entry.exists//false then dir.touch if);
   '/foo1' -> (home.copy).append -> dir.entry.myLinkFrom;
   '/foo2' -> (home.copy).append -> dir.entry.myLinkFrom;
   'foo' -> dir.myCreateFile;
   
   '\nConverting paths:' -> putline;
   '\t''~/foo1/foo'' is converted to ' -> puttext;
   ('/foo1/foo'-> (home.copy).append, '.') 
     -> handler.convertFilePath -> putline;
   '\t''~/bar/foo''  is converted to ' -> puttext;
   ('/bar/foo'-> (home.copy).append, '.')  
     -> handler.convertFilePath -> putline;
   '\t''~/foo2/foo'' is converted to ' -> puttext;
   ('/foo2/foo'-> (home.copy).append, '.') 
     -> handler.convertFilePath -> putline;
   
   '\nCleaning up.\n' -> putline;
   'foo' -> dir.deleteFile;
   home[] -> dir.name;
   'foo1' -> dir.deleteFile;
   'foo2' -> dir.deleteFile;
   'bar'  -> dir.deleteDir;
#)
