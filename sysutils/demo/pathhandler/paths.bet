ORIGIN '~beta/sysutils/v1.6/pathhandler';
--PROGRAM:descriptor--

(* An example of using PathHandler.
 * The results of ConvertFilePath on various combinations of paths and bases
 * are demonstrated.
 *)

(# handler: @pathhandler
     (# ConvertFilePath::< 
          (# NoFather::< 
               (# (* Print exception message, but continue *)
               do '-------------------------'->putline;
                  '**** Exception processing'->putline;
                  msg[] -> puttext;
                  '-------------------------'->putline;
                  true -> continue;
               #)
          #)
     #);
   
   converter:
     (# basis: ^text;
        convert:
          (# t: ^text;
          enter t[]
          do '\t' ->puttext; t[] -> puttext; ' is converted to:\n' -> puttext;
             (t[], basis[])  -> handler.convertfilepath -> t[];
             '\t\t' -> puttext; t[] -> putline;
          #);
        
     do INNER;
        newline;
        'Paths converted relative to \''->puttext;
        basis[] -> puttext; '\':' -> putline;
   
        '~beta' -> convert;
        '~beta/basiclib' -> convert;
        '~' -> convert;
        '$(BETALIB)/sub' -> convert;
        '/top' -> convert;
        '/' -> convert;
        '/top/sub/subsub' -> convert;
        'sub/subsub' -> convert;
        'sub/.foo' -> convert;
        'sub/..bar' -> convert;
        '../sub' -> convert;
        '.' -> convert; 
        '..' -> convert;
        '../.' -> convert;
        '../..' -> convert;
     #)        
   
do handler.init;
   '\nTHIS(PathHandler).BetaLib is:\n\t\t' -> puttext; 
   handler.betalib -> putline;
   'Current directory is:\n\t\t' -> puttext; 
   handler.currentdirectory -> putline;
   
   converter(# do handler.currentdirectory -> basis[] #);
   converter(# do '.' -> basis[] #);
   converter(# do './bsub' -> basis[] #);
   converter(# do 'bsub' -> basis[] #);
   converter(# do 'bsub/bsubsub' -> basis[] #);
   converter(# do '/' -> basis[] #);
   converter(# do '/btop' -> basis[] #);
   converter(# do '/btop/bsub' -> basis[] #);
#)
