ORIGIN '~beta/basiclib/betaenv';
LIB_DEF 'sysutilswstring' '../lib';
BODY 'private/wstringbody';

(*
 * COPYRIGHT
 *       Copyright (C) Aarhus University
 *       All rights reserved.
 *)

-- LIB: attributes --

WString:
  (* BETA interface to NULL-terminated Wide-strings (16 bit), externally
   * allocated, e.g., using malloc.
   *)
  (# ptr: @integer
       (* The pointer to the external string *);
     init: 
       (* Allocate length wchars (default 256) using malloc, and assign the
        * result to ptr. You may want to explicitly free THIS(WString)
        * before a reinitialization.
        *)
       (# length: @integer;
       enter length 
       do <<SLOT WStringInit: descriptor>>
       #);
     set: 
       (* Initializes THIS(WString) to be large enough to hold the chars of 
        * T as wchars, and then copies the chars of T to THIS(WString) as wchars. 
        * You may want to explicitly free THIS(WString) before this operation, 
        * if this is a reinitialization.
        * For convenience, it also exits ptr.
        *)
       (# T: ^text; 
       enter T[]
       do <<SLOT WStringSet: descriptor>>
       exit ptr
       #);
     get:
       (* Return the chars of THIS(CString) copied into the text T.
        * Chars may be chopped from 16 to 8 bits 
        *)
       (# T: ^text;
       do <<SLOT WStringGet: descriptor>>  
       exit T[]
       #);
     getChars:
       (* Return a char-repetition, with the wchars of THIS(WString) copied to 
        * it, possibly chopped
        *)
       (# R: [0]@char;
       do <<SLOT WStringGetChars: descriptor>>
       exit R
       #);
     length: IntegerValue
       (* Return the string-length if THIS(WString), i.e., the number of wchars
        * before the NULL-wchar in the externally allocated string.
        *)
       (# <<SLOT WStringLength: dopart>> #);
     inxPut:
       (* Put ch at index inx in the externally allocated WString. The first wchar
        * is at index 0. NOTICE: There is no index check, it is  the programmer to
        * ensure, that it is allowed to put at the given index.
        *)
       (# inx: @integer;
          ch: @wchar;
       enter (inx, ch) 
          <<SLOT WStringInxPut: dopart>>
       #);
     inxGet:
       (* Obtain the wchar at the given index in the externally allocated string.
        * The first char is at index 0. 
        *)
       (# inx: @integer;
          ch: @wchar;
       enter inx
          <<SLOT WStringInxGet: dopart>>
       exit ch
       #);
     free: 
       (* Free the externally allocated string. The wchars of the externally
        * allocated string will be unaccessible after this operation.
        *)
       (# <<SLOT WStringFree: dopart>> #);
     
     <<SLOT CStringLib: attributes>>;
  enter ptr
  do INNER
  exit ptr
  #);
