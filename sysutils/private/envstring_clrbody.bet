ORIGIN '../envstring';
INCLUDE '~beta/basiclib/private/betaenv_clrbody';
INCLUDE '~beta/dotnetlib/System/Environment';
--- expandEnvVarBody: descriptor ---
(#
   startInx,endInx: @integer;
   envVarString: ^String;
   envVar,envvarvalue: ^text;
do
   (if t[]<>NONE then
       theLoop: 
         (# 
         do  (* Find first '$' *)
             0 -> startInx;
             loop: '$' -> t.findAll(# do inx -> startInx; leave loop #);
             (if (startInx>0) then
                 (if (startInx+1<t.length) then
                     (if (startInx+1 -> t.inxGet) = '(' then
                         (* Find matching ')' *)
                         loop: ')' -> t.findAll
	                 (# 
	                 do (if (inx>startInx) then
	                        inx -> endInx; leave loop
	                    if);
	                 #);
                         (if (endInx>0) then
	                     (startInx+2,endinx-1) -> t.sub -> envVar[];
                             (*'Looking up '->puttext; envVar[]->putline;*)
                             envVar[] -> Environment.GetEnvironmentVariable -> envVarString[];
	                     (if envVarString[] = NONE then 
                                 envVar[] -> defaultValue -> envVarValue[];
                                 (if envVarValue[]=NONE then 
                                     &text[] -> envVarValue[] 
                                 if);
                              else
                                 envvarString[] -> envVarValue[];
                             if);
                             (startInx,endInx) -> t.delete;
	                     (envvarvalue[],startInx) -> t.insert;
	                     0 -> startInx -> endInx;
	                     restart theloop
                         if)
                     if)
                 if)
             if)
         #)
   if)
#)



