ORIGIN '../cstring';

(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)

--LIB: attributes--

stralloc: External
  (# size: @integer;
     ptr: @integer;
  enter size
     do 'malloc' -> callC
  exit ptr
  #);
strfree: External
  (# ptr: @integer
  enter ptr
  do 'free' -> callC;
  #);

--CStringInit: descriptor--
(# 
do (if length//0 then 256 -> stralloc -> charptr
    else length -> stralloc -> charptr
   if)
#)

--CStringSet: descriptor--
(# addr:@integer; 
do (if t[]//NONE then (failuretrace, 'Enter parameter to C string is NONE')->stop; if);
   t.length+1 -> init;
   charptr -> addr;
   t.scanall(# do (addr,ch) -> TOS'%putByte[0]'; addr + 1 -> addr #);
   (addr,0) -> tos'%putByte[0]';
#)

--CStringGet: descriptor--
(# len: @integer;
do &text[] -> t[];
   (if charptr//0 then else
       length -> len;
       len-t.T.range -> t.extend;
       (for i:len repeat 
            charptr + i-1 -> TOS'%adrGetByte' -> t.T[i];
       for);
       len -> t.lgth -> t.pos;
   if);
#)

--CStringGetChars: descriptor--
(# len: @integer;
do (if charptr//0 then else
       length -> len;
       len -> R.extend;
       (for i:len repeat 
            charptr + i-1 -> TOS'%adrGetByte' -> R[i];
       for);
   if)
#)

--CStringLength: dopart--
do (if charptr//0 then else charptr->strlen->value if) 

--CStringInxPut: dopart--
do (charptr+inx,ch) -> TOS'%putByte[0]' 

--CStringInxGet: dopart--
do charptr + inx -> TOS'%adrGetByte' -> ch 

--CStringFree: dopart--
do charptr -> strfree
