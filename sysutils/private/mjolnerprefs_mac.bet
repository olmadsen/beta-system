ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/maclib/v3.1/files';
INCLUDE '~beta/maclib/v3.1/folders';
INCLUDE '~beta/basiclib/v1.6/mbs_version';

-- lib: attributes --

mjolnerPrefsName:
	(# name: ^text;
	do &text[] -> name[];
		'Mjolner ' -> name.append;
		(mbs_version).desc -> name.append;
		' Prefs' -> name.append;
	exit name[]
	#);
	
saveBetaLib:
	(# location: ^text;
		error: @integer;
	enter location[]
	do (# spec: @FSSpec;
			vRefNum: @shortRef;
			dirID: @longInt;
			name: ^text;
			
			MPWCreator: (# exit (* 'MPS ' *) 0x4d505320 #);
			TextType: (# exit (* 'TEXT' *) 0x54455854 #);
			smSystemScript: (# exit -1 #);
			ignore: @integer;
			refnum: @shortRef;
			length: @longInt;
		do (kOnSystemDisk, kPreferencesFolderType, kCreateFolder, vRefNum[], dirID[])
				-> FindFolder -> error;
			(if error = 0 then
				mjolnerPrefsName -> name[];
				(vRefNum, dirID, name, spec[]) -> FSMakeFSSpec -> error;
				(if error = -43 then
					(spec[], MPWCreator, TextType, smSystemScript) -> FSpCreate -> error;
				if);
				(if error = 0 then
					(spec[], 0, refnum[]) -> FSpOpenDF -> error;
					(if error = 0 then
						(refnum, 0) -> SetEOF -> error;
						(if error = 0 then
							(if location.length > 0 then
								location.length -> length;
								(refnum, length[], @@location.T[1]) -> FsWrite -> error;
								refNum -> FSClose -> ignore;
							if);
						if);
					if);
				if);
			if);
		#);
	exit error
	#);
getBetaLib:
	(# location: ^text;
	do (# spec: @FSSpec;
			vRefNum: @shortRef;
			dirID: @longInt;
			name: ^text;
			ignore: @integer;
			error: @integer;
			refnum: @shortRef;
			length: @longInt;
		do '' -> location[];
			(kOnSystemDisk, kPreferencesFolderType, kCreateFolder, vRefNum[], dirID[])
				-> FindFolder -> error;
			(if error = 0 then
				mjolnerPrefsName -> name[];
				(vRefNum, dirID, name, spec[]) -> FSMakeFSSpec -> error;
				(if (error = 0) then
					(spec[], 0, refnum[]) -> FSpOpenDF -> error;
					(if error = 0 then
						(refnum, length[]) -> GetEOF -> error;
						(if error = 0 then
							length -> location.extend;
							(refnum, length[], @@location.T[1]) -> FSRead -> error;
							length -> location.lgth -> location.pos;
							refNum -> FSClose -> ignore;
						if);
					if);
				if);
			if);
		#);
	exit location[]
	#);
	
(*
-- program: descriptor --
(# error: @integer;
do getBetalib -> putLine;
	(if noOfArguments = 2 then
		2 -> arguments -> saveBetaLib -> error;
		(if error = 0 then
			(normal, NONE) -> stop;
		else
			(failure, NONE) -> stop;
		if);
	if);
#)
*)
