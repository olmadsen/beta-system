ORIGIN '../RepetitionObject';

MDBODY linux   'RepetitionObject_little'
       nti     'RepetitionObject_little'
       default 'RepetitionObject_big';

-- RepObjPutDP:dopart --
do (if (r[2] + 1 -> r[2]) > r.range then r.range -> r.extend if);
   (if r[2] - 1 > r[1] then r[2] - 1 -> r[1] if);
   
-- RepObjMakeSpaceDP:dopart --
do (if r[2] + nlongs > r.range then
       (r[2] + nlongs + 10 - r.range, r.range) -> max -> r.extend;
   if);

-- RepObjGetTextDP:dopart --
do &Text[] -> t[];
   getLong -> len -> t.lgth -> t.pos;
   (if len > t.T.range then len - t.T.range -> t.T.extend if);
   (@@t.T[1], @@r[r[2]], len) -> memcpy;
   r[2] + ((len + 3) div 4) -> r[2];
   
-- RepObjcheckGetTextDP:dopart --
do ((PeekLong < maxLen) 
   and (pos+((peekLong+3) div 4) <= size)) 
     -> value;
   
-- RepObjcheckGetDP:dopart --
do (pos <= size) -> value;
   
-- RepObjPutTextDP:dopart --
do t.length -> len -> putLong;
   (len + 3) div 4 -> wsize -> makeSpace;
   (@@r[r[2]], @@t.t[1], len) -> memcpy;
   r[2] + wsize -> r[2];
   (if r[2] - 1 > r[1] then r[2] - 1 -> r[1] if)

-- RepObjResetDP:dopart --
do firstPos -> r[2] 
   
-- RepObjInitDP:dopart --
do (initialSize, firstPos) -> (r[1], r[2])

-- RepObjcheckSizeDP:dopart --
do (if r[2] - 1 > r[1] then r[2] - 1 -> r[1] if)
