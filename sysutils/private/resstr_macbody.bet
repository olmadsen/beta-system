ORIGIN '~beta/basiclib/v1.6/betaenv';
-- lib: Attributes --
(*
 *  COPYRIGHT
 *    Copyright Mjolner Informatics, 1989-1996
 *    All rights reserved.
 *)
stringGetter:
  (# 
     strName: @text;
     theHandle,addr,length: @integer;
     Text2ResType:
       (# 
          const3: (# exit 16777216 #);
          const2: (# exit 65536 #);
          const1: (# exit 256 #);
          newtxt: [4] @Char;
          result: @Integer;
          
       enter newtxt
       do
          newtxt[1]*const3->result;
          (newtxt[2]*const2)+result->result;
          (newtxt[3]*const1)+result->result;
          newtxt[4]+result->result;
          
       exit result
       #);
     GetNamedResource: External
       (# theType: @integer; name: [1] @char; result: @integer; 
       enter (theType,name)
       do '$A9A1'->pascalTrap
       exit result
       #);
     ChangedResource: External
       (# handle: @integer enter handle do '$A9AA'->pascalTrap #);
     WriteResource: External
       (# handle: @integer enter handle do '$A9B0'->pascalTrap #);
     SetHandleSize: External
       (# h,newSize: @integer
       enter (h,newSize)
       do (*'SETHANDLESIZE'->*) callpascal
       #);
     getFromResourceFile:
       (# value: @text
       do
          ('STR '->Text2ResType,strName)->getNamedResource->theHandle;
          (if theHandle
           // 0 then (FAILURE,'Missing BETALIB-STR resource')->stop
          if);
          theHandle->tos'%adrGetLONG'->addr;
          addr->tos'%adrGetByte'->length;
            (# t: [length] @char
            do
               (for i: length repeat addr+i->tos'%adrGetByte'->t[i] for);
               t->value;
               
            #)
       exit value
       #);
     updateResource:
       (# value: @text
       enter value
       do
          (theHandle,value.length+1)->setHandleSize;
          theHandle->tos'%adrGetLONG'->addr;
          (addr,value.length)->tos'%PutByte[0]';
          (for i: value.length repeat
            (addr+i,(i->value.inxGet))->tos'%PutByte[0]'
          for);
          theHandle->changedResource;
          theHandle->writeResource;
          
       #)
  #);
  

