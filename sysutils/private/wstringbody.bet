ORIGIN '../wstring';
LIB_ITEM 'sysutilswstring';
(*
 * COPYRIGHT
 *       Copyright (C) Aarhus University
 *       All rights reserved.
 *)


--LIB: attributes--

malloc: External
  (# size: @integer;
     ptr: @integer;
  enter size
  exit ptr
  #);
cfree: External
  (# ptr: @integer
  enter ptr
  do 'free' -> callC;
  #);

--WStringInit: descriptor--
(# 
do (if length=0 then
       2*256 -> malloc -> ptr
    else 
       2*length -> malloc -> ptr
   if)
#)

--WStringSet: descriptor--
(# addr:@integer; 
do (if t[]=NONE then 
       (failuretrace, 'Enter parameter to wstring is NONE')->stop; 
   if);
   t.length+1 -> init;
   ptr -> addr;
   t.scanall(# do ch %putShortAt(addr); addr + 2 -> addr #);
   0 %putShortAt(addr);
#)

--WStringGet: descriptor--
(# len: @integer;
do &text[] -> t[];
   (if ptr<>0 then
       length -> len;
       len-t.T.range -> t.extend;
       (for i:len repeat 
             %GetShortAt(ptr+(i-1)*2) -> t.T[i];
       for);
       len -> t.lgth -> t.pos;
   if);
#)

--WStringGetChars: descriptor--
(# len: @integer;
do (if ptr<>0 then
       length -> len;
       len -> R.extend;
       (for i:len repeat 
            %GetShortAt(ptr+(i-1)*2) -> R[i];
       for);
   if)
#)

--WStringLength: dopart--
do (if ptr<>0 then 
       get:
         (if (%getShortAt(ptr+value*2))<>0 then 
             value+1->value; 
             restart get 
         if);
   if) 

--WStringInxPut: dopart--
do ch %putShortAt(ptr+inx*2)

--WStringInxGet: dopart--
do %GetShortAt(ptr + inx*2) -> ch 

--WStringFree: dopart--
do ptr -> cfree
