ORIGIN '../scanobjects';

(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1994-96
 *       All rights reserved.
 *)

INCLUDE '~beta/basiclib/v1.5/external';
INCLUDE '../objinterface';
--- lib:attributes ---

callbackPattern: external
  (# addr: @integer;
  enter addr
  do CExternalEntry;
     INNER;
  #);

bool2int:
  (# b: @boolean;
     i: @integer;
  enter b
  do (if b//true then 1->i else 0->i if);
  exit i
  #);

lowScanIOA: external
  (# printVisited,printOrigin,printSize,docallback: @integer;
     rootAddr: @integer;
     callback: ##callbackPattern;
  enter (printVisited,printOrigin,printSize,docallback,rootAddr,callback##)
  do 'lowScanIOA'->callC
  #);

lowScanAOA: external
  (# printVisited,printOrigin,printSize,docallback: @integer;
     rootAddr: @integer;
     callback: ##callbackPattern;
  enter (printVisited,printOrigin,printSize,docallback,rootAddr,callback##)
  do 'lowScanAOA'->callC
  #);

notyet: exception (* TEMPORARY *)
  (#
  do 'Sorry: not yet implemented!'->screen.putline;
  #);

--- scanIOA:descriptor ---
(#
   pv,po,ps,docallback: @integer;
   rootProto: @integer;
   a2o: @addressToObject;
   theCB: @callback;
   cbIOA: callbackPattern(# do addr->a2o->theCB #);
do
   INNER scanIOA;
   printVisited->bool2int->pv;
   printOrigin->bool2int->po;
   printSize->bool2int->ps;
   (callback##<scanCallback##)->bool2int->docallback;
   (if root## // NONE then 0->rootProto
    else root##->getProtoTypeForStruc->rootProto;
   if);
   doGC;
   (pv,po,ps,docallback,rootProto,cbIOA##)->lowScanIOA;
#)

--- scanAOA:descriptor ---
(#
   pv,po,ps,docallback: @integer;
   rootProto: @integer;
   a2o: @addressToObject;
   theCB: @callback;
   cbAOA: callbackPattern(# do addr->a2o->theCB #);
do
   INNER scanAOA;
   printVisited->bool2int->pv;
   printOrigin->bool2int->po;
   printSize->bool2int->ps;
   (callback##<scanCallback##)->bool2int->docallback;
   (if root## // NONE then 0->rootProto
    else root##->getProtoTypeForStruc->rootProto;
   if);
   doGC;
   (pv,po,ps,docallback,rootProto,cbAOA##)->lowScanAOA;
#)

--- scanLVRA:descriptor ---
(#
do notyet;
#)
