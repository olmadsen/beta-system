ORIGIN '../envstring';
(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)

--- expandEnvVarBody: descriptor ---
(#
   
   getenv: External  
     (* Get pointer to value of an environment variable *)
     (# var: [1]@char;
        Ccharpointer: @integer;
     enter var
     do callC
     exit Ccharpointer
     #);
   
   getenvstring: External
     (* Get text value of an environment variable *)
     (# var: [1]@char;
        result: [1]@char
     enter var
     do 'getenv' -> callC
     exit result
     #);
   
   startInx,endInx: @integer;
   envVar,envvarvalue: ^text
     
do
   (if t[]<>NONE then
       theLoop: 
         (# 
         do  (* Find first '$' *)
             0 -> startInx;
             loop: '$' -> t.findAll(# do inx -> startInx; leave loop #);
             (if (startInx>0) then
                 (if (startInx+1<t.length) then
                     (if (startInx+1 -> t.inxGet) = '(' then
                         (* Find matching ')' *)
                         loop: ')' -> t.findAll
	                 (# 
	                 do (if (inx>startInx) then
	                        inx -> endInx; leave loop
	                    if);
	                 #);
                         (if (endInx>0) then
	                     (startInx+2,endinx-1) -> t.sub -> envVar[];
	                     (if (envVar -> getenv) = 0 then 
                                 envVar[] -> defaultValue -> envVarValue[];
                                 (if envVarValue[]=NONE then 
                                     &text[] -> envVarValue[] 
                                 if);
	                      else 
                                 &text[] -> envVarValue[];
                                 envVar -> getenvstring -> envvarvalue;
                             if);
                             (startInx,endInx) -> t.delete;
	                     (envvarvalue[],startInx) -> t.insert;
	                     0 -> startInx -> endInx;
	                     restart theloop
                         if)
                     if)
                 if)
             if)
         #)
   if)
#)



