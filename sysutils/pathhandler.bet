ORIGIN '~beta/basiclib/v1.5/betaenv';
MDBODY   mac     'private/pathhandler_macbody'
nti     'private/pathhandler_ntbody'
default 'private/pathhandler_unixbody';

(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)

-- LIB: attributes --

(* idx=2 *)

(* Utility patterns *)
DirectoryChar:
  (* The character which is used in this operating system to merge local
   * names to full path-names. Placed here to allow use without having
   * an instance of pathhandler.
   *)
  (# c: @char;
  <<SLOT PathHandlerDirectoryChar: dopart>>
  exit c
  #);
CurrentDirectory: 
  (* The current working directory of this process.
   * Placed here to allow use without having an instance of pathhandler.
   *)
  (# cwd: ^text; 
  <<SLOT PathHandlerCurrentDirectory: dopart>>
  exit cwd[]
  #);
scanSearchPath:
  (* Scan through a list of paths separated by the delimiter
   * specified. INNER is called for each path found in
   * the list.
   * If pathlist is none, the default search path of the machine.
   * If delimiter is 0, the default deliminter for searchpaths
   * is used.
   *)
  (# pathlist: ^text; (* list if paths seperated by delimiter *)
     delimiter: @char;
     path: ^text; (* pathlist component set before each call of INNER *)
  enter (pathlist[], delimiter)
  <<SLOT PathHandlerScanSearchPath: dopart>>
  #);

PathHandler: 
  (*
   *  This pattern converts the Mjolner path notation into a full UNIX path.
   *
   *  The following abbreviations are handled:
   *
   *      ~    is expanded into the home directory of the user
   *      ~foo is expanded into the home directory of foo
   *      .    is interpreted relatively to another path 
   *           (see ConvertFilePath below)
   *      ..   is interpreted relatively to another path 
   *           (see ConvertFilePath below)
   * 
   *      Environment variables like $(HOME) are expanded within paths.
   *
   *  PathHandler examines two environment variables:
   *      
   *      BETALIB. If set, this determines the home directory of the Mjolner 
   *               BETA system. If not set, the home directory defaults to 
   *               /usr/local/lib/beta.
   * 
   *      AUTOMOUNTDIR.
   *               If set, this designates a path prefix to strip off the paths
   *               manipulated by THIS(PathHandler). 
   *               On some unix systems, directories like user homedirectories 
   *               are automatically mounted by need, and a link from the mount
   *               point to the actual directory is created. The link is 
   *               usually placed in a certain directory, e.g. '/a', and named
   *               identically to the actual directory.
   *               Since the automatic mount is not maintained unless needed, 
   *               it may be an error to access the directory via the link.
   *               In this situation the correct name to access the directory
   *               is obtained by stripping off the name of the automount 
   *               directory, in the example '/a'. This can be handled by 
   *               THIS(PathHandler) by setting AUTOMOUNTDIR to /a.
   * 
   *      The values of both environment variables may, but need not, be
   *      terminated by '/'.
   *)
  (# 
     <<SLOT PathHandlerLib: attributes>>;
     
     BetaLib: (* BETA Mjolner directory *)
       (# value: ^text; 
       <<SLOT PathHandlerBetaLib: dopart>>
       exit value[]
       #);
      
     CurrentDirectory: (* The current working directory of this process *)
       (# value: ^text; 
       <<SLOT PathHandlerCurDir: dopart>>
       exit value[]
       #);

     ConvertFilePath:<
       (* Convert the 'path' relative to 'basis'.  Within 'path' the
        * conversion expands the abbreviations mentioned above.  
        * '..' is a relative path designating the parent of a directory 
        * (if any) whereas '.' is a relative path designating the
        * directory itself.  If path starts with ~, this is expanded
        * to the home directory of the user, and if it starts with
        * ~foo, this is expanded to the home directory of the user
        * foo. These expansions ignore basis.  
        * These abbreviations are  only expanded if they are the first 
        * component(s) of 'path'.
        * The only meaningful abbreviation in 'basis' is that it may
        * start with '.' Using this, 'basis' may be specified as the
        * relative path '.' instead of the absolute path
        * THIS(PathHandler).CurrentDirectory.  
        * If both the path and the basis are relative paths, the result 
        * will be a relative path, otherwise it wil be an absolute path. 
        * Path and basis may be terminated by '/', but need not be.
        *)
       (# path,basis, convertedpath: ^text;
          NoFather:< Exception
            (* Raised if there are too many '..' in the beginning of 'path'
             * compared to the given (absolute) basis.
             * If basis is relative, excess '..'s will be converted to '..'
             *)
            (# <<SLOT ConvertFileNameNoFather: dopart>> #);
          convert: @<<SLOT ConvertFilePathBody: descriptor>>
       enter(path[],basis[])
       do convert;
          INNER
       exit convertedpath[]
       #);
     
     DirectoryChar: @char 
       (* The character which is used in this operating system to merge local
        * names to full path-names; '/' in unix, ':' on Macintosh
        *);
     
     localPath: 
       (* Given a path and a basis, exit the local path relative to the basis, 
        * if possible. The local path will be a path relative to basis, if
        * basis is a prefix of path. If this is not the case, it is attempted 
        * to use some of the paths allready converted by THIS(PathHandler) as
        * prefix to path. In this case the result may be a relative path, or 
        * it may be a path in the form ~foo.
        * If it is not possible to find any legal prefix, the exception 
        * LocalPathException is raised.
        *)
       (# path,basis: ^text;
          localName: ^text;
          LocalPathException:< Exception
            (# <<SLOT LocalPathException: dopart>> #);
          localize: @<<SLOT LocalPathBody: descriptor>>;
       enter (path[],basis[])
       do localize
       exit localName[]
       #);
     
     init:< 
       (# <<SLOT pathHandlerInit: dopart>> #);
     
     Private: @<<SLOT pathHandlerPrivate: descriptor>>;
     
  #);

FilenameConverter: PathHandler
  (#
     (* This pathhandler is capable of keeping track of files which have 
      * allready been "converted".
      * 
      * convertFilePath will return the same pathname every time the same 
      * file is accessed even if the absolute path-names differs.
      *  
      * E.g. if /users/beta/basiclib/current is a link to the directory 
      * ~cn/beta/basiclib then
      *       '/users/beta/basiclib/current/file.bet' -> convertFilePath -> ...
      * and 
      *       '~cn/beta/basiclib/file.bet' -> convertFilePath -> ...
      *  
      * will give the same result, and the result will be the exit-parameter of
      * the first executed call of "convertFilePath".
      * Notice that if the file itself is a link to another file, these two
      * disk entries are considered different.
      * Notice also that in this special pathhandler, it is significant whether
      * the path are terminated by '/' or not: If it is, the path is considered
      * a directory.
      *)
     
     <<SLOT FileNameConverterLib: attributes>>;
     
     convertFilePath::<
       (# fileNotFound:< (# do INNER #);
          convert: @<<SLOT FCConvertFilePath: descriptor>>
       do convert
       #);
     
     init::<
       (# <<SLOT FCInit: dopart>> #); 
     
     FCPrivate: @<<SLOT FCPrivate: descriptor>>
  #)
