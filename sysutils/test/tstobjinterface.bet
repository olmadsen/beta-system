ORIGIN '~beta/basiclib/v1.6/betaenv';

INCLUDE '~beta/sysutils/v1.6/objinterface';
INCLUDE '~beta/sysutils/v1.6/cstring';
INCLUDE '~beta/containers/v1.6/hashTable';

--- program:descriptor ---
(# pt: @prototype;
   pref: @prototype;
   
   localInt: @IntegerObject 
     (# vtype:< Text;
        virtual: @vtype; 
        stat: @<<SLOT localslot:descriptor>>;
        dyn: ^Text;
        realstat: @IntegerObject;
     #);
   
   current: ^group_header;
   id: @cstring;
   
   h: @HashTable;
do
   'Groups in executable: ' -> putLine; newline;
        
   loop:
     (if current[] -> NextGroup -> current[] //NONE then else
         current[] -> NameOfGroup -> id;
         id.get -> screen.putLine;
         restart loop;
     if); 
        
   newline;
        
   (for i:2 repeat
        newline;
        (if i 
         //1 then
            (* localInt[] -> getProtoType -> pt; *)
            &h.theCellType[] -> getProtoType -> pt;
         //2 then
            localInt## -> getProtoTypeForStruc -> pt;
        if);
        
        'scanRefs: ' -> putLine; newline;
        pt.scanRefs
        (# tpm: @prototype;
        do
           'offset = ' -> putText;
           thisOffset -> putInt;
           '; ' -> putText; 
           'thisAddr = ' -> putText;
           thisAddr -> putInt;
           '; ' -> putText; 
           (if isStatic //true then
               'static reference to ' -> putText;
               thisProto -> pref;
               pref.labId -> putLine;
            else
               (if refType
                //REFTYPE_DYNAMIC then
                   'dynamic reference' -> putLine;
                //REFTYPE_OFFLINE then
                   'offline static reference' -> putLine;
                //REFTYPE_ORIGIN then
                   'origin reference' -> putLine;
               if);
           if);
        #);
        newline;
        
        'GCtab = ' -> putText;
        pt.GCtab -> putInt;
        newline;
        'OriginOff = ' -> putText;
        pt.OriginOff -> putInt;
        newline;
        'GPart = ' -> putText;
        pt.GPart -> putInt;
        newline;
        'Size = ' -> putText;
        pt.Size -> putInt;
        newline;
        'LabId = ' -> putText;
        pt.LabId -> putLine;
        'Prefix labId = ' -> putText;
        pt.Prefix -> pref;
        pref.labId -> putLine;
        'GroupId = ' -> putText;
        pt.GroupId -> putLine;
        'astIndex = ' -> putText;
        pt.astIndex -> putInt;
        newline;
        'formIndex = ' -> putText;
        pt.formIndex -> putInt;
   for)
#)
   
--- localslot:descriptor ---
(# 
#)
