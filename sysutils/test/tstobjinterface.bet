ORIGIN '~beta/basiclib/betaenv';

INCLUDE '~beta/sysutils/objinterface';
INCLUDE '~beta/sysutils/cstring';
INCLUDE '~beta/containers/hashTable';

--- program:descriptor ---
(# pt: @prototype;
   pref: @prototype;
   
   localInt: @IntegerObject 
     (# vtype:< Text;
        virtual: @vtype; 
        stat: @<<SLOT localslot:descriptor>>;
        dyn: ^Text;
        realstat: @IntegerObject;
     #);
   
   current: ^group_header;
   id: @cstring;
   obj: ^object;
   
   h: @HashTable;
do
   'Groups in executable: ' -> putLine; newline;
        
   loop:
     (if current[] -> NextGroup -> current[] //NONE then else
         current[] -> NameOfGroup -> id;
         id.get -> screen.putLine;
         restart loop;
     if); 
        
   newline;
        
   (for i:2 repeat
        newline;
        '---------------- ' -> puttext;
        (if i 
         //1 then
            (if true then
                localInt[] -> obj[] -> getProtoType -> pt;
             else
                &h.theCellType[] -> obj[] -> getProtoType -> pt;
            if);
            'Using getProtoType:' ->putline;
         //2 then
            localInt## -> getProtoTypeForStruc -> pt;
            localint[] -> obj[];
            'Using getProtoTypeForStruc:' ->putline;
        if);
        
        (* demo/objinterface/objint included here: *)
        '\nPrint of obj: ' -> puttext;
        (obj[], screen[]) -> printobject;
        '\nFound using the prototype of obj:' -> putline;
        '\tPattern name:     ' -> puttext; obj[] -> getPatternName -> putline;
        '\tGC table:         ' -> puttext; pt.GCtab -> putint; newline;
        '\tOrigin Offset:    ' -> puttext; pt.OriginOff -> putint; newline;
        '\tGeneration Part:  ' -> puttext; pt.Gpart -> putint; newline;
        '\tSize (longs):     ' -> puttext; pt.size -> putint; newline;
        '\tPrefix labId:     ' -> puttext; 
        pt.prefix -> pref.address; pref.LabId -> putline;
        '\tLocated in file:  ' -> puttext; pt.groupId -> putline;
        
        '\tLabId:            ' -> puttext; pt.labId -> putline;
        newline;
        pt.scanRefs
        (# 
        do (if isStatic
            // true  then '\tStatic reference at offset  ' -> puttext;
            // false then '\tDynamic reference at offset ' -> puttext;
           if);
           thisOffset -> putint; newline;
        #);
        
        'GC field (using getGCfield) = ' -> putText;
        obj[] -> getGCfield -> putInt;
        newline;
        
        'Origin: ' -> puttext;
        obj[] -> getOrigin -> getPatternName -> putline;

        'scanRefs: ' -> putLine; newline;
        pt.scanRefs
        (# tpm: @prototype;
        do
           'offset = ' -> putText;
           thisOffset -> putInt;
           '; ' -> putText; 
           'thisAddr = ' -> putText;
           thisAddr -> putInt;
           '; ' -> putText; 
           (if isStatic //true then
               'static reference to ' -> putText;
               thisProto -> pref;
               pref.labId -> putLine;
            else
               (if refType
                //REFTYPE_DYNAMIC then
                   'dynamic reference' -> putLine;
                //REFTYPE_OFFLINE then
                   'offline static reference' -> putLine;
                //REFTYPE_ORIGIN then
                   'origin reference' -> putLine;
               if);
           if);
        #);
        newline;
        
        'GCtab = ' -> putText;
        pt.GCtab -> putInt;
        newline;
        'OriginOff = ' -> putText;
        pt.OriginOff -> putInt;
        newline;
        'GPart = ' -> putText;
        pt.GPart -> putInt;
        newline;
        'Size = ' -> putText;
        pt.Size -> putInt;
        newline;
        'LabId = ' -> putText;
        pt.LabId -> putLine;
        'Prefix labId = ' -> putText;
        pt.Prefix -> pref;
        pref.labId -> putLine;
        'GroupId = ' -> putText;
        pt.GroupId -> putLine;
        'astIndex = ' -> putText;
        pt.astIndex -> putInt;
        newline;
        'formIndex = ' -> putText;
        pt.formIndex -> putInt;
        
        newline;
   for)
#)
   
--- localslot:descriptor ---
(# 
#)
