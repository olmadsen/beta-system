ORIGIN '~beta/distribution/basicshell';
INCLUDE 'notification'
        'notificationmanager';
(* This file is included by the sharedpersistentstore - library
 * in order to send notifications to the PersistentStoreManager.
 *)
-- shellEnvLib: Attributes --
notificationmanagement:
  (#
     server: ^notificationmanager;
     serverEnsemblename: ^text;
     serverensemble: ^ensemble;
     init:
       (# s: ^notificationmanager; n: ^text; 
       enter (s[],n[])
       do s[]->server[]; n[]->serverEnsemblename[]
       #);
     notify:
       (#
          c: ^NPSclient;
          user: ^text;
          PSname: ^text;
          type: @integer;
          thenotification: ^notification
       enter (c[],user[],PSname[],type)
       do
          notifyserver: theNoterrorhandler
            (# 
            do
               (c[],user[],PSname[],type)->sendPSnotification
                 ->theNotification[];
               true->successful;
               (c[],theNotification[])->server.notify;
               (if not successful then restart notifyserver if)
            #)
       #);
     theNotErrorhandler: errorhandler
       (#
          successful: @boolean;
          onFailure:
            (# theError: ^error; 
            enter theError[]
            do
               (if theError.theObj## <= NotificationManager## then
                   getHost:
                     (# 
                     do
                        none ->serverensemble[];
                        (ensemble##,serverEnsembleName[])
                          ->theShell.myEnsemble.ns.get
                            (#
                               notFound:: 
                                 (# 
                                 do
                                    serverEnsembleName[]->screen.putText;
                                    ' not found. '->putline;
                                    
                                 #)
                            #)->serverEnsemble[];
                        (if serverEnsemble[]
                         // none then
                            'Please, start new Ensemble on '->puttext;
                            serverEnsemblename[]->putline;
                            5->sleep;
                            restart gethost
                        if);
                        
                     #);
                   getserver:
                     (# 
                     do
                        (NotificationManager##,'Notificationmanager')
                          ->serverEnsemble.ns.get
                            (#
                               notFound:: 
                                 (# 
                                 do
                                    'No Notificationmanager on '
                                      ->screen.putText;
                                    serverEnsembleName[]->screen.putLine;
                                    'Please, restart new server on '->puttext;
                                    serverEnsemblename[]->putline;
                                    5->sleep;
                                    restart getserver;
                                    
                                 #)
                            #)->server[];
                        
                     #);
                   theError.ignore
                else
                   theError.abort; 
               if)
            #);
          connectionBroken::< 
            (# 
            do false->successful; INNER ; THIS(connectionBroken)[]->onFailure
            #);
          connectionFailed::< 
            (# 
            do false->successful; INNER ; THIS(connectionFailed)[]->onFailure
            #);
          timeOut::< 
            (# 
            do false->successful; INNER ; THIS(timeOut)[]->onFailure
            #);
          timeOutValue::<  (#  do INNER #)
       do INNER
       #)
  #)  

