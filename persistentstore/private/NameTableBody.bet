ORIGIN '../NameTable';
(* 
 * $RCSfile: NameTableBody.bet,v $ $Revision: 1.3 $ $Date: 1994-04-28 17:55:32 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- lib:attributes ---

hashFunction: 
  (# t: ^Text; inx, value: @Integer;
  enter t[]
  do 0 -> value;
     loop:
       (for i: 7 repeat
            t.lgth-i+1 -> inx;
            (if inx < 1 // true then leave loop if);
            t.T[inx]+value->value;
       for)
  exit value
  #)

--- saveName:descriptor ---
(# inx: @Integer;
do
   ((new.name[] -> hashFunction) MOD 97) + 1 -> inx;
   
   Table[inx][] -> new.next[];
   new[] -> Table[inx][];
#)

--- findName:descriptor ---
(# inx: @Integer;
do
   ((name[] -> hashFunction) MOD 97) + 1 -> inx;
   Table[inx][] -> found[];
   
   loop:
     (if found[] //NONE then else
         (if name[] -> found.name.equal //true then
             leave loop;
         if);
         found.next[] -> found[];
         restart loop;
     if);
#)

--- NameTableScan:descriptor ---
(# 
do
   (for i: 97 repeat
        table[i][] -> current[];
        loop:
          (if current[] //NONE then else
              INNER Scan;
              current.next[] -> current[];
              restart loop;
          if);
   for);
#)
