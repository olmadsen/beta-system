ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/regexp';
(* File samefile.bet
 * =================
 * This file contains the method 'samefile', which exits true, if
 * the two entered files are the same and false otherwise. This library 
 * is used instead of file1name[]->file2name.equal because the
 * same file can have different names dependent on the machine, where a
 * process starts.
 * F.eks. /a/home/scandium9/jojo/speciale/pstore1
 * and    ~jojo/speciale/pstore1 are the same files, but would result
 * in false, if using the .equal operation.
 *)
-- lib: Attributes --
cutafterusers:
  (# path1,path2: ^text; regex: ^text; 
  enter path1[]
  do
     path1.copy->path2[];
     0->path2.pos;
     '/users/'->regex[];
     (if
     regex[]
       ->path2.regexp_search
         (# posToMatchEnd::<  (#  do true->value #); ; 
         do (if (0->regs.start) >= 0 then (1,0->regs.end)->path2.delete;  if)
         #) then
         
      else
         '/a/home/[^/]*/'->regex[];
         (if
         regex[]
           ->path2.regexp_search
             (# posToMatchEnd::<  (#  do true->value #); ; 
             do
                (if (0->regs.start) >= 0 then
                    (1,0->regs.end)->path2.delete; 
                if)
             #) then
             
          else
             
         if);
         
     if);
     
  exit path2[]
  #);
cutPath: (* removes everything before the last '\' or '/' including that char *)
  (#
     path1: ^text;
     charIndex: (* the index in path1 of the last '/' or '\' *) @integer
  enter path1[]
  do
     - 1->charIndex;
     '/'
       ->path1.findAll
         (#  do (if inx > charIndex then inx->charIndex if) #);
     '\\'
       ->path1.findAll
         (#  do (if inx > charIndex then inx->charIndex if) #);
     (if charIndex > - 1 then
         (charIndex+1,path1.length)->path1.sub->path1[]
     if)
  exit path1[]
  #);
sameFile: (* Ignores the _path_ of the file *) booleanValue
  (# path1,path2: ^text; 
  enter (path1[],path2[])
  do
     path1[]->cutPath->path1[];
     path2[]->cutPath->path2[];
     (if path1[]->path2.equalNCS then true->value;  else false->value;  if)
  #);
sameFileUnix:
(* Simple routine to test if two files are identical from
 their names. Works on UNIX *) booleanValue
  (# path1,path2: ^text; 
  enter (path1[],path2[])
  do
     path1[]->cutafterusers->path1[];
     path2[]->cutafterusers->path2[];
     (if path1[]->path2.equalNCS then true->value;  else false->value;  if);
     debugInfo:
       (# 
       do
          (if false then
              'sps.samefile: "'->puttext;
              path1[]->puttext;
              '" and "'->puttext;
              path2[]->puttext;
              '" are'->puttext;
              (if value then
                  ' the same'->putline
               else
                  ' NOT the same!!!'->putline
              if)
          if)
       #)
  #);
  

