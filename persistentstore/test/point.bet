ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/random';
INCLUDE '~beta/guienv/private/datastructures/sequence';

-- lib: attributes --

tickcount: external
  (# tick: @integer;
  do 'getmilisectimestamp' -> callC;
  exit tick
  #);
Database:
  (# top: ^Point;
  #);

Point:
  (# x, y: @integer;
     next: ^Point;
  #);

MakePointList:
  (# n: @integer;
     top: ^Point;
     new: ^Point;
     before, after: @integer;
  enter n
  do tickcount -> before;
     (87, 666) -> setall;
     (for n repeat
          &Point[] -> new[];
          (1024, 32000) -> ignuin -> new.x;
          top[] -> new.next[];
          new[] -> top[];
     for);
     tickcount -> after;
     'pointlist created in: ' -> puttext;
     after - before -> putint;
     newline;
  exit top[]
  #);

Sort:
  (# top: ^Point;
     seq: @sequence
       (# Element:: Point;
       #);
     current: ^Point;
     append: @seq.append;
     one, two, three, four: @integer;
  enter top[]
  do tickcount -> one;
     seq.init;
     top[] -> current[];
     loop:
       (if current[] <> NONE then
           current[] -> append;
           current.next[] -> current[];
           restart loop;
       if);
     tickcount -> two;
     seq.sort
     (# less::
          (#
          do left.x > right.x -> value;
          #);
     #);
     tickcount -> three;
     
     (for inx: 10 repeat
	 	(inx -> seq.get).x -> putint;
		' ' -> put;
	 for);
	 newline;
     'list => sequence: ' -> puttext;
     two - one -> putint;
     newline;
     'sort: ' -> puttext;
     three - two -> putint;
     newline;
     'total: ' -> puttext;
     three - one -> putint;
     newline;
	 
  #);
