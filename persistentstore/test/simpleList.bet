ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/persistentstore/persistentstore';
-- program: Descriptor --
(#
   link:
     (#
        next: ^link;
        name: @text;
        key: @integer;
        print:
          (# 
          do
             'name: '->putText;
             name[]->putText;
             ', key: '->putText;
             key->putInt;
             putLine
          #)
     #);
   buildList:
     (# head,tail: ^link; size,current: @integer
     enter (head[],size)
     do
        (if size > 0 then
            head[]->tail[];
            0->current;
            Loop:
              (# 
              do
                 'link'->tail.name;
                 current->tail.key;
                 current+1->current;
                 (if current < size then
                     &link[]->tail.next[]; tail.next[]->tail[]; restart Loop
                  else
                     leave Loop
                 if)
              #)
        if)
     exit (tail[])
     #);
   printList:
     (# head: ^link
     enter head[]
     do
        'Starting print list'->putLine;
        Loop:
        (if head[] <> none then
            'Head is not none'->putLine;
            head.print;
            head.next[]->head[];
            restart Loop
         else
            'Head is none'->putLine; leave Loop
        if)
     #);
   PS: @persistentStore;
   tail,head: ^link;

do
   PS.init;
   'simpleListStore' -> PS.openWrite
   (# notFound :: (# do 'simpleListStore' -> PS.create; true -> continue #) #);
   
   ('root', link##) -> PS.get
   (# notFound :: 
        (# 
        do &link[] -> head[];
           (head[], 'root') -> PS.put;
           (head[],1000)->buildList->tail[];
           head[] -> theObject[];
           true -> continue
        #)
   #) -> head[];
   head[] -> printList;
   PS.close
#)  

