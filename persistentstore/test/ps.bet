ORIGIN '~beta/basiclib/betaenv';

INCLUDE '~beta/persistentstore/persistentstore';
INCLUDE 'person';

--- program: descriptor ---

(# p,q: ^person;
   PS: @persistentstore
     (# allowLazyFetch:: (# do true -> value #);
        maxCountonDanglerhit:: (# do 4 -> value #);
        onDanglerHit:: (# do 'Dangler hit!!' -> putLine #);
        afterDanglerhit:: (# do 'Got it!' -> putLine #);
     #);
   
do PS.init;
   &person[] -> p[];
   'root' -> p.name[];
   (for i: 5 repeat
        &person[] -> q[];
        'friend' -> q.name[]; 
        q[] -> p.add;
        q.name.length -> q.name.setPos; 
        i -> q.name.putInt;
   for);
   
   'persons' -> PS.create
   (# alreadyOpen:: (# do true -> continue #);
      exists:: (# do true -> continue #);
   #);
   (p[], 'p') -> PS.put;
   PS.close;
   PS.init;
   (* get it again *)
   
   'persons' -> PS.openRead;
   ('p', person##) -> PS.get -> p[];
   
   'Got ' -> putText; p.name[] -> putLine;
   'Friends:' -> putLine;
   p.scan(# do current.name[] -> putLine #);
   
   newLine;
   'Bye bye' -> putLine;
#)
