ORIGIN '~beta/basiclib/v1.4/betaenv';

--- lib: attributes ---

person:
  (# name: ^Text;
     friends: [5] ^person;
     count: @Integer;
     persistent: @Boolean;
     locate:
       (# name: ^Text;
          inx: @Integer;
       enter name[]
       do L: (for i: count repeat
                  (if friends[i].name[] -> name.equalNCS // true then
                      i -> inx; leave L;
                  if);
             for);
          INNER;
       #);
     add:
       (# p: ^person;
       enter p[]
       do p.name[] -> locate
          (# 
          do (if inx // 0 then          
                 (if count // friends.range then count -> friends.extend if);
                 p[] -> friends[count+1 -> count][];
              else p[] -> friends[inx][];
             if);
          #);
       #);
     find: locate
       (# p: ^person;
       do (if inx // 0 then NONE -> p[] else friends[inx][] -> p[] if);
       exit p[]
       #);
     remove: locate
       (# 
       do (if inx // 0 then else
              friends[count][] -> friends[inx][];
              NONE -> friends[count][];
              count-1 -> count;
          if);
       #);
     scan:
       (# current: ^person;
       do (for i:count repeat friends[i][] -> current[]; INNER for);
       #);
     list: scan
       (# any: @Boolean;
       do true -> any; current.name[] -> puttext;
          (if current.persistent // true then '*' -> putLine else newLine if);
       exit (# do (if any // false then 'none.' -> putLine if) exit any #)
       #);
     parent:< (# p: ^person do NONE -> p[]; INNER exit p[] #);
     child: person(# parent:: (# do this(person)[] -> p[] #) #);
  #);
