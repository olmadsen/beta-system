ORIGIN '~beta/distribution/basicshell';
(* File clientinterface.bet
 * ========================
 * 
 * PSClient
 * 
 * This file describes the interface to the clients using the 
 * shared PS. The program nonotclient is an example 
 * implementation of this client interface. 
 * The PSClient pattern is subclassed in the notificationlib.bet
 * file in order to yield the NPSClient pattern, used instead 
 * PSClient when the client wants to subscribe to notifications.
 * The program notclient is an example implementation of a client subscribing
 * to UPDATE notifications.
 *)
-- shellEnvLib: Attributes --
clientShell: remoteAble
  (#
     onClientStart:< (# do INNER #);
     getMyEnsemble: entry
       (# value: ^ensemble
       do clientShellPrivate.myEnsemble[]->value[]
       exit value[]
       #);
     start: (#  do clientShellPrivate.start #);
     clientShellPrivate: @
       (#
          myEnsemble: ^ensemble;
          started: @Boolean;
          clientShellThread: @|system (#  do onClientStart #);
          start:
            (# 
            do
               (if not started then
                   true->started;
                   THIS(shellEnv).theShell.myEnsemble[]
                     ->myEnsemble[];
                   (if myEnsemble[] = none then
                       (failure,'Could not get client ensemble.')->stop;
                   if);
                   clientShellThread[]->fork;
                   
                else
                   (failure,'Attempt to restart shell.')->stop
               if)
            #)
       #)
  #);
PSclient: clientShell
  (#
     ping: entry (#  do pingImpl;  #);
     pingImpl:< (#  do INNER ;  #);
     registerClient: entry (#  do registerClientImpl;  #);
     registerClientImpl:<
       (# 
       do
          (THIS(PSclient)[],THIS(PSclient).ri.shellAdr.astext)
            ->(getMyEnsemble).ns.put
          (# overWrite::  (#  do true->value #) #);
          INNER
       #)
  #);
write: (#  exit 1 #);
read: (#  exit 0 #);
shared: (#  exit 1 #);
exclusive: (#  exit 0 #);


