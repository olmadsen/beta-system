ORIGIN '~beta/basiclib/v1.4/betaenv';
INCLUDE '~beta/persistentstore/v1.4.2/persistentstore';
INCLUDE 'persistentGC';
INCLUDE 'textHash';

(* This fragment is an example usage of the simple "persistentGC.bet"
 * fragment. This program reads the hashtable saved by "largeWrite.bet",
 * deletes half the elements from the table. Finally the persistentstore
 * containing the table is garbage collected. *)

--- program:descriptor ---
(# ps: @persistentstore;
   th: ^TextHashTable;
do
   'largeStore' -> ps.openWrite;
   
   'Getting...' -> screen.putText;
   ('TextTable', textHashTable##) -> ps.get -> th[];
   'done' -> screen.putLine;
   
   'Deleting half the elements...' -> screen.putText;
   th.scan
   (# delete: @Boolean;
   do (if not delete -> delete //true then
          current[] -> th.delete;
      if);
   #);
   'done' -> screen.putLine;
   
   'Closing and checkpointing...' -> screen.putText;
   ps.close;
   'done' -> screen.putLine;
   
   'Garbage collecting...' -> screen.putText;
   'largeStore' -> persistentGC;
   'done' -> screen.putLine;
#)
