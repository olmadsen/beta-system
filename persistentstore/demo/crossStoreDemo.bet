ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'carsAndPersons'
'~beta/containers/arrayContainer'
'~beta/persistentstore/persistentstore';
-- program: Descriptor --
(#
   addNewPerson:
     (# persons: ^personList; aPerson: ^person; number: @integer
     enter (persons[],number)
     do
        &person[]->aPerson[];
        'John Doe '->aPerson.name;
        number->aPerson.name.putInt;
        (aPerson[],number)->persons.put
     #);
   addNewCar:
     (# cars: ^carList; aCar: ^car; number: @integer
     enter (cars[],number)
     do
        &car[]->aCar[];
        'Porsche'->aCar.vendor;
        1000->aCar.weight;
        (aCar[],number)->cars.put
     #);
   personList: arrayContainer (# element:: person #);
   carList: arrayContainer (# element:: car #);
   persons: ^personList;
   cars: ^carList;
   personStore: @persistentstore
     (# openpstore::
          (# do 'personStore opened by system' -> screen.putLine; 
          #);
     #);
   carStore: @persistentstore
     (# openpstore::
          (# do 'carStore opened by system' -> screen.putLine; 
          #);
     #);
   size: @integer
do
   carStore.init;
   personStore.init;
   'personStore'
     ->personStore.openWrite
   (#
      notFound:: 
        (#  do 'personStore'->personStore.create; true->continue #)
   do 'Opening personStore' -> screen.putLine;
   #);
   'carStore'
     ->carStore.openWrite
   (#
      notFound:: 
        (#  do 'carStore'->carStore.create; true->continue #)
   do 'Opening carStore' -> screen.putLine;
   #);
   10->size;
   ('root',personList##)
     ->personStore.get
   (#
      notFound:: 
        (# 
        do
           &personList[]->persons[];
           persons.init;
           (persons[],'root')->personStore.put;
           (for i: size repeat (persons[],i)->addNewPerson for);
           persons[]->theObject[];
           true->continue
        #)
   #)->persons[];
   personStore.checkpoint;
   ('root',carList##)
     ->carStore.get
   (#
      notFound:: 
        (# 
        do
           &carList[]->cars[];
           cars.init;
           (cars[],'root')->carStore.put;
           (for i: size repeat (cars[],i)->addNewCar for);
           cars[]->theObject[];
           true->continue
        #)
   #)->cars[];
   carStore.checkpoint;
   (for i: size repeat
        (# aPerson: ^person; aCar: ^car
        do
           i->persons.get->aPerson[];
           i->cars.get->aCar[];
           aCar[]->aPerson.myCar[]
        #)
   for);
   (for i: size repeat
        (# aPerson: ^person
        do i->persons.get->aPerson[]; aPerson.print; screen.newLine
        #)
   for);
   personStore.close(# do 'Closing personStore' -> screen.putLine; #);
   carStore.close(# do 'Closing carStore' -> screen.putLine; #);
#)  

