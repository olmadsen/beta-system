ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/persistentstore/virtualobjectstore';
INCLUDE '~beta/containers/hashTable';

---lib: attributes---
TextHashTable: hashTable
  (# element::< Text;
     hashfunction::< (# do e.scanAll (# do ch*26+value -> value #)#);
  #);

--- program : descriptor ---
(# 
   crossPS: persistentstore
     (# allowLazyFetch::< TrueObject;
        openpstore::<
          (# 
          do 'openpstore called: ' -> screen.putText;
             psname[] -> screen.putLine;
             &crossPS[] -> ps[] -> newps[];
             newps.init;
             psname[] -> ps.openWrite;
             doGC;
          #);
     #); 
   
   PS, PS2: @crossPS;
   newps: ^crossPS;
   
   H: ^TextHashTable;
   H2: ^TextHashTable;
   T: ^Text;
   
   created: @Boolean;
   
do
   PS.init; PS2.init;
   open: 'crosslocStore' -> PS.openWrite
   (# notFound::< 
        (#
        do 'crosslocStore' -> PS.create;
           true -> created; 
           leave open 
        #)
   do 'Opening PS' -> screen.putLine;      
   #);
   
   (if created //true then
       
       'crosslocStore2' -> PS2.create;
       'created both stores' -> putLine;
       
       &TextHashTable[] -> H[]; H.init;
       &TextHashTable[] -> H2[]; H2.init;
       
       'In a single table (1)' -> H.insert;
       'In a single table (2)' -> H.insert;
       'In both tables' -> T[] -> H.insert;
       T[] -> H2.insert;
       
       (H[],'TextTable') -> PS.put;
       (H2[],'TextTable2') -> PS2.put;
       
       'put done' -> putLine;
    else
       'opened first store' -> putLine;
       ('TextTable', TextHashTable##) -> PS.get -> H[];
       'get done' -> putLine;
   if);
   
   H.scan  (# do current[] -> putLine #);
   'scan of first table done' -> putLine;
   
   (if created//true then 
       PS.checkpoint; PS2.checkpoint;
       'checkpoint of both stores done' -> putLine;
       PS2.close (# doCheckpoint::< (# do false -> value #)
                    do 'Closing PS2' -> screen.putLine;
                 #);
   if);
   
   PS.close (# doCheckpoint::< (# do false -> value #)
            do 'Closing PS' -> screen.putLine;
            #);
   'all stores closed' -> putLine;
   
   'crosslocStore2' -> PS2.openRead(# do 'Opening PS2' -> screen.putLine; #);
   'opened second store' -> putLine;
   
   ('TextTable2', TextHashTable##) -> PS2.get -> H2[];
   'Got second table' -> Putline;
   
   H2.scan  
   (#
   do current[] -> putLine;
      'In both tables after change' -> current;
      'changed shared element through second table' -> putLine;
   #);
   'scan of second table done' -> putLine;
   
   (* newps.checkpoint;
    'Checkpointet first ps' -> putLine;
    *)
   
   PS2.close(# do 'Closing PS2' -> screen.putLine; #); 
   (* Only opened for reading, so no checkpoint is performed. *)
   (* newps.close (# doCheckpoint::< (# do false -> value #)#); *)
   
   'all stores closed' -> putLine;
#)
