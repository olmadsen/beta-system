ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/containers/list';

--- lib: attributes ---
date:
  (# day: @integer;
     month: @text;
     year: @integer;
  #);
monthnames:
  (# t: [12] ^text;
     i: @integer;
     init: 
       (#
       do (for i:12 repeat &Text[]->t[i][] for);
          'January'->t[1];
          'February'->t[2];
          'March'->t[3];
          'April'->t[4];
          'May'->t[5];
          'June'->t[6];
          'July'->t[7];
          'August'->t[8];
          'September'->t[9];
          'October'->t[10];
          'November'->t[11];
          'December'->t[12];
       #);
  enter i
  exit t[i]
  #);

typenames:
  (# t: [10] ^text;
     i: @integer;
     init: 
       (#
       do (for i:10 repeat &Text[]->t[i][] for);
          'part-type0'->t[1];
          'part-type1'->t[2];
          'part-type2'->t[3];
          'part-type3'->t[4];
          'part-type4'->t[5];
          'part-type5'->t[6];
          'part-type6'->t[7];
          'part-type7'->t[8];
          'part-type8'->t[9];
          'part-type9'->t[10];
       #);
  enter i
  exit t[i]
  #);
connection: 
  (# p:^part;
     type: @text;
     length: @integer;
  #);
part: 
  (# id: @integer;
     type: @text;
     x: @integer;
     y: @integer;
     build: @date;
     to: @list(#element::< connection#);
     from: @list(#element::< part#);
     
     init: 
       (# 
       do to.init;
          from.init;
       #);
     print: 
       (# 
       do 'id: '->puttext; id->putint;
          ', type: '->puttext; type[]->puttext;
          ', x: '->puttext; x->putint;
          ', y: '->puttext; y->putint;
          ', date: '->puttext; 
          build.day->putint;
          '.'->puttext; build.month[]->puttext;
          '.'->puttext; build.year->putint;
          newline;
          ', to: '->puttext; 
          to.scan
          (# do
             current.p.id->putint; 
             ' type: '->puttext; current.type[]->puttext;
             ' length: '->puttext; current.length->putint; newline;
          #);
          ', from: '->puttext; from.scan
          (# 
          do current.id->putint; 
          #);
          newline;
       #);
  #);




