ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/persistentstore/virtualobjectstore';
INCLUDE '~beta/containers/hashTable';
INCLUDE '~beta/sysutils/objinterface';

--- program:descriptor ---
(# ps: @persistentstore; exists: @Boolean;
   
   (* By registering the program object as a special instance it is posible
    * to persistently save objects having origin in the program object: *)
   TextHashTable: hashTable
     (# element::< Text;
        hashfunction::< (# do e.scanAll (# do ch*26+value -> value #)#);
     #);
   H: ^TextHashTable;
   
   (* Using runtime types allows us to persistently save objects with
    * pointers to e.g. components: *)
   textWithComponent: Text
     (# comp: ^|IntegerObject;
        io: ^IntegerObject;
        init: (# do &|IntegerObject[] -> comp[]; &IntegerObject[] -> io[] #);
     #);
   tc: ^textWithComponent;
   SpecialProgram: (# exit 43 #);
   
do
   ps.init;
   true -> exists;
   
   open: 'specialStore' -> PS.openRead
   (# notFound::< 
        (# 
        do 'specialStore' -> PS.create; 
           false -> exists; 
           leave open 
        #)
   #);
   
   
   (* Register the instance of 'SpecialProgram' to be used in this 
    * program execution: *)
   (theProgram[] -> componentToObject, SpecialProgram)
     -> ps.RegisterSpecialInstance;
   
   (if exists //false then
       'created' -> PutLine;
       
       (* Specify that we want references to instances of IntegerObject and
        * subpatterns saved as NONE references: *)
       IntegerObject## -> ps.registerRuntimeType;
       
       &TexthashTable[] -> H[]; 
       H.init;
       
       'first' -> H.insert;
       
       &textWithComponent[] -> tc[]; tc.init; 'dangerous1' -> tc;
       tc[] -> H.insert;
       
       'second' -> H.insert;
       
       &textWithComponent[] -> tc[]; tc.init; 'dangerous2' -> tc;
       tc[] -> H.insert;
       
       'third' -> H.insert;
       
       (H[],'TextTable') -> PS.put;
    else
       'opened' -> putLine;
       ('TextTable',TextHashTable##) -> PS.get -> H[];
   if);
   
   H.scan (# do current[] -> putLine; #);
   'did scan \n' -> putLine;
   
   PS.close;
#)
