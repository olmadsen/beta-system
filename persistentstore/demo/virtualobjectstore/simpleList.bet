ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/persistentstore/virtualobjectstore';
-- program: Descriptor --
(#
   link:
     (#
        next: ^link;
        name: @text;
        key: @integer;
        print:
          (# 
          do
             'name: '->putText;
             name[]->putText;
             ', key: '->putText;
             key->putInt;
             putLine
          #)
     #);
   buildList:
     (# head,tail: ^link; size,current: @integer
     enter (head[],size)
     do
        (if size > 0 then
            head[]->tail[];
            0->current;
            Loop:
              (# 
              do
                 'link'->tail.name;
                 current->tail.key;
                 current+1->current;
                 (if current < size then
                     &link[]->tail.next[]; tail.next[]->tail[]; restart Loop
                  else
                     leave Loop
                 if)
              #)
        if)
     exit (tail[])
     #);
   printList:
     (# head: ^link
     enter head[]
     do
        'Starting print list'->putLine;
        Loop:
        (if head[] <> none then
            'Head is not none'->putLine;
            head.print;
            head.next[]->head[];
            restart Loop
         else
            'Head is none'->putLine; leave Loop
        if)
     #);
   PS: @persistentStore
     (# rebindSpecialReference::
          (# toSpecialObject::
               (# 
               do (if objectTag = (BETAENVOBJ) then
                      this(program)[] -> getOrigin -> target[]
                   else 
                      (if objectTag = (PSOBJ) then
                          PS[] -> target[];
                       else 
                          notHandled
                      if)
                  if)
               #)
          #)
     #);
   tail,head: ^link;
   BETAENVOBJ:(# exit 42 #);  
   PSOBJ:(# exit 43 #)

do
   PS.init;
   (this(program)[] -> getOrigin, (BETAENVOBJ)) -> registerSpecialInstance;
   (PS[], (PSOBJ)) -> registerSpecialInstance;
   'simpleListStore' -> PS.openWrite
   (# notFound :: (# do 'simpleListStore' -> PS.create; true -> continue #) #);
   
   ('root', link##) -> PS.get
   (# notFound :: 
        (# 
        do &link[] -> head[];
           (head[], 'root') -> PS.put;
           (head[],1000)->buildList->tail[];
           head[] -> theObj[];
           true -> continue
        #)
   #) -> head[];
   head[] -> printList;
   PS.close
#)  

