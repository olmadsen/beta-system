ORIGIN '~beta/basiclib/v1.4/betaenv';

--- include '~beta/persistentstore/v1.41/persistentstore'
--- include '~beta/containers/v1.4/hashTable'
--- include '~beta/sysutils/v1.4/objinterface'

--- program:descriptor ---
(# ps: @persistentstore; exists: @Boolean;
   
   (* Use of a special object allows us to persistently save objects
    * having origin in the program object: *)
   TextHashTable: hashTable
     (# element::< Text;
        hashfunction::< (# do e.scanAll (# do ch*26+value -> value #)#);
     #);
   H: ^TextHashTable;
   
   (* Using runtime types allows us to persistently save objects with
    * pointers to components: *)
   textWithComponent: Text
     (# comp: ^|IntegerObject;
        io: ^IntegerObject;
        init: (# do &|IntegerObject[] -> comp[]; &IntegerObject[] -> io[] #);
     #);
   tc: ^textWithComponent;
   
do
   true -> exists;
   
   open: 'specialStore' -> PS.openRead
   (# notFound::< 
        (# 
        do 'specialStore' -> PS.create; 
           false -> exists; 
           (* Register a special object named 'SpecialName' of type
            * program in this newly created persistent store. *)
           ('SpecialName', program##) -> ps.RegisterSpecialObject;
           leave open 
        #)
   #);
   
   'opened' -> putLine;
   
   (* Register the instance of 'SpecialName' to be used in this 
    * program execution: *)
   (theProgram[] -> componentToObject, 'SpecialName')
     -> ps.RegisterSpecialInstance;
   
   (if exists //false then
       
       IntegerObject## -> ps.registerRuntimeType;
       
       &TexthashTable[] -> H[]; 
       H.init;
       
       'first' -> H.insert;
       
       &textWithComponent[] -> tc[]; tc.init; 'dangerous1' -> tc;
       tc[] -> H.insert;
       
       'second' -> H.insert;
       
       &textWithComponent[] -> tc[]; tc.init; 'dangerous2' -> tc;
       tc[] -> H.insert;
       
       'third' -> H.insert;
       
       (H[],'TextTable') -> PS.put;
       'did put' -> putLine; screen.newline;
       
       H.scan (# do current[] -> putLine; #)
    else
       ('TextTable',TextHashTable##) -> PS.get -> H[];
       H.scan (# do current[] -> putLine; #)
   if);
   
   'scanRootNames: ' -> screen.putLine;
   PS.scanRootNames (# do current[] -> screen.putLine #);
   
   PS.close;
#)
