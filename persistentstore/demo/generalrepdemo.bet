ORIGIN '~beta/basiclib/v1.5/betaenv';
INCLUDE '~beta/persistentstore/v1.5/persistentstore';
INCLUDE '~beta/containers/v1.5/hashTable';
INCLUDE '~beta/sysutils/v1.5/objinterface';


---lib: attributes---
RepHashTable: hashTable
  (# element::
       (# rep: [10]@Text;
       #);
     hashfunction::
       (# 
       do e.rep[1].scanAll (# do ch*26+value -> value #)
       #);
  #);

--- program : descriptor ---
(# PS: @persistentstore;
   H: ^RepHashTable;
   exists: @Boolean;
   psName: ^Text;
   new: ^RepHashTable.element;
do
   'Enter name of persistent store: ' -> putText;
   getLine -> psName[];
   
   true -> exists;
   open: psName[] -> PS.openWrite
   (# notFound::< 
        (# do psName[] -> PS.create; false -> exists; leave open #)
   #);
   
   (if not exists then
       'created' -> putLine;
       &RepHashTable[] -> H[];
       H.init;
       (H[],'RepTable') -> PS.put;
    else
       'opened' -> putLine;
       ('RepTable', RepHashTable##) -> PS.get -> H[];
   if);
   
   &H.element[]->new[];
   (for i:10 repeat
        (H.size*10)+i->new.rep[i].putint;
   for);
   new[]->H.insert;
   
   H.scan  
   (# 
   do (for i: current.rep.range repeat
           current.rep[i][] -> putLine
      for);
   #);
   'scan done\n' -> putLine;
   
   PS.close; 'closed' -> putLine;
#)



