ORIGIN '~beta/basiclib/v1.4/betaenv';
(* 
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- include '~beta/persistentstore/v1.41/persistentstore'
--- include '~beta/containers/v1.4/hashTable'

---lib: attributes---
TextHashTable: hashTable
  (# element::< Text;
     hashfunction::< (# do e.scanAll (# do ch*26+value -> value #)#);
  #);

--- program : descriptor ---
(# 
   crossPS: persistentstore
     (# openpstore::<
          (# 
          do 'openpstore called: ' -> screen.putText;
             psname[] -> screen.putLine;
             &crossPS[] -> ps[] -> newps[];
             psname[] -> ps.openWrite;
          #);
     #); 
   
   PS, PS2: @crossPS;
   newps: ^crossPS;
   
   H: ^TextHashTable;
   H2: ^TextHashTable;
   T: ^Text;
   
   exists: @Boolean;
   
do
   'running\n' -> putLine;
   true -> exists;
   open: 'myStore' -> PS.openWrite
   (# notFound::< 
        (# do 'myStore' -> PS.create; false -> exists; leave open 
        #)
   #);
   
   (if exists //false then
       
       'myStore2' -> PS2.create;
       
       '\ncreated\n' -> putLine;
       
       &TextHashTable[] -> H[];
       &TextHashTable[] -> H2[];
       H.init;
       H2.init;
       
       'first' -> T[] -> H.insert;
       'second' -> T[] -> H.insert;
       'third' -> T[] -> H.insert;
       T[] -> H2.insert;
       
       (H[],'TextTable') -> PS.put;
       (H2[],'TextTable2') -> PS2.put;
       
       '\ndid put\n' -> putLine;
    else
       '\nopened\n' -> putLine;
       ('TextTable', TextHashTable##) -> PS.get -> H[];
       '\ndid get\n' -> putLine;
   if);
   
   H.scan  (# do current[] -> putLine #);
   '\ndid scan\n' -> putLine;
   
   (if exists //false then 
       PS.checkpoint; 
       PS2.checkpoint;
       PS2.close (# doCheckpoint::< (# do false -> value #)#);
   if);
   
   PS.close (# doCheckpoint::< (# do false -> value #)#);
   
   'closed' -> putLine;
   
   'myStore2' -> PS2.openWrite;
   'opened' -> putLine;
   
   ('TextTable2', TextHashTable##) -> PS2.get -> H2[];
   
   'fourth' -> T[] -> H2.insert;
   H2.scan  
   (# 
   do current[] -> putLine;
      (if true 
       // ('third' -> current.equal) then
          'third changed' -> current;
       // ('fourth' -> current.equal) then
          'fourth changed' -> current;
      if)
   #);
   
   PS2.close; newps.close;
   
   'closed' -> putLine;
#)
