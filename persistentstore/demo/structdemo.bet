ORIGIN '~beta/basiclib/v1.4/betaenv';


--- include '~beta/persistentstore/v1.41/persistentstore'
--- include '~beta/containers/v1.4/hashTable'

---lib: attributes---
StructHashTable: hashTable
  (# element::< 
       (# aStruct: ##Object; 
          name: ^Text;
       #);
     hashfunction::< (# do e.name.scanAll (# do ch*26+value -> value #)#);
  #);

first: (# do 'first pattern' -> screen.putLine #);
second: (# do 'second pattern' -> screen.putLine #);
third: (# do 'third pattern' -> screen.putLine #);
fourth: (# do 'fourth pattern' -> screen.putLine #);
  

--- program : descriptor ---
(# PS: @persistentstore;
   H: ^StructHashTable;
   e: ^H.element;
   exists: @Boolean;

do
   true -> exists;
   open: 'structStore' -> PS.openWrite
   (# notFound::< 
        (# do 'structStore' -> PS.create; false -> exists; leave open #)
   #);
   'opened' -> putLine;
   
   (if exists //false then
       &StructHashTable[] -> H[];
       H.init;
       
       &H.element[] -> e[];
       'first' -> e.name[];
       first## -> e.aStruct##;
       e[] -> H.insert;
       
       &H.element[] -> e[];
       'second' -> e.name[];
       second## -> e.aStruct##;
       e[] -> H.insert;
       
       &H.element[] -> e[];
       'third' -> e.name[];
       third## -> e.aStruct##;
       e[] -> H.insert;
    else
       ('StructTable', StructHashTable##) -> PS.get -> H[];
       'did get' -> putText;
   if);
   
   H.scan  (# do &current.aStruct; current.name[] -> screen.putLine #);
   
   (H[],'StructTable') -> PS.put;
   
   PS.close;
   'closed' -> putLine;
   
   'structStore' -> PS.openWrite;
   'opened' -> putLine;
   
   ('StructTable', StructHashTable##) -> PS.get -> H[];
   
   &H.element[] -> e[];
   'fourth' -> e.name[];
   fourth## -> e.aStruct##;
   e[] -> H.insert;
   
   H.scan  (# do &current.aStruct; current.name[] -> screen.putLine #);
   
   (H[],'StructTable') -> PS.put;
   
   PS.close;
   'closed' -> putLine;
#)


