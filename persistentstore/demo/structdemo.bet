ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/persistentstore/v1.6/persistentstore';
INCLUDE '~beta/containers/v1.6/hashTable';

---lib: attributes---
StructHashTable: hashTable
  (# element::< 
       (# aStruct: ##Object; 
          name: ^Text;
       #);
     hashfunction::< (# do e.name.scanAll (# do ch*26+value -> value #)#);
  #);

first: (# do 'first pattern' -> putLine #);
second: (# do 'second pattern' -> putLine #);
third: (# do 'third pattern' -> putLine #);
extra: (# do 'extra pattern' -> putLine #);
  

--- program : descriptor ---
(# PS: @persistentstore;
   H: ^StructHashTable;
   e: ^H.element;
   created: @Boolean;

do
   open: 'structStore' -> PS.openWrite
   (# notFound::< 
        (# do 'structStore' -> PS.create; true -> created; leave open #)
   #);
   
   (if created//true then
       'created' -> putLine;
       
       &StructHashTable[] -> H[];
       H.init;
       
       &H.element[] -> e[]; 'first' -> e.name[]; first## -> e.aStruct##;
       e[] -> H.insert;
       
       &H.element[] -> e[]; 'second' -> e.name[]; second## -> e.aStruct##;
       e[] -> H.insert;
       
       &H.element[] -> e[]; 'third' -> e.name[]; third## -> e.aStruct##;
       e[] -> H.insert;
       
       (H[],'StructTable') -> PS.put;
    else
       'opened' -> putLine;
       
       ('StructTable', StructHashTable##) -> PS.get -> H[];
       
       &H.element[] -> e[]; 'extra' -> e.name[]; H.size+1 -> e.name.putInt;
       extra## -> e.aStruct##;
       e[] -> H.insert;
   if);
   
   H.scan  (# do &current.aStruct; current.name[] -> screen.putLine #);
   
   PS.close;
   'closed' -> putLine;
#)


