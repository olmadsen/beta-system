ORIGIN '~beta/basiclib/v1.4/betaenv';

(* 
 *  This form contains an example of the use of persistence in BETA.
 * 
 *  The following classification hierarchy is defined in the  
 *  library fragment 'recordlib'
 * 
 *  1.  Record  
 *    2.  Person  
 *      3. Employee  
 *      3. Student  
 *    2. Book  
 * 
 *   It also contains a Register pattern which describes a  
 *   register of records.
 *)
INCLUDE '~beta/persistentstore/v1.41/persistentstore';
INCLUDE 'recordlib';

--- program: descriptor ---
(# ps: @persistentstore;
   s: ^register;
   registerName: ^text;
   
   addRecord: 
     (# elm:< record;  p: ^elm
     do  &elm[] -> p[];
        'Key      ?' -> screen.putText; keyBoard.getInt -> p.key;
        INNER;
        p[] -> s.insert
     #);
   addPerson: addRecord
     (# elm::< person; 
     do 'Name     ?' -> screen.putText; keyBoard.getLine -> p.name[];
        'Sex      ?' -> screen.putText; keyBoard.getLine -> p.sex[];
	INNER
     #);
   addEmployee: addPerson
     (# elm::< employee
     do 'Salary   ?' -> screen.putText; keyBoard.getInt -> p.salary;
        'Position ?' -> screen.putText; keyBoard.getLine -> p.position[];
        INNER
     #);
   addStudent: addPerson
     (# elm::< student
     do 'Status   ?' -> screen.putText; keyBoard.getLine -> p.status[];
        INNER
     #);
   addBook: addRecord
     (# elm::< book
     do 'Author   ?' -> screen.putText; keyBoard.getLine -> p.author[];
        'Title    ?' -> screen.putText; keyBoard.getLine -> p.title[];
        INNER
     #);
   
   findWithKey: 
     (# k: @integer
     do 'Key      ?'->screen.putText; keyBoard.getInt->k;
        s.scan(# do (if elm.key // k then screen[] -> elm.display if) #);
     #)
do
   'Open register ? ' -> screen.putText;
   keyBoard.getLine -> registerName[];
   
   open: registername[] -> PS.openWrite
   (# notFound::< (# do registername[] -> PS.create; leave open #)#);
   
   ('register',register##) -> ps.get
   (# notFound::< (# do true -> continue; &register[] -> theObject[]; #);
   #) -> s[];
   
   commandLoop: cycle
     (#
     do screen.newLine;
        'Display[d], add[a], findKey[f], quit[q] ? '->puttext;
        (if getNonBlank
         // 'd' then screen[] -> s.display
         // 'a' then 
	    'Student [s], employee[e], person [p], book[b], record[r] ?'->putText;
	    (if getNonBlank
             // 's' then addStudent
             // 'e' then addEmployee
             // 'b' then addBook
             // 'p' then addPerson
             // 'r' then addRecord
	    if);
         // 'f' then findWithKey
         // 'q' then leave commandLoop
        if)
     #);
   (s[],'register') -> ps.put;
   ps.close;
#)


