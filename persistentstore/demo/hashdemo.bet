ORIGIN '~beta/basiclib/v1.4/betaenv';
(* 
 * $RCSfile: hashdemo.bet,v $ $Revision: 1.3 $ $Date: 1994-04-28 17:55:06 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- include '~beta/persistentstore/v1.41/persistentstore'
--- include '~beta/containers/v1.4/hashTable'

---lib: attributes---
TextHashTable: hashTable
  (# element::< Text;
     hashfunction::< (# do e.scanAll (# do ch*26+value -> value #)#);
  #);

--- program : descriptor ---
(# PS: @persistentstore;
   H: ^TextHashTable;
   exists: @Boolean;

do
   'running' -> putLine;
   true -> exists;
   open: 'myStore' -> PS.openWrite
   (# notFound::< 
        (# do 'myStore' -> PS.create; false -> exists; leave open #)
   #);
   'opened' -> putLine;
   
   (if exists //false then
       &TextHashTable[] -> H[]; H.init;
   
       'first' -> H.insert;
       'second' -> H.insert;
       'third' -> H.insert;
       (H[],'TextTable') -> PS.put;
       
       'did put' -> putText;
    else
       ('TextTable', TextHashTable##) -> PS.get -> H[];
       'did get' -> putText;
   if);
   
   H.scan  (# do current[] -> putLine #);
   'did scan' -> putText;
   
   PS.close;
   'closed' -> putLine;
   
   'myStore' -> PS.openWrite;
   'opened' -> putLine;
   
   ('TextTable', TextHashTable##) -> PS.get -> H[];
   
   'fourth' -> H.insert;
   H.scan  (# do current[] -> putLine #);
   (H[],'TextTable') -> PS.put;
   
   PS.close;
   'closed' -> putLine;
#)



