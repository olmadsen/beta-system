ORIGIN '~beta/basiclib/v1.4/betaenv';
INCLUDE '~beta/persistentstore/v1.41/persistentstore';
INCLUDE '~beta/containers/v1.4/hashTable';

---lib: attributes---
TextHashTable: hashTable
  (# element::< Text;
     hashfunction::< (# do e.scanAll (# do ch*26+value -> value #)#);
  #);

--- program : descriptor ---
(# PS: @persistentstore;
   H: ^TextHashTable;
   exists: @Boolean;
   psName: ^Text;

do
   'running' -> putLine;
   
   'Enter name of persistent store: ' -> putText;
   getLine -> psName[];
   
   true -> exists;
   open: psName[] -> PS.openWrite
   (# notFound::< 
        (# do psName[] -> PS.create; false -> exists; leave open #)
   #);
   'opened' -> putLine;
   
   (if exists //false then
       &TextHashTable[] -> H[]; H.init;
   
       'first' -> H.insert;
       'second' -> H.insert;
       'third' -> H.insert;
       (H[],'TextTable') -> PS.put;
       
       'did put' -> putText;
    else
       ('TextTable', TextHashTable##) -> PS.get -> H[];
       'did get' -> putText;
   if);
   
   H.scan  (# do current[] -> putLine #);
   'did scan\n' -> putLine;
   
   PS.close;
   'closed' -> putLine;
   
   psName[] -> PS.openWrite;
   'opened' -> putLine;
   
   ('TextTable', TextHashTable##) -> PS.get -> H[];
   
   'fourth' -> H.insert;
   H.scan  (# do current[] -> putLine #);
   (H[],'TextTable') -> PS.put;
   
   PS.close;
   'closed' -> putLine;
#)



