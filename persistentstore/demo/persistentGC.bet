ORIGIN '~beta/basiclib/v1.4/betaenv';
INCLUDE '~beta/persistentstore/v1.4.2/persistentstore';
INCLUDE '~beta/containers/v1.4/list';

(* This demo fragment implements a very simple garbage collection
 * algorithm for a persistentstore that is not referenced
 * from other persistent stores, and does not contain references
 * to any special objects. *)

--- lib:attributes ---
persistentGC:
  (# rootTable: @list
       (# element::
            (# name: ^Text;
               obj: ^Object;
            #);
       #);
     ps: @persistentstore;
     psname: ^Text;
  enter psname[]
  do
     rootTable.init; 
     
     (* Read in all objects in the persistent store: *)
     psname[] -> ps.openRead;
     ps.scanRootNames
     (# new: ^rootTable.element;
     do &rootTable.element[] -> new[];
        (current[],Object##) -> ps.get -> new.obj[];
        current[] -> new.name[];
        new[] -> rootTable.append;
     #);
     ps.close;
     
     (* Delete the persistent store: *)
     psname[] -> deletePersistentStore;
     
     (* Recreate the persistent store: *)
     psname[] -> ps.create;
     rootTable.scan
     (# 
     do (current.obj[],current.name[]) -> ps.put;
     #);
     ps.close;
  #)
