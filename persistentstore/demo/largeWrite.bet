ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/persistentstore/persistentstore';
INCLUDE 'texthashtable';

--- program : descriptor ---
(# PS: @persistentstore(# allowLazyFetch::(# do false->value #) #);
   H: ^textHashTable;
   t: @Text;
   count: @Integer;
do
   PS.init;
   
   'Number of elements in table: ' -> putText;
   getInt -> count;

   'largeStore' -> PS.openWrite
   (# notFound::
        (# 
        do 'Creating...' -> screen.putText;
           'largeStore' -> PS.create;
           &textHashTable[] -> H[]; H.init;
           (H[],'TextTable') -> PS.put;
           true -> continue
        #);
   do 'Opening...' -> screen.putText;
      ('TextTable', textHashTable##) -> PS.get -> H[];
   #);
   
   (for i: count repeat
        t.clear;
        i -> t.putInt;
        t.copy -> H.insert;
   for);
   screen.newline; screen.newline;
   
   'Checkpointing and closing...' -> screen.putText;
   
   PS.close;
   screen.newline; screen.newline;
#)
