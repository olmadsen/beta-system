ORIGIN '~beta/basiclib/betaenv';
BODY 'private/xmlbody';


-- LIB: attributes --

XML:
  (# <<SLOT XMLLib: attributes >>;
     
     AbstractElement:
       (# <<SLOT XMLAbstractElementLib: attributes >>;
          
          next: ^AbstractElement;
          
          clone:<
            (# CloneType:< AbstractElement;
               new: ^CloneType; 
            <<SLOT AbstractElementClone: doPart>>
            exit new[]
            #);
          
          select:
            (# onElement:<
                 (# current: ^Element;
	         enter current[]
	         do INNER;
	         #);
	       onData:<
	         (# current: ^DataElement;
	         enter current[]
	         do INNER;
	         #);
            do THIS(Select)[] -> selector;
            #);
          selector:<
            (# theSelect: ^Select;
            enter theSelect[]
            do INNER;
            #);
          print:<
            (# output: ^Stream;
               level: @integer;
               indentFiller: ^Text;
            enter (output[], level, indentFiller[])
            do INNER;
            #);
       #);
     DataElement: AbstractElement
       (# <<SLOT XMLDataElementLib: attributes >>;
          
          data: ^Text;
          clone::
            (# CloneType:: DataElement;
            do data.copy -> new.data[];
            #);
          selector::
            (#
            do THIS(DataElement)[] -> theSelect.onData;
            #);
          print::
            (#
            do (data[], output[]) -> xmlEscape;
            #);
       #);
     Element: AbstractElement
       (# <<SLOT XMLElementLib: attributes >>;
          
          tag: ^Text;
          attributes: ^Attribute;
          children: ^AbstractElement;
          
          clone::
            (# CloneType:: Element;
            <<SLOT ElementClone: doPart>>
            #);
          
          addElement:
            (# child: ^AbstractElement;
            enter child[]
            <<SLOT ElementAddElement: doPart>>
            #);
          addAttribute:
            (# theAttribute: ^Attribute;
            enter theAttribute[]
            <<SLOT ElementAddAttribute: doPart>>
            #);
          
          scan:
            (# current: ^AbstractElement;
            <<SLOT ElementScan: doPart>>
            #);
          scanAttributes:
            (# current: ^Attribute;
            <<SLOT ElementScanAttributes: doPart>>
            #);
          scanElements:
            (# current: ^Element;
            <<SLOT ElementScanElements: doPart>>
            #);
          scanData:
            (# current: ^DataElement;
            <<SLOT ElementScanData: doPart>>
           #);
          faultyGetData:
            (# data: ^Text;
            do &Text[] -> data[];
               scanData 
               (# 
               do current.data[] -> data.puttext;
               #);
            exit data[]
            #);
          print::
            (# 
            <<SLOT  ElementPrint: doPart>>  
            #);
          selector::
            (#
            do THIS(Element)[] -> theSelect.onElement;
            #);
       #);
     
     Attribute:
       (# <<SLOT XMLAttributeLib: attributes >>;
          
          clone:
            (# new: ^Attribute;
            <<SLOT AttributeClone: doPart>>
            exit new[]
            #);
          
          key: ^Text;
          value: ^Text;
          next: ^Attribute;
       #);
     
     parse:
       (# input: ^Stream;
          output: ^Element;
          onError:< Exception
            (# errorString: ^Text;
               errorLine: @Integer;
               errorColumn: @Integer;
               errorByteIndex: @Integer;
               errorByteCount: @Integer;
            enter (errorString[], errorLine, errorColumn, errorByteIndex, errorByteCount)
            do INNER;
            #);
       enter input[]
       <<SLOT XMLParse: doPart>>
       exit output[]
       #);
     
     xmlEscape:
       (# input: ^Text;
          output: ^Stream;
       enter (input[], output[])
       <<SLOT XMLxmlEscape: doPart>>
       #);
     
     print:
       (# input: ^Element;
          output: ^Stream;
          indentFiller:<
            (# filler: ^Text;
            do '  ' -> filler[];
               INNER;
            exit filler[]
            #);
       enter (input[], output[])
       <<SLOT XMLPrint: doPart>>
       #);
     printNoIndent: print
       (# indentFiller::
            (# 
            do NONE -> filler[];
            #);
       #);
  #);
