ORIGIN '~beta/basiclib/betaenv';
INCLUDE '../xmlparser';
INCLUDE '~beta/basiclib/file';
--- program: descriptor ---
(# parser: @XMLParser
     (# onError::
          (#
          do 'Parse error in line '->PutText;
             errorLine->PutInt;
             ': '->PutText;
             errorString[]->PutLine;
          #);
        
        onElementStart:: 
          (# 
          do '<'->PutText;
             name[]->Screen.PutText;
             attributes.scan
             (#
             do ' '->Screen.Put;
                current.name[]->puttext;
                '="'->puttext;
                current.value[]->puttext;
                '"'->put;
             #);
             '>'->PutText;
          #);
        
        onElementEnd::
          (#
          do '</'->PutText;
             name[]->Screen.PutText;
             '>'->PutText;
          #);
        
        onCharacterData::
          (#
          do data[]->PutText;
          #);
        
        
     #);
   
   XMLFile: @File;
   
do NONE->parser.Init;
   'expat.xml'->XMLFile.name;
   XMLFile.openRead;
   XMLFile[]->parser; 
   XMLFile.close;      
   parser.Close;
#)


