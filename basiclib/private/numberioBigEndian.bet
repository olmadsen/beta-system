ORIGIN 'numberioBody'
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1992-96
 *       All rights reserved.
 *)   
--- numberioPutHex: descriptor ---
(# d, j: @integer;
   achar:@char;
   ch:@char;
   pos:@integer;
   putC: @put;
   first:@boolean;
   myput:@
     (# ch:@char;
     enter ch
     do
        (if ch>'0' then
            (if not first then
                (for i:width-(8-pos) repeat
                     padding->putC;
                for);
            if);
            true->first;
            ch->putC;
        if);
     #);
do 
   (if padding=0 then ' '->padding if);
   (if x=0 then
       (for i:width-1 repeat padding->putC; for);
       '0'->putC;
    else
       (if lowerCase then
           'a'->achar;
        else
       'A'->achar;
       if);
       (for i: 4 repeat
            (i-1)->j; j->x.%getByte->d;
            d div 16->j; (if j<=9 then '0'+j->myput else achar+j-10->myput if);
            1+pos->pos;
            d mod 16->j; (if j<=9 then '0'+j->myput else achar+j-10->myput if);
            1+pos->pos;
       for);
   if);
#)

--- numberioPutByteHex: descriptor ---
(# d, j: @integer;
   putC: @put
do (byte-1)->j; j->x.%getByte->d;
   d div 16->j; (if j<=9 then '0'+j->putC else 'A'+j-10->putC if);
   d mod 16->j; (if j<=9 then '0'+j->putC else 'A'+j-10->putC if)
#)

--- numberioPutOctal: descriptor ---
(# j:@integer;
   first:@boolean;
   ch:@char;
   putC:@put
do
   (if padding=0 then ' '->padding if);
   ((0,2)->x.%getbits)+'0'->ch;
   (if ch>'0' then
       true->first;
       ch->putC;
    else
       (if width=11 then
           padding->putC;
       if);
   if);
   
   (for i:10 repeat
        ((3*i-1,3)->x.%getbits)+'0'->ch;
        (if first then 
            ch->putC;
         else
            (if ch>'0' then
                ch->putC;
                true->first;
             else
                (if i>10-width then
                    padding->putC;
                if);
            if);
        if);
   for);
#)


--- numberioPutBinary: descriptor ---
(# putC: @put;
   first:@boolean;
   ch:@char;
do (if padding=0 then ' '->padding if);
   (for i:32 repeat
        '0'+(((i-1),1)->x.%getBits)->ch;
        (if first then
            ch->putC;
         else
            (if ch='1' then
                ch->putC;
                true->first;
             else
                (if i>32-width then
                    padding->putC;
                if);
            if);
        if);
   for)
#)
--- numberioPutByteBinary: descriptor ---
(# j: @integer;
   putC: @put
do (byte-1)*8->j;
   (for i:8 repeat
        '0'+(((j+i-1),1)->x.%getBits)->putC
   for)
#)

