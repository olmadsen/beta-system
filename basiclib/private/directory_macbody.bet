ORIGIN 'directorybody';
(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-96
 *       All rights reserved.
 *)
INCLUDE 'file_macbody';
INCLUDE '~beta/maclib/v3.1/morefiles';

-- DirectoryLib: Attributes --
CountEntries: External
  (# ID: @Integer; vRefNum: @ShortInt; NoOfEntries: @Integer; 
  enter (ID,vRefNum)
  do callC
  exit NoOfEntries
  #);
GetIndEntryName: External
  (# ID: @Integer; vRefNum: @ShortInt; inx: @Integer; name: [1] @char
  enter (ID,vRefNum,inx)
  do callC
  exit name
  #);
numFiles: External
  (# Path: [1] @Char; NoOfFiles: @Integer; 
  enter Path
  do callC
  exit NoOfFiles
  #);
entryDelete:
  (# fs: ^FSSpec; p: ^Text; result: @ShortInt; 
  enter fs[]
  do
     (if HasFSSpecCalls
      // true then fs[]->FSpDelete->result; 
      else
         fs.name->p[]; (fs.vRefNum,fs.parID,p)->HDelete->result
     if);
     
  exit result
  #);
  

-- DirPrivate: Descriptor --
(# #)  

-- DirException: Descriptor --
(# 
do 'Directory exception for '''->msg.putText; Name->msg.putText; ''''->msg.put; 
#)  

-- DirTouch: Descriptor --
(# p: ^Text; res: @Integer; 
do
   entry.private.FSpCheckNil;
   (if entry.exists
    // false then
       entry.private.fs.name->p[];
       (if hasFSSpecCalls
        // true then
           (if p.length = 0 then
               entry.private.path[]->p[];
               (if ((0,0,p,entry.private.fs[])->MakeFSSpec)
                // 0 // - 43 then 
                else
                   error; 
               if);
               
           if);
           (entry.private.fs[],- 1,entry.private.dirID[])->FSpDirCreate->convert
             ->res;
           TRUE->entry.private.FSSpecValid;
           
        else
           (if p.length = 0 then entry.private.path[]->p[];  if);
           (entry.private.fs.vRefNum,entry.private.fs.parID,p,
            entry.private.dirID[])->DirCreate->convert->res;
           TRUE->entry.private.FSSpecValid;
           
       if);
       (if res
        // 0 then (* now we have a FSSpec; do not use private.path *)
           entry.private.path.clear; 
        else
           error; 
       if);
       
   if);
   
#)  

-- DirNoOfEntries: DoPart --
do
   entry.private.FSpCheckNil;
   (entry.private.dirID,entry.private.fs.vRefNum)->CountEntries->Value;
     

-- DirEmpty: DoPart --
do (NoOfEntries = 0)->Value;   

-- DirDelete: Descriptor --
(# res: @Integer; 
do
   entry.private.FSpCheckNil;
   (if empty
    // true then
       entry.private.fs[]->entryDelete->convert->res;
       (if res // 0 then  else error;  if);
       
    else
       error
   if);
   
#)  

-- DirCreateFile: DoPart --
do
     (# 
        makeFile:
          (# 
             fs: ^FSSpec;
             Creator: (# exit 'MPS '->Text2ResType #);
             Type: (# exit 'TEXT'->Text2ResType #);
             p: ^Text;
             result: @Integer;
             li: ^longInt;
          enter fs[]
          do
             (if HasFSSpecCalls
              // true then
                 (fs[],Creator,Type,- 1)->FSpCreate->convert->result; 
              else
                 fs.name->p[];
				 (* OLM: 27/19/96: someone Please check this! *)
				 &longInt[] -> li[]; fs.parID->li;
                 (fs.vRefNum,fs.parID,p,Creator,Type)->HCreate->convert->result;
                 
             if);
             
          exit result
          #);
        
     do
        entry.private.FSpCheckNil;
        &EntryDesc[]->newEntry[];
        &FSSpec[]->newEntry.private.fs[];
        (entry.private.fs.vRefNum,entry.private.dirID,name,
         newEntry.private.fs[])->MakeFSSpec;
        TRUE->newEntry.private.FSSpecValid;
        (if (newEntry.private.fs[]->makeFile) // 0 then  else error;  if);
        
     #);
     

-- DirDeleteFile: DoPart --
do
     (# fs: @FSSpec; res: @Integer; 
     do
        entry.private.FSpCheckNil;
        (entry.private.fs.vRefNum,entry.private.dirID,name,fs[])->MakeFSSpec
          ->convert->res;
        (if res // 0 then fs[]->entryDelete->convert->res;  if);
        (if res // 0 then  else error;  if);
        
     #);
     

-- DirCreateDir: DoPart --
do
     (# 
        makeDir:
          (# fs: ^FSSpec; dirID: ^LongInt; p: ^Text; result: @Integer; 
          enter (fs[],dirID[])
          do
             (if HasFSSpecCalls
              // true then
                 (fs[],- 1,dirID[])->FSpDirCreate->convert->result; 
              else
                 fs.name->p[];
                 (fs.vRefNum,fs.parID,p,dirID[])->DirCreate->convert->result;
                 
             if);
             
          exit result
          #);
        
     do
        entry.private.FSpCheckNil;
        (if Name[]
         // none then error
         else
            &EntryDesc[]->newEntry[];
            &FSSpec[]->newEntry.private.fs[];
            (entry.private.fs.vRefNum,entry.private.dirID,name,
             newEntry.private.fs[])->MakeFSSpec;
            (if ((newEntry.private.fs[],newEntry.private.dirID[])->makeDir)
             // 0 then
                newEntry.private.fs[]->FSpGetDirID->newEntry.private.dirID;
                TRUE->newEntry.private.FSSpecValid;
                
             else
                error; 
            if);
            
        if);
        
     #);
     

-- DirDeleteDir: DoPart --
do
     (# fs: @FSSpec; res: @Integer; 
     do
        entry.private.FSpCheckNil;
        (entry.private.fs.vRefNum,entry.private.dirID,name,fs[])->MakeFSSpec
          ->convert->res;
        (if res // 0 then fs[]->entryDelete->convert->res;  if);
        (if res // 0 then  else error;  if);
        
     #);
     

-- DirFindSelect: Descriptor --
(# do (if found[] // none then NotFoundException;  if);  #)  

-- DirFindDispatch: Descriptor --
(# 
do
   (if selectedInInner
    // false then
       (if true
        // found.isFile then whenfile;  // found.isDirectory then whendir; 
        else
           whenother; 
       if)
   if);
   
#)  

-- DirFindFullPath: Descriptor --
(# vRefnum: @integer;
   parID: @integer;
   error: @integer;
   spec: @FSSpec;
   path: ^text;
do 
   (if found[]
    // none then NotFoundException; 
    else
	   entry.private.fs.vRefnum -> vRefnum;
	   entry.private.dirID -> parID;
	   found.path -> path[];
	   (vRefnum, parID, path, spec[]) -> MakeFSSpec -> error;
	   (if (error = 0) OR (error = -43) then
	      spec[] -> convertToFullPath -> p[];
	   if);
   if)
#)  

-- DirFindGetFile: Descriptor --
(# 
do
   (if found[]
    // none then NotFoundException; 
    else
       &foundFile[]->f[]; found.private.fs[]->f.entry.private.fs[]; 
	   true -> f.entry.private.FSSpecValid;
   if);
   
#)  

-- DirFindGetDir: Descriptor --
(# 
do
   (if found[]
    // none then NotFoundException; 
    else
       &foundDir[]->d[];
       found.private.fs[]->d.entry.private.fs[];
       found.private.dirID->d.entry.private.dirID;
	   true -> d.entry.private.FSSpecValid;
   if);
   
#)  

-- DirFindEntry: Descriptor --
(# fs: ^FSSpec; 
do
   (if candidate[]
    // none then 
    else
       entry.private.FSpCheckNil;
       &FSSpec[]->fs[];
       (if
       (entry.private.fs.vRefNum,entry.private.dirID,candidate,fs[])->MakeFSSpec
        // 0 then
           &foundDesc[]->found[];
           fs[]->found.private.fs[];
           TRUE->found.private.FSSpecValid;
           (if (found.private.fs[]->FSpIsDir->convert)
            // 1 then found.private.fs[]->FSpGetDirID->found.private.dirID; 
           if);
           INNER findEntry;
           
        // - 43 then
           notFound; 
        else
           DirSearchException; 
       if);
       
   if);
   
#)  

-- DirScanFullPath: Descriptor --
(# vRefnum: @integer;
   parID: @integer;
   error: @integer;
   spec: @FSSpec;
   path: ^text;
do 
   (if found[]
    // none then NotFoundException; 
    else
	   entry.private.fs.vRefnum -> vRefnum;
	   entry.private.dirID -> parID;
	   found.path -> path[];
	   
	   (vRefnum, parID, path, spec[]) -> MakeFSSpec -> error;
		  
	   (if (error = 0) OR (error = -43) then
	      spec[] -> convertToFullPath -> p[];
	   if);
   if)
#)  

-- DirScanSelect: Descriptor --
(# do (if found[] // none then NotFoundException;  if);  #)  

-- DirScanDispatch: Descriptor --
(# 
do
   (if selectedInInner
    // false then
       (if true
        // found.isFile then whenfile;  // found.isDirectory then whendir; 
        else
           whenother; 
       if)
   if);
   
#)  

-- DirScanGetFile: Descriptor --
(# 
do
   (if found[]
    // none then NotFoundException; 
    else
       &foundFile[]->f[]; found.private.fs[]->f.entry.private.fs[]; 
	   true -> f.entry.private.fsspecvalid;(* datpete *)
   if);
   
#)  

-- DirScanGetDir: Descriptor --
(# 
do
   (if found[]
    // none then NotFoundException; 
    else
       &foundDir[]->d[];
       found.private.fs[]->d.entry.private.fs[];
       found.private.dirID->d.entry.private.dirID;
	   true -> d.entry.private.fsspecvalid;(* datpete *)
       
   if);
   
#)  

-- DirScanEntries: Descriptor --
(# specs: [1] ^foundDesc; CurName: @Text; itsName: ^Text; res: @Integer; 
do
   entry.private.FSpCheckNil;
   NoOfEntries->specs.new;
   (for i: specs.range repeat
     &foundDesc[]->specs[i][];
     &FSSpec[]->specs[i].private.fs[];
     CurName.Clear;
     (entry.private.dirID,entry.private.fs.vRefNum,i)->GetIndEntryName->pToC
       ->CurName;
     (entry.private.fs.vRefNum,entry.private.dirID,CurName,
      specs[i].private.fs[])->MakeFSSpec;
	  curName -> specs[i].private.path;
     TRUE->specs[i].private.FSSpecValid;
     (if (specs[i].private.fs[]->FSpIsDir->convert)
      // 1 then specs[i].private.fs[]->FSpGetDirID->specs[i].private.dirID; 
     if);
     specs[i].path->itsName[];
     (if itsName.Length > longest
      // true then itsName.Length->longest; 
     if);
     
   for);
   (for i: specs.range repeat
     specs[i][]->found[];
     (if (specs[i].private.fs[]->FSpIsDir->convert)
      // 1 then specs[i].private.dirID->found.private.dirID; 
     if);
     INNER scanEntries;
     
   for);
   
#)  

