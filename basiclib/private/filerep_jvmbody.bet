ORIGIN 'filerepbody';
INCLUDE 'file_jvmbody';

---- lib: attributes ---
bytewise: 
  (# exit true #);

---- FileRepSave: descriptor ----
(# f: @file;
   x, y: @int32; (* Only needed for bytewise *)
do (if filename[]=NONE then &text[]->filename[] if);
   filename[] -> f.name;
   f.OpenWrite; 
   (for i:Top repeat
        (if bytewise then
            R[i]->x;
            (x %srl 24) %Band 0xff -> y -> f.private.raf.writeByte;
            (x %srl 16) %Band 0xff -> y -> f.private.raf.writeByte;
            (x %srl  8) %Band 0xff -> y -> f.private.raf.writeByte;
            (x %srl  0) %Band 0xff -> y -> f.private.raf.writeByte;
         else
            R[i] -> f.private.raf.writeInt;
        if);
   for);
   f.close
#)

---- FileRepRestore: descriptor ----
(# f: @file;
   x: @int32; (* Only needed for bytewise *)
do (if filename[]=NONE then &text[]->filename[] if);
   filename[] -> f.name;
   F.OpenRead; 
   (F.length+3) div 4 -> R.new;
   (for i:R.range repeat
        (if bytewise then
            0 -> x;
            ((f.private.raf.readByte %band 0xff) %sll 24) %Bor x -> x;
            ((f.private.raf.readByte %band 0xff) %sll 16) %Bor x -> x;
            ((f.private.raf.readByte %band 0xff) %sll  8) %Bor x -> x;
            ((f.private.raf.readByte %band 0xff) %sll  0) %Bor x -> x;
            x->R[i];
         else
            f.private.raf.readInt->R[i];
        if)
   for);
   F.close;
   R.range ->Top;
#)
