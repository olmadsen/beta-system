ORIGIN '../formatio';
LIB_ITEM 'formatio';
INCLUDE '../numberio'
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1995-98
 *       All rights reserved.
 *)   
--- formatIoGetFormatWidthDopart: dopart ---
do w->private.defaultWidth
   
--- formatIoGetFormatPrecisionDopart: dopart ---
do p->private.defaultPrecision

--- formatIoGetFormatMarkerDopart: dopart ---
do (if private.formatStr_peek = mark then private.formatStr_get; INNER marker
    else mark->illegalFormat
   if)

--- formatIoGetFormatDmarkerDopart: dopart ---
do getInt->value
   
--- formatIoGetFormatImarkerDopart: dopart ---
do scanWhiteSpace;
   (if private.doPeek = '0' then private.doGet;
       (if private.doPeek//'x'//'X' then private.doGet; 16->getRadix->value
	else (if private.doPeek->ascii.isWhiteSpace then 0->value
              else 8->getRadix->value
             if)
       if)
    else getInt->value
   if)
   
--- formatIoGetFormatOmarkerDopart: dopart ---
do 8->getRadix->value
   
--- formatIoGetFormatXmarkerDopart: dopart ---
do 16->getRadix->value
   
--- formatIoGetFormatUXmarkerDopart: dopart ---
do 16->getRadix->value
   
--- formatIoGetFormatRmarkerDopart: dopart ---
do private.precisionSpec->getRadix->value
   
--- formatIoGetFormatURmarkerDopart: dopart ---
do private.precisionSpec->getRadix->value
   
--- formatIoGetFormatBmarkerDopart: dopart ---
do getBased->(base,value)
   
--- formatIoGetFormatUBmarkerDopart: dopart ---
do getBased->(base,value)
   
--- formatIoGetFormatFmarkerDopart: dopart ---
do getreal->value
   
--- formatIoGetFormatEmarkerDopart: dopart ---
do getreal->value
   
--- formatIoGetFormatUEmarkerDopart: dopart ---
do getreal->value
   
--- formatIoGetFormatGmarkerDopart: dopart ---
do getreal->value
   
--- formatIoGetFormatUGmarkerDopart: dopart ---
do getreal->value
   
--- formatIoGetFormatCmarkerDopart: dopart ---
do private.doGet->value
   
--- formatIoGetFormatSmarkerDopart: dopart ---
do scanWhiteSpace;
   (if private.fieldWidth > 0 then
       &text[]->value[];
       L: scan(# i: @integer;
		 while::< (# do not (ch->ascii.isWhiteSpace)->value #)
	      do ch->value.put; i+1->i;
		 (if i = private.fieldWidth then leave L if);
	      #)
    else
       getAtom->value[]; (if not eos then (* skip the space *) private.doGet if)
   if)

--- formatIoGetFormatNmarkerDopart: dopart ---
do getPos-private.startPos->value
   
--- formatIoGetFormatMatchDopart:dopart ---
do (if ch=private.doPeek then
       (if not eos then private.doGet if)
    else (private.doPeek, ch)->inputError
   if)
   
--- formatIoPutFormatWidthDopart: dopart ---
do w->private.defaultWidth
   
--- formatIoPutFormatPrecisionDopart: dopart ---
do p->private.defaultPrecision
   
--- formatIoPutFormatMarkerDopart: dopart ---
do (if private.formatStr_peek = mark then private.formatStr_get; INNER marker
    else mark->illegalFormat
   if);
   
--- formatIoPutFormatDmarkerDopart: dopart ---
do value->private.t.putInt
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      (if private.fieldWidth>0 then private.fieldWidth->width if)
   #)
   
--- formatIoPutFormatImarkerDopart: dopart ---
do value->private.t.putInt
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      (if private.fieldWidth>0 then private.fieldWidth->width if)
   #)
   
--- formatIoPutFormatYmarkerDopart: doPart ---
do  
   (if value>=0 then (* print signed *)
       (2,value)->private.t.putBased
       (# do leftFlag->adjustLeft; signedFlag->signed;
          blankFlag->blankSign; zeroFlag->zeroPadding;
          true->noBasePrefix;
          (if private.fieldWidth>0 then private.fieldWidth->width if)
       #)
    else  (* print signed using putBinary *)
       l:(# ch:@char;
         do
            (if zeroFlag then '0'-> ch; if);
            (if leftFlag then
                (for i:private.fieldWidth-11 repeat
                     ch->private.t.put;
                for);
            if);
            value->private.t.putBinary;
            (if not leftFlag then
                (for i:private.fieldWidth-11 repeat
                     ch->private.t.put;
                for);
            if);
         #);
   if);
   
--- formatIoPutFormatOmarkerDopart: dopart ---
do  
   (if value>=0 then (* print signed *)
        (8,value)->private.t.putBased
        (# do leftFlag->adjustLeft; signedFlag->signed;
           blankFlag->blankSign; zeroFlag->zeroPadding;
           true->noBasePrefix;
           (if private.fieldWidth>0 then private.fieldWidth->width if)
        #)
     else  (* print signed using putOctal *)
        l:(# ch:@char;
          do
             (if zeroFlag then '0'-> ch; if);
             (if leftFlag then
                 (for i:private.fieldWidth-11 repeat
                      ch->private.t.put;
                 for);
             if);
             value->private.t.putOctal;
             (if not leftFlag then
                 (for i:private.fieldWidth-11 repeat
                      ch->private.t.put;
                 for);
             if);
          #);
    if);
   
--- formatIoPutFormatXmarkerDopart: dopart ---
do  
   (if value>=0 then (* print signed *)
       (16,value)->private.t.putBased
       (# do leftFlag->adjustLeft; signedFlag->signed;
          blankFlag->blankSign; zeroFlag->zeroPadding;
          true->noBasePrefix;
          (if private.fieldWidth>0 then private.fieldWidth->width if)
       #)
    else  (* print signed using putHex *)
       l:(# ch:@char;
         do
            (if zeroFlag then '0'-> ch; if);
            (if leftFlag then
                (for i:private.fieldWidth-8 repeat
                     ch->private.t.put;
                for);
            if);
            value->private.t.putHex(# do false->uppercase #);
            (if not leftFlag then
                (for i:private.fieldWidth-8 repeat
                     ch->private.t.put;
                for);
            if);
         #);
   if);
   
--- formatIoPutFormatUXmarkerDopart: dopart ---
do 
   (if value>=0 then (* print signed *)
       (16,value)->private.t.putBased
       (# do leftFlag->adjustLeft; signedFlag->signed;
          blankFlag->blankSign; zeroFlag->zeroPadding;
          true->noBasePrefix;
          (if private.fieldWidth>0 then private.fieldWidth->width if)
       #)
    else  (* print signed using putHex *)
       l:(# ch:@char;
         do
            (if zeroFlag then '0'-> ch; if);
            (if leftFlag then
                (for i:private.fieldWidth-8 repeat
                     ch->private.t.put;
                for);
            if);
            value->private.t.putHex(# do true->uppercase #);
            (if not leftFlag then
                (for i:private.fieldWidth-8 repeat
                     ch->private.t.put;
                for);
            if);
         #);
   if);
   
--- formatIoPutFormatRmarkerDopart: dopart ---
do (private.precisionSpec,value)->private.t.putBased
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      true->noBasePrefix;
      (if private.fieldWidth>0 then private.fieldWidth->width if)
   #)
   
--- formatIoPutFormatURmarkerDopart: dopart ---
do (private.precisionSpec,value)->private.t.putBased
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      true->uppercase; true->noBasePrefix;
      (if private.fieldWidth>0 then private.fieldWidth->width if)
   #)
   
--- formatIoPutFormatBmarkerDopart: dopart ---
do (private.precisionSpec,value)->private.t.putBased
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      (if private.fieldWidth>0 then private.fieldWidth->width if)
   #)
   
--- formatIoPutFormatUBmarkerDopart: dopart ---
do (private.precisionSpec,value)->private.t.putBased
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      true->uppercase;
      (if private.fieldWidth>0 then private.fieldWidth->width if)
   #)
   
--- formatIoPutFormatFmarkerDopart: dopart ---
do value->private.t.putreal
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      noexp->style;
      (if private.fieldWidth>0 then private.fieldWidth->width if);
      (if private.precisionSpec>0 then private.precisionSpec->precision if)
   #)
   
--- formatIoPutFormatEmarkerDopart: dopart ---
do value->private.t.putreal
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      exp->style;
      (if private.fieldWidth>0 then private.fieldWidth->width if);
      (if private.precisionSpec>0 then private.precisionSpec->precision if)
   #)
   
--- formatIoPutFormatUEmarkerDopart: dopart ---
do value->private.t.putreal
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      exp->style; true->upcase;
      (if private.fieldWidth>0 then private.fieldWidth->width if);
      (if private.precisionSpec>0 then private.precisionSpec->precision if)
   #)
   
--- formatIoPutFormatGmarkerDopart: dopart ---
do value->private.t.putreal
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      plain->style;
      (if private.fieldWidth>0 then private.fieldWidth->width if);
      (if private.precisionSpec>0 then private.precisionSpec->precision if)
   #)
   
--- formatIoPutFormatUGmarkerDopart: dopart ---
do value->private.t.putreal
   (# do leftFlag->adjustLeft; signedFlag->signed;
      blankFlag->blankSign; zeroFlag->zeroPadding;
      plain->style; true->upcase;
      (if private.fieldWidth>0 then private.fieldWidth->width if);
      (if private.precisionSpec>0 then private.precisionSpec->precision if)
   #)
   
--- formatIoPutFormatCmarkerBody: descriptor ---
(# space: @integer
do private.fieldWidth-1->space;
   (if leftFlag then
       (for space repeat ' '->private.t.put for); value->private.t.put
    else value->private.t.put; (for space repeat ' '->private.t.put for)
   if)
#)

--- formatIoPutFormatSmarkerBody: descriptor ---
(# txt: ^text; space: @integer
do (if value[]=NONE then ''->value[] if);
   (if private.precisionSpec>0 then
       (1,private.precisionSpec)->value.sub->txt[]
    else value[]->txt[]
   if);
   private.fieldWidth-txt.length->space;
   (if leftFlag then
       (for space repeat ' '->private.t.put for); txt[]->private.t.puttext
    else txt[]->private.t.puttext; (for space repeat ' '->private.t.put for)
   if)
#)

--formatIoPutFormatTmarkerDopart:dopart--
do 
   (if value then
       trueText->localTrueText->private.t.puttext 
    else
       falseText->localFalseText->private.t.puttext 
   if)

--- formatIoPutFormatNmarkerDopart: dopart ---
do private.t.length->value

--- formatIoPutFormatMatchDopart:dopart ---
do ch->private.t.put

--- formatIoScanForMarkerDopart: dopart ---
do L:
     (#
     do -1->private.fieldWidth->private.precisionSpec;
	false->leftFlag->signedFlag->blankFlag
	  ->alternativeFlag->zeroFlag->longFlag;
	(if true
	 // formatStr.eos then
	    formatEOS;
	 // private.formatStr_peek = '%' then
	    private.formatStr_get;
	    (if private.formatStr_peek = '%' then
		'%'->private.doMatch; private.formatStr_get; restart L
	    if);
	    (if private.formatStr_peek (* test for flags *)
	     // '-' then true->leftFlag; private.formatStr_get
	     // '+' then true->signedFlag; private.formatStr_get
	     // ' ' then true->blankFlag; private.formatStr_get
	    if);
	    (if private.formatStr_peek = '0' then (* test for zero padding *)
		true->zeroFlag; private.formatStr_get
	    if);
	    (if private.formatStr_peek (* test for field width *)
	     //'0'//'1'//'2'//'3'//'4'//'5'//'6'//'7'//'8'//'9' then
		formatStr.getInt->private.fieldWidth
	     //'*' then private.defaultWidth->private.fieldWidth; private.formatStr_get
	    if);
	    (if private.formatStr_peek = '.' then (* test for precision specification *)
		private.formatStr_get;
		(if private.formatStr_peek = '0' then (* test for zero padding *)
		    true->zeroFlag; private.formatStr_get
		if);
		(if private.formatStr_peek (* test for precision *)
		 //'0'//'1'//'2'//'3'//'4'//'5'//'6'//'7'//'8'//'9' then
		    formatStr.getInt->private.precisionSpec
		 //'*' then private.defaultPrecision->private.precisionSpec; private.formatStr_get
		if)
	    if);
	    INNER scanForMarker
	 else
	    private.formatStr_get->private.doMatch; restart L
	if)
     #)

--- formatIoFormatterIllegalFormatDopart: dopart ---
do 'Format mismatch:'->msg.putline;
   '\t%'->msg.puttext; private.formatStr_peek->msg.put; ' found in format string:'->msg.putline;
   '\t\t"'->msg.puttext; formatStr[]->msg.puttext; '"'->msg.putline;
   '\t\tat position: '->msg.puttext; formatStr.pos->msg.putInt; msg.newline;
   '\t%'->msg.puttext; mark->msg.put; ' field specified'->msg.putline;
   '\tposition in stream: '->msg.puttext; getPos->msg.putInt; msg.newline;
   INNER illegalFormat

--- formatIoFormatterMissingMarkerDopart: dopart ---
do 'Missing marker:'->msg.putline;
   '\t%'->msg.puttext; mark->msg.put; ' expected in format string:'->msg.putline;
   '\t\t"'->msg.puttext; formatStr[]->msg.puttext; '"'->msg.putline;
   '\tbut not found before end of format string reached'->msg.putline;
   INNER missingMarker

--- formatIoFormatterMissingFieldDopart: dopart ---
do 'Missing field:'->msg.putline;
   '\t%'->msg.puttext; private.formatStr_peek->msg.put; ' found in format string:'->msg.putline;
   '\t\t"'->msg.puttext; formatStr[]->msg.puttext; '"'->msg.putline;
   '\t\tat position: '->msg.puttext; formatStr.pos->msg.putInt; msg.newline;
   '\tno corresponding field specified'->msg.putline;
   INNER missingField

--- formatIoFormatterInputErrorDopart: dopart ---
do 'Format mismatch:'->msg.putline;
   '\t"'->msg.puttext; chExpected->msg.put; '" found in format string:'->msg.putline;
   '\t\t"'->msg.puttext; formatStr[]->msg.puttext; '"'->msg.putline;
   '\t\tat position: '->msg.puttext; formatStr.pos->msg.putInt; msg.newline;
   '\t"'->msg.puttext; chFound->msg.put; '" found in stream'->msg.putline;
   '\t\tat position: '->msg.puttext; getPos->msg.putInt; msg.newline;
   INNER inputError

--- formatIoFormatterPrivate: descriptor ---
(# defaultWidth, defaultPrecision: @integer;
   fieldWidth, precisionSpec: @integer;
   startPos: @integer;
   doGet: @get;
   formatStr_get: ^formatStr.get;
   doPeek: @peek;
   formatStr_peek: ^formatStr.peek;
   doMatch: @match;
   t: ^text
#)

--- formatIoFormatterDopart: dopart ---
do &formatStr.get[]->private.formatStr_get[];
   &formatStr.peek[]->private.formatStr_peek[];
   &text[]->private.t[]; getpos->private.startPos;
   formatStr.reset;
   -1->private.defaultWidth->private.defaultPrecision;
   INNER formatter;
   (if not formatStr.eos then
       scanForMarker
       (# formatEOS:: (# do true->continue #)
       do missingField
       #)
   if);
   private.t[]->puttext
