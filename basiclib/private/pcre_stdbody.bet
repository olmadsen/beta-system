ORIGIN 'pcrebody';
INCLUDE 'pcrelib';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 2000
 *       All rights reserved.
 *       Written by Erik Corry
 *)

--- PcreInit: dopart ----
do (# error: ^CString;
      opt: @Integer;
      errtext: @Integer;
      erroffset: @Integer;
   do options -> opt;
      (if opt %Band pcre_C_LOCALE <> 0 then
	  (@@exp.T[1],
	  opt %Band pcre_NONBETAOPTIONS,
	  @@errtext,
	  @@erroffset, 0) -> pcre_compile -> private.compiled_regexp;
       else
	  (@@exp.T[1],
	  opt %Band pcre_NONBETAOPTIONS,
	  @@errtext,
	  @@erroffset) -> locale_pcre_compile -> private.compiled_regexp;
      if);
      (if private.compiled_regexp = 0 then
	  &CString[] -> error[];
	  errtext -> error;
	  error.get -> compilation_error;
       else
          (if opt %Band pcre_DO_STUDY <> 0 then
	      (private.compiled_regexp, opt %Band pcre_NONBETAOPTIONS, @@errtext) ->
              pcre_study -> private.extra;
	      (if errtext <> 0 then
	          &CString[] -> error[];
	          errtext -> error;
	          error.get -> compilation_error;
	      if);
	  if);
	  (private.compiled_regexp, private.extra, pcre_INFO_CAPTURECOUNT, @@subpatterns) ->
          pcre_fullinfo;
	  ((subpatterns + 1) * 3) -> private.oVector.new;
      if);
   #);
   
-- PcreExec: dopart--
do (private.compiled_regexp, 
   private.extra, 
   @@subject.T[1], 
   subject.lgth,
   position, 
   options, 
   @@private.oVector[1], 
   private.oVector.range) 
     -> pcre_exec 
     -> status;
