ORIGIN 'betaenvbody';
BODY 'console';
OBJFILE mac '$/betaenv_macbody.c.o';
MAKE    mac 'external/betaenv_macbody.make'
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1984-95
 *       All rights reserved.
 *
 *)
--betaenvStop:descriptor--
(# BetaExit : External(# status : @integer enter status do CallC #);
   FailureExit: External(#do CallC #)
do newLine; 
   (if T[]<>NONE then T[]->putText; newline; if);
   TerminateConsole;
   (if termCode
    // normal  then 0->BetaExit
    // failure then 1->BetaExit;
    // failureTrace then FailureExit
if)#)
--betaenvbodyMDInitAscii: descriptor--
(* initialize upCase and lowCase *)
     (# UClc : @
        (# UC,lc : @integer
        enter (UC,lc)
        do UC -> private.UPCASE[lc]; lc -> private.lowCase[UC];
        #)
     do  (128,138) -> UClc; (* swedish ae *)     
         (129,140) -> UClc; (* danish aa *)
         (130,141) -> UClc; (* french c *)
         (131,142) -> UClc; (* french e *)
         (132,150) -> UClc; (* strange n *)
         (133,154) -> UClc; (* swedish o *)     
         (134,159) -> UClc; (* german U *)
         (174,190) -> UClc; (* danish ae *)
         (175,191) -> UClc; (* danish oe *)
         (202, 32) -> UClc; (* nonbreaking space *)
         (203,136) -> UClc; (* funny french a *)
         (204,139) -> UClc; (* funny french a *)
         (205,155) -> UClc; (* funny french o *)
         (206,207) -> UClc; (* french ae *)
     #)

--betaenvInitEnv: descriptor--
   (# 
      UnixScreen: Stream (* A standard UNIX screen *)
      (# Eos::< (#do False -> value #);
         OtherError::< (# do 'OtherError in Screen ' -> msg.puttext #);
         Put::< 
           (# PutToScreen: External(# ch: @integer enter ch do CallC #);
           do ch -> PutToScreen;
           #);
         PutText::<    (* For efficiency reasons. *)
           (# PutTextToScreen: External(# t: @integer enter t #);
           do (if T.lgth > 0 then
                 (if T.lgth < T.T.range then 0 -> T.T[T.lgth+1] if);
                 @@T.T[1]->PutTextToScreen;
             if)
           #);
      #);

      UnixKeyBoard: Stream (* A standard Unix keyboard *)
      (#
         Eos::<
           (# KeyboardEOS: External
                (# yes: @boolean
                do CallC
                exit yes
                #);
           do KeyboardEOS -> value;
           #);
         OtherError::< (#do 'Keyboard ' -> msg.puttext #); (****????*)
         Get::<
           (# GetFromKeyboard: External
                (# ch: @char;
                do CallC
                exit ch #);
           do GetFromKeyboard -> ch;
           #);
         GetAtom::<
           (# KeyboardPeek: External(# ch: @char do CallC exit ch #);
	      GetFromKeyboard: External(# ch: @char do CallC exit ch #);
           do skipBlanks:
                (if KeyboardPeek->ascii.isWhiteSpace then
                    GetFromKeyboard; restart skipBlanks 
                if);
              scan:
                (if not (KeyboardPeek->ascii.isWhiteSpace) then
                    (if (txt.pos+1->txt.pos) > txt.T.range then txt.T.range+16->txt.T.extend if);
                    GetFromKeyboard->txt.T[txt.pos]; txt.lgth+1->txt.lgth;
                    restart scan
                if)
	   #);
         GetLine::<
           (# KeyboardPeek: External(# ch: @char do CallC exit ch #);
	      GetFromKeyboard: External(# ch: @char do CallC exit ch #);
           do scan:
                (if KeyboardPeek<>ascii.newline then
                    (if (txt.pos+1->txt.pos) > txt.T.range then txt.T.range+16->txt.T.extend if);
                    GetFromKeyboard->txt.T[txt.pos]; txt.lgth+1->txt.lgth;
                    restart scan
                if)
           #);
         Peek::<
           (# KeyboardPeek: External(# ch: @char do CallC exit ch #)
           do KeyboardPeek->ch
           #);
      #);
      
      getStandAlone: external
        (# isRunningAlone: @integer do callC exit isRunningAlone #);
      
   do 
      ascii.init;
      ascii.cr -> ascii.newLine;
      (if getStandAlone=1 then 
		   <<SLOT betaenvmacbodyStartConsole: descriptor>>; (* kjm *)
	   else
	     (* a MPW tool *)
		  &UnixScreen[] -> screen[]; 
		  &UnixKeyBoard[] -> keyboard[];
	  if)
   #)

--betaenvTerminateEnv: descriptor--
    (# do TerminateConsole #)

--betaenvbodyTerminateConsole: descriptor--
    (# do TerminateConsole #)

-- LIB: attributes --

TerminateConsole: <<SLOT betaenvmacbodyDoTerminateConsole: descriptor>>
