ORIGIN 'betaenvbody';
MDBODY  default 'console_macbody';

LINKOPT default '"{PPCLibraries}"OpenTptInetPPC.o';
LINKOPT default '"{PPCLibraries}"OpenTransportAppPPC.o';
LINKOPT default '"{SharedLibraries}"OpenTransportLib';
LINKOPT default '"{SharedLibraries}"OpenTptInternetLib';



BUILD default ':$$:betaenv_mac.obj' ':external:betaenv_macbody.c' 'mrc -proto strict -proto auto -shared_lib_export on -o $0 $1';

RESOURCE default 'external/betaenv.rsrc'
-- betaenvbodyMDInitAscii: descriptor -- (* initialize upCase and lowCase *)
 (# UClc : @
	(# UC,lc : @integer
	enter (UC,lc)
	do UC -> private.UPCASE[lc]; lc -> private.lowCase[UC];
	#)
 do  (128,138) -> UClc; (* swedish ae *)     
	 (129,140) -> UClc; (* danish aa *)
	 (130,141) -> UClc; (* french c *)
	 (131,142) -> UClc; (* french e *)
	 (132,150) -> UClc; (* strange n *)
	 (133,154) -> UClc; (* swedish o *)     
	 (134,159) -> UClc; (* german U *)
	 (174,190) -> UClc; (* danish ae *)
	 (175,191) -> UClc; (* danish oe *)
	 (202, 32) -> UClc; (* nonbreaking space *)
	 (203,136) -> UClc; (* funny french a *)
	 (204,139) -> UClc; (* funny french a *)
	 (205,155) -> UClc; (* funny french o *)
	 (206,207) -> UClc; (* french ae *)
   (* Initialize private.NonWhiteSpace. Offsett by one to allow test for
    * isNonWhiteSpace(0)
    *)
   (* From '!'(33) to '~'(126) is NonWhiteSpace *)
   (for ch: (126-33+1) repeat true->private.NonWhiteSpace[ch+33] for);
   (* From <inverted exclamation>(161) to <y umlaut>(255) is NonWhiteSpace 
    * in Macintosh ASCII table
    *)
   (for ch: (255-161+1) repeat true->private.NonWhiteSpace[ch+161] for);
 #)

--betaenvinitEnv: descriptor--
(# getStandAlone: external
     (# isRunningAlone: @integer do callC exit isRunningAlone #);
   
   getGUI:
     (# value: @integer;
	 <<SLOT betaenvGetGui: doPart>>
	 exit value
	 #);
do ascii.init;
   ascii.cr -> ascii.newLine;
   &rawArgumentHandler[]->argumentHandler[];
   
   (if getStandAlone=1 then
   	   (if getGUI = 1 then
	      &stream[] -> screen[];
		  &stream[] -> keyboard[];
	   else
          <<SLOT StartConsole: descriptor>>;
	   if);
    else
	   (* an MPW tool *)
	   &DefaultScreen[] -> screen[]; 
	   &DefaultKeyboard[] -> keyboard[];
	   
	   (if getGUI = 1 then
	      exception (# do 'GUIenv applications must be build as applications' -> msg #);
	   if);
   if);
   SetupExceptions; (* Set up exceptions. Used to switch between old and new
                     * exceptions *)
#)

-- betaenvterminateEnv: descriptor --
    (# do TerminateConsole #)

-- betaenvBodyTerminateConsole: descriptor --
    (# do TerminateConsole #)

-- LIB: attributes --

TerminateConsole: <<SLOT DoTerminateConsole: descriptor>>

--- betaenvNoOfArguments: dopart ---
do Argc->value
   
--- betaenvArguments: dopart ---
do (if ((1<=argNo) and (argNo<=ArgC)) then
       &text[]->theArg[]; argNo->Argv->theArg
   if)
   
--- betaenvExpandArgumentsprivate: descriptor ---
(##)

--- betaenvExpandNoOfArguments: dopart ---
do Argc->value
   
--- betaenvExpandArguments: dopart ---
do (if ((1<=argNo) and (argNo<=ArgC)) then
       &text[]->theArg[]; argNo->Argv->theArg
   if)
