ORIGIN  'filebody';
BUILD nti '$$/fileInt_nt.obj' 'external/fileInt_nt.c'
          'betacc $0 $1';

(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1986-96
 *       All rights reserved.
 *)


(* This fragment group is the NT dependent implementation of files.
 * It uses a lot of calls to external C-routines from NT.
 *)

INCLUDE '~beta/win32lib/v1.6/ntinterface'

--LIB:attributes--

readMode: External
  (# result: @integer;
     binary: @boolean;
  enter binary
  do CallC
  exit result
  #);

writeCreateMode: External
  (* Open for writing, create if not existing *)
  (# result: @integer;
     binary: @boolean;
  enter binary
  do CallC
  exit result 
  #);

readWriteCreateMode: External
  (* Open for reading and  writing, create if not existing *)
  (# result: @integer;
     binary: @boolean;
  enter binary
  do CallC
  exit result
  #);

appendCreateMode: External
  (* Open for writing at the end, create if not existing *)
  (# result: @integer;
     binary: @boolean;
  enter binary
  do CallC
  exit result
  #);

readAppendCreateMode: External
  (* Open for reading and writing writing at the end, create if not existing *)
  (# result: @integer;
     binary: @boolean;
  enter binary
  do CallC
  exit result
  #);

noExecPerm: External
  (* Denotes creation of a file with read permission to all, but only
   * write permission to the owner of the file. Nobody is given 
   * execute permission to the file.
   *)
  (# result: @integer;
  do CallC
  exit result
  #);

execPerm: External
  (* Denotes creation of a file with read and execute permission to all,
   * but only write permission to the owner of the file. 
   *)
  (# result: @integer;
  do CallC
  exit result
  #);

EOFpeek: External
  (# ntStream: @integer;
     eof: @boolean;
  enter ntStream
  do CallC
  exit eof
  #);

setEntryModtime: External
  (# path: [1]@char;
     time: @integer;
     status: @integer; (* -1 => error *)
  enter (path,time)
  exit status
  #);

deleteNtFile: External
  (# path: [1]@char;
     status: @integer; (* -1 => error *)
  enter path
  exit status
  #);

createNtFile: External
  (# path: [1]@char;
     status: @integer; (* -1 => error *)
  enter path
  exit status
  #);

renameNtEntry: External
  (# newPath,oldPath: [1]@char;
     status: @integer; (* -1 => error *)
  enter(oldPath,newPath)
  exit status
  #);

chownNtEntry: External
  (# path: [1]@char;
     uid, gid: @integer;
     status: @integer; (* -1 => error *)
  enter(path,uid,gid)
  exit status
  #);

(* Operations for abtaining information about and manipulating
 * individual entries.
 * The entries are named by their absolute path
 *)

entryExists: External
  (# path: [1]@char;
     followlinks: @boolean (* ignored for NT *);
     status: @integer; (* -1 => error, 1 => exists, 0 => non-existing *)
  enter (path, followlinks)
  exit status
  #);

touchEntry: External
  (# path: [1]@char;
     status: @integer; (* -1 => error *)
  enter path
  exit status
  #);

changeProtection: External
  (# path: [1]@char;
     protectionBuffer: @integer; (* Start address of int-rep *)
     status: @integer; (* -1 => error *)
  enter(path,protectionBuffer)
  exit status
  #);

makeSymLink: External
  (* Make src point to dst. exits -1 if an error occurred *)
  (# src, dst: [1]@char;
     status: @integer;
     enter (src,dst)
  exit status
  #);

access: External
  (# path: [1]@char;
     mode: @integer;
     result: @integer;
  enter (path, mode)
  exit result
  #);

F_OK: (# exit 0 #);
R_OK: (# exit 4 #);
W_OK: (# exit 2 #);
X_OK: (# exit 1 #);

SetBinTxtMode:
  (# Mode: @text;
     Binary: @boolean;
  enter (Mode, Binary)
  do (if Binary then 'b'->Mode.append
      else 't'->Mode.append if)
  exit Mode
  #);

CheckIfText:
  (# textfile_is_text: External
       (# name: [1]@char;
          istext: @boolean;
       enter name
       do CallC
       exit istext
       #);
     name: ^Text;
     binary: @boolean;
  enter (name[],binary)
  do (if not binary then
         (if not (name -> textfile_is_text) then true -> binary
     if)if)
  exit binary
  #);

--DiskEntryException: dopart-- 
do 'Disk entry exception for '''->msg.puttext;
   path -> msg.puttext;
   ''''->msg.putline;
   'Error in system call: ' -> msg.puttext;
   getErrno -> errorstring -> msg.putline;
   INNER;

--DiskEntryPrivate: descriptor--
(# path: @text;
   pathchanged: @boolean;
   head, name, main, suf: @text;
   decomposepath: @
     (# i: @integer;
     do (if pathchanged then
	    (* decompose path *)
	    0->i;
            (* Assume \ is separator *)
            '\\' -> path.findAll(# do inx -> i #);
            (* No \ found, check for / instead *)
            (if i=0 then '/' -> path.findAll(# do inx -> i #); if);
	    (if i=0 then head.clear; path -> name;
	     else
		path.t[1:i-1] -> head;
		path.t[i+1:path.length] -> name;
	    if);
	    (* decompose name *)
            0->i;
	    '.' -> name.findAll(# do inx -> i #);
	    (if i=0 then suf.clear; name -> main;
	     else
		name.t[1:i-1] -> main;
		name.t[i:name.length] -> suf;
	    if);
	    false -> pathchanged
	if);
     #);
#)

--DiskEntryPathHead: dopart--
do INNER; &text[]->h[]; private.decomposepath; private.head -> h 

--DiskEntryPathNamePref: dopart--
do INNER; &text[] -> p[]; private.decomposepath; private.main -> p

--DiskEntryPathNameSuf: dopart--
do INNER; &text[]->s[]; private.decomposepath; private.suf -> s

--DiskEntryPathNameExt: dopart--
do INNER;
   &text[]->e[]; 
   private.decomposepath; 
   private.suf.t[2:private.suf.length] -> e

--DiskEntryPathNameGet: dopart--
do INNER; &text[]->n[]; private.decomposepath; private.name -> n

--DiskEntryPathSet: dopart--
do INNER;
   (if p[]=NONE then &text[]->p[] if);
   p -> private.path;
   true -> private.pathchanged;

--DiskEntryPathGet: dopart--
do INNER; private.path.copy -> p[]

--DiskEntryExists: dopart--
do (if ((private.path, 0) -> entryExists)
    // 0  then false -> value;
    // 1  then true -> value;
    // -1 then error;
   if);

--DiskEntryTouch: dopart--
do INNER;
   (if not isDirectory then
      (if (private.path -> touchEntry)=-1 then error;
      if)
   if);
   
--DiskEntrySize: dopart--
do (if private.dostat then -1 -> value; error;
    else private.statbuf[15] -> value;
   if)

--DiskEntryGetModTime:dopart--
do (if private.dostat then
       -1 -> time; error;
    else private.statbuf[17] -> time
   if)
--DiskEntrySetModTime:dopart--
do (if ((private.path,time) -> setEntryModtime)=-1 then error if);

--DiskEntryRename: dopart--
do (if newpath[]=NONE then &text[]->newpath[] if);
   (if ((private.path, 0) -> entryExists) <> 1 then error
    else
       (if ((private.path, newpath) -> renameNtEntry)=-1 then error;
        else newpath -> private.path;
       if);
   if);

--DiskEntryReadable: dopart--
do (((private.path, R_OK) -> access) = 0) -> value

--DiskEntryWriteable: descriptor--
(# dir: @text;
do (if ((private.path, F_OK) -> access)=0 then (* File exists *)
       (((private.path, W_OK) -> access) = 0) -> value
    else
       (* file does not exist; check if it can be created *)
       private.decomposepath; 
       private.head -> dir;
       (if dir.length=0 then '.' -> dir if);
       (((dir, W_OK + X_OK) -> access) = 0) -> value
   if)
#)

--DiskEntryIsDir: dopart--
do (if private.dostat then error;
    else private.statBuf[3]=1 -> value
   if)

--DiskEntryIsFile: dopart--
do (if private.dostat then error;
    else private.statBuf[6]=1 -> value
   if)



--FilePrivate:descriptor--
(# outputFile: @boolean; (* True iff this file is opened for writing. *)
   ntStream: @integer; (* NT FILE (cf. stdio.h)  pointer for file. *)
   NtErrNo (* may contain NT variable 'errno'*) : @ integer; 
   index: @integer; (* The NT filedescriptor index *)
   
   ProcessError:
     (# msg: ^text
     enter msg[]
     do (if (getErrno->NtErrNo)
	 // EPERM// EACCESS then msg[]->AccessError
	 // ENOENT then msg[]->NoSuchFileError
	 // EEXIST then msg[]->FileExistsError
	 // ENOSPC then msg[]->NoSpaceError
	 else
	    INNER
	if);
     #);
   
   SetUpNtStream:
     (# type: @text;
     enter type
     do
	(if index>=0 then
	    (if ((index,type) -> fdopen -> ntStream)=0 then
		processError(# do OtherError #);
	    if);
	if)
     #);
#)

--FileOtherException: dopart--
do 'Error in system call: ' -> msg.puttext;
   getErrno -> errorstring -> msg.putline;
   INNER

--FileTouch: dopart--
do (if ((entry.private.path, 0) -> entryExists) 
    // -1 then error;
    // 0 then
       (if (entry.private.path -> createntfile)=-1 then error if);
   if);
   INNER;

--FileLength: dopart--
do entry.size -> value

--FileDoOpen:descriptor--
(# open:External
     (# mode,permission,index: @integer; name: [1]@char
     enter(name,mode,permission)
     do CallC
     exit index
     #);

do (if exe then (name,mode,execPerm)  -> &open -> private.index;
    else (name,mode,noExecPerm) -> &open -> private.index;
   if);
     (if (private.index<0) then 
       private.processError(# do OtherError #);
   if);
#)

--FileOpenRead:dopart--
do INNER;
   (entry.private.path[],
      (entry.private.path[],binary)->CheckIfText->ReadMode,
      false) -> doOpen;
   ('r',(entry.private.path[],binary)->CheckIfText) -> SetBinTxtMode
      -> private.SetUpNtStream; 

--FileOpenWrite:dopart--
do INNER;
   (entry.private.path[],binary->WriteCreateMode,false) -> doOpen;
   true -> private.outputFile; 
   ('w',binary) -> SetBinTxtMode -> private.SetUpNtStream; 

--FileOpenReadWrite:dopart--
do INNER;
   (entry.private.path[],
      (entry.private.path[],binary)->CheckIfText->readWriteCreateMode,
      false) -> doOpen;
   true -> private.outputFile;
   ('r+',(entry.private.path[],binary)->CheckIfText) -> SetBinTxtMode
      -> private.SetUpNtStream; 

--FileOpenAppend:dopart--
do INNER;
   (entry.private.path[],
      (entry.private.path[],binary)->CheckIfText->appendCreateMode,
      false) -> doOpen;
   true -> private.outputFile;
   ('a',(entry.private.path[],binary)->CheckIfText) -> SetBinTxtMode
      -> private.SetUpNtStream; 

--FileOpenReadAppend:dopart--
do INNER;
   (entry.private.path[],
      (entry.private.path[],binary)->CheckIfText->readAppendCreateMode,
      false) -> doOpen;
   true -> private.outputFile;
   ('a+',(entry.private.path[],binary)->CheckIfText) -> SetBinTxtMode
      -> private.SetUpNtStream; 

--FileDelete:dopart--
do (if ((entry.private.path ->&unlink)<0) then
       'Cannot delete file'->private.processError(#do msg[]->&OtherError#)
   if);
   INNER;

--FilePut:dopart--
do INNER put; (if ((ch,private.ntStream)->fputc)=EOFvalue then WriteError if);

--FileGet:dopart--
do private.ntStream -> fgetc -> ch;
   (if ch=EOFvalue then   (* Either a read error or simply end of file. *)
       (if (private.ntStream -> EOFfunction) then
	   EOSError;
	else
	   ReadError;
       if);
   if);
   INNER get

--FilePeek:dopart--
do private.ntStream -> fgetc -> ch;
   (if ((ch,private.ntStream) -> ungetc)=EOFvalue 
       then   (* Either a read error or simply end of file. *)
       (if (private.ntStream -> EOFfunction) then
	   EOSError;
	else
	   ReadError;
       if);
   if);
   INNER peek
   
--FilePutText:dopart--
do INNER puttext;
   (if txt.lgth > 0 then
       (if txt.lgth < txt.T.range then 0 -> txt.T[txt.lgth+1] if);
       (if ((@@txt.T[1],private.ntStream)->fputs)= EOFvalue then WriteError if);
   if);

--FileGetAtom:dopart--
do (private.ntStream,false) -> GetTextFromStream -> txt;
   (if StreamError then '' -> txt if);
   INNER getAtom

--FileGetLine:dopart--
do (private.ntStream,true) -> GetTextFromStream -> txt;
   (if StreamError then '' -> txt if);
   INNER getline
   
--FileGetPos:dopart--
do private.ntStream -> ftell -> value; INNER getpos
   
--FileSetPos:dopart--
do INNER setpos; 
   (private.ntStream,p,mode) -> fseek -> newpos;      (* Ignore status. *)
   private.ntStream -> ftell -> newpos;  (* Get resulting absolute pos. *)
   
--FileEos:dopart--
do private.ntStream -> EOFpeek -> value;

--GetRep:dopart--
do (repAdr,1,length*4,private.ntStream) -> fread -> length; INNER getRep

--PutRep:dopart--
do INNER putRep; (repAdr,1,length*4,private.ntStream) -> fwrite -> length;

--FileFlush:dopart--
do (if private.OutputFile then
       (if (private.ntStream->fflush)=EOFvalue then WriteError; if);
   if);

--FileClose:dopart--
do (if (private.ntStream->fclose)=EOFvalue then OtherError; if);
   (* Note that the fclose call closes both the ntStream and the
    * filedescriptor (private.index). 
    *)


