ORIGIN  'filebody';
INCLUDE '~beta/dotnetlib/beta_helpers';
INCLUDE 'betaenv_clrbody';
INCLUDE '~beta/dotnetlib/System/IO/File';
INCLUDE '~beta/dotnetlib/System/IO/Directory';
INCLUDE '~beta/dotnetlib/System/IO/FileInfo';
INCLUDE '~beta/dotnetlib/System/IO/FileSystemInfo';
INCLUDE '~beta/dotnetlib/System/IO/DirectoryInfo';
INCLUDE '~beta/dotnetlib/System/IO/FileAttributes';
INCLUDE '~beta/dotnetlib/System/DateTime';
INCLUDE '~beta/dotnetlib/System/Convert';
INCLUDE '~beta/dotnetlib/System/IO/SeekOrigin';
INCLUDE '~beta/dotnetlib/System/IO/FileMode';
INCLUDE '~beta/dotnetlib/System/IO/StreamReader';
INCLUDE '~beta/dotnetlib/System/IO/StreamWriter';
INCLUDE '~beta/dotnetlib/System/IO/Stream';

(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1986-2003
 *       All rights reserved.
 *)


(* This fragment group is the .NET dependent implementation of files *)

--DiskEntryException: dopart-- 
do 'Disk entry exception for \''->msg.puttext;
   path -> msg.puttext;
   '\''->msg.putline;
   'Error in system call: ' -> msg.puttext;
   (*getErrno -> errorstring -> msg.putline;*)
   INNER;

--DiskEntryPrivate: descriptor--
(# path: @text;
   pathchanged: @boolean;
   head, name, main, suf: ^text;
   info: ^FileSystemInfo;
   finfo: ^FileInfo;
   dinfo: ^DirectoryInfo;
   checkInfo: booleanvalue
     (# 
     do (if true 
         // (path[]->DotnetDirectory.Exists) then
            (if dinfo[]=NONE then path[] -> &DirectoryInfo[] -> dinfo[] -> info[] if);
            true -> value;
         // (path[]->DotnetFile.Exists) then
            (if finfo[]=NONE then path[] -> &FileInfo[] -> finfo[] -> info[] if);
            true -> value;
         else 
            NONE -> info[];
            NONE -> dinfo[];
            NONE -> finfo[];
            false -> value;
        if);
     #);
   hasAttribute: booleanValue
     (# att: @int32;
     enter att
     do (if checkInfo then
            (if false then
                'file_clrbody: diskEntry.hasAttribute'  -> NYI;
                (* FIXME: HACK: bit-and of "reference" and integer!
                 * Works almost (by coincidence) if a pop instruction is deleted in the IL (:-)
                 *)
                (*((info.get_Attributes->asFileAttributes) %Band att = att) -> value; *)
            if);
            (info[], att) -> BetaHelpers.diskentryHasAttribute -> value;
        if);
     #);
   decomposepath: 
     (# i: @integer;
     do (if pathchanged then
	    (* decompose path. FIXME: Could use System.IO.Path *)
	    0->i;
            (* Assume \ is separator *)
            '\\' -> path.findAll(# do inx -> i #);
            (* No \ found, check for / instead *)
            (if i=0 then '/' -> path.findAll(# do inx -> i #); if);
	    &text[]->head[];
            (if i=0 then 
                path.copy -> name[];
	     else
		path.t[1:i-1] -> head;
                &text[] -> name[];
		path.t[i+1:path.length] -> name;
	    if);
	    (* decompose name *)
            0->i;
	    '.' -> name.findAll(# do inx -> i #);
            &text[] -> suf[];
	    (if i=0 then 
                name.copy -> main[];
	     else
                &text[] -> main[];
		name.t[1:i-1] -> main;
		name.t[i:name.length] -> suf;
	    if);
	    false -> pathchanged
	if);
     #);
#)

--DiskEntryPathHead: dopart--
do INNER; &text[]->h[]; private.decomposepath; private.head -> h 

--DiskEntryPathNamePref: dopart--
do INNER; &text[] -> p[]; private.decomposepath; private.main -> p

--DiskEntryPathNameSuf: dopart--
do INNER; &text[]->s[]; private.decomposepath; private.suf -> s

--DiskEntryPathNameExt: dopart--
do INNER;
   &text[]->e[]; 
   private.decomposepath; 
   private.suf.t[2:private.suf.length] -> e

--DiskEntryPathNameGet: dopart--
do INNER; &text[]->n[]; private.decomposepath; private.name -> n

--DiskEntryPathSet: dopart--
do INNER;
   (if p[]=NONE then &text[]->p[] if);
   p -> private.path;
   private.checkInfo;
   true -> private.pathchanged;

--DiskEntryPathGet: dopart--
do INNER; private.path.copy -> p[]

--DiskEntryExists: dopart--
do (private.path[]->DotnetFile.Exists) or 
   (private.path[]->DotnetDirectory.Exists) 
     -> value

--DiskEntryTouch: dopart--
do (if true
    // private.dinfo[]<>NONE then
       DateTime.get_Now -> private.dinfo.set_LastWriteTime;
    // private.finfo[]<>NONE then
       DateTime.get_Now -> private.finfo.set_LastWriteTime;
    else
       (* Create file *)
       private.path[] -> &FileInfo[] -> private.finfo[];
       private.finfo.Create;
   if);
   
--DiskEntrySize: dopart--
do (if true
    // private.dinfo[]<>NONE then
       0 -> value;
    // private.finfo[]<>NONE then 
       private.finfo.get_Length -> value
    else 
       error;
   if)
   
--DiskEntryGetModTime:dopart--
do (if true
    // private.dinfo[]<>NONE then
       (private.dinfo.get_LastWriteTime->asDateTime).ToFileTime -> BetaHelpers.fileTimeToModtime -> time
    // private.finfo[]<>NONE then
       (private.finfo.get_LastWriteTime->asDateTime).ToFileTime -> BetaHelpers.fileTimeToModtime -> time
    else 
       error;
   if);

--DiskEntrySetModTime:dopart--
do (if true
    // private.dinfo[]<>NONE then
       (time -> BetaHelpers.modtimeToFileTime) -> DateTime.FromFileTime -> private.dinfo.set_LastWriteTime;
    // private.finfo[]<>NONE then
       (time -> BetaHelpers.modtimeToFileTime) -> DateTime.FromFileTime -> private.finfo.set_LastWriteTime;
    else 
       error;
   if);

--DiskEntryRename: dopart--
do (if true
    // private.dinfo[]<>NONE then
       newpath[] -> private.dinfo.MoveTo;
    // private.finfo[]<>NONE then
       newpath[] -> private.finfo.MoveTo;
    else
       error
   if);

--DiskEntryReadable: dopart--
do true->value; (* FIXME: Really always readable? *)

--DiskEntryWriteable: descriptor--
(# 
do not (FileAttributes.ReadOnly->private.hasAttribute) -> value
#)

--DiskEntryIsDir: dopart--
do (FileAttributes.DotnetDirectory->private.hasAttribute) -> value
   
--DiskEntryIsFile: dopart--
do not (FileAttributes.DotnetDirectory->private.hasAttribute) -> value (* FIXME: check *)
   
   
   
   
   
--FilePrivate:descriptor--
(# 
   sr: ^StreamReader;
   sw: ^StreamWriter;
   i: @integer;
   s: ^DotnetStream;
#)

--FileOtherException: dopart--
do 'Error in system call: ' -> msg.puttext;
   (*getErrno -> errorstring -> msg.putline;*)
   INNER

--FileTouch: dopart--
do entry.touch;

--FileLength: dopart--
do entry.size -> value

--FileDoOpen:descriptor--
(# 
do 'file_clrbody: FileDoOpen'  -> NYI;
#)

--FileOpenRead:dopart--
do (if private.sr[]<>NONE then private.sr.Close if);
   entry.private.finfo.OpenText -> private.sr[];

--FileOpenWrite:dopart--
do (if entry.private.finfo[]<>NONE then
       (* File existed - delete it *)
       entry.private.finfo.Delete;
    else
       (* Set up finfo for new file *)
       entry.private.path[] -> &FileInfo[] -> entry.private.finfo[] -> entry.private.info[];
   if);
   entry.private.finfo.CreateText -> private.sw[];
   
--FileOpenReadWrite:dopart--
do 'file_clrbody: FileOpenReadWrite'  -> NYI;
   
--FileOpenAppend:dopart--
do (if entry.private.finfo[]=NONE then
       (* Set up finfo for new file and create it *)
       entry.private.path[] -> &FileInfo[] -> entry.private.finfo[] -> entry.private.info[];
       entry.private.finfo.CreateText -> private.sw[];
       private.sw.Close;
   if);
   entry.private.finfo.AppendText -> private.sw[];
   
--FileOpenReadAppend:dopart--
do 'file_clrbody: FileOpenReadAppend'  -> NYI;
   
--FileDelete:dopart--
do (if entry.private.checkinfo then
       entry.private.info.Delete;
    else
       (*error;*)
   if);

--FilePut:dopart--
do ch -> private.sw.Write_char;

--FileGet:dopart--
do private.sr.read -> ch;

--FilePeek:dopart--
do private.sr.Peek -> ch;
   
--FilePutText:dopart--
do txt[] -> private.sw.Write_String;

--FileGetAtom:dopart--
do private.sr.Read -> private.i;
   skipBlanks:
     (if private.i<> -1 then
           (if private.i->ascii.isWhiteSpace then
               private.sr.Read -> private.i;
               restart skipBlanks;
            else
               scan:
                 (* atom not yet complete *)
                 (if txt.pos+1 > txt.T.range then
                     txt.T.range+16->txt.T.extend;
                     restart scan;
                  else
                     txt.pos+1->txt.pos;
                     private.i ->txt.T[txt.pos]; 
                     txt.lgth+1->txt.lgth;
                     (if private.i = -1 then
                         (* End of file - stop here *)
                      else
                         (if private.sr.Peek -> ascii.isWhiteSpace then
                             (* end of atom - stop *)
                          else
                             private.sr.Read -> private.i;
                             restart scan;
                         if)
                     if)
                 if);
           if);
      else 
         (* EOF while skipping blanks *)
     if)


--FileGetLine:dopart--
do private.sr.ReadLine -> txt[]
   
--FileGetPos:dopart--
do (if private.sr[]<>NONE then
       (private.sr.get_BaseStream->private.s[]).get_Position -> value
    else
       (if private.sw[]<>NONE then
           (private.sw.get_BaseStream->private.s[]).get_Position -> value
       if);
   if)
   
--FileSetPos:dopart--
do (if private.sr[]<>NONE then
       newpos -> (private.sr.get_BaseStream->private.s[]).set_Position;
    else
       (if private.sw[]<>NONE then
           newpos -> (private.sw.get_BaseStream->private.s[]).set_Position;
       if);
   if)
   
--FileEos:dopart--
do private.sr.Peek = -1 -> value;

--FileFlush:dopart--
do private.sw.Flush;

--FileClose:dopart--
do (if private.sr[]<>NONE then
       private.sr.Close;
       NONE -> private.sr[];
   if);
   (if private.sw[]<>NONE then
       private.sw.Close;
       NONE -> private.sw[];
   if);



