ORIGIN 'numberioBody';
MDBODY nti     'numberioLittleEndian'
       linux   'numberioLittleEndian'
       x86sol  'numberioLittleEndian'
       default 'numberioBigEndian';
INCLUDE '../math';
INCLUDE '~beta/sysutils//endian';

--- numberIoPutRealBody: descriptor ---
(# sprintf: External
     (# str: @integer;
        fmt: @integer;
        r: @real;
     enter (str, fmt, r)
     #);
   control: @text;
   controlput: @control.put; controlputint: @control.putint;
   buffer: [400]@char; (* Make room for sprintf to write to *)
   result: @text;
   putchar: @result.put;
do (* Set up format string for sprintf *)
   format; control.clear; result.clear;
   '%' -> control;
   (if width<>0 then
       (if adjustLeft  then '-' -> controlput if);
       (if signed      then '+' -> controlput if);
       (if blankSign   then ' ' -> controlput if);
       (if zeroPadding then '0' -> controlput if);
       width -> controlputint;
   if);
   '.'->controlput;
   precision -> controlputint;
   (if style
    // noexp then
       'f' -> controlput
    // exp then
       (if upcase then  'E' -> controlput
        else 'e' -> controlput
       if)
    // plain then
       (if upcase then  'G' -> controlput
        else 'g' -> controlput
       if)
   if);
   0 -> controlput; (* Null-terminate control-string *)
   (@@buffer[1], @@control.T[1], r) -> sprintf;
   copy:
     (#
     do (for i: buffer.range repeat
             (if buffer[i] = 0 then
                 leave copy (* sprintf NULL-terminates result *)
              else
                 buffer[i]-> putchar
             if)
        for);
        (* NULL termination not reached !! *)
        'PutReal: Error in buffer, sprintf may have destroyed objects!'
          -> screen.putline
     #);
   result[]->puttext
#)


-- NumberioReadnumberToReal: dopart --
do A->value;
   (if (D<>0) and (B<>0) then
       value+(B div D)->value;
   if);
   (if true
    // C<0 then
       value div ((10,-C)->pow) -> value
    // C>0 then
       value * ((10,C)->pow) -> value
   if)
   
--numberIoReal2Ints: dopart--
do (if isBigEndian then
       %getlongAt(@@r)   -> i1; 
       %getlongAt(@@r+4) -> i2; 
    else
       %getlongAt(@@r+4) -> i1;
       %getlongAt(@@r)   -> i2; 
   if);
   
--numberIoInts2Real: dopart--
do (if isBigEndian then
       i1 %putlongAt(@@r); 
       i2 %putlongAt(@@r+4); 
    else
       i1 %putlongAt(@@r+4);
       i2 %putlongAt(@@r); 
   if);
