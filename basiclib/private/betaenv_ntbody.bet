ORIGIN 'betaenvbody';
INCLUDE '~beta/win32lib/ntinterface';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1984-96
 *       All rights reserved.
 *
 *)
-- betaenvbodyMDInitAscii: descriptor -- 
(#
do
   (* Initialize private.NonWhiteSpace. Offsett by one to allow test for
    * isNonWhiteSpace(0)
    *)
   (* From '!'(33) to '~'(126) is NonWhiteSpace *)
   (for ch: (126-33+1) repeat true->private.NonWhiteSpace[ch+33] for);
   (* From <inverted exclamation>(161) to <y umlaut>(255) is NonWhiteSpace 
    * (iso8859, Latin-1)
    *)
   (for ch: (255-161+1) repeat true->private.NonWhiteSpace[ch+161] for);

#)
--betaenvInitEnv: descriptor--
(# 
do ascii.init;
   ascii.nl-> ascii.newline;
   &DefaultScreen[]->screen[]; 
   &DefaultKeyboard[]->keyboard[];
   &rawArgumentHandler[]->argumentHandler[];
   SetupExceptions;
#)
-- betaenvTerminateEnv: descriptor --
(##)
-- betaenvbodyTerminateConsole: descriptor --
(##)
  
--- betaenvNoOfArguments: dopart ---
do Argc->value
   
--- betaenvArguments: dopart ---
do (if ((1<=argNo) and (argNo<=ArgC)) then
       &text[]->theArg[]; argNo->Argv->theArg
   if)

--- betaenvExpandArgumentsprivate: descriptor ---
(# 
   noOfArgs: @Integer;
   ArgStrings: [0]^Text;
   
   init:
     (# T: @Text; Path: ^Text;
        idx: @Integer;
        count: @Integer;
        ch: @Char;
        AddArg:
          (# T: ^Text;
          enter T[]
          do noOfArgs+1 -> noOfArgs;
             (if noOfArgs > ArgStrings.range then
                 noOfArgs -> ArgStrings.extend;
             if);
             T.Copy -> ArgStrings[noOfArgs][];
          #);
     do (for i: Argc repeat
             i -> Argv -> T;
             T -> OpenNTFindFiles -> count;
             (if count < 1 then
                 T[] -> AddArg;
              else
                 0 -> idx;
                 (for i: T.length repeat 
                      i -> T.inxGet -> ch;
                      (if (ch = '\\') or (ch = '/') then
                          i -> idx 
                      if)
                 for);
                 (if idx > 0 then
                     (1, idx) -> T.sub -> Path[]
                 if);                       
                 (for j: count repeat
                      GetNTFindFiles -> T;
                      (if idx > 0 then Path[] -> T.prepend if);
                      T[] -> AddArg
                 for)
             if);
             CloseNTFindFiles
        for)
     #);
   
   getNoOfArgs:
     (# 
     do (if noOfArgs=0 then init if);
     exit noOfArgs
     #);
   
   getArg:
     (# argNo: @Integer; theArg: ^Text;
     enter argNo
     do (if noOfArgs=0 then init if);
        (if ((1<=argNo) and (argNo<=noOfArgs)) then
            ArgStrings[argNo].copy -> theArg[]
        if)
     exit theArg[]
     #);
#)

--- betaenvExpandNoOfArguments: dopart ---
do private.getNoOfArgs->value

--- betaenvExpandArguments: dopart ---
do argNo -> private.getArg -> theArg[];
   
