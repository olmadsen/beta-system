ORIGIN '../betaenv'
[[--INCLUDE '../systemenv'
---program:descriptor--
systemenv
(# Prod: @ | System
     (# 
     do  cycle
	 (# L: [8] @char 
	 do (for i: L.range repeat get->L[i] for); 
	    L->Pipe.Put 
     #)#);
   Pipe: @ | System
     (# In: @port; Put: In.entry(# L: @text enter L do L->inLine #);
	inLine: @ text;
        DisAsm: @ | System
          (# 
          do cycle
             (#
             do In.accept;
  		inLine.scan(#do ch->Squash.put #);
		' '->Squash.put
	  #)#);
        Squash: @ | System
          (# P: @port; put: P.Entry(# c: @Char enter c do c->ch #); ch: @Char
          do cycle
	     (#
	     do P.accept;
		(if ch // '*' then
		    P.accept;
		    (if ch // '*' then '^'->Asm.put
		     else '*'->asm.put; ch->asm.put if)
		 else ch->Asm.put
	  if)#)#);
        Asm: @ | System
          (# P: @Port; put: P.entry(# c: @Char enter c do c->ch #); ch: @Char
          do cycle
	     (#
	     do (for i: OutLine.range repeat
		     P.accept; ch->OutLine[i];
		     (if ch // '.' then 
		 	 (i+1,OutLine.range)->ForTo(#do ' '->OutLine[inx] #);
			 (* stop *)
		     if)for);
		Out.accept
	  #)#);
        Get: Out.Entry(# exit OutLine #); 
	Out: @ Port;
        OutLine: [10] @char
     do conc(#do DisAsm[]->start; Squash[]->start; Asm[]->start #)
     #);
   Cons: @ | System
     (#
     do cycle(#do Pipe.Get->mkTextRef->putLine #)
     #);
do (*true->trace;*) 
   conc(#do Prod[]->start; Pipe[]->start; Cons[]->start #)
#)
---]]

