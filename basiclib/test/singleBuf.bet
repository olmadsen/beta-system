ORIGIN '../betaenv';
INCLUDE '../systemenv'
---program:descriptor--
systemenv
(# Producer: System(# do INNER #);
   Consumer: System(# do INNER #);
   
   SingleBuf: @ | System
     (# bufCh: @ Char;
	P1: @ QualifiedPort;
        Put: P1.entry(# ch: @ char enter ch do ch->bufCh #); 
	P2: @ QualifiedPort;
        Get: P2.entry(# ch: @ char do bufCh->ch exit ch #);
     do cycle(#do Producer##->P1.accept; Consumer##->P2.accept#)
     #);
   Prod: @ | Producer
     (#
     do L: Cycle
          (# ch: @ char
          do get->ch;
             (if ch // '!' then '@'->SingleBuf.Put
              else ch->SingleBuf.Put
             if);
             (if ch // '+' then leave L if)
     #)#);
   Cons: @ | Consumer
     (#
     do L: Cycle
          (# ch: @ Char
          do SingleBuf.get->ch;
             (if ch // '#' then '$'->put else ch->put if);
             (if ch // '+' then leave L if)
     #)#);
   
do conc(#do Prod[]->start; SingleBuf[]->start; Cons[]->start #)
#)
