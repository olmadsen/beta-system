ORIGIN '../betaenv';
INCLUDE '../systemenv'
---program:descriptor---
SystemEnv
(# buffer: @ Monitor
     (# R: [20] @ char; in,out: @integer;
        full,empty: @  Condition;

        Put: Entry
          (# ch: @ char
          enter ch
          do (if in // out then full.wait if);
             ch->R[in]; (in mod R.range)+1 ->in;
             empty.signal; 
          #);
        get: Entry
          (# ch: @char
          do (if in // (out mod R.range)+1 then empty.wait if);
             R[(out mod R.range)+1->out]->ch; 
             full.signal; 
          exit ch
          #);
        init::< (# do 1->in; R.range->out #)
      #);

   prod: @ | System(# do cycle(#do keyboard.get->buffer.put #) #);
   cons: @ | System(# do cycle(#do buffer.get->screen.put #) #);

do buffer.init;
   conc(#do prod[]->start; cons[]->start #)
#)
