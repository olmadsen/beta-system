ORIGIN '../betaenv'
--- program: descriptor ---
(# (* example of using the BETA text-concept *)
   (* The text-concept is preliminary        *)
   T1,T2: @text; T3: ^text;
   sep: (# do '!' -> put; newline #);
do 'foo' -> T1;
   T1[] -> putText; sep;
   ('.bet') -> (T1.copy).append -> putText; sep;
   ('.lst') -> (T1.copy).append -> putText; sep;
   T2.clear;
   T1[] -> T2.append;
   '.' -> T2.put; 
   'bet' -> T2.append;
   T2[] -> putText; sep;
   ('BQF',4) -> T2.insert;
   T2[] -> putText; sep;
   ('PIP',1) -> T2.insert;
   T2[] -> putText; sep;
   ('*',13) -> T2.insert;
   T2[] -> putText; sep;
   ('Mjolner',16) -> T2.insert;
   ('Snavs',0) -> T2.insert;
   T2[] -> putText; sep;
   (10,15) -> T2.delete;
   T2[] -> putText; sep;
   T2.makeUC; T2[] -> putText; sep;
   T2.makeLC; T2[] -> putText; sep;
   (# mark: [T2.length] @boolean
   do (for i: mark.range repeat false -> mark[i] for);
      'o' -> T2.findAll
      (# do inx-1 -> T2.SetPos; inx -> T2.putInt; true -> mark[inx]
      #); 
      's' -> T2.findAll
      (# do inx-1 -> T2.SetPos; inx -> T2.putInt; true -> mark[inx]
      #);
      'r' -> T2.findAll
      (# do inx-1 -> T2.SetPos; inx -> T2.putInt; true -> mark[inx]
      #);
      'n' -> T2.findAll
      (# do inx-1 -> T2.SetPos; inx -> T2.putInt; true -> mark[inx]
      #);
      0 -> T2.SetPos;
      loop: 
        (if T2.eos // false then
            T2.get -> put; restart loop
        if);
      sep;
      (for i: mark.range repeat 
           (if mark[i]
            // true then '^' -> put
            // false then ' ' -> put
      if)for);
      sep
   #);
   T1.clear; T1 -> T2; T2[] -> putText; sep;
   'Type a line: ' -> puttext; keyboard.getLine -> T3[];
   'Echo: ' -> puttext; T3[] -> puttext; newline;
   'Type another line: ' -> puttext;
   keyboard.getAtom -> T3[];
   'First atom in line: '-> puttext; T3[] -> puttext; newline
#)


