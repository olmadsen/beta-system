ORIGIN '../../betaenv'
--- lib: attributes ---
tstenv:
  (# tests:< (# value: @text do inner exit value[] #);
     testsequence: @text;
     t: @text;
     put: @t.put;
     putint: @t.putint;
     puttext: @t.puttext;
     putline: @t.putline;
     newline: @t.newline;
     tst:
       (# result: ^text; ch: @char;
       enter (ch,result[])
       do t.clear;
          INNER; 
          (if t[]->result.equalNCS//true
              then ch->screen.put; ch->testsequence.put
           else
              '*'->testsequence.put;
              screen.newline;
              '\tERROR in test: "'->screen.puttext; ch->screen.put; '"'->screen.putline;
              '\tExpected: "'->screen.puttext; result[]->screen.puttext; '"'->screen.putline;
              '\tReturned: "'->screen.puttext; t[]->screen.puttext; '"'->screen.putline;
          if)
       #);
   tstBool:
     (# result, value: @boolean; ch: @char;
     enter (ch,result)
     do false->value;
        INNER;
        (if result=value
            then ch->screen.put; ch->testsequence.put
         else
            '*'->testsequence.put;
            screen.newline;
            '\tERROR in test: "'->screen.puttext; ch->screen.put; '"'->screen.putline;
            '\tExpected: "'->screen.puttext; (if result then 'true'->screen.puttext else 'false'->screen.puttext if); '"'->screen.putline;
            '\tReturned: "'->screen.puttext; (if value then 'true'->screen.puttext else 'false'->screen.puttext if); '"'->screen.putline;
        if)
     #);
   tstInt:
     (# result, value: @integer; ch: @char;
     enter (ch,result)
     do 0->value;
        INNER;
        (if result=value
            then ch->screen.put; ch->testsequence.put
         else
            '*'->testsequence.put;
            screen.newline;
            '\tERROR in test: "'->screen.puttext; ch->screen.put; '"'->screen.putline;
            '\tExpected: "'->screen.puttext; result->screen.putint; '"'->screen.putline;
            '\tReturned: "'->screen.puttext; value->screen.putint; '"'->screen.putline;
        if)
     #);
   tstChar:
     (# result, value: @char; ch: @char;
     enter (ch,result)
     do 0->value;
        INNER;
        (if result=value
            then ch->screen.put; ch->testsequence.put
         else
            '*'->testsequence.put;
            screen.newline;
            '\tERROR in test: "'->screen.puttext; ch->screen.put; '"'->screen.putline;
            '\tExpected: "'->screen.puttext; result->screen.put; '"'->screen.putline;
            '\tReturned: "'->screen.puttext; value->screen.put; '"'->screen.putline;
        if)
     #);
  do inner;
     (if tests->testsequence.equal
      //true then ' <<<<<<< Test OK'->screen.putline
      else ' <<<<<<< ERRORS in test !!!'->screen.putline;
         tests->screen.puttext; ' Expected'->screen.putline;
         testsequence[]->screen.puttext; ' found'->screen.putline
     if)
  #)
         
