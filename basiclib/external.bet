ORIGIN 'betaenv';
-- CStructLib: attributes---
(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-96
 *       All rights reserved.
 *
 ****** Patterns for external interface *****
 * 
 * In CStructLib, the operations on a cStruct are defined.
 * The pattern ExternalRecord is an interface to e.g. CStruct objects
 * allocated from C or other external languages.
 *)
(* idx=1 *)
GetByte: 
  (# byteoffset: @integer 
  enter byteoffset 
  do byteoffset->ChkBounds 
  exit (R,byteoffset)->TOS'%inxGetByte'
  #);
PutByte: 
  (# val,byteoffset: @integer
  enter(byteoffset,val) 
  do byteoffset->ChkBounds; (R,byteoffset,val) ->TOS'%inxPutByte'
  #);
GetShort: 
  (# byteoffset: @integer
  enter byteoffset do byteoffset->ChkBounds exit (R,byteoffset)->TOS'%inxGetShort'
  #);
PutShort: 
  (# val,byteoffset: @integer
  enter(byteoffset,val) 
  do byteoffset->ChkBounds; (R,byteoffset,val) ->TOS'%inxPutShort'
  #);
GetSignedShort: 
  (# byteoffset: @integer
  enter byteoffset 
  exit @@R[1]+byteoffset->TOS'%adrGetSignedShort[0]'
  #);
GetLong: 
  (# byteoffset: @integer
  enter byteoffset
  do byteoffset->ChkBounds
  exit (R,byteoffset)->TOS'%inxGetLong'
  #);
PutLong: 
  (# val,byteoffset: @integer
  enter(byteoffset,val)
  do byteoffset->ChkBounds;
     (R,byteoffset,val) ->TOS'%inxPutLong'
  #);
Byte: (* Used for declaring CStruct fields *)
  (# p: @pos; pos:< IntegerObject; val: @integer;
     set: @(# enter val do (R,p,val)->TOS'%inxPutByte' #);
  enter set
  exit (R,p)->TOS'%inxGetByte'
  #);
Short: (* Used for declaring CStruct fields *)
  (# p: @pos; pos:< IntegerObject; val: @integer;
     set: @(# enter val do (R,p,val)->TOS'%inxPutShort' #);
  enter set
  exit (R,p)->TOS'%inxGetShort'
  #);
SignedShort: 
  (# p: @pos; pos:< IntegerObject; val: @integer;
     set: @(# enter val do (R,p,val)->TOS'%inxPutShort' #);
  enter set
  exit @@R[1]+p->TOS'%adrGetSignedShort[0]'
  #);
Long: (* Used for declaring CStruct fields *)
  (# p: @pos; pos:< IntegerObject; val: @integer;
     set: @(# enter val do (R,p,val)->TOS'%inxPutLong' #);
  enter set
  exit (R,p)->TOS'%inxGetLong'
  #);

(* idx=2 *)

--LIB: attributes--
ExternalRecord: 
  (* Super-pattern for describing externally allocated record-structures.
   * A call to e.g. a C routine may often return a pointer to a CStruct.
   * By assigning such a pointer to the ptr-field of an externalRecord object
   * it is possible to interface to such a CStruct.
   *)
  (# ptr: @integer; (* pointer to the external record *)
     GetByte: 
       (# byteoffset: @integer
       enter byteoffset
       exit ptr + byteoffset -> TOS'%adrGetByte'
       #);
     PutByte: 
       (# val,byteoffset: @integer
       enter(byteoffset,val)
       do (ptr+byteoffset,val) -> TOS'%putByte[0]'
       #);
     GetShort: 
       (# byteoffset: @integer
       enter byteoffset
       exit ptr + byteoffset -> TOS'%adrGetShort'
       #);
     GetSignedShort: 
       (# byteoffset: @integer
       enter byteoffset
       exit ptr + byteoffset -> TOS'%adrGetSignedShort'
       #);
     PutShort: 
       (# val,byteoffset: @integer
       enter(byteoffset,val)
       do (ptr+byteoffset,val) -> TOS'%putShort[0]'
       #);
     GetLong: 
       (# byteoffset: @integer
       enter byteoffset
       exit ptr + byteoffset -> TOS'%adrGetLong'
       #);
     PutLong: 
       (# val,byteoffset: @integer
       enter(byteoffset,val)
       do (ptr+byteoffset,val) -> TOS'%putLong';
       #);
     Byte: (* For declaring field *)
       (# pos:< IntegerValue; val: @integer; p: @pos;
          set: @(# enter val do (ptr+p,val) -> TOS'%putByte[0]' #)
       enter set
       exit ptr+p -> TOS'%adrGetByte'
       #);
     Short: (* For declaring field *)
       (# pos:< IntegerValue; val: @integer; p: @pos;
          set: @(# enter val do (ptr+p,val) -> TOS'%putShort[0]' #);
       enter set
       exit ptr+p -> TOS'%adrGetShort'
       #);
     SignedShort: (* For declaring field *)
       (# pos:< IntegerValue; val: @integer; p: @pos;
          set: @(# enter val do (ptr+p,val) -> TOS'%putShort[0]' #);
       enter set
       exit ptr+p -> TOS'%adrGetSignedShort'
       #);
     Long: (* For declaring field *)
       (# pos:< IntegerValue; val: @integer; p: @pos;
          set: @(# enter val do (ptr+p,val) -> TOS'%putLong' #);
       enter set
       exit ptr+p-> TOS'%adrGetLong'
       #);
     DoubleLong: (* For declaring field *)
       (# pos:< IntegerValue; v1,v2: @integer; p: @pos;
          set: @(# enter(v1,v2) do (ptr+p,v1) -> TOS'%putLong'; (ptr+4+p,v2) -> TOS'%putLong' #);
       enter set
       exit (ptr+p-> TOS'%adrGetLong',ptr+4+p-> TOS'%adrGetLong')
       #);
  #) (* ExternalRecord *);

makeCBF: External
  (* Call this external to install a callback and get
   * an integer pointer to it.
   *)
  (# pat: ##External;
     cb: @integer;
  enter pat##
  exit cb
  #);

freeCBF: External
  (* Call this external with an integer pointer to an installed
   * callback (obtained via MakeCBF) when it is certain that the
   * callback will NOT be called again.
   * This will free BETA heap space associated with the callback.
   *)
  (# cbf: @integer;
  enter cbf
  #);
