ORIGIN 'betaenv';
BODY 'private/streampositionbody';

--StreamLib: attributes--

positionConverter:
  (* Convert between character-positions and linenumber/column in 
   * a bounded stream, e.g. a Text or a File.
   * Positions are numbered from 1.
   * Lines and colums are numbered from 1.
   *)
  (# cache: @<<SLOT StreamPositionConverterCache: descriptor>>;
     update:
       (* Rebuild cache of newlines. Automatically called when any of 
        * the other operations are used for the first time.
        * Call this whenever the contents of THIS(Stream) later changes.
        * Notice: This operation reads entire stream, which MUST 
        * contain EOS for this operation to complete. That is, it is most
        * useful for specializations like Text and File.
        * Notice also, that if used on a File, the file must be open for
        * reading when using this operation.
        * Complexity: O(numlines).
        *)
       (# 
       <<SLOT StreamPositionConverterUpdate: dopart>>
       #);
     posToLineCol:
       (* Converts position to line-number and column (offset in this 
        * line). If no position is specified, THIS(Stream).Position 
        * is used instead of returning the trivial (line,col) = (1,1).
        * If an illegal positon is given either (1,1) or the last 
        * line-number and column in this line is returned, for negative 
        * and too large positions, respectively.
        * Complexity: O(log2(numlines)).
        *)
       (# pos, line, col: @integer
       enter pos
       <<SLOT StreamPositionConverterPosToLineCol: dopart>>
       exit (line,col)
       #);
     lineColToPos:
       (* Converts (line, column) to character-position.
        * If no (line,col) is given, returns THIS(Stream).Position 
        * instead of the trivial position = 1.
        * If illegal (line,col) is specified (i.e. if col characters 
        * from the beginning of line number line is outside the range of
        * THIS(Stream), the closest position is returned.
        * Complexity: O(1).
        *)
       (# pos, line, col: @integer
       enter (line,col)
       <<SLOT StreamPositionConverterLineColToPos: dopart>>
       exit  pos
       #);
     numLines: integerValue
       (* Returns the total number of lines in THIS(Stream)
        * Complexity: O(1).
        *)
       (#
       <<SLOT StreamPositionConverterNumLines: dopart>>
       #);
  #)
       
