ORIGIN '~beta/basiclib/v1.5/regexp';
--- program: descriptor ---
(#
do loop:
     (# regex: ^text;
     do 'Search for: ' -> puttext; getLine -> regex[];
        (if regex.length = 0 then leave loop if);
        loop:
          (# string: ^text;
          do 'Search for: '->puttext; regex[]->puttext;
             ' in: ' -> puttext; getline -> string[];
             (if string.length = 0 then leave loop if);
             0 -> string.pos;
             regex[] -> string.regexp_search
             (# noMatch::< (# do '\tNo match' -> putline #)
             do (for i: regexp_numberOfRegisters repeat
                     (if (i-1->regs.start)>=0 then
                         '\tregister '->puttext; i-1 -> putint; ': '->puttext;
                         i-1->regs.start->putint;
                         ' to '->puttext;
                         i-1->regs.end->putint;
                         ': '->puttext;
                         ((i-1->regs.start)+1, i-1->regs.end)->string.sub->putline
                     if)
                for);
             #);
             restart loop
          #);
        restart loop
     #)
#)
