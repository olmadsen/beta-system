ORIGIN '~beta/basiclib/v1.5/regexp';
--- program: descriptor ---
(#
do loop:
     (# regex: ^text;
     do 'Replace: ' -> puttext; getLine -> regex[];
        (if regex.length = 0 then leave loop if);
        loop:
          (# string: ^text;
          do 'Replace: '->puttext; regex[]->puttext;
             ' in: ' -> puttext; getline -> string[];
             (if string.length = 0 then leave loop if);
             loop:
               (# t, replacement: ^text
               do string.copy -> t[]; 0 -> t.pos;
                  'Replace: '->puttext; regex[]->puttext;
                  ' in: ' -> puttext; string[]->puttext;
                  ' with: '->puttext; getline -> replacement[];
                  (if replacement.length = 0 then leave loop if);
                  loop:
                    (if t.pos<t.length then
                        (regex[], replacement[]) -> t.regexp_replace
                        (# noMatch::< (# do '\tNo match'->screen.putline; leave loop #);
                        posToMatchEnd::< (# do true -> value #);   
                        #);
                        'Replace: '->puttext; regex[]->puttext;
                        ' in: ' -> puttext; string[]->puttext;
                        ' with: '->puttext; replacement[]->puttext;
                        ' gives: '->puttext; t[] -> putline;
                        restart loop
                    if);
                  restart loop
               #);
             restart loop
          #);
        restart loop
     #)
#)
