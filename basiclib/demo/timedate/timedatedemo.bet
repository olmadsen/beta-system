ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/basiclib/v1.6/timedate';

--- program:descriptor ---
(# t, t1, t2: @time;
   p, p1, p2: @period;
do 'Time min: \t'->puttext; timeMin->putTime;newline;
   'Time now: \t'->puttext; timeNow->putTime;newline;
   'Time max: \t'->puttext; timeMax->putTime;newline;
   
   timeNow->t;
   
   (* read individual attributes... *)
   'timeNow:'->putline;
   '\tyear:'->putText;    t.year->putInt;    newLine;
   '\tmonth:'->putText;   t.month->putInt;   newLine;
   '\tday:'->putText;     t.day->putInt;     newLine;
   '\thour:'->putText;    t.hour->putInt;    newLine;
   '\tminute:'->putText;  t.minute->putInt;  newLine;
   '\tsec:'->putText;     t.sec->putInt;     newLine;
   '\tweekday:'->putText; t.weekday->putInt; newline;
   
   'Time now:\t\t\t'->puttext; timeNow->t2->putTime;newline;
   
   'timeNow + two days and 7 hours:\t'->puttext;
   t2->t; (2,7,0,0)->t.add; t->putTime;newline;
   'Time difference:\t\t'->puttext; (t2,t)->timeDifference->putPeriod;newline;
   
   'timeNow - two days and 7 hours:\t'->puttext;
   t2->t; (2,7,0,0)->t.sub; t->putTime;newline;
   'Time difference:\t\t'->puttext; (t2,t)->timeDifference->putPeriod;newline;
   
   '\nTime:\t\t\t\t'->puttext; (1996,12,02,23,16,13)->t2->putTime;newline;
   
   'Time + two days and 7 hours:\t'->puttext;
   t2->t;t.day+2->t.day;t.hour+7->t.hour; t->putTime;newline;
   'Time difference:\t\t'->puttext; (t2,t)->timeDifference->putPeriod;newline;
   
   'Time - two days and 7 hours:\t'->puttext;
   t2->t;t.day-2->t.day; t.hour-7->t.hour; t->putTime;newline;
   'Time difference:\t\t'->puttext; (t2,t)->timeDifference->putPeriod;newline;

   
   (* test value assignment *)
   '\nKrab\'s birthday:\t'->putText;
   (1970,1,10,8,45,0)->t-> putTime;	
   newLine;
   
   (* test normalization and excessive values... *)
   t.month + 12 ->t.month;
   'One year later:\t\t'->putText; t->putTime; newLine;
   
   (1970,1,10,8,45,0)->t;

   t->t2;
   'Three years before:\t'->putText; 
   (t2.year-3)->t2.year;
   t2->putTime;  newline;
   
   t->t2;
   'Three month before:\t'->putText; 
   (t2.month-3)->t2.month; 
   t2->putTime;  newline;
   
   t->t2;
   'Three month after:\t'->putText; 
   (t2.month+3)->t2.month; 
   t2->putTime;  newline;
   
   t->t2;
   'Twelve month before:\t'->putText; 
   (t2.month-12)->t2.month; 
   t2->putTime;  newline;
   
   t->t2;
   '23 month before:\t'->putText; 
   (t2.month-23)->t2.month; 
   t2->putTime;  newline;
   
   t->t2;
   '24 month before:\t'->putText; 
   (t2.month-24)->t2.month; 
   t2->putTime;  newline;
   
   t->t2;
   '25 month before:\t'->putText; 
   (t2.month-25)->t2.month; 
   t2->putTime;  newline;
   
   (* test getTime, getDate, getClock, and getPeriod *)
   (# at:@text; p: @period
   do (1970,1,10,8,45,0)->t;
      t->at.putTime; at.reset;
      'Krab\'s birthday again:\t'->putText;
      at.getTime->putTime; newLine;
      '\tDate: '->puttext; t->putDate; newline;
      '\tClock: '->puttext; t->putClock; newline;
      at.clear; t->at.putDate; at.reset;
      '\tDate: '->puttext; at.getDate->putDate;
      '#'->put; at.reset; at.getDate->putTime; newline;
      at.clear; t->at.putClock; at.reset;
      '\tClock: '->puttext; at.getClock->putClock;
      '#'->put; at.reset; at.getClock->putTime; newline;
      (2,7,45,30)->p;
      'Period: '->puttext; p->putPeriod; newline;
      'Period: '->puttext; at.clear; p->at.putPeriod; at.reset; at.getPeriod->putPeriod; newline;
   #);
   
   (# D1,D2: @time;
   do (1996,12,2,8,0,0)->D1;
      'D1: '->puttext;D1->putTime;putline;
      (1997,1,1,8,0,0)->D2;
      'D2: '->puttext;D2->putTime;putline;
      (if D2->D1.before then
          '\tD1 before D2'->putline
       else
          '\tD1 not before D2'->putline
      if);
      (if D1->D2.before then
          '\tD2 before D1'->putline
       else
          '\tD2 not before D1'->putline
      if);
   #);
   
   (# D1,D2: @time;
   do (1996,12,2,8,0,0)->D1;
      'D1: '->puttext;D1->putTime;putline;
      timeMax->D2;
      'D2: '->puttext;D2->putTime;putline;
      (if timeMax->D1.before then
          '\tD1 before timeMax'->putline
       else
          '\tD1 not before timeMax'->putline
      if);
      (if D1->D2.before then
          '\tD2 before D1'->putline
       else
          '\tD2 not before D1'->putline
      if);
   #);
   
   (1996,1,50,0,0,0)->t; t->puttime; newline;
   (1996,50,1,0,0,0)->t; t->puttime; newline;
   
#)
   
