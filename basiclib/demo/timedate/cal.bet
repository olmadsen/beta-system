ORIGIN '~beta/basiclib/v1.6/timedate';
INCLUDE '~beta/basiclib/v1.6/textUtils';

--- program: descriptor ---
(# year, month, w: @integer; 
   t: @time;
do (if noOfArguments>1 then
       (2->arguments).asint->month;
       (if noOfArguments>2 then
           (3->arguments).asint->year
        else timeNow->t; t.year->year;
       if);
    else timeNow->t; t.year->year; t.month->month
   if);
   
   (if (month<1) or (month>12) then
       'usage: cal [ [month] year ]'->putline;
    else
       (year, month, 1, 0, 0, 0)->t;
       t.weekday-1->w;
       
       ' S  M Ti  O To  F  L'->putline;
       (for i: w repeat '   '->puttext for);
       l: (if t.month<>month then leave l
           else
              t.day->putint(# format:: (# do 2->width #) #);' '->put;
              t.day+1->t.day;
              (if ((w+1->w) mod 7) = 0 then newline if);
              restart l
          if);
       newline
   if)
   
#)
