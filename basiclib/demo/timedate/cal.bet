ORIGIN '~beta/basiclib/timedate';
INCLUDE '~beta/basiclib/formatio';
INCLUDE '~beta/basiclib/textUtils';

--- program: descriptor ---
(# m, w: @integer; 
   t: @time;
do timeNow->t;
   (if noOfArguments
    // 1 then (* OK - use timeNow *)
    // 2 then
       (2->arguments).asint->t.month;
    // 3 then
       (2->arguments).asint->t.month;
       (3->arguments).asint->t.year
    else
       'usage: cal [ [month] year ]'->putline; stop;
   if);
   
   (t.year, t.month, 1, 0, 0, 0)->t; t.month->m;
   
   t.weekday->w;
   
   '%d/%d\n\n' -> putFormat(# do t.month -> d; t.year -> d; #);
   '      M   T   W   T   F   S   S'->putline;
   '[%02i]'->putformat(# do t.weekOfYear->i #);
   (for i: w-1 repeat '    '->puttext for);
   l: (if t.month=m then
          t.day->screen.putint(# format:: (# do 3->width #) #);' '->put;
          t.day+1->t.day;
          ((w+1) mod 7)->w;
          (if w=1 then
              (if t.month=m then
                  newline;
                  '[%02i]'->putformat(# do t.weekOfYear->i #);
              if);
          if);
          restart l
       else newline;
      if);
#)

