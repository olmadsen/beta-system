ORIGIN '~beta/basiclib/v1.5/systemenv';

--- program:descriptor ---
systemEnv
(# 
   (* everyNthSecond calls INNER every Nth second. *)
   
   everyNthSecond: System
     (# N:< IntegerValue;
        now: @Integer;
        continue: @Boolean; 
     do 
        run: cycle 
          (# 
          do 
             INNER everyNthSecond;
             (if continue then
                 N -> sleep;
                 now+N -> now;
              else
                 leave run
             if)
          #);
     #);
   
   every: @| everyNthSecond
     (# N::< (# do 1 -> value #);
     do 'every: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   fourth: @| everyNthSecond
     (# N::< (# do 4 -> value #);
     do 'fourth: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   eigth: @| everyNthSecond
     (# N::< (# do 8 -> value #);
     do 'eigth: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   
do
   conc (# do every[] -> start; fourth[] -> start; eigth[] -> start; #);
   'conc done' -> screen.putLine;
#)
