ORIGIN '~beta/basiclib/v1.6/systemenv';

--- program:descriptor ---
systemEnv
(# 
   (* everyNthSecond calls INNER every Nth second. *)
   
   everyNthSecond: System
     (# N:< RealValue;
        now: @Real;
        continue: @Boolean; 
     do 
        run: cycle 
          (# 
          do 
             INNER everyNthSecond;
             (if continue then
                 N -> sleep;
                 now+N -> now;
              else
                 leave run
             if)
          #);
     #);
   
   speedy: @| everyNthSecond
     (# N::< (# do 0.25 -> value #);
     do 'speedy: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   every: @| everyNthSecond
     (# N::< (# do 1 -> value #);
     do 'every: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   fourth: @| everyNthSecond
     (# N::< (# do 4 -> value #);
     do 'fourth: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   eigth: @| everyNthSecond
     (# N::< (# do 8 -> value #);
     do 'eigth: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   
do
   conc (# do speedy[] -> start; every[] -> start; fourth[] -> start; eigth[] -> start; #);
   'conc done' -> screen.putLine;
#)
