ORIGIN '~beta/basiclib/v1.5/systemenv';
INCLUDE '~beta/basiclib/v1.5/timehandler'

--- program:descriptor ---
systemEnv
(# 
   th: @timeHandler;
   
   (* everyNthSecond calls INNER every Nth second. *)
   
   everyNthSecond: System
     (# N:< IntegerValue;
        now: @Integer;
        continue: @Boolean; 
     do 
        run: cycle 
          (# sem: @semaphore;
             dorestart: @(# do sem.V #);
          do 
             INNER everyNthSecond;
             (if continue then
                 (dorestart[], N) -> th.register;
                 (* Sleep until we are restarted by dorestart: *)
                 sem.P;
                 now+N -> now;
              else
                 leave run
             if)
          #);
     #);
   
   every: @| everyNthSecond
     (# N::< (# do 1 -> value #);
     do 'every: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   fourth: @| everyNthSecond
     (# N::< (# do 4 -> value #);
     do 'fourth: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   eigth: @| everyNthSecond
     (# N::< (# do 8 -> value #);
     do 'eigth: ' -> putText; now -> screen.putInt; newline;
        (now<16) -> continue;
     #);
   
   neverCalled: @(# do 'neverCalled called' -> screen.putLine; #);
   ncid: @Integer;
do
   th.init; 
   conc (# do every[] -> start; fourth[] -> start; eigth[] -> start; #);
   'conc done' -> screen.putLine;
   
   'Registering neverCalled' -> screen.putLine;
   (neverCalled[],10) -> th.register -> ncid;
   3 -> sleep;
   'Unregistering neverCalled' -> screen.putLine;
   ncid -> th.unregister;
   'Done' -> screen.putLine;
#)
