ORIGIN '~beta/basiclib/v1.5/systemenv';

---program: descriptor--
systemenv
(# Slave: System
     (# receive: @port;
	Clear: receive.entry(# do 0->sum #);
	Add: receive.entry(# V: @integer enter V do sum+V->sum #);
	Result: receive.entry(# S: @integer do sum->S exit S #);
	sum: @Integer;
     do 0->sum;
        Cycle(# do receive.accept #)
     #);
   
   Slave1: @| Slave;
   Slave2: @| Slave;
   
   Master: @| System
     (# pos,neg: @Integer; V: [10] @Integer
     do L: cycle
	  (#
	  do 'Type 10 numbers or 0 to terminate: '->putText;
	     (if (getInt->V[1]) = 0 then leave L if);
	     (for i: V.range-1 repeat getInt->V[i+1] for);
       	     Slave1.Clear; Slave2.Clear;
	     (for inx: V.range repeat 
                  (if True 
                   // V[inx]>0 then V[inx]->Slave1.Add
                   // V[inx]<0 then V[inx]->Slave2.Add
             if)for);
             Slave1.Result->pos->putInt; ' ' ->put;
             Slave2.Result->neg->putInt; newline
	  #);
        Slave1[] -> kill;
        Slave2[] -> kill;
     #);
   
do conc(# do Master[]->start; Slave1[]->start; Slave2[]->start #)
#)

