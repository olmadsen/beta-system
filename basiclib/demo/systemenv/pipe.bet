ORIGIN '~beta/basiclib/v1.6/systemenv';

---program: descriptor--
systemenv
(# Prod: @| System
     (# 
     do cycle
        (# L: @text; ch: @Char;
        do L.clear; 
           (for 8 repeat 
                get -> L.put;
           for); 
           L->Pipe.Put; 
     #)#);
   
   Pipe: @| System
     (# Put: In.entry(# L: @text enter L do L->inLine #); In: @port;
	inLine: @text;
        DisAsm: @| System
          (# 
          do cycle
             (#
             do In.accept;
  		inLine.scanAll(# do ch->Squash.put #);
		' '->Squash.put;
	  #)#);
        Squash: @| System
          (# put: P.Entry(# c: @Char enter c do c->ch #); P: @port;
             ch: @Char
          do cycle
	     (#
	     do P.accept; 
		(if ch = '*' then
		    P.accept;
		    (if ch =  '*' then '^'->Asm.put
		     else '*'->asm.put; ch->asm.put if)
		 else ch->Asm.put
	  if)#)#);
        Asm: @| System
          (# put: P.entry(# c: @Char enter c do c->ch #); P: @Port;
             ch: @Char
          do cycle
	     (#
	     do OutLine.clear; 
                loop:
                  (for i: 10 repeat
                       P.accept; ch->OutLine.put; 
                       (if ch = '.' then 
                           (i+1,10)->ForTo(# do ' '->OutLine.put #);
                           leave loop;
                  if)for);
		Out.accept;
	  #)#);
        Get: Out.Entry(# L: @text do OutLine->L exit L #); Out: @Port;
        OutLine: @text
     do conc(# do DisAsm[]->start; Squash[]->start; Asm[]->start #)
     #);
   Cons: @| System
     (#
     do cycle(# t: @Text; do Pipe.Get->t; t[] ->putLine #)
     #);
   
do conc(# do Prod[]->start; Pipe[]->start; Cons[]->start #)
#)

