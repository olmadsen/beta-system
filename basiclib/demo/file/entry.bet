ORIGIN '~beta/basiclib/v1.6/file';
INCLUDE '~beta/sysutils/v1.6/time';

--- program: descriptor ---
(* A simple example of using DiskEntry: First a DiskEntry corresponding to the
 * path specified on the commnad line is investigated.
 * Then, if the DiskEntry with path '/tmp/test' exists, the user is asked if an
 * attempt to rename it to './testentry' should be performed.
 *)

(# e: @diskentry;
do (if noOfArguments <> 2 then
       'Usage: ' -> puttext; 1->arguments->puttext; ' path' -> putline;
       stop;
   if);
   2 -> arguments -> e.path;
   e.path -> puttext;
   (if e.exists then 
       ' exists and was last modified ' -> puttext;
       e.modtime -> formattime -> putline;
       'It has the size ' -> puttext; e.size -> putint; newline;
       (if e.isdirectory then
           'It is a directory' -> putline;
        else
           'It is not a directory' -> putline;
       if);
       (if e.isfile then
           'It is a file' -> putline;
        else
           'It is not a file' -> putline;
       if);
       (if e.readable then 'It can be read.' -> putline;
        else 'It can''t be read.' -> putline;
       if);
       (if e.writeable then 'It can be written to.' -> putline;
        else 'It can''t be written to.' -> putline;
       if);
       'Now it will be touched.' -> putline;
       e.touch;
       'It''s new modification time is: ' -> puttext;
       e.modtime -> formattime -> putline;
    else
       ' does not exist.' -> putline;
   if);
   '/tmp/test' -> e.path;
   (if e.exists then
       newline;
       'The disk entry ''/tmp/test'' exists.' -> putline;
       'Do you want to rename it to ''./testentry''? (y/n) ' -> puttext;
       (if keyboard.get = 'y' then
           'Ok, then it is renamed to ''./testentry''.' -> putline;
           './testentry' -> e.rename;
           'Checking:' -> putline;
           (if e.exists then
               e.path -> puttext; 
               ' exists, and was last modified '-> puttext;
               e.modtime -> formattime -> putline;
            else
               'Error: ' -> puttext; e.path -> puttext; ' does not exist!' 
               -> putline;
           if);
        else
           'Ok, then it will not be renamed.' -> putline;
       if);
   if)
#)
