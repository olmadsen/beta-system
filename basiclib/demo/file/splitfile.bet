ORIGIN '~beta/basiclib/v1.5/file';
--- program: descriptor ---

(* A simple example of using File: The file 'input' is read word by word,
 * and each word is printed on the output file specified on the command line.
 * After this, the output file is opened again, and a line of text is appended 
 * to it.
 *)

(# inFile, outFile: @ file;
do (if noOfArguments <> 2 then
       'Usage: ' -> puttext; 1->arguments->puttext; ' out-file' -> putline;
       stop;
   if);
   2 -> arguments -> outFile.name;
   outFile.OpenWrite;
   
   'input' -> inFile.name;
   inFile.OpenRead;
   
   readFile: 
     (# 
     do (if inFile.eos then
            leave readFile
         else 
            inFile.getAtom -> outFile.putText;
            outFile.newLine;
            restart readFile
        if)
     #);
   inFile.close;
   outFile.close;
   
   (* Now we'll append a little too *)
   outfile.OpenAppend;
   'This is one line appended' -> outfile.puttext;
   outfile.newline;
#)
