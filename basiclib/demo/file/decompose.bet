ORIGIN '~beta/basiclib/v1.6/file';

--- program: descriptor ---
(* An example showing the use of the 'path' attribute of DiskEntry: The path
 * specified on the command line is decomposed, and the various parts of it are
 * printed.
 *)

(# e: @diskentry;
do (if noOfArguments <> 2 then
       'Usage: ' -> puttext; 1->arguments->puttext; ' path' -> putline;
       stop;
   if);
   2 -> arguments -> e.path;
   'The path '''->puttext; 
   e.path -> puttext;
   ''' is composed like this:'->putline;
   'Head of path:      '->puttext; e.path.head -> putline;
   'Name:              '->puttext; e.path.name -> putline;
   'Prefix of name:    '->puttext; e.path.name.prefix -> putline;
   'Suffix of name:    '->puttext; e.path.name.suffix -> putline;
   'Extension of name: '->puttext; e.path.name.extension -> putline;
   (if e.exists then '(and the entry exists on disk)' -> putline;
    else '(there is no such entry on disk)' -> putline;
   if);
#)
