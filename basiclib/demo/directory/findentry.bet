ORIGIN '~beta/basiclib/v1.5/directory';

--- program: descriptor ---

(* Program showing a simple use of directory: The directory with the path
 * given as first argument is searched for the entry given as the second
 * argument. If the path given is not a directory, an exception will be raised.
 *)

(# d: @directory; candidate: ^text;
do (if noOfArguments <> 3 then
       'Usage: ' -> puttext; 
       1->arguments->puttext;
       ' directory candidate' -> putline;
       stop;
   if);
   '''' -> put;
   2 -> arguments -> d.name -> puttext;
   3 -> arguments -> candidate[];
   candidate[] -> d.findentry
   (# notfound::< 
        (# do ''' does not contain an entry named '''->puttext #);
   do select
      (# whenfile::< (# do ''' contains a file named ''' -> puttext #);
         whendir::< (# do ''' contains a directory named ''' -> puttext #);
         whenother::< (# do ''' contains an entry named ''' -> puttext #);
      #);
   #);
   candidate[] -> puttext;
   '''' -> putline;
#)
