ORIGIN '~beta/basiclib/v1.5/directory';

--- program: descriptor ---

(* A simple use of Directory: If the directory specified on the command line
 * exists, the user is offered the opportunity to either add or delete either a
 * file or a directory in that directory
 *)

(# d: @directory;
do (if noOfArguments <> 2 then
       'Usage: ' -> puttext; 1->arguments->puttext; ' directory' -> putline;
       stop;
   if);
   2 -> arguments -> d.name;
   
   (if d.entry.exists then
       'What do you want to do with ''' -> puttext;
       d.name->puttext;
       ''' ?'->putline;
       '\tAdd a file         (f)'-> putline; 
       '\tAdd a directory    (d)'-> putline; 
       '\tDelete a file      (F)'-> putline; 
       '\tDelete a directory (D)'-> putline; 
       'Type answer: ' -> puttext;
       (if keyboard.get
        // 'f' then
           'What should the file be called ? '->puttext;
           keyboard.getatom -> d.createfile;
        // 'd' then
           'What should the directory be called ? '->puttext;
           keyboard.getatom -> d.createdir;
        // 'F' then
           'What file should be deleted ? '->puttext;
           keyboard.getatom -> d.deletefile;
        // 'D' then
           'What directory shold be deleted ? '->puttext;
           keyboard.getatom -> d.deletedir;
       if);
    else
       ''''->put;
       d.name->puttext;
       ''' does not exist. Goodbye.'->putline;
   if);
#)
           
