ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/file';
(* A simple demo showing the use of pattern stream 
 * - the superpattern of text and file
 *)
---lib:attributes--
addLineNumbers:
  (* The stream S is copied to the text T
   * with line numbers added to each line
   *)
  (# S: ^stream; T: ^text; ln:@integer
  enter S[]
  do &text[]->T[];
     read:
       (if not S.eos then
           ln + 1 -> ln -> T.putint; 
           ':' -> T.put;
           S.getLine -> T.putline;
           restart read
       if)
  exit T[]
  #);
tryWithText:
  (* Using addLineNumbers on a text *)
  (# T: @text
  do 'hello world\n'
     'here I am\n'
     'how are you today!'
       -> T;
     0 -> T.setPos;
     T[] -> addlineNumbers -> putline;
  #);
tryWithFile:  
  (* Using addLineNumbers on a file *)
  (# F: @file
  do 'addLineNumbers.bet'->F.name; 
     F.openRead;
     F[] -> addlineNumbers -> putline;
  #)
---program:descriptor---
(#
do tryWithText;
   tryWithFile
#)

