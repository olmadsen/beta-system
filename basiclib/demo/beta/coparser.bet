ORIGIN '~beta/basiclib/v1.5/betaenv'
--- PROGRAM: descriptor ---
(# (* recursive descent parser organized as a component *)
   Parse: @|
     (# NextCh: 
          (* read next non blank character *)
          (# do getNonBlank -> ch  #);
        ch: @char; (* next char on input *)
        Exp: 
          (# do
             &Term;
             addOp: 
               (if ch // '+' then
                   nextCh;
                   &Term;
                   '+' -> symb;
                   suspend;
                   restart addOp
          if)#);
        Term: 
          (# do
             &factor;
             multOp: 
               (if ch // '*' then
                   NextCh;
                   &Factor;
                   '*' -> symb;
                   suspend;
                   restart multOp
          if)#);
        Factor: 
          (# do
             (if ch
              // 'a' // 'b' // 'c' then
                 ch -> symb;
                 suspend;
                 NextCh
              // '(' then
                 NextCh;
                 &Exp;
                 (if (ch=')') 
                  // true then NextCh
                  // false then &error 
          if)if)#);
        Error: 
          (# do '?' -> Symb; Suspend #);
        Symb: @char
     do NextCh;
        &Exp;
        (if (ch='!')
         // true then '!' -> symb; suspend
         // false then  &Error
        if)
     exit Symb
     #); (* end Parse *)
   Buffer: @|
     (# buf: [10] @char; Top: @integer;
        print: 
          (# do
             newLine;
             (for i: Top repeat
                  '.' -> put;
                  buf[i] -> put;
             for);
             newline
          #);
        ch: @char
     enter ch
     do (for i: buf.range repeat
             ch -> buf[i->Top];
             suspend;
        for);
        'Buffer Overflow' -> puttext; newLine;
        '?' -> ch
     exit ch
     #);
do 'Type an expression consisting of a, b, c, +, *: ' -> putText;
   L: 
     cycle
     (# S: @char
     do Parse -> Buffer -> S -> put;
        (if S
         // '!' then
            buffer.print;
            Leave L
         // '?' then
            newline; 'syntax error' -> puttext; newline;
            Leave L
        if);
     #); 
#)

