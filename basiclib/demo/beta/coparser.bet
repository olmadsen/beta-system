ORIGIN '~beta/basiclib/betaenv'
--- PROGRAM: descriptor ---
(* recursive descent parser organized as a component (coroutine) *)
(# 
   Parse: @ |
     (# NextCh: 
          (* read next non blank character *)
          (# do getNonBlank -> ch  #);
        ch: @char; (* next char on input *)
        Exp: 
          (# 
          do &Term;
             addOp: 
               (if ch = '+' then
                   nextCh;
                   &Term;
                   '+' -> symb;
                   suspend;
                   restart addOp
          if)#);
        Term: 
          (# 
          do &factor;
             multOp: 
               (if ch = '*' then
                   NextCh;
                   &Factor;
                   '*' -> symb;
                   suspend;
                   restart multOp
          if)#);
        Factor: 
          (# 
          do (if ch
              // 'a' // 'b' // 'c' then
                 ch -> symb;
                 suspend;
                 NextCh
              // '(' then
                 NextCh;
                 &Exp;
                 (if ch = ')' then 
                     NextCh
                  else
                     &error 
             if)if)
          #);
        Error: 
          (# do '?' -> Symb; Suspend #);
        Symb: @char
     do NextCh;
        &Exp;
        (if ch = '!' then 
            '!' -> symb; suspend
         else
            &Error
        if)
     exit Symb
     #); (* end Parse *)
   Buffer: @ |
     (# buf: [10] @char; top: @integer;
        print: 
          (# 
          do newLine;
             (for i: top repeat
                  buf[i] -> put;
             for);
             newline
          #);
        ch: @char
     enter ch
     do Loop:
          (#
          do (if (top+1 -> top) > buf.range then buf.range -> buf.extend if);
             ch -> buf[top];
             suspend;
             restart Loop
          #);
     exit ch
     #);
do 'Type an expression consisting of: ' -> puttext;
   '\n\t"a, b, c, +, *" ' -> puttext;
   '\nterminated by "!"\n>>> ' -> putText;
   L: 
     cycle
     (# S: @char
     do Parse -> Buffer -> S -> put;
        (if S
         // '!' then
            buffer.print;
            Leave L
         // '?' then
            '\nsyntax error\n' -> puttext; 
            Leave L
        if);
     #); 
#)

