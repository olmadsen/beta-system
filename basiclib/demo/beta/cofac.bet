ORIGIN '~beta/basiclib/betaenv'
--- PROGRAM: descriptor ---
(# Factorial: @|
     (* Component that computes factorial of the enter parameter N
      * Factorial values that have been computed are cached in T
      *)
     (# T: [100] @integer; Top: @integer;
        N: @integer
     enter N
     do 1 -> Top -> T[1];
        Cycle
        (# do
           (if (Top<N) then
               (Top+1,N) -> ForTo(# do T[inx-1]*inx -> T[inx]#);
               N -> Top
            else
               (* Use cached result *)
           if);
           suspend
        #)
     exit T[N]
     #);
do 'Computation of factorial' -> putText; newline;
   Loop: 
     cycle
     (# N: @integer
     do 'Value? ' -> putText; getInt -> N;
        (if (0<N) then
            N -> putInt; '! = ' -> putText;
            N -> Factorial -> putInt; newline
         else
            leave Loop
        if)
     #)
#)
