ORIGIN '~beta/basiclib/v1.5/betaenv'
--- PROGRAM: descriptor ---
(# Factorial: @|
     (* Component that computes factorial of the enter parameter N
      * Factorial values that have been computed are save in T
      *)
     (# T: [100] @integer; Top: @integer;
        N: @integer
     enter N
     do 1 -> Top -> T[1];
        Cycle
        (# do
           (if (Top<N) // true then
               '.' -> put;
               (Top+1,N) -> ForTo(# do T[inx-1]*inx -> T[inx]#);
               N -> Top
           if);
           suspend
        #)
     exit T[N]
     #);
do 'Computation of factorial' -> putText; newline;
   Loop: 
     cycle
     (# N: @integer
     do 'Value? ' -> putText; getInt -> N;
        (if (0<N)
         // True then
            '! = ' -> putText;
            N -> Factorial -> putInt; newline
         // False then
            leave Loop
if)#)#)
