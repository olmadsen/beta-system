ORIGIN '~beta/basiclib/v1.5/betaenv'
--- PROGRAM: descriptor ---
(# (* Example of BETA program for: *)
   (* (1) computing factorial,        *)
   (* (2) implementing a linked list. *)
   (* Output is (0 4! 3! 2! 1!)       *)
   Factorial: 
     (# N,fac: @integer
     enter N
     do (if (N<1)
         // true then 1 -> fac
         // false then ((N-1) -> &Factorial) * N -> fac
        if)
     exit fac
     #);
   Link: 
     (* A linked list of integers *)
     (# elm: @integer;
        succ: ^Link;
        Insert: (* insert an element before this element *)
          (# e: @integer; R: ^Link
          enter e
          do &Link[] -> R[];
             e -> R.elm;
             succ[] -> R.succ[];
             R[] -> succ[]
          #);
        Display: 
          (# do
             elm -> screen.putInt (# format::< (# do 3 -> width #)#);;
             (if (succ[] = NONE) // false then
                 &succ.display
             if)
          #)
     #);
   Head: @Link
do 'Computation of ' -> puttext; newline;
   '( 0! 4! 3! 2! 1!)' -> puttext; newline;
   0  -> &Factorial -> head.elm;
   (for i: 4 repeat
        i -> &Factorial -> head.Insert
   for);
   '(' -> put; head.display; ')' -> put;
   newline;
#)
