ORIGIN '~beta/basiclib/v1.4/betaenv'
--- INCLUDE 'cosysenv'
--- PROGRAM: descriptor ---
(#
do 'Type a line of characters.' -> putText; newline;
   'Double ''a''s will be converted to ''b''s'-> putText; newline;
   'Double ''b''s will be converted to ''c''s' -> putText; newline;
   SymmetricCoroutineSystem
   (# DoubleAtoB: @| SymmetricCoroutine
        (# ch: @char
        do Cycle
           (# do get -> ch; 
              (if ch 
               // 'a' then
                  get -> ch;
                  (if ch 
                   // 'a' then 
                      (DoubleBtoC[],'b') -> DoubleBtoC.Resume
                   else
                      (DoubleBtoC[],'a') -> DoubleBtoC.resume;
                      (DoubleBtoC[],ch) -> DoubleBtoC.resume
                  if)
               else (DoubleBtoC[],ch) -> DoubleBtoC.resume
        if)#)#);
      DoubleBtoC: @| SymmetricCoroutine
        (# ch: @char;
           Resume::< (# enter ch #);
        do Cycle
           (# do
              (if ch
               // 'b' then
                  DoubleAtoB[] -> DoubleAtoB.Resume;
                  (if ch
                   // 'b' then
                      'c' -> Put
                   else
                      'b' -> put;
                      ch -> put
                  if)
               // ascii.nl then SUSPEND
               else ch -> put
              if);
              DoubleAtoB[] -> DoubleAtoB.Resume
        #)#);
   do DoubleAtoB[] -> Start;     
   #);
   newline;
#)


