ORIGIN '~beta/basiclib/v1.5/betaenv';

--- program: descriptor --
(* Demo example shwoing examples of how to use the text concept from betaenv.
 * The text object Records, consists of a sequence of records. Each record
 * has the form:
 *    name Job:aJob Salary:aSalary /
 * The program shows various patterns for manipulating the Records text.
 *)
(# GetName: (* read next name from T *)
     (# T: ^text; T1: @text
     enter T[]
     do (* scan and skip until a letter is met *)
        T.scan(# while::<(#do NOT (ch->Ascii.isLetter)->value #)#);
        (* scan and read while letters in T *)
        T.scan
        (# while::<(#do ch->Ascii.isLetter->value #)
        do ch->T1.put
        #)
     exit T1
     #);
   GetRecord: 
     (* Get the record with the name N  and return name and dat part *)
     (# N: ^Text; name,data: @Text
     enter N[]
     do Records.reset;
        FindName:
        (if not Records.eos then
            Records[]->getName->name; data.clear;
           (* scan and read until '/' is met *)
            Records.scan(# while::<(#do (ch<>'/')->value#) do ch->data.put #);
            (if not N[]->name.equal then restart FindName 
        if)if)
     exit(name,data)
     #); 
   GetJobAndSalary: 
     (* get the  job and salary from data part, which is the part after name *)
     (# Data: ^Text; Job: @Text; Salary: @integer
     enter Data[]
     do Data.reset;
        Data[]->GetName; Data.get (* skip ':' *); Data[]->GetName->Job;
        Data[]->GetName; Data.get; Data.GetInt->Salary
     exit(Job,Salary)
     #);

   Records: @Text;
do '----------1:'->putLine; 
   (*  initialize Records *)
   'John Job:Programmer salary:120000 / '->Records.append;
   'Joan Job:Doctor salary:130000 / '->Records.append;
   'Mary Job:Boss salary:140000 / '->Records.append;
   Records[]->putLine;
   
   '----------2:'->putLine;
   (* split Records into atoms *)
   Records.reset;
   scan:
   cycle
     (# 
     do Records.getAtom->putline;
        (if Records.eos then leave scan
     if)#);
   '----------3:'->putLine;
   (* Find record with name Joan and decode data part *)
   (# Name,Data,Job: @ text; Salary: @Integer
   do 'Joan'->GetRecord->(Name,Data);
      'Ms. '->Name.prePend; 
      ' II'->name.append;
      Name[]->putText; ' has the data: '->putText;
      Data[]->GetJobAndSalary->(Job,Salary);
      'Job='->putText; Job.makeUC;  Job[]->putText;
      ' Salary='->putText; Salary->putInt; newline
   #)
   
#)
