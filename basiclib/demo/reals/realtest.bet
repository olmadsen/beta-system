ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/basiclib/v1.6/math';
INCLUDE '~beta/basiclib/v1.6/numberio';

--- program: descriptor ---
(# x,y: @real;
   b: @boolean;
do '                                Demo of reals'->putline;
   '                         ==========================='->putline;
   newline;
   newline;

   'First print the number 1.23: '->putline;
   1.230000->x;
   x->y;
   y->putreal;newline;
   newline;

   'Test constants: '->putline;
   'This is 0: '->puttext;
   0->putreal;newline;
   'This is 1: '->puttext;
   1->putreal;newline;
   'This is pi: '->puttext;
   pi->putreal;newline;
   newline;

   'This test prints the square root of 2 ( = '->puttext;
   2.0->sqrt->y -> putreal;
   ' ).'->putline;
   'The number is printed 12 times. The first 6 times with 11 decimal digits,'
     -> putline;
   'the last 6 times with 16 decimal digits.'->putline;
   'The first 6 times is in a field of width 30, the last 6 times in a field'
     -> putline;
   'in a field of width 40. Number 1,2,3,7,8 and 9 should be left justified,' 
     -> putline;
   'whereas the others should be right justified. Number 1,4,7 and 10 will use'
     -> putline;
   'scientific format, the others will use ordinary decimal format.'
     -> putline;
   '                         column 30'->putline;
   '                             |         column 40'->putline;
   '                             |         |'->putline;
   y->screen.putreal(# format::< (# do exp->style; 10->precision; 30->width; true->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do noexp->style; 10->precision; 30->width; true->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do 11->precision; 30->width; true->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do exp->style; 10->precision; 30->width; false->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do noexp->style; 10->precision; 30->width; false->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do 11->precision; 30->width; false->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do exp->style; 15->precision; 40->width; true->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do noexp->style; 15->precision;  40->width; true->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do 16->precision;  40->width; true->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do exp->style; 15->precision;  40->width; false->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do noexp->style; 15->precision;  40->width; false->adjustLeft; #)#);newline;
   y->screen.putreal(# format::< (# do 16->precision;  40->width; false->adjustLeft; #)#);newline;
   newline;

   'Now testing real addition. Computing 1.0+2.0+3.0+...+99.0=4950'->putline;
   0->x;
   (for i: 99 repeat
        i+x->x;
   for);
   'Result: '->puttext;
   x->putreal; newline;
   newline;

   'Now testing real multiplication.'->putline;
   '3.0*7.0 = '->puttext;
   3.0 * 7.0->putreal; newline;
   '(-4.0)*1.3 = '->puttext;
   (-4.0) * 1.3->putreal; newline;
   '(-4.0)*(-3.0) = '->puttext;
   -4.0 * (-3.0)->putreal; newline;
   newline;

   'Testing real division.'->putline;
   '1.0/3.0 = '->puttext;
   1 div 3.0->putreal; newline;
   '(-4.0)/8.0 = '->puttext;
   -4.0 div 8.0->putreal; newline;
   newline;

   'Testing real subtraction.'->putline;
   '1.0-3.0 = '->puttext;
   1.0 - 3.0->putreal; newline;
   '(-4.0)-(-8.0) = '->puttext;
   (-4.0) - (-8.0)->putreal; newline;
   newline;

   'Testing real powers.'->putline;
   '3.0^3.0 = '->puttext;
   (3.0,3.0)->pow->putreal; newline;
   '(2.0)^(-3.0) = '->puttext;
   (2.0,-3.0)->pow->putreal; newline;
   '(2.0)^(0.5) = '->puttext;
   (2.0,0.5)->pow->putreal; newline;
   newline;
   (# putR: @screen.putreal
        (# format::< 
             (# 
             do noexp->style; 
                6->precision; 
                10->width;
                false->adjustLeft;
        #)#);
   do 'Testing unary functions, part 1.'->putline;
      '     x      floor(x)   sqrt(x)    exp(x)   log(x)    abs(x)    neg(x)' 
        -> putline;
      (for i: 15 repeat
           (-1+(0.2*i))->x;
           x->putR;
           x->floor->putR;
           (if 0.0<=x then
               x->sqrt->putR;
            else
               '          '->puttext;
           if);
           x->exp->putR;
           (if 0.0<x then
               x->log->putR;
            else
               '          '->puttext;
           if);
           x->fabs->putR;
           -x->putR;
           newline;
      for);
      newline;
      
      'Testing unary functions, part 2.'->putline;
      '     x       sin(x)    cos(x)    tan(x)    asin(x)   acos(x)   atan(x)'
        -> putline;
      (for i: 15 repeat
           -1+(0.2*i)->x;
           x->putR;
           x->sin->putR;
           x->cos->putR;
           x->tan->putR;
           (if (x->fabs)<=1.0 then
               x->asin->putR;
            else
               '          '->puttext;
           if);
           (if (x->fabs)<=1.0 then
               x->acos->putR;
            else
               '          '->puttext;
           if);
           x->atan->putR;
           newline;
      for);
      newline;
   #);
#)
