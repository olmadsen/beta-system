origin 'minienv'
---minienv_lib:attributes---
Queue:
  (# isEmpty: 
       (# value: @boolean
       do head[] = none -> value
       exit value
       #);
     insert:
       (# P: ^| Beta_system; E: ^elem
       enter P[]
       do P[] -> elem -> E[];
          (if last[] <> none then
              E[] -> last.next[] ;
          if);
          E[] -> last[];
          (if head[] = none then last[] -> head[] if)
       #);
     next:
       (# P: ^| Beta_system
       do (if head[] <> none then
              head.P[] -> P[];
              head.next[] -> head[];
          if)
       exit P[]
       #);
     scan:
       (# current: ^| Beta_system; E: ^elem
       do head[] -> E[];
          L: (if E[] <> none then
                 E.P[] -> current[];
                 inner;
                 E.next[] -> E[];
                 restart L
             if)
       #);
     elem:
       (# P: ^| Beta_system;
          next: ^elem;
       enter P[]
       exit this(elem)[]
       #);
     head, last: ^elem
     
  #);
(* system: 
     (# id: @text; 
        setId: (# T: ^text enter T[] do T -> id #);
     do inner
     #);
*)
     
