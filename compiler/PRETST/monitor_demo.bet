origin 'minienv'; 
include 'monitor'
---program:descriptor---
ConcurrentSystem
(# trace: (# exit false #);
   M: @monitor
     (# S: ^text;
        set: entry
          (# T: ^text;
          enter T[]
          do (if trace then 'set:'->out.puttext; T[] -> out.putline if);
             T[] -> S[]
          #);
        get: entry
          (# T: ^text
          do S[] -> T[];
             (if trace then 'get:'->out.puttext; T[] -> out.putline if)
          exit T[]
          #);
        init:: (# do 'Go!'-> S[] #)
     #);
   prod: @| cThread
     (#
     do exclusive(#do 'producer: put hello in the buffer'->out.putline #);
        'hello' -> M.set;
        exclusive(#do 'producer: put world in the buffer'->out.putline #);
        'world' -> M.set;
     #);
   cons: @| cThread
     (# T: ^text;
     do (for i: 6 repeat
             exclusive(#do 'consumer: got from buffer: '->out.puttext #);
             M.get -> T[];
             exclusive(#do T[] -> out.putline #);
        for);
     #)
do M.init;
   prod.fork;
   cons.fork;
#)
