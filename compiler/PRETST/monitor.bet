ORIGIN 'scheduler';
BODY 'monitorbody'
---BETA_systemLib:attributes---
monitor:
  (# Condition:
       (# q: @Semaphore;
          Wait: <<SLOT conditionWait:descriptor>>;
          Signal: <<SLOT contitionSignal:descriptor>>;
       #);
     Wait:
       (# cond: @boolean 
       do INNER;
          (if not cond then 
              return; (* exit monitor *)
              (* pause;
               * make sure that this process is moved
               * to the end of the ready queue
               *)
              suspend;
              mutex.wait; (* reentry of monitor *)
              restart Wait
          if)
       #);
     entry:
       (#
       do mutex.wait; INNER; return
       #);
     init:<
       (# 
       do INNER; mutex.signal; 
       #);
     (* private:
      * 
      * mutex controls entry to the Monitor.  urgent delays a
      * signalling process.
      * 
      * return is executed by processes leaving the monitor.
      * Reactivates possible processes waiting for entry: delayed
      * signalling processes (urgent) have first priority
      *)
     mutex: @semaphore;
     urgent: @semaphore; 
     return: @<<SLOT MonitorReturn:descriptor>>;

  #)
