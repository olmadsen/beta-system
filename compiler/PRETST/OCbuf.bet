ORIGIN 'minienv';
INCLUDE 'monitor';
---program: descriptor--
(# buffer: @Monitor
     (# R: [4] @char; in,out: @integer;
        full: (# exit in = out #);
        empty: (#exit (in = (out mod R.range)+1) #);
        Put: Entry
          (# ch: @char
          enter ch
          do wait(# do (not full) -> cond #);
             ch -> R[in]; (in mod R.range)+1 -> in;
          #);
        get: Entry
          (# ch: @char
          do wait(# do (not empty) -> cond #); 
             R[(out mod R.range)+1->out] -> ch; 
          exit ch
          #);
        init::< (# do 1 -> in; R.range -> out #)
     #);
   
   prod: @| BETA_system
     (# ch: @char
     do L: cycle
          (# 
          do in.get -> ch -> buffer.put;
             (if ch = '!' then leave L if)
          #) 
     #);
   cons: @| BETA_System
     (# ch: @char
     do L:
          cycle
          (# do buffer.get -> ch -> out.put;
             (if ch = '!' then leave L if)
          #) 
     #);
   
do buffer.init;
   prod[] -> fork; cons[] -> fork
#)

