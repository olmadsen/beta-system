ORIGIN 'minienv';
INCLUDE 'monitor';
---program: descriptor--
ConcurrentSystem
(# buffer: @Monitor
     (# R: [4] @char; in,out: @integer;
        full: (# exit in = out #);
        empty: (#exit (in = (out mod R.range)+1) #);
        Put: Entry
          (# ch: @char
          enter ch
          do wait(# do (not full) -> cond #);
             ch -> R[in]; (in mod R.range)+1 -> in;
          #);
        get: Entry
          (# ch: @char
          do wait(# do (not empty) -> cond #); 
             R[(out mod R.range)+1->out] -> ch; 
          exit ch
          #);
        init::< (# do 1 -> in; R.range -> out #)
     #);
   
   prod: @| cThread
     (# ch: @char
     do L: cycle
          (# 
          do in.get -> ch -> buffer.put;
             (if ch = '!' then leave L if)
          #) 
     #);
   cons: @| cThread
     (# ch: @char
     do L:
          cycle
          (# do buffer.get -> ch -> out.put;
             (if ch = '!' then leave L if)
          #) 
     #);
   
do buffer.init;
   cons[] -> cons.thisThread[];
   prod[] -> prod.thisThread[];
   prod.fork; 
   cons.fork
#)

