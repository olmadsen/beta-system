ORIGIN 'minienv';
--- PROGRAM: descriptor ---
(# (* Example of BETA program for: *)
   (* (1) computing factorial,        *)
   (* (2) implementing a linked list. *)
   (* Output is (0 4! 3! 2! 1!)       *)
   Factorial: 
     (# N,fac: @integer
     enter N
     do (if (N<1)
         // true then 1 -> fac
         // false then ((N-1) -> &Factorial) * N -> fac
        if)
     exit fac
     #);
   Link:
     (* A linked list of integers *)
     (# elm: @integer;
        succ: ^Link;
        Insert: (* insert an element before this element *)
          (# e: @integer; R: ^Link
          enter e
          do &Link[] -> R[];
             e -> R.elm;
             succ[] -> R.succ[];
             R[] -> succ[]
          #);
        Display: 
          (# do
             elm -> out.putInt;
             (if (succ[] = NONE) // false then
                 ',' -> out.put;
                 &succ.display
             if)
          #)
     #);  
   Head: @Link
do 'Computation of ' -> out.puttext; out.newline;
   '( 0! 4! 3! 2! 1!)' -> out.puttext; out.newline;
   0  -> &Factorial -> head.elm;
   (for i: 4 repeat
        i -> &Factorial -> head.Insert
   for);
   '(' -> out.put; head.display; ')' -> out.put;
   out.newline;
#)
