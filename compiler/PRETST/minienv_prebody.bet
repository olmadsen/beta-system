ORIGIN 'minienvbody' ;
INCLUDE 'Queue';
BUILD pre
'pre/BetaComponent.prc'
'BetaComponent.st'
'pre-st -o $0 $1';

BUILD pre
'pre/BetaStructure.prc'
'BetaStructure.st'
'pre-st -o $0 $1';

---minienv_lib:attributes---
InterruptHandler: class 
  (# writeText: external
       (# T: [0] @char 
       enter T        
       do '%primitive:put' -> callC 
       #);   
     setIHI: external
       (# n: @integer
       enter n
       do '%primitive:setIHI' -> callC
       #); 

     handle: proc
       (#
       do scheduler.switch
       #)
  #);
 
setIH: external
  (#  
  do '%primitive:setIH' -> callC  
  #);   

--fork:doPart-- 
do 
   P[] -> scheduler.SQS.insert
   
---scheduler:descriptor---
(#
   out: @ stream
     (# put::<
          (# write: External  
               (# ch: @char
               enter ch 
               do '%primitive:put' -> callC 
               #);
          do ch -> write
          #) 
        
     #);
   in: @ stream
     (# get::<
          (# read: external 
               (# ch: @char
               do '%primitive:get' -> callC 
               exit ch
               #);
          do read -> ch
          #)
     #);
   setIHI: external
     (# n: @integer
     enter n
     do '%primitive:setIHI' -> callC
     #); 

   switch:
     (#
     do (if (active[] <> none) and intOk then
            active.%xsuspend;
        if)
     #);
   IH: @InterruptHandler; (* Need to assure that
                           * InterruptHandler is imported
                           *)
   main: ^| main_program;
   SQS: @ Queue;
   active: ^| beta_system;
   intOK: @boolean;
do 'Start'->out.putline;
   (in[],out[],'') -> main_program;
   '\nmain ended\n' -> out.puttext;
   (if not SQS.isEmpty then
       setIH;
       L:
         (# 
         do SQS.next -> active[];
            (if active[] = none then leave L if);
            200 -> setIHI;
            true -> intOK;
            active;
            false -> intOK;
            active[] -> SQS.insert;
            restart L
   #)if)
#)



