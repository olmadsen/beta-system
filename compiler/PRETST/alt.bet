origin 'minienv';
include 'scheduler'
--program:descriptor---
ConcurrentSystem
(# S1: @ | cThread
     (# 
     do 'S1 started: ' -> out.putline;
        AlternatingSystem
        (# P1: @ | aThread
             (# foo: (# n: @integer enter n do V+n -> V #);
                V: @integer
             do 'P1 started: ' -> out.putline;
                L:  (# 
                    do 'P1_here: ' -> out.putline;
                       (for i: 100 repeat i-> foo for);
                       (if not P2.terminated then M.wait if);
                       (if V < 500000 then restart L if);
                    #)
             #);
           P2: @ | aThread
             (# bar: (# n: @integer enter n do  V + n*n -> V #);
                V: @integer;
                terminated: @boolean
             do 'P2 started: ' -> out.putline;
                 L:  (#
                     do 'P2_here: ' -> out.putline;
                        (for i: 100 repeat i -> bar for);
                        M.signal; 
                        (if V < 5000000 then restart L if)
                     #);
                true -> terminated;
                M.signal
             #);
           M: @semaphore
        do P1.start;
           P2.start
        #)
     #);
   S2: @ | cThread
     (# bar: (# n: @integer enter n do  V + n*2 -> V #); 
        V: @integer
     do 'S2 started: ' -> out.putline;
        L:  (#
            do 'S2: ' -> out.putline;
               (for i: 100 repeat i -> bar for); 
               (if V < 1000000 then restart L if)
            #)
     #)
do 
   S1.fork;
   S2.fork
#)
