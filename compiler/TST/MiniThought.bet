ORIGIN 'tstenv';
INCLUDE 'IUnknown';
BUILD sun4s '$$/MiniThoughtUser.o' 'MiniThoughtUser.c' '\$CC -g -c -o $0 $1';
(* BETA implementation of COM object passed to external C.
 * Implements MiniThought that supports the 2 interfaces
 * calculator and displayer
 *)
---program:descriptor---
(# calculator: IUnknown
     (# add:< (# n: @integer enter n do INNER #); 
        sub:< (# n: @integer enter n do INNER #);
     #);
   displayer: IUnknown
     (# displayEnglish:< (# do inner #);
        displayDanish:< (# do inner #);
     #);  

   MiniThought: IUnknown
     (# val: @integer;
        MiniThoughtCalc: calculator  
          (# QueryInterface::QIF;
             add:: 
               (# 
               do 'Add: '->puttext; n ->putint; newline; 
                  val + n -> val 
               #);
             sub:: 
               (# 
               do 'Sub: '->puttext; n ->putint; newline; 
                  val - n -> val 
               #);
          #);
        MiniThoughtDisplay: displayer
          (# QueryInterface:: QIF;
             displayEnglish:: 
               (#do 'Value: ' ->puttext; val ->putint; newline #);
             displayDanish:: 
               (#do 'Vaerdi: ' ->puttext; val ->putint; newline #);
          #);
        QIF: Query
          (# 
          do (if IfId
              // 1 then 
                 'Returning calculator interface'->puttext;
                 &MiniThoughtCalc[] -> I[]
              // 2 then 
                 'Returning displayer interface'->puttext;
                 &MiniThoughtDisplay[] -> I[]
             if);                 
          #);
        QueryInterface :: QIF
     #);

   PutMiniThought: external(# R: ^MiniThought enter R[] #);
   h: ^MiniThought;
do &MiniThought[] -> h[];
   h[] -> PutMiniThought
#)

