origin 'tstenv';
INCLUDE 'tstlib';
BODY 'tstbreakbody'
---lib:attributes---
TstBreak:    
  (#
     A: (# b: @ boolean     
        do true -> b; 'a'->put;
           L: (if b then 
                  'b'->put;
                  false -> b;    
                  restart L;
              if);
           M: (# foo: (# do 'd'->put; leave M ; true -> b#);  
              do 'c'->put;
                 foo;  
                 false -> b
              #);
           'e'->put;
           true -> b;
           K: (if b then 
                  <<SLOT N:descriptor>> 
               else 
                  (# <<SLOT NN:doPart>> #);
                  leave K;
                  '?'->put;
                  (# x: @real do leave K; '!'-> put #);
              if);  
        #);  
     B: (# 
        do ch->put; 
           (if not bb then 
               true->bb;  
               ch+1->put; ch+2->ch;
               restart B
            else
               'k'->put;
               leave B;
               '!'->put
           if)
        #);
     bb: @boolean; ch: @char;
     BreakWithLocalDesc: (* 'lmno' *)
       (# b: @boolean
       do ch->put; (* 'l' *)
          true->b;
          L1: (if b then
                  L2:
                    (# x: @integer
                    do
                       (if b then
                           ch+1->ch;
                           ch -> put; (* 'm' *)
                           false -> b;
                           restart L2
                       if)
                    #);
                  ch+1->ch;  (* 'n' *)
                  ch->put;
                  restart L1
              if);
          ch+1->ch->put; (* 'o' *)
       #);
     Break:
       (# ch: @char; first: @boolean;
          foo: (# boo: @boolean
               enter boo
               do ch->put;
                  (if boo then 
                      ch+1->ch;
                      false->boo;
                      restart foo
                   else
                      ch+1->ch->put;
                      leave foo;
                      '!'->put
                  if)
               #);
          bar: (# do inner #);
          fooBar: bar
            (# do (if ch='7' then ch+1->ch->put; restart fooBar 
                   else ch+1->ch->put; leave fooBar  
            if)#)
       do L: (for i:4 repeat
                  '0'-> put;
                  leave L;
                  '!'->put
             for);
          '1'->ch; true->first;
          loop:
            (for i:4 repeat
                 ch->put;
                 (if first then false->first; '2'->ch; restart loop 
                  else leave loop 
            if)for);
          N:
            (#  p: (# do '4'->put; leave N #)
            do
               '3'->put;
               p;
               '!'->put;
               (# do leave N #);
               '?'->put;
            #);
          '5'->ch;
          true->foo;
          fooBar
       #);
     BreakComponent:
       (# 
       do 'a' -> put;
          L:
            (# Comp: @|
                 (# 
                 do 'b' -> put;
                    leave L; (* leave out of component *)
                    '!' -> put;
                 #);
            do Comp;
               '%' -> put;
            #);
          'c' -> put;
       #);
     BreakCallback:
       (# 
       do (* NYI *)
          'd' -> fill;
       #);
  do A;
     'h'->ch;
     B;
     'l'->ch;
     BreakWithLocalDesc;
     'p'->put;
     (for i: 'z'-'p' repeat 'p'+i -> put for);  
     Break;     
     newl;
     BreakComponent;
     BreakCallback;
  #)                

