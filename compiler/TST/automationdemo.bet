ORIGIN '~beta/guienv/guienv';
INCLUDE 'iautomationdemo';
INCLUDE '~beta/comlib/comlib';
INCLUDE '~beta/comlib/OAIdl'
        '~beta/comlib/Unknwn'
        '~beta/comlib/comtypes';
build	nti     '$$/copyRef.obj'  'copyRef.c' 'betacc $0 $1'
--PROGRAM: descriptor--
guienv
(# CLSID_simpleserver: ^CLSID;
   IID_UNKNW,SimpleServer_IID: @IID;
   pIUnknown: ^IUnknown;
   pISimpleServer: ^simpleDispatchServer;
   clsstr: @BSTR;
   str: @BSTR;
   hr: @HRESULT; 
   bh: @BSTRHolder;
   ih: @int16Holder;
 
   obj: ^object;
   copyRef: external(# source,dest: @int32 enter(source,dest)#)
do 0 -> CoInitialize;
   
   &CLSID[] -> CLSID_simpleserver[];
   SimpleDispatchServer_CLSID -> CLSID_simpleserver;

   IUnknown_IID -> IID_UNKNW;

   IDispatch_IID -> SimpleServer_IID; 

   'Create simple server instance '->putLine;
   
   (CLSID_simpleserver[], NONE, CLSCTX_LOCAL_SERVER, IID_UNKNW[])
     -> CoCreateInstance
	 -> pIUnknown[];
   
   'CoCreateInstance done'->putLine;

   (if pIUnknown[]<>NONE then
       'Succeeded creating component.'->putLine;
       
       (pIUnknown[], SimpleServer_IID[]) -> Query -> obj[];
	   
	   (@@obj,@@pISimpleServer) -> copyRef;

       (if (pISimpleServer[]<>NONE) then
	       'Calling SimpleDispatchServer.print' -> screen.putline;

		   'Called from BETA automation client' -> str.setText;

           str -> pISimpleServer.print;

           'Done calling simpleDispatchServer.print' -> screen.putline;

           'Releasing interfaces' -> putline;
           pISimpleServer.Release;
       if);
       pIUnknown.Release;
    else
       'Could not create component' -> putline;
   if); 
   'CoUninitialize'-> putline;
   CoUninitialize;
   'CoUninitialize done' -> putline;
   stop; 
#)
