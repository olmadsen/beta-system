ORIGIN 'tstenv';
BUILD default '$$/ctstcom.o' 'ctstcom.c' '\$CC -g -c -o $0 $1'     
nti     '$$/ctstcom.obj' 'ctstcom.c' 'betacc $0 $1';     
---lib:attributes---
tstCOM:
  (* tst patterns for COM interface *)
  (# test: (# exit false #);
     
     (******************************************** 
      * Definitions for interfacing to external 
      * COM class impoted to BETA 
      ********************************************)
     xCOMclass: COM
       (* Interface to external COM class *)
       (# f1:< 
            (# n: @integer; ch: @char
            enter n
            exit ch
            #);
          f2:< 
            (# a,b,c: @integer; ch: @char
            enter(a,b,c)
            exit ch
            #);
          f3:<
            (# t: [1] @char; ix: @integer; ch: @char
            enter(t,ix)
            exit ch
            #);
          f4:<
            (# t,s: [1] @char; ix: @integer
            enter(ix,t)
            exit s
            #)
       #);
     GetXobj: 
       (* external function to import instance of xXOMclass *)
       external (# xR: ^xCOMclass exit xR[] #);
          
     (******************************************
      * definitions of COMclass implemented in 
      * BETA and exported to external language
      * *****************************************)
     bCOMclass: COM
       (* COM class implemented in BETA *)
       (# g0:<
            (#
            do 'h'-> chx -> put; 
            #);
          g1:< 
            (# c: @integer (* char dont work - stores long instead of byte*)
            enter c 
            do (if test then '   bCOMclass::g1'->putline if);
               c -> put; 
            #);
          g2:< 
            (# a,b,c: @integer
            enter(a,b,c)
            do (if test then '  bCOMclass::g2'->putline if);
               a+b+c -> val;
               (if val = 111 then ch -> put else '!' -> put if);
            exit 117
            #);
          g3:< 
            (# s: [1] @char; r: @integer
            enter s
            do (if test then '  bCOMclass::g3'->putline if);
               s -> puttext;
               S -> str;
               str.range -> r
            exit r
            #);
          ch:< 
            (# c: @char 
            do chx + 1 -> chx -> c -> c 
            exit c 
            #);
          chx: @char;
          val: @integer;
          str: [1] @ char
       #);
     PutBobj: 
       (* external function to export instance of bCOMclass *)
       external(# bR: ^bCOMclass; ch: @char enter(bR[],ch) #);
     
     (********** using xCOMclass and bCOMclass *******)
     xR: ^xCOMclass;
     ch: @char;
     T: [1] @char
  do GetXobj -> xR[];
     1013 -> xR.f1 -> ch;           ch -> put;    (* 'a' *)
     (10,20,2) -> xR.f2 -> ch;      ch -> put;    (* 'b' *)
     ('!$c?d#',2) -> xR.f3 -> ch;   ch -> put;    (* 'c' *)
     (4,'....d....') -> xR.f4 -> T; T -> puttext; (* 'def' *)
     (&bCOMclass[],'g') -> PutBobj;     
  #)
---program:descriptor---
(#
do tstCOM
#)
