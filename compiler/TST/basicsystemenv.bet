ORIGIN 'tstenv';
---lib:attributes---
BasicSystemEnv:
  (#
     Semaphore:
       (# P: (# do private.P #);
          V: (# do private.V #);
          private: @<<SLOT SemaphorePrivate:descriptor>>;
       #);
     Fork:
       (# S: ^| object;
          attToProcessor: external
            (# comp: @integer;
               tid: @integer;
            enter comp
            exit tid
            #);
          my_tid: @integer;
       enter S[]
       <<SLOT basicSystemEnvFork:doPart>>
       exit my_tid
       #);
     Wait:
       (# go: (# do #)
       do (for i: 100000 repeat  for)
       #);
     IO: @ 
       (# m: @semaphore;
          entry: (# do m.P; INNER; m.V #);
          init: (#do m.V #);
          putC: entry
            (# ch: @char
            enter ch
            do ch -> put
            #);
          putT: entry
            (# S: ^text
            enter S[]
            do S[] -> puttext2
            #);
          putI: entry
            (# i: @integer
            enter i
            do i -> putint
            #);
       #);
     
  do 'Start' -> putline;
     IO.init;
     INNER
  #)

-- SemaphorePrivate:descriptor --
(# strace: (#exit false #);
   mutex: @Integer;
   cnt: @Integer;
   P: (# 
      do L:
           (#
           do mutex.%lock;
              (if (cnt < 1) then
                  (if strace then 'P'->put; cnt -> putint;  if);
                  mutex.%unlock;             
                  Wait;
                  restart L
               else
                  (if strace then 'p'->put if);
                  cnt-1->cnt;
                  mutex.%unlock;
           if)#)
      #);
   V: (# 
      do 
         mutex.%lock;
         (if (cnt < 0) then
             (if strace then 'V'->put; cnt->putint  if);
             cnt+1->cnt;
             mutex.%unlock;
          else
             (if strace then 'v'->put if);
             cnt+1->cnt;
             mutex.%unlock;

         if)
      #);
#)

---basicSystemEnvFork:doPart---
do 'Fork('->puttext;
   (%getLongAt @@S) -> attToProcessor -> my_tid;
   ')'->put
   
