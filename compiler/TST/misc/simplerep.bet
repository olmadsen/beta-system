origin '~beta/basiclib/v1.5/betaenv';
(*
 * Generates a large amount of objects and repetitions.
 * Use
 *      simplerep <foo
 * 
 * where foo is a  text file.
 * The file foo is read.
 * The lines are reverse 10000 times
 * The lines are then printed.
 * A certian points some object are saved in a global cash.
 * The gloabl cash is cleared at regular intervals. This should
 * generate objects that survive for some time in IOA and
 * later becomes garbage.
 * See also generalrep.
 *)
---program:descriptor---
(# word:
     (# t: @text;
        read:<
          (# ch: @char
          do t.clear; get->ch;
             L: (if keyboard.eos then ascii.nul->ch
                 else (if ch
                       // ' ' then
                       // '\n' then
                       else 
                          ch->t.put;
                          get->ch;
                          restart L
                if)if)
          exit ch
          #);
        print:<
          (#
          do t[]->puttext; ' '->put;
          #);
     #);
   line:
     (# words: [0] ^word; top,n: @integer;
        read:<
          (# ch: @char
          do (if (top+1->top) > words.range then
                 words.range -> n;
                 (if n=0 then 10 -> words.extend
                  else n -> words.extend
                 if);
                 (for i: (n,10) -> max  repeat &word[] -> words[n+i][] for)
             if);
             (if (words[top].read -> ch) = ' ' then restart read if);
          exit ch (* ch = '\n' or ch = eos *)
          #);
        reverse:
          (#
          do
          #);
        copy:<
          (# l: ^|line
          enter l[]
          do l.words->words; l.top->top;
             l[]->cash.saveC;
          #)             
     do 2->words.extend;
        (for i: 2 repeat &word[] -> words[words.range-i+1][] for);
        
        '***'->words[top+1->top].t.append;
        'end'->words[top+1->top].t.append;
        (for i: top repeat words[i].print for);
        newline;
     #);
   document: 
     (# lines: [0] ^ | line; top,n: @integer;
        read:<
          (#
          do (if (top+1->top) > lines.range then
                 lines.range-> n;
                 (if n=0 then 10 -> lines.extend
                  else
                     n -> lines.extend
                 if);
                 (for i: (n,10) -> max repeat &|line[] -> lines[n+i][] for)
             if);
             (if lines[top].read = '\n' then restart read if);
          #);
        print:<
          (#
          do (for i : top repeat lines[i] for);
          #);
        reverse:
          (# l: [top] ^ | line;
          do (for i: top repeat &|line[] -> l[i][] for);
             (for i: top repeat
                  lines[i].reverse;
             for);
             (for i: top repeat
                  lines[top-i+1][] -> l[i].copy
             for);
             l -> lines;
             this(reverse)[]->cash.saveI
          #);
     #);
   d: @document;
   cash: @ 
     (# c: [4] ^| object; ctop: @integer;
        saveC:
          (#  r: ^| object;
          enter r[]
          do (if (ctop+1->ctop) > c.range then c.range->c.extend if);
             r[]-> c[ctop][]
          #);
        i: [4] ^ object; itop: @integer;
        saveI:
          (#  r: ^ object;
          enter r[]
          do (if (itop+1->itop) > i.range then i.range->i.extend if);
             r[]-> i[itop][]
          #);
        clear:
          (#
          do 4->c.new; 4->i.new; 0->ctop->itop; 
          #)
     #);
   
do d.read;
   d.print;
   (for i: 10000 repeat 
        d. reverse; 
        (if (i mod 300) = 0 then 
            '*********************************************Clear: '->puttext;
            i->putint; newline;
            cash.clear 
        if)
   for);
   d.print
#)
  
