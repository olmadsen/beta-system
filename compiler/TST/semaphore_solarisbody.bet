ORIGIN 'basicsystemenvbody';

--lib:attributes--
(* Interface to solaris libthread semaphores *)

(* Semaphore representation *)
sema_t:
  (#    count: @integer; (* semaphore count *)
        type:  @integer;
        pad1_1: @int64u; (* reserved for a mutex_t *)
        pad1_2: @int64u; 
        pad1_3: @int64u; 
        pad2_1: @int64u; (* reserved for a cond_t *)
        pad2_2: @int64u; 
  #);

(* Semaphore types *)
USYNC_THREAD:
  (* The semaphore can be used to synchronize
   * threads  in  this process, only.  arg is
   * ignored.
   *)
  (# exit 0 #);
USYNC_PROCESS: 
  (* The semaphore can be used to synchronize
   * threads   in   this  process  and  other
   * processes.  Only one process should ini-
   * tialize the semaphore.  arg is ignored.
   *)
  (# exit 1 #);

(* Semaphore functions *)
sema_init: external
  (# sp_addr: @integer;
     count: @integer;
     type: integer;
     dummy_arg: @integer;
     status: @integer;
  enter (sp_addr, count, type, dummy_arg)
  exit status
  #);
sema_destroy: external
  (# sp_addr: @integer;
     status: @integer;
  enter sp_addr
  exit status
  #);
sema_wait: external
  (# sp_addr: @integer;
     status: @integer;
  enter sp_addr
  exit status
  #);
sema_trywait: external
  (# sp_addr: @integer;
     status: @integer;
  enter sp_addr
  exit status
  #);
sema_post: external
  (# sp_addr: @integer;
     status: @integer;
  enter sp_addr
  exit status
  #);

(* Error codes *)
EINVAL:
  (* Invalid argument *)
  (# exit 22 #);
EFAULT:
  (* sp or arg points to an illegal address. *)
  (# exit 14 #);
EINTR:
  (* The wait was interrupted by a signal or fork() *)
  (# exit 4 #);
EBUSY:
  (* The semaphore pointed to by sp has a zero count *)
  (# exit 16 #);


--SemaphoreInit:dopart--
do (if (@@private.sema, count, USYNC_THREAD, 0) -> sema_init
    // EINVAL then 
       'SemaphoreInit: Invalid argument' -> screen.putline 
    // EFAULT then 
       'SemaphoreInit: sp or arg points to an illegal address' -> screen.putline
   if)
--SemaphoreP:dopart--
do (if @@private.sema -> sema_wait
    // EINVAL then 
       'SemaphoreP: Invalid argument' -> screen.putline 
    // EFAULT then 
       'SemaphoreP: sp or arg points to an illegal address' -> screen.putline
    // EINTR then
       'SemaphoreP: The wait was interrupted by a signal or fork()' -> screen.putline
   if)
--SemaphoreV:dopart--
do (if @@private.sema -> sema_post
    // EINVAL then 
       'SemaphoreV: Invalid argument' -> screen.putline 
    // EFAULT then 
       'SemaphoreV: sp or arg points to an illegal address' -> screen.putline
   if)
   
-- SemaphorePrivate:descriptor --
(# sema: @sema_t;
#)
  

