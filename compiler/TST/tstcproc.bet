ORIGIN 'tstenv';         
OBJFILE sun3 'sun3/ctstcproc.o'
        sun4 'sun4/ctstcproc.o'
	hp	'hp/ctstcproc.o'
	apollo	'apollo/ctstcproc.o'
	mac	'macobj/ctstcproc.o'
	default	'$/ctstcproc.o'
[[
-----LIB:attributes-----
tstCProc:
(#
   (* Should test the External interface from Beta.
    * This fragment is dependent on ctstcproc.c, where all c functions
    * are defined.
    *)
   ident: External(# r,s:[1]@char enter r  exit s #);
   Q,P: [1]@char;

   giveArg:  External(# a,b,c,d:@char enter (a,b,c) exit d #);
   giveMe1Argument: giveArg(##);
   giveMe2Argument: giveArg(##);
   giveMe3Argument: giveArg(##);    
   doCallBack: External(# F: ##foo; ch: @ char enter F## do callC exit ch #);
   foo: External
     (* Note that a :@ char does NOT work. The compiler generates mov.b
      * to pop a from C stack
      *)
     (# a,b:@ integer; T: [1] @ char;
        x3,x4,x5,x6,x7,x8: @integer;
        r: [1]@char;;
     enter(a,T,x3,x4,x5,x6,x7,x8)
     do cExternalEntry;
	a->put; T->puttext; x8->put;
        'p'->b 
     exit b
     #);
   sumMany: External
     (# a,b,c,d,e,f,g,h,i,j,k: @integer
     enter(a,b,c,d,e,f,g,h,i,j)
     exit k
     #);
   exitk: (# ch: @char do 'k'->ch exit ch #)
do
     (* Test char repetition as enter/exit parameters to Externals.
      * Validating PackVarTextToC (on enter) and CopyCtext (on exit)
      *)  
   'abcdefg' -> Q;
   Q -> ident -> P;          
   (if Q.range//P.range then 'a' -> put else '!' -> put if);
   (for i:P.range repeat  
      (if Q[i]//P[i] then 'a'+i -> put else '!' -> put if)
   for);
   (* Test the order of the parameters. *)
   ('i','!','!') -> giveMe1Argument -> put;
   ('!','j','!') -> &giveMe2Argument -> put;
   ('!','!',exitk) -> giveMe3Argument -> put;
   foo##->doCallBack->put; 
   (* call back from C of ('l','mn','!','!','!','!','!','o')->foo;*)    
   (if (1,2,3,4,5,6,7,8,9,10)->sumMany//55 then 'q'->put
    else '!'->put
   if);

   (('a','!','!') -> giveMe1Argument,('!','r','!') -> giveMe2Argument,
   ('!','!','g') -> giveMe3Argument)->giveMe2Argument->put;
   
   ('!','!','s') -> giveMe3Argument -> put;
   'tuvwxyz0123456789'->puttext
#)
---]]


