ORIGIN 'tstenv'; (*'~beta/basiclib/v1.6/betaenv';*)            
BUILD sun4s '$$/COMaccountUser.o' 'COMaccountUser.c' '\$CC -g -c -o $0 $1';
---lib:attributes--- 
(* An example of a COM object written in BETA and passed to a C function *)
Account: COM 
  (# init:<  
       (# name: [1] @char; initialAmount: @integer
       enter(name,initialAmount)
       do name -> this(Account).name;
          'Init account for '-> puttext;
          name -> puttext; 
          '\n\twith: \t'->puttext;
          initialAmount -> print;
          initialAmount -> balance; 

       exit balance
       #);     
     deposit:<  
       (# amount: @integer   
       enter amount   
       do 'Deposit: \t'->puttext; 
          amount->print;
          balance + amount -> balance 
       exit balance
       #);       
     withdraw:< 
       (# amount,id: @integer     
       enter amount 
       do 'Withdraw: \t'->puttext;
          amount -> print;
          balance - amount -> balance  
       exit balance  
       #); 
     display:< 
       (* operation with no enter/exit parameters *)
       (# 
       do 'Balance: \t'->puttext; balance -> print 
       #);
     getBalance:<
       (* operation with no enter parameter *)
       (# b: @integer
       do  balance -> b
       exit b
       #);
     print: (# a : @integer enter a do a -> putint; newline #);
     name: @text; 
     balance: @integer;
  #);
PutAccount: external(# R: ^Account enter R[] #); 

--program:descriptor--- 
(# R: ^Account; am: @integer  
do &Account[] -> R[];    
   ('John',5000) -> R.init; 
   1200 -> R.deposit -> am;  
   2100 -> R.withdraw -> am; 
   R.display ; (* if no enter param, no call is made??*)
   R[] -> PutAccount;  
   'Final balance:\t'-> puttext;
   R.getBalance -> putint; newline ;
#) 


