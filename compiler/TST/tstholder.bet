ORIGIN 'tstenv';  
INCLUDE 'tstlib';
---lib:attributes---  
test: (#exit false #); 
(* Using: 
 *    holder: (# adr: @integer #)     
 *)
intHolder: holder  
  (# value: @integer;
     isNone: (# exit value = 0 #);
  enter value
  exit value          
  #);
refHolder: holder    
  (# type:< COM;
     ref: ^type;
  enter ref[] 
  exit ref[]
  #); 
tstHolder: 
  (# myClass: COM
       (# foo:<
            (# n: @integer; S: ^intHolder;       
            enter(n,S[])
            do (if test then
                   n -> putint; ' ' -> put; S -> putint; ' '->put;
               if); 
               (if S = 111 then 'a' -> put else error if);
               n + S -> S; 
               (if test then S -> putint; newline; if)  
            #);
          bar:<
            (# n: @integer; R: ^myClassHolder        
            enter(n,R[]) 
            do (if test then 'myClass::bar'->putline if); 
               &myClass[] -> R; 
               n -> R.ref.kuk;
               (if test then 'myClass::bar:end'->putline if);
            #);    
          foo1:<   
            (# S: ^intHolder 
            enter S[] 
            do (if test then  S -> putInt; newline if);
               (if S = 444 then 'd' -> put else error if);
               111 + S -> S; 
            #);
          foo2:<
            (# n,a,b,c: @integer; S: ^myClassHolder
            enter(n,S[])
            do (* Call foo3, which instantiates a myClass and return via Holder (two level holder writeback) *)
               (n,S[]) -> foo3;
            #);
          foo3:<
            (# n: @integer; S: ^myClassHolder
            enter(n,S[])
            do (* Instantiate a myClass and return via Holder *)
               &myClass[] -> S;
               n -> S.ref.kuk;
            #);
               
          kuk: @integer   
       #);  

   TTTT: COM  
     (# foo1:<
          (# S: ^intHolder  
          enter S[]
          do (*n -> putint; ','->put; S -> putint; newline;*)
             (if S.isNone then
                 'i' -> put 
              else
                 (if S = 1010 then 'h' -> put else error if);
             if)
          #);
        foo2:<        
            (# S: ^intHolder; n: @integer
            enter (n,S[])
            do (if S.isNone then
                   'k'->put
                else 
                   (if S = 101 then 'j' -> put else error if);
               if)
            #);
     #);

     R: @myClass;
     myClassHolder: refHolder(# type::myClass #);         
     
     Holders: @COM
       (# Q: @intHolder; 
          P: @myClassHolder 
       #);
      
     (* Q: @intHolder;
      * P: @myClassHolder;
      *)
  do 111->Holders.Q;
     (if test then 'main 1:'->putline if);        
     (11,Holders.Q[]) -> R.foo; (* 'a' *) 
     (* The above fails if GC during AlloI in foo G-part because address of 
      * integer inside Q is pushed on stack .
      * NO not anymore: The COM object around Q ensures that Q is 
      * allocated in AOA and thus never moved.
      *)
       
     (if test then                
         'main 2:'->putline;
         Holders.Q -> putint; newline;
     if);  
     (if Holders.Q = 122 then 'b' -> put else error if);
     
     (333,Holders.P[]) -> R.bar;   
     (if test then Holders.P.ref.kuk -> putint; newline; if);
     (if Holders.P.ref.kuk = 333 then 'c' -> put else error if);
     
     444 -> Holders.Q; 
     Holders.Q[] -> R.foo1;
     (* Q -> putint; newline;   *)
     (if Holders.Q = 555 then 'e' -> put else error if);
     
     (* Call foo2 which calls foo3 *)
     (# x: @char; 
     do (666,Holders.P[]) -> R.foo2;
     #);
     (# x: @char; 
     do (if Holders.P.ref.kuk = 666 then 'f' -> put else error; Holders.P.ref.kuk->putint; if);
     #);
     
     (* call foo3 directly *)
     (# S: ^ myClassHolder
     do (*&myClassHolder[] -> S[] // datpete:2003/02/20: Holders MUST be in COM when given to external that calls back! *)
        Holders.P[] -> S[]; 
        (777,S[]) -> R.foo3;
        (if S.ref.kuk = 777 then 'g' -> put else error if);
     #);
     
     (# R: @TTTT;
        (* X: @intHolder; // datpete:2003/02/20: Holders MUST be in COM when given to external that calls back! *)
        X: ^intHolder;
     do Holders.Q[] -> X[];
        1010 -> X; 
        X[] -> R.foo1;
        none -> R.foo1;
        101 -> X;
        (5,X[]) -> R.foo2; 
        (6,none) -> r.foo2;
     #);

     'l' -> fill;
  #)
