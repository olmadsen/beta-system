ORIGIN 'tstenv'       ;
INCLUDE 'tstlib'
--lib:attributes---
TstVarPtn:
  (# T: (# do INNER #);       
     T1: T(# do ch->put #); ch: @char;
     T2: T(# do 'b'->put #);   
     
     F1,F2: ## T;
     S,S1: ^ T;
     
     X: @ T(# ss: @text do 'd'->put #);
     F3: ##Object;
     IntToInt: (# x,z: @Integer enter x do INNER exit z #);
     comp: 
       (# f,g: ##IntToInt; h: IntToInt(#do x->f->g->z #)
       enter(f##,g##)
       exit h##
       #);
     A: IntToInt(#do x+x->z #);
     B: IntToInt(#do x*x->z #);
     C: ##IntToInt; 
     
     TX: T(# do 'g'->put; INNER #);
     D: (# V:<TX; F: ##T; R: ^T do V##->F##; &F[]->R[]; R #);
     DD: D(# V::<(#do 'h'->put #)#);
     
     Y: @T(# #);
     YY: @
       (# X: @T(# #)
       do (if T##>=X## //true then '4'->put else '!'->put if)
       #)
  do 'a'->ch; T1##->F1##; T2##->F2##; F1;  F2;
     F1##->F2##; 'c'->ch;   F2;  
     '!'->ch; X##->F3##;
     F3;
     &F1[]->S[]; 'e'->ch; S;
     (A##,B##)->comp->C##;
     (if 5->C //100 then 'f'->put else '!'->put if);
     DD;
     
     (if S[] // S1[] // none then if);
     (if none // S[] then if);
     NONE->F1##;
     (for i: 4 repeat
          (if F1## 
           // none then 'i'->put; T1##->F1##
           // T1## then 'j'->put; T2##->F1##->F2##
           // F2## then 'k'->put; TX##->F1##
           else 'l'->put
     if)for);
     NONE->F1##;
     (for i:2 repeat
          (if none 
           // F1## then 'm'->put; T1##->F1## 
           // S[] then '!'->put
           else 'n'->put
     if)for);
     T1##->F1##; T##->F2##; 
     (F1##=T1##,true, 'o')->chkBool;
     (F1##=T2##,false,'p')->chkBool;
     (F1##<=T1##,true,'q')->chkBool;
     (F1##<T1##,false,'r')->chkBool;
     (T1##=T2##,false,'s')->chkBool;
     (T1##=T1##,true, 't')->chkBool;
     (T1##=F1##,true, 'u')->chkBool;
     (T1##>=F1##,true,'v')->chkBool;
     (T1##>F1##,false,'w')->chkBool;
     (F1##=F2##,false,'x')->chkBool;
     (F1##<=F2##,true,'y')->chkBool;
     (F1##<F2##, true,'z')->chkBool;
     (F1##>=F2##,false,'0')->chkBool;
     (F1##<>F2##,true,'1')->chkBool;
     (T1##<>T2##,true,'2')->chkBool;
     
     (if Y##<= T## //true then '3'->put else '!'->put if);
     yy;
     &T2[]->S[]; (* none->s[]; - should give ref. is none error on S## *)
     (if S##<=T2## // true then '5'->put else '!'->put if);

     (# P: (#  do '6'->put; suspend; '7' ->put #);
        Pcr: ^|P;
     do &|P[]->Pcr[];
        Pcr;    
        (if Pcr##// P## then
            Pcr; '8'->put 
         else Pcr; '!'->put
        if)
     #);
     '9'->puttext;
  #)
     
     
