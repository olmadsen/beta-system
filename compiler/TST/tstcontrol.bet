ORIGIN 'tstenv';
INCLUDE 'tstlib'
---LIB:attributes---    
TstFor:
  (#
  do (for i: 26 repeat ('a'-1+i)->put for); 
     (for i: -1 repeat '!'->put for);
     tstBreak 
  #);
TstIf:
  (# a,b: @integer;
     foo: (# do exit b #);
     foo2: (# x: @integer do 123->x exit x#);
     foo3: @ foo2
  do 3->a; 2->b;
     (for i: 10 repeat
	  (if i
  	   // 1 then 'a'->put
	   // 2 then 'b'->put
	   // a then 'c'->put
	   // a+1 then 'd'->put
	   // a+b then 'e'->put
	   // 6 then 'f'->put; 7->b
	   // foo then 'g'->put; 100->a
	   else
	  if)
     for);
     (for i: 2 repeat
          (if (i=1) 
	   // true then 'h'->put
	   // false then 'i'->put
     if)for);
     (for i: 2 repeat
          (if (i=1) // true then 'j'->put  
	   else 'k'->put
     if)for);
     (if true // (a-93)=b then 'l'->put if);
     (if foo2// foo3 then 'm'->put if);
     (for i: 100 repeat
          (if i<14 then 'n'-1+i->put
	   else 
              (if i<24 then '0'+i-14->put if)
     if)for);
  #);
tstBreak:
  (# ch: @char; first: @boolean;
     foo: (# boo: @boolean
          enter boo
          do ch->put;
             (if boo then 
                 ch+1->ch;
                 false->boo;
                 restart foo
              else
                 ch+1->ch->put;
                 leave foo;
                 '!'->put
             if)
          #);
     bar: (# do inner #);
     fooBar: bar
       (# do (if ch='7' then ch+1->ch->put; restart fooBar
              else ch+1->ch->put; leave fooBar  
       if)#)
  do L: (for i:4 repeat
             '0'-> put;
             leave L;
             '!'->put
        for);
     '1'->ch; true->first;
     loop:
       (for i:4 repeat
            ch->put;
            (if first then false->first; '2'->ch; restart loop 
             else leave loop 
       if)for);
     N:
       (#  p: (# do '4'->put; leave N #)
       do
          '3'->put;
          p;
          '!'->put;
          (# do leave N #);
          '?'->put;
       #);
     '5'->ch;
     true->foo;
     fooBar
  #);
tstCase:
  (# six:(#exit 6 #); three:(#exit 3 #); ten: (#exit 10 #)
  do (for i: 12 repeat          
          (if i
           //5 then 'e'->put;
           // six then 'f'->put
           //8 then 'h'->put
           //2 then 'b'->put
           // three then 'c'->put
           //9 // ten //11 then
              (if i
               //9 then 'i'->put
               //10 then 'j'->put
               //11 then 'k'->put 
              if)
           else 
              (if i
               //1 then 'a'->put
               //4 then 'd'->put
               //7 then 'g'->put
               //12 then 'l'->put
               else '!'->put
              if)
          if);
     for);
     (for i: 15 repeat
          (if i
           //4 then (if i=4 then 'm'->put else '!'->put if);
           //6 then 'o'->put
           //5 then 'n'->put
           //9 then 'r'->put
           //8 then 'q'->put
           //7 then 'p'->put
           //10 then 's'->put
           //11 then 't'->put
           else (if i>11 then 'u'+i-12->put if) (* 'u'-'x' *)
          if);
     for);
     (for i: 12 repeat
          (if i-6
           //-4 then 'z'->put
           //-3 then '0'->put
           //-2 then '1'->put
           //-1 then '2'->put
           //0 then '3'->put
           //1 then '4'->put
           //2 then '5'->put
           //3 then '6'->put
           else (if i
                 // 1 then 'y'->put
                 // 10 then '7'->put
                 // 11 then '8'->put
                 // 12 then '9'->put
                 else '!'->put
                if)
          if);
     for)
  #)



