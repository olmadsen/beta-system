ORIGIN 'tstenv'   
---lib:attributes---
IID: data
  (# id1,id2,id3,id4: @integer (* not the right format *)
  #);
(***************************************
 * The general COM IUnknown Interface:
 * To be used when importing COM objects
 ****************************************)
IUnknown: COM   
  (# IQuery: 
       (# theIID: ^IID;  
          ppv: @ (*RefToComRef2; *) Integer;
          H: @integer;
       enter(theIID[],ppv)
       do INNER
       exit H 
       #);
     QueryInterface:< IQuery;
     AddRef:< (# do refCount + 1 -> refCount; INNER #);
     Release:< (# do refCount - 1 -> refCount; INNER #);
     refCount: @integer
  #);

(*****************************************
 * Subpattern UNknown of IUnknown:
 * To be used for implementing COM objects
 ******************************************)
Unknown: IUnknown
  (# Query: IQuery
       (# ppvObj: ^IUnknown;
          returnPPV:
            (#
            do ' - ppv adr: ' -> puttext;
               (%getLongAt (@@ppvObj)) -> putint;
               (%getLongAt (@@ppvObj)) %putLongAt (ppv);
            #);
       do '(QueryInterface: '->puttext;  
          theIID.id1 -> putint; ' '-> put;
          theIID.id2 -> putint; ' '-> put;
          theIID.id3 -> putint; ' '-> put;
          theIID.id4 -> putint; ' '-> put;
          INNER ; 
          returnPPV;
          ')\n'->puttext
       #);
     QueryInterface::< Query
  #);

(*************************************************************
 * An experimental nicer IUnknown, but NOT real COM
 * ***********************************************************)

IUnknownX: COM   
  (# Query: 
       (# IfId: @integer; I: ^IUnknownX  
       enter IfId 
       do 'Query interface: ('->puttext; INNER ; ')\n'->puttext
       exit I[] 
       #);
     QueryInterface:< Query;
     AddRef:< (# do refCount + 1 -> refCount; INNER #);
     Release:< (# do refCount - 1 -> refCount; INNER #); 
     refCount: @integer
  #);

