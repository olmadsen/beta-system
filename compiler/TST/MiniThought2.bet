ORIGIN 'tstenv';
INCLUDE 'IUnknown';
BUILD sun4s 
      '$$/MiniThoughtUser2.o' 'MiniThoughtUser2.c' '\$CC -g -c -o $0 $1';
(* BETA implementation of COM object passed to external C.
 * Implements MiniThought2 that supports the 2 interfaces
 * calculator and displayer
 *)
---program:descriptor---
(# calculator: IUnknown2
     (# add:< (# n: @integer enter n do INNER #); 
        sub:< (# n: @integer enter n do INNER #);
     #);
   displayer: IUnknown2
     (# displayEnglish:< (# do inner #);
        displayDanish:< (# do inner #);
     #);  

   MiniThought: IUnknown2
     (# val: @integer;
        MiniThoughtCalc: calculator   
          (# QueryInterface::QIF;
             add:: 
               (# 
               do 'Add: '->puttext; n ->putint; newline; 
                  val + n -> val 
               #);
             sub:: 
               (# 
               do 'Sub: '->puttext; n ->putint; newline; 
                  val - n -> val 
               #);
          #);
        MiniThoughtDisplay: displayer
          (# QueryInterface:: QIF;
             displayEnglish:: 
               (#do 'Value: ' ->puttext; val ->putint; newline #);
             displayDanish:: 
               (#do 'Vaerdi: ' ->puttext; val ->putint; newline #);
          #);
        QIF: Query
          (# 
          do (if theIID.id1
              // 1 then 
                 'Calculator interface'->puttext;
                 &MiniThoughtCalc[] -> ppvObj[]
              // 2 then 
                 'Displayer interface'->puttext;
                 &MiniThoughtDisplay[] -> ppvObj[]
             if);                 
          #);
        QueryInterface :: QIF
     #);
 
   PutMiniThought: external(# R: ^MiniThought enter R[] #);
do &MiniThought[] -> PutMiniThought
#)



