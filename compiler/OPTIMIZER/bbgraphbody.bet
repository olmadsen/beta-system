ORIGIN 'bbgraph';
-- AppendNode: DoPart --
do
   nodeCnt+1->nodeCnt->i.index->nodetop;
   i[]->nodelist[nodeCnt][];
   (if nodeCnt >= nodelist.range then nodeCnt->nodelist.extend if);
   INNER;  

-- EdgeInsert: DoPart --
do
   nodeList[connectFrom].succTop+1->nodeList[connectFrom].succTop;
   connectTo->nodeList[connectFrom].succI[nodeList[connectFrom].succTop];
   (if nodeList[connectFrom].succTop >= nodeList[connectFrom].succI.Range then
       nodeList[connectFrom].succTop->nodeList[connectFrom].succI.extend; 
       nodeList[connectFrom].succTop->nodeList[connectFrom].succA.extend; 
   if);
   EdgeCount+1->edgeCount;
   (* analysis is placed after in-information *)
   INNER ;
     

-- DoubleEdgeInsert: DoPart --
do 
   nodeList[connectTo].predTop+1->nodeList[connectTo].predTop;
   connectFrom->nodeList[connectTo].predI[nodeList[connectTo].predTop];
   (if nodeList[connectTo].predTop >= nodeList[connectTo].predI.range then
       nodeList[connectTo].predTop->nodeList[connectTo].predI.extend; 
   if);
   INNER ; 
     
--doubleremodenode:dopart--
do (* 'Removing node %d\n'->putformat(# do inx->d #); *)
   nodelist[inx][]->remnode[];
   (for i:remnode.predTop repeat
      nodelist[remnode.predI[i]][]->othnode[];
      (remnode.succI,remnode.succTop
      ,othnode.succI,othnode.succTop,
      inx)->mergelists->othnode.succI;
      othnode.succI.range->othnode.succTop
   for);
   (for i:remnode.succTop repeat
      nodelist[remnode.succI[i]][]->othnode[];
      (remnode.predI,remnode.predTop
      ,othnode.predI,othnode.predTop,
        inx)->mergelists->othnode.predI;
      othnode.predI.range->othnode.predTop
   for);
   none->nodeList[inx][];
   INNER;
-- doublemoveedge:dopart --
do
   1->i;
   l:(if nodelist[tn.predI[i]][]=fn[] then
         (if i <> tn.predtop then
             tn.predI[tn.predtop]->tn.predI[i];
         if);
         tn.predtop - 1 -> tn.predtop;
         leave l;
      else
         i + 1 -> i;
         restart l;
     if);
   (if fn.succI[1] = tn.index then
       nn.index -> fn.succI[1];
    else
       nn.index -> fn.succI[2];
   if);
   1->nn.predI.extend;
   nn.predtop + 1 -> nn.predtop;
   fn.index -> nn.predI[nn.predtop];
-- CodeRemoveNode:dopart --
do
   (if remnode.next <> 0 then
       remnode.next->nodelist[remnode.prev].next;
   if);
   (if remnode.prev <> 0 then
       remnode.prev->nodelist[remnode.next].prev;
   if);
   nodetop - 1 -> nodetop;
   INNER;
-- CodeAppendNode:DoPart --
do
   nodecnt->nodelist[nodeCnt-1].next;
   nodecnt-1->i.prev;
   INNER;
--BBEdgeInsert:DoPart--
do
   (if nodeList[connectFrom].succTop >= nodeList[connectFrom].killA.Range then
       nodeList[connectFrom].succTop->nodeList[connectFrom].killA.extend; 
   if);
   graphsize + 1 -> graphsize;

   
   
      








