ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'bbgraph' 'carriers';
BODY 'analyzerbody';
-- lib: Attributes --
Forward:Object(# #);
Backward:Forward(# #);
Analyzer:
  (# <<SLOT AnalyzerLib:attributes>>;
     direction:<Forward;
     carrier:<Information;
     init:< (* used to initialize new carriers before combining with older *)
       (# c:^carrier;
       do 
          &carrier[]->c[];
          inner
       exit c[]
       #);
     init_start:< (* used to initialize the in carrier for the first node *)
       (# c:^carrier;
       do     &carrier[]->c[];inner;
       exit c[]
       #);
     initialize:<Object;
     combine:<
       (# left,right:^carrier;
          result:^carrier;
       enter (left[],right[])
       do (* none U b = b, b U none = b, none U none = none *)
          (if left[]=none then right[]->result[]; 
              leave combine if);
          (if right[]=none then left[]->result[]; 
              leave combine if);
          INNER
       exit result[]
       #);
     widening:<
       (# left,right:^carrier;
          result:^carrier;
       enter (left[],right[])
       do INNER
       exit result[]
       #);
     narrowing:<
       (# left,right:^carrier;
          result:^carrier;
       enter (left[],right[])
       do INNER
       exit result[]
       #);
     equal:<
       (# result:@boolean;
          left,right:^carrier;
       enter (left[],right[])
       do INNER
       exit result
       #);
     flowFunction:@flowFunctionType;
     flowFunctionType:<
       (# current:^graphType.realnode;
          in,result:^carrier;
          edgeval:@boolean;
       enter (current[],edgeval,in[])
       do
          INNER;
       exit result[]
       #);
     BBflowFunction:@BBflowFunctionType;
     BBflowFunctionType:<
       (# current:^graphType.node;
          in,result:^carrier;
          edgeval:@boolean;
       enter (current[],edgeval,in[])
       do
          INNER;
       exit result[]
       #);
     firstNode:<(# n:^doublegraph.node do INNER exit n[] #);
     graphType:<BBgraph;
     graph:^graphType;
     temporer:@integer;
     thisAnalysis:^analysisinformation;
     AnalyseGraph:
       (# enter graph[]
       <<SLOT AnalyseGraph:dopart>>
       #);
     AnalyseBBGraph:
       (# enter graph[]
       <<SLOT AnalyseBBGraph:dopart>>
       #);
     AnalyzerP:@<<SLOT analyzerPrivate:descriptor>>;
  #);

fastAnalyzer:
  (# <<SLOT fastAnalyzerLib:attributes>>;
     direction:<Forward;
     carrier:<Information;
     
     init:< (* used to initialize new carriers before combining with older *)
       (# c:^carrier;
       do inner
       exit c[]
       #);
     init_start:< (* used to initialize the in carrier for the first node *)
       (# c:^carrier;
       do inner
       exit c[]
       #);
     combine:<
       (# leftkill,leftgen,rightkill,rightgen:^carrier;
          resultkill,resultgen:^carrier;
       enter (leftkill[],leftgen[],rightkill[],rightgen[])
       do           
          INNER
       exit (resultkill[],resultgen[])
       #);
     widening:<
       (# left,right:^carrier;
          result:^carrier;
       enter (left[],right[])
       do INNER
       exit result[]
       #);
     narrowing:<
       (# left,right:^carrier;
          result:^carrier;
       enter (left[],right[])
       do INNER
       exit result[]
       #);
     equal:<
       (# result:@boolean;
          left,right:^carrier;
       enter (left[],right[])
       do INNER
       exit result
       #);
     
     genkillset:<
       (#
          n:^graph.realnode;
          result:^carrier
       enter n[]
       do 
          inner
       exit result[]
       #);
     gengenset:<
       (#
          n:^graph.realnode;
          result:^carrier
       enter n[]
       do
          inner
       exit result[]
       #);
     killset:@genkillset;
     genset:@gengenset;
     killfunction:<
       (# 
          X:^carrier;
          remove:^carrier;
          result:^carrier;
       enter
          (X[],remove[])
       do
          inner;
       exit result[]
       #);
     genfunction:<
       (# 
          X:^carrier;
          add:^carrier;
          result:^carrier;
       enter (X[],add[])
       do 
          inner;
       exit (result[])
       #);
     kill:@killfunction;
     gen:@genfunction;
     killcombine:<
       (#
          kill1,kill2,result:^carrier;
       enter (kill1[],kill2[])
       do
          INNER;
       exit result[]
       #);
     firstNode:<(# n:^graph.node do INNER exit n[] #);
     graphType:<BBgraph;
     graph:^graphType;
     thisAnalysis:^AnalysisInformation;
     AnalyseGraph:
       (# enter graph[]
       <<SLOT fastAnalyseGraph:dopart>>
       #);
     initialize:<Object;
     fastAnalyzerP:@<<SLOT fastanalyzerPrivate:descriptor>>;
     
  #);

peepholer:
  (# 
     init:<
       (# 
       do
          0->nofuncts;
          inner;
       #);
     changed:@boolean;
     graphtyp:<BBgraph;
     thegraph:^graphtyp;
     nofuncts:@integer;
     
     peepfunction:
       (# 
          n:^thegraph.node;
          scannode:
            (# rr:^thegraph.realnode;
            do
               (if n[]<>none then
                   n.realgraph[]->rr[];
                   l:(if rr[]<>none then
                         INNER;
                         rr.next[]->rr[];
                         restart l
                     if);
               if);
            #);
       enter n[]
       do
          inner;
       #);
     addfunction:
       (# np:^peepfunction;
       enter np[]
       do
          nofuncts+1->nofuncts;
          np[]->peepfunctions[nofuncts][];
          (if nofuncts = peepfunctions.range then
              nofuncts -> peepfunctions.extend;
          if)
       #);
     peepfunctions:[16]^peepfunction
  enter thegraph[]
  do
     init;
     true->changed;
     l:(if changed then
           false->changed;
           (for i:nofuncts repeat
                thegraph.codescan
                (# 
                do
                   curr[]->peepfunctions[i];
                #);
           for);
           restart l;
        else
           leave l;
       if);
     
  #);






