ORIGIN '~beta/basiclib/v1.5/betaenv';
---lib:attributes--
libs:
  (#
     getEntry:
       (# G: ^AST.fragmentGroup; libName: ^text; 
          libEntry: ^ Library
       enter(G[],libName[])
       <<SLOT libsgetentry:doPart>>
       exit libEntry[]
       #);
     
     scan: (# current: ^Library <<SLOT libsscan:doPart>> #);
     
     Library:
       (* defines a library.
        * name: the name of the library.
        * group: the group defining the library.
        * name=group.fullname
        *)
       (# name: ^text;  group: ^ AST.fragmentGroup;
          addmember:
            (# memName: ^text; inx: @integer
            enter memName[]
            <<SLOT libsmembername:doPart>>
            #);
          private: @<<SLOT libsmemberprivate:descriptor>>
       #);
     private: @<<SLOT libsprivate:descriptor>>
  #)

---libsprivate:descriptor--
(# lib: [10] ^Library; top: @integer
#)
--libsgetentry:doPart--
do find:
     (for i: private.top repeat
        (if private.lib[i].name[]->libName.equalNCS then
            private.lib[i][]->libEntry[];
            leave find
     if)for);
   (if libEntry[]//NONE then (* add entry *)
       (if G.fullName[]->libName.equalNCS  then
           '\nMain group in library being defined'->puttext;
       if);
       (if (private.top+1->private.top)>private.lib.range  then
           private.lib.range->private.lib.extend
       if);
       &Library[]->libEntry[]->private.lib[private.top][];
       libName[]->libEntry.name[]
    else
         (if G.fullName[]->libName.equalNCS then
             '\nNo entry and NOT main group!'->puttext;
         if) 
   if);

--libsmemberprivate:descriptor--
(# mem: [10] ^text; top: @integer
#)
--libsmembername:dopart--
do find:
     (for i: private.top repeat
          (if private.mem[i][]->memName.equalNCS then
            i->inx;
            leave find
     if)for);
   (if inx//0 then (* add entry *)
       (if (private.top+1->private.top)>private.mem.range then
           private.mem.range->private.mem.extend
       if);
       memName[]->private.mem[private.top][]
   if);
   
