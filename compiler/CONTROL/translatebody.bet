ORIGIN 'translate';
---initTarget:descriptor---
(* Set the variables TargetDirectory and asmLink.jobFileMachine.
 * The following strategy is used.
 * If the attributes targetMach and jobfileMach are NONE,
 * the variable asmLink.jobfileMachine is set to NONE
 * (this signals that the jobfile should be executed on THIS machine).
 * If targetMachine is NONE and jobfileMachine is not,
 * then jobfileMachine is assigned to asmLink.jobfileMachine.
 * If targetMachine is <> NONE (i.e. another machine than THIS
 * machine is the target) then
 * - if jobfileMachine is<>NONE, it is assigned to asmlink.jobfileMachine
 * - else a default value for asmlink.jobfileMachine is looked up in
 *   the global Mjolnerrc.
 *) 
(# found, setJobFileMachine: @boolean;
do (if targetMach[] = NONE then 
       ensemble.hostMachine(*Type*)->targetMachine;
       jobFileMach[]->asmlink.jobFileMachine[];
    else (if jobFileMach[] = NONE then true->setJobFileMachine
          else jobFileMach[]->asmlink.jobFileMachine[]
         if);
       targetMach->targetMachine;
   if);
   (if verboseLevel < verboseLevel.nothing then
       'Target machine type '->infostream.puttext;
       TargetMachine[]->infostream.puttext;
       (if 'nti'->TargetMachine.equal then
           '('->infostream.put;
           common.sdk.name[]->infostream.putText;
           ')'->infostream.putLine
        else infostream.newline
       if);
   if);
   (* Look for a global property : machine.<TargetMachine>: ... *)
   (GlobalOnly,'machine',TargetMachine)->ScanMjolnerProps
   (# count: @integer;
   do propVal.scanTokens
      (#
      do count+1->count;
         (if true
          //(count=1) then
             token->targetDirectory;
          //((count=2) and setJobFileMachine) then
             token[]->asmlink.jobFileMachine[];
         if);
      #);
      (if found then
          (# T: @text
          do 'Multiple defined property, machine.'->T.puttext;
             TargetMachine[]-> T.putline;
             T[] -> SystemError
          #)
      if);
      true->found;
   #);
   (if not found then
       (# T: @text
       do 'Unknown target machine type: '->T.puttext;
          TargetMachine[]-> T.putline;
          T[] -> SystemError
       #)
   if);
   targetMachine[] -> common.SetTargetMachineId; (* -> targetMachId;*)
#)
---initTranslate:descriptor---
(#
do 
   65 -> SwitchOn;  (* Generate  BETA_DATA file for persistence *)
   66 -> SwitchOn;  (* Inner-dispatch as part of virtual-dispatch *)
   214 -> SwitchOn; (* No code is generated for empty G-parts *)
   (if common.switch[64] then 68 -> SwitchOn if);
   depGraph.Init;
   initTarget;
   common.targetMachine -> depGraph.targetMachine;
   common.targetMachine -> depGraph.targetDirectory;
   asmLink.TX.clear; asmLink.Libs.clear;
   &text[]->asmLink.fSwitch[]; &text[]->asmLink.BetaRun[];
   (if betarunSwitch[]<>NONE then betarunSwitch->asmLink.betaRun if);
   &text[]->asmLink.ObjName[]; (*  to assure NOT none *)
#)

---translateDoBody:descriptor---
(# oldSW6: @boolean;
do init;
   (* The hasProgramSlot code in controlbody may override switch[6];
    * here we make sure to restore the original value
    *)   
   common.switch[6] -> oldSW6;
   rootFragment[] -> depGraph.xcontroller;
   (if not (common.switch[5] or common.switch[24]) then asmLink if);
   oldSW6 -> common.switch[6]
#)
