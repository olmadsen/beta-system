ORIGIN 'com';
LIB_ITEM 'betacompiler';
--com_betaenv_private:descriptor--
(# _group, _form: ^text;
#)
--com_betaenv_group:dopart--
do (if private._group[]=NONE then 
       'betaenv.groupname: warning: using default' -> screen.putline;
       'betaenv' -> g[]; (* default if not set *)
    else
       private._group.copy -> g[];
   if);
--com_betaenv_form:dopart--
do (if private._form[]=NONE then 
       'betaenv.formname: warning: using default' -> screen.putline;
       'betaenv' -> f[]; (* default if not set *)
    else
       private._form.copy -> f[];
   if);
--com_betaenv_set:dopart--
do (if not is_set then
       f[] -> private._form[];
       g[] -> private._group[];
       true -> is_set;
       (if not switch[188] then
           '\nbetaenv.set: group='->screen.puttext;
           g[] -> screen.puttext;
           ', form='->screen.puttext;
           f[] -> screen.putline;
       if)
   if);
   
--BetaArrayLocation:dopart--
do (if false then
       (* Use BetaArray.{java,cs} generated class *)
       'BetaArray' -> value[]
    else
       (* Use compiler generated class *)
       betaenv.groupname -> value[];
   if)
   
--StructureLocation:dopart--
do (if false then
       (* Use Structure.{java,cs} generated class *)
       'Structure' -> value[]
    else
       (* Use compiler generated class *)
       betaenv.groupname -> value[];
   if)
--ComponentLocation:dopart--
do (if true then
       (* Use Component.{java,cs} generated class *)
       'Component' -> value[]
    else
       (* Use compiler generated class *)
       betaenv.groupname -> value[];
   if)
   
---CompClass:doPart---
do 'Component' -> value[]
   
---doMethod:doPart---
do 'xdo' -> value[]
   
---enterMethod:doPart---
do 'enter' -> value[]
   
---exitMethod:doPart---
do 'exit' -> value[]
   
--nameManglingCh:doPart--
do '$' -> ch
---comPrivate:descriptor--
(* Used keys:
 * 1: 
 * 2:
 * 3:
 * 4:
 * 5:
 *)
(# keys: [10] @integer;
   top: (#exit 6 #)
#)
--comCount:doPart--
do priv.keys[key] + 1 -> priv.keys[key]
--comPrint:descriptor---
(# extra,sum: @integer
do '\nThe following V-entries are generated:\n'->tracestream.puttext;
   (for i: priv.top repeat
        i-> tracestream.putint; ':'->tracestream.put;
        (if i
         // 1 then 'No V-entry      : ' -> tracestream.puttext;
            0 -> extra
         // 2 then 'Simple V-entry  : ' -> tracestream.puttext;
            4 -> extra
         // 3 then 'Dynamic V-entry : ' -> tracestream.puttext;
            3 -> extra
         // 4 then 'General V-entry : ' -> tracestream.puttext;
            3 -> extra
         // 5 then 'No. of statAlloc: ' -> tracestream.puttext;
            1 -> extra
         // 6 then 'No. of dynAlloc : ' -> tracestream.puttext;
            -2 -> extra
         else
            'Unknown: ' -> traceStream.puttext
        if);
        priv.keys[i] -> tracestream.putint; 
        '\t'->tracestream.put; extra -> tracestream.putint;
        '\t'->tracestream.put; extra * priv.keys[i] -> tracestream.putint;
        sum + extra * priv.keys[i] -> sum;
        tracestream.newline;
        (if i 
         // 4 then
            'Total             : '->tracestream.puttext;
            priv.keys[1] + priv.keys[2] + priv.keys[3] + priv.keys[4]  
              -> tracestream.putint; newline; newline;
            'The split of allocations are:\n'->tracestream.puttext;
         // 6 then
            'Total             : '->tracestream.puttext;
            priv.keys[5] + priv.keys[6] -> tracestream.putint; 
            newline; newline
            
        if)
   for);
   'Code difference: '->tracestream.puttext; sum -> tracestream.putint;
   ' (longs) ' -> tracestream.puttext;
   4*sum -> tracestream.putint; ' (bytes)\n' ->tracestream.puttext;
#)

   
