ORIGIN 'system';
LIB_ITEM 'betacompiler'
--- translib: attributes ----
singleLinkedList:
  (#
     Insert:<
       (# E: ^elm;
       do &elm[] -> E[]; 
          INNER;
          head[] -> E.succ[]; 
          E[] -> head[];
       #);

     Scan:
       (# thisElm: ^Elm
       do head[] ->ThisElm[];
          L: 
            (if (thisElm[]<>NONE)  then
                INNER;
                thisElm.succ[] -> thisElm[];
                restart L
            if);
       #); 

     clear: (# do NONE -> head[] #);

     Elm:< (# succ: ^elm #);

     
     (*---------- private --------------*)
     head: ^Elm;
  #);

InsertSubDir:
  (* Inserts the directory name TargetDirectory immedidately before the
   base name of the entered path name. E.g. if TargetDirectory='sun3'
   and path='/home/frede/beta/system/v3.9/control' then the result will
   be:      '/home/frede/beta/system/v3.9/sun3/control'.
   Another example:  path='fusk' will result in: 'sun3/fusk'.
   *)
  (# path: ^text; dirCh: @text;
     lastSlash,L: @integer;
     asmDir: @boolean
  enter(path[],asmDir)
  do thePathHandler.DirectoryChar -> path.findAll(# do inx -> lastSlash #);
     thePathHandler.DirectoryChar -> dirCh.put;
     (if b2c then
         (# T: @text
         do 'b2c'->T; 3->L;
            (T[],lastSlash+1) -> path.insert;
            (if not asmDir then
                T.clear;
                thePathHandler.DirectoryChar->T.put;
                (T[],lastSlash+4)->path.insert;
                (common.targetDirectory[],lastSlash+5) -> path.insert;
                4+common.targetDirectory.length->L;
         if)#)
      else
         common.targetDirectory.length->L;
         (common.targetDirectory[],lastSlash+1) -> path.insert;
     if);
     (dirCh[],lastSlash+1+L)-> path.insert;
     inner;
  exit path[]
  #);

ExpandMacro:
  (* Enters a text string and replaces every occurence of the char '$' 
   in it with the string TargetDirectory (assumed not to contain any '$').
   WARNING : This operation destroys the entered text.
   *)
  (# macroChar: (# exit '$' #);
     T: ^text;
  enter T[]
  do
     t[] -> expandEnvVar -> t[];
     rundkoersel: macroChar -> T.findAll
     (#
     do 
        (inx,inx) -> T.Delete;   (* Delete the '$'. *)
        (common.targetDirectory[],inx) -> T.Insert;
        restart rundkoersel      (* Necessary? *)
     #);
  exit T[]
  #);

isFile:
  (# F: @File
  enter F.name
  do (if not F.entry.exists then 
         '\n**** Warning! File does not exist: ' -> infostream.puttext;
         F.name->infostream.putLine;
     if)
  exit F.name
  #);
