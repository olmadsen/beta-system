ORIGIN '~beta/mps/astlevel';
INCLUDE '~beta/betaast/index.bet';
INCLUDE '~beta/sysutils/objinterface';
INCLUDE '~beta/mps/grammarinit';

---AstInterfaceLib:attributes---
AstOfObject:
  (# obj: ^ object;
     astInx: @integer;
     FF: ^fragmentForm;
     FG: ^ fragmentGroup;
  enter obj[]
  do (# fragFormNo: @integer;
        objProto: @ protoType;
        fragName: ^ text;

     do obj[] -> GetProtoType -> objProto;
        2* objProto.astIndex -> astInx;
        objProto.groupId -> fragName[];
        objProto.formIndex -> fragFormNo;
        (fragName[]->expandToFullPath,screen[]) -> top.open -> FG[];
        search: FG.fragmentList.scan
          (# fragmentFormCounter: @integer;
             start::  (#  do 1->fragmentFormCounter #)
          do current.open;
             (if current.f.type = formType then
                  (if fragmentFormCounter = fragFormNo then
                      current.f[] -> FF[];
                      leave search
                   else
                      fragmentFormCounter+1->fragmentFormCounter
                  if)  
          if)#);
     #);
     INNER
  #);
AstIndexOfObject: AstOfObject
  (# objIndex: ^ASTindex
  do &ASTindex[] -> objIndex[];
     (astInx,FF[]) -> objIndex
  exit(objIndex[],FG[])
  #);
