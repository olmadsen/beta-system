ORIGIN '~beta/basiclib/betaenv';
INCLUDE '../v5.2/system';
INCLUDE '~beta/toollibs/labelnametable/name_to_address_table';
INCLUDE '../v5.2/SUN4Bmachine' ;
INCLUDE 'linkobject';
BODY '../v5.2/synthesizer';
BODY '../v5.2/SUN4SBasmlink';
---program:descriptor---
(# NT: @ name_to_address_table;
   comp: @compiler;

   activateCompiler:
       (#
       do L:
            comp.translate
            (# Report:
                 (# msg: ^text
                 enter msg[]
                 do  msg[]->comp.messagestream.putline; leave L 
                 #);
               
               MsgException::< (#do msg[]->Report #);
               TransAccessException::< (#do msg[]->Report #);
               TransCreateDirException::< (#do msg[]->Report #);
               NoSpaceException::<(#do msg[]->Report #);
               MPSException::< (#do msg[]->Report #);
               notExistingException::< (#do msg[]->Report #);
               parseException::< (#do msg[]->Report #);
               betaRunException::< (#do msg[]->Report #);
               RshException::< (#do msg[]->Report #);
               jobFileException::< (#do msg[]->Report #);
               multipleMachException::< (#do msg[]->Report #);
               unknownMachException::< (#do msg[]->Report #);
               doubleFormException::<(#do msg[]->Report #);
               fragmentException::
                 (* Note that the group is closed here.
                  * Sif and other tools may NOT want to do this!
                  *)
                 (# 
                 do CloseGroup;
                    msg[]->Report
                 #);
               propertyException::
                 (#
                 do (if warning then
                        msg[]->comp.messagestream.putLine;
                        true->continue
                     else 
                        CloseGroup;
                        msg[]->Report
                    if)
                 #);
               SystemException:: (#do msg[]->Report #)
            #)
       #);
   init:
     (#
     do 'Dynamic Mjolner Environment ...'->puttext;
        screen[]->comp.infostream[];
        screen[]->comp.tracestream[];
        screen[]->comp.messagestream[];
        screen[]->comp.bugstream[];
        comp.init;
        'dynenv' -> NT.init;
        (true,6) -> comp.setSwitch;
        newline;
     #);
   getNextArgument:
     (# f: ^text
     do 'Type fragment:'->puttext; 
        keyboard.getline -> f[];
        f[] -> putline;
        f[] -> comp.rootfragment[]
     exit f[]
     #);
   executeObject:
     (# F: ##object
     do (if comp.theMAchine[] <> NONE then 
            (*'machine ok'->putline;*)
            (*this(executeObject).origin.origin[] -> betaenv[];*)
            NT[]
              -> comp.theMachine.linkObject
              -> F##;
            (*((%getLongAt (@@F)),4) -> comp.theMachine.dumpObject;*)
            'Executing:'->putLine;
            F;
        if)
     #)
do init;
   loop:
     (#
     do (if getNextArgument <>  NONE then
            activateCompiler;
            executeObject;
            restart loop
        if)
     #)
#)
