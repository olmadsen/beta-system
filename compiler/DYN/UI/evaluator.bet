ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls'
        '~beta/guienv/fields';
-- windowLib: Attributes --
evaluatorCanvas: canvas
  (#
     onCloseButton:< (#  do INNER #);
     onEvaluateButton:< (#  do INNER #);
     evaluatorVisible: @boolean;
     editorType:< textEditor;
     editor: @editorType;
     doEditorBindings:
       (#  do true->editor.bindLeft->editor.bindRight->editor.bindTop->editor.bindBottom #);
     calculateEditorSize:
       (# mysize: @Point
       do
          (if evaluatorVisible then
              editor.father.size->mysize;
              (0,0)->editor.position;
              mysize.v-20->mySize.v;
              mysize->editor.size
          if)
       #);
     buttonCanvas: @canvas
       (#
          calculateSize:
            (# fatherSize,editorSize: @point
            do
               father.size->fatherSize;
               (fatherSize.h,20)->size;
               editor.size->editorSize;
               (0,editorSize.v)->position;
               evaluateButton.calculateSize;
               closeButton.calculateSize
            #);
          evaluateButton: @pushButton
            (#
               eventHandler::< 
                 (#
                    onFatherFrameChanged::<  (#  do calculateSize #);
                    onMouseUp::< 
                      (#  do THIS(evaluatorCanvas).onEvaluateButton #)
                 #);
               calculateSize:
                 (# sz: @point
                 do
                    (if evaluatorVisible then
                        father.size->sz;
                        ((sz.h / 2)-2,16)->size;
                        (2,2)->position
                    if)
                 #);
               open::< 
                 (#
                    doBindings:
                      (# 
                      do false->bindTop->bindRight; true->bindBottom->bindLeft
                      #)
                 do 'Evaluate'->label; doBindings; INNER ; 
                 #)
            #);
          closeButton: @pushButton
            (#
               eventHandler::< 
                 (#
                    onMouseUp::< 
                      (#  do THIS(evaluatorCanvas).onCloseButton #);
                    onFatherFrameChanged::<  (#  do calculateSize #)
                 #);
               calculateSize:
                 (# sz: @point
                 do
                    (if evaluatorVisible then
                        father.size->sz;
                        ((sz.h / 2)-2,16)->size;
                        (sz.h / 2,2)->position
                    if)
                 #);
               open::< 
                 (#
                    doBindings:
                      (# 
                      do true->bindBottom->bindRight; false->bindTop->bindLeft
                      #)
                 do 'Close'->label; doBindings; INNER ; 
                 #)
            #);
          open::< 
            (#
               doBindings:
                 (# 
                 do true->bindRight->bindLeft->bindBottom; false->bindTop
                 #)
            do
               (100,100)->size;
               THIS(buttonCanvas)[]->evaluateButton.open;
               THIS(buttonCanvas)[]->closeButton.open;
               doBindings
            #)
       #);
     calculateSize:<
       (# listSize,evaluatorSize: @point
       do
          (if evaluatorVisible then
              INNER calculateSize;
              (listSize.h,((100,(5*listSize.v) / 4)->max))->size;
              calculateEditorSize;
              buttonCanvas.calculateSize
          if)
       #);
     open::< 
       (#
          doBindings:
            (#  do true->bindBottom->bindLeft->bindRight->bindTop #)
       do
          (200,200)->size;
          THIS(evaluatorCanvas)[]->editor.open;
          doEditorBindings;
          THIS(evaluatorCanvas)[]->buttonCanvas.open;
          doBindings;
          INNER
       #)
  #)  

