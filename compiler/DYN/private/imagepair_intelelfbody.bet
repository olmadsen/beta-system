ORIGIN 'imagepairbody';
INCLUDE '../../GENERATOR/INTEL/intel';
INCLUDE '../../GENERATOR/INTEL/INTELBmachine'; 
INCLUDE '~beta/unixlib/dlfcn';

--RelocateMapOffset: dopart--
do 

---NeedsGOT:doPart---
do false -> value
   
--- FlushCodeCache:descriptor---
(#   #)

--RelocateBody: descriptor--
(# adr: @integer (* note this is NOT the old adr variable *)
do false->dump;
   (if mark
    // wordMark (* R_386_32 *)then
       (if trace then
           'patching wordmark: off='->puttext; relativeOff->puthex;
           ', adr=' -> puttext; xAdr->puthex; newline;
       if);
       ((relativeOff -> getItem)+xAdr, relativeOff) -> patch;
    // jmpMark (* R_386_PC32 *) then
       (* Compare with emitrel32off in INTELmstate.bet *)
       (* (adr-(off+4)) %putLongAt (off); *)
       (xAdr-(absAdr+4),relativeOff) -> patch;
       (if trace then
           'patched jmpmark: off='->puttext; relativeOff->puthex;
           ', xAdr=' -> puttext; xAdr->puthex; 
           ', name="'->puttext; name[]->puttext; '"'->put;
           '; patched to '->puttext; (xAdr-(absAdr+4)) -> puthex; 
           newline;
       if);
    // dataMark  then
       (* relocations relative to section .data *)
       (*%getLongAt (off) -> adr;*)
       relativeOff -> getItem -> adr;
       (* (adr + dataStart) %putLongAt (off); *)
       (adr + dataStart,relativeOff) -> patch;
       (if trace then
           'patched dataMark in '->puttext;
           (if isCode then
               'code' -> puttext;
            else
               'data' -> puttext;
           if);
           ': off='->puttext; relativeOff->puthex;
           ', adr=' -> puttext; adr->puthex; 
           '; patched to '->puttext; adr+datastart -> puthex; 
           newline;
       if);
    // codeMark  then
       (* relocations relative to section .text *)
       (*%getLongAt (off) -> adr;*)
       relativeOff -> getItem -> adr;
       (* (adr + codeStart) %putLongAt (off); *)
       (adr + codeStart, relativeOff) -> patch;
       (if trace then
           'patched codeMark in '->puttext;
           (if isCode then
               'code' -> puttext;
            else
               'data' -> puttext;
           if);
           ': off='->puttext; relativeOff->puthex;
           ', adr=' -> puttext; adr->puthex; 
           '; patched to '->puttext; adr+codestart -> puthex; 
           newline;
       if);
    else
       'unknown mark in RelocateBody' -> screen.putline;
   if);
#)
   
---PatchGOT:descriptor---
(# #)
