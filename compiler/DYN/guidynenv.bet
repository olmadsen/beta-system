ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/fields';
INCLUDE '~beta/guienv/stddialogs';
INCLUDE '~beta/basiclib/file';
INCLUDE '../CONTROL/system';
INCLUDE 'linkobject';   
INCLUDE '~beta/toollibs/labelnametable/name_to_address_table';
BODY '../SYNTHESIZER/synthesizer';
MDBODY sun4s '../GENERATOR/SUN4S/SUN4SBasmlink'
       linux '../GENERATOR/LINUX/LINUXBasmlink'
       nti '../GENERATOR/NTI/NTIBasmlink';
--- program: descriptor ---
systemEnv
(# setWindowEnv::< (# do myWindowEnv[] -> theWindowEnv[] #);
   myWindowEnv: @guienv
     (#
        textWindow: @window
          (#
             quitButton: @pushButton
               (#
                  eventHandler:: 
                    (#
                       onMouseUp:: 
                         (# 
                         do
                            'Exit dynamic Mjolner environment'->info.putline;
                            terminate
                         #)
                    #);
                  open:: 
                    (#  do (5,5)->position; (100,30)->size; 'Quit'->label #)
               #);
             newObjButton: @pushButton
               (#
                  eventHandler:: 
                    (#
                       onMouseUp:: 
                         (# R: ^objectEditor
                         do
                            (ObjC+1->ObjC,false,none ,0)->&ObjectEditor->R[];
                            R.open;
                            (if (eTop+1->eTop) > ed.range then
                                ed.range->ed.extend
                            if);
                            R[]->ed[eTop][]
                         #)
                    #);
                  open:: 
                    (# 
                    do (120,5)->position; (100,30)->size; 'New object'->label
                    #)
               #);
             newImpButton: @pushButton
               (#
                  eventHandler:: 
                    (#
                       onMouseUp:: 
                         (# R: ^objectEditor
                         do
                            (ObjC+1->ObjC,true,none ,0)->&ObjectEditor->R[];
                            R.open;
                            (if (eTop+1->eTop) > ed.range then
                                ed.range->ed.extend
                            if);
                            R[]->ed[eTop][]
                         #)
                    #);
                  open:: 
                    (# 
                    do (230,5)->position; (100,30)->size; 'New imperative'->label
                    #)
               #);
             open:: 
               (# 
               do
                  'Dynamic Mjolner Environment'->title;
                  (350,40)->size;
                  quitButton.open;
                  newObjButton.open;
                  newImpButton.open;
                  contents->target
               #);
             ed: [5] ^objectEditor;
             eTop: @integer
          #);
        ObjC: @integer;
        objectEditor: window
          (#
             textFile: @file;
             w: @canvas
               (#
                  doItButton: @pushButton
                    (#
                       eventHandler:: 
                         (#
                            onMouseUp:: 
                              (# T: @text; S: ^text
                              do
                                 'OBJ'->T;
                                 myObjC->T.putint;
                                 '.bet'->T.puttext;
                                 T[]->textFile.name;
                                 textFile.openWrite;
                                 (if theOrigin[] = none then
                                     'origin \'~beta/basiclib/v1.6/betaenv\''
                                       ->textFile.putline;
                                     '---lib:attributes---\nPobj'
                                       ->textFile.putText;
                                     
                                  else
                                     'origin \''->textFile.putText;
                                     'OBJ'->textFile.puttext;
                                     originObjC->textFile.putInt;
                                     '\'\n---'->textFile.puttext;
                                     'XOBJ'->textFile.puttext;
                                     originObjC->textFile.putInt;
                                     ':attributes---\nPobj'->textFile.putText;
                                     
                                 if);
                                 myObjC->textFile.putInt;
                                 ':'->textFile.put;
                                 textFile.newline;
                                 '(# <<SLOT XOBJ'->textFile.puttext;
                                     myObjC->textFile.putInt;
                                     ':attributes>>;\n'->textFile.puttext;
                                     (if impEd then
                                         'do '->textFile.putline;
                                         txtEdit.contents.contents->textFile.putline;
                                         '#)'->textFile.putline
                                   else
                                     txtEdit.contents.contents->S[];
                                     (3,S.length)->S.sub->textFile.puttext;
                                     
                                  if);
                                 textFile.close;
                                 textFile.name->comp.rootFragment[];
                                 (if not activateCompiler and not noExe then
                                     theOrigin[]->executeObject
                                       ->theObject[]
                                     (* else some error has happened *)
                                 if)
                              #)
                         #);
                       open:: 
                         (# 
                         do (5,5)->position; (100,30)->size; 'DoIt'->label
                         #)
                    #);
                  newSubObjButton: @pushButton
                    (#
                       eventHandler:: 
                         (#
                            onMouseUp:: 
                              (# R: ^objectEditor
                              do
                                 (ObjC+1->ObjC,false,theObject[],myObjC)
                                   ->&ObjectEditor->R[];
                                 R.open;
                                 (*(if (eTop+1->eTop ) > ed.range 
                                  then ed.range -> ed.extend 
                                  if);
                                  R[] -> ed[eTop][]*)
                                 
                              #)
                         #);
                       open:: 
                         (# 
                         do
                            (120,5)->position;
                            (130,30)->size;
                            'New internal-object'->label
                         #)
                    #);
                  open:: 
                    (# 
                    do (280,40)->size; doItButton.open; newSubObjButton.open
                    #)
               #);
             open:: 
               (# 
               do
                  (if impEd then 'Imperative'->title else 'Object'->title if);
                  (270,290)->size;
                  W.open;
                  txtEdit.open;
               #);
             txtEdit: @textEditor
               (#
                  open:: 
                    (# s: @styledtext;
                    do '(# \ndo \n#)' -> s;
                       s[] -> contents.contents;
                       (7,7) -> contents.selection;
                       (5,40)->position;
                       (250,250)->size;
                       true->bindBottom;
                       true->bindRight;
                       contents[]->target;
                    #)
               #);
             myObjC,originObjC: @integer;
             impEd: @boolean;
             theObject: ^Object;
             theOrigin: ^object
          enter (myObjC,impEd,theOrigin[],originObjC)
          exit THIS(objectEditor)[]
          #);
        NT: @name_to_address_table;
        comp: @compiler;
        activateCompiler:
          (# error: @boolean
          do
             L: comp.translate
               (#
                  Report:
                    (# msg: ^text
                    enter msg[]
                    do msg[]->comp.messagestream.putline; true->error; leave L
                    #);
                  MsgException::<  (#  do msg[]->Report #);
                  TransAccessException::<  (#  do msg[]->Report #);
                  TransCreateDirException::<  (#  do msg[]->Report #);
                  NoSpaceException::<  (#  do msg[]->Report #);
                  MPSException::<  (#  do msg[]->Report #);
                  notExistingException::<  (#  do msg[]->Report #);
                  parseException::<  (#  do msg[]->Report #);
                  betaRunException::<  (#  do msg[]->Report #);
                  RshException::<  (#  do msg[]->Report #);
                  jobFileException::<  (#  do msg[]->Report #);
                  multipleMachException::<  (#  do msg[]->Report #);
                  unknownMachException::<  (#  do msg[]->Report #);
                  doubleFormException::<  (#  do msg[]->Report #);
                  fragmentException::
                    (* Note that the group is closed here.
                     * Sif and other tools may NOT want to do this!
                     *)  (#  do CloseGroup; msg[]->Report #);
                  propertyException:: 
                    (# 
                    do
                       (if warning then
                           msg[]->comp.messagestream.putLine; true->continue
                        else
                           CloseGroup; msg[]->Report
                       if)
                    #);
                  SystemException::  (#  do msg[]->Report #)
               #)
          exit error
          #);
        initDyn:
          (# T: ^text; 
          do
             screen[]->comp.infostream[];
             screen[]->comp.tracestream[];
             screen[]->comp.messagestream[];
             screen[]->comp.bugstream[];
             comp.init;
             (true,6)->comp.setSwitch;
             (true,33)->comp.setSwitch;
             &stream[]->info[];
             comp.verboseLevel.nothing->comp.verboseLevel;
             (true,19)->comp.setSwitch;
             (true,90)->comp.setSwitch;
             (for i: NoOfArguments-1 repeat
                  i+1->Arguments->T[];
                  (if true
                   // '--noexe'->T.equalNCS then
                      true->noExe
                   // '--trace'->T.equalNCS then
                      true->trace; screen[]->msg[]->NT.tracestream[];
                   // '--mute'->T.equalNCS then
                      &stream[]->info[];
                      comp.verboseLevel.nothing->comp.verboseLevel;
                      (true,19)->comp.setSwitch;
                      (true,90)->comp.setSwitch;
                      
                   // '--verbose'->T.equalNCS then
                      screen[]->info[];
                      comp.verboseLevel.nothing->comp.verboseLevel;
                      (false,19)->comp.setSwitch;
                      (false,90)->comp.setSwitch;
                      
                  if)
             for);
             (if msg[] = none then &stream[]->msg[] if);
             'Dynamic Mjolner Environment ...'->info.puttext;
             info.newline;
             1->Arguments->NT.init;
          #);
        executeObject:
          (# theOrigin: ^object; F: ##object; R: ^object
          enter theOrigin[]
          do
             (if comp.theMachine[] <> none then
                 'Linking new descriptor!'->info.putline;
                 (NT[],theOrigin[],trace)->comp.theMachine.linkObject->F##;
                 'Executing:'->info.putLine;
                 &F[]->R[];
                 R
             if)
          exit R[]
          #);
        noExe,trace: @boolean;
        info,msg: ^stream
     do textWindow.open; initDyn; 
     #) ;
#)

