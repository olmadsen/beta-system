ORIGIN '~beta/bifrost/Bifrost';
INCLUDE '~beta/bifrost/PredefinedGO';
INCLUDE '~beta/bifrost/ColorNames';
(*INCLUDE '~beta/bifrost/v2.3/private/Impl/debug';*)
---guienvLib:attributes---
FigureWindow: window
  (# myCanvas: @BifrostCanvas     
       (# open :: (#do this(FigureWindow).size -> size #);
       #);
     open::
       (#
       do (500,500) -> size; 
          myCanvas.open; 
       #);
     Figure:
       (# FigureType:<  AbstractGraphicalObject;
          F: @FigureType;
          hilited: @boolean;
          initialized: @boolean;
          init:<
            (#
            do F.init; 
               true -> initialized;
               red -> color;
               INNER init;
               F[] -> myCanvas.draw;
            #);
          colorValue: (# R,G,B: @ integer enter(R,G,B) exit (R,G,B) #);
          rePaint:
            (# rehilite: @boolean;
            do (if not initialized then init if);
               (if hilited->rehilite then unhilite if);
               F.getBounds -> myCanvas.damaged;
               INNER RePaint;
               F.getBounds -> myCanvas.damaged;
               myCanvas.repair;              
               (if rehilite then hilite if);
            #);
          color: RePaint
            (# c: @colorValue; aSolidColor: ^solidColor;
            enter c
            do &solidColor[] -> aSolidColor[];
               c -> aSolidColor.RGBvalues;
               aSolidColor[] -> F.setPaint;
            exit c
            #);
          move: rePaint
            (# p: @point
            enter p
            do p -> F.move;
            #);
          hilite:
            (#
            do F[] -> mycanvas.hilite; true -> hilited;
            #);
          unHilite:
            (#
            do F[] -> mycanvas.unHilite; false -> hilited;
            #);
          BringForward:
            (#
            do F[] -> myCanvas.bringForward
            #);
          SendBehind:
            (#
            do F[] -> myCanvas.sendBehind
            #);
       #);
     square: Figure
       (# FigureType::< Rect;
          init::
            (# 
            do (10, 110) -> F.upperleft;
               100 -> F.width;
               100 -> F.height;
            #);
       #);
     Ellipse: Figure
       (# FigureType::this(window).Ellipse;
          init::
            (#
            do (350,350) -> F.center;
	       30 -> F.horizontalRadius;
	       20 -> F.verticalRadius;               
            #);
       #);
  #);
Hello:
  (# 
  do 'Hello'->putline
  #)


