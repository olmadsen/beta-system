ORIGIN 'asmlink';
INCLUDE '~beta/unixlib/unixfile';
--asmlinklib: attributes ---
quote :
  (# t : @text
  enter t
  exit t[]
  #);
stripPath:
  (# T: ^text; pos: @integer
  enter T[]
  do AST.thePathhandler.directoryChar->T.findAll(#do inx->pos #);
     (if pos//0 then else (pos+1,T.length)->T.sub->T[] if)
  exit T[]
  #);
ldUNIXtmpFile:
  (# tmpArgs,TmpF: ^text
  enter tmpArgs[]
  do  (if asmState.tmpCount//0 then
          'trap "echo :linking interrupted.; '->AFF.puttext;
          rmTmpFiles;
          '; exit 2" 2 9 10'->AFF.putLine;
      if);
     &text[]->TmpF[]; 
     '${TMPDIR}/'->TmpF; (* NOTE wont work if concurrent compilations *)
     objName[]->stripPath->TmpF.puttext; '.$$.'->TmpF.putText;
     asmState.tmpCount+1->asmState.tmpCount->TmpF.putInt;
     'rm -f '->AFF.putText; TmpF[]->AFF.putLine;
     INNER;
     TmpF[]->AFF.putText; 
     ' \\\n\t' ->AFF.putText;
     tmpArgs[]->AFF.putText;
  exit tmpF[]
  #);
rmTmpFiles:
  (#
  do '/bin/rm -f '->AFF.puttext;
     '${TMPDIR}/'->AFF.putText; objName[]->stripPath->AFF.putText; '.$$.*'->AFF.puttext;
  #)   
--asmState:descriptor--
(# tmpCount,targetML: @integer #)
--HasSharedLibs:descriptor--
(#do switch[53]->value#)
--- cont: descriptor --
(#
do '\\' -> Aff.put; aff.newLine; ascii.ht -> aff.put;
#)
--- header: descriptor ---
(# 
do (* datpete 6/3/96: changed use of targetMachine and targetMachineId 
    * according to OLM: 11/2/96 in CHANGES.
    *)
   common.targetMachine.length->asmState.targetML;
   '#! /bin/sh'->AFF.putLine;
   'BETALIB='->AFF.putText; BetaLib[]->AFF.putLine;
   'export BETALIB'->AFF.putLine;
   'MACHINETYPE='->AFF.putText; common.targetMachine[]->AFF.putLine;
   'export MACHINETYPE'->AFF.putLine;
   'TMPDIR=${TMPDIR-/tmp}'->AFF.putLine;
   (if common.targetMachineId//common.sgi then
       'PATH=/users/beta/GNU/bin/sgi:$PATH'->AFF.putline;
       'export PATH'->AFF.putline;
   if);
   'BetaAsm(){'->AFF.putLine;
   '  trap "echo C-compiling interrupted.; /bin/rm -f $1.o; exit 2" 2 9 10'
     ->AFF.putLine;
   (if verboseLevel<=verboseLevel.actions then
       '  echo C-compiling $1.c'->AFF.putLine;
   if);
   '  if gcc -c' ->AFF.puttext;
   (if switch[18] or switch[55] then 
       ' -g'->AFF.puttext;
   if);
   (if switch[90] then
       ' -w' -> AFF.puttext;
    else
       ' -Wall' -> AFF.puttext;
   if);
   (if switch[29] then
       ' -pg'->AFF.puttext;
   if);
   '-Wno-unused -D $MACHINETYPE -O2'->AFF.puttext;
   ' $CC_USER -I b2c -I $BETALIB/betarun/v2.7/b2c -o $2.o $1.c\n then'->AFF.putline;
   (if switch[18]//false then 
       '    /bin/rm -f $1.c'->AFF.putLine;
       '    /bin/rm -f $1.h'->AFF.putLine 
    else
       '    return' ->AFF.putLine
   if);
   '  else '->AFF.putLine;
   '    /bin/rm -f $2.o; echo  Aborted.; exit 1'->AFF.putLine;
   '  fi  '->AFF.putLine;
   '}'->AFF.putLine;
   
   'BetaMake(){'                                  -> AFF.putLine;
   '  dir=`dirname $1`; export dir'               -> AFF.putLine;
   '  file=`basename $1`; export file'            -> AFF.putLine;
   '  sh -c ''\\'                                 -> AFF.putLine;
   '    cd $dir; \\'                              -> AFF.putLine;
   '    make -q -f $file MACHINETYPE=$MACHINETYPE; \\' 
     -> AFF.putLine;
   '    if [ $? -ne 0 ]; then \\'                 -> AFF.putLine;
   (if verboseLevel<verboseLevel.nothing then
   '        echo make $dir/$file; \\'             -> AFF.putLine;
   if);
   '        make -f $file MACHINETYPE=$MACHINETYPE; \\' 
     -> AFF.putLine;
   '        if [ $? -ne 0 ]; then exit 1; fi; \\' -> AFF.putLine;
   '    fi; \\'                                   -> AFF.putLine;
   '    exit 0'''                                 -> AFF.putLine;
   '  if [ $? -ne 0 ]; then'                      -> AFF.putLine;
   '     echo Make failed. Exiting.'              -> AFF.putLine;
   '     exit 1'                                  -> AFF.putLine;
   '  fi'                                         -> AFF.putLine;
   '}'                                            -> AFF.putLine;

#)
--- AsmExt: descriptor --- 
(# do '.c'->ext #) 
--- BinExt: descriptor --- 
(# do '.o'->ext #)
---LibExt: descriptor ---
(# do '.sl'->ext #)
--- JobExt: descriptor ---
(# do '..job'->ext #)
---ExeExt:descriptor---
(* the extension for an executable file is empty 
 * on most platforms except Windows NT 
 *)
(#do ''->ext #)
--- Echo: descriptor ---
(# do 'echo '->AFF.puttext #)
--- Remove: descriptor ---
(# do '/bin/rm -f '->AFF.puttext; name[]->AFF.putline; #)
--- Make: descriptor ---
(# 
do (if first//true then
       'trap "echo make interrupted.; exit 2" 2 9 10'->AFF.putLine;
   if); 
   (* test makefile dependencies *)
   'BetaMake '->AFF.putText; m[]->AFF.putLine;
   
   (*
   echo; 'make '->AFF.puttext; m[] -> AFF.putLine;
   'make -f '->AFF.putText; m[]->AFF.puttext; 
   ' MACHINETYPE='->AFF.puttext; common.targetMachine[]->AFF.putLine*)
#)
--- Resource: descriptor ---
(# #)
(*--- RemoteShell: descriptor ---
 (# do '/usr/bin/remsh ' -> rsh #)*)
 --- assemble: descriptor ---
(# p: @integer; G: @text
do 'BetaAsm '->AFF.putText; 
   F->G;
   AST.thePathHandler.directoryChar->G.findAll(#do inx-> p #);
   (p-asmState.targetML,p)->G.delete; G[]->AFF.putText; ' ' ->AFF.put;
   F[]->AFF.putLine
#)
--linkLibraries:descriptor--
(#
do(* -b below is not enough*)
   (if switch[53]//true then
       Libs.scan
       (#
       do (if thisElm.args.length//0 then 
           else
              'echo linking shared lib: '->AFF.puttext; thisElm.T[]->AFF.putLine;
              
              'ld -b -q \\\n\t-o '->AFF.puttext;
              thisElm.T[]->AFF.puttext; ' '->AFF.put;
              thisElm.args[]->AFF.putline
          if)
   #)if)
#)
--contCh:descriptor--
(#do '\\'->value #)
--ldArgMax:descriptor--
(#do 9000->value #)
--ldTmp:descriptor--
(#
do tmpArgs[]
     ->ldUNIXtmpFile(#do 'ld -r -o '->AFF.putText #)
     ->TmpF[] 
#)
--- link: descriptor ---
(# ldLibPath,betaLinkOpt: ^ text;
   start: @Integer;
do 'trap "echo :linking interrupted.; /bin/rm -f '->AFF.puttext;
   objName[]->AFF.puttext;
   '; exit 2" 2 9 10'->AFF.putLine;
   
   '$(BETALINKOPTIONS)'
     -> expandEnvVar(# defaultValue::<(#do ' -static '->envvarvalue[] #)#)
     ->betaLinkOpt[];
   (if switch[37]//true then ' -s ' -> betaLinkOpt.append if);
   (if switch[29] then ' -pg'->betaLinkOpt.append if);
   
   'gcc '->AFF.puttext; 
   
   (* FIXME: the following should be replaced by:
    * 
    * ('$(LD_LIBRARY_PATH)'->expandEnvVar, ':') -> scanSearchPath
    *    (# 
    *    do (if path.length>0 then ' -L'->AFF.putText; path[]->AFF.puttext if)
    *    #);
    *)

   '$(LD_LIBRARY_PATH)'->expandEnvVar->ldLibPath[];
   (if ldLibPath.length//0 then (* skip *)
    else (* parse ':' separated list of directories *)
       1->start;
       ldLibPath.reset;
       ':'->ldLibPath.findAll
       (#
       do 
          (if (inx-1>start)//true then 
              ' -L'->AFF.putText; 
              (start,inx-1)->ldLibPath.sub->AFF.putText;
          if);
          inx+1->start;
       #);
       (if start-1<ldLibPath.length // true then
           ' -L'->AFF.putText; 
           (start,ldLibPath.length)->ldLibPath.sub->AFF.putText;
       if);
   if);
   
   betaLinkOpt[]->AFF.putText; 
   
   ' -o '->AFF.puttext; objName[]->AFF.puttext; ' '->AFF.put; cont;
#)
---addBetaRun:descriptor---
(* the following can be for most platforms except Windows NT *)
(#
do (if userDefinedBetaRun//true then
       betaRun[]->linkObj
    else
       asmLink.binExt->betaRun.CopyAppend->linkObj
   if)
#)
--asmlinkdynamic:descriptor--
(##)
--- linkObj:descriptor ---
(#
do lobj[]->ldA.putText; ' \\\n\t'->ldA.putText
#)
--- libraries: descriptor ---
(# 
do ' -lm -lc '->ldA.putLine;
#)
--- linktrailer: descriptor ---
(# 
do (if asmState.tmpCount//0 then
    else
       rmTmpFiles; AFF.putLine
   if);
  
   'case $? in'->AFF.putLine;
   '0)'->AFF.putText; AFF.newline;
   'echo Object program on file: '->AFF.puttext;
   objName[]->AFF.putLine;
   ';;'->AFF.putLine;
   '*)'->AFF.putLine;
   '/bin/rm -f '->AFF.PutText;
   objName[]->AFF.putLine;
   (if asmState.tmpCount//0 then
    else
       rmTmpFiles; AFF.putLine
   if);
   'esac'->AFF.PutLine;
#)
--- trailer: descriptor ---
(# #)
-- jobFilePermission: descriptor --
(# (* change permission on the jobfile so it can be excuted *)
   ue: @unixentry;
do AFF.name -> ue.path;
   ('a','x') -> ue.permission.add;
#)
