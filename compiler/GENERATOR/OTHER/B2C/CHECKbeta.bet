ORIGIN 'interactive';
BODY 'dummysynthesizer';
BODY 'HPasmlink';
-----program: descriptor---
(# comp: @compiler;
   
   activateChecker:
     (# fragmentFile: ^text
     enter fragmentFile[]
     do L:
        fragmentFile[]->
        comp.translate
        (#  Report:
                 (# msg: ^text
                 enter msg[]
                 do  msg[]->comp.messagestream.putline; leave L 
                 #);
           MsgException::< (#do msg[]->Report #);
           TransAccessException::< (#do msg[]->Report #);
           TransCreateDirException::< (#do msg[]->Report #);
           NoSpaceException::<(#do msg[]->Report #);
           MPSException::< (#do msg[]->Report #);
           notExistingException::< (#do msg[]->Report #);
           parseException::< (#do msg[]->Report #);
           betaRunException::< (#do msg[]->Report #);
           RshException::< (#do msg[]->Report #);
           jobFileException::< (#do msg[]->Report #);
           multipleMachException::< (#do msg[]->Report #);
           unknownMachException::< (#do msg[]->Report #);
           doubleFormException::<(#do msg[]->Report #);
           fragmentException::
             (* Note that the group is closed here.
              * Sif and other tools may NOT want to do this!
              *)
             (# 
             do CloseGroup;
                msg[]->Report
             #);
           propertyException::
             (#
             do (if warning then
                    msg[]->comp.messagestream.putLine;
                    true->continue
                 else 
                    CloseGroup;
                    msg[]->Report
                if)
             #);
           SystemException:: (#do msg[]->Report #)
           
        #);
     #);
   
do (* Here it is possibleto initialize the compiler/checker to use specific instances
    * of ASTinterface. If comp.AST[] is none, then
    * comp.init will generate a new instance of ASTinterface.
    * 
    * myASTinterface[] -> comp.AST[]
    *)
   comp.init;
   true->comp.switch[5]; (* prevent activation of code generator *) 
   
   (if noOfArguments>=2 // true then
       2->arguments->activateChecker
    else
       'Type fragment name:'->comp.infostream.putText;
       GetAtom->activateChecker
   if)
#)

