ORIGIN 'asmlink';
(*BODY 'DesFileCompressor'*)
--- asmlinkdynamic:descriptor --- (# #)
--asmState:descriptor--
(# tmpCount: @integer #)
--- hasSharedLibs:descriptor(*dopart*) ---
(#
do false->value;
#)
--- asmlinklib: attributes ---
  quote:
    (# t: ^text; copy: ^Text;
    enter t[]
    do &Text[] -> copy[]; t -> copy;
	'"' -> copy.put; '"'->copy.prepend;
    exit copy[]
    #)
--- cont: descriptor --
    (#
    do 182->Aff.put; aff.newLine; ascii.ht->aff.put;
    #)
--- header: descriptor ---
    (# do true->switch[33] (* we cannot execute the jobfile from compiler *) #)
--- AsmExt: descriptor --- 
    (# #) 
--- BinExt: descriptor --- 
    (# do '.o'->ext #)
---LibExt: descriptor ---
    (# do '.o'->ext #)
---ExeExt:descriptor---
(* the extension for an executable file is empty 
 * on most platforms except Windows NT 
 *)
(#do ''->ext #)
--- JobExt: descriptor ---
    (# do '.job'->ext #)
--- Echo: descriptor ---
    (# do 'echo '->AFF.puttext #)
--- Remove: descriptor ---
    (# do 'delete -i '->AFF.puttext; name[]->AFF.putline; #)
--- Make: descriptor ---
(# 
do 'BetaMake '->AFF.puttext;
   m[]->quote->AFF.putText; ' '->AFF.put; objName[]->quote->AFF.putLine;
   'Exit If {Status} != 0' -> AFF.putLine;
#)
---Resource:descriptor---
(# i: @Integer;
   ext: @Text;
do m.reset;
   '.'->m.findAll(# do inx -> i #);
   (if (i>0) and (i<m.lgth) //true then
       m.T[i+1:m.lgth] -> ext;
       (if true
        // ('rsrc'->ext.equalNCS) then 'BetaRez '->AFF.puttext;
        // ('r'->ext.equalNCS) then 'BetaResource '->AFF.puttext;
        else
           '\n**** Warning: unknown extension in resource: '->messagestream.puttext;
           m[]->messagestream.putline;
           'BetaRez '->AFF.puttext;
       if);
    else
       '\n**** Warning: no extension in resource: '->messagestream.puttext;
       m[]->messagestream.putline;
       'BetaRez '->AFF.puttext;
   if);
   m[]->quote->AFF.putText; ' '->AFF.put; objName[]->quote->AFF.putLine;
   'Exit If {Status} != 0' -> AFF.putLine;
#)
--- assemble: descriptor ---
    (# t: ^text;
       copy: @Text; (* due to error in CopyAppend *)
    do
      (******
       F -> copy;
       AsmExt -> copy.Append; copy[] -> quote -> t[];
       'BetaAsmO ' ->AFF.putText; t[] -> AFF.putLine;
       'Exit If {Status} != 0' -> AFF.putLine;
       (if switch[18] // false then
           'delete -y '->AFF.putText;
	   t[]->AFF.putLine;
       if);
       *************)
    #)

--ldArgMax:descriptor--
(#do maxint->value (* do not split on mac *) #)

--ldTmp:descriptor--
(# (* What is this on the Mac? -pjs *)
(*do tmpArgs[]
     ->ldUNIXtmpFile(#do 'ld -r -o '->AFF.putText #)
     ->TmpF[] *)
#)

--contCh:descriptor--
(#do '¶'->value #)

--- link: descriptor ---
    (#
    do '"{MPW}tools:link" -w -t {BetaLinkType} -c {BetaLinkCreator} -o '->AFF.putText;
       objName[]->quote->AFF.puttext; cont;
       '{BetaLinkOptions}'->Aff.putText; cont;
    #)
--linkLibraries:descriptor--
(#
(*do Libs.scan
   (#
   do (if thisElm.args.length//0 then 
       else
          'echo linking shared lib: '->AFF.puttext; thisElm.T[]->AFF.putLine;
          
          '/usr/ccs/bin/ld -dy -G \\\n\t-o '->AFF.puttext;
          thisElm.T[]->AFF.puttext; ' '->AFF.put;
          thisElm.args[]->AFF.putline
      if)
   #)*)
#)
--- linkObj:descriptor ---
(# do '"' -> ldA.put; lobj[]->ldA.putText; '"' -> ldA.put; ' ¶\n\t'->ldA.putText; #)
--- addBetaRun:descriptor ---
(* the following can be for most platforms except Windows NT *)
(#
do (if userDefinedBetaRun//true then
       betaRun[]->linkObj
    else
       asmLink.binExt->betaRun.CopyAppend->linkObj
   if)
#)
--- libraries: descriptor ---
(# do (*'{BetaLinkLibs}'->ldA.putText !BetaLinkLibs added to betarun.o! *) #)
--- linktrailer: descriptor ---
    (# 
    do AFF.newline;
       'If {Status} == 0'->AFF.putLine;
       '   echo "Object program on file: '->AFF.puttext; objname[]->AFF.puttext;
       '"'->AFF.put; Aff.newline;
       'Else'->AFF.putLine; 
       '   delete -i -y '->AFF.PutText; objName[]->quote->AFF.putLine;
       '   Exit 5' -> AFF.putLine;
       'End'->AFF.PutLine;
        (if switch[18] // false then
              'delete -i -y '->AFF.putText; AFF.name->quote->AFF.putLine;
        if);
    #)
--- trailer: descriptor ---
(##)
-- jobFilePermission: descriptor --
(##)
