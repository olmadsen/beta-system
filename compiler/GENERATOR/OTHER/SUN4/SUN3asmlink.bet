ORIGIN 'asmlink';
INCLUDE '~beta/unixlib/unixfile'
--- cont: descriptor --
    (#
    do '\\' -> Aff.put; aff.newLine; ascii.ht -> aff.put;
    #)
--- header: descriptor ---
    (# 
    do '#! /bin/sh'->AFF.putLine;
       'BETALIB='->AFF.putText; BetaLib[]->AFF.putLine;
       'export BETALIB'->AFF.putLine;
       'MACHINETYPE='->AFF.putText; targetMachine[]->AFF.putLine;
       'export MACHINETYPE'->AFF.putLine;
       'BetaAsm(){'->AFF.putLine;
       '  trap "echo :assembling interrupted.; /bin/rm -f $1.o; exit 2" 2 9 10'
	    ->AFF.putLine;
       '  echo assembling $1..s'->AFF.putLine;
       '  if /bin/as -o $1.o $1..s'->AFF.putLine;
       '  then'->AFF.putLine;
       (if switch[18]//false then 
       '    /bin/rm -f $1..s'->AFF.putLine 
	else
       '    return' ->AFF.putLine
       if);
       '  else '->AFF.putLine;
       '    /bin/rm -f $1.o; echo  Aborted.; exit 1'->AFF.putLine;
       '  fi  '->AFF.putLine;
       '}'->AFF.putLine
    #)
--- AsmExt: descriptor --- 
    (# do '..s'->ext #) 
--- BinExt: descriptor --- 
    (# do '.o'->ext #)
--- JobExt: descriptor ---
    (# do '..job'->ext #)
--- Echo: descriptor ---
    (# do 'echo '->AFF.puttext #)
--- Remove: descriptor ---
    (# do '/bin/rm -f '->AFF.puttext #)
--- Make: descriptor ---
(#
do echo; 'make '->AFF.puttext; m[] -> AFF.putLine;
   '/bin/make -f '->AFF.putText; m[]->AFF.puttext; 
   ' MACHINETYPE='->AFF.puttext; targetMachine[]->AFF.putLine
#)
--- resource: descriptor ---
(##)
--- RemoteShell: descriptor ---
    (# do '/usr/bin/remsh ' -> rsh #)
--- assemble: descriptor ---
    (# 
    do (*
       'as -o a.out '->AFF.putText; InFile[]->AFF.putLine;
       'mv a.out '->AFF.puttext;    OutFile[]->AFF.putLine;
       (InFile.length-2,Infile.length)->Infile.delete;*)
       'BetaAsm '->AFF.putText; F[]->AFF.putLine
    #)
--- link: descriptor ---
    (# betaLinkOpt: ^ text
    do 'trap "echo :linking interrupted.; /bin/rm -f '->AFF.puttext;
       objName[]->AFF.puttext;
       '; exit 2" 2 9 10'->AFF.putLine;

      '$(BETALINKOPTIONS)'
	-> expandEnvVar
	     (# defaultValue::<
	          (#
		  do 
                '-e start -n'->envvarValue[]#)#)
	 ->betaLinkOpt[];
       (if switch[37]//true then ' -s ' -> betaLinkOpt.append if);

      'ld '->AFF.puttext; betaLinkOpt[]->AFF.putText; ' -o '->AFF.puttext;
       objName[]->AFF.puttext; ' '->AFF.put; cont;
 
       (if true//switch[29]//switch[30]//switch[31] then
	   ' lib/mcrt0.o /lib/Fcrt1.o ' ->AFF.puttext
       else
	   '/lib/crt0.o ' ->AFF.puttext
       if);

       BetaRun[]->AFF.puttext; ' '->AFF.put;
    #)
--asmlinkdynamic:descriptor--
(##)
--- linkObj:descriptor ---
(#
do lobj[]->ldA.putText; ' \\\n\t'->ldA.putText
#)
--- libraries: descriptor ---
(# 
do ' -lm -lc '->ldA.putLine 
#)
--- trailer: descriptor ---
    (# do
       'case $? in'->AFF.putLine;
       '0)'->AFF.putText; AFF.newline;
       'echo Object program on file: '->AFF.puttext;
       objName[]->AFF.putLine;
       ';;'->AFF.putLine;
       '*)'->AFF.putLine;
       '/bin/rm -f '->AFF.PutText;
       objName[]->AFF.putLine;
       'esac'->AFF.PutLine;
    #)
-- jobFilePermission: descriptor --
(# (* change permission on the jobfile so it can be excuted *)
   ue: @unixentry;
do AFF.name -> ue.path;
   ('a','x') -> ue.permission.add;
#)
