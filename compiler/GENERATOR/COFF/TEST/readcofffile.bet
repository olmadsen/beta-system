ORIGIN '../coffread';
INCLUDE '~beta/basiclib/formatio';

---PROGRAM:descriptor---
(#
   puthex: (* Peter do not trust the one in formatio (:-) *)
     (# i: @integer;
        printf: external
          (# fmt: [0]@char;
             val: @integer;
          enter (fmt,val)
          #);
     enter i
     do ('0x%08x\t', i) -> printf;
     #);

   f: @coffReader;
   hdr: ^filehdr;

do (if noOfArguments < 2 then
       'Usage: readelffile coff-file' -> screen.putline;
       stop;
    else
       2 -> arguments -> f.name;
       (if not f.entry.exists then
           'No such file: '->screen.puttext;
           2 -> arguments -> putline;
           stop;
       if)
   if);
   f.openRead;
   '\nFile header:'->putline;
   f.getFilehdr -> hdr[];
   (if hdr[]<>NONE then
       'magic\t' -> puttext; hdr.f_magic -> puthex; newline;
       'nscns\t' -> puttext; hdr.f_nscns -> puthex; newline;
       'timdat\t' -> puttext; hdr.f_timdat -> puthex; newline;
       'symptr\t' -> puttext; hdr.f_symptr -> puthex; newline;
       'nsyms\t' -> puttext; hdr.f_nsyms -> puthex; newline;
       'opthdr\t' -> puttext; hdr.f_opthdr -> puthex; newline;
       'flags\t' -> puttext; hdr.f_flags -> puthex; newline;
   if);
   
   '\nSection Headers:'->putline;
   f.scanScnhdrs
   (# 
   do current_inx -> putint;
      ascii.ht->put; 
      (current.s_name1_4, current.s_name5_8) 
        -> f.getName -> putline;
   #);
   
   '\nSymbol table:'->putline;
   f.scanSyments
   (# num_aux: @integer;
   do current_inx -> putint; ascii.ht->put; 
      (if num_aux>0 then
          '[AUX]' -> putline;
          num_aux-1->num_aux;
       else
          current.n_value -> puthex;
          (current.n_name1_4,current.n_name5_8)
            -> f.getName -> putline;
          current.n_numaux -> num_aux;
      if);
   #);
   
   '\nText reloc entries:'->putline;
   F.scanTextReloc
   (# sym: ^syment;
   do current.r_vaddr -> puthex;
      '%24s  ' -> putFormat(# do current.r_type->reloc_type_astext->s #);
      current.r_symndx -> f.getSyment -> sym[];
      (sym.n_name1_4,sym.n_name5_8) -> f.getName -> putline;
   #);
   
   '\nData reloc entries:'->putline;
   F.scanDataReloc
   (# sym: ^syment;
   do current.r_vaddr -> puthex;
      '%24s  ' -> putFormat(# do current.r_type->reloc_type_astext->s #);
      current.r_symndx -> f.getSyment -> sym[];
      (sym.n_name1_4,sym.n_name5_8) -> f.getName -> putline;
   #);

   f.close;
#)
