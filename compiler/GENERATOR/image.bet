ORIGIN '~beta/basiclib/betaenv';
LIB_DEF 'image' '../../lib';
BODY 'imagebody';

-- LIB: Attributes --

entryPointInfo:
  (# t: ^text          (* name of entrypoint *);
     data: @boolean    (* entry-point in data area *);
     local: @boolean   (* defined in this segment *);
     export: @boolean  (* export this entry-point*);
     LIP: @integer     (* local offset of the entrypoint *);
  #);

Image:
  (* Pattern describing code- or data segment in a binary file.
   * FIXME: Much more can possibly be abstracted from the
   * corresponding images in XXXmachine.
   *)
  (# <<SLOT ImageLib: Attributes>>;
     LIP: @integer
       (* offsets of next item to be emitted in buffer.
        * Either a byte offset (intel) or a long offset (risc).
        *);
     
     (* "Substance" of the segment. If buffer_ptr is non-zero,
      * it points to an externally allocated buffer and buffer_size
      * is the number of bytes in this external buffer.
      * If buffer_ptr is zero, the array named buffer is used
      * for the substance.
      *)
     buffer_ptr, buffer_size: @integer;
     buffer: [0] @integer;
     
     (* Marked positions in buffer, i.e. relocation entries and 
      * entrypoint definitions
      *)
     marks: [500] @integer;
     (* Relocation types for the corresponding marked positions *)
     types: [500] @char (*integer*);
     (* Symbol information for corresponding relocation marks *)
     EntryPointElement:< EntryPointInfo;
     epElm: [500] ^EntryPointElement;
     
     (* Number of marked positions *)
     markTop: @integer;
     (* No of relocation items *)
     noOfRel: @integer; 
     
     display:
       (# 
       <<SLOT ImageDisplay: dopart>>
       #);

  do INNER
  #);

(* Mark types used for entrypoints and relocations *)
entryDefMark: (# exit 1 #); (* RISC/INTEL *)
wordMark: (# exit 2 #);     (* RISC/INTEL *)
jmpMark: (# exit 3 #);     (* RISC/INTEL *)
dataMark: (# exit 4 #);    (* PPCMAC/INTEL/MIPS *)
codeMark: (# exit 5 #);    (* PPCMAC/INTEL *)

(* RISC specific marks *)
callMark: (# exit 6 #); 
bgtuMark: (# exit 7 #);
setHiMark: (# exit 8 #);
setLoMark: (# exit 9 #);
setLoMarkSt: (# exit 10 #);
setLoMarkJmp: (# exit 11 #);
setLoMarkLdd: (# exit 12 #);
setLoMarkLd: (# exit 13 #);

(* PPCMAC specific marks *);
TOCstart: (# exit 14 #); 
TOCmarkDS: (# exit 15 #); 
TOCmarkRW: (# exit 16 #); 
TOCmarkTC: (# exit 17 #); 
TOCwordMark: (# exit 18 #); 

mark_astext:
  (# mark: @integer;
     desc: ^text;
  enter mark
  <<SLOT ImageMarkAsText: dopart>>
  exit desc[]
  #);
