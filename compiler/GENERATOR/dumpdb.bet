ORIGIN '~beta/basiclib/betaenv' ;
INCLUDE '~beta/sysutils/binarystream';
INCLUDE '~beta/sysutils/objinterface';
INCLUDE '~beta/betaast/index.bet';
INCLUDE '~beta/mps/grammarinit';
---program:descriptor---
(* dumps the content of a debug file produced by the BETA compiler *)
(# B: @binaryStream;
   FN: ^text;
   noOfFrag,noOfDesc,V: @integer;
   MPS: @ASTInterface;
   beta: ^MPS.treeLevel;
   N: ^ MPS.ASTindex;
   FG: ^MPS.FragmentGroup;
   FF: ^MPS.fragmentForm;
   PP:
     (# astInx: @integer; 
        objIndex: ^mps.ASTindex; fragFormNo: @integer
     enter(astInx,fragFormNo)
     do search: 
          FG.fragmentList.scan
          (# fragmentFormCounter: @integer;
             start::  (#  do 1->fragmentFormCounter #)
          do current.open;
             (if current.f.type = mps.formType then
                  (if fragmentFormCounter = fragFormNo then
                      current.f[] -> FF[];
                      leave search
                   else
                      fragmentFormCounter+1->fragmentFormCounter
                  if)  
          if)#);
        &mps.ASTindex[] -> objindex[];
        (astInx,FF[]) -> objIndex;
        (objIndex,screen[],100,true)->MPS.PrettyPrinter;        
     #);
do MPS;
   &MPS.treeLevel[] -> beta[];
   ('~beta/grammars/beta/beta','beta',screen[])
     ->beta.betagrammarInit;    

   (if noOfArguments > 2 then
       2 -> arguments -> B.restore;
       (3 -> arguments,screen[]) -> mps.top.open -> FG[]
    else
       '\nUsage: dumpdb filename.db ../filename\n'->puttext
   if);
   (* producing test output *)
   'no. of fragments: '->putText; B.getShort->noOfFrag->putInt; newline;
   'FN length: '->putText; B.getLong->putInt; newline;
   
   (for i:noOfFrag repeat
        'Fragment: '->putText; B.gettext->putText;  newline;
        'Fragment type: '->putText; B.getShort->putInt; newline;
   for);
   
   (* 'code size: '->putText; B.getLong->putInt;  newline;
    *
    * 'Prototypes: \n'->putText;
    * (for i: noOfFrag repeat
    *    L: (#
    *       do B.getShort->V->putInt; newline;
    *          (if V//0 then else restart L if)
    *       #)
    * for);
    *)
   
   (for I: noOfFrag repeat
        'No.of. descriptors: '->putText; 
        B.getShort->noOfDesc->putInt; newline;
        (for k: noOfDesc repeat
             '\n**** Do-part at AST index: ' -> putText; 
             B.getShort -> V -> putInt; 
             newline;
             (V,I) -> PP;
             L: (#
                do B.getShort->V->putInt;
                   (if V = 65535 (* -1,NOTE: may not work on all machines*)
                       then newline
                    else 
                       ','->put; 
                       B.getShort -> V -> putInt; 
                       newline;
                       (V,I) -> PP;
                       restart L 
                   if)
                #);
        for)
   for)
#)
