ORIGIN '../CONTROL/system';
LIB_ITEM 'betacodegen';
INCLUDE '../CONTROL/misk';
INCLUDE '../CONTROL/touchdir';
INCLUDE '~beta/sysutils/private/pathhandlerbody' (* removeDoubleCh *);
INCLUDE '~beta/toollibs/utils/fileExtensions';
BODY 'asmlinkbody';
(* 16/9/00: NOTE!!! A restructuring of asmlink has been made.
 * The stuff in this file has been moved from CONTROL/translate;
 * The stuff that used to be here is move to asmlinkbody
 ***********************************************************
 * Code for generation of the job-file;
 * the job-file handles 
 * - assembly - if needed - only needed for HPUX9PA -
 * - and linking
 *)
---translate_AsmLink:descriptor---
(# <<SLOT translateasmlinklib:attributes>>;
   init:< (# <<SLOT asmlink_init:doPart>> #);
   
   fSwitch,  (* name of C floating point library *)
   BetaRun,  (* file-name for BETA run-time system *)
   rootFragment, (* the file being translated *)
   ObjName,  (* file-name for object program *)
   objDirectory, (* directory holding object program *)
   codeDirectory: (* directory holding object files and jobfile*)^Text;
   NoLink: @boolean; (* true ==> do not generate link directive *)
   
   (* The different kinds of files that can be put into the job file *) 
   BetaEnvKind: (# exit 1 #);
   BetaKind: (# exit 2 #);
   ObjKind:  (# exit 3 #);  
   LibKind:  (# exit 4 #);
   MakeKind: (# exit 5 #);
   LinkOptKind:  (# exit 6 #); (* KJM *)
   ResourceKind: (# exit 7 #); (* KJM *)
   BetaDataKind: (# exit 8 #);
   ByteCodeKind: (# exit 9 #);      (* a bytecode class *)
   ByteCodeFileKind: (# exit 10 #); (* a file that was not translated
                                     * asmlink should add the class-list
                                     * for this file to the 'link' directive
                                     *)
   
   JobfileMachine: ^text;  (* The name of the machine on which the jobfile
                            * will be executed. If = NONE then the jobfile
                            * is executed on THIS machine.
                            *)
   CreateCodeDirectory: <<SLOT createCodeDirectory:descriptor>>;
   CreateObjDirectory: <<SLOT createObjDirectory:descriptor>>;
   TextLst: singleLinkedList
     (# Insert::<
          (* Makefiles are ``expensive'' to execute, so when makefiles
           * are inserted we check for duplicates.
           *) 
          (# FullFN: ^text;
             kind: @integer;
             assemble,inLib: @boolean;
          enter(FullFN[],kind,assemble,inLib)
          <<SLOT asmlink_TextLst_insert:doPart>>
          #);  
        
        Elm::< (# T: ^Text; assemble,inLib: @boolean; kind: @integer; #);
        (* inLib is true of the object file is in a library.
         * For systems with (shared) libraries, such object files
         * should not be included in the link directive
         *)
     #);   
   
   TX: @TextLst;
   
   Libs: @ singleLinkedList
     (# elm::< (# T: ^text; doLink: @boolean; args: ^text #);
        insert::<
          (# T,linkArgs: ^text
          enter T[]
          <<SLOT asmlink_Libs_insert:doPart>>
          exit linkArgs[]
          #)
     #);
   
   build: @ singleLinkedList
     (# elm:: (# directory,command: ^text #);
        insert::
          (# d,c: ^text
          enter(d[],c[])
          <<SLOT asmlink_build_insert:doPart>>
          #);
     #);
   AsmExt: (# ext: @text do <<SLOT AsmExt: descriptor>> exit ext[] #);
   BinExt: (# ext: @text do <<SLOT BinExt: descriptor>> exit ext[] #);
   LibExt: (# ext: @text do <<SLOT LibExt: descriptor>> exit ext[] #);
   JobExt: (# ext: @text do <<SLOT JobExt: descriptor>> exit ext[] #);
   ExeExt: (# ext: @text do <<SLOT ExeExt: descriptor>> exit ext[] #);
   
   hasSharedLibs: booleanValue
     (#
     do <<SLOT hasSharedLibs:descriptor>> ;
        (*(if not common.switch[63] then false -> value if)*)
     #);
   
do <<SLOT AsmLink: descriptor>>
#)
