ORIGIN '../asmlinkbody';
BODY '../UNIXasmlink';
LIB_ITEM 'betacodegen';

-- lib: attributes --
unQuote:
  (# t: ^text;
  enter t[]
  do (if ((1 -> t.inxGet) = '"') AND ((t.length -> t.inxGet) = '"') then
         (2, t.length - 1) -> t.sub -> t[];
     if);
  exit t[]
  #);
containerList: list (# #);

-- streamLib: attributes --
continue:
  (#
  do 
  #);

-- asmlinklib: attributes --

textList: containerList
  (# element:: text;
  #);


linkerName:
  (# name: ^text;
  do 'cc ' -> name[];
  exit name[]
  #);


continue:
  (#
  do '\\' -> AFF.put; AFF.newLine; ascii.ht -> AFF.put;
  #);
strip:
  (# in: ^Text;
     out: ^Text;
     slash: @integer;
  enter in[]
  do in.scanAll
     (# inx: @integer;
     do inx+1 -> inx;
        (if ch = '/' then
            inx -> slash;
        if);
     #);
     
     (slash+1, in.length-1) -> in.sub -> out[];
     'macosx/' -> out.prepend;
     out[] -> quote -> out[];
  exit out[]
  #);

--- asmlinkdynamic:descriptor --- (# #)


--asmState:descriptor--
(# 
   job: @File;
   
#)
--- hasSharedLibs:descriptor(*dopart*) ---
(#
do false->value;
#)
--- cont: descriptor --
(#
do '\\' ->Aff.put; aff.newLine; ascii.ht->aff.put;
#)
--- header: descriptor ---
(# 
do '#! /bin/sh '->AFF.putLine;
   'CC=cc' -> AFF.putline;
   
#)
--- AsmExt: descriptor --- 
(# 
do (if common.switch[34] then (* obsolete? *)
       '..s'->ext
    else
       (if common.switch[120] then
           '..BETA-'->ext
        else
           '-BT-'->ext
   if)if)
#)
--- BinExt: descriptor --- 
(# do '.o'->ext #)
---LibExt: descriptor ---
(# do '.a'->ext #)
---ExeExt:descriptor---
(* the extension for an executable file is empty 
 * on most platforms except Windows NT 
 *)
(#do ''->ext #)
--- JobExt: descriptor ---
(# do '.job'->ext #)
--- Echo: descriptor ---
(# 
do 'echo ' ->AFF.puttext;
#)
--- Remove: descriptor ---
(# 
do 'rm -f '->AFF.puttext; name[]->quote->AFF.putline; 

#)
---ChangeDirectory:doPart---
do 'cd '->AFF.puttext; dir[] -> quote -> AFF.putline;
   
---AddCommand:doPart---
do cmd[] -> AFF.putline;
   
--- Make: descriptor ---
(# 
do 'make" '->AFF.puttext;
   m[]->quote->AFF.putText; ' '->AFF.put; objName[]->quote->AFF.putLine;

#)
---Resource:descriptor---
(# i: @Integer;
   ext: @Text;
do m.reset;
   '.'->m.findAll(# do inx -> i #);
   (if (i>0) and (i<m.lgth)  then
       m.T[i+1:m.lgth] -> ext;
       (if true
        // ('rsrc'->ext.equalNCS) then 'betarez" '->AFF.puttext;
        // ('r'->ext.equalNCS) then 'BetaResource" '->AFF.puttext;
        else
           '\n**** Warning: unknown extension in resource: '->messagestream.puttext;
           m[]->messagestream.putline;
       if);
    else
       '\n**** Warning: no extension in resource: '->messagestream.puttext;
       m[]->messagestream.putline;
   if);
   m[]->quote->AFF.putText; ' '->AFF.put; objName[]->quote->AFF.putLine;
#)
(*--- RemoteShell: descriptor ---
 (# do ': '->rsh #)*)
--- assemble: descriptor ---
(#
do 
#)

--ldArgMax:descriptor--
(#do maxint->value #)

--ldTmp:descriptor--
(# 
   (*do tmpArgs[]
    ->ldUNIXtmpFile(#do 'ld -r -o '->AFF.putText #)
    ->TmpF[] *)
#)

--contCh:descriptor--
(#do '\\'->value #)

--- link: descriptor ---
(# betaLinkOpt: ^text;
do 'trap "echo :linking interrupted.; /bin/rm -f '->AFF.puttext;
   objName[]->AFF.puttext;
   '; exit 2" 2 9 10'->AFF.putLine;
   
   '$(BETALINKOPTIONS)' 
     -> expandEnvVar (# defaultValue::< (# do ' ' -> envvarValue[] #) #) 
     -> betaLinkOpt[];
   (if verboseLevel<=verboseLevel.verbose then
       '-v ' -> betaLinkOpt.append;
   if);
   '-Wl,-X' -> betaLinkOpt.append;
   (if not common.switch[41] then 
       (* beta --nodebug *) 
       ',-s' -> betaLinkOpt.append 
   if);
   
   'cc' -> AFF.puttext;
   ' -o '->AFF.puttext; objName[]->AFF.puttext; ' '->AFF.put; cont;
   betaLinkOpt[]->AFF.putText; ' '->AFF.put; cont;
   
#)
--linkLibraries:descriptor--
(#
do 'linkLibraries: ' -> putline;   
#)
--- linkObj:descriptor ---
(# 
do lobj[] -> ldA.putText; 
   ' \\\n\t'->ldA.putText;
#)
--- addBetaRun:descriptor ---
(* the following can be for most platforms except Windows NT *)
(#
do 
   
   (if userDefinedBetaRun then
       betaRun[] -> quote -> linkObj
    else
       asmLink.binExt -> betaRun.CopyAppend -> quote -> linkObj
   if)
#)
--- libraries: descriptor ---
(# 
do  '-lm' -> ldA.puttext;
   
#)
--- linktrailer: descriptor ---
(# 
do AFF.newline;
   'case $? in'->AFF.putLine;
   '0)'->AFF.putText; AFF.newline;
   (if verboseLevel<=verboseLevel.actions then
        'echo Object program on file: '->AFF.puttext; 
       objname[]->AFF.putLine;
   if);
   ';;'->AFF.putLine;
   '*)'->AFF.putLine; 
   '/bin/rm -f '->AFF.PutText;  
   objName[]->AFF.putLine;
   'esac'->AFF.PutLine;
#)
--- trailer: descriptor ---
(##)

