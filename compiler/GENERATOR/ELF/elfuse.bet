ORIGIN 'elffile';
INCLUDE 'elf_mips';

--program: descriptor--
(# bc: @elffile;
   shstr: ^text;
   putb: @bc.putbytes;
   
do 'Writing foo.o ... ' -> puttext;

   'foo.o' -> bc.name;
   bc.openWrite;
   
   (* Data for section string table *)
   'X.shstrtabX.symtabX.strtabX' -> shstr[];
   0 -> shstr.T[1];
   0 -> shstr.T[11];
   0 -> shstr.T[19];
   0 -> shstr.T[shstr.lgth];
   
   (* Construct elf header *)
   (
   0 (* No entry point *), 
   elfheadersize+shstr.lgth (* offset to section header table in foo.o *), 
   0 (* no flags *), 
   2 (* only undefined section and section string table present *), 
   1 (* index of .shstrndx in section header table *)
   ) -> bc.putEhdr;
   
   (* Write section string table *)
   (@@shstr.T[1], shstr.lgth) -> putb;
   
   (* Write the empty section header (always present) *)
   (0, SHT_NULL, 0, 0, 0, 0, SHN_UNDEF, 0, 0, 0) -> bc.putShdr;
    
   (* Write the section string table header *)
   (1 (* name is index 1 in .shstrtab *), 
   SHT_STRTAB (* type *), 
   SHF_ALLOC (* flags *), 
   0 (* address in executable unspecified *),
   elfheadersize (* Offset to section in file *), 
   shstr.lgth (* size of section *), 
   SHN_UNDEF (* no link info *), 
   0 (* No info *), 
   1 (* byte alignment *), 
   0 (* not fixed size entries *)
   ) -> bc.putShdr;

   bc.close;

   'done' -> putline;
#)

