ORIGIN '~beta/basiclib/binfile';
LIB_DEF 'elffile' '../../../lib';
INCLUDE 'elf';
BODY 'elffilebody';

(* elffile.bet:
 *   Pattern for manipulating object files in ELF format.
 *
 *   $Id: elffile.bet,v 1.5 2002-03-15 13:30:45 corry Exp $
 *)


--LIB: attributes--

elffile: binfile
  (# 
     <<SLOT ElfFileLib: attributes>>;
     
     putEhdr:
       (* Put ELF header on file - ignore program headers *)
       (# entry (* start address *),
          shoff (* section header offset *),
          flags (* file flags *),
          shnum (* number of section headers *),
          shstrndx (* Section header string index *): @integer;
          putit: @<<SLOT ElfFilePutEhdr: descriptor>>;
       enter (entry, shoff, flags, shnum, shstrndx)
       do putit;
       #);

     putShdr:
       (* Put a section header on file *)
       (# sh: @Elf32_Shdr;
       enter 
          (sh.sh_name, 
          sh.sh_type,
          sh.sh_flags,
          sh.sh_addr, 
          sh.sh_offset,
          sh.sh_size, 
          sh.sh_link, 
          sh.sh_info, 
          sh.sh_addralign,
          sh.sh_entsize)
       <<SLOT ElfFilePutShdr: dopart>>
       #);
     
     putSymEnt:
       (* Put a symbol table entry on file *)
       (# sym: @Elf32_Sym
       enter (sym.st_name,
          sym.st_value,
          sym.st_size,
          sym.st_info,
          sym.st_other,
          sym.st_shndx)
       <<SLOT ElfFilePutSymEnt: dopart>>
       #);
     
     putRelEnt:
       (* Put a relocation entry on file *)
       (# rel: @Elf32_Rel;
       enter (rel.r_offset, rel.r_info)
       <<SLOT ElfFilePutRelEnt: dopart>>
       #); 
     
     putRelaEnt:
       (* Put a relocation entry with addend on file *)
       (# rel: @Elf32_Rela;
       enter (rel.r_offset, rel.r_info, rel.r_addend)
       <<SLOT ElfFilePutRelaEnt: dopart>>
       #); 
     
  #);
