ORIGIN 'image';
LIB_ITEM 'image';

--ImageLib: attributes--

(* FIXME: these should use formatio instead of printf *)

puthex: 
  (# i: @integer;
     printf: external
       (# fmt: [1]@char;
          val: @integer
       enter (fmt, val)
       #);
  enter i
  do ('%08x', i) -> printf;
  #);
putstring12: 
  (# s: ^text;
     printf: external
       (# fmt: [1]@char;
          val: [0]@char
       enter (fmt, val)
       #);
  enter s[]
  do ('%-12s', s) -> printf;
  #);

--ImageMarkAsText: dopart--
do (if mark
    // entryDefMark then 'entryDefMark'->desc[]
    // wordMark then 'wordMark'->desc[]
    // jmpMark then 'jmpMark'->desc[]
    // dataMark then 'dataMark'->desc[]
    // codeMark then 'codeMark'->desc[]
    // callMark then 'callMark'->desc[]
    // bgtuMark then 'bgtuMark'->desc[]
    // setHiMark then 'setHiMark'->desc[]
    // setLoMark then 'setLoMark'->desc[]
    // setLoMarkSt then 'setLoMarkSt'->desc[]
    // setLoMarkJmp then 'setLoMarkJmp'->desc[]
    // setLoMarkLdd then 'setLoMarkLdd'->desc[]
    // setLoMarkLd then 'setLoMarkLd'->desc[]
    // TOCstart then 'TOCstart'->desc[]
    // TOCmarkDS then 'TOCmarkDS'->desc[]
    // TOCmarkRW then 'TOCmarkRW'->desc[]
    // TOCmarkTC then 'TOCmarkTC'->desc[]
    // TOCwordMark then 'TOCwordMark'->desc[]
    // lo16Mark then 'lo16Mark' -> desc[];
    // hi16Mark then 'hi16Mark' -> desc[];
    // ha16Mark then 'ha16Mark' -> desc[];
   if)
   
--ImageDisplay: dopart--
do 'LIP: 0x'->screen.puttext; LIP->puthex; screen.newline;
   (if buffer_ptr<>0 then
       'buffer_ptr:' -> screen.putline;
       (for i:(buffer_size div 4) repeat 
            %getLongAt (buffer_ptr+4*(i-1))->puthex; ' '->screen.put;
            (if i mod 8 = 0 then
                screen.newline;
            if);
       for);
    else
       'buffer:' -> screen.putline;
       (for i:(LIP div 4) repeat 
            buffer[i]->puthex; ' '->screen.put;
            (if i mod 8 = 0 then
                screen.newline;
            if);
       for);
   if);
   screen.newline;
   'noOfRel: 0x'->screen.puttext; noOfRel->puthex; screen.newline;
   'marks: 0x' -> screen.puttext; marktop -> puthex; screen.newline;
   'offset      type          info' -> screen.putline;
   (for i:marktop repeat
        '0x' -> screen.puttext;
        marks[i] -> puthex;
        '  '->screen.puttext;
        types[i] -> mark_astext -> putstring12;
        (if epElm[i][]<>NONE then
            '  "'->screen.puttext;
            epElm[i].T[] -> screen.puttext;
            '",' -> screen.puttext;
            (if epElm[i].data then 
                'data,' -> screen.puttext;
             else
                'text,' -> screen.puttext;
            if); 
            (if epElm[i].local then 
                'local,' -> screen.puttext;
            if); 
            (if epElm[i].export then 
                'export,' -> screen.puttext;
            if); 
            'LIP=0x'->screen.puttext;
            epElm[i].LIP -> puthex;
        if);
        screen.newline;
   for);
