ORIGIN '../asmlinkbody';
INCLUDE '~beta/unixlib/unixfile'
--asmState:descriptor--
(# tmpCount: @integer #)
--HasSharedLibs:descriptor--
(#do false -> value #)
--asmlinklib: attributes ---
stripPath:
  (# T: ^text; pos: @integer
  enter T[]
  do thePathhandler.directoryChar->T.findAll(#do inx->pos #);
     (if pos <> 0 then (pos+1,T.length)->T.sub->T[] if)
  exit T[]
  #);
ldUNIXtmpFile:
  (# tmpArgs,TmpF: ^text
  enter tmpArgs[]
  do  (if asmState.tmpCount//0 then
          'trap "echo :linking interrupted.; '->AFF.puttext;
          rmTmpFiles;
          '; exit 2" 2 9 10'->AFF.putLine;
      if);
     &text[]->TmpF[]; 
     '${TMPDIR}/'->TmpF; (* NOTE wont work if concurrent compilations *)
     objName[]->stripPath->TmpF.puttext; '.$$.'->TmpF.putText;
     asmState.tmpCount+1->asmState.tmpCount->TmpF.putInt;
     (false, TmpF[])->remove;
     INNER;
     TmpF[]->AFF.putText; 
     ' \\\n\t' ->AFF.putText;
     tmpArgs[]->AFF.putText;
  exit tmpF[]
  #);
rmTmpFiles:
  (#
  do (* patter 'remove' could be used here *)
     '/bin/rm -f '->AFF.puttext;
     '${TMPDIR}/'->AFF.putText; objName[]->stripPath->AFF.putText; '.$$.*'->AFF.puttext;
  #)   
---cont: descriptor --
(#
do '\\'->AFF.put; AFF.newLine; ascii.ht->AFF.put;
#)
---header: descriptor ---
(#
do '#! /bin/sh '->AFF.putLine;
   'BETALIB='->AFF.putText; BetaLib[]->AFF.putLine;
   'export BETALIB'->AFF.putLine;
   'CC=${CC-"gcc -O6"}\nexport CC'-> AFF.putLine;
   'MACHINETYPE='->AFF.putText; TargetMachine->AFF.putLine;
   'export MACHINETYPE'->AFF.putLine;
   'TMPDIR=${TMPDIR-/tmp}'->AFF.putLine;
   'BetaAsm(){'->AFF.putLine;
   '  trap "echo :interrupted.; /bin/rm -f $1.o; exit 2"  2 9 10'
     ->AFF.putLine;
   (if verboseLevel<=verboseLevel.actions then
       '  echo assembling $1..s'->AFF.putLine
   if);
   '  if as -o $1.o $1..s'->AFF.putLine;
   '  then'->AFF.putLine;
   (if switch[18]//false then 
       '    /bin/rm -f $1..s'->AFF.putLine 
    else
       '    return'->AFF.putLine
   if);
   '  else '->AFF.putLine;
   '    /bin/rm -f $1.o; echo  Aborted.; exit 1'->AFF.putLine;
   '  fi  '->AFF.putLine;
   '}'->AFF.putLine;
  
#)
---AsmExt: descriptor ---
(#do '..s'->ext #) 
---BinExt: descriptor ---
(#do '.o'->ext #)
---LibExt: descriptor ---
(#do '.so'->ext #)
---JobExt: descriptor ---
(#do '..job'->ext #)
---ExeExt:descriptor---
(* the extension for an executable file is empty 
 * on most platforms except Windows NT 
 *)
(#do ''->ext #)
---Echo: descriptor ---
(#do 'echo '->AFF.puttext #)
---Remove: descriptor ---
(#
do '/bin/rm -f '->AFF.puttext; name[]->AFF.putLine;
#)
---ChangeDirectory:doPart---
do 'cd '->AFF.puttext; dir[] -> AFF.putline
---AddCommand:doPart---
do cmd[] -> AFF.putline;
   'if [ $? -ne 0 ]; then echo BUILD failed. Exiting.; exit 1; fi' -> AFF.putline;
---Make: descriptor ---
(# 
do (if first then
       'trap "echo make interrupted.; exit 2" 2 9 10' ->AFF.putLine;
       'BetaMake(){'                                        -> AFF.putLine;
       '  dir=`dirname $1`; export dir'                     -> AFF.putLine;
       '  file=`basename $1`; export file'                  -> AFF.putLine;
       '  sh -c ''\\'                                       -> AFF.putLine;
       '    cd $dir; \\'                                    -> AFF.putLine;
       '    make -q -f $file MACHINETYPE=$MACHINETYPE; \\'  -> AFF.putLine;
       '    if [ $? -ne 0 ]; then \\'                       -> AFF.putLine;
       (if verboseLevel<verboseLevel.nothing then
           '        echo make $dir/$file; \\'                   -> AFF.putLine;
       if);
       '        make -f $file MACHINETYPE=$MACHINETYPE; \\' -> AFF.putLine;
       '        if [ $? -ne 0 ]; then exit 1; fi; \\'       -> AFF.putLine;
       '    fi; \\'                                         -> AFF.putLine;
       '    exit 0'''                                       -> AFF.putLine;
       '  if [ $? -ne 0 ]; then'                            -> AFF.putLine;
       '     echo Make failed. Exiting.'                    -> AFF.putLine;
       '     exit 1'                                        -> AFF.putLine;
       '  fi'                                               -> AFF.putLine;
       '}'                                                  -> AFF.putLine;
   if);
   (* test makefile dependencies *)
   'BetaMake '->AFF.putText; m[]->AFF.putLine;
#)
---resource: descriptor ---
(##)
---assemble: descriptor ---
(# 
do 'BetaAsm '->AFF.putText; F[]->AFF.putLine
#)
--linkLibraries:descriptor--
(* to be fixed *)
(#
do (if switch[53] then 
       Libs.scan
       (#
       do (if thisElm.args.length//0 then 
           else
              'echo linking shared lib: '->AFF.puttext; thisElm.T[]->AFF.putLine;
              
              'ld -dy -G \\\n\t-o '->AFF.puttext;
              thisElm.T[]->AFF.puttext; ' '->AFF.put;
              thisElm.args[]->AFF.putline
          if)
   #)if)
#)
--contCh:descriptor--
(#do '\\'->value #)
--ldArgMax:descriptor--
(#do 9000->value #)
--ldTmp:descriptor--
(#
do tmpArgs[]
     ->ldUNIXtmpFile(#do 'ld -r -o '->AFF.putText #)
     ->TmpF[] 
#)
---link: descriptor ---
(# betaLinkOpt: ^text; ldLibPath: ^text;
   start: @integer;
do 'trap "echo :linking interrupted.; /bin/rm -f '->AFF.puttext;
   objName[]->AFF.puttext;
   '; exit 2" 2 9 10'->AFF.putLine;
   
   '$(BETALINKOPTIONS)'
     ->expandEnvVar
   (# defaultValue::<
        (#
        do '-m elf_i386 -dynamic-linker /lib/ld-linux.so.1 -dc -dp -Bstatic -X'
             ->envvarValue[]
        #)
   #)
     ->betaLinkOpt[];
   (if not switch[41] then ' -s '->betaLinkOpt.append if);
   'ld '->AFF.puttext;
   
   (* FIXME: When sysutils/v1.5 is available, do
    * this instead:
    * 
    * ('LD_LIBRARY_PATH', ';') -> scanSearchPath
    *   (# do ' -L'->AFF.putText; path[]->AFF.puttext #);
    *)
   '$(LD_LIBRARY_PATH)'->expandEnvVar->ldLibPath[];
   (if ldLibPath.length//0 then (* skip *)
    else (* parse ':' separated list of directories *)
       1->start;
       ldLibPath.reset;
       ':'->ldLibPath.findAll
       (#
       do 
          (if (inx-1>start) then 
              ' -L'->AFF.putText; 
              (start,inx-1)->ldLibPath.sub->AFF.putText;
          if);
          inx+1->start;
       #);
       (if start-1<ldLibPath.length // true then
           ' -L'->AFF.putText; 
           (start,ldLibPath.length)->ldLibPath.sub->AFF.putText;
       if);
   if);
   
   ' '->AFF.put;
   betaLinkOpt[]->AFF.putText; ' -o '->AFF.puttext;
   asmlink.ExeExt->objName.copyAppend->AFF.puttext; 
   ' '->AFF.put; cont;
   
   (if switch[29] or switch[30] or switch[31] then
       ' /lib/mcrt0.o /lib/Fcrt1.o '->AFF.puttext
    else
       '/usr/lib/crt1.o' -> AFF.puttext;
       (if common.switch[13] (* full debug (betarun_debug.o) *) then
           ' /usr/lib/crti.o /usr/lib/crtbegin.o'->AFF.puttext;
       if);
   if);
   cont;
#)
---linkObj:descriptor ---
(#
do lobj[] -> ldA.putText; ' \\\n\t'->ldA.putText;
#)
---addBetaRun:descriptor---
(* the following can be for most platforms except Windows NT *)
(#
do (if userDefinedBetaRun then
       betaRun[]->linkObj
    else
       asmLink.binExt->betaRun.CopyAppend->linkObj
   if)
#)
--asmlinkdynamic:descriptor--
(#
do (if switch[17] then
       '-Bstatic \\\n\t'->ldA.puttext 
    else
       '-Bdynamic \\\n\t'->ldA.puttext
   if);
#)
---libraries: descriptor ---
(# 
do (if switch[17] then
       (* static X libraries requires -lSM -lICE *)
       ' -lSM -lICE' -> ldA.puttext;
   if);
   ' -lm -lc '->ldA.puttext; (* -lm includes sin, cos, exp,etc *)
   (if common.switch[13] (* full debug (betarun_debug.o) *) then
       ' /usr/lib/crtend.o /usr/lib/crtn.o' -> ldA.putline;
    else
       ldA.newline;
   if);
#)
---linkTrailer: descriptor ---
(# 
do     
   'case $? in'->AFF.putLine;
   '0)'->AFF.putText; AFF.newline;
   (if verboseLevel<=verboseLevel.actions then
        'echo Object program on file: '->AFF.puttext; 
       objname[]->AFF.putLine;
   if);
   (if asmState.tmpCount<>0 then rmTmpFiles; AFF.newLine if);
   ';;'->AFF.putLine;
   '*)'->AFF.putLine; 
   '/bin/rm -f '->AFF.PutText;  
   objName[]->AFF.putLine;
   (if asmState.tmpCount<>0 then rmTmpFiles; AFF.newLine if);
   'esac'->AFF.PutLine;
#)
---trailer: descriptor ---
(# #)
-- jobFilePermission: descriptor --
(# (* change permission on the jobfile so it can be excuted *)
   ue: @unixentry;
do AFF.name -> ue.path;
   ('a','x') -> ue.permission.add;
#)

