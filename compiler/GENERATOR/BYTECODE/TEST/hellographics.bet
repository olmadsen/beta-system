ORIGIN 'tstenv';

---PROGRAM:descriptor---  
(# 
do &HelloWorld[] -> Run; 
#)
  
---LIB:attributes---

default_font: (# exit false #);

HelloWorld: Form
  (# _init: 
       (# 
       do 'Hello Århus' -> set_Text; (* window title *)
          (*Color_White -> set_BackColor; FIXME: value type argument *)
       #);
     OnPaint: (*::*) proc
       (# e: ^PaintEventArgs; 
          g: ^Graphics;  
          f: ^Font; 
          b: ^Brush;
       enter e[]
       do (if default_font then 
              get_Font -> f[] (* HelloWorld's default font *)
           else
              ('Times New Roman', 26.0) -> &Font[] -> f[];
          if);
          e.get_Graphics -> g[];
          Brushes_CornflowerBlue -> b[];
          ('Hej Jørgen!', f[], b[], 15.0, 15.0) ->  g.DrawString;
       #);
  #);
 
String: ExternalClass
  (#  
  do '[mscorlib]System.String' -> classname;
  #);
Control: ExternalClass
  (# get_Font: proc (* Property *)
       (# f: ^Font;
       exit f[]
       #); 
     set_Text: proc (* Property *)
       (# t: ^String;
       enter t[]
       #);
  do '[System.Windows.Forms]System.Windows.Forms.Control' -> classname;
     INNER;
  #);
Form: Control 
  (# (* OnPaint:< proc
      *   (# e: ^PaintEventArgs;
      *   enter e[]
      *   do INNER;
      *   #);
      *)
     set_BackColor: proc (* Property *)
       (# c: ^Color;
       enter c[]
       #);
  do '[System.Windows.Forms]System.Windows.Forms.Form' -> classname;
     INNER;
  #);
PaintEventArgs: ExternalClass
  (# get_Graphics: proc (* Property *)
       (# g: ^Graphics
       exit g[]
       #);
  do '[System.Windows.Forms]System.Windows.Forms.PaintEventArgs' -> classname;
  #);
Graphics: ExternalClass
  (# DrawString: proc
       (# str: ^String;
          f: ^Font;
          b: ^Brush;
          x: @real32;
          y: @real32;
       enter (str[], f[], b[], x, y)
       #);
  do '[System.Drawing]System.Drawing.Graphics' -> classname;
  #);
Font: ExternalClass
  (# _init:
       (# name: ^String;
          size: @real32;
     enter (name[], size) 
     #);  
  do '[System.Drawing]System.Drawing.Font' -> classname;
  #);
Color: ExternalClass
  (# 
  do '[System.Drawing]System.Drawing.Color' -> classname;
     INNER;
  #);
Color_White: External (* Static method in struct Color *)
  (# c: ^Color;
  do 'valuetype [System.Drawing]System.Drawing.Color '
     '[System.Drawing]System.Drawing.Color::get_White()'
       -> callC;
  exit c[]
  #);
Brush: ExternalClass
  (# 
  do '[System.Drawing]System.Drawing.Brush' -> classname;
     INNER;
  #); 
SolidBrush: Brush 
  (# 
  do '[System.Drawing]System.Drawing.SolidBrush' -> classname;
     INNER;
  #);  
Brushes_CornflowerBlue: External (* Static method in class Brushes *)
  (# b: ^Brush
  do 'class [System.Drawing]System.Drawing.Brush '
     '[System.Drawing]System.Drawing.Brushes::get_CornflowerBlue()'
       -> callC;
  exit b[]
  #);
Run: External (* Static method in class Form *)
  (# f: ^Form
  enter F[]
  do 'void '
     '[System.Windows.Forms]System.Windows.Forms.Application::'
     'Run(class [System.Windows.Forms]System.Windows.Forms.Form)' 
       -> callC;
  #);
     
