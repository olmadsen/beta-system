ORIGIN 'google';
--LIB: attributes--

String: ExternalClass
  (# 
  do '[mscorlib]System.String' -> className;
  #);
ResultElement: ExternalClass
  (# URL: ^String;
  do '[GoogleSearchService]ResultElement' -> className;
  #);
GoogleSearchResult: ExternalClass
  (# resultElements: [0]^ResultElement;
  do '[GoogleSearchService]GoogleSearchResult' -> className;
  #);
GoogleSearchService: ExternalClass
  (# 
     doGoogleSearch: proc
       (# key: ^String;
          q: ^String;
          start: @int32;
          maxResults: @int32;
          filter: @boolean;
          restrict: ^String;
          safeSearch: @Boolean;
          lr, ie, oe: ^String;
          result: ^GoogleSearchResult;
       enter (
          key[], 
          q[], 
          start, 
          maxResults, 
          filter, 
          restrict[], 
          safeSearch, 
          lr[], 
          ie[], 
          oe[])
       exit result[]
       #) 
  do '[GoogleSearchService]GoogleSearchService' -> className;
  #);

--SearchGooglePrivate:descriptor--
(# google: ^GoogleSearchService;
   result: ^GoogleSearchResult;
#)

--DoSearchGoogle: dopart--
do (if private.google[]=NONE then
       &GoogleSearchService[] -> private.google[];
   if);
   ((*BetaLicenseKey*)'hweCKhQvbXY6kvKmOpDS2P3lTqQ5ggBu', (*FIXME *)
   query[], 
   0, 
   10, 
   false, 
   '', 
   false, 
   '', 
   'latin1', 
   'latin1')
     -> private.google.doGoogleSearch
     -> private.result[];
   (if false then
       'Number of results: ' -> puttext;
       private.result.resultElements.range -> putint;
       newline;
   if);
   
   (for i:private.result.resultElements.range repeat
        private.result.resultElements[i].URL[] -> current[];
        (if false then current[] -> putline; if);
        INNER;
   for);
#)
