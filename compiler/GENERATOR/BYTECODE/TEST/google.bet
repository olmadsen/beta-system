ORIGIN 'tstenv';
BUILD dotnet 'googleapi/dotnet/GoogleSearchService.dll' 'googleapi/dotnet/GoogleSearchService.cs' 'csc -target:library -out:$0 $1';

(* Do a search on the Google web service.
 * Requires that you go to http://www.google.com/apis/ and from here
 *   1. get yourself a license key and replace the one below with it (please!)
 *   2. download the API and 
 *   3. extract it to subdirectory 'googleapi'
 *)
---LIB:attributes---
BETAs_license_key:
  (* The Google API License key for beta@daimi.au.dk.
   * Used by default below.
   * PLEASE REPLACE with your own key, if you want to use this service.
   *)
  (# exit 'hweCKhQvbXY6kvKmOpDS2P3lTqQ5ggBu' #);
ResultElement: ExternalClass
  (# URL: @char; (* ^String *)
  do '[GoogleSearchService]ResultElement' -> className;
  #);
GoogleSearchResult: ExternalClass
  (# resultElements: [0]^ResultElement;
  do '[GoogleSearchService]GoogleSearchResult' -> className;
  #);
GoogleSearchService: ExternalClass
  (# 
     doGoogleSearch: proc
       (# key: [0]@char; (* ^String *)
          q: [0]@char; (* ^String *)
          start: @int32;
          maxResults: @int32;
          filter: @boolean;
          restrict: [0]@char; (* ^String *)
          safeSearch: @Boolean;
          lr, ie, oe: [0]@char; (* ^String *)
          result: ^GoogleSearchResult;
       enter (key, q, start, maxResults, filter, restrict, safeSearch, lr, ie, oe)
       exit result[]
       #) 
  do '[GoogleSearchService]GoogleSearchService' -> className;
  #)
---program:descriptor---
(# query: ^text;
   google: ^GoogleSearchService;
   result: ^GoogleSearchResult;
do (if false then 
       'Search term for Google: ' -> puttext;
       getLine -> Query[];
    else
       'Ole Lehrmann Madsen BETA.NET' -> Query;
   if);
   &GoogleSearchService[] -> google[];
   (BETAs_license_key, query, 0, 10, false, '', false, '', 'latin1', 'latin1')
     -> google.doGoogleSearch
     -> result[];
   'Results for Google search: ' -> putText; query[] -> putLine;
   (for i:result.resultElements.range repeat
        i -> putInt; '. ' -> putText;
        result.resultElements[i].URL -> putText;
        newline;
   for); 
#)
   


