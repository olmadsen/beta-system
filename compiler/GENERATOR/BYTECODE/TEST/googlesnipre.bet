ORIGIN 'googlesearch';  

(* Extended google demo: 
 * Uses .NET regular expressions to filter out HTML from snippet
 *)

--LIB: attributes--
String: ExternalClass
  (# 
  do '[mscorlib]System.String' -> className;
  #);
Regex: ExternalClass
  (# _init:
       (# pattern: ^String;
       enter pattern[]
       #);
     Replace: proc
       (# str, replacement, result: ^String;
       enter (str[], replacement[])
       exit result[]
       #);
  do '[System]System.Text.RegularExpressions.Regex' -> classname;
  #);

--PROGRAM:descriptor--
(# query: ^text;
   i: @integer;
   paras, breaks, tags, greater, less, amp: ^Regex;
do &text[] -> query[]; 
   'Search term for Google: ' -> puttext;
   getLine -> query[];
   (if query.length=0 then
       'Ole Lehrmann Madsen BETA.NET' -> query[];
   if);
   (* Regular expressions for matching HTML elements *)
   '<p>'     -> &Regex[] -> paras[]  (* HTML paragraph break tags *);
   '<br>'    -> &Regex[] -> breaks[] (* HTML line break tags *);
   '<[^>]+>' -> &Regex[] -> tags[]   (* match HTML tags *);
   '&gt;'    -> &Regex[] -> greater[](* match HTML greater *);
   '&lt;'    -> &Regex[] -> less[]   (* match HTML less *);
   '&amp;'   -> &Regex[] -> amp[]    (* match HTML ampersand *);
   'Results for Google search: ' -> putText; query[] -> putLine;
   (BetaLicenseKey, query[]) 
     -> SearchGoogle(#  
                    do newline;
                       (* i -> putInt; '. ' -> putText; *)
                       'URL: ' -> puttext;
                       url[] -> putline;
                       'Snippet:' -> putline;
                       (snippet[], '\n') -> paras.Replace  -> snippet[];
	               (snippet[], '\n') -> breaks.Replace -> snippet[];
	               (snippet[], '')   -> tags.Replace   -> snippet[];
	               (snippet[], '<')  -> less.Replace   -> snippet[];
	               (snippet[], '>')  -> greater.Replace-> snippet[];
	               (snippet[], '&')  -> amp.Replace    -> snippet[];
                       snippet[] -> putText;
                       newline; 
                       i+1 -> i;
                    #); 
#)

   
