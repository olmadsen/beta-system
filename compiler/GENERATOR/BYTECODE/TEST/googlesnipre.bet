ORIGIN 'googlesearch';  

(* Extended google demo: 
 * Uses .NET regular expressions to filter out HTML from snippet
 *)

--LIB: attributes--
String: ExternalClass
  (# 
  do '[mscorlib]System.String' -> className;
  #);
Regex: ExternalClass
  (# _init:
       (# pattern: ^String;
       enter pattern[]
       #);
     Replace: proc
       (# str, replacement, result: ^String;
       enter (str[], replacement[])
       exit result[]
       #);
  do '[System]System.Text.RegularExpressions.Regex' -> classname;
  #);

--PROGRAM:descriptor--
(# query: ^text;
   i: @integer;
   paras, breaks, tags: ^Regex;
do &text[] -> query[]; 
   'Search term for Google: ' -> puttext;
   getLine -> query[];
   (if query.length=0 then
       'Ole Lehrmann Madsen BETA.NET' -> query[];
   if);
   (* Regular expressions for matching HTML elements *)
   '<p>'     -> &Regex[] -> paras[]  (* HTML paragraph break tags *);
   '<br>'    -> &Regex[] -> breaks[] (* HTML line break tags *);
   '<[^>]+>' -> &Regex[] -> tags[]   (* match HTML tags *);
   'Results for Google search: ' -> putText; query[] -> putLine;
   (BetaLicenseKey, query[]) 
     -> SearchGoogle(#  
                    do newline; 
                       i -> putInt; '. ' -> putText;
                       url -> putText;
                       newline; 
                       'Snippet:' -> putline;
                       (snippet[], '\n') -> paras.Replace  -> snippet[];
	               (snippet[], '\n') -> breaks.Replace -> snippet[];
	               (snippet[], '')   -> tags.Replace   -> snippet[];
                       snippet -> putText;
                       newline; 
                       i+1 -> i;
                    #); 
#)

   
