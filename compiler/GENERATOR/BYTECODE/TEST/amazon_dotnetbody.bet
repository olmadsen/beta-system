ORIGIN 'amazonsearch';
BUILD dotnet 'amazon/dotnet/amazon.dll' 'amazon/dotnet/amazon.cs'       'csc  -nologo -target:library -debug -out:$0 $1';
BUILD dotnet 'amazon/dotnet/amazon.cs'  'amazon/AmazonWebServices.wsdl' 'mkdir amazon/dotnet; wsdl -nologo -language:CS -out:$0 $1';
--LIB: attributes--

String: ExternalClass
  (# 
  do '[mscorlib]System.String' -> className; 
  #);
AmazonSearchService: ExternalClass
  (# AuthorSearchRequest: proc
       (# req: ^AuthorRequest;
          info: ^ProductInfo;
       enter req[]
       exit info[]
       #);
  do '[amazon]AmazonSearchService' -> classname;
  #);
ProductInfo: ExternalClass
  (# TotalResults: ^String;
  do '[amazon]ProductInfo' -> classname;
  #);
AuthorRequest: ExternalClass
  (# author, page, mode, tag, type, devtag, sort: ^String;
  do '[amazon]AuthorRequest' -> classname;
  #)

--SearchAmazonPrivate:descriptor--
(# ws: ^AmazonSearchService;
   info: ^ProductInfo;
   author: ^AuthorRequest;
#)

--DoSearchAuthor: dopart--
do (if private.ws[]=NONE then
       &AmazonSearchService[] -> private.ws[];
   if);
   (if private.author[]=NONE then
       &AuthorRequest[] -> private.author[];
   if);
   
   author[]         -> private.author.author[];
   token[]          -> private.author.devtag[];
   'heavy'          -> private.author.type[];
   'books'          -> private.author.mode[];
   'webservices-20' -> private.author.tag[];
   '1'              -> private.author.page[];
   
   private.author[] -> private.ws.AuthorSearchRequest -> private.info[];
    
   private.info.TotalResults[] -> screen.putline;
   
#)
   
   
 
