ORIGIN 'betaenv'
--- program: descriptor ---
(* Display the lyrics to "99 Bottles of Beer"  *)
(# bottles: @integer;
   putBottles: 
     (# high, low: @integer;
        few:
          (# i: @integer;
          enter i
          do (if i
              // 1 then  'one' -> puttext;
              // 2 then  'two' -> puttext;
              // 3 then  'three' -> puttext;
              // 4 then  'four' -> puttext;
              // 5 then  'five' -> puttext;
              // 6 then  'six' -> puttext;
              // 7 then  'seven' -> puttext;
              // 8 then  'eight' -> puttext;
              // 9 then  'nine' -> puttext;
              // 10 then 'ten' -> puttext;
              // 11 then 'eleven' -> puttext;
              // 12 then 'twelve' -> puttext;
              // 13 then 'thirteen' -> puttext;
              // 14 then 'fourteen' -> puttext;
              // 15 then 'fifteen' -> puttext;
              // 16 then 'sixteen' -> puttext;
              // 17 then 'seventeen' -> puttext;
              // 18 then 'eighteen' -> puttext;
              // 19 then 'nineteen' -> puttext;
             if);
          #);
     do bottles div 10 -> high;
        bottles mod 10 -> low;
        (if high
         // 0 then
            (if low=0 then 
                'no more' -> puttext;
             else 
                bottles -> few;
            if);
         // 1 then bottles   -> few;
         // 2 then 'twenty'  -> puttext; low -> few;
         // 3 then 'thirty'  -> puttext; low -> few;
         // 4 then 'fourty'  -> puttext; low -> few;
         // 5 then 'fifty'   -> puttext; low -> few;
         // 6 then 'sixty'   -> puttext; low -> few;
         // 7 then 'seventy' -> puttext; low -> few;
         // 8 then 'eighty'  -> puttext; low -> few;
         // 9 then 'ninety'  -> puttext; low -> few;
        if);
        (if bottles=1 then
            ' bottle of beer' -> puttext;
         else
            ' bottles of beer' -> puttext;
        if);
     #);
do 99->bottles;
   drinking:
     (if bottles>0 then
         putBottles; ' on the wall, '->puttext;
         putBottles; '.' -> putline;
         'Take one down, pass it around, '->puttext;
         bottles-1->bottles;
         putBottles;
         ' on the wall.\n'->putline;
         restart drinking
     if);
#)
