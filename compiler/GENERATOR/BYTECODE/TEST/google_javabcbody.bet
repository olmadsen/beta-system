ORIGIN 'googlesearch'; 
INCLUDE 'javastring';
LIBFILE javabc 'googleapi/googleapi.jar';

--LIB: attributes--

trace: 
  (# exit false #);

GoogleSearch: ExternalClass
  (# setKey: proc 
       (# key: ^String;
       enter key[]
       #);
     setMaxResults: proc
       (# max: @integer
       enter max
       #);
     setQueryString: proc
       (# q: ^String;
       enter q[]
       #);
     doSearch: proc
       (# r: ^GoogleSearchResult
       exit r[]
       #);
  do 'com/google/soap/search/GoogleSearch' -> classname;
  #);

GoogleSearchResult: ExternalClass
  (# toString: proc
       (# s: ^String;
       exit s[]
       #);
     getResultElements: proc
       (# resultElements: [0]^GoogleSearchResultElement
       exit resultElements
       #);
  do 'com/google/soap/search/GoogleSearchResult' -> className;
  #);

GoogleSearchResultElement: ExternalClass
  (# getURL: proc
       (# url: ^String;
       exit url[]
       #);
     getSnippet: proc
       (# snippet: ^String;
       exit snippet[]
       #);
  do 'com/google/soap/search/GoogleSearchResultElement' -> className;
  #);


--SearchGooglePrivate:descriptor--
(# google: ^GoogleSearch;
   result: ^GoogleSearchResult;
   resultElements: [0]^GoogleSearchResultElement;
#)

--DoSearchGoogle: dopart--
do (if private.google[]=NONE then
       &GoogleSearch[] -> private.google[];
       BetaLicenseKey -> private.google.setKey;
       10 -> private.google.setMaxResults;
   if);
   query[] -> private.google.setQueryString;
   private.google.doSearch -> private.result[];
   
   (if trace then
       'google result: ' -> putline;
       private.result.toString -> putline;
   if);
   
   private.result.getResultElements -> private.resultElements;
   
   (if trace then
       'Number of results: ' -> puttext;
       private.resultElements.range -> putint;
       newline;
   if);
   
   (for i:private.resultElements.range repeat
        private.resultElements[i].getURL -> url[];
        private.resultElements[i].getSnippet -> snippet[];
        (if false then url[] -> putline; if);
        INNER;
   for);
   
