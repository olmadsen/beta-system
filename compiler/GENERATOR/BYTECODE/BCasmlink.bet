ORIGIN '../asmlinkbody' ;
INCLUDE '~beta/basiclib/fileSetExe';

(* verboseLevels:
 * verbose: (# exit 0 #); {* more than default? *}
 * default: (# exit 1 #); {* normal, the old way, open, bind, etc. *}
 * actions: (# exit 2 #); {* only if somthing happens, parse, etc.*}
 * nothing: (# exit 3 #); {* nothing, only errors *}
 *)

--lib: attributes--
use_dotnet_betarun:
  (# exit false #);
use_dotnet_modules:
  (# exit false #);
main_in_program:
  (# exit true #);

generate_jar_manifest:
  (# exit true #);
use_java_classpath:
  (# exit true #);

unQuote:
  (# t: ^text;
  enter t[]
  do (if ((1 -> t.inxGet) = '"') AND ((t.length -> t.inxGet) = '"') then
         (2, t.length - 1) -> t.sub -> t[];
     if);
  exit t[]
  #);
escDollar:
  (# T,S: ^text
  enter T[]
  do &text[] -> S[];
     T.scanAll
     (#
     do (if ch = '$' then
            '\\' -> S.put
        if);
        ch -> S.put
     #);
  exit S[]
  #);
TODO: 
  (# T: ^text enter T[] 
  do 'TODO: ' -> screen.puttext;
     T[] -> screen.putline 
  #);
AppendExe:
  (# objname: ^text;
  enter objname[]
  do '.exe' -> objname.appendExtension
  exit objname[]
  #);
SlashToBackslash: 
  (# T: ^Text;
  enter T[]
  do (for i:T.lgth repeat
          (if T.T[i] = '/' then '\\' -> T.T[i] if)
     for);
  exit T[]
  #);
BackslashToSlash: 
  (# T: ^Text;
  enter T[]
  do (for i:T.lgth repeat
          (if T.T[i] = '\\' then '/' -> T.T[i] if)
     for);
  exit T[]
  #);

--translateasmlinklib: attributes ---
isJava:  
  (# exit common.targetMachineId = common.javabc #);
isIL:    
  (# exit common.targetMachineId = common.dotnet #);
hostIsWindows: booleanValue
  (# 
  do ('nti'->((1,3)->(machine_type).sub).equal) -> value;
  #);

--asmState:descriptor--
(#  #)
--HasSharedLibs:descriptor--
(#do false -> value #)
---cont: descriptor --
(# 
do (if hostIsWindows then
       ' '->AFF.put;
    else
       '\\\n\t' -> AFF.puttext
   if);
#)
---header: descriptor ---
(#
do (if true
    // isIL or hostIsWindows then
       '@echo off\n'
         ->AFF.putText;
       (if isIL then
           'if exist "ilasm.out" (del "ilasm.out")\n'
             ->AFF.putText;
       if);
    // isJava then
       '#! /bin/sh '->AFF.putLine;
       'BETALIB='->AFF.putText; BetaLib[]->AFF.putLine;
       'export BETALIB'->AFF.putLine;
       'CC=${CC-"gcc -O6"}\nexport CC'-> AFF.putLine;
       'MACHINETYPE='->AFF.putText; TargetMachine->AFF.putLine;
       'export MACHINETYPE'->AFF.putLine;
       'TMPDIR=${TMPDIR-/tmp}'->AFF.putLine;
    else
       'AsmHeader: unknown platform' -> TODO; 
   if);
#)
---AsmExt: descriptor ---
(#
do (if true
    // isIL then
       '.il' -> ext;
    // isJava then
       '.J' -> ext
    else
       'AsmExt: unknown platform' -> TODO;
   if);
#) 
---BinExt: descriptor ---
(# 
do (if true
    // isIL then
       (if use_dotnet_modules then
           '.netmodule' -> ext;
        else
           '.dll' -> ext;
       if);
    // isJava then
       '.class' -> ext
    else
       'BinExt: unknown platform' -> TODO;
   if);
#)
---LibExt: descriptor ---
(# 
do 'LibExt'->TODO
#)
---JobExt: descriptor ---
(# 
do (if true
    // isIL or hostIsWindows then
       '.bat' -> ext;
    // isJava then
       '..job' -> ext
    else
       'JobExt: unknown platform' -> TODO;
   if);
#) 
---ExeExt:descriptor---
(# 
do (if true
    // isIL then
       '.exe' -> ext;
    else
       ''-> ext (* not used by Java *)
   if);
   #)
---Echo: descriptor ---
(# 
do (if true
    // isIL then
       'echo '->AFF.puttext;
    // isJava then
       'echo '->AFF.puttext;
    else
       'Echo: unknown platform' -> TODO;
   if);
#)
---Remove: descriptor ---
(# 
do (if true
    // isIL or hostIsWindows then
       (if check then
           'if exist "'->AFF.putText; name[]->AFF.putText; '" '->AFF.putText
       if);
       '(del "'->AFF.putText; name[]->AFF.putText; 
       (if verboseLevel > verboseLevel.verbose then
           '" >NUL'->AFF.puttext;
       if);
       ')' -> AFF.putline;
    // isJava then
       'rm -f '->AFF.puttext; name[]->AFF.putline (* only OK for unix *)
    else
       'remove' -> todo
   if);
#)
---ChangeDirectory:doPart---
do (if true
    // isIL or hostIsWindows then
       (if (dir.lgth>1) and (dir.T[2]=':') then
           (* change drive *)
           dir.T[1]->AFF.put; ':' -> AFF.put; AFF.newline;
       if);
       'cd '->AFF.puttext; dir[] -> AFF.putline
    // isJava then
       'cd '->AFF.puttext; dir[] -> AFF.putline
    else
       'ChangeDirectory'->TODO
   if);
       
---AddCommand:doPart---
do (if true
    // isIL or hostIsWindows then
       cmd[] -> AFF.putline;
       'if errorlevel 1 goto builderror' -> AFF.putLine;
    // isJava then
       cmd[] -> AFF.putline;
    else
       'AddCommand'->TODO
   if);
---Make: descriptor ---
(# 
do 'Make'->TODO
#)
---resource:descriptor---
(#
do 'resource'->TODO
#)
---assemble: descriptor ---
(# e: @diskentry;
   out: ^text;
   FF: ^text;
   
   GenerateDotnetVerify:
     (# 
     do (if common.switch[37] then
            AFF.newline;
            echo;
            'verifying '->AFF.puttext;
            F[] -> AFF.puttext;
            (if use_dotnet_modules then
                '.netmodule ' -> AFF.puttext;
             else
                '.dll ' -> AFF.puttext;
            if);
            AFF.newline;
            'peverify /nologo '->AFF.puttext; 
            F[] -> AFF.puttext;
            (if use_dotnet_modules then
                '.netmodule ' -> AFF.puttext;
             else
                '.dll ' -> AFF.puttext;
            if);
            AFF.newline;
            (if not common.switch[21] then
                'if errorlevel 1 goto verifyerror' -> AFF.putLine;
            if);
        if);
     #);
do (if common.switch[184] or common.switch[189] then
       asmext -> F.append;
   if);
   F[] -> e.path;
   (if not e.exists then
       (if isJava then
           (if common.switch[18] then
               (if not hostIsWindows then
                   F[] -> escDollar -> F[];
               if);
               (# FN: ^text
               do (F.length-1,F.length) -> (F.copy).delete -> FN[];
                  'jcf-dump -c "' -> AFF.puttext;
                  '.class' -> (FN.copy).append -> AFF.puttext;
                  '" > "' -> AFF.puttext;
                  '..j"' -> (FN.copy).append -> AFF.putline
           #)if)
        else           
           (if verboseLevel <= verboseLevel.verbose then
               'OOPS! AsmLink: skipping non-existing\n\t' -> screen.puttext;
               F[] -> screen.putline;
               ' (compiler error).' 
                 -> screen.putline;
       if)if)
    else
       (F.copy, '') -> thePathHandler.localPath -> FF[];
       (if verboseLevel <= verboseLevel.actions then
           AFF.newline;
           (if not isJava then
               echo;
               'assembling '->AFF.puttext;
               FF[] ->AFF.putline;
           if);
       if);
       (if true
        // isIL then
           'ilasm /nologo /dll '->AFF.puttext; 
           (if verboseLevel > verboseLevel.verbose then
               '/quiet ' ->AFF.puttext;
           if);
           (if true (*common.switch[41]*) then 
               '/debug '->AFF.puttext
           if);
           '/output:' -> AFF.puttext;
           '.il' -> F.stripextension;
           F[] -> AFF.puttext;
           (if use_dotnet_modules then
               '.netmodule ' -> AFF.puttext;
            else
               '.dll ' -> AFF.puttext;
           if);
           F[] -> AFF.puttext;
           (if verboseLevel > verboseLevel.verbose then
               ' >"ilasm.out" 2>&1'->AFF.puttext;
           if);
           AFF.newline;
           (if not common.switch[21] then
               'if errorlevel 1 goto asmerror' -> AFF.putLine;
           if);
           GenerateDotnetVerify;
        // isJava then
           (*'jasmin '->AFF.putText; F[]->AFF.putLine*)
        else
           'assemble: unknown platform' -> TODO;
       if);
   if);
#)
--linkLibraries:descriptor--
(#
do 'linkLibraries'->TODO
#)
--contCh:descriptor--
(# 
do '\\' -> value
#)
--ldArgMax:descriptor--
(# 
do 1000000 -> value
#)
--ldTmp:descriptor--
(# 
do 'ldTmp'->TODO
#)
---link: descriptor ---
(# e: @diskentry;
   MakeDotnetConfigFile:
     (# ConfigFileName: ^text;
     do ObjName.copy -> ConfigFileName[];
        '.exe.config' -> ConfigFileName.append;
        WriteConfig:
          (# f: @file
               (# error: 
                    (# 
                    do 'Failed to create '
                         ->(ConfigFileName.copy).prepend 
                         -> screen.putline; 
                       leave WriteConfig;
                    #);
                  AccessError::(# do error #);
                  WriteError::(# do error #);
                  NoSpaceError::(# do error #);
               #);
          do ConfigFileName[] -> f.name;
             (if f.entry.exists then
                 (* Will no overwrite existing .config file *)
                 '.new' -> ConfigFileName.append;
                 ConfigFileName[] -> f.name;
             if);
             f.openwrite;
             '<configuration>\n'
             '   <runtime>\n'
             '      <assemblyBinding xmlns="urn:schemas-microsoft-com:asm.v1">\n'
             '         <probing privatePath="dotnet"/>\n'
             '      </assemblyBinding>\n'
             '   </runtime>\n'
             '</configuration>\n'
               -> f.puttext;
             f.close;
          #)
     #);
   
   BuildDotnetBetarun:
     (# cs: @diskentry;
        bd: @diskentry;
        csc:
          (# 
          do 'csc /t:module /out:' -> AFF.puttext;
             e.path -> AFF.puttext;
             ' ' -> AFF.put;
             cs.path -> AFF.putline;
          #);
     do (if use_dotnet_betarun then
            (if not userDefinedBetaRun then
                ('~beta/betarun/CS/betarun.cs','') -> thePathHandler.convertFilePath -> cs.path;
                (if cs.exists then
                    '.dll' -> (betarun.copy).append -> e.path;
                    (if (not e.exists) or (e.modtime < cs.modtime) then
                        (betarunlib[], '') -> thePathHandler.convertFilePath -> bd.path;
                        (if (not bd.exists) then
                            echo; 'Creating directory ' -> AFF.puttext; bd.path -> AFF.putline;
                            bd.path -> (&directory[]).touchDir
                        if);
                        echo; 'Building ' -> AFF.puttext; e.path -> AFF.putline;
                        csc;
                    if);
                    'copy ' -> AFF.puttext; e.path -> AFF.puttext; ' .' -> AFF.putline;
                if)
            if)
        if)
     #);
   
   GenerateDotnetVerify:
     (# 
     do (if common.switch[37] then
            AFF.newline;
            echo;
            'verifying '->AFF.puttext;
            ObjName[] -> AFF.puttext;
            '.exe ' -> AFF.putline;
            'peverify /nologo '->AFF.puttext; 
            ObjName[] -> AFF.puttext;
            '.exe ' -> AFF.putline;
            (if not common.switch[21] then
                'if errorlevel 1 goto verifyerror' -> AFF.putLine;
            if);
        if);
     #)
        
do (if true
    // isJava then
       (if generate_jar_manifest then
           'echo Name: javabc/program.class >' -> AFF.puttext;
           objName[]->AFF.puttext;
           '.manifest' -> AFF.putline;
           'echo Main-Class: program >>' -> AFF.puttext;
           objName[]->AFF.puttext;
           '.manifest' -> AFF.putline;
           'jar cmf' -> AFF.puttext;
        else
           'jar cf' -> AFF.puttext;           
       if);
       (if verboseLevel <= verboseLevel.verbose then
           'v' ->AFF.puttext;
       if);
       ' '-> AFF.puttext; 
       (if generate_jar_manifest then
           objName[]->AFF.puttext;
           '.manifest ' -> AFF.puttext;
       if);
       CodeDirectory[] -> AFF.puttext;
       DirectoryChar -> AFF.put;
       objName[] -> e.path;
       e.path.name -> AFF.puttext; '.jar '->AFF.puttext; cont
    // isIL then
       (if use_dotnet_betarun then
           BuildDotnetBetarun;
           'al /nologo /t:exe /main:tstenv.main ' -> AFF.puttext;
           (if common.switch[41] then 
               (* debug info? *)
           if);
           '/out:' -> AFF.puttext;
           objName.copy -> AppendExe -> quote -> AFF.puttext;
        else
           'ilasm /nologo /exe '->AFF.puttext; 
           (if verboseLevel > verboseLevel.verbose then
               '/quiet ' ->AFF.puttext;
           if);
           (if true (*common.switch[41]*) then 
               '/debug '->AFF.puttext
           if);
           '/output:' -> AFF.puttext;
           ObjName[] -> AFF.puttext;
           '.exe ' -> AFF.puttext;
           'dotnet\\' -> AFF.puttext;
           (if main_in_program then
               'program' -> AFF.puttext;
            else
               objname[] -> AFF.puttext;
           if);
           '.il' -> AFF.puttext;
           (if verboseLevel > verboseLevel.verbose then
               '> NUL '->AFF.puttext;
           if);
           AFF.newline;
       if);
       (if not common.switch[21] then
           'if errorlevel 1 goto linkerror' -> AFF.putLine;
       if);
       GenerateDotnetVerify;
       MakeDotnetConfigFile;
    else
       'link' -> TODO;
   if);
#)

---addBetaRun:descriptor---
(#
do (if true
    // isJava then
       (* currently nothing *)
    // isIL then
       (if use_dotnet_betarun then
           (if userDefinedBetaRun then
               betaRun[]->linkObj
            else
               '.dll'->betaRun.CopyAppend->linkObj
           if)
       if)
    else
       'addbetarun' -> TODO;
   if);
#)
--asmlinkdynamic:descriptor--
(#
do 'asmlinkdynamic'->TODO
#)
---linkObj:descriptor ---
(# e: @diskentry;
   path, name: ^text;
   pwd: ^text;
do 
   (if true
    // isJava then
       lObj[] -> unquote -> name[];
       '.J.class' -> name.stripExtension;
       '.class' -> name.AppendExtension;
       name[] -> e.path;
       (if (not e.exists) and false then
           (* test disabled: Also make class files build by BUILD be skipped 
            * as they do not exist yet.
            *)
           (if verboseLevel <= verboseLevel.verbose then
               'OOPS! AsmLink: skipping non-existing\n\t' -> screen.puttext;
               name[] -> screen.putline;
               '(compiler error).' 
                 -> screen.putline;
           if);
        else
           (* Extract path of lObj as a relative path from current directory *)
           e.path.head -> path[];
           thePathHandler.CurrentDirectory -> pwd[];
           (if hostIsWindows then
               (* Make sure drive name is same case *)
               path.T[1] -> ascii.upcase -> path.T[1];
               pwd.T[1]  -> ascii.upcase -> pwd.T[1];
           if);
           (path[], pwd[]) -> thePathHandler.localPath -> path[];
           ' -C ' -> ldA.puttext;
           path[] -> quote -> ldA.puttext;
           ' ' -> ldA.put;
           e.path.name -> name[];
           (if hostIsWindows then
               name[] -> quote -> ldA.puttext;
            else
               name[] -> escDollar -> quote -> ldA.puttext;
               '\\\n\t' -> ldA.puttext
           if);
       if);
    // isIL then
       (if use_dotnet_modules then
           (* lObj = '"foo.il.netmodule"' *)
           '.il.netmodule"' -> lObj.stripExtension;
           '.netmodule"' -> lObj.appendExtension;
           ' '->ldA.puttext;
           (if not hostIsWindows then
               lObj[] -> escDollar -> quote -> ldA.putText; 
           if);
        else
           (* lObj = '"foo.il.dll"' *)
           '.il.dll"' -> lObj.stripExtension;
           '.dll"' -> lObj.appendExtension;
           (* File not inserted *)
       if);
    else
       'linkObj' -> TODO;
   if);
#)
---libraries: descriptor ---
(#
do 'libraries'->TODO
#)
--linktrailer:descriptor--
(# 
do (if isJava then AFF.newline; if);
#)
---trailer: descriptor ---
(# e: @diskentry;
   generateBatFileLabels:
     (# 
     do 'goto exit\n'
        '\n'
        ':builderror\n'
        'echo Error(s) during BUILD.\n'
        'goto exit\n'
        ':asmerror\n'
        'if exist "ilasm.out" (type "ilasm.out")\n'
        'echo Error(s) during assembling.\n'
        'goto exit\n'
        ':reserror\n'
        'echo Error(s) during resource compiling.\n'
        'goto exit\n'
        ':linkerror\n'
        'echo Error(s) during link\n'
        'goto exit\n'
          ->AFF.putText;
        (if common.switch[37] then
            ':linkerror\necho A Verification Error Occurred\ngoto exit'->AFF.putLine;
        if);
        AFF.newline;
        ':exit\n'
          ->AFF.putText;
     #);
   generateJavaWrapperScript:
     (# 
     do (if hostIsWindows then
            'echo @echo off    > '       -> AFF.puttext; 
         else
            'echo "#!/bin/sh"  > '       -> AFF.puttext;
        if);
        objName[]                        -> AFF.puttext;
        (if hostIsWindows then  
            '.bat'                       -> AFF.puttext;
            '\necho java '               -> AFF.puttext;
         else
            '\necho exec java '          -> AFF.puttext;
        if);
        (if use_java_classpath then
            '-cp '                       -> AFF.puttext;
         else
            '-jar '                      -> AFF.puttext;
        if);
        (* Add LIBFILES to classpath *)
        TX.scan
        (#
        do (if thisElm.kind = LibKind then 
               thisElm.T[] -> AFF.puttext;
               (if hostIsWindows then
                   ';' -> AFF.put;
                else
                   ':' -> AFF.put;
               if);
           if)
        #); 
        (CodeDirectory[], ObjDirectory[]) 
          -> thePathHandler.localpath
          -> AFF.puttext;
        DirectoryChar -> AFF.put;
        objName[] -> e.path;
        e.path.name                               -> AFF.puttext; 
        '.jar '                                   -> AFF.puttext;
        (if use_java_classpath then
            (if main_in_program then
                'program' -> AFF.puttext;
             else
                objname[] -> AFF.puttext;
            if);
        if);
        '>> '                                     -> AFF.puttext;
        objName[]                                 -> AFF.puttext;
        (if hostIsWindows then 
            '.bat'                                -> AFF.putline;
         else
            '\nchmod a+x '                        -> AFF.puttext;
            objName[] -> AFF.puttext;
        if);
     #);
do (if true
    // isIL then
       (if not common.switch[6] then
           (* Link has been done *)
           'if exist "' -> AFF.puttext;
           objName[] -> AFF.puttext;
           '.exe" ' -> AFF.puttext;
           
           '(echo Object program on file: '->AFF.puttext;
           objName[] -> AFF.puttext;
           '.exe)' -> AFF.putline;
       if);
       AFF.newline;
       generateBatFileLabels;
       (if not common.switch[23] then
           'if exist "ilasm.out" (del "ilasm.out")\n'
             ->AFF.putText;
       if);
    // isJava then
       (if not common.switch[6] then
           (if generate_jar_manifest and (not common.switch[23]) then
               (false, '.manifest' -> (objName.copy).append) -> Remove;
           if);
           generateJavaWrapperScript;
           (if hostIsWindows then
               generateBatFileLabels;
           if);
           '\necho Object program on file: '->AFF.puttext;
           objName[] -> AFF.putline;
       if);
    else
       'trailer: unknown platform' -> TODO;
   if);
#)

--AddSharedLibFile: descriptor--
(# 
do 'AddSharedLibFile'->TODO
#)

--jobFilePermission: descriptor--
(# 
do AFF.setExe;
#)

--AddLinkDirs: descriptor--
(# 
do 'AddLinkDirs'->TODO   
#)


