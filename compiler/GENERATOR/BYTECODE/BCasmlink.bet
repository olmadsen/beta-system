ORIGIN '../asmlinkbody' ;
INCLUDE '~beta/basiclib/fileSetExe';
(* INCLUDE '~beta/basiclib/pcre'; *)
INCLUDE '~beta/basiclib/formatio';
INCLUDE '~beta/containers/list';
INCLUDE '~beta/containers/hashTable';
INCLUDE '~beta/basiclib/texthash';

(* verboseLevels:
 * verbose: (# exit 0 #); {* more than default? *}
 * default: (# exit 1 #); {* normal, the old way, open, bind, etc. *}
 * actions: (# exit 2 #); {* only if somthing happens, parse, etc.*}
 * nothing: (# exit 3 #); {* nothing, only errors *}
 *)

(* FIXME: Could fragment windows/unix dependant code out into 
 *   BCasmlinkWindows.bet and BCasmlinkUnix.beta
 * (all places with an explicit common.hostIsWindows test.
 *)

--lib: attributes--

use_pcre:
  (# exit false #);

unQuote:
  (# t: ^text;
  enter t[]
  do (if ((1 -> t.inxGet) = '"') AND ((t.length -> t.inxGet) = '"') then
         (2, t.length - 1) -> t.sub -> t[];
     if);
  exit t[]
  #);
tick:
  (# t, new: ^text;
  enter t[]
  do '\''->new[];
     T.scanall(# do (if ch='\'' then '"' -> new.put else ch->new.put if) #);
     '\''->new.append;
     new[] -> t[];
  exit t[]
  #);
escDollar:
  (# T,S: ^text
  enter T[]
  do &text[] -> S[];
     T.scanAll
     (#
     do (if ch = '$' then
            '\\' -> S.put
        if);
        ch -> S.put
     #);
  exit S[]
  #);
sh2cmd:
  (* Hack used to replace unix sh commends with cmd.exe commands when host is windows *)
  (# cmd: ^text;
     (* colon2amp: @pcre;
      * mv2move: @pcre;
      * success: @boolean;
      *)
  enter cmd[]
  do (if use_pcre then
         (* ';' -> colon2amp;
          * 'mv ' -> mv2move;
          * (cmd[], '&&') -> colon2amp.replaceAll -> (success, cmd[]);
          * (cmd[], 'move ') -> mv2move.replaceAll -> (success, cmd[]);
          *)
      else
          ';'   -> (cmd.copy).findAll(# do (inx, inx) -> cmd.delete; ('&&',inx) -> cmd.insert; #);
          'mv ' -> (cmd.copy).findTextAll(# do (inx, inx+2) -> cmd.delete; ('move ',inx) -> cmd.insert; #);
     if)
  exit cmd[]
  #);

TODO: 
  (# T: ^text enter T[] 
  do 'TODO: ' -> screen.puttext;
     T[] -> screen.putline 
  #);
AppendExe:
  (# objname: ^text;
  enter objname[]
  exit '.exe' -> objname.appendExtension
  #);

containerlist: list(# #);

--translateasmlinklib: attributes ---
(* Configuration: *)
main_in_program:
  (# exit true #);
only_program_jar:
  (# exit common.switch[326] and (not common.switch[6]) #);
no_jar_compression:
  (# exit common.switch[325] #);
use_relative_jar_paths:
  (# exit false #);

isJava:  
  (# exit common.targetMachineId = common.jvm #);
isIL:    
  (# exit common.targetMachineId = common.clr #);
isRotor:
  (# exit common.switch[328] #);
applicationbase:
  (# path: ^text;
     e: @diskentry;
  do CodeDirectory.copy -> path[] (* Full path *);
     DirectoryChar -> path.put;
     objname[] -> e.path; e.path.name -> path.append;
     INNER
  exit path[]
  #);
is_full_path: booleanValue
  (# path: ^text
  enter path[]
  do (if common.hostIsWindows then
         (path[]<>NONE) and (path.lgth>=2) and (path.T[2]=':') -> value
      else
         (path[]<>NONE) and (path.lgth>=2) and (path.T[1]='/') -> value
     if);
  #);
lineCont: 
  (# T: ^text;
  do (if common.hostIsWindows then
         ' ^\n\t' -> T[]
      else
         ' \\\n\t' -> T[]
     if);
  exit T[]
  #);
ReplaceBetalib:
  (# T: ^text;
     l: @integer;
  enter T[]
  do (*'ReplaceBetalib: '->puttext; T[] -> puttext; '\n\t-> '->puttext;*)
     (thePathHandler.betalib).length -> l;
     (if thePathHandler.betalib -> ((1,l)->T.sub).equal then
         (1,l-1)->T.delete; (* don't delete directorychar *)
         (if common.hostIsWindows then
             '%%%%BETALIB%%%%' -> T.prepend;
          else
             '\\${BETALIB}' -> T.prepend;
         if);
     if);
     (*T[] -> putline;*)
  exit T[]
  #);

del:
  (# name: ^text;
     check: @boolean;
     AFF: ^stream;
  enter (name[], check, AFF[])
  do (if common.hostIsWindows then
         (if check then
             'if exist "'->AFF.putText; 
             name[]->AFF.putText; 
             '" '->AFF.putText;
         if);
         'del "'->AFF.putText; 
         name[]->AFF.putText; 
         '"' -> AFF.put; 
         (if verboseLevel > verboseLevel.verbose then
             ' >NUL 2>&1'->AFF.puttext;
         if);
      else
         'rm -f "'->AFF.putText; 
         name[]->AFF.putText; 
         '"' -> AFF.put; 
     if);
     '' -> AFF.putline;
  #);
    
--asmlinklib: attributes --

AFFputText:
  (# T: ^text
  enter T[]
  do asmstate.link_line_length+T.lgth->asmstate.link_line_length;
     T[] -> AFF.puttext;
  #);
jarfilename: applicationbase
  (# 
  do '.jar' -> path.append;
  #);
jatfilename: applicationbase
  (# 
  do '.jat' -> path.append;
  #);
jaroptions:
  (# cmd: ^text;
     update: @boolean;
  enter update
  do 0 -> asmstate.link_line_length;
     '' -> cmd[];
     INNER;
     (if update then
         'uf' -> cmd.puttext;           
      else
         'cf' -> cmd.puttext;           
     if);
     (if verboseLevel <= verboseLevel.verbose then
         'v' ->cmd.puttext;
     if);
     (if no_jar_compression then
         '0' -> cmd.put;
     if);
     ' '-> cmd.puttext; 
     (if common.hostIsWindows then
         jarfilename -> cmd.puttext; 
      else
         jarfilename -> tick -> cmd.puttext; 
     if);
  exit cmd[]
  #);  
jarcommand: jaroptions
  (# 
  do 'jar ' -> cmd.puttext;
  #);

CheckLinkError:
  (# cmd: ^text;
  do ''->cmd[];
     (if common.hostIsWindows then
         '\nif errorlevel 1 goto linkerror' -> cmd.putLine;
      else
         '\n'
         'case $? in\n'
         '0)\n'
         '   ;;\n'       
         '*)\n'
         '   echo Errors during link.\n' 
         '   exit 1\n'
         '   ;;\n'
         'esac'->cmd.PutLine;
     if);
  exit cmd[]
  #);

--asmState:descriptor--
(# link_line_length: @integer;
   verifylist: @containerlist(# element::text #);
   program_path: ^text;
   (* jvmregexp: @pcre; *)
   
   texthash: hashtable
     (# element:: text;
        honeyM: @honeyMan;
        init:: (# do honeyM.init; #);
        equal::
          (# 
          do (if common.hostIsWindows then
                 (left[]->right.equalNCS) -> value;
              else
                 (left[]->right.equal) -> value;
             if);
          #);
        hashFunction::(# do e[]-> honeyM.hash -> value #);
        insertUnique:
          (# T: ^text
          enter T[]
          do (if not (T[]->has) then T[] -> insert if);
          #);
     #);
   duplicateCheck: @texthash;
   jvmDirs: @texthash;

#)
--HasSharedLibs:descriptor--
(#do false -> value #)
---cont: descriptor --
(# 
do lineCont -> AFFputtext
#)
---header: descriptor ---
(#
do (if use_pcre then
       (* 'jvm/' -> asmstate.jvmregexp; (* used by jvm linkObj *)
   if);
   (if true
    // isIL then
       asmstate.verifylist.init;
       asmstate.duplicateCheck.init; (* used by clr linkObj *)
    // isJava then
       asmstate.jvmDirs.init;        (* used by jvm linkObj *)
    else
       'AsmHeader: unknown platform' -> TODO; 
   if);
   (if common.hostIsWindows then
       '@echo off' ->AFF.putline;
    else
       '#!/bin/sh '->AFF.putLine;
       'BETALIB=${BETALIB-/usr/local/lib/beta}' ->AFF.putline;
       '. $BETALIB/configuration/env.sh' -> AFF.putline;
       'export BETALIB'->AFF.putLine;
       'PATH=${JAVAHOME}/bin:$PATH' -> AFF.putline;
       'export PATH' -> AFF.putline;
       'TMPDIR=${TMPDIR-/tmp}'->AFF.putLine;
   if);
   (if isIL then
       ('ilasm.out', true, AFF[]) -> del;
   if);
#)
---AsmExt: descriptor ---
(#
do (if true
    // isIL then
       '.il' -> ext;
    // isJava then
       '.J' -> ext
    else
       'AsmExt: unknown platform' -> TODO;
   if);
#) 
---BinExt: descriptor ---
(# 
do (if true
    // isIL then
       '.dll' -> ext;
    // isJava then
       '.dep' -> ext
    else
       'BinExt: unknown platform' -> TODO;
   if);
#)
---LibExt: descriptor ---
(# 
do 'LibExt'->TODO
#)
---JobExt: descriptor ---
(# 
do (if common.hostIsWindows then
       '.bat' -> ext;
    else 
       '..job' -> ext
   if);
#) 
---ExeExt:descriptor---
(# 
do (if true
    // isIL then
       '.exe' -> ext;
    else
       ''-> ext (* not used by Java *)
   if);
   #)
---Echo: descriptor ---
(# 
do 'echo '->AFF.puttext; (* same in bat/sh *)
#)
---Remove: descriptor ---
(# 
do (if common.hostIsWindows then
       (* Delete .exe file - even for java *)
       name.copy -> name[]; 
       '.exe' -> name.appendExtension;
       (name[], check, AFF[]) -> del;
       (if isJava then
           (* Delete existing wrapper too *)
           '.exe' -> name.stripExtension;
           '.bat' -> name.appendExtension;
           (name[], check, AFF[]) -> del; (* delete cmd wrapper *)
       if)
    else
       (name[], check, AFF[]) -> del;
   if);
   (if isRotor then
       (* Delete existing wrapper too *)
       '.exe' -> name.stripExtension;
       (if common.hostIsWindows then
           '.bat' -> name.appendExtension;
       if);
       (name[], check, AFF[]) -> del; (* delete cmd/sh wrapper *)
   if);
#)
---ChangeDirectory:doPart---
do (if true
    // common.hostIsWindows then
       (if (dir.lgth>1) and (dir.T[2]=':') then
           (* change drive *)
           dir.T[1]->AFF.put; ':' -> AFF.put; AFF.newline;
       if);
       'cd '->AFF.puttext; dir[] -> slashToBackslash -> quote -> AFF.putline
    else
       'cd '->AFF.puttext; dir[] -> tick -> AFF.putline
   if);
       
---AddCommand:doPart---
do (if common.hostIsWindows then
       cmd[] -> sh2cmd -> AFF.putline;
       'if errorlevel 1 goto builderror' -> AFF.putLine;
    else
       cmd[] -> AFF.putline;
   if);
---Make: descriptor ---
(# 
do 'Make'->TODO
#)
---resource:descriptor---
(#
do 'resource'->TODO
#)
---assemble: descriptor ---
(# e: @diskentry;
   out: ^text;
   FF: ^text;
   
do (if common.switch[184] or common.switch[189] then
       asmext -> F.append;
   if);
   F[] -> e.path;
   (if not e.exists then
       (if isJava then
           (if common.switch[18] then
               (if not common.hostIsWindows then
                   F[] -> escDollar -> F[];
               if);
               (# FN: ^text
               do F.copy -> FN[];
                  'jcf-dump -c "' -> AFF.puttext;
                  '.class' -> (FN.copy).append -> AFF.puttext;
                  '" > "' -> AFF.puttext;
                  '..j"' -> (FN.copy).append -> AFF.putline
               #)
           if)
        else           
           (if verboseLevel <= verboseLevel.verbose then
               'AsmLink: skipping non-existing\n\t' -> screen.puttext;
               F[] -> screen.putline;
           if)
       if)
    else
       (F.copy, '') -> thePathHandler.localPath -> FF[];
       (if verboseLevel <= verboseLevel.actions then
           AFF.newline;
           (if not isJava then
               echo;
               'assembling '->AFF.puttext;
               (if common.hostIsWindows then
                   FF[] ->AFF.putline;
                else
                   FF[] ->escDollar-> AFF.putline;
               if);
           if);
       if);
       (if true
        // isIL then
           'ilasm /nologo /dll '->AFF.puttext; 
           (if verboseLevel > verboseLevel.verbose then
               '/quiet ' ->AFF.puttext;
           if);
           (if true (*common.switch[41]*) then 
               '/debug '->AFF.puttext
           if);
           '/output:' -> AFF.puttext;
           '.il' -> F.stripextension;
           (if common.hostIsWindows then
               '.dll' -> (F.copy).appendExtension -> AFF.puttext;
               ' '-> AFF.put;
               '.il' -> (F.copy).appendExtension -> AFF.puttext;
            else
               '.dll' -> (F.copy).appendExtension->escDollar->AFF.puttext;
               ' '-> AFF.put;
               '.il' -> (F.copy).appendExtension->escDollar->AFF.puttext;
           if);
           (if verboseLevel > verboseLevel.verbose then
               ' >"ilasm.out" 2>&1'->AFF.puttext;
           if);
           AFF.newline;
           (if common.hostIsWindows and (not common.switch[21]) then
               'if errorlevel 1 goto asmerror' -> AFF.putLine;
           if);
           (if false then
               (if common.switch[41](*highlevel debug*) and (not common.switch[23](*preserve*)) then
                   'del '                            -> AFF.puttext;
                   '.il' -> (F.copy).appendExtension -> AFF.puttext;
                   '>NUL 2>&1'                       -> AFF.putline;
               if);
           if);
           F[] -> asmstate.verifylist.append;
        // isJava then
           (*'jasmin '->AFF.putText; F[]->AFF.putLine*)
        else
           'assemble: unknown platform' -> TODO;
       if);
   if);
#)
--linkLibraries:descriptor--
(#
do 'linkLibraries'->TODO
#)
--contCh:descriptor--
(# 
do (if common.hostIsWindows then
       '^' -> value
    else
       '\\' -> value
   if);
#)
--ldArgMax:descriptor--
(# 
do maxint -> value
#)
--ldTmp:descriptor--
(# 
do 'ldTmp'->TODO
#)
---link: descriptor ---
(# e: @diskentry;
   GenerateClrConfigFile:
     (# ConfigFileName: ^text;
     do ObjName.copy -> ConfigFileName[];
        '.exe.config' -> ConfigFileName.append;
        WriteConfig:
          (# f: @file
               (# error: 
                    (# 
                    do 'Failed to create '
                         ->(ConfigFileName.copy).prepend 
                         -> screen.putline; 
                       leave WriteConfig;
                    #);
                  AccessError::(# do error #);
                  WriteError::(# do error #);
                  NoSpaceError::(# do error #);
               #);
             e: @diskentry;
             probingdir: ^text;
          do 'clr\\' -> probingdir[];
             objname[] -> e.path;
             e.path.name -> probingdir.append;
             ConfigFileName[] -> f.name;
             (if f.entry.exists then
                 (* Will no overwrite existing .config file *)
                 '.new' -> ConfigFileName.append;
                 ConfigFileName[] -> f.name;
             if);
             f.openwrite;
             '<configuration>\n'
             '   <runtime>\n'
             '      <assemblyBinding xmlns="urn:schemas-microsoft-com:asm.v1">\n'
             '         <probing privatePath="%s"/>\n'
             '      </assemblyBinding>\n'
             '   </runtime>\n'
             '</configuration>\n'
               -> f.putformat(# do probingdir[] -> s #);
             f.close;
          #)
     #);
   
   GenerateClrExe:
     (#
     do 'ilasm /nologo '->AFF.puttext; 
        (if false and common.switch[71] then
            (* No console *)
            '/winexe ' -> AFF.puttext;
         else
            '/exe ' -> AFF.puttext;
        if);
        (if verboseLevel > verboseLevel.verbose then
            '/quiet ' ->AFF.puttext;
        if);
        (if false (*common.switch[41]*) then 
            '/debug '->AFF.puttext
        if);
        '/output:' -> AFF.puttext;
        ObjName[] -> AFF.puttext;
        '.exe ' -> AFF.puttext;
        'clr' -> AFF.puttext; DirectoryChar -> AFF.put;
        '_main.il' -> AFF.puttext;
        (if verboseLevel > verboseLevel.verbose then
            ' >"ilasm.out" 2>&1'->AFF.puttext;
        if);
        AFF.newline;
        (if not common.switch[21] then
            CheckLinkError -> AFF.putline;
        if);
        (if common.switch[41](*highlevel debug*) 
            and (not common.switch[23](*preserve*)) then
            ('clr\\_main.il', true, AFF[])->del;
        if);
     #);
   
   GenerateClrVerify:
     (# 
     do (if common.switch[37] then
            asmstate.verifylist.scan
            (# F: ^text;
            do (if '.exe' -> current.hasExtension then
                   current[] -> F[];
                else
                   '.dll ' -> current.append -> f[];
               if);
               AFF.newline;
               'echo verifying '->AFF.puttext;
               F[] -> AFF.puttext;
               AFF.newline;
               'peverify /nologo '->AFF.puttext; 
               F[] -> AFF.puttext;
               ' > peverify.out' -> AFF.putline;
               'if errorlevel 1 goto verifyerror' -> AFF.putLine;
               'goto verifydone' -> AFF.putline;
               ':verifyerror' -> AFF.putline;
               'echo.' -> AFF.putline;
               'echo Verification errors:' -> AFF.putline;
               'type peverify.out' -> AFF.putline;
               ':verifydone' -> AFF.putline;
               'del peverify.out' -> AFF.putline;
               'echo.' -> AFF.putline;
            #);
        if);
     #);
   
do (if true
    // isJava then
       'echo ' -> AFF.puttext;
       (if common.hostIsWindows then
           false -> jaroptions -> AFF.puttext;
        else
           false -> jaroptions -> tick -> AFF.puttext;
       if);
       ' > ' -> AFF.puttext;
       jatfilename -> AFF.putline;
    // isIL then
       (* Ensure applicationbase directory existence *)
       (# applicationbasedir: @Directory;
       do applicationbase -> applicationbasedir.name;
          applicationbasedir.touch;
       #);
       (* Delete existing applicationbase directory contents *)
       (if common.hostIsWindows then
           'del /f /q /s ' -> AFF.puttext;
           applicationbase -> quote -> AFF.puttext;
           (if verboseLevel > verboseLevel.verbose then
               ' >NUL 2>&1' -> AFF.puttext;
           if);
        else
           'rm -rf ' -> AFF.puttext;
           applicationbase -> quote -> AFF.putline;
           'mkdir ' -> AFF.puttext;
           applicationbase -> quote -> AFF.puttext;
       if);
       AFF.newline;
       (if not common.switch[6] then
           GenerateClrExe;
           GenerateClrConfigFile;
       if);
       GenerateClrVerify;
       AFF.newline;
    else
       'link' -> TODO;
   if);
#)

---addBetaRun:descriptor---
(#
do (if true
    // isJava then
       (* currently nothing *)
    // isIL then
       (* currently nothing *)
    else
       'addbetarun' -> TODO;
   if);
#)
--asmlinkdynamic:descriptor--
(#
do 'asmlinkdynamic'->TODO
#)
---linkObj:descriptor ---
(# e: @diskentry;
   path, packagepath, name: ^text;
   ldAputText:
     (# T: ^text
     enter T[]
     do asmstate.link_line_length+T.lgth->asmstate.link_line_length;
        T[] -> ldA.puttext;
     #);
   addClassFile:
     (# path, pwd: ^text;
        jvmpath: ^text;
        start,end: @integer;
        addToJatFile:
          (#
          do 'echo ' -> ldA.puttext;
             (if common.hostIsWindows then
                 '-C ' -> (jvmpath.copy -> tick).prepend -> ldAputtext;
              else
                 '-C ' -> (jvmpath.copy -> tick).prepend -> tick -> ldAputtext;
             if);
             ' ' -> ldA.putText;
             path[] -> tick -> ldAputtext;
             ' >> ' -> ldAputtext;
             jatfilename -> ldA.putline;
          #);
     enter path[]
     do (if false then
            'addclassfile: ' -> puttext; path[] -> putline;
        if);
        path.copy -> path[]; (* avoid aliasing *)
        (* Find jvm directory in path *)
        path[] -> BackslashToSlash -> jvmpath[];
        (if use_pcre then
            (*jvmpath[] -> asmstate.jvmregexp.match(# do matchPos->(start,end) #)*)
         else
            'jvm/' -> jvmpath.findTextAll(# do inx->start; inx+3->end #);
        if);
        (if start>0 then
            (1,end-1) -> path.sub -> jvmpath[];
            (end+1, path.length) -> path.sub -> path[];
            (if use_relative_jar_paths then
                thePathHandler.CurrentDirectory -> pwd[];
                (if common.hostIsWindows then
                    (* Make sure drive letter on windows is same case *)
                    jvmpath.T[1] -> ascii.upcase -> jvmpath.T[1];
                    pwd.T[1]     -> ascii.upcase -> pwd.T[1];
                    jvmpath[] -> slashToBackslash;
                if);
                (jvmpath[], pwd[]) -> thePathhandler.localpath -> jvmpath[];
                (if common.hostIsWindows then
                    jvmpath[] -> BackslashToSlash -> jvmpath[];
                if);
            if);
            jvmpath.copy -> ReplaceBetalib -> asmstate.jvmDirs.insertUnique;
            (if only_program_jar then
                (*'only_program_jar: ' -> puttext; path[] -> putline;*)
                (if 'beta/program.class' -> path.equal then
                    addToJatFile;
                if);
             else
                addToJatFile;
            if);
        if)
     #);
   addDllFile:
     (# lObj, name, src, dest: ^text;
        e: @diskentry;
     enter lObj[]
     do (if false then
            'adddllfile: ' -> puttext; lObj[] -> putline;
        if);
        (if lObj.copy -> asmstate.duplicateCheck.has then
            '*** AddDLLfile: warning: ignoring duplicate (compiler error):\n\t'
              -> screen.puttext;
            lObj[] -> screen.putline;
         else
            lObj.copy -> asmstate.duplicateCheck.insert;
            '.dll' -> lObj.appendExtension -> src[];
            src[] -> e.path;
            e.path.name -> name[];
            name[] -> (DirectoryChar->(applicationbase).put).puttext -> dest[];
            (if common.switch[37] then
                (* Generate check for if source dll exists *)
                'if not exist '->ldA.puttext;
                src[] -> quote -> ldA.puttext;
                ' echo warning: file not found: ' -> ldA.puttext;
                src[] -> ldA.putline;
            if);
            
            (if common.switch[37] then
                (* Generate check for if dll already copied *)
                'if exist '->ldA.puttext;
                dest[] -> quote -> ldA.puttext;
                ' echo warning: file multiply copied: ' -> ldA.puttext;
                src[] -> ldA.putline;
            if);
            
            (* Copy source dll *)
            (if common.hostIsWindows then
                'copy ' -> ldA.puttext;
                src[] -> quote -> ldA.append;
                ' ' -> ldA.append;
                dest[] -> quote -> ldA.puttext;
                (if verboseLevel > verboseLevel.verbose then
                    ' >NUL 2>&1' -> ldA.puttext;
                if);
                ldA.newline;
             else
                'cp ' -> ldA.puttext;
                src[] -> escDollar -> ldA.append;
                ' ' -> ldA.append;
                dest[] -> escDollar -> ldA.puttext;
                ldA.newline;
            if);
        if);
     #);
   addFile:
     (# lObj: ^text;
     enter lObj[]  
     do (if true
         // isJava then
            lObj[] -> addClassFile;
         // isIL then
            lObj[] -> addDllFile;
         else
            'linkObj: addFile' -> TODO;
        if);
     #);
do (* lObj = '"foo"' *)
   lObj[] -> unquote -> lObj[];
   (if false then
       'linkObj: ' -> puttext; lObj[] -> putline;
       lObj[]->e.path;
       (*e.path.name.prefix->putline;*)
       (if 'betaenv'->(e.path.name.prefix).equal then
           (dumpstack, '') -> stop;
       if);
   if);
   (if true
    // isJava then
       lObj.copy -> path[];
       '.class' -> path.AppendExtension -> addClassFile;
       (if common.hostIsWindows then
           path.copy -> SlashToBackslash -> e.path;
        else
           path[] -> e.path;
       if);
       e.path.name -> name[];
       (if true
        // 'program.class'->name.equal then
           (if common.hostIsWindows then
               e.path.head -> BackslashToSlash -> asmstate.program_path[];
            else
               e.path.head -> asmstate.program_path[];
           if);
        // ('BetaObject.class'->name.equal) then
           (* Add compiler generated special classes too *)
           (* Get directory of BetaObject.class as full path *)
           e.path.head -> path[]; 
           (if path.length>0 then
               path.copy -> packagepath[];
               '/' -> packagepath.append;
            else
               '' -> packagepath[];
           if);
           'ExOException.class' -> (packagepath.copy).append -> addClassFile;
           (* Add Structure.class too *)
           'Structure.class' -> (packagepath.copy).append -> addClassFile;
           (* Add BetaArray.class too *)
           'BetaArray.class' -> (packagepath.copy).append -> addClassFile;
           (if not common.ComponentDotCJ then
               (* Add Component.class too *)
               'Component.class' -> (packagepath.copy).append -> addClassFile;
           if);
       if);
    // isIL then
       lObj[] -> addDllFile;
    else
       'linkObj' -> TODO;
   if);
#)
---libraries: descriptor ---
(#
do 'libraries'->TODO
#)
--linktrailer:descriptor--
(# 
do (if isJava then
       'jar @' -> AFF.puttext;
       jatfilename -> AFF.putline;
       CheckLinkError -> AFF.putline;
   if);
#)
---trailer: descriptor ---
(# e: @diskentry;
   generateBatFileLabels:
     (# 
     do 'goto exit\n'
        '\n'
        ':builderror\n'
        'echo Errors during BUILD.\n'
        'goto exit\n'
        ':asmerror\n'
          -> AFF.puttext;
        (if isIL then
            'if exist "ilasm.out" type "ilasm.out"\n'
              -> AFF.puttext;
        if);
        'echo Errors during assembling.\n'
        'goto exit\n'
        ':reserror\n'
        'echo Errors during resource compiling.\n'
        'goto exit\n'
        ':linkerror\n'
          -> AFF.puttext;
        (if isIL then
            'if exist "ilasm.out" type "ilasm.out"\n'
              -> AFF.puttext;
        if);
        'echo Errors during link\n'
        'goto exit\n'
          ->AFF.putText;
        (if common.switch[37] then
            ':linkerror\necho A Verification Error Occurred\ngoto exit'->AFF.putLine;
        if);
        AFF.newline;
        ':exit\n'
          ->AFF.putText;
     #);
   generateJavaWrapperScript:
     (# wrapperfilename:
          (# T: ^text
          do objName.copy -> T[];
             (if for_jdb then
                 '-jdb' -> T.puttext;
             if);
             (if common.hostIsWindows then  
                 '.bat'  -> T.puttext;
             if);
          exit T[]
          #);
        jarpath:
          (# jarpath: ^text;
          do (CodeDirectory[], ObjDirectory[]) 
               -> thePathHandler.localpath
               -> jarpath[];
             DirectoryChar -> jarpath.put;
             objName[] -> e.path;
             e.path.name -> jarpath.append; 
             '.jar'->jarpath.append;
          exit jarpath[]
          #);
        fullclasspath:
          (# classpath: ^text;
          do &text[]->classpath[];
             asmstate.jvmdirs.scan
             (# 
             do searchPathDelimiter -> classpath.put;
                current[] -> classpath.append;
             #);
          exit classpath[]
          #);
        for_jdb: @boolean;
     enter for_jdb
     do (if common.hostIsWindows then
            'echo @echo off                 > ' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            (* PATH: Used by JNI to find libraries loaded with LoadLibrary().
             * When used from a shell, %objdir% is typically not set on windows,
             * whereas run.demos and subsequently testall set it.
             * So we specify both locations.
             *)
            'echo set PATH=%%BETALIB%%/javalib/jvm;%%BETALIB%%/javalib/jvm/%%objdir%%;%%PATH%% >> ' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo set _opts_=               >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo :getopts                  >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo if "%%1"=="" goto execute >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo set _opts_=%%_opts_%% %%1 >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo shift                     >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo goto getopts              >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo :execute                  >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
         else
            'echo "#!/bin/sh"  > '            -> AFF.puttext;
            wrapperfilename                   -> AFF.putline;
            'echo "BETALIB=\\${BETALIB-/usr/local/lib/beta}">> ' ->AFF.puttext;
            wrapperfilename                   -> AFF.putline;
            'echo ". \\$BETALIB/configuration/env.sh" >>' ->AFF.puttext;
            wrapperfilename                   -> AFF.putline;
            'echo LD_LIBRARY_PATH=\\"jvm:\\$BETALIB/javalib/jvm/\\${objdir}:\\$LD_LIBRARY_PATH\\" >> ' ->AFF.puttext;
            wrapperfilename                   -> AFF.putline;
            'echo export LD_LIBRARY_PATH >> ' ->AFF.putText;
            wrapperfilename                   -> AFF.putline;    
        if);
        
        (if for_jdb then
            'echo echo Running jdb on '-> AFF.puttext;
            objName[]                  -> AFF.puttext;
            ':>> '                     -> AFF.putText;
            wrapperfilename            -> AFF.putline;
            'echo echo To stop at program start issue this command: stop in beta.program.'
              -> AFF.puttext;
            common.doMethod            -> AFF.putText;
            '>> '                      -> AFF.putText;
            wrapperfilename            -> AFF.putline;
        if);
        (if common.hostIsWindows then  
            'echo '                    -> AFF.puttext;
         else
            'echo exec '               -> AFF.puttext;
        if);
        (if for_jdb then
            'jdb '                     -> AFF.puttext;
         else
            (if common.switch[322] then
                (* Use GNU java interpreter *)
                'gij '                 -> AFF.puttext;
             else
                'java '                -> AFF.puttext;
            if);
        if);
        (* (if verboseLevel <= verboseLevel.verbose then
         *     '-verbose '             -> AFF.puttext;
         * if);
         *)
        (if for_jdb then
            '-sourcepath \\"'          -> AFF.puttext;
            asmstate.jvmDirs.scan
            (# 
            do current[]               -> AFF.puttext;
               searchPathDelimiter     -> AFF.put;
            #);
            '\\" '                     -> AFF.puttext;
        if);
        '-classpath '                  -> AFF.puttext;
        (* Add LIBFILES to classpath *)
        TX.scan
        (#
        do (if thisElm.kind = LibKind then 
               thisElm.T[] -> AFF.puttext;
               searchPathDelimiter -> AFF.put;
           if)
        #); 
        jarpath -> AFF.puttext;
        (if only_program_jar then
            fullclasspath -> AFF.puttext;
        if);
        ' ' -> AFF.puttext;
        (if main_in_program then
            (if (asmstate.program_path[]<>NONE) and (asmstate.program_path.length>0) then
                (* Get program path relative to CodeDirectory.
                 * This is the package path of program.class.
                 * On SUN it works to give this path as foo/bar/program.
                 * However using GIJ this only worls using foo.bar.program.
                 * But since that works on SUN java too, we use that (:-).
                 *)
                (asmstate.program_path[], CodeDirectory.copy) 
                  -> thePathHandler.localPath
                  -> slashToDot
                  -> AFF.puttext;
                '.' -> AFF.put;
            if);
            'program' -> AFF.puttext;
         else
            objname[] -> AFF.puttext;
        if);
        (if common.hostIsWindows then 	     
            ' %%_opts_%%'            -> AFF.puttext;
            ' >> '                   -> AFF.puttext;
            wrapperfilename          -> AFF.putline;
         else
            ' \'$*\''                -> AFF.puttext;
            ' >> '                   -> AFF.puttext;
            wrapperfilename          -> AFF.putline;
            'chmod a+x '             -> AFF.puttext;
            wrapperfilename          -> AFF.putline;
        if);
     #);
   generateRotorWrapperScript:
     (# wrapperfilename:
          (# T: ^text
          do objName.copy -> T[];
             (if common.hostIsWindows then  
                 '.bat'  -> T.puttext;
             if);
          exit T[]
          #);
     do (if common.hostIsWindows then
            'echo @echo off                 > ' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo set PATH=%%BETALIB%%/javalib/jvm;%%PATH%% >> ' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo set _opts_=               >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo :getopts                  >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo if "%%1"=="" goto execute >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo set _opts_=%%_opts_%% %%1 >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo shift                     >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo goto getopts              >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
            'echo :execute                  >>' -> AFF.puttext; 
            wrapperfilename                     -> AFF.putline;
         else
            'echo "#!/bin/sh"  > '            -> AFF.puttext;
            wrapperfilename                   -> AFF.putline;
        if);
        
        'echo clix '                 -> AFF.puttext;
        objname[]                    -> AFF.puttext;
        '.exe'                       -> AFF.puttext;
        (if common.hostIsWindows then 	     
            ' %%_opts_%%'            -> AFF.puttext;
            ' >> '                   -> AFF.puttext;
            wrapperfilename          -> AFF.putline;
         else
            ' \'$*\''                -> AFF.puttext;
            ' >> '                   -> AFF.puttext;
            wrapperfilename          -> AFF.putline;
            'chmod a+x '             -> AFF.puttext;
            wrapperfilename          -> AFF.putline;
        if);
     #);
do (if true
    // isIL then
       (if common.switch[6] then
           (* No link, but DLL directory created for library *)
           '\necho DLL Library created: '->AFF.puttext;
           (applicationbase, thePathHandler.CurrentDirectory) -> thePathHandler.localPath 
             -> AFF.putline;
        else
           (* Link has been done *)
           (if common.hostIsWindows then
               'if exist "' -> AFF.puttext;
               objName[] -> AFF.puttext;
               '.exe" ' -> AFF.puttext;
               '(echo Object program on file: '->AFF.puttext;
               objName[] -> AFF.puttext;
               '.exe)' -> AFF.putline;
            else
               'echo Object program on file: '->AFF.puttext;
               objName[] -> AFF.puttext;
               '.exe' -> AFF.putline;
           if);
           (if isRotor then
               'echo Rotor wrapper on file: '->AFF.puttext;
               objName[] -> AFF.puttext;
               (if common.hostIsWindows then
                   '.bat' -> AFF.puttext;
               if);
               AFF.newline;
           if);
       if);
       AFF.newline;
       (if common.hostIsWindows then
           generateBatFileLabels;
           (if not common.switch[18] then
               'if exist "ilasm.out" (del "ilasm.out")\n'
                 ->AFF.putText;
           if);
       if);
       (if isRotor then
           generateRotorWrapperScript;
       if);
    // isJava then
       (if common.switch[6] then
           (* No link, but jar file created for library *)
           '\necho Library JAR file on file: '->AFF.puttext;
           (jarfilename, thePathHandler.CurrentDirectory) -> thePathHandler.localPath 
             -> AFF.putline;
        else
           (* Create wrapper script for program jar file *)
           false -> generateJavaWrapperScript;
           (if common.switch[41](* highlevel debug *) then
               (* Create wrapper script for jdb session *)
               true -> generateJavaWrapperScript;
           if);
           '\necho Object program wrapper on file: '->AFF.puttext;
           objName[] -> AFF.puttext;
           (if common.hostIsWindows then
               '.bat' -> AFF.puttext;
           if);
           AFF.newline;
           (if common.hostIsWindows then
               generateBatFileLabels;
           if);
       if);
       (if not common.switch[18] then
           (* delete jar @file *)
           (jatfilename, true, AFF[]) -> del; 
       if);
    else
       'trailer: unknown platform' -> TODO;
   if);
#)

--AddSharedLibFile: descriptor--
(# 
do 'AddSharedLibFile'->TODO
#)

--jobFilePermission: descriptor--
(# 
do AFF.setExe;
#)

--AddLinkDirs: descriptor--
(# 
do 'AddLinkDirs'->TODO   
#)


