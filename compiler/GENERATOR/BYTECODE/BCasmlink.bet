ORIGIN '../asmlinkbody' ;
INCLUDE '~beta/basiclib/fileSetExe';

--lib: attributes--
use_dotnet_betarun:
  (# exit false #);
use_dotnet_modules:
  (# exit false #);
TODO: 
  (# T: ^text enter T[] 
  do 'TODO: ' -> screen.puttext;
     T[] -> screen.putline 
  #);
AppendExe:
  (# objname: ^text;
  enter objname[]
  do '.exe' -> objname.appendExtension
  exit objname[]
  #);
SlashToBackslash: 
  (# T: ^Text;
  enter T[]
  do (for i:T.lgth repeat
          (if T.T[i] = '/' then '\\' -> T.T[i] if)
     for);
  exit T[]
  #);
BackslashToSlash: 
  (# T: ^Text;
  enter T[]
  do (for i:T.lgth repeat
          (if T.T[i] = '\\' then '/' -> T.T[i] if)
     for);
  exit T[]
  #);

--translateasmlinklib: attributes ---
isJava:  (# exit common.targetMachineId = common.javabc #);
isIL:    (# exit common.targetMachineId = common.dotnet #);


--asmState:descriptor--
(#  #)
--HasSharedLibs:descriptor--
(#do false -> value #)
---cont: descriptor --
(# do '\\\n\t' -> AFF.puttext  #)
---header: descriptor ---
(#
do (if true
    // isIL then
       '@echo off\n'->AFF.putText;
    // isJava then
       '#! /bin/sh '->AFF.putLine;
       'BETALIB='->AFF.putText; BetaLib[]->AFF.putLine;
       'export BETALIB'->AFF.putLine;
       'CC=${CC-"gcc -O6"}\nexport CC'-> AFF.putLine;
       'MACHINETYPE='->AFF.putText; TargetMachine->AFF.putLine;
       'export MACHINETYPE'->AFF.putLine;
       'TMPDIR=${TMPDIR-/tmp}'->AFF.putLine;
       (* Disable linking *)
       (*true -> common.switch[6];*)
    else
       'AsmHeader: unknown platform' -> TODO; 
   if);
#)
---AsmExt: descriptor ---
(#
do (if true
    // isIL then
       '.il' -> ext;
    // isJava then
       '.J' -> ext
    else
       'AsmExt: unknown platform' -> TODO;
   if);
#) 
---BinExt: descriptor ---
(# 
do (if true
    // isIL then
       (if use_dotnet_modules then
           '.netmodule' -> ext;
        else
           '.dll' -> ext;
       if);
    // isJava then
       '.class' -> ext
    else
       'BinExt: unknown platform' -> TODO;
   if);
#)
---LibExt: descriptor ---
(# 
do 'LibExt'->TODO
#)
---JobExt: descriptor ---
(# 
do (if true
    // isIL then
       '.bat' -> ext;
    // isJava then
       '..job' -> ext
    else
       'JobExt: unknown platform' -> TODO;
   if);
#) 
---ExeExt:descriptor---
(# 
do (if true
    // isIL then
       '.exe' -> ext;
    else
       ''-> ext (* not used by Java *)
   if);
   #)
---Echo: descriptor ---
(# 
do (if true
    // isIL then
       'echo '->AFF.puttext;
    // isJava then
       'echo '->AFF.puttext;
    else
       'Echo: unknown platform' -> TODO;
   if);
#)
---Remove: descriptor ---
(# 
do (if true
    // isJava then
       '/usr/bin/rm -f '->AFF.puttext; name[]->AFF.putline (* only OK for unix *)
    // isIL then
       (if check then
           'if exist "'->AFF.putText; name[]->AFF.putText; '" '->AFF.putText
       if);
       'del "'->AFF.putText; name[]->AFF.putText; '" >nul'->AFF.putLine;
    else
       'remove' -> todo
   if);
#)
---ChangeDirectory:doPart---
do 'ChangeDirectory'->TODO
---AddCommand:doPart---
do 'AddCommand'->TODO
---Make: descriptor ---
(# 
do 'Make'->TODO
#)
---resource:descriptor---
(#
do 'resource'->TODO
#)
---assemble: descriptor ---
(# e: @diskentry;
   out: ^text;
   FF: ^text;
do (if common.switch[184] or common.switch[189] then
       asmext -> F.append;
   if);
   F[] -> e.path;
   (if not e.exists then
       'OOPS! AsmLink: skipping\n\t' -> screen.puttext;
       F[] -> screen.putline;
       'as is has not been generated (compiler error).' -> screen.putline;
    else
       (F.copy, '') -> thePathHandler.localPath -> FF[];
       (if verboseLevel <= verboseLevel.actions then
           echo;
           'assembling '->AFF.puttext;
           FF[] ->AFF.putline;
       if);
       (if true
        // isIL then
           'ilasm /nologo /dll '->AFF.puttext; 
           (if verboseLevel > verboseLevel.verbose then
               '/quiet ' ->AFF.puttext;
           if);
           (if common.switch[41] then 
               '/debug '->AFF.puttext
           if);
           '/output:' -> AFF.puttext;
           objName[] -> e.path;
           (if (e.path.head).length>0 then
               ((*objDirectory.copy*) e.path.head, '') 
                 -> thePathHandler.localPath
                 -> AFF.puttext;
               '/' -> AFF.puttext;
           if);
           F.copy -> BackslashToSlash -> e.path;
           e.path.name -> out[];
           '.il' -> out.stripextension;
           out[] -> AFF.puttext;
           (if use_dotnet_modules then
               '.netmodule ' -> AFF.puttext;
            else
               '.dll ' -> AFF.puttext;
           if);
           F[] -> AFF.putline;
        // isJava then
           'jasmin '->AFF.putText; F[]->AFF.putLine
        else
           'assemble: unknown platform' -> TODO;
       if);
   if);
#)
--linkLibraries:descriptor--
(#
do 'linkLibraries'->TODO
#)
--contCh:descriptor--
(# 
do '\\' -> value
#)
--ldArgMax:descriptor--
(# 
do 1000000 -> value
#)
--ldTmp:descriptor--
(# 
do 'ldTmp'->TODO
#)
---link: descriptor ---
(# e: @diskentry;
   BuildDotnetBetarun:
     (# cs: @diskentry;
        bd: @diskentry;
        csc:
          (# 
          do 'csc /t:module /out:' -> AFF.puttext;
             e.path -> AFF.puttext;
             ' ' -> AFF.put;
             cs.path -> AFF.putline;
          #);
     do (if use_dotnet_betarun then
            (if not userDefinedBetaRun then
                ('~beta/betarun/CS/betarun.cs','') -> thePathHandler.convertFilePath -> cs.path;
                (if cs.exists then
                    '.dll' -> (betarun.copy).append -> e.path;
                    (if (not e.exists) or (e.modtime < cs.modtime) then
                        (betarunlib[], '') -> thePathHandler.convertFilePath -> bd.path;
                        (if (not bd.exists) then
                            echo; 'Creating directory ' -> AFF.puttext; bd.path -> AFF.putline;
                            bd.path -> (&directory[]).touchDir
                        if);
                        echo; 'Building ' -> AFF.puttext; e.path -> AFF.putline;
                        csc;
                    if);
                    'copy ' -> AFF.puttext; e.path -> AFF.puttext; ' .' -> AFF.putline;
                if)
            if)
        if)
     #);
        
do (if true
    // isJava then
       'echo "Main-Class: ' -> AFF.puttext;
       (if common.switch[186] then
           'tstenv" >' -> AFF.puttext;
        else
           'program" >' -> AFF.puttext;
       if);
       objName[]->AFF.puttext;
       '.manifest' -> AFF.putline;
       
       'jar cmf' -> AFF.puttext;
       (if verboseLevel <= verboseLevel.verbose then
           'v' ->AFF.puttext;
       if);
       ' '-> AFF.puttext; 
       objName[]->AFF.puttext;
       '.manifest ' -> AFF.puttext;  
       objName[]->AFF.puttext; '.jar '->AFF.puttext; cont
    // isIL then
       (if use_dotnet_betarun then
           BuildDotnetBetarun;
           'al /nologo /t:exe /main:tstenv.main ' -> AFF.puttext;
           (if common.switch[41] then 
               (* debug info? *)
           if);
           '/out:' -> AFF.puttext;
           objName.copy -> AppendExe -> quote -> AFF.puttext;
        else
           'ilasm /nologo /dll '->AFF.puttext; 
           (if verboseLevel > verboseLevel.verbose then
               '/quiet ' ->AFF.puttext;
           if);
           (if common.switch[41] then 
               '/debug '->AFF.puttext
           if);
           '/output:' -> AFF.puttext;
           ObjName[] -> AFF.puttext;
           '.exe ' -> AFF.puttext;
           'dotnet\\' -> AFF.puttext;
           objname[] -> AFF.puttext;
           '.il' -> AFF.puttext;
           AFF.newline;
       if)
    else
       'link' -> TODO;
   if);
#)

---addBetaRun:descriptor---
(#
do (if true
    // isJava then
       (* currently nothing *)
    // isIL then
       (if use_dotnet_betarun then
           (if userDefinedBetaRun then
               betaRun[]->linkObj
            else
               '.dll'->betaRun.CopyAppend->linkObj
           if)
       if)
    else
       'addbetarun' -> TODO;
   if);
#)
--asmlinkdynamic:descriptor--
(#
do 'asmlinkdynamic'->TODO
#)
---linkObj:descriptor ---
(#
do (if true
    // isJava then
       (* hack *)
       (if (lObj.length >= 9) and ((lObj.length-8->lObj.inxGet) = '.') then
           (* lObj = foo.J.class *)
           (lObj.length-7,lObj.length-6) -> lObj.delete;
       if);
       lObj[] -> ldA.putText; '\\\n\t' -> ldA.puttext
    // isIL then
       (if use_dotnet_modules then
           (* lObj = '"foo.il.netmodule"' *)
           '.il.netmodule"' -> lObj.stripExtension;
           '.netmodule"' -> lObj.appendExtension;
           ' '->ldA.puttext;
           lObj[] -> ldA.putText; 
        else
           (* lObj = '"foo.il.dll"' *)
           '.il.dll"' -> lObj.stripExtension;
           '.dll"' -> lObj.appendExtension;
           (* File not inserted *)
       if);
    else
       'linkObj' -> TODO;
   if);
#)
---libraries: descriptor ---
(#
do 'libraries'->TODO
#)
--linktrailer:descriptor--
(# 
do (if isJava then AFF.newline; if);
#)
---trailer: descriptor ---
(# e: @diskentry;
do (if true
    // isIL then
       (* 'if exists "' -> AFF.puttext;
        * objName[] -> AFF.puttext;
        * '.exe ' -> AFF.puttext;
        *)
       'echo Object program on file: '->AFF.puttext;
       objName[] -> AFF.puttext;
       '.exe' -> AFF.putline;
    // isJava then
       '/bin/rm -f '                             -> AFF.puttext; 
       objName[]                                 -> AFF.puttext;
       '.manifest'                               -> AFF.putline;
       'echo "#!/bin/sh"                       >'-> AFF.puttext; 
       objName[]                                 -> AFF.puttext;
       '\necho "exec java -jar '                 -> AFF.puttext;
       objName[]                                 -> AFF.puttext; 
       '.jar" >> '                               -> AFF.puttext;
       objName[]                                 -> AFF.puttext;
       '\nchmod a+x '                            -> AFF.puttext;
       objName[] -> AFF.puttext;
       '\necho Object program on file: '->AFF.puttext;
       objName[] -> AFF.putline
    else
       'trailer: unknown platform' -> TODO;
   if);
#)

--AddSharedLibFile: descriptor--
(# 
do 'AddSharedLibFile'->TODO
#)

--jobFilePermission: descriptor--
(# 
do AFF.setExe;
#)

--AddLinkDirs: descriptor--
(# 
do 'AddLinkDirs'->TODO   
#)


