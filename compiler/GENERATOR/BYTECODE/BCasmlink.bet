ORIGIN '../asmlinkbody' ;
INCLUDE '~beta/basiclib/fileSetExe';
INCLUDE '~beta/basiclib/pcre';
INCLUDE '~beta/basiclib/formatio';
INCLUDE '~beta/containers/list';
INCLUDE '~beta/containers/dictionary';
INCLUDE '~beta/basiclib/texthash';

(* verboseLevels:
 * verbose: (# exit 0 #); {* more than default? *}
 * default: (# exit 1 #); {* normal, the old way, open, bind, etc. *}
 * actions: (# exit 2 #); {* only if somthing happens, parse, etc.*}
 * nothing: (# exit 3 #); {* nothing, only errors *}
 *)

--lib: attributes--
unQuote:
  (# t: ^text;
  enter t[]
  do (if ((1 -> t.inxGet) = '"') AND ((t.length -> t.inxGet) = '"') then
         (2, t.length - 1) -> t.sub -> t[];
     if);
  exit t[]
  #);
tick:
  (# t, new: ^text;
  enter t[]
  do '\''->new[];
     T.scanall(# do (if ch='\'' then '"' -> new.put else ch->new.put if) #);
     '\''->new.append;
     new[] -> t[];
  exit t[]
  #);
escDollar:
  (# T,S: ^text
  enter T[]
  do &text[] -> S[];
     T.scanAll
     (#
     do (if ch = '$' then
            '\\' -> S.put
        if);
        ch -> S.put
     #);
  exit S[]
  #);
sh2cmd:
  (* Hack used to replace unix sh commends with cmd.exe commands when host is windows *)
  (# cmd: ^text;
     colon2amp: @pcre;
     mv2move: @pcre;
     success: @boolean;
  enter cmd[]
  do ';' -> colon2amp;
     'mv ' -> mv2move;
     (cmd[], '&&') -> colon2amp.replaceAll -> (success, cmd[]);
     (cmd[], 'move ') -> mv2move.replaceAll -> (success, cmd[]);
  exit cmd[]
  #);

TODO: 
  (# T: ^text enter T[] 
  do 'TODO: ' -> screen.puttext;
     T[] -> screen.putline 
  #);
AppendExe:
  (# objname: ^text;
  enter objname[]
  exit '.exe' -> objname.appendExtension
  #);

containerlist: list(# #);

textlist: list(# element:: text; #);

--translateasmlinklib: attributes ---
(* Configuration: *)
use_java_classpath:
  (* Use 'java -cp ... for executing jar files instead of 'java -jar ...' *)
  (# exit true #);
main_in_program:
  (# exit true #);
no_jar_compression:
  (# exit hostIsWindows #);
use_directory_map:
  (* Change directory to jvm directory and add all needed files 
   * from there using one jar command. 
   * Instead of using -C <jvm dir> for each class file, which is in the order
   * of a *factor 10* slower!
   *)
  (# exit false #);
use_jar_atfile:
  (* Use (undocumented!) command file for jar.
   * Solution pointed out by neal.gafter@sun.com.
   *)
  (# exit true #);
use_relative_jar_paths:
  (# exit hostIsWindows and (not use_directory_map) #);

isJava:  
  (# exit common.targetMachineId = common.jvm #);
isIL:    
  (# exit common.targetMachineId = common.clr #);
applicationbase:
  (# path: ^text;
     e: @diskentry;
  do CodeDirectory.copy -> path[] (* Full path *);
     DirectoryChar -> path.put;
     objname[] -> e.path; e.path.name -> path.append;
     INNER
  exit path[]
  #);
is_full_path: booleanValue
  (# path: ^text
  enter path[]
  do (if hostIsWindows then
         (path[]<>NONE) and (path.lgth>=2) and (path.T[2]=':') -> value
      else
         (path[]<>NONE) and (path.lgth>=2) and (path.T[1]='/') -> value
     if);
  #);
lineCont: 
  (# T: ^text;
  do (if hostIsWindows then
         ' ^\n\t' -> T[]
      else
         ' \\\n\t' -> T[]
     if);
  exit T[]
  #);

--asmlinklib: attributes --
break_long_line: booleanValue
  (# length: @integer
  enter length
  do (if use_jar_atfile then
         (* line cannot become too long *)
         false -> value;
      else
         (if hostIsWindows then
             (* http://www.ss64.com/nt/cmd.html *)
             length+asmstate.link_line_length>8192 -> value
          else
             (* SH limit? *)
             length+asmstate.link_line_length>30000 -> value
         if);
     if);
  #);
AFFputText:
  (# T: ^text
  enter T[]
  do asmstate.link_line_length+T.lgth->asmstate.link_line_length;
     T[] -> AFF.puttext;
  #);
jarfilename: applicationbase
  (# 
  do '.jar' -> path.append;
  #);
jatfilename: applicationbase
  (# 
  do '.jat' -> path.append;
  #);
jaroptions:
  (# cmd: ^text;
     update: @boolean;
  enter update
  do 0 -> asmstate.link_line_length;
     '' -> cmd[];
     INNER;
     (if update then
         'uf' -> cmd.puttext;           
      else
         'cf' -> cmd.puttext;           
     if);
     (if verboseLevel <= verboseLevel.verbose then
         'v' ->cmd.puttext;
     if);
     (if no_jar_compression then
         '0' -> cmd.put;
     if);
     ' '-> cmd.puttext; 
     (if hostIsWindows then
         jarfilename -> cmd.puttext; 
      else
         jarfilename -> tick -> cmd.puttext; 
     if);
     (if not use_jar_atfile then
         lineCont -> cmd.puttext;
     if);
  exit cmd[]
  #);  
jarcommand: jaroptions
  (# 
  do 'jar ' -> cmd.puttext;
  #);

CheckLinkError:
  (# cmd: ^text;
  do ''->cmd[];
     (if hostIsWindows then
         '\nif errorlevel 1 goto linkerror' -> cmd.putLine;
      else
         '\n'
         'case $? in\n'
         '0)\n'
         '   ;;\n'       
         '*)\n'
         '   echo Errors during link.\n' 
         '   exit 1\n'
         '   ;;\n'
         'esac'->cmd.PutLine;
     if);
  exit cmd[]
  #);

--asmState:descriptor--
(# link_line_length: @integer;
   verifylist: @containerlist(# element::text #);
   program_path: ^text;
   jvmregexp: @pcre;
   
   directoryMap: @dictionary
     (* Maps directory (full) paths to lists of class files in that directory *)
     (# key:: text;
        keyEqual::
          (# 
          do (if hostIsWindows then
                 right[]->left.equalNCS->value
              else
                 right[]->left.equal->value
             if);
          #);
        element:: textlist;
        honeyM: @honeyMan;
        init:: (# do honeyM.init; #);
        hashFunction::(# do k[]-> honeyM.hash -> value #);
        insert:
          (# dir, file: ^text;
             files: ^textlist;
          enter (dir[], file[])
          do (if (dir[]->lookup->files[])=NONE then
                 &textlist[] -> files[];
                 (dir[], files[]) -> associate;
             if);
             file[] -> files.append;
          #);
     #);

#)
--HasSharedLibs:descriptor--
(#do false -> value #)
---cont: descriptor --
(# 
do lineCont -> AFFputtext
#)
---header: descriptor ---
(#
do 'jvm/' -> asmstate.jvmregexp; (* used by linkObj *)
   (if true
    // isIL or hostIsWindows then
       asmstate.verifylist.init;
       '@echo off\n'
         ->AFF.putText;
       (if isIL then
           'if exist "ilasm.out" del "ilasm.out" >NUL 2>&1\n'
             ->AFF.putText;
       if);
    // isJava then
       '#! /bin/sh '->AFF.putLine;
       'BETALIB='->AFF.putText; BetaLib[]->AFF.putLine;
       'export BETALIB'->AFF.putLine;
       'MACHINETYPE='->AFF.putText; TargetMachine->AFF.putLine;
       'export MACHINETYPE'->AFF.putLine;
       'TMPDIR=${TMPDIR-/tmp}'->AFF.putLine;
    else
       'AsmHeader: unknown platform' -> TODO; 
   if);
#)
---AsmExt: descriptor ---
(#
do (if true
    // isIL then
       '.il' -> ext;
    // isJava then
       '.J' -> ext
    else
       'AsmExt: unknown platform' -> TODO;
   if);
#) 
---BinExt: descriptor ---
(# 
do (if true
    // isIL then
       '.dll' -> ext;
    // isJava then
       '.dep' -> ext
    else
       'BinExt: unknown platform' -> TODO;
   if);
#)
---LibExt: descriptor ---
(# 
do 'LibExt'->TODO
#)
---JobExt: descriptor ---
(# 
do (if true
    // isIL or hostIsWindows then
       '.bat' -> ext;
    // isJava then
       '..job' -> ext
    else
       'JobExt: unknown platform' -> TODO;
   if);
#) 
---ExeExt:descriptor---
(# 
do (if true
    // isIL then
       '.exe' -> ext;
    else
       ''-> ext (* not used by Java *)
   if);
   #)
---Echo: descriptor ---
(# 
do (if true
    // isIL then
       'echo '->AFF.puttext;
    // isJava then
       'echo '->AFF.puttext;
    else
       'Echo: unknown platform' -> TODO;
   if);
#)
---Remove: descriptor ---
(# 
do (if true
    // isIL or hostIsWindows then
       (if check then
           'if exist "'->AFF.putText; 
           name[]->AFF.putText; 
           (if isJava then  
               '.bat' -> AFF.puttext;
           if);
           '" '->AFF.putText
       if);
       'del "'->AFF.putText; 
       name[]->AFF.putText; 
       (if isJava then  
           '.bat' -> AFF.puttext;
       if);
       '"' -> AFF.put; 
       (if verboseLevel > verboseLevel.verbose then
           ' >NUL 2>&1'->AFF.puttext;
       if);
       '' -> AFF.putline;
    // isJava then
       'rm -f '->AFF.puttext; name[]->AFF.putline
    else
       'remove' -> todo
   if);
#)
---ChangeDirectory:doPart---
do (if true
    // isIL or hostIsWindows then
       (if (dir.lgth>1) and (dir.T[2]=':') then
           (* change drive *)
           dir.T[1]->AFF.put; ':' -> AFF.put; AFF.newline;
       if);
       'cd '->AFF.puttext; dir[] -> slashToBackslash -> quote -> AFF.putline
    // isJava then
       'cd '->AFF.puttext; dir[] -> tick -> AFF.putline
    else
       'ChangeDirectory'->TODO
   if);
       
---AddCommand:doPart---
do (if true
    // isIL or hostIsWindows then
       cmd[] -> sh2cmd -> AFF.putline;
       'if errorlevel 1 goto builderror' -> AFF.putLine;
    // isJava then
       cmd[] -> AFF.putline;
    else
       'AddCommand'->TODO
   if);
---Make: descriptor ---
(# 
do 'Make'->TODO
#)
---resource:descriptor---
(#
do 'resource'->TODO
#)
---assemble: descriptor ---
(# e: @diskentry;
   isProgram: @boolean;
   out: ^text;
   FF: ^text;
   
do (if common.switch[184] or common.switch[189] then
       asmext -> F.append;
   if);
   F[] -> e.path;
   (if not e.exists then
       (if isJava then
           (if common.switch[18] then
               (if not hostIsWindows then
                   F[] -> escDollar -> F[];
               if);
               (# FN: ^text
               do F.copy -> FN[];
                  'jcf-dump -c "' -> AFF.puttext;
                  '.class' -> (FN.copy).append -> AFF.puttext;
                  '" > "' -> AFF.puttext;
                  '..j"' -> (FN.copy).append -> AFF.putline
               #)
           if)
        else           
           (if verboseLevel <= verboseLevel.verbose then
               'OOPS! AsmLink - skipping non-existing\n\t' -> screen.puttext;
               F[] -> screen.putline;
               ' (compiler error).' 
                 -> screen.putline;
       if)if)
    else
       (F.copy, '') -> thePathHandler.localPath -> FF[];
       'program.il' -> (e.path.name).equal -> isProgram;
       (if verboseLevel <= verboseLevel.actions then
           AFF.newline;
           (if not isJava then
               (if not isProgram then
                   echo;
                   'assembling '->AFF.puttext;
                   FF[] ->AFF.putline;
               if)
           if);
       if);
       (if true
        // isIL then
           (if isProgram and (not common.switch[37]) then
               (* program.il is assembled to foo.exe by link phase.
                * Only if doing verification (switch 37) should program.dll be created.
                *)
            else
               'ilasm /nologo /dll '->AFF.puttext; 
               (if verboseLevel > verboseLevel.verbose then
                   '/quiet ' ->AFF.puttext;
               if);
               (if true (*common.switch[41]*) then 
                   '/debug '->AFF.puttext
               if);
               '/output:' -> AFF.puttext;
               '.il' -> F.stripextension;
               '.dll' -> (F.copy).appendExtension -> AFF.puttext;
               ' '-> AFF.put;
               '.il' -> (F.copy).appendExtension -> AFF.puttext;
               (if verboseLevel > verboseLevel.verbose then
                   ' >"ilasm.out" 2>&1'->AFF.puttext;
               if);
               AFF.newline;
               (if not common.switch[21] then
                   'if errorlevel 1 goto asmerror' -> AFF.putLine;
               if);
               F[] -> asmstate.verifylist.append;
           if)
        // isJava then
           (*'jasmin '->AFF.putText; F[]->AFF.putLine*)
        else
           'assemble: unknown platform' -> TODO;
       if);
   if);
#)
--linkLibraries:descriptor--
(#
do 'linkLibraries'->TODO
#)
--contCh:descriptor--
(# 
do (if hostIsWindows then
       '^' -> value
    else
       '\\' -> value
   if);
#)
--ldArgMax:descriptor--
(# 
do 1000000 -> value
#)
--ldTmp:descriptor--
(# 
do 'ldTmp'->TODO
#)
---link: descriptor ---
(# e: @diskentry;
   GenerateClrConfigFile:
     (# ConfigFileName: ^text;
     do ObjName.copy -> ConfigFileName[];
        '.exe.config' -> ConfigFileName.append;
        WriteConfig:
          (# f: @file
               (# error: 
                    (# 
                    do 'Failed to create '
                         ->(ConfigFileName.copy).prepend 
                         -> screen.putline; 
                       leave WriteConfig;
                    #);
                  AccessError::(# do error #);
                  WriteError::(# do error #);
                  NoSpaceError::(# do error #);
               #);
             e: @diskentry;
             probingdir: ^text;
          do 'clr\\' -> probingdir[];
             objname[] -> e.path;
             e.path.name -> probingdir.append;
             ConfigFileName[] -> f.name;
             (if f.entry.exists then
                 (* Will no overwrite existing .config file *)
                 '.new' -> ConfigFileName.append;
                 ConfigFileName[] -> f.name;
             if);
             f.openwrite;
             '<configuration>\n'
             '   <runtime>\n'
             '      <assemblyBinding xmlns="urn:schemas-microsoft-com:asm.v1">\n'
             '         <probing privatePath="%s"/>\n'
             '      </assemblyBinding>\n'
             '   </runtime>\n'
             '</configuration>\n'
               -> f.putformat(# do probingdir[] -> s #);
             f.close;
          #)
     #);
   
   GenerateClrExe:
     (#
     do 'ilasm /nologo '->AFF.puttext; 
        (if false and common.switch[71] then
            (* No console *)
            '/winexe ' -> AFF.puttext;
         else
            '/exe ' -> AFF.puttext;
        if);
        (if verboseLevel > verboseLevel.verbose then
            '/quiet ' ->AFF.puttext;
        if);
        (if true (*common.switch[41]*) then 
            '/debug '->AFF.puttext
        if);
        '/output:' -> AFF.puttext;
        ObjName[] -> AFF.puttext;
        '.exe ' -> AFF.puttext;
        'clr\\' -> AFF.puttext;
        (if main_in_program then
            'program' -> AFF.puttext;
         else
            objname[] -> AFF.puttext;
        if);
        '.il' -> AFF.puttext;
        (if verboseLevel > verboseLevel.verbose then
            ' >"ilasm.out" 2>&1'->AFF.puttext;
        if);
        AFF.newline;
        (if not common.switch[21] then
            CheckLinkError -> AFF.putline;
        if);
     #);
   
   GenerateClrVerify:
     (# 
     do (if common.switch[37] then
            asmstate.verifylist.scan
            (# F: ^text;
            do (if '.exe' -> current.hasExtension then
                   current[] -> F[];
                else
                   '.dll ' -> current.append -> f[];
               if);
               AFF.newline;
               'echo verifying '->AFF.puttext;
               F[] -> AFF.puttext;
               AFF.newline;
               'peverify /nologo '->AFF.puttext; 
               F[] -> AFF.puttext;
               ' > peverify.out' -> AFF.putline;
               'if errorlevel 1 goto verifyerror' -> AFF.putLine;
               'goto verifydone' -> AFF.putline;
               ':verifyerror' -> AFF.putline;
               'echo.' -> AFF.putline;
               'echo Verification errors:' -> AFF.putline;
               'type peverify.out' -> AFF.putline;
               ':verifydone' -> AFF.putline;
               'del peverify.out' -> AFF.putline;
               'echo.' -> AFF.putline;
            #);
        if);
     #);
   
   GenerateJarFromDirectories:
     (# first_done: @boolean;
        cwd: ^text;
        AddJarCommand:
          (# files: ^textlist
          enter files[]
          do (if first_done then
                 CheckLinkError -> AFF.putline;
                 true -> jarcommand -> AFFputtext;
              else
                 false -> jarcommand -> AFFputtext;
                 true -> first_done;
             if);
             files.scan
             (# 
             do (if 7+current.length->break_long_line then
                    CheckLinkError -> AFF.putline;
                    true -> jarcommand -> AFFputtext;
                if);
                (if hostIsWindows then
                    current[] -> AFFputtext; cont;
                 else
                    current[] -> tick -> AFFputtext; cont;
                if);
             #);
             CheckLinkError -> AFF.putline;
          #);
     do CurrentDirectory -> cwd[];
        asmstate.directoryMap.scanAssociations
        (# 
        do '\n' -> AFF.puttext;
           (if ''->k.equal then
               cwd[] -> ChangeDirectory;
            else
               k[] -> ChangeDirectory;
           if);
           e[] -> AddJarCommand;
        #);
        (if first_done then        
            (* CD back to where we started *)
            '\n\n' -> AFF.puttext;
            cwd[] -> ChangeDirectory;
        if);
     #);
        
do (if true
    // isJava then
       (if true
        // use_directory_map then
           GenerateJarFromDirectories
        // use_jar_atfile then
           'echo ' -> AFF.puttext;
           false -> jaroptions -> tick -> AFF.puttext;
           ' > ' -> AFF.puttext;
           jatfilename -> AFF.putline;
        else
           '\n' -> AFFputtext;
           false -> jarcommand -> AFFputtext;
       if);
    // isIL then
       (* Ensure applicationbase directory existence *)
       (# applicationbasedir: @Directory;
       do applicationbase -> applicationbasedir.name;
          applicationbasedir.touch;
       #);
       (if true (*common.switch[12]*) then
           (* Delete exising applicationbase directory contents *)
           'del /f /q /s ' -> AFF.puttext;
           applicationbase -> quote -> AFF.puttext;
           (if verboseLevel > verboseLevel.verbose then
               ' >NUL 2>&1' -> AFF.puttext;
           if);
       if);
       AFF.newline;
       (if not common.switch[6] then
           GenerateClrExe;
           GenerateClrConfigFile;
       if);
       GenerateClrVerify;
       AFF.newline;
    else
       'link' -> TODO;
   if);
#)

---addBetaRun:descriptor---
(#
do (if true
    // isJava then
       (* currently nothing *)
    // isIL then
       (* currently nothing *)
    else
       'addbetarun' -> TODO;
   if);
#)
--asmlinkdynamic:descriptor--
(#
do 'asmlinkdynamic'->TODO
#)
---linkObj:descriptor ---
(# e: @diskentry;
   path, packagepath, name: ^text;
   ldAputText:
     (# T: ^text
     enter T[]
     do asmstate.link_line_length+T.lgth->asmstate.link_line_length;
        T[] -> ldA.puttext;
     #);
   addClassFile:
     (# path, pwd: ^text;
        jvmpath: ^text;
        start,end: @integer;
     enter path[]
     do path.copy -> path[]; (* avoid aliasing *)
        (* Find jvm directory in path *)
        (if path[] -> BackslashToSlash -> asmstate.jvmregexp.match(# do matchPos->(start,end) #) then
            (1,end-1) -> path.sub -> jvmpath[];
            (end+1, path.length) -> path.sub -> path[]
        if);
        (if use_directory_map then
            (if jvmpath[]->is_full_path then
                (jvmpath[], path[])->asmstate.directoryMap.insert;
             else
                ('',        path[])->asmstate.directoryMap.insert;
            if)
         else
            (if jvmpath[]<>none then
                (if use_relative_jar_paths then
                    thePathHandler.CurrentDirectory -> pwd[];
                    (if hostIsWindows then
                        (* Make sure drive letter on windows is same case *)
                        jvmpath.T[1] -> ascii.upcase -> jvmpath.T[1];
                        pwd.T[1]     -> ascii.upcase -> pwd.T[1];
                        jvmpath[] -> slashToBackslash;
                    if);
                    (jvmpath[], pwd[]) -> thePathhandler.localpath -> jvmpath[];
                    (if hostIsWindows then
                        jvmpath[] -> BackslashToSlash -> jvmpath[];
                    if);
                if);
            if);
            (if use_jar_atfile then
                'echo ' -> ldA.puttext;
            if);
            (if 13+jvmpath.length+path.length->break_long_line then
                CheckLinkError -> ldA.putline;
                true -> jarcommand -> ldAputtext;
            if);
            (if use_jar_atfile then
                '-C ' -> (jvmpath.copy -> tick).prepend -> tick -> ldAputtext;
             else
                ' -C ' -> (jvmpath.copy -> tick).prepend -> ldAputtext;
            if);
            ' ' -> ldA.putText;
            (if hostIsWindows and (not use_jar_atfile) then
                path[] -> quote -> ldAputtext;
             else
                path[] -> tick -> ldAputtext;
            if);
            (if use_jar_atfile then
                ' >> ' -> ldAputtext;
                jatfilename -> ldA.putline;
             else
                lineCont -> ldAputtext
            if);
        if);        
     #);
   addDllFile:
     (# lObj: ^text;
     enter lObj[]
     do (* lObj = '"foo"' *)
        lObj[] -> unquote -> lObj[];
        (*'adddllfile: ' -> puttext; lObj[] -> putline;*)
        (if lObj.length>0 then
            'copy ' -> ldA.puttext;
            '.dll' -> lObj.appendExtension -> quote -> ldA.append;
            ' ' -> ldA.append;
            applicationbase -> quote -> ldA.puttext;
            (if verboseLevel > verboseLevel.verbose then
                ' >NUL 2>&1' -> ldA.puttext;
            if);
            ldA.newline;
         else
            (failuretrace, 'Empty DLL file') -> stop;
        if);
     #);
   addFile:
     (# lObj: ^text;
     enter lObj[]  
     do (if true
         // isJava then
            lObj[] -> addClassFile;
         // isIL then
            lObj[] -> addDllFile;
         else
            'linkObj: addFile' -> TODO;
        if);
     #);
do (*'linkObj: ' -> puttext; lObj[] -> putline;*)
   (if true
    // isJava then
       lObj[] -> unquote -> path[];
       '.class' -> path.AppendExtension -> addClassFile;
       (if hostIsWindows then
           path.copy -> SlashToBackslash -> e.path;
        else
           path[] -> e.path;
       if);
       e.path.name -> name[];
       (if true
        // 'program.class'->name.equal then
           (if hostIsWindows then
               e.path.head -> BackslashToSlash -> asmstate.program_path[];
            else
               e.path.head -> asmstate.program_path[];
           if);
        // ('BetaObject.class'->name.equal) then
           (* Add compiler generated special classes too *)
           (* Get directory of BetaObject.class as full path *)
           e.path.head -> path[]; 
           (if path.length>0 then
               path.copy -> packagepath[];
               '/' -> packagepath.append;
            else
               '' -> packagepath[];
           if);
           'ExOException.class' -> (packagepath.copy).append -> addClassFile;
           (* Add Structure.class too *)
           'Structure.class' -> (packagepath.copy).append -> addClassFile;
           (* Add BetaArray.class too *)
           'BetaArray.class' -> (packagepath.copy).append -> addClassFile;
           (if not common.ComponentDotCJ then
               (* Add Component.class too *)
               'Component.class' -> (packagepath.copy).append -> addClassFile;
           if);
       if);
    // isIL then
       lObj[] -> addDllFile;
    else
       'linkObj' -> TODO;
   if);
#)
---libraries: descriptor ---
(#
do 'libraries'->TODO
#)
--linktrailer:descriptor--
(# 
do (if isJava then
       (if use_jar_atfile then
           'jar @' -> AFF.puttext;
           jatfilename -> AFF.putline;
           CheckLinkError -> AFF.putline;
       if);
   if);
#)
---trailer: descriptor ---
(# e: @diskentry;
   generateBatFileLabels:
     (# 
     do 'goto exit\n'
        '\n'
        ':builderror\n'
        'echo Errors during BUILD.\n'
        'goto exit\n'
        ':asmerror\n'
        'if exist "ilasm.out" type "ilasm.out"\n'
        'echo Errors during assembling.\n'
        'goto exit\n'
        ':reserror\n'
        'echo Errors during resource compiling.\n'
        'goto exit\n'
        ':linkerror\n'
        'if exist "ilasm.out" type "ilasm.out"\n'
        'echo Errors during link\n'
        'goto exit\n'
          ->AFF.putText;
        (if common.switch[37] then
            ':linkerror\necho A Verification Error Occurred\ngoto exit'->AFF.putLine;
        if);
        AFF.newline;
        ':exit\n'
          ->AFF.putText;
     #);
   generateJavaWrapperScript:
     (# 
     do (if hostIsWindows then
            'echo @echo off    > '       -> AFF.puttext; 
         else
            'echo "#!/bin/sh"  > '       -> AFF.puttext;
        if);
        objName[]                        -> AFF.puttext;
        (if hostIsWindows then  
            '.bat'                       -> AFF.puttext;
            '\necho '                    -> AFF.puttext;
         else
            '\necho exec '               -> AFF.puttext;
        if);
        (if common.switch[322] then
            (* Use GNU java interpreter *)
            'gij '                       -> AFF.puttext;
         else
            'java '                      -> AFF.puttext;
        if);
        (* (if verboseLevel <= verboseLevel.verbose then
         *     '-verbose '                  -> AFF.puttext;
         * if);
         *)
        (if use_java_classpath then
            '-cp '                       -> AFF.puttext;
         else
            '-jar '                      -> AFF.puttext;
        if);
        (* Add LIBFILES to classpath *)
        TX.scan
        (#
        do (if thisElm.kind = LibKind then 
               thisElm.T[] -> AFF.puttext;
               (if hostIsWindows then
                   ';' -> AFF.put;
                else
                   ':' -> AFF.put;
               if);
           if)
        #); 
        (CodeDirectory[], ObjDirectory[]) 
          -> thePathHandler.localpath
          -> AFF.puttext;
        DirectoryChar -> AFF.put;
        objName[] -> e.path;
        e.path.name                  -> AFF.puttext; 
        '.jar '                      -> AFF.puttext;
        (if use_java_classpath then
            (if main_in_program then
                (if (asmstate.program_path[]<>NONE) and (asmstate.program_path.length>0) then
                    (* Get program path relative to CodeDirectory.
                     * This is the package path of program.class.
                     * On SUN it works to give this path as foo/bar/program.
                     * However using GIJ this only worls using foo.bar.program.
                     * But since that works on SUN java too, we use that (:-).
                     *)
                    (asmstate.program_path[], CodeDirectory.copy) 
                      -> thePathHandler.localPath
                      -> slashToDot
                      -> AFF.puttext;
                    '.' -> AFF.put;
                if);
                'program' -> AFF.puttext;
             else
                objname[] -> AFF.puttext;
            if);
        if);
        ' >> '                       -> AFF.puttext;
        objName[]                    -> AFF.puttext;
        (if hostIsWindows then 	     
            '.bat'                   -> AFF.putline;
         else			     
            '\nchmod a+x '           -> AFF.puttext;
            objName[]                -> AFF.puttext;
        if);
     #);
do (if true
    // isIL then
       (if common.switch[6] then
           (* No link, but DLL directory created for library *)
           '\necho DLL Library created: '->AFF.puttext;
           (applicationbase, thePathHandler.CurrentDirectory) -> thePathHandler.localPath 
             -> AFF.putline;
        else
           (* Link has been done *)
           'if exist "' -> AFF.puttext;
           objName[] -> AFF.puttext;
           '.exe" ' -> AFF.puttext;
           
           '(echo Object program on file: '->AFF.puttext;
           objName[] -> AFF.puttext;
           '.exe)' -> AFF.putline;
       if);
       AFF.newline;
       generateBatFileLabels;
       (if not common.switch[23] then
           'if exist "ilasm.out" (del "ilasm.out")\n'
             ->AFF.putText;
       if);
    // isJava then
       (if common.switch[6] then
           (* No link, but jar file created for library *)
           '\necho Library JAR file on file: '->AFF.puttext;
           (jarfilename, thePathHandler.CurrentDirectory) -> thePathHandler.localPath 
             -> AFF.putline;
        else
           (* Create wrapper script for program jar file *)
           generateJavaWrapperScript;
           (if hostIsWindows then
               generateBatFileLabels;
           if);
           '\necho Object program wrapper on file: '->AFF.puttext;
           objName[] -> AFF.puttext;
           (if hostIsWindows then
               '.bat' -> AFF.puttext;
           if);
           AFF.newline;
       if);
       (if use_jar_atfile then
           (if not common.switch[23] then
               (* delete jar @file *)
               (if hostIsWindows then
                   'del ' -> AFF.puttext;
                   jatfilename -> AFF.puttext;
                   ' >NUL 2>&1' -> AFF.putline;
                else
                   'rm -f ' -> AFF.puttext;
                   jatfilename -> AFF.putline;
               if)
           if);
       if);
    else
       'trailer: unknown platform' -> TODO;
   if);
#)

--AddSharedLibFile: descriptor--
(# 
do 'AddSharedLibFile'->TODO
#)

--jobFilePermission: descriptor--
(# 
do AFF.setExe;
#)

--AddLinkDirs: descriptor--
(# 
do 'AddLinkDirs'->TODO   
#)


