ORIGIN '../asmlinkbody' ;
INCLUDE '~beta/basiclib/fileSetExe';
INCLUDE '~beta/basiclib/pcre';
INCLUDE '~beta/basiclib/formatio';
INCLUDE '~beta/containers/list';

(* verboseLevels:
 * verbose: (# exit 0 #); {* more than default? *}
 * default: (# exit 1 #); {* normal, the old way, open, bind, etc. *}
 * actions: (# exit 2 #); {* only if somthing happens, parse, etc.*}
 * nothing: (# exit 3 #); {* nothing, only errors *}
 *)

--lib: attributes--
use_clr_betarun:
  (# exit false #);
use_clr_modules:
  (# exit false #);
main_in_program:
  (# exit true #);

generate_jar_manifest:
  (# exit false #);
use_java_classpath:
  (# exit true #);

unQuote:
  (# t: ^text;
  enter t[]
  do (if ((1 -> t.inxGet) = '"') AND ((t.length -> t.inxGet) = '"') then
         (2, t.length - 1) -> t.sub -> t[];
     if);
  exit t[]
  #);
escDollar:
  (# T,S: ^text
  enter T[]
  do &text[] -> S[];
     T.scanAll
     (#
     do (if ch = '$' then
            '\\' -> S.put
        if);
        ch -> S.put
     #);
  exit S[]
  #);
sh2cmd:
  (* Hack used to replace unix sh commends with cmd.exe commands when host is windows *)
  (# cmd: ^text;
     colon2amp: @pcre;
     mv2move: @pcre;
     success: @boolean;
  enter cmd[]
  do ';' -> colon2amp;
     'mv ' -> mv2move;
     (cmd[], '&&') -> colon2amp.replaceAll -> (success, cmd[]);
     (cmd[], 'move ') -> mv2move.replaceAll -> (success, cmd[]);
  exit cmd[]
  #);

TODO: 
  (# T: ^text enter T[] 
  do 'TODO: ' -> screen.puttext;
     T[] -> screen.putline 
  #);
AppendExe:
  (# objname: ^text;
  enter objname[]
  do '.exe' -> objname.appendExtension
  exit objname[]
  #);
SlashToBackslash: 
  (# T: ^Text;
  enter T[]
  do (for i:T.lgth repeat
          (if T.T[i] = '/' then '\\' -> T.T[i] if)
     for);
  exit T[]
  #);
BackslashToSlash: 
  (# T: ^Text;
  enter T[]
  do (for i:T.lgth repeat
          (if T.T[i] = '\\' then '/' -> T.T[i] if)
     for);
  exit T[]
  #);
containerlist: list(# #);

--translateasmlinklib: attributes ---
isJava:  
  (# exit common.targetMachineId = common.jvm #);
isIL:    
  (# exit common.targetMachineId = common.clr #);
hostIsWindows: booleanValue
  (# 
  do ('nti'->((1,3)->(machine_type).sub).equal) -> value;
  #);
jarfilename:
  (# path: ^text;
  do CodeDirectory.copy -> path[];
     DirectoryChar -> path.put;
     '.jar' -> (objName.copy).append -> path.append;
  exit path[]
  #);

--asmState:descriptor--
(# link_line_length: @integer;
   verifylist: @containerlist(# element::text #);
#)
--HasSharedLibs:descriptor--
(#do false -> value #)
---cont: descriptor --
(# 
do (if hostIsWindows then
       ' ^\n\t'->AFF.puttext;
    else
       ' \\\n\t' -> AFF.puttext
   if);
#)
---header: descriptor ---
(#
do (if true
    // isIL or hostIsWindows then
       asmstate.verifylist.init;
       '@echo off\n'
         ->AFF.putText;
       (if isIL then
           'if exist "ilasm.out" del "ilasm.out"\n'
             ->AFF.putText;
       if);
    // isJava then
       '#! /bin/sh '->AFF.putLine;
       'BETALIB='->AFF.putText; BetaLib[]->AFF.putLine;
       'export BETALIB'->AFF.putLine;
       'CC=${CC-"gcc -O6"}\nexport CC'-> AFF.putLine;
       'MACHINETYPE='->AFF.putText; TargetMachine->AFF.putLine;
       'export MACHINETYPE'->AFF.putLine;
       'TMPDIR=${TMPDIR-/tmp}'->AFF.putLine;
    else
       'AsmHeader: unknown platform' -> TODO; 
   if);
#)
---AsmExt: descriptor ---
(#
do (if true
    // isIL then
       '.il' -> ext;
    // isJava then
       '.J' -> ext
    else
       'AsmExt: unknown platform' -> TODO;
   if);
#) 
---BinExt: descriptor ---
(# 
do (if true
    // isIL then
       (if use_clr_modules then
           '.netmodule' -> ext;
        else
           '.dll' -> ext;
       if);
    // isJava then
       '.class' -> ext
    else
       'BinExt: unknown platform' -> TODO;
   if);
#)
---LibExt: descriptor ---
(# 
do 'LibExt'->TODO
#)
---JobExt: descriptor ---
(# 
do (if true
    // isIL or hostIsWindows then
       '.bat' -> ext;
    // isJava then
       '..job' -> ext
    else
       'JobExt: unknown platform' -> TODO;
   if);
#) 
---ExeExt:descriptor---
(# 
do (if true
    // isIL then
       '.exe' -> ext;
    else
       ''-> ext (* not used by Java *)
   if);
   #)
---Echo: descriptor ---
(# 
do (if true
    // isIL then
       'echo '->AFF.puttext;
    // isJava then
       'echo '->AFF.puttext;
    else
       'Echo: unknown platform' -> TODO;
   if);
#)
---Remove: descriptor ---
(# 
do (if true
    // isIL or hostIsWindows then
       (if check then
           'if exist "'->AFF.putText; name[]->AFF.putText; '" '->AFF.putText
       if);
       'del "'->AFF.putText; name[]->AFF.putText; 
       (if verboseLevel > verboseLevel.verbose then
           '" >NUL'->AFF.puttext;
       if);
       '' -> AFF.putline;
    // isJava then
       'rm -f '->AFF.puttext; name[]->AFF.putline (* only OK for unix *)
    else
       'remove' -> todo
   if);
#)
---ChangeDirectory:doPart---
do (if true
    // isIL or hostIsWindows then
       (if (dir.lgth>1) and (dir.T[2]=':') then
           (* change drive *)
           dir.T[1]->AFF.put; ':' -> AFF.put; AFF.newline;
       if);
       'cd '->AFF.puttext; dir[] -> AFF.putline
    // isJava then
       'cd '->AFF.puttext; dir[] -> AFF.putline
    else
       'ChangeDirectory'->TODO
   if);
       
---AddCommand:doPart---
do (if true
    // isIL or hostIsWindows then
       cmd[] -> sh2cmd -> AFF.putline;
       'if errorlevel 1 goto builderror' -> AFF.putLine;
    // isJava then
       cmd[] -> AFF.putline;
    else
       'AddCommand'->TODO
   if);
---Make: descriptor ---
(# 
do 'Make'->TODO
#)
---resource:descriptor---
(#
do 'resource'->TODO
#)
---assemble: descriptor ---
(# e: @diskentry;
   isProgram: @boolean;
   out: ^text;
   FF: ^text;
   
do (if common.switch[184] or common.switch[189] then
       asmext -> F.append;
   if);
   F[] -> e.path;
   (if not e.exists then
       (if isJava then
           (if common.switch[18] then
               (if not hostIsWindows then
                   F[] -> escDollar -> F[];
               if);
               (# FN: ^text
               do (F.length-1,F.length) -> (F.copy).delete -> FN[];
                  'jcf-dump -c "' -> AFF.puttext;
                  '.class' -> (FN.copy).append -> AFF.puttext;
                  '" > "' -> AFF.puttext;
                  '..j"' -> (FN.copy).append -> AFF.putline
           #)if)
        else           
           (if verboseLevel <= verboseLevel.verbose then
               'OOPS! AsmLink: skipping non-existing\n\t' -> screen.puttext;
               F[] -> screen.putline;
               ' (compiler error).' 
                 -> screen.putline;
       if)if)
    else
       (F.copy, '') -> thePathHandler.localPath -> FF[];
       'program.il' -> (e.path.name).equal -> isProgram;
       (if verboseLevel <= verboseLevel.actions then
           AFF.newline;
           (if not isJava then
               (if not isProgram then
                   echo;
                   'assembling '->AFF.puttext;
                   FF[] ->AFF.putline;
               if)
           if);
       if);
       (if true
        // isIL then
           (if isProgram and (not common.switch[37]) then
               (* program.il is assembled to foo.exe by link phase.
                * Only if doing verification (switch 37) should program.dll be created.
                *)
            else
               'ilasm /nologo /dll '->AFF.puttext; 
               (if verboseLevel > verboseLevel.verbose then
                   '/quiet ' ->AFF.puttext;
               if);
               (if true (*common.switch[41]*) then 
                   '/debug '->AFF.puttext
               if);
               '/output:' -> AFF.puttext;
               '.il' -> F.stripextension;
               F[] -> AFF.puttext;
               (if use_clr_modules then
                   '.netmodule ' -> AFF.puttext;
                else
                   '.dll ' -> AFF.puttext;
               if);
               F[] -> AFF.puttext;
               (if verboseLevel > verboseLevel.verbose then
                   ' >"ilasm.out" 2>&1'->AFF.puttext;
               if);
               AFF.newline;
               (if not common.switch[21] then
                   'if errorlevel 1 goto asmerror' -> AFF.putLine;
               if);
               F[] -> asmstate.verifylist.append;
           if)
        // isJava then
           (*'jasmin '->AFF.putText; F[]->AFF.putLine*)
        else
           'assemble: unknown platform' -> TODO;
       if);
   if);
#)
--linkLibraries:descriptor--
(#
do 'linkLibraries'->TODO
#)
--contCh:descriptor--
(# 
do (if hostIsWindows then
       '^' -> value
    else
       '\\' -> value
   if);
#)
--ldArgMax:descriptor--
(# 
do 1000000 -> value
#)
--ldTmp:descriptor--
(# 
do 'ldTmp'->TODO
#)
---link: descriptor ---
(# e: @diskentry;
   MakeClrConfigFile:
     (# ConfigFileName: ^text;
     do ObjName.copy -> ConfigFileName[];
        '.exe.config' -> ConfigFileName.append;
        WriteConfig:
          (# f: @file
               (# error: 
                    (# 
                    do 'Failed to create '
                         ->(ConfigFileName.copy).prepend 
                         -> screen.putline; 
                       leave WriteConfig;
                    #);
                  AccessError::(# do error #);
                  WriteError::(# do error #);
                  NoSpaceError::(# do error #);
               #);
          do ConfigFileName[] -> f.name;
             (if f.entry.exists then
                 (* Will no overwrite existing .config file *)
                 '.new' -> ConfigFileName.append;
                 ConfigFileName[] -> f.name;
             if);
             f.openwrite;
             '<configuration>\n'
             '   <runtime>\n'
             '      <assemblyBinding xmlns="urn:schemas-microsoft-com:asm.v1">\n'
             '         <probing privatePath="clr"/>\n'
             '      </assemblyBinding>\n'
             '   </runtime>\n'
             '</configuration>\n'
               -> f.puttext;
             f.close;
          #)
     #);
   
   BuildClrBetarun:
     (# cs: @diskentry;
        bd: @diskentry;
        csc:
          (# 
          do 'csc /t:module /out:' -> AFF.puttext;
             e.path -> AFF.puttext;
             ' ' -> AFF.put;
             cs.path -> AFF.putline;
          #);
     do (if use_clr_betarun then
            (if not userDefinedBetaRun then
                ('~beta/betarun/CS/betarun.cs','') -> thePathHandler.convertFilePath -> cs.path;
                (if cs.exists then
                    '.dll' -> (betarun.copy).append -> e.path;
                    (if (not e.exists) or (e.modtime < cs.modtime) then
                        (betarunlib[], '') -> thePathHandler.convertFilePath -> bd.path;
                        (if (not bd.exists) then
                            echo; 'Creating directory ' -> AFF.puttext; bd.path -> AFF.putline;
                            bd.path -> (&directory[]).touchDir
                        if);
                        echo; 'Building ' -> AFF.puttext; e.path -> AFF.putline;
                        csc;
                    if);
                    'copy ' -> AFF.puttext; e.path -> AFF.puttext; ' .' -> AFF.putline;
                if)
            if)
        if)
     #);
   
   GenerateClrVerify:
     (# 
     do (if common.switch[37] then
            asmstate.verifylist.scan
            (# F: ^text;
            do (if '.exe' -> current.checkExtension then
                   current[] -> F[];
                else
                   (if use_clr_modules then
                       '.netmodule ' -> current.append -> f[];
                    else
                       '.dll ' -> current.append -> f[];
                   if);
               if);
               AFF.newline;
               'echo verifying '->AFF.puttext;
               F[] -> AFF.puttext;
               AFF.newline;
               'peverify /nologo '->AFF.puttext; 
               F[] -> AFF.puttext;
               ' > peverify.out' -> AFF.putline;
               'if errorlevel 1 goto verifyerror' -> AFF.putLine;
               'goto verifydone' -> AFF.putline;
               ':verifyerror' -> AFF.putline;
               'echo.' -> AFF.putline;
               'echo Verification errors:' -> AFF.putline;
               'type peverify.out' -> AFF.putline;
               ':verifydone' -> AFF.putline;
               'del peverify.out' -> AFF.putline;
               'echo.' -> AFF.putline;
            #);
        if);
     #);
        
do (if true
    // isJava then
       (if generate_jar_manifest then
           (if packagePath[] = none then
               'echo Name: jvm/program.class >' -> AFF.puttext;
            else
               'echo Name: jvm/' -> AFF.puttext;
               packagePath[] -> AFF.puttext;
               'program.class >' -> AFF.puttext;
           if);
           objName[]->AFF.puttext;
           '.manifest' -> AFF.putline;
           'echo Main-Class: program >>' -> AFF.puttext;
           objName[]->AFF.puttext;
           '.manifest' -> AFF.putline;
           '\njar cmf' -> AFF.puttext;
        else
           '\njar cf' -> AFF.puttext;           
       if);
       (if verboseLevel <= verboseLevel.verbose then
           'v' ->AFF.puttext;
       if);
       ' '-> AFF.puttext; 
       (if generate_jar_manifest then
           objName[]->AFF.puttext;
           '.manifest ' -> AFF.puttext;
       if);
       jarfilename -> AFF.puttext; ' '->AFF.put; cont;
    // isIL then
       (if use_clr_betarun then
           BuildClrBetarun;
           'al /nologo ' -> AFF.puttext;
           (if common.switch[71] then
               (* No console *)
               '/t:winexe ' -> AFF.puttext;
            else
               '/t:exe ' -> AFF.puttext;
           if);
           '/main:betaenv.main ';
           (if common.switch[41] then 
               (* debug info? *)
           if);
           '/out:' -> AFF.puttext;
           objName.copy -> AppendExe -> quote -> AFF.puttext;
        else
           'ilasm /nologo '->AFF.puttext; 
           (if false and common.switch[71] then
               (* No console *)
               '/winexe ' -> AFF.puttext;
            else
               '/exe ' -> AFF.puttext;
           if);
           (if verboseLevel > verboseLevel.verbose then
               '/quiet ' ->AFF.puttext;
           if);
           (if true (*common.switch[41]*) then 
               '/debug '->AFF.puttext
           if);
           '/output:' -> AFF.puttext;
           ObjName[] -> AFF.puttext;
           '.exe ' -> AFF.puttext;
           'clr\\' -> AFF.puttext;
           (if main_in_program then
               'program' -> AFF.puttext;
            else
               objname[] -> AFF.puttext;
           if);
           '.il' -> AFF.puttext;
           (if verboseLevel > verboseLevel.verbose then
               ' >"ilasm.out" 2>&1'->AFF.puttext;
           if);
           AFF.newline;
       if);
       (if not common.switch[21] then
           'if errorlevel 1 goto linkerror' -> AFF.putLine;
       if);
       MakeClrConfigFile;
       (*'.exe' -> (objname.copy).append -> asmstate.verifylist.append;*)
       GenerateClrVerify;
    else
       'link' -> TODO;
   if);
#)

---addBetaRun:descriptor---
(#
do (if true
    // isJava then
       (* currently nothing *)
    // isIL then
       (if use_clr_betarun then
           (if userDefinedBetaRun then
               betaRun[]->linkObj
            else
               '.dll'->betaRun.CopyAppend->linkObj
           if)
       if)
    else
       'addbetarun' -> TODO;
   if);
#)
--asmlinkdynamic:descriptor--
(#
do 'asmlinkdynamic'->TODO
#)
---linkObj:descriptor ---
(# e: @diskentry;
   path, name: ^text;
   pwd: ^text;
   ldAputText:
     (# T: ^text
     enter T[]
     do asmstate.link_line_length+T.lgth->asmstate.link_line_length;
        T[] -> ldA.puttext;
     #);
   addClassFile:
     (# name: ^text
     enter name[]
     do ' -C ' -> ldAputtext;
        path[] -> quote -> ldAputtext;
        ' ' -> ldA.putText;
        (if hostIsWindows then
            name[] -> quote -> ldAputtext;
            ' ^\n\t' -> ldAputtext
         else
            name[] -> escDollar -> quote -> ldAputtext;
            ' \\\n\t' -> ldAputtext
        if);
     #);
do 
   (if true
    // isJava then
       (if asmstate.link_line_length>4096 then
           '\n\njar uf ' -> ldA.puttext;
           jarfilename -> ldA.puttext;
           ' '-> ldA.put;
           0 -> asmstate.link_line_length;
       if);
       lObj[] -> unquote -> name[];
       '.J.class' -> name.stripExtension;
       '.class' -> name.AppendExtension;
       name[] -> e.path;
       (* Extract path of lObj as a relative path from current directory *)
       e.path.head -> path[];
       thePathHandler.CurrentDirectory -> pwd[];
       (if hostIsWindows then
           (* Make sure drive name is same case *)
           path.T[1] -> ascii.upcase -> path.T[1];
           pwd.T[1]  -> ascii.upcase -> pwd.T[1];
       if);
       (path[], pwd[]) -> thePathHandler.localPath -> path[];
       e.path.name -> name[] -> addClassFile;
       (if 'BetaObject.class'->name.equal then
           (* Add ExOException.class too *)
           'ExOException.class' -> addClassFile;
           (* Add Structure.class too *)
           'Structure.class' -> addClassFile;
           (* Add BetaArray.class too *)
           'BetaArray.class' -> addClassFile;
       if);
    // isIL then
       (if use_clr_modules then
           (* lObj = '"foo.il.netmodule"' *)
           '.il.netmodule"' -> lObj.stripExtension;
           '.netmodule"' -> lObj.appendExtension;
           ' '->ldA.puttext;
           (if not hostIsWindows then
               lObj[] -> escDollar -> quote -> ldA.putText; 
           if);
        else
           (* lObj = '"foo.il.dll"' *)
           '.il.dll"' -> lObj.stripExtension;
           '.dll"' -> lObj.appendExtension;
           (* File not inserted *)
       if);
    else
       'linkObj' -> TODO;
   if);
#)
---libraries: descriptor ---
(#
do 'libraries'->TODO
#)
--linktrailer:descriptor--
(# 
do (if isJava then AFF.newline; if);
#)
---trailer: descriptor ---
(# e: @diskentry;
   generateBatFileLabels:
     (# 
     do 'goto exit\n'
        '\n'
        ':builderror\n'
        'echo Errors during BUILD.\n'
        'goto exit\n'
        ':asmerror\n'
        'if exist "ilasm.out" type "ilasm.out"\n'
        'echo Errors during assembling.\n'
        'goto exit\n'
        ':reserror\n'
        'echo Errors during resource compiling.\n'
        'goto exit\n'
        ':linkerror\n'
        'if exist "ilasm.out" type "ilasm.out"\n'
        'echo Errors during link\n'
        'goto exit\n'
          ->AFF.putText;
        (if common.switch[37] then
            ':linkerror\necho A Verification Error Occurred\ngoto exit'->AFF.putLine;
        if);
        AFF.newline;
        ':exit\n'
          ->AFF.putText;
     #);
   generateJavaWrapperScript:
     (# 
     do (if hostIsWindows then
            'echo @echo off    > '       -> AFF.puttext; 
         else
            'echo "#!/bin/sh"  > '       -> AFF.puttext;
        if);
        objName[]                        -> AFF.puttext;
        (if hostIsWindows then  
            '.bat'                       -> AFF.puttext;
            '\necho '                    -> AFF.puttext;
         else
            '\necho exec '               -> AFF.puttext;
        if);
        (if common.switch[322] then
            (* Use GNU java interpreter *)
            'gij '                       -> AFF.puttext;
         else
            'java '                      -> AFF.puttext;
        if);
        (if use_java_classpath then
            '-cp '                       -> AFF.puttext;
         else
            '-jar '                      -> AFF.puttext;
        if);
        (* Add LIBFILES to classpath *)
        TX.scan
        (#
        do (if thisElm.kind = LibKind then 
               thisElm.T[] -> AFF.puttext;
               (if hostIsWindows then
                   ';' -> AFF.put;
                else
                   ':' -> AFF.put;
               if);
           if)
        #); 
        (CodeDirectory[], ObjDirectory[]) 
          -> thePathHandler.localpath
          -> AFF.puttext;
        DirectoryChar -> AFF.put;
        objName[] -> e.path;
        e.path.name                               -> AFF.puttext; 
        '.jar '                                   -> AFF.puttext;
        (if use_java_classpath then
            (if main_in_program then
                'program' -> AFF.puttext;
             else
                objname[] -> AFF.puttext;
            if);
        if);
        '>> '                                     -> AFF.puttext;
        objName[]                                 -> AFF.puttext;
        (if hostIsWindows then 
            '.bat'                                -> AFF.putline;
         else
            '\nchmod a+x '                        -> AFF.puttext;
            objName[] -> AFF.puttext;
        if);
     #);
do (if true
    // isIL then
       (if not common.switch[6] then
           (* Link has been done *)
           'if exist "' -> AFF.puttext;
           objName[] -> AFF.puttext;
           '.exe" ' -> AFF.puttext;
           
           '(echo Object program on file: '->AFF.puttext;
           objName[] -> AFF.puttext;
           '.exe)' -> AFF.putline;
       if);
       AFF.newline;
       generateBatFileLabels;
       (if not common.switch[23] then
           'if exist "ilasm.out" (del "ilasm.out")\n'
             ->AFF.putText;
       if);
    // isJava then
       (if not common.switch[6] then
           (if generate_jar_manifest and (not common.switch[23]) then
               (false, '.manifest' -> (objName.copy).append) -> Remove;
           if);
           generateJavaWrapperScript;
           (if hostIsWindows then
               generateBatFileLabels;
           if);
           '\necho Object program wrapper on file: '->AFF.puttext;
           objName[] -> AFF.puttext;
           (if hostIsWindows then
               '.bat' -> AFF.puttext;
           if);
           AFF.newline;
       if);
    else
       'trailer: unknown platform' -> TODO;
   if);
#)

--AddSharedLibFile: descriptor--
(# 
do 'AddSharedLibFile'->TODO
#)

--jobFilePermission: descriptor--
(# 
do AFF.setExe;
#)

--AddLinkDirs: descriptor--
(# 
do 'AddLinkDirs'->TODO   
#)


