ORIGIN '../javaimage';

(* Writes file exceptions.class equivalent to 
 * the file written by compiling exceptions.java
 * without debug info.
 * 
 *)

--program: descriptor--
(# jcf: @JavaImage;
do 'Writing exceptions.class ... ' -> puttext;
   'exceptions.class' -> jcf.init;

   'exceptions'
     -> jcf.constant_pool.classes.addtext
     -> jcf.thisClassIndex;
   'java/lang/Object'
     -> jcf.constant_pool.classes.addtext 
     -> jcf.superClassIndex;
   
   (* Method: <init> *)
   (ACC_PUBLIC, '<init>', '()V',  1,  1) -> jcf.currentmethod.new;
   aload_0       -> jcf.emit;
   invokespecial -> jcf.emit; ('java/lang/Object', '<init>', '()V') -> jcf.emitMethodRef;
   return        -> jcf.emit;
   
   (* Method: do_throw *)
   (ACC_PUBLIC, 'do_throw', '()V',  2,  1) -> jcf.currentmethod.new;
   new           -> jcf.emit; 'java/lang/ArithmeticException' -> jcf.emitClassRef;
   dup           -> jcf.emit;
   invokespecial -> jcf.emit; ('java/lang/ArithmeticException', '<init>', '()V') -> jcf.emitMethodRef;
   athrow        -> jcf.emit;
   (* This method throws java/lang/ArithmeticException.
    * So we ought to specify Exceptions attribute?
    * According to http://java.sun.com/docs/books/vmspec/2nd-edition/html/ClassFile.doc.html
    * section 4.7.4, this is only required at java language level, and is not enforced 
    * by java VM (see last sentence of section 4.7.4).
    * So the statement below is NOT necessary, and the program works fine without it.
    *)
   (if false then
       'java/lang/ArithmeticException' -> jcf.emitThrows;
   if);

   (* Method: try_catch *)
   (ACC_PUBLIC, 'try_catch', '()V',  2,  2) -> jcf.currentmethod.new;
   (* try *)
   (*  0: *) aload_0       -> jcf.emit;
   (*  1: *) invokevirtual -> jcf.emit; ('exceptions', 'do_throw', '()V') -> jcf.emitMethodRef;
   (*  4: *) goto          -> jcf.emit; 16-4 -> jcf.emitShort;
   (* catch *)
   (*  7: *) astore_1      -> jcf.emit;
   (*  8: *) getstatic     -> jcf.emit; ('java/lang/System', 'out',  'Ljava/io/PrintStream;') -> jcf.emitFieldRef;
   (* 11: *) ldc           -> jcf.emit; 'Caught ArithmeticException' -> jcf.emitString;
   (* 13: *) invokevirtual -> jcf.emit; ('java/io/PrintStream', 'println', '(Ljava/lang/String;)V') -> jcf.emitMethodRef;
   (* 16: *) return        -> jcf.emit;
   (0 (*start_pc*), 4 (*end_pc*), 7 (*handler_pc*), 'Ljava/lang/ArithmeticException;') -> jcf.emitExceptionHandlerDef;
   
   (* Method: main *)
   (ACC_PUBLIC %Bor ACC_STATIC, 'main', '([Ljava/lang/String;)V',  2,  2) -> jcf.currentmethod.new;
   new           -> jcf.emit; 'exceptions' -> jcf.emitClassRef;
   dup           -> jcf.emit;
   invokespecial -> jcf.emit; ('exceptions', '<init>', '()V') -> jcf.emitMethodRef;
   astore_1      -> jcf.emit;
   aload_1       -> jcf.emit;
   invokevirtual -> jcf.emit; ('exceptions', 'try_catch', '()V') -> jcf.emitMethodRef;
   return        -> jcf.emit;
   
   (* Now write the class file *)
   jcf.emitToFile;
   
   'done' -> putline;
#)

