ORIGIN '../../javaimage';

(* Writes file HelloWorld.class identical to 
 * the file written by compiling helloworld.java
 * without debug info, USING JAVAIMAGE.
 * 
 *)

--program: descriptor--
(# jcf: ^JavaImage;
do 'Writing HelloWorld.class ... ' -> puttext;
   &JavaImage[] -> jcf[];
   'HelloWorld.class' -> jcf.init;
   true -> jcf.traceClassFile;

   'HelloWorld'
     -> jcf.constant_pool.classes.addtext
     -> jcf.thisClassIndex;
   'java/lang/Object'
     -> jcf.constant_pool.classes.addtext 
     -> jcf.superClassIndex;
   
   (* Method: <init> *)
   (ACC_PUBLIC, '<init>', '()V',  1,  1) -> jcf.currentmethod.new;
   aload_0       -> jcf.emit;
   invokespecial -> jcf.emit; ('java/lang/Object', '<init>', '()V') -> jcf.emitMethodRef;
   return        -> jcf.emit;
   
   (* Method: print *)
   (ACC_PUBLIC, 'print', '()Ljava/lang/String;',  4,  1) -> jcf.currentmethod.new;
   new -> jcf.emit; 'java/lang/StringBuffer' -> jcf.emitClassRef;
   dup -> jcf.emit;
   invokespecial -> jcf.emit;  ('java/lang/StringBuffer', '<init>', '()V') -> jcf.emitMethodRef;
   'Hello ' -> jcf.emitLoadString;
   invokevirtual -> jcf.emit;  ('java/lang/StringBuffer', 'append', '(Ljava/lang/String;)Ljava/lang/StringBuffer;') -> jcf.emitMethodRef;
   new -> jcf.emit;  'HelloWorld$World' -> jcf.emitClassRef;
   dup -> jcf.emit;  
   aload_0 -> jcf.emit;  
   invokespecial -> jcf.emit;   ('HelloWorld$World', '<init>', '(LHelloWorld;)V') -> jcf.emitMethodRef;
   invokevirtual -> jcf.emit;   ('HelloWorld$World', 'print', '()Ljava/lang/String;') -> jcf.emitMethodRef;
   invokevirtual -> jcf.emit;   ('java/lang/StringBuffer', 'append', '(Ljava/lang/String;)Ljava/lang/StringBuffer;') -> jcf.emitMethodRef;
   invokevirtual -> jcf.emit;   ('java/lang/StringBuffer', 'toString', '()Ljava/lang/String;') -> jcf.emitMethodRef;
   
   areturn        -> jcf.emit;
   
   (* Method: main *)
   (ACC_PUBLIC %Bor ACC_STATIC, 'main', '([Ljava/lang/String;)V',  3,  1) -> jcf.currentmethod.new;
   getstatic     -> jcf.emit; ('java/lang/System', 'out', 'Ljava/io/PrintStream;')->jcf.emitFieldRef;
   new -> jcf.emit; 'HelloWorld' -> jcf.emitClassRef;
   dup -> jcf.emit; 
   invokespecial -> jcf.emit;  ('HelloWorld', '<init>', '()V') -> jcf.emitMethodRef;
   invokevirtual -> jcf.emit;  ('HelloWorld', 'print', '()Ljava/lang/String;') -> jcf.emitMethodRef;
   invokevirtual -> jcf.emit;  ('java/io/PrintStream', 'println', '(Ljava/lang/String;)V') -> jcf.emitMethodRef;
   return        -> jcf.emit;
   
   (* Mark Hello$World as inner class *)
   ('HelloWorld$World' (* Inner Class mangled name *)
   ,'HelloWorld'       (* Outer Class name *)
   ,'World'            (* Inner class unmangled name *)
   ,ACC_PUBLIC         (* Inner class access flags *))
     -> jcf.emitInnerClassRef;
   
   (* Now write the class file *)
   jcf.emitToFile;
   
   (***************************************************************)
   
   'Writing HelloWorld$World.class ... ' -> puttext;
   &JavaImage[] -> jcf[];
   'HelloWorld$World.class' -> jcf.init;
   true -> jcf.traceClassFile;

   'HelloWorld$World'
     -> jcf.constant_pool.classes.addtext
     -> jcf.thisClassIndex;
   'java/lang/Object'
     -> jcf.constant_pool.classes.addtext 
     -> jcf.superClassIndex;
   
   ('this$0', 'LHelloWorld;') -> jcf.emitPrivateFinalSyntheticFieldDef;
   
   (* Method: <init> *)
   (ACC_PUBLIC, '<init>', '(LHelloWorld;)V', 2, 2) -> jcf.currentmethod.new;
   aload_0       -> jcf.emit;
   invokespecial -> jcf.emit; ('java/lang/Object', '<init>', '()V') -> jcf.emitMethodRef;
   (* Save "origin" in synthetic private field *)
   aload_0       -> jcf.emit;
   aload_1       -> jcf.emit;
   putfield      -> jcf.emit; ('HelloWorld$World', 'this$0', 'LHelloWorld;') -> jcf.emitFieldRef;
   return        -> jcf.emit;
   
   (* Method: print *)
   (ACC_PUBLIC, 'print', '()Ljava/lang/String;',  1,  1) -> jcf.currentmethod.new;
   'World ' -> jcf.emitLoadString;
   areturn -> jcf.emit;
   
   (* Hello$World also marks itself as inner class *)
   ('HelloWorld$World' (* Inner Class mangled name *)
   ,'HelloWorld'       (* Outer Class name *)
   ,'World'            (* Inner class unmangled name *)
   ,ACC_PUBLIC         (* Inner class access flags *))
     -> jcf.emitInnerClassRef;
   
   (* Now write the class file *)
   jcf.emitToFile;
   
   'done' -> putline;
#)

