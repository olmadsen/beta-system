ORIGIN '../classfile';

(* Writes file HelloWorld.class corresponding to 
 * the file written by compiling helloworld.java
 * (except currently for line number information)
 * 
 * Examine the generated file using (Sun)
 *   javap -c  -private -s -verbose HelloWorld (must be complete file)
 * or (GNU gcc3.1)
 *   jcf-dump -c HelloWorld (forgiving)
 * 
 * If file complete, run with
 *    java HelloWorld
 *)

--program: descriptor--
(# bc: @ClassFile;
   putb: @bc.putbytes;
   mi: ^BinFile.Method_info;
   ca: ^BinFile.Code_attribute;
do 'Writing HelloWorld.class ... ' -> puttext;

   'HelloWorld.class' -> bc.name;
   bc.openWrite;
   
   (* Header *)
   (JAVA_MAGIC, JAVA_MINOR, JAVA_MAJOR) -> bc.putHeader;
   
   (* Constant Pool *)
   17 -> &bc.CONSTANT_String_info 
     -> (*1*) bc.constant_pool.add;
   
   18 -> &bc.CONSTANT_Class_info 
     -> (*2*) bc.constant_pool.add;
   23 -> &bc.CONSTANT_Class_info 
     -> (*3*) bc.constant_pool.add;
   24 -> &bc.CONSTANT_Class_info 
     -> (*4*) bc.constant_pool.add;
   25 -> &bc.CONSTANT_Class_info 
     -> (*5*) bc.constant_pool.add;
   
   (4,9) -> &bc.CONSTANT_Methodref_info 
     -> (*6*) bc.constant_pool.add;
   (5,10) -> &bc.CONSTANT_Fieldref_info 
     -> (*7*) bc.constant_pool.add;
   (3,11) -> &bc.CONSTANT_Methodref_info 
     -> (*8*) bc.constant_pool.add;
   
   (15,12) -> &bc.CONSTANT_NameAndType_info 
     -> (*9*) bc.constant_pool.add;
   (27,20) -> &bc.CONSTANT_NameAndType_info 
     -> (*10*) bc.constant_pool.add;
   (28,13) -> &bc.CONSTANT_NameAndType_info 
     -> (*11*) bc.constant_pool.add;
   
   '()V' -> &bc.CONSTANT_Utf8_info 
     -> (*12*) bc.constant_pool.add;
   '(Ljava/lang/String;)V' -> &bc.CONSTANT_Utf8_info 
     -> (*13*) bc.constant_pool.add;
   '([Ljava/lang/String;)V' -> &bc.CONSTANT_Utf8_info 
     -> (*14*) bc.constant_pool.add;
   '<init>' -> &bc.CONSTANT_Utf8_info 
     -> (*15*) bc.constant_pool.add;
   'Code' -> &bc.CONSTANT_Utf8_info 
     -> (*16*) bc.constant_pool.add;
   'Hello World!' -> &bc.CONSTANT_Utf8_info 
     -> (*17*) bc.constant_pool.add;
   'HelloWorld' -> &bc.CONSTANT_Utf8_info 
     -> (*18*) bc.constant_pool.add;
   'LineNumberTable' -> &bc.CONSTANT_Utf8_info 
     -> (*19*) bc.constant_pool.add;
   'Ljava/io/PrintStream;' -> &bc.CONSTANT_Utf8_info 
     -> (*20*) bc.constant_pool.add;
   'SourceFile' -> &bc.CONSTANT_Utf8_info 
     -> (*21*) bc.constant_pool.add;
   'helloworld.java' -> &bc.CONSTANT_Utf8_info 
     -> (*22*) bc.constant_pool.add;
   'java/io/PrintStream' -> &bc.CONSTANT_Utf8_info 
     -> (*23*) bc.constant_pool.add;
   'java/lang/Object' -> &bc.CONSTANT_Utf8_info 
     -> (*24*) bc.constant_pool.add;
   'java/lang/System' -> &bc.CONSTANT_Utf8_info 
     -> (*25*) bc.constant_pool.add;
   'main' -> &bc.CONSTANT_Utf8_info 
     -> (*26*) bc.constant_pool.add;
   'out' -> &bc.CONSTANT_Utf8_info 
     -> (*27*) bc.constant_pool.add;
   'println' -> &bc.CONSTANT_Utf8_info 
     -> (*28*) bc.constant_pool.add;
   bc.constant_pool.write;
   
   (* Access Flags *)
   ACC_SUPER -> bc.putAccessFlags;
   
   (* This Class *)
   2 -> bc.putThisClass;
   
   (* Super class *)
   4 -> bc.putSuperClass;
   
   (* Interfaces *)
   bc.interfaces.write;
   
   (* Fields *)
   bc.fields.write;
   
   (* Methods *)
   (* Method: <init> *)
   (0, 15, 12) -> &bc.Method_info -> mi[]
     -> bc.methods.add;
   (16 (*Code*), 2(*max_stack*) ,1(*max_locals*) )
     -> &bc.Code_attribute 
     -> ca[]
     -> mi.attributes.add;
   aload_0       -> ca.putByte;
   invokespecial -> ca.putByte; 6 -> ca.putShort;
   return        -> ca.putByte;
   
   (* Method: main *)
   (ACC_PUBLIC %Bor ACC_STATIC, 26, 14) -> &bc.Method_info -> mi[]
     -> bc.methods.add;
   (16 (*Code*), 2(*max_stack*) ,1(*max_locals*) )
     -> &bc.Code_attribute 
     -> ca[]
     -> mi.attributes.add;
   getstatic     -> ca.putByte; 7 -> ca.putShort;
   ldc           -> ca.putByte; 1 -> ca.putByte; (* 1 byte operand inx *)
   invokevirtual -> ca.putbyte; 8 -> ca.putShort;
   return        -> ca.putByte;
   
   bc.methods.write;
   
   (* Attributes *)
   bc.attributes.write;
   
   bc.close;

   'done' -> putline;
#)

