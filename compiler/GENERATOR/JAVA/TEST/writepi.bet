ORIGIN '../classfile';

(* Writes file Pi.class identical to 
 * the file written by compiling pi.java
 * without debug info.
 * 
 * Examine the generated file using (Sun)
 *   javap -c  -private -s -verbose Pi (must be complete file)
 * or (GNU gcc3.1)
 *   jcf-dump -c Pi (forgiving)
 * 
 * If file complete, run with
 *    java Pi
 *)

--program: descriptor--
(# bc: @ClassFile;
   putb: @bc.putbytes;
   mi: ^BinFile.Method_info;
   ca: ^BinFile.Code_attribute;
do 'Writing Pi.class ... ' -> putline;

   (* Setup Constant Pool *)
   19 -> &bc.CONSTANT_Class_info(# trace::(# do true->value #)#)
     -> (*1*) bc.constant_pool.add;
   20 -> &bc.CONSTANT_Class_info(# trace::(# do true->value #)#)
     -> (*2*) bc.constant_pool.add;
   21 -> &bc.CONSTANT_Class_info(# trace::(# do true->value #)#)
     -> (*3*) bc.constant_pool.add;
   22 -> &bc.CONSTANT_Class_info(# trace::(# do true->value #)#)
     -> (*4*) bc.constant_pool.add;
  
   (3,10) -> &bc.CONSTANT_Methodref_info(# trace::(# do true->value #)#)
     -> (*5*) bc.constant_pool.add;
   (4,11) -> &bc.CONSTANT_Fieldref_info(# trace::(# do true->value #)#)
     -> (*6*) bc.constant_pool.add;
   (2,12) -> &bc.CONSTANT_Methodref_info(# trace::(# do true->value #)#)
     -> (*7*) bc.constant_pool.add;
   
   3.1415 -> &bc.CONSTANT_Double_info(# trace::(# do true->value #)#)
     -> (*8*) bc.constant_pool.add; (* Fills TWO entries!!! *)
   
   (16,13) -> &bc.CONSTANT_NameAndType_info(# trace::(# do true->value #)#)
     -> (*10*) bc.constant_pool.add;
   (24,18) -> &bc.CONSTANT_NameAndType_info(# trace::(# do true->value #)#)
     -> (*11*) bc.constant_pool.add;
   (25,14) -> &bc.CONSTANT_NameAndType_info(# trace::(# do true->value #)#)
     -> (*12*) bc.constant_pool.add;
   
   '()V' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*13*) bc.constant_pool.add;
   '(D)V' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*14*) bc.constant_pool.add;
   '([Ljava/lang/String;)V' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*15*) bc.constant_pool.add;
   '<init>' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*16*) bc.constant_pool.add;
   'Code' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*17*) bc.constant_pool.add;
   'Ljava/io/PrintStream;' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*18*) bc.constant_pool.add;
   'Pi' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*19*) bc.constant_pool.add;
   'java/io/PrintStream' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*20*) bc.constant_pool.add;
   'java/lang/Object' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*21*) bc.constant_pool.add;
   'java/lang/System' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*22*) bc.constant_pool.add;
   'main' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*23*) bc.constant_pool.add;
   'out' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*24*) bc.constant_pool.add;
   'println' -> &bc.CONSTANT_Utf8_info(# trace::(# do true->value #)#)
     -> (*25*) bc.constant_pool.add;
   
   
   (* Setup Methods *)
   (* Method: <init> *)
   (0, 16, 13) 
     -> &bc.Method_info 
     -> mi[]
     -> bc.methods.add;
   (17 (*Code*), 1(*max_stack*) ,1(*max_locals*) )
     -> &bc.Code_attribute 
     -> ca[]
     -> mi.attributes.add;
   aload_0       -> ca.putByte;
   invokespecial -> ca.putByte; 5 -> ca.putShort;
   return        -> ca.putByte;
   
   (* Method: main *)
   (ACC_PUBLIC %Bor ACC_STATIC, 23, 15) 
     -> &bc.Method_info 
     -> mi[]
     -> bc.methods.add;
   (17 (*Code*), 3(*max_stack*) ,1(*max_locals*) )
     -> &bc.Code_attribute 
     -> ca[]
     -> mi.attributes.add;
   getstatic     -> ca.putByte; 6 -> ca.putShort;
   ldc2_w        -> ca.putByte; 8 -> ca.putShort; 
   invokevirtual -> ca.putbyte; 7 -> ca.putShort;
   return        -> ca.putByte;
   
   (* Now write the class file *)
   'Pi.class' -> bc.name;
   bc.openWrite;
   (JAVA_MAGIC, JAVA_MINOR, JAVA_MAJOR, ACC_SUPER, 1(*this*), 3(*super*)) 
     -> bc.write(# trace::(# do true->value #)#);
   bc.close;
   
   'done' -> putline;
#)

