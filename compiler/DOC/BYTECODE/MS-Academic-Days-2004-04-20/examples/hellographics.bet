ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/dotnetlib/System/Windows/Forms/Application';
INCLUDE '~beta/dotnetlib/System/Windows/Forms/Control';
INCLUDE '~beta/dotnetlib/System/Windows/Forms/Form';
INCLUDE '~beta/dotnetlib/System/Windows/Forms/PaintEventArgs';
INCLUDE '~beta/dotnetlib/System/Drawing/Brush';
INCLUDE '~beta/dotnetlib/System/Drawing/SolidBrush';
INCLUDE '~beta/dotnetlib/System/Drawing/Brushes';
INCLUDE '~beta/dotnetlib/System/Drawing/Color';
INCLUDE '~beta/dotnetlib/System/Drawing/Graphics';
INCLUDE '~beta/dotnetlib/System/Drawing/Rectangle';
INCLUDE '~beta/dotnetlib/System/Drawing/Font';

---PROGRAM:descriptor---  
(# hello: ^HelloWorld;
do &HelloWorld[]->hello[];
   hello.init; 
   'Hello Oslo' -> (hello[]->asControl).set_Text; (* window title *)
   hello[] -> Application.Run_Form; 
#)
  
---LIB:attributes---

HelloWorld: Form
  (# init: proc
       (# 
       do Color.get_White -> set_BackColor;
       #);
     
     OnPaint: proc
       (# e: ^PaintEventArgs; 
          g: ^Graphics;  
          f: ^Font; 
          b: ^Brush;
          r: ^Rectangle;
       enter e[]
       do ('Times New Roman', 26.0) 
            -> Font._init_String_real32 
            -> f[];
          e.get_Graphics 
            -> g[];
          Brushes.get_CornflowerBlue 
            -> b[];
          (200, 200, 30, 60) 
            -> Rectangle._init_int32_int32_int32_int32 
            -> r[];
          (b[], r[]) 
            -> g.FillRectangle_Brush_Rectangle;
          ('Hello Oslo!', f[], b[], 15.0, 15.0) 
            ->  g.DrawString_String_Font_Brush_real32_real32;
       #);
  #);
