ORIGIN '~beta/basiclib/betaenv';
--PROGRAM: descriptor--
(# 
   Calculator:
     (# R: @integer;
        set: 
          (# V: @integer enter V do V -> R #);
        add: 
          (# V: @integer enter V do R+V -> R; INNER; exit R #);
        display:
          (# do 'Value: '->putText; R -> putInt; newline; #);
     #);
   
   WeirdCalculator: Calculator
     (# 
        xadd: add (# do R * V -> R #)
     #);

   C: @WeirdCalculator; 
   X: @integer;
   
   A: ^C.add;
   
do 'Using xadd as a method' -> putLine;
   12 -> C.set;
   C.display;
   5 -> C.xadd -> X;
   C.display;
   
   'Using xadd as a class' -> putLine;
   &C.xadd[] -> A[];
   17 -> C.set;
   C.display;
   13 -> A -> X;
   C.display;
   10 -> A.V;
   A -> X;
   C.display;

#)
