ORIGIN 'googlesearch';
BUILD clr 'googleapi/dotnet/GoogleSearchService.dll' 'googleapi/dotnet/GoogleSearchService.cs' 'csc -nologo -target:library -debug -out:$0 $1';
--LIB: attributes--

String: ExternalClass
  (# 
  do '[mscorlib]System.String' -> className;
  #);
ResultElement: ExternalClass
  (# URL: ^String;
     snippet: ^String;
  do '[GoogleSearchService]ResultElement' -> className;
  #);
GoogleSearchResult: ExternalClass
  (# resultElements: [0]^ResultElement;
  do '[GoogleSearchService]GoogleSearchResult' -> className;
  #);
GoogleSearchService: ExternalClass
  (# 
     doGoogleSearch: proc
       (# key: ^String;
          q: ^String;
          start: @int32;
          maxResults: @int32;
          filter: @boolean;
          restrict: ^String;
          safeSearch: @Boolean;
          lr, ie, oe: ^String;
          result: ^GoogleSearchResult;
       enter (
          key[], 
          q[], 
          start, 
          maxResults, 
          filter, 
          restrict[], 
          safeSearch, 
          lr[], 
          ie[], 
          oe[])
       exit result[]
       #) 
  do (* No need to specify classname when the assembly-name is the same
      * as the class-name.
      * But the (empty) do-keyword must currently be present to indicate 
      * that this is not to be considered a BETA subclassing of the 
      * external class.
      * This is a temporary solution to be replaced by C#-alike properties.
      *)
  #);

--SearchGooglePrivate:descriptor--
(# google: ^GoogleSearchService;
   result: ^GoogleSearchResult;
#)

--DoSearchGoogle: dopart--
do (if private.google[]=NONE then
       &GoogleSearchService[] -> private.google[];
   if);
   (BetaLicenseKey,
   query[], 
   0, 
   10, 
   true, 
   '', 
   false, 
   '', 
   'latin1', 
   'latin1')
     -> private.google.doGoogleSearch
     -> private.result[];
   (if false then
       'Number of results: ' -> puttext;
       private.result.resultElements.range -> putint;
       newline;
   if);
   
   (for i:private.result.resultElements.range repeat
        private.result.resultElements[i].URL[] -> url[];
        private.result.resultElements[i].snippet[] -> snippet[];
        (if false then url[] -> putline; if);
        INNER;
   for);
