ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/figureitems';
INCLUDE '~beta/guienv/fields'
---windowlib:attributes---
textBox: statictext
  (# open:: 
       (# t: @styledText;
          aTextStyle: ^textStyle;
          x,y,w,h: @integer;
          t1: ^text
       enter(x,y,w,h,t1[])
       do (x, y)->position;
          (*(w, h)->size;*)
          &textStyle[] -> aTextStyle[];
          12 -> aTextStyle.size;
          'Times' -> aTextStyle.name;
          (* textFaces.bold -> aTextStyle.face;*)
          aTextStyle[] -> style;
          t1 -> t;
          t[]->label;
          fitToContents;
       #)
  #);
BoxWithText:
  (# box: @rect
       (# open::
            (# x,y,w,h: @integer
            enter(x,y,w,h)
            do (x,y)->position;
               (w, h)->size;
               3->pen.size
            #)
       #);
     txt: @textBox;
     vLine: @ line
       (# open::
            (# x,y: @integer
            enter(x,y)
            do (x,y) -> start; (x,y+200) -> end
            #)
       #);
     open:<
       (# x,y,w,h: @integer; t: ^text
       enter(x,y,w,h,t[])
       do (none,x+8,y+5,w-14,h-10,t[]) -> txt.open;
          txt.size -> (w,h);
          (none,x,y,w+14,h+12) -> box.open;
          (none,x + (w div 2) + 4->xCenter, y+h+8) -> vLine.open
       #);
     xCenter,startY,lastY: @integer;
     mark:
       (# nextY: @integer;
          L: ^line
       enter nextY
       do (if startY = 0 then
              nextY -> startY
          if);
          nextY -> lastY
       #);
     markEnd:
       (# nextY: @integer;
          L: ^line
       enter nextY
       do &line
          (# open::
               (#
               do 3 -> pen.size;
                  (xCenter,startY) -> start;
                  (xcenter,nextY) -> end;
               #)
          #)[] -> L[];
          L.open
       #);
  do inner
  exit this(BoxWithText)[]
  #);
hLineWithArrow: 
  (# hLine: line
       (# open::
            (# x1,y1,x2,y2: @integer
            enter(x1,y1,x2,y2)
            do (*3 -> pen.size;*)
               (x1,y1) -> start; (x2,y2) -> end;
               (*'From: ' -> puttext; x1 -> putint; 
                ' ' -> put; y1 -> putint; newline;
                ' To: ' -> puttext; x2 -> putint; 
                ' ' -> put; y2 -> putint; newline;*)
            #);
       #);
     main,a1,a2: ^hLine ;
     open:<
       (# yInc: (# exit 15 #);
          x1,y1,x2,y2,yi: @integer
       enter(x1,y1,x2,y2)
       do &hLine[] -> main[];  
          (if x1 = x2 then
              (# main2,main3: ^hline
              do (none,x1, y1, x1+yInc, y1) -> main.open;
                 &hLine[] -> main2[];
                 (none,x1+yInc, y1, x1+yInc, y1+yInc) -> main2.open;
                 &hLine[] -> main3[];
                 (none,x1+yInc, y1+yInc, x1, y1+yInc) -> main3.open;
                 x1->x2;
                 x1+yInc->x1; y1+yInc->y1 -> y2;
                 yInc -> yi
              #);
           else
              (none,x1, y1, x2, y2) -> main.open;
          if);
          &hLine[] -> a1[];
          &hLine[] -> a2[];
          (if x1 < x2 then
              (none,x2-5,y2-3,x2,y2) -> a1.open;
              (none,x2-5,y2+3,x2,y2) -> a2.open;
           else
              (none,x2,y2,x2+5,y2-3) -> a1.open;
              (none,x2,y2,x2+5,y2+3) -> a2.open;
          if)
       exit yi
       #)
  #);
NewBox:
  (# txt: ^text;
     next: @
       (# x,y,w,h: @integer;
          init:<
            (#
            do 10 -> x; 10 -> y; 100 -> w; 34 -> h;
            #)
       do x + 110 -> x;                   
       #);
     init:<
       (#
       do next.init
       #);
     B: ^BoxWithText
  enter txt[]
  do BoxWithText -> B[];
     (next.x,next.y,next.w,next.h,txt[]) -> B.open;
     next
  exit B[]
  #);
NewConn:
  (# lastY,nextY,yInc: @integer;
     endCall:
       (# B: ^BoxWithText
       enter B[]
       do lastY -> B.markEnd
       #);
     call: ^textBox;
     init: (# do 60 -> nextY #);
     from,to: ^BoxWithText;;
     LL: ^hLineWithArrow;
     txt: ^text
  enter(from[],to[],txt[])
  do &hLineWithArrow[] -> LL[];
     (from.xCenter, nextY,   to.xCenter,nextY) 
       -> LL.open
       -> yInc;
     
     &textBox[] -> call[];
     (if (from.xCenter < to.xCenter) or (yInc > 0) then
         (none,from.xCenter+4,nextY-18,30,14,txt[]) -> call.open
      else
         (none,from.xCenter-38,nextY-18,30,14,txt[]) -> call.open
     if);
     nextY + yInc -> nextY;
     
     nextY -> from.mark;
     nextY -> to.mark;
     nextY -> lastY;
     nextY + 24 -> nextY
  #);  

