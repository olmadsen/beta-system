ORIGIN 'genlib';
INCLUDE 'auxlib';
INCLUDE '~beta/basiclib/formatio';
BODY 'signaturebody';
---theGenLib:attributes---

defaultSuperName: (# exit common.BetaObjectClass #);
platformSuperName:
  (# superName: ^text
  do (if true
      // common.targetMachineId = common.jvm then
         'java/lang/Object'->supername[];
      // common.targetMachineId = common.clr then
         '[mscorlib]System.Object'->supername[];
         (*'/*3*/'->supername.append;*)
      else
         'Unknown_platform.Object' -> supername[];
     if);
  exit superName[]
  #);
platFormSuperSignature:
  (# sig: ^text
  do (if isJava then
         platformSuperName -> text2classRef -> sig[];
      else
         platformSuperName -> sig[]
     if)
  exit sig[]
  #);

descLocation:
  (* Return the fragmentgroup (file location) of desc
   * 1.  location = ''    desc is in this group
   * 2.  location = 'foo' desc is declared in 'foo'
   * 3.  location = none  desc is slot and location
   *                          of the form for the slot
   *                          is not known. The slot is
   *                          a descriptor- doPart or 
   *                          mainpart-slot
   * Part of the code here is copied form mkEntryPoint;
   * this should be unified.
   *)
  (# desc: ^ASTindex; location: ^text;
     N: @ASTindex;
     formId: ^text; descNo: @integer;
     findSlashDash:
       (# N: ^text; pos,slashPos,dashPos: @integer
       enter N[] (* N= '/users/smith/.../foo-formName' *)
       do N.scanAll(#
                   do pos+1->pos;
                      (if ch = '-' then pos -> dashPos if);
                      (if ch = '/' then pos -> slashPos if)
                   #);
          (* 0 <= slashPos < dashPos < N.length *)
          inner;
       exit N[]
       #);
     stripForm: findSlashDash
       (# 
       do (if dashPos > 0 then (dashPos,N.length) -> N.delete -> N[] if)
       #);
     stripFile: findSlashDash
       (# 
       do (slashPos+1,dashPos) -> N.delete -> N[]
       #);
     stripFileAndForm: findSlashDash
       (# 
       do (slashPos+1,N.length) -> N.delete -> N[]
       #);
     slotLocation:
       (# d: ^ASTindex; L: ^text
       enter D[]
       do (D[],true) -> EH.descName -> mch.import;
          (* note we deliberately return L[] = none *)
       exit L[]
       #);
     EH: @evalHandler;
     S: ^text
  enter desc[]
  <<SLOT descLocation:doPart>>
  exit location[]
  #);

AddClrLocation:
  (# class, location, T, trace: ^Text;
     prefix5,prefix6,prefix7,prefix8,prefix9, prefix12: ^text;
     simple: @boolean;
     add_resolution:
       (# T, resolution: ^text;
       enter (T[], resolution[])
       <<SLOT signature_AddClrLocation_add_resolution: dopart>>
       #);
  enter (class[], location[])
  <<SLOT signature_AddClrLocation: dopart>>
  exit T[]
  #);

Type: 
  (# isRef: < booleanValue;
     isList:< booleanValue;
     elmType:< integerValue;
     theDesc:<
       (# desc: ^ASTindex
       do INNER
       exit desc[]
       #);
     asText:<
       (# T: ^text
       do inner;
          187->ctrace(# 
                     do 'Signature:'  ->xT; T[] -> xT;
                        ' location:'->xT;
                        (if location[] = none then
                            'unknown' -> xT
                         else
                            location[] -> xT
                     if)#);
       exit T[]
       #);
     className:< 
       (* for a reference, the class name is returned;
        * for others the empty text
        *)
       (# value: ^text do &text[] -> value[]; inner exit value[] #);
     markAsField: (# do true -> isField #);
     location: ^text;
     isField,               (* type of a field - no '|' from
                             * interface should be passsed
                             *)
     isNone: @boolean      (* signature of NONE value *)     
  do inner
  exit this(Type)[]
  #);
int32Value: Type 
  (# elmType::(#do 1 -> value #);
     theDesc:: (# do sematt.integerDesc[] -> desc[] #);
     asText::
       (#
       do (if isJava then 'I'->T[] else 'int32'->T[] if)
       #)
  #);
unknownValue: Type 
  (# elmType::(#do -11 -> value #);
     theDesc:: (# do NONE -> desc[] #);
     asText::
       (#
       do 'Unknown Value'->T[];
       #)
  #);

(* FIXME: we need to considered signed/unsiged int8 and int16 
 * For clr the names are just prepended 'unsigned '
 *)
int16Value: Type 
  (# elmType::(#do 4 -> value #);
     theDesc:: (# do sematt.integerDesc[] -> desc[] #);
     asText::
       (#
       do (if isJava then 'S'->T[] else 'int16'->T[] if)
       #)
  #);

int8Value: Type 
  (# elmType::(#do 5 -> value #);
     theDesc:: (# do sematt.integerDesc[] -> desc[] #);
     asText::
       (#
       do (if isJava then 'B'->T[] else 'int8'->T[] if)
       #)
  #);
int64value: Type
  (# elmType::(#do 8 -> value #);
     theDesc:: (# do sematt.int64Desc[] -> desc[] #);
     asText::
       (#
       do (if isJava then 'J'->T[] else 'int64'->T[] if)
       #)
  #);
  
charValue: Type 
  (# elmType::(#do 2 -> value #);
     theDesc:: (# do sematt.charDesc[] -> desc[] #);
     asText::
       (#
       do (if isJava then 'C'->T[] else 'char'->T[] if)
       #)
  #);
boolValue: Type 
  (# elmType::(#do 3 -> value #);
     theDesc:: (# do sematt.boolDesc[] -> desc[] #);
     asText::
       (#
       do (if isJava then 'Z'->T[] else 'bool'->T[] if)
       #)
  #);

realValue: Type 
  (# elmType::(#do 6 -> value #);
     theDesc:: (# do sematt.realDesc[] -> desc[] #);
     asText::
       (#
       do (if isJava then 'D'->T[] else 'float64'->T[] if)
       #)
  #);
real32Value: Type 
  (# elmType::(#do 7 -> value #);
     theDesc:: (# do sematt.real32Desc[] -> desc[] #);
     asText::
       (#
       do (if isJava then 'F'->T[] else 'float32'->T[] if)
       #)
  #);

Void: Type
  (# elmType::(# do 10 -> value #);
     theDesc:: (# do sematt.integerDesc[] -> desc[] #);
     asText:: 
       (#
       do (if isJava then 'V'->T[] else 'void'->T[] if)
       #)
  #);

Reference: Type 
  (# class: ^text;
     isRef:: TrueValue;
     elmType:: 
       (# 
       do (if isExternalText then 
              13 -> value 
           else 
              (if this(reference).desc->sematt.textDesc.equal then
                  14 -> value 
               else
                  11 -> value 
          if)if) 
       #);
     isExternalClass, isOutsideExternalClass, isExternalText: @boolean;
     theDesc:: (# do this(reference).desc[] -> desc[] #);
     className:: (# do class[] -> value[] #);
     asText::
       (# 
          <<SLOT signature_reference_asText:doPart>>
       #);
     EH: @evalHandler;
     desc: ^ASTindex; 
  enter desc[] 
  <<SLOT signature_reference:doPart>>
  #);
Array: Type
  (# element: ^Type;
     isRef:: trueValue;
     elmType:: (# do 12 -> value #);
     theDesc:: (# do element.theDesc -> desc[] #);
     asText::
       (#
       do (if isJava then 
              '[' -> T[];
              element.asText -> T.append; 
           else (* FIXME *)
              element.asText -> T[]; '[]' -> T.append
          if);
       #)
  enter element[]
  #);
mkListSignature:
  (# T:  ^type; L:@ListSignature
  enter T[]
  do (T[],none) -> L.append
  exit L[]
  #);
ListSignature: Type
  (# L,dL: [3] ^Type; top: @integer;
     isList::trueValue;
     clear: (# do 0 -> top #);
     append:
       (# T,dclT: ^Type; TL: ^ListSignature
       enter(T[],dclT[])
       do (if T.isList then
              T[] -> TL[];
              TL.scan(# do (current[],dclCurrent[]) -> append #)
           else
              (if (top+1->top) > L.range then
                  L.range -> L.extend;
                  dL.range -> dL.extend
              if);              
              T[]-> L[top][];
              dclT[]-> dL[top][];
          if)
       #);
     scan:
       (# current,dclCurrent: ^Type
       do (for i: top repeat
               L[i][] -> current[];
               dL[i][] -> dclCurrent[];
               INNER
          for)
       #);
     asText::
       (# 
       do &text[] -> T[];
          (if isJava then
              scan(#do current.asText -> T.append #);
           else
              scan
              (# addSep: @boolean;
              do (if addSep then ','->T.put if);
                 current.asText -> T.append;
                 true -> addSep;
              #);
          if)
       #)
  #);
BetaTextSignatureId: (# exit 1 #);
JavaStringSignatureId: (# exit 2 #);
StructureSignatureId: (# exit 3 #);
ComponentSignatureId: (# exit 4 #);
RepetitionSignatureId: (# exit 5 #);
RepetitionElementSignatureId: (# exit 6 #);
specialSignature: Type
  (# sigId: @integer;
     isRef:: TrueValue;
     elmType::
       (#
       do 11 -> value
       #);
     theDesc::
       (#
       <<SLOT SpecialSignatureTheDesc: dopart>>
       #);
     astext::
       (#
       <<SLOT SpecialSignatureAsText: dopart>>
       #)
  enter sigId
  #);

valueSignature:
  (# desc: ^ASTindex; sig: ^Type
  enter desc[]
  do (if desc.label = gram.mainPart then
         (failureTrace,'ValueSignature:mainPart') -> stop
     if);
     (if true 
      // desc -> sematt.integerDesc.equal 
      // desc -> sematt.int32Desc.equal then
         int32Value -> sig[]
      // desc -> sematt.charDesc.equal then
         charValue -> sig[]
      // desc -> sematt.wcharDesc.equal then (* OBS! *)
         charValue -> sig[]
      // desc -> sematt.boolDesc.equal then
         boolValue -> sig[]
      // desc -> sematt.int16Desc.equal then
         int16Value -> sig[]
      // desc -> sematt.int16uDesc.equal  (* problem here with 'u' *)
      // desc -> sematt.shortDesc.equal then
         (if not common.switch[188] then
             '\nint16u not implemented: converted to int16' -> putline;
         if);
         int16Value -> sig[]
      // desc -> sematt.int8Desc.equal then
         int8Value -> sig[]         
      // desc -> sematt.int8uDesc.equal then
         (if not common.switch[188] then
             '\nint8u not implemented: converted to int32' -> putline;
         if);
         int32Value -> sig[]  
      // desc -> sematt.realDesc.equal then
         realValue -> sig[]         
      // desc -> sematt.real32Desc.equal then
         real32Value -> sig[]    
      // desc -> sematt.int32uDesc.equal then
         (if not common.switch[188] then
             '\nint32u not implemented: converted to int32' -> putline;
         if);
         int32Value -> sig[]   
      // desc -> sematt.int64uDesc.equal then
         (if not common.switch[188] then
             '\nint64u not implemented: converted to int64' -> putline;
         if);
         int64Value -> sig[]         
      // desc -> sematt.int64Desc.equal then
         int64Value -> sig[]         
      else
         'valueSignature: falling back on unknownValue for desc='->screen.puttext;
         desc.index->screen.putint; screen.newline;
         unknownValue -> sig[]
     if)
  exit sig[]
  #);
mkSig:
  (# desc: ^ASTindex; 
     sig: ^Type
  enter desc[]
  do (* should be made nicer *)
     (if (desc[] <> none) and not desc.isNull then
         (if (desc -> sematt.descKind) = sematt.standardKind then
             desc[] -> valueSignature -> sig[]
          else  
             desc[] -> reference -> sig[]
         if)
      else
         desc[] -> reference -> sig[];
     if)
  exit sig[]
  #);

mkSignature:
  (# desc: ^ASTindex; sig: ^Type; tsig: ^text; 
     EH: @evalHandler
  enter desc[]
  do (if desc.label = gram.doPart then
         'mkSignature: Unexpected doPart:'->thisTranslate.systemException;
     if);
     (if desc.label = gram.mainPart then
         (# D: @ASTindex
         do desc.father -> D;
            (if D.isNull then
                desc.xOrigin -> D;
                D.slotOrigin -> D;
            if);
            D[] -> desc[]
         #)
      if);

     (if (desc -> sematt.descKind) = sematt.standardKind then
         (desc[] -> valueSignature -> sig[]).astext -> tsig[]
      else  
         desc[] -> reference -> sig[];
         sig.asText -> tsig[];
     if);
     (if false and isJava then
         (* hack *)
         (if tsig.length > 2 then
             (2,tsig.length-1) -> tsig.sub -> tsig[]
         if)
     if);
  exit tsig[]
  #);
mkArraySig:
  (* we should simplify mkSignature and this one *)
  (# desc: ^ASTindex; sig: ^type;
  enter desc[]
  do (if (desc -> sematt.descKind) = sematt.standardKind then
         desc[] -> valueSignature ->array ->  sig[]
      else
         desc[] -> Reference -> array -> sig[]
     if)
  exit sig[]
  #);
mkArraySignature:
  (* we should simplify mkSignature and this one *)
  (# desc: ^ASTindex; tsig: ^text;
  enter desc[]
  do (if (desc -> sematt.descKind) = sematt.standardKind then
         (desc[] -> valueSignature ->array).astext -> tsig[]
      else
         (desc[] -> reference -> array).asText -> tSig[];
     if);
  exit tsig[]
  #);

ISignature:
  (# asText:<
       (# T: ^text
       do inner;
          (if isJava and common.switch[189] then
              (* to be able to debug locations using java bytecode *)
              '[\'' -> T.append;
              (if location[] = none then 
                  (* to be able to debug locations using java bytecode *)
                  '-unknown-' -> T.append
               else
                  location[] -> T.append;
              if);
              '\']' -> T.append
          if)
       exit T[]
       #);
     location: ^text;
     isMultiValExit: @boolean; (* more than on eelement in exit-list *)
  do inner;
  exit this(ISignature)[]
  #);
Signature: ISignature
  (* Obs! ISignature has recently been introduced as a superclass
   * of Signature to handle requirements for distinguishing
   * between signatures for method-declarations, method-calls,
   * instance creation, superclasses, etc. Currently Signature
   * covers method-calls and method-declarations as described below:
   * A signature may have the following two abstract forms:
   * 1. Method def :           method, input arguments, return type;
   * 2. Method call: receiver, method, input arguments, return type
   * As can be seen the difference is the receiver present in
   * the method call
   *)
  (# receiver: ^text;         (* an optional receiver *)
     receiverDesc: ^ASTindex; (* descriptor for receiver *)
     isRealStatic,            (* a real external static method*)
     isStatic: @boolean;      (* a Beta pattern at the outermost
                               * level in a lib-file
                               *)
     isPtnClassMethod: @boolean; (* the method is a PtnClassMethod:
                                  * a method which instatiates and
                                  * return an instance of the pattern
                                  *)
     method: ^text;           (* method name *)
     NNsig: ^ListSignature;   (* input arguments *)
     XXsig                    (* return type at point of declaration *)
     ,aplSig: ^Type;          (* return type at point of application *) 
     XXLsig: ^ListSignature;  (* if isMultiValExit then the list of 
                               * exit types similar to NNsig
                               *)
     
     
     thisMethodDesc:@ASTindex; (* descriptor of this method (pattern) *)
     exitDesc: ^ASTindex;      (* descriptor for returnType in case of
                                * multiValExit - see addExit
                                *)
     location: ^text;          (* fragment group (file) where thisMethodDesc
                                * is declared - see descLocation above
                                *)
     thisChain: ^DH.superChain;(* desc chain of def- or call-location *)
     
     setMethodDesc:
       (# EV: ^ASTindex; MN,vKind: @integer;
          chain,c: ^DH.superChain;
          addToChain: @boolean
       enter(EV[],chain[],addToChain)
       <<SLOT byteCode_setMethodDesc:doPart>>
       #);
     markStatic: (# do true -> isStatic #);
     markRealStatic: (# do true -> isRealStatic #);
     markAsPtnClassMethod: (# do true -> isPtnClassMethod #);
     isExternalClass, isOutsideExternalClass: @boolean;
     addReceiver: 
       (* only receiverDesc should be enter arg;
        * at all call sites, receiver is computed;
        * this can be done here
        *)
       (# 
       enter(receiver[],receiverDesc[])
       do (if (receiverDesc -> sematt.descKind) = sematt.externalClassKind then
              true -> isExternalClass;
              (if not (receiverDesc[] -> isBETAsubOfExternalClass) 
                    -> isOutsideExternalClass then
                  receiverDesc[] -> getExternalClassName -> receiver[];
              if);
              (if false then
                  '(2)ExternalClass '->puttext; 
                  (if isOutsideExternalClass then
                      'outside BETA: ' -> puttext;
                   else
                      'specialization in BETA:' -> puttext;
                  if);
                  receiver[] -> putline;
              if);
          if);
          (if isOutsideExternalClass then
              NONE -> location[]; (* force default location to be same as class name *)
           else
              (if (receiverDesc[] -> descLocation -> location[]) <> none then
                  location[] -> mch.import
              if)
          if);
       #);     
     addMethod: (# enter method[] #);
     
     noOfEnterArgs: 
       (# N: @integer 
       do (if NNsig[] <> none then NNsig.top -> N if) 
       exit N 
       #);
     enterSig:
       (# noNsig: @boolean;
          T: @text
       enter noNsig
       do '('->T;
          (if not NoNsig then 
              NNsig.asText -> T.append;
          if);
          ')'->T.put;
       exit T[]
       #);
     exitSig:
       (# noXsig: @boolean; T: ^text
       enter noXsig
       do (if noXsig then voidExitSig -> T[] else XXsig.asText -> T[] if)
       exit T[]
       #);
     voidExitSig: 
       (# T: @text 
       do (if isJava then 'V' -> T else 'void'->T if);
       exit T[]
       #);
     hasNoEnterAndExitThis: booleanValue
       (* A pattern P with no enter-part and with exit this(P)[]
        * will get the same signature as the new-P method generated
        * for instantiating a P. To avoid this a '$' is appended
        * to the method-name (P).
        * This is also the case for patterns with no enter-part
        * and with a list of exit values, in which case this(p)[]
        * is returned.
        *)
       (#
       <<SLOT signature_hasNoEnterAndExitThis:dopart>>
       #);
     asText::
       (* producec a textual representation of this signature *)
       (# noNsig,noXsig: @boolean;
       enter(noNsig,noXsig)          
       <<SLOT signature_astext:doPart>>
       #);
     noEnter: (# do ListSignature -> NNsig[] #);
     setEnter: (# enter NNsig[] #);
     addEnter:
       (# EH: @EvalHandler;
          nScan: @ | EH.scanNAdr;   
          nD: ^sematt.AttDesc; 
          theDesc: ^ASTindex;
          more: @boolean;
          sig: ^reference
       enter theDesc[]
       <<SLOT signature_addEnter:doPart>>
       #);
     scanEnter:
       (# current: ^type; isRef: @boolean
       do NNsig.scan
          (#
          do current[] -> this(scanEnter).current[];
             current.isRef -> isRef; 
             INNER scanEnter
          #)
       #);
     scanExit:
       (# current: ^type;
       do (if isMultiValExit then
              XXLsig.scan
              (#
              do current[] -> this(scanExit).current[];
                 INNER scanExit
              #)
           else
              XXsig[] -> current[];
              INNER
          if)
       #);
          
     exitVoid: 
       (# 
       do setVoidXsig
       #);
     setVoidXsig: 
       (#
       do Void -> xxSig[]; 
       #);
     exitRef:
       (# desc: ^ASTindex;
          EH: @evalHandler
       enter desc[]
       <<SLOT signature_exitRef:doPart>>
       #);
     setExit: (# enter XXsig[] #);
     addExit:
       (# EH: @EvalHandler;
          xScan: @ | EH.scanXadr;
          exitP: ^ASTindex;    
          more: @boolean
       enter exitP[]
       <<SLOT signature_addExit:doPart>>
       #);

     exitVal: ^ASTindex;
     exitId: @integer (* dirty *)   
  #);
fieldSignature: Isignature
  (# desc: ^AStindex;
     fieldSig: ^text;
     isExternalClass,isOutsideExternalClass,isValueSig: @boolean;
     asText::
       (# S: ^text
       do (*'FieldSig: ' -> puttext; fieldsig[] -> putline;*)
          (if fieldSig.T[1]='|' then
              (* interface type *)
              (1,1)-> fieldsig.delete;
              (*'\nFieldSig: removed interface indicator. result:' -> puttext; fieldsig[] -> putline;*)
          if);
          (if isJava then
              (if isValueSig then
                  fieldSig[] -> T[];
               else
                  fieldSig.copy -> text2classRef -> T[];
              if)
           else
              (fieldSig[],location[]) -> AddClrLocation -> T[];
          if)
       #);
     sig: ^type;
     EH: @evalHandler
  enter desc[]
  do (if desc.label = gram.doPart then
         (*'FieldSignature: Unexpected doPart:'->putline;*)
         desc.xorigin -> desc; desc.slotorigin -> desc
     if);
     (if (desc -> sematt.descKind) = sematt.standardKind then
         (desc[] -> valueSignature -> sig[]).astext -> fieldSig[];
         true -> isValueSig
      else         
         (if (desc -> sematt.descKind) = sematt.externalClassKind then
             true -> isExternalClass;
             (if not (desc[] -> isBETAsubOfExternalClass) 
                   -> isOutsideExternalClass then
                 desc[] -> getExternalClassName -> fieldSig[]
              else
                 (desc[],true) -> EH.descName -> fieldSig[];
             if);
             (if false then
                 '(3)ExternalClass '->puttext; 
                 (if isOutsideExternalClass then
                     'outside BETA: ' -> puttext;
                  else
                     'specialization in BETA:' -> puttext;
                 if);
                 fieldSig[] -> putline;
             if);
          else         
             (desc[],true) -> EH.descName -> fieldSig[];
         if);
         (if isOutsideExternalClass then
             NONE -> location[]; (* force default location to be same as class name *)
          else
             desc[] -> descLocation -> location[];
             (if false then
                 (if isExternalClass then
                     'Using location ' -> puttext; location[] -> putline;
                 if)
             if)
         if);
     if);
     313->trace(#do 'fieldSignature: '->xT; fieldSig[] -> xT; xN;
                  desc[] -> xA; xN; 'location: '->xT;
                  (if location[] <> none then location[] -> xT
               if)#);
     (* (if not (desc[] = none) and not desc.isNull then
         desc[] -> descLocation -> location[];
     if);*)
     (if location[] <> none then
         location[] -> mch.import
     if)
  #);
     
classSignature: ISignature
  (* used for instance generation in newInstance;
   * OBS! className should be eliminated as enter param
   * since it can be computed from desc; however, do
   * check all call-sites carefully before doing this
   *)
  (# desc: ^ASTindex;  (* descriptor for className *)
     className         (* perhaps unify with receiver in Signature?*)
     ,location : ^text;(* form location of class *) 
     setClass: (# enter className[] #);
     asText::
       (#
       do (if isJava then 
              className[] -> T[];
           else
              (className[],location[]) -> AddClrLocation -> T[];
          if)
       #);
     isExternalClass, isOutsideExternalClass: @boolean;
  enter(desc[],className[])
  do (if not (desc[] = none) and not desc.isNull then
         (if ((desc -> sematt.descKind) = sematt.externalClassKind) then
             true -> isExternalClass;
             (if not (desc[] -> isBETAsubOfExternalClass) 
                   -> isOutsideExternalClass then
                 desc[] -> getExternalClassName -> className[];
             if);                 
             
             (if false then
                 '(4)ExternalClass '->puttext; 
                 (if isOutsideExternalClass then
                     'outside BETA: ' -> puttext;
                  else
                     'specialization in BETA:' -> puttext;
                 if);
                 className[] -> putline;
             if);

         if);
         
         (if isOutsideExternalClass then
             NONE -> location[]; (* force default location to be same as class name *)
          else
             (if (desc[] -> descLocation -> location[]) <> none then
                 location[] -> mch.import
             if)
         if);
     if);
     313->trace(#
               do 'classSignature: ' -> xT; className[] -> xT;
                  ' in: ' -> xT; 
                  (if location[] = none then
                      'UnKnown'->xT
                   else
                      location[] -> xT
                  if)
               #);          
  #);
constructorSignature: ISignature
  (* signature of class constructor *)
  (# originDesc: ^ASTindex;
     originName,locations: ^text;
     sig: @ListSignature;
     addConstructorDesc:
       (# consDesc: ^ASTindex; 
          chain: ^DH.superChain;
          cSig: @signature;
          isDefaultConstructor: @boolean
       enter(consDesc[],isDefaultConstructor,chain[])
       do 313->trace(# c: ^DH.superChain;
                    do 'addConstructorDesc:'->xT; xN;
                       consDesc[] -> xA; xN;
                       consDesc->DH.newSingle -> c[]; c
                    #);
          (consDesc[],consDesc->DH.newSingle,false) 
            -> cSig.setMethodDesc;
          consDesc[] -> cSig.addEnter;
          (if isDefaultConstructor then sig.clear if);
          (cSig.NNsig[],none) -> sig.append;
          ''->originName; (* to handle MyClass.Cons - i.e. constructors*)
       #);
     asText::
       (#
       do (if isJava then
              (if '-' -> originName.equal then
                  '()V' -> T[]
               else
                  '(' -> T[];
                  sig.astext -> T.append;
                  ')V' -> T.append                  
              if)              
           else
              (if '-' -> originName.equal then
                  '' -> T[]
               else
                  (sig.asText,location[]) -> AddClrLocation -> T[];
              if)                  
          if)
       #);
     EH: @evalHandler
  enter(originDesc[],originName[]) (* eliminate originName[] as argument;
                                    * however, '-' must be handled in
                                    * some way
                                    *)
  <<SLOT signature_constructorSignature:doPart>>
  #);
superSignature: ISignature
  (* use for super class name *)
  (# desc: ^ASTindex;
     className: ^text; (* perhaps unify with receiver in Signature?*)
     setClass: (# enter className[] #);
     asText::
       (#
       do (if true 
           // isJava // isPre then 
              className[] -> T[]
           else
              (className[],location[]) -> AddClrLocation -> T[];
          if)
       #);
     isExternalClass, isOutsideExternalClass: @boolean;
  enter(desc[],className[])
  do (if desc[] <> none then
         (if (not desc.isNull) then
             (if (desc -> sematt.descKind) = sematt.externalClassKind then
                 true -> isExternalClass;
                 not (desc[] -> isBETAsubOfExternalClass) 
                   -> isOutsideExternalClass;
                 (if false then
                     '(5)ExternalClass '->puttext; 
                     (if isOutsideExternalClass then
                         'outside BETA: ' -> puttext;
                      else
                         'specialization in BETA:' -> puttext;
                     if);
                     desc[] -> getExternalClassName -> putline;
                 if);
             if);
         if);
         (if isOutsideExternalClass then
             NONE -> location[]; (* force default location to be 
                                  * same as class name *)
          else
             (if (desc[] -> descLocation -> location[]) <> none then
                 location[] -> mch.import
             if)
         if) 
     if)
  #);
scanNargs:
  (# EH: @EvalHandler;
     nScan: @ | EH.scanNAdr;   
     nD: ^sematt.AttDesc; 
     theDesc: ^ASTindex;
     more: @boolean;
     xSig: ^ListSignature;
     chain: ^DH.superChain
  enter(theDesc[],chain[])
  do (theDesc[],gen.thisRegAdr,0,chain[],true) -> nScan -> more;
     ListSignature -> xSig[];
     Loop:
       (if more then
           313->trace(#
                     do 'scanNargs:elm:'->xT;
                        nScan.thisEv[]->xA
                     #);
           (nScan.thisEv[],theDesc[],true,nScan.thisChain[])
             -> getAttSig 
             -> xSig.append;
           313->trace(#do 'sig: '->xt; xSig.asText -> xT #);
           nScan -> more;
           restart Loop
       if)
  exit xSig[]
  #);
ScanXargs:
  (# EH: @EvalHandler;
     xScan: @ | EH.scanXAdr;   
     nD: ^sematt.AttDesc; 
     theDesc: ^ASTindex;
     more: @boolean;
     xSig: ^ListSignature;
     sig: ^Type;
     chain: ^DH.superChain
  enter(theDesc[],chain[])
  do (theDesc[],gen.thisRegAdr,0,chain[]) -> xScan -> more;
     ListSignature -> xSig[];
     Loop:
       (if more then
           313->trace(#
                     do 'scanXargs:elm:'->xT;
                        xScan.thisEv[]->xA
                     #);
           (xScan.thisEv[],theDesc[],false,xScan.thisChain[])
             -> getAttSig
             -> xSig.append;
           313->trace(#do 'sig: '->xt; xSig.asText -> xT #);
           xScan -> more;
           restart Loop
       if);
     (if xSig.top = 1 then 
         (* Avoid returning a list, since
          * a list signals a multiValExit
          *)
         xSig.L[1][] -> sig[]
      else
         xSig[] -> sig[]
     if)
  exit sig[]
  #);
     
ScanXarg:
  (# theDesc,exitP: ^ASTindex;
     EH: @evalHandler;
     xScan: @ | EH.scanXAdr;
     ex: ^ASTindex;
     more: @boolean
  enter theDesc[] (*exitP[]*)
  do (theDesc[],gen.thisRegAdr,0,theDesc -> DH.newSingle) -> xScan -> more;
     (if true then
         (if more then
             &ASTindex[] -> ex[];
             xScan.thisEv -> ex;
             xScan -> more;
             (if more then
                 '\nScanXarg: more than one exit element' -> putline;
             if)
         if)
      else
         (if exitP.label <> gram.empty then
             &ASTindex[] -> ex[];
             exitP.son -> ex; (* check that ONLY one exit elm *)          
         if)
     if)
  exit ex[]
  #);
getAttSig:
  (# EV: ^ASTindex;
     theDesc: ^ASTindex;
     isEnter: @boolean;
     chain: ^DH.superChain;
     aplSig,      (* signature know at point of application *)
     xSig: ^Type; (* signature known at point of declaration  *)
  enter(EV[],theDesc[],isEnter,chain[])
  <<SLOT signature_getAttSig:doPart>>
  exit(xsig[],aplSig[])
  #);

isBETAsubOfExternalClass: booleanValue
  (* test if desc is an interface to an external class
   * or a real BETA subclass of an externalclass.
   * if interface then no code should be generated;
   * if 'real' then code should be generated.
   * As a simple hack, a direct subclass of externalClass
   * is considered to be an interface.
   * Subclasses at level 2 and below are considerered 'real'.
   * A better means for specifying interface og real
   * should be invented
   *)
  (# desc: ^ASTindex;
  enter desc[]
  <<SLOT signature_isBETAsubOfExternalClass:dopart>>
  #);
     
newInstance:
  (# desc: ^ASTindex; 
     kind: @integer;
     consSig: ^ConstructorSignature;
     hasOrigin: @boolean
  enter(desc[],kind)
  <<SLOT signature_newinstance:doPart>>
  #);
getExternalClassName:
  (# EH: @evalHandler;
     desc: ^ASTindex; class: ^text; extKind: @integer
  enter desc[]
  <<SLOT signature_getExternalClassName:doPart>>
  exit class[]
  #);
isExternalString: booleanValue
  (# desc: ^ASTindex
  enter desc[]
  <<SLOT signature_isExternalString:dopart>>
  #);
text2ClassRef:
  (# classname: ^text
  enter classname[]
  do 'L' -> classname.prepend;
     ';' -> classname.append;
  exit classname[]
  #);

getSuperAT:
  (# desc: ^ASTindex; PL: @integer;
     superDesc: @ASTindex
  enter(desc[],PL)
  <<SLOT bytecode_getSuperAt:doPart>>
  exit superDesc[]
  #);
