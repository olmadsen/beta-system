ORIGIN '../SYNTHESIZER/synthesizerbody';
INCLUDE 'newgen';
INCLUDE 'display_visitor';
INCLUDE 'codegen_visitor';
---newsynthesizerbody:descriptor---
(#
do (# G: @newgen;
      betaCfl: ^cflLevel;
   do common[] -> MchFactory -> mch[];
      this(astinterface)[] -> BetaCfl[]; (* get the right cflLevel instance;
                                          * a qua check is generated here
                                          *)
      common[] -> G.common[];
      BCname[] -> G.BCname[] -> putline;
      mch[] -> G.mch[]; 

      ((theGroup.FullName,'~beta') -> thePathHandler.localPath
      ,common[]
      ,BCname[]
      ,(this(AstInterface)[],theGroup[])->getGroupIDProperty
      ) -> mch.init;
      G;
      
      (if (not common.betaenv.is_set) then
          (# g: ^fragmentgroup;
             f: ^fragmentform;
             e: @diskentry;
          do theGroup[] -> g[];
             L: (if g.origin<>NONE then g.origin->g[]; restart L if);
             g.textfilename -> e.path;
             g.scan(# do current[] -> f[] #); (* Only one fragment in betaenv *)
             (e.path.name.prefix, (f.name).copy) -> common.betaenv.set;
          #)
      if);

      
      theGroup.fragmentList.scan
      (# F: ^fragmentForm; N: ^ast
      do (if current.type = formType then
             current.open -> F[];
             F.root[] -> N[];
             (*betacfl.displayVisitor -> N.accept;*)
             G[] -> main.thegen.FormVisitor -> N.accept
         if)
      #);
      mch.close;
   #)
#)
