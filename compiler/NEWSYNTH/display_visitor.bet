ORIGIN '~beta/dependency/betaast';

---betaAttributes:attributes---

DisplayVisitor: treeLevelvisitor
  (# visit_descriptorForm::
       (#
       do 'descriptorForm:'->puttext;
          this(displayVisitor)[] -> (N.getObjectDescriptor).accept
       #);
     visit_objectDescriptor::
       (# A: ^ast;
          P: ^beta.prefix
       do 'descriptor:'->puttext;
          N.getPrefixOpt -> A[];
          (if A## <> optional## then  
              A[] -> P[];
              this(displayVisitor)[] -> P.accept
          if);
          this(displayVisitor)[] -> (N.getMainPart).accept
       #);
     visit_prefix::
       (#
       do 'prefix:'->puttext
       #);
     visit_patternDecl::
       (# names: ^beta.names;
          ND: ^beta.nameDcl;
          NDX: ^nameDecl
       do 'pattern:'->puttext;
          N.getNames -> names[];
          names.newscan
          (#
          do current[] -> ND[];
             ND.getNameDecl -> NDX[];
             NDX.gettext -> puttext; ':'->put 
          #)
       #);
     visit_mainPart::
       (# A: ^beta.attributes;
          M: ^Beta.actionPart
       do 'mainPart:'->puttext;
          N.getAttributes -> A[];
          A.newScan
          (#
          do '\n   '->puttext;
             this(displayVisitor)[] -> current.accept
          #);
          N.getActionPart -> M[];
       #);
  exit this(displayVisitor)[]        
  #);
