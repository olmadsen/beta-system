ORIGIN '~beta/basiclib/basicsystemenv';
LIB_DEF 'interactive' '../lib';
(*INCLUDE '~beta/betaast/betacfl';*)
INCLUDE 'CONTROL/system';
INCLUDE 'CONTROL/argumenthandler';
INCLUDE 'CONTROL/translateWithErrorHandling';
INCLUDE 'GENERATOR/runJobFile';
BODY 'SYNTHESIZER/synthesizer';
(*
 * COPYRIGHT
 *       Copyright Mjolner Informatics A/S, 1985-2000
 *       All rights reserved.
 *
 *  Compiler for the BETA programming language
 *
 *  Ole Lehrmann Madsen
 *  Computer Science Department, Aarhus University           
 *  Mjolner Informatics A/S
 * 
 *  See compilerrcmversion.bet for rcm version number;
 *  this file is generated by rcm at each check-in
 *)
---systemlib: attributes---
InteractiveCompiler: 
  (# Mjolner: ^text; (* text written by the compiler;
                      * may be set by the specific platform
                      * versions of the compiler
                      *)
     
     BETAastIF: @ASTInterface;   (* metaprogramming interfaces *)
     beta: ^BETAastIF.treeLevel;     

     comp: @BETAastIF.compiler;  (* the compiler *)

     getNextArgument: @ BetaArgumentHandler;
     
     ActivateCompiler:<
       (# rootFragment: ^text
       enter rootFragment[]
       do INNER;
          L: rootFragment[] -> comp.translateWithErrorHandling
          (# Report::
               (# 
               do msg[]->comp.messagestream.putline; 
                  leave L
               #);
             JobFileRunner::
               (#
               do (AFF[],common[],infoStream[],messageStream[],
                  hostMachineType[], targetmachine[], jobfilemachine[],
                  interactiveCompiler)
                    -> runJobFile
                  (# error:: 
                       (#do msg[] -> jobFileException #)
                  #);
               #);
          #);
       #);
     
     init:<
       (# InitBetaTarget: (# <<SLOT InitBetaTarget:doPart>> #);
       do InitBetaTarget;
          
          (if Mjolner[] = NONE then 'Mjolner'->mjolner[] if);
          
          BETAastIF;
          &BETAastIF.treeLevel[] -> beta[];
          (*&BETAastIF.beta[] -> beta[]; *)
          ('~beta/grammars/beta/beta','beta',comp.bugStream[])
            -> beta.betagrammarInit; 
          
          true -> comp.init;
          
          (keyBoard[],BETAastIF[],comp[]) -> getNextArgument.init;          
          
          (if comp.verboseLevel <= comp.verboseLevel.actions then
              Mjolner[] -> comp.infostream.puttext;
              ' BETA Compiler version: ' -> comp.infostream.puttext;
              comp.compilerversion -> comp.infostream.puttext;
              ' --- Runtime System version: '->comp.infostream.puttext;
              (# get_betarun_version: external
                   (# version: [0] @char exit version #);
                 BV: @text
              do get_betarun_version->BV; BV[]->comp.infostream.puttext;
              #);
              '\nTarget machine platform: ' -> comp.infostream.puttext;
              comp.targetMachineText[] -> comp.infostream.putline;
          if);
          INNER init
       #)
  do init;
     
     dealWithArgs:
       (# arg: ^text
       do (if (getNextArgument->arg[]) <> NONE then
                            
              arg[] -> ActivateCompiler;
              
              restart dealWithArgs;
       if)#);
     
     (if comp.trans.common.switch[68] then
         comp.trans.common.print;
         comp.trans.dStat.print;
     if)
  #)

