ORIGIN  'compiler';
INCLUDE '~beta/basiclib/systemenv';
INCLUDE '~beta/sysutils/envstring';
BODY 'SYNTHESIZER/synthesizer';
BODY 'CONTROL/translate_asmbody';
BODY 'GENERATOR/BYTECODE/bytecodebackend';
BODY 'GENERATOR/BYTECODE/bytecodetarget';
(*BODY 'GENERATOR/BYTECODE/X_BCjavaBody';*)

BODY 'GENERATOR/BYTECODE/bytecodebackend_jvmbody';

(* INCLUDE '~beta/profiler/profiler'; *)
-----program: descriptor---
systemEnv
(# IC: @JvmInteractiveCompiler;
   (* prof: @Profiler;*)
   
   JvmInteractiveCompiler: InteractiveCompiler
     (# ActivateCompiler::
          (* Force regeneration of rootfragment, 
           * assuming it has program slot *)
          (# objdir: @directory
               (# delfile: @deletefile
                    (# nosuch::(# do true->continue #);
                       error::(# do true->continue #);
                    #)
               #);
             e: @diskentry;
          do 'jvm' -> objdir.name;
             rootfragment[] -> e.path;
             (if objdir.entry.exists then
                 '.dep' -> (e.path.name.prefix).append -> objdir.delfile;
                 'program.dep' -> objdir.delfile;
             if);
             (if not IC.comp.trans.common.switch[188] then
                 (* Issue warning if using ComponentDotCJ *)
                 (if IC.comp.trans.common.ComponentDotCJ then
                     '\n*** Using Component.java: \n'
                     '    Method xdo() will be called '
                     '(you may need recompile)\n'
                     '    Remember to enable BUILD in '
                     '{tst,beta}env_jvmbody\n'
                       -> putline;
                 if);
             if);
          #);
     #);
   
do (* prof.prepare;
    * true -> prof.switch;
    *)
   
   true -> IC.comp.trans.common.switch[120];
   true -> IC.comp.trans.common.switch[121];
   true -> IC.comp.trans.common.switch[122];
   true -> IC.comp.trans.common.switch[129];
   true -> IC.comp.trans.common.switch[182];
   true -> IC.comp.trans.common.switch[14];  (* NO refNone check *)
   true -> IC.comp.trans.common.switch[327]; (* Use Component.java *)

   (* Supress noise in compiler distributed *)
   true -> IC.comp.trans.common.switch[188];

   'jvm' -> IC.comp.targetMachine;
   (if ('$(JAVAHOME)'->expandEnvVar).length = 0 then
       '\n***Warning: For JNI to work environment variable JAVAHOME '
       'should be\n'
       '            set to point to the root of your Java 2 '
       'installation.\n' -> putline;
   if);      
   (if ('$(JNIDIR)'->expandEnvVar).length = 0 then
       '\n***Warning: For JNI to work environment variable JNIDIR '
       'should be\n'
       '            set to the name of your platform specific JNI '
       'include directory\n'
       '            (e.g. "solaris", "linux", or "win32").\n' -> putline;
   if);      
   (if ('$(DYNEXTENSION)'->expandEnvVar).length = 0 then
       '\n***Warning: For JNI to work environment variable '
       'DYNEXTENSION should be\n'
       '            set to the file-extension of dynamic '
       'libraries on your platform\n'
       '            (e.g. "so" or "dll").\n' -> putline;
   if);      
   IC;
   
   (* false -> prof.switch;
    * prof.conclude;
    *)
#)
