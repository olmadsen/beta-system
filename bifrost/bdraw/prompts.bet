ORIGIN '~beta/guienv/v1.2/guienv';
BODY 'promptsbody';

--GuienvLib: attributes--

(* Standard text dialogs for guienv.
 * Known problems:
 *     1. Buttons should be highlighted when invoked via <return>, 
 *        <escap> etc.
 *     2. Patterns should be specializations of special dialog pattern,
 *        which would make the window manager treat the prompts as dialogs.
 *)

Prompt: window
  (* Dialog with a message text and an OK and Cancel button.
   * When typing <Return> in the text fiels, the OK button
   * is invoked, and when typing <Escape> the cancel button 
   * is invoked.
   *)
  (# <<SLOT Prompt: attributes>>;
     ok:< Object
       (* Called when the OK button is clicked *);
     cancel:< object 
       (* Called when the Cancel button is clicked *);
     popup:<
       (* Used to pop up the dialog and wait for the text to be typed *)
       (# titleText, msgText: ^text;
          father: ^window;
       enter (father[], titleText[], msgText[])
       <<SLOT PromptPopup: dopart>>
       #);

     open::< (# <<SLOT PromptOpen: dopart>> #);
     private: @<<SLOT PromptPrivate: descriptor>>;
  enter popup
  #);

PromptForText: Prompt
  (* Prompt with a field to enter a text added. *)
  (# <<SLOT PromptForTextLib: attributes>>;
     
     usertext: ^text; (* The text entered by user *)
     
     ok::<
       (# <<SLOT PromptForTextOk: dopart>> #);
     
     validate:< BooleanValue
       (* Called when the OK button is pressed. THIS(Prompt) is only
        * dismissed (and INNER in the ok virtual called) if validate returns 
        * true.
        *)
       (# hiliteOnError: @boolean
            (* Hilite text on error. Default true *);
       <<SLOT PromptForTextValidate: dopart>>
       #);
     popup::<
       (# defaultText: ^text;
       enter defaultText[]
       <<SLOT PromptForTextPopup: dopart>>
       #);
     
     open::< (# <<SLOT PromptForTextOpen: dopart>> #);
     textprivate: @<<SLOT PromptForTextPrivate: descriptor>>;
  #);

PromptForInteger: PromptForText
  (* Special PromptForText that will only accept integer input *)
  (# <<SLOT PromptForIntegerLib: attributes>>;
     userinteger: @integer (* The integer typed by the user *);
     validate::<
       (# 
       <<SLOT PromptForIntegerValidate: dopart>> 
       #)
  #);

promptForBoolean: Prompt
  (* Special Prompt where the OK buttons has label 'Yes' and
   * the Cancel button has label 'No'.
   *)
  (# open::<
       (# <<SLOT promptForBooleanBody: dopart>> #)
  #);
