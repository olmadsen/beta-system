ORIGIN '~beta/guienv/v1.2/guienv';
BODY 'promptsbody';

--GuienvLib: attributes--

(* Standard text dialogs for guienv.
 * Known problems:
 *     1. Messages should be left aligned - guienv does not allow that
 *     2. Buttons should be highlighted when invoked via <return>, 
 *        <escap> etc.
 *     3. Patterns should be specializations of special dialog pattern,
 *        which would make the window manager treat the prompts as dialogs.
 *)

PromptForText: window
  (* Dialog with a message text, a field to enter a text,
   * and an OK and Cancel button.
   * When typing <Return> in the text fiels, the OK button
   * is invoked, and when typing <Escape> the cancel button 
   * is invoked.
   *)
  (# <<SLOT PromptForTextLib: attributes>>;
     
     usertext: ^text; (* The text entered by user *)
     ok:< 
       (* Called when the OK button is clicked *)
       (# <<SLOT PromptForTextOk: dopart>> #);
     cancel:< object 
       (* Called when the Cancel button is clicked *);
     validate:< BooleanValue
       (* Called when the OK button is pressed. THIS(PromptForText) is only
        * dismissed (and INNER in the ok virtual called) if validate returns 
        * true.
        *)
       (# hiliteOnError: @boolean
            (* Hilite text on error. Default true *);
       <<SLOT PromptForTextValidate: dopart>>
       #);
     popup:
       (* Used to pop up the dialog and wait for the text to be typed *)
       (# titleText, msgText, defaultText: ^text;
          father: ^window;
       enter (father[], titleText[], msgText[], defaultText[])
       <<SLOT PromptForTextPopup: dopart>>
       #);

     open::< (# <<SLOT PromptForTextOpen: dopart>> #);
     private: @<<SLOT PromptForTextPrivate: descriptor>>;
  #);

PromptForInteger: PromptForText
  (* Special PromptForText that will only accept integer input *)
  (# <<SLOT PromptForIntegerLib: attributes>>;
     userinteger: @integer (* The integer typed by the user *);
     validate::<
       (# 
       <<SLOT PromptForIntegerValidate: dopart>> 
       #)
  #);
