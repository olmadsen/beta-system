ORIGIN 'bdrawenv';
INCLUDE '~beta/guienv/v1.2/stddialogs';
INCLUDE '~beta/guienv/v1.2/fields';

(* Dialogs for bdraw.
 * They are not complete yet:
 *   ReadText etc have problems with
 *     1. stupid xxx_popup title (guienv error)
 *     2. buttons seem to be pressed (guienv error I think)
 *     3. RET, CR, ESC characters typed in textfields should not
 *        be displayed. Have tryed using onBeforeChange without success.
 *        The current solution (delete) does not work properly.
 *   Read3Int is not complete yet.
 *)

-- Dialogs: descriptor -- 
(#
   ReadText: window
     (# usertext: ^text;
        ok:< 
          (# 
          do thetext.contents->usertext[];
             (if validatevalue then 
                 INNER; 
                 THIS(ReadText).hide 
              else
                 (* bell *)
                 thetext.all -> thetext.selection;
             if)
          #);
        cancel:< object;
        validate:< BooleanValue
          (* Called when the OK button is pressed. THIS(ReadText) is only
           * dismissed (and INNER in the ok virtual called) if validate returns true.
           *)
          (# do usertext.length>0 -> value; INNER #);
        validatevalue: @validate;
        
        theMessage: @staticText
          (# open::
               (# 
               do (20,10) -> position;
                  (200,30) -> size;
               #);
          #);
        DialogText: textField
          (# eventhandler::
               (# onKeyDown:: 
                    (# 
                    do (if ch
                        // ascii.nl
                        // ascii.cr then 
                           delete; (* delete the character *)
                           okButton.theEventhandler.onMouseup
                        // ascii.esc then 
                           delete; (* delete the character *)
                           cancelButton.theEventhandler.onMouseup
                       if)
                    #);
               #);
          #);
        theText: @DialogText
          (# open::
               (# 
               do (20,40) -> Position;
                  (200,30) -> Size;
               #);
          #);
        okButton: @pushButton
          (# eventHandler:: (# onMouseUp:: (# do ok #)#);
             open::
               (# 
               do (180,80) -> position;
                  (40,30) -> size;
                  'OK' -> label;
                  true -> border.visible;
               #);
          #);
        cancelButton: @pushButton
          (# eventHandler:: 
               (# onMouseUp:: (# do cancel; THIS(ReadText).hide #)#);
             open::
               (# 
               do (120,80) -> position;
                  (50,30) -> size;
                  'Cancel' -> label;
                  true -> border.visible;
               #);
          #);
        open::<
          (# 
          do (if dialogopened//false then
                 true->dialogopened;
                 hide;
                 theMessage.open;
                 theText.open;
                 okButton.open;
                 cancelButton.open;
                 okButton[] -> defaultButton;
                 (230,120) -> size;
                 INNER;
             if);
          #);
        dialogopened: @boolean;
        popup:<
          (# titleText, msgText, defaultText: ^text;
             mainpos, mainsize, mysize: @point;
          enter (titleText[], msgText[], defaultText[])
          do (if dialogopened//false then
                 open;
             if);
             titleText[] -> title;
             msgText[] -> theMessage.label;
             theText.clear;
             defaultText[] -> theText.insert;
             (* theText.all-> thetext.selection;*)
             maincanvas.size -> mainsize;
             size -> mysize;
             maincanvas.position -> mainpos;
             (mainpos.x + (mainsize.x-mysize.x) div 2,
             mainpos.y + (mainsize.y-mysize.y) div 2) -> position;
             showmodal;
          #);
     #);
   
   ReadInteger: ReadText
     (# userinteger: @integer;
        validate::<
          (# 
          do usertext.reset; 
             usertext.getint(# syntaxerror::(# do false->THIS(validate).value; true->continue #)#)
               -> userinteger; 
          #)
     #);
   Read3Int: ReadInteger
     (# userinteger2, userinteger3: @integer;
        message2: @staticText
          (# open::
               (# 
               do (20,30) -> position;
                  (200,30) -> size;
               #)
          #);
        text2: @DialogText
          (# open::
               (# 
               do (20,50) -> position;
                  (200,30) -> size;
                  '0'->insert;
               #)
          #);
        message3: @staticText
          (# open::
               (# 
               do (20,70) -> position;
                  (200,30) -> size;
               #)
          #);
        text3: @DialogText
          (# open::
               (# 
               do (20,90) -> position;
                  (200,30) -> size;
                  '0'->insert;
               #)
          #);
        open::
          (# 
          do text2.open;
             text3.open;
             (0,80) -> okButton.move;
             (0,80) -> cancelButton.move;
          #);
     #);
   
   
   readrgb: @Read3Int
     (# #);
   readhsv: @Read3Int
     (# #);
   readcmy: @Read3Int
     (# 
     #);
   
#)

-- InitDialogs: descriptor -- 
(# 
do 
#)

-- ReadIntBody:dopart --
do (title[], prompt[], '0') 
     -> (&dialogs.readinteger(# ok::(# do userinteger->useit #)#)[]).popup;
-- ReadRGB: dopart --
do   
-- ReadHSV: dopart --
do
-- ReadCMY: dopart --
do
   
-- BdrawNote: dopart --
do (topwindow[], message[], 'Bdraw Note') -> AlertUser
   
