ORIGIN 'prompts';
INCLUDE '~beta/guienv/v1.2/fields';

(******** Prompt ********)

--PromptPrivate: descriptor--
(# fatherpos, fathersize, mysize: @point;
   dialogopened: @boolean;
   popdown: @boolean;
   theMessage: @staticText
     (# open::
          (# 
          do (20,10) -> position;
             (250,30) -> size;
          #);
     #);
   okButton: @pushButton
     (# eventHandler:: 
          (# onMouseUp:: 
               (# 
               do false -> THIS(prompt).private.popdown;
                  ok;
                  (if THIS(prompt).private.popdown then THIS(Prompt).hide if)
               #)
          #);
        open::
          (# 
          do (230,80) -> position;
             (40,30) -> size;
             'OK' -> label;
          #);
     #);
   cancelButton: @pushButton
     (# eventHandler:: 
          (# onMouseUp:: (# do cancel; THIS(Prompt).hide #)#);
        open::
          (# 
          do (170,80) -> position;
             (50,30) -> size;
             'Cancel' -> label;
          #);
     #);
#)

--PromptPopup: dopart--
do (if not private.dialogopened then
       open;
   if);
   titleText[] -> title;
   msgText[] -> private.theMessage.label;
   INNER;
   (if father[] <> NONE then
       father.size -> private.fathersize;
       size -> private.mysize;
       father.position -> private.fatherpos;
       (private.fatherpos.v + (private.fathersize.v-private.mysize.v) div 2,
       private.fatherpos.h + (private.fathersize.h-private.mysize.h) div 2) 
         -> position;
   if);
   showmodal;
   
--PromptOpen: dopart--
do (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       private.theMessage.open;
       private.okButton.open;
       private.cancelButton.open;
       (*private.okButton[] -> defaultButton;*)
       (280,120) -> size;
       INNER;
   if);
   
(********* PromptForText **********)
   
--PromptForTextOpen: dopart--
do textprivate.theText.open;
   INNER;
   
--PromptForTextOk: dopart--
do textprivate.theText.contents->usertext[];
   (if textprivate.validatevalue then 
       INNER; 
       true->THIS(prompt).private.popdown
    else
       system.beep;
       (if textprivate.hilite then textprivate.theText.all 
             -> textprivate.theText.selection
       if);
   if)
   
--PromptForTextValidate: dopart--
do true -> hiliteOnError;
   usertext.length>0 -> value; 
   INNER;
   hiliteOnError -> textprivate.hilite;
   
--PromptForTextPopup: dopart--
do textprivate.theText.all-> textprivate.theText.selection;
   textprivate.theText.delete;
   defaultText[] -> textprivate.theText.insert;
   (* textprivate.theText.all-> textprivate.theText.selection;*)
   INNER;
   
--PromptForTextPrivate: descriptor--
(# hilite: @boolean;
   validatevalue: @validate;
   DialogText: textField
     (# eventhandler::
          (# onBeforeChange:: 
               (# txt: ^text
               do (if length=1 then
                      theText -> txt[];
                      (if 1->txt.inxGet
                       // ascii.nl
                       // ascii.cr then 
                          THIS(Prompt).private.okButton.theEventhandler.onMouseup;
                          false -> allow;
                      if);
                  if)
               #);
             onKeyDown::
               (# 
               do (if ch=ascii.esc then 
                      THIS(Prompt).private.cancelButton.theEventhandler.onMouseup;
                  if)
               #);
          #);
     #);
   theText: @DialogText
     (# open::
          (# 
          do (20,40) -> Position;
             (250,30) -> Size;
          #);
     #);
#)


(********* PromptForInteger **********)

--PromptForIntegerValidate: dopart--
do usertext.reset; 
   usertext.getint(# syntaxerror::
                       (# do false->THIS(validate).value; true->continue #)
                  #) -> userinteger;
   INNER;
   
(******* PromptForBoolean ************)
   
--PromptForBooleanOk: dopart--
do true->THIS(prompt).private.popdown;
   INNER
   
--PromptForBooleanOpen: dopart--
do 'Yes' -> private.okButton.label;
   'No' -> private.cancelButton.label;
   INNER;
