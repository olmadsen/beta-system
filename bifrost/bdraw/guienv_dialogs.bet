ORIGIN '~beta/guienv/v1.2/guienv';
INCLUDE '~beta/guienv/v1.2/fields';

--GuienvLib: attributes--

(* Standard dialogs for guienv.
 * Known problems:
 *     1. stupid xxx_popup title (guienv error)
 *     2. buttons seem to be pressed (guienv error I think)
 *     3. RET, CR, ESC characters typed in textfields should not
 *        be displayed. Have tryed using onBeforeChange without success.
 *        The current solution (delete) does not work properly.
 *)

PromptForText: window
  (# <<SLOT PromptForTextLib: attributes>>;
     
     usertext: ^text; (* The text entered by user *)
     ok:< 
       (* Called when the OK button is clicked *)
       (# 
       do thetext.contents->usertext[];
          (if validatevalue then 
              INNER; 
              THIS(PromptForText).hide 
           else
              (* bell *)
              thetext.all -> thetext.selection;
          if)
       #);
     cancel:< object (* Called when the Cancel button is clicked *);
     validate:< BooleanValue
       (* Called when the OK button is pressed. THIS(PromptForText) is only
        * dismissed (and INNER in the ok virtual called) if validate returns 
        * true.
        *)
       (# do usertext.length>0 -> value; INNER #);
     popup:
       (* Used to pop up the dialog and wait for the text to be typed *)
       (# titleText, msgText, defaultText: ^text;
          father: ^window;
          fatherpos, fathersize, mysize: @point;
       enter (father[], titleText[], msgText[], defaultText[])
       do (if dialogopened//false then
              open;
          if);
          titleText[] -> title;
          msgText[] -> theMessage.label;
          theText.clear;
          defaultText[] -> theText.insert;
          (* theText.all-> thetext.selection;*)
          father.size -> fathersize;
          size -> mysize;
          father.position -> fatherpos;
          (fatherpos.v + (fathersize.v-mysize.v) div 2,
          fatherpos.h + (fathersize.h-mysize.h) div 2) -> position;
          showmodal;
       #);
     
     (* Private *)
     validatevalue: @validate;
     theMessage: @staticText
       (# open::
            (# 
            do (20,10) -> position;
               (200,30) -> size;
            #);
       #);
     DialogText: textField
       (# eventhandler::
            (# onKeyDown:: 
                 (# 
                 do (if ch
                     // ascii.nl
                     // ascii.cr then 
                        delete; (* delete the character *)
                        okButton.theEventhandler.onMouseup
                     // ascii.esc then 
                        delete; (* delete the character *)
                        cancelButton.theEventhandler.onMouseup
                    if)
                 #);
            #);
       #);
     theText: @DialogText
       (# open::
            (# 
            do (20,40) -> Position;
               (200,30) -> Size;
            #);
       #);
     okButton: @pushButton
       (# eventHandler:: (# onMouseUp:: (# do ok #)#);
          open::
            (# 
            do (180,80) -> position;
               (40,30) -> size;
               'OK' -> label;
               true -> border.visible;
            #);
       #);
     cancelButton: @pushButton
       (# eventHandler:: 
            (# onMouseUp:: (# do cancel; THIS(PromptForText).hide #)#);
          open::
            (# 
            do (120,80) -> position;
               (50,30) -> size;
               'Cancel' -> label;
               true -> border.visible;
            #);
       #);
     open::<
       (# 
       do (if dialogopened//false then
              true->dialogopened;
              hide;
              theMessage.open;
              theText.open;
              okButton.open;
              cancelButton.open;
              okButton[] -> defaultButton;
              (230,120) -> size;
              INNER;
          if);
       #);
     dialogopened: @boolean;
  #);

PromptForInteger: PromptForText
  (* Special PromptForText that will only accept integer input *)
  (# <<SLOT PromptForIntegerLib: attributes>>;
     userinteger: @integer (* The integer typed by the user *);
     validate::<
       (# 
       do usertext.reset; 
          usertext.getint(# syntaxerror::(# do false->THIS(validate).value; true->continue #)#)
            -> userinteger; 
       #)
  #);
