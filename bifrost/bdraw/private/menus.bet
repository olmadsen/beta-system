ORIGIN '../bdrawenv';
INCLUDE '~beta/guienv/utils/simplemenu';
INCLUDE '~beta/guienv/utils/private/streamwindowbody';

-- bdrawmenus: descriptor --
(#     
   FileMenu: @simplemenu
     (# inew:    @item(# onSelect:: (# do NewFile    #)#);
        iopen:   @item(# onSelect:: (# do OpenFile   #)#);
        iclose:  @item(# onSelect:: (# do Iconify    #)#);
        isave:   @item(# onSelect:: (# do SaveFile   #)#);
        isaveas: @item(# onSelect:: (# do SaveFileAs #)#);
        iexport: @item(# onSelect:: (# do ExportEPS  #)#);
        iprint:  @item(# onSelect:: (# do Print      #)#);
        iquit:   @item(# onSelect:: (# do Quit       #)#);
        
        open:: 
          (# 
          do ('New...',  'n')       -> inew.newkey;
             ('Open...', 'o')       -> iopen.newkey;
             ('Close',   'w')       -> iclose.newkey;
             newseparator;
             ('Save',    's')       -> isave.newkey;
             ('Save As...')         -> isaveas.new;
             newseparator;
             ('Print...', 'p')      -> iprint.newkey; 
             (* ('Export EPS...', 'e') -> iexport.newkey; *)
             newseparator;
             ('Quit',    'q')       -> iquit.newkey;
          #)
     #);
   EditMenu: @simplemenu             	    
     (# iundo: @item(# onSelect:: (# do Undo #)#);
        icut: @item(# onSelect:: (# do Cut  #)#);
        icopy: @item(# onSelect:: (# do Copy #)#);
        ipaste: @item(# onSelect:: (# do Paste #)#);
        iclear: @item(# onSelect:: (# do ClearSelection #)#);
        idup: @item(# onSelect:: (# do Duplicate #)#);
        iall: @item(# onSelect:: (# do SelectAll #)#);
        
        open:: 
          (# 
          do ('Undo', 'z') ->iundo.newkey; false -> iundo.enabled;
             newseparator;
             ('Cut', 'x') ->icut.newkey; 
             ('Paste', 'v') ->ipaste.newkey; 
             ('Copy', 'c') ->icopy.newkey; 
             newseparator;
             'Clear' ->iclear.new; 
             ('Duplicate') ->idup.new; 
             ('Select All', 'a') ->iall.newkey;
          #);
     #);
   PaintMenu: @simplemenu              	    
     (# icolor: @item
          (# onSelect:: 
               (# 
               do colorwin.show;
                  colorwin.bringtofront;
                  true -> ViewMenu.icolor.checked;
               #)
          #);
        ipattern: @item(# #);
        open:: 
          (# 
          do 'Color...'->icolor.new;
             'Pattern...'->ipattern.new; false->ipattern.enabled;
          #);
     #);
   ShapeMenu: @simplemenu              	    
     (# ismooth: @item(# onSelect:: (# do Smooth #)#);
        iunsmooth: @item(# onSelect:: (# do Unsmooth #)#);
        ismoothness: @item
          (# onSelect::(# do 'Smoothness cannot yet be set!'->Note #)#);
        ireverse: @item
          (# onSelect:: (# do ReverseOrientation #)#);
        icombine: @item
          (# onSelect:: (# do CombineShape #)#);
        istroke: @item
          (# onSelect:: (# do StrokeShape #)#);
        ievenodd: @radioitem
          (# onSelect:: (# do EvenOddRule->SetFillRule #)#);
        iwinding: @radioitem
          (# onSelect:: (# do WindingRule->SetFillRule #)#);
        open:: 
          (# 
          do 'Smooth'->ismooth.new; false -> ismooth.enabled;    
             'Unsmooth'->iunsmooth.new;  false -> iunsmooth.enabled;
             'Smoothness...'->ismoothness.new; 
             'Reverse Orientation'->ireverse.new; false -> ireverse.enabled; 
             'Combine Shape'->icombine.new;
             'Stroke'->istroke.new;
             'Even-Odd Fillrule'->ievenodd.new; 
             'Winding Fillrule'->iwinding.new;
          #);
     #);
   transformationMenu: @simplemenu            	    
     (# imove: @item(# onSelect:: (# do Move #)#);
        imoveto: @item(# onSelect:: (# do MoveTo #)#);
        iscale: @item(# onSelect:: (# do Scale #)#);
        irot: @item(# onSelect:: (# do Rotate #)#);
        ihflip: @item(# onSelect:: (# do FlipHor #)#);
        ivflip: @item(# onSelect:: (# do FlipVer #)#);
        open:: 
          (# 
          do 'Move...'->imove.new; false -> imove.enabled;
             'Move To...'->imoveto.new; false -> imoveto.enabled;
             'Scale...'->iscale.new; false -> iscale.enabled;
             'Rotate...'->irot.new; false -> irot.enabled;
             'Flip Horizontal'->ihflip.new; false -> ihflip.enabled;
             'Flip Vertical'->ivflip.new; false -> ivflip.enabled;
          #);
     #);
   ArrangeMenu: @simplemenu              	    
     (# iraise: @item(# onSelect:: (# do RaiseSelection #)#);
        ilower: @item(# onSelect:: (# do LowerSelection #)#);
        igroup: @item(# onSelect:: (# do Group #)#);
        iungroup: @item(# onSelect:: (# do Ungroup #)#);
        open:: 
          (# 
          do ('Raise', '+')->iraise.newkey; 
             ('Lower', '-')->ilower.newkey;    
             ('Group', 'g')->igroup.newkey; false -> igroup.enabled;
             ('UnGroup', 'u')->iungroup.newkey; false -> iungroup.enabled;
          #);
     #);
   TextMenu: @simplemenu             	    
     (# TextSubmenu: simplemenu
          (# textitem: radioitem
               (# onSelect::< (# do INNER; TextAttChanged #)#);
          #);
        ifont: @item
          (# open::
               (# 
               do Fontmenu.open;
                  Fontmenu[] -> subMenu;
               #);
             FontMenu: @TextSubmenu               	    
               (# icourier: @textitem   (# onSelect:: (# do Courier->myFontName #)#);
                  itimes: @textitem     (# onSelect:: (# do Times->myFontName #)#);
                  ihelvetica: @textitem (# onSelect:: (# do Helvetica->myFontName #)#);
                  open:: 
                    (# 
                    do 'Courier' -> icourier.new;
                       'Times' -> itimes.new;
                       'Helvetica' -> ihelvetica.new;
                    #);
               #);
          #);
        istyle: @item
          (# open::
               (# 
               do Stylemenu.open;
                  Stylemenu[] -> subMenu;
               #);
             StyleMenu: @TextSubmenu               	    
               (# iplain: @textitem  (# onSelect:: (# do Plain->myStyle #)#);
                  ibold: @textitem   (# onSelect:: (# do Bold->myStyle #)#);
                  iitalic: @textitem (# onSelect:: (# do Italic->myStyle #)#);
                  open:: 
                    (# 
                    do 'Plain' -> iplain.new;
                       'Bold' -> ibold.new;
                       'Italic' -> iitalic.new;
                    #);
               #);
          #);
        isize: @item
          (# open::
               (# 
               do Sizemenu.open;
                  Sizemenu[] -> subMenu;
               #);
             SizeMenu: @TextSubmenu               	    
               (# textsizeitem: textitem
                    (# value: @integer;
                       newsize: new(# enter value #);
                       onSelect:: (# do value->mySize #);
                    #);
                  i2, 
                  i6, i7, i8, i10, i11, i12, i13, 
                  i14, i15, i16, i17, i18, i19, i20, 
                  i24, i25, i26, 
                  i30, i33, i34, i40, i50, i60: @textsizeitem;
                  open:: 
                    (# 
                    do ( '2', 2)->i2.newsize;
                       ( '6', 6)->i6.newsize;
                       ( '7', 7)->i7.newsize;
                       ( '8', 8)->i8.newsize;
                       ('10',10)->i10.newsize;
                       ('11',11)->i11.newsize;
                       ('12',12)->i12.newsize;
                       ('13',13)->i13.newsize;
                       ('14',14)->i14.newsize;
                       ('15',15)->i15.newsize;
                       ('16',16)->i16.newsize;
                       ('17',17)->i17.newsize;
                       ('18',18)->i18.newsize;
                       ('19',19)->i19.newsize;
                       ('20',20)->i20.newsize;
                       ('24',24)->i24.newsize;
                       ('25',25)->i25.newsize;
                       ('26',26)->i26.newsize;
                       ('30',30)->i30.newsize;
                       ('33',33)->i33.newsize;
                       ('34',34)->i34.newsize;
                       ('40',40)->i40.newsize;
                       ('50',50)->i50.newsize;
                       ('60',60)->i60.newsize;
                    #);
               #);
          #);
        iunderline: @toggleitem
          (# onSelect:: 
               (# 
               do (not myUnderlineFlag)->myUnderlineFlag;
                  TextAttChanged;
               #)
          #);
        open:: (* TextMenu *)
          (# 
          do 'Font' -> ifont.new;
             'Style' -> istyle.new;
             'Size' -> isize.new;
             'Underline'->iunderline.new;
          #);
     #);
   LineMenu: @simplemenu               	    
     (# LineSubmenu: simplemenu
          (# lineitem: radioitem
               (# onSelect::< 
                    (# do INNER; LineAttChanged #)
               #);
          #);
        icapjoin: @item
          (# open::
               (# 
               do CapJoinMenu.open;
                  CapJoinMenu[] -> subMenu;
               #);
             CapJoinMenu: @LineSubmenu               	    
               (# noOfRadioGroups:: (# do 2 -> value #);
                  ibutt: @lineitem    (# onSelect:: (# do CapButt->mycapstyle #)#); 
                  irounded: @lineitem (# onSelect:: (# do CapRounded->mycapstyle #)#); 
                  isquare: @lineitem  (# onSelect:: (# do CapSquare->mycapstyle #)#); 
                  imiter: @lineitem
                    (# onSelect:: (# do JoinMiter->myjoinstyle #);
                       open:: (# do 2 -> checkgroup #);
                    #); 
                  iround: @lineitem
                    (# onSelect:: (# do JoinRound->myjoinstyle #);
                       open:: (# do 2 -> checkgroup #);
                    #); 
                  ibevel: @lineitem
                    (# onSelect:: (# do JoinBevel->myjoinstyle #);
                       open:: (# do 2 -> checkgroup #);
                    #);
                  open:: 
                    (# 
                    do 'CapButt'->ibutt.new; 
                       'CapRounded'->irounded.new; 
                       'CapSquare'->isquare.new;
                       newseparator;
                       'JoinMiter'->imiter.new; 
                       'JoinRound'->iround.new;  
                       'JoinBevel'->ibevel.new; 
                    #);
               #);
          #);
        ilinewidth: @item
          (# open::
               (# 
               do LineWidthMenu.open;
                  LineWidthMenu[] -> subMenu;
               #);
             LineWidthMenu: @LineSubmenu
               (# linewidthitem: lineitem
                    (# value: @integer;
                       newwidth: new(# enter value #);
                       onSelect:: (# do value->myLineWidth #);
                    #);
                  
                  i0,i1,i2,i3,i4,i5,i6,i7,i8,i9: @linewidthitem;
                  iother: @lineitem
                    (# onSelect:: 
                         (# 
                         do ('Enter new linewidth: ', 'Bdraw: Linewidth')
                              -> readint(# useit:: (# do value->myLineWidth; #)#)
                         #)
                    #);			     
                  open:: 
                    (# 
                    do ('0',0)->i0.newwidth;
                       ('1',1)->i1.newwidth;
                       ('2',2)->i2.newwidth;
                       ('3',3)->i3.newwidth;
                       ('4',4)->i4.newwidth;
                       ('5',5)->i5.newwidth;
                       ('6',6)->i6.newwidth;
                       ('7',7)->i7.newwidth;
                       ('8',8)->i8.newwidth;
                       ('9',9)->i9.newwidth;
                       ('Other...', 'l')->iother.newkey;
                    #);
               #);
          #);
        open:: 
          (# 
          do 'Cap/Join' -> icapjoin.new;
             'Line Width' -> ilinewidth.new;
          #);
     #);
   ViewMenu: @simplemenu              	    
     (# iclipb: @toggleitem
          (# onStatus::
               (# 
               do (if clipwin.isOpen then
                      clipwin.visible->checked;
                   else false->checked;
                  if);
               #);
             
             onSelect::
               (# do ShowClipboard #)
          #);
        icolor: @item
          (* colorwin can be made visible from the Paint menu too,
           * and can be closed from the window itself.
           * Have to make the following to keep check-mark correct.
           *)
          (# eventhandler::
               (# onSelect:: 
                    (# do ShowColorWindow #);
                  onStatus::
                    (# do colorwin.visible -> checked #);
               #);
          #);
        ilog: @item
          (# open::
               (# 
               do LogMenu.open;
                  LogMenu[] -> subMenu;
               #);
             LogMenu: @SimpleMenu               	    
               (# iwarn: @toggleitem
                    (# onSelect:: 
                         (# do (not displaywarnings)->displaywarnings #)
                    #);
                  ishow: @toggleitem
                    (#
                       onStatus::
                         (# 
                         do (if logwin.isOpen then
                                logwin.visible->checked;
                             else false->checked;
                            if);
                         #);
                       onSelect:: 
                         (# do ShowLogWindow #)
                    #);
                  iflush: @item
                    (# onSelect:: 
                         (# do logwin.thestream.flush; #)
                    #);
                  isaveas: @item
                    (# onSelect:: 
                         (# do SaveLog #)
                    #);
                  open:: 
                    (# 
                    do 'Show Log Window' -> ishow.new;
                       'Display Bifrost Warnings' -> iwarn.new;
                       'Flush Log' -> iflush.new;
                       'Save Log As...' -> isaveas.new;
                    #);
               #);
          #);
        irefresh: @item(# onSelect:: (# do RefreshAll #)#);
        izoom: @item
          (# open::
               (# 
               do ZoomMenu.open;
                  ZoomMenu[] -> subMenu;
               #);
             ZoomMenu:@ simplemenu
               (# CurrentZoom: @real;
                  NewZoomFactor:
                    (* Setting zoom using this pattern will ensure
                     * that correct item is selected in menu.
                     *)
                    (# z: @real;
                    enter z
                    do (if z
                        // 0.25 then i25.onSelect;
                        // 0.50 then i50.onSelect;
                        // 0.75 then i75.onSelect;
                        // 1.00 then i100.onSelect;
                        // 1.25 then i125.onSelect;
                        // 1.50 then i150.onSelect;
                        // 2.00 then i200.onSelect;
                        // 2.50 then i250.onSelect;
                        // 3.00 then i300.onSelect;
                        else
                           (* FIXME: last selected item in menu
                            * will remain selected. An 'uncheckcurrent'
                            * operation is missing for radioitems in
                            * simplemenu.
                            *)
                           (if z<0.01 then 0.01 -> z if);
                           z -> CurrentZoom;
                           (z,z) -> maincanvas.ZoomFactor; 
                       if);
                    #);
                  idouble: @item
                    (# onSelect:: 
                         (# do CurrentZoom*2->NewZoomFactor #)
                    #);
                  ihalf: @item
                    (# onSelect:: 
                         (# do CurrentZoom/2->NewZoomFactor #)
                    #);
                  zoomitem: radioitem
                    (# value: @real;
                       newzoom: new(# enter value #);
                       onSelect::
                         (#
                         do value -> CurrentZoom;
                            (value, value) -> maincanvas.ZoomFactor
                         #);
                    #);
                  
                  i25, i50, i75, i100, i125, i150, i200, i250, i300: @zoomitem;
                  iother: @radioitem
                    (# onSelect:: 
                         (# 
                         do ('Enter new zoomfactor (%): ', 'Zoom Factor')
                              -> readint
                            (# useit:: (# do value/100->NewZoomFactor #)#)
                         #)
                    #);
                  open:: 
                    (# 
                    do ('Double Size', 'd')->idouble.newkey;
                       ('Half Size', 'h')->ihalf.newkey;
                       newseparator;
                       ('25%',0.25)->i25.newzoom;
                       ('50%',0.50)->i50.newzoom;
                       ('75%',0.75)->i75.newzoom;
                       ('100%',1.00)->i100.newzoom;
                       ('125%',1.25)->i125.newzoom;
                       ('150%',1.50)->i150.newzoom;
                       ('200%',2.00)->i200.newzoom;
                       ('250%',2.50)->i250.newzoom;
                       ('300%',3.00)->i300.newzoom;
                       ('Other...')->iother.new;
                    #);
               #);
          #);
        open:: 
          (# 
          do 'Show Clipboard' ->iclipb.new; 
             'Show Color Selector' ->icolor.new; 
             'Log Window' ->ilog.new; 
             newseparator;
             ('Refresh', 'r')->irefresh.newkey;
             'Zoom'->izoom.new;
          #);
     #);
   init:
     (# 
     do 'File' -> FileMenu.new;
        'Edit' -> EditMenu.new;
        'Paint' -> PaintMenu.new;
        'Shape' -> ShapeMenu.new;
        'Transform' -> TransformationMenu.new;
        'Arrange' -> Arrangemenu.new;
        'Text' -> TextMenu.new;
        'Line' -> Linemenu.new;
        'View' -> ViewMenu.new;
     #);
#)

-- BdrawInitmenus: dopart --
do menus.init;
   
-- BdrawMenusDefaults: descriptor --
(# (* Select initial items in menus *)
do false -> displaywarnings;
   menus.Shapemenu.ievenodd.onSelect;
   menus.LineMenu.ilinewidth.LineWidthMenu.i2.onSelect;
   menus.Linemenu.icapjoin.CapJoinMenu.ibutt.onSelect;
   menus.Linemenu.icapjoin.CapJoinMenu.imiter.onSelect;
   menus.Textmenu.ifont.FontMenu.itimes.onSelect;
   menus.Textmenu.istyle.StyleMenu.iplain.onSelect;
   menus.Textmenu.isize.SizeMenu.i20.onSelect;
   menus.Viewmenu.izoom.ZoomMenu.i100.onSelect;
   menus.ViewMenu.ilog.LogMenu.iwarn.onSelect;
#)
