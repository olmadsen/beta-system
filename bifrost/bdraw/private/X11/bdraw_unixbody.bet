ORIGIN '../../bdrawenv';
INCLUDE '~beta/bifrost/private/X11/CanvasXtImpl';
INCLUDE '~beta/guienv/private/X11/guienv_unixbody';
INCLUDE '~beta/sysutils/envstring';
INCLUDE '~beta/basiclib/file';
INCLUDE '../menus';
INCLUDE '~beta/guienv/utils/prompts';
INCLUDE '../cursors';
INCLUDE 'printdialog';

--LIB: attributes--
mktemp: external
  (# template: [0]@char;
     temp: [0]@char;
  enter template
  exit temp
  #);
system: external
  (# cmd: [0]@char;
     diag: @integer;
  enter cmd
  exit diag    
  #)

-- BdrawPrint: descriptor--
(# 
   oldSelection: @picture;
   thePrintDialog: @PrintDialog
     (# ok:: 
          (# 
          do 'Spooling ...' -> Inform;
             printcommand[]->extensions.printcommand[]; 
             (if true
              // 'A4'->papersize.equal then paper_A4 -> paper;
              // 'Letter'->papersize.equal then paper_letter -> paper;
              // 'Legal'->papersize.equal then paper_legal -> paper;
              // 'Executive'->papersize.equal then paper_Executive -> paper;
             if);
             numcopies -> num;
             printEPS;
             (if extensions.print_failed then
                 error;
              else
                 'Spooling ... done' -> Inform;
             if);
             (if out.entry.exists then
                 out.delete;
             if);
          #)
     #);
   num: @integer;
   paper: @point;
   page: @rectangle;
   
   fname: ^text;
   cmd: ^text;
   out: @file;
   error:
     (# msg: ^text;
     do 'Bdraw: Print: Failed to execute print command \''-> msg[];
        cmd[]->msg.puttext;
        '\''->msg.puttext;
        msg -> Inform;
        msg[] -> Note;
     #);
   printEPS:
     (# 
     do SaveCursor;
        WatchCursor;
        false -> extensions.print_failed;
        
        (* Construct unique temporary file name *)
        '$(TMPDIR)' -> expandEnvVar -> fname[];
        (if fname.length=0 then 
            '/tmp' -> fname[] 
        if);
        '/bdraw.XXXXXX'->fname.append;
        fname -> mktemp -> fname;
        
        (* Open temporary file *)
        fname[]->out.name;
        out.openwrite;
        
        (* Construct print command *)
        extensions.printcommand.copy -> cmd[];
        ' '->cmd.append;
        fname[] -> cmd.append;
        
        (* If the selection has been moved or otherwise transformed, this 
         * transformation is only registered by the selection TM matrix.
         * Temporarily remove the GOs in theSelection to make the GOs
         * incorporate the transformations.
         *)
        oldSelection.init;
        theSelection.scanGOs(# do go[] -> oldSelection.add #);
        theSelection.clear;
        
        (* Produce the postscript on the file 'out' *)
        (0, 0, paper.x, paper.y) -> page;
        (page, true, num, out[]) -> maincanvas.writeEPS;
        (* Restore selection *)
        oldSelection.scanGOs(# do go[] -> theSelection.add #);
        out.close;
        
        (* Execute the print command *)
        (*cmd[] -> screen.putline;*)
        (cmd->system)<>0 -> extensions.print_failed;
        
        RestoreCursor;
     #);
do (if extensions.printcommand[]//NONE then
       '$(BDRAWPRINT)' -> expandEnvVar -> extensions.printcommand[];
       (if extensions.printcommand.length=0 then 
           'lpr' -> extensions.printcommand[] 
       if);
   if);
   (topwindow[], 'Bdraw Print', extensions.printcommand[]) 
     -> theprintdialog.popup;
#)

-- BdrawExtensions: descriptor --
(# printcommand: ^text;
   print_failed: @boolean;
   dpy, root, bitmap, status: @integer;
   filename: ^text;
   width, height, bmap: @integerref;
   doiconify: 
     (# do (XtNiconic, true) -> topWindow.setBooleanResource #);
   menufonts: @MotifFontList
     (# addText:
          (* Add one entry to THIS(MotifFontList) *)
          (# font, charset: ^text;
             fontstruct: @integer;
          enter (font[], charset[])
          do (THIS(GuiENV).private.display, font) -> XLoadQueryFont -> fontstruct;
             (if value
              = 0 then
                 (fontstruct, @@charset.T[1])
                   -> XmFontListCreate -> value
              else
                 (value, fontstruct, @@charset.T[1])
                   -> XmFontListAdd -> value
             if)
          #);
     #);     
   menulabel: @MotifString
     (# setT:
          (# string, charset: ^text;
          enter (string[], charset[])
          do (string[], charset[], XmSTRING_DIRECTION_L_TO_R)  -> setTextSegment;
          #)
     #);
   menubar: ^menubarpattern;
do 
   (* Add iconify pattern *)
   doiconify## -> iconify##;
   
   (* Specify icon for toplevel *)
   ('~beta/bifrost/bitmaps/mjoelner.xbm', '') 
     -> bifrostprivate.PH.convertFilePath
     -> filename[];
   THIS(GuiENV).private.display -> dpy -> XDefaultRootWindow -> root;
   (dpy,root,filename,width,height,bmap,0,0)
     -> XReadBitmapFile -> status;
   (if status
    // XBitmapOpenFailed then
       (if displayWarnings then
           'Cannot open bitmap file '->puttext; filename[]->putline; 
       if);
    // XBitmapFileInvalid then
       (if displayWarnings then
           filename[]->puttext;
           ' is not a valid bitmap file' -> putline;
       if);
    // XBitmapNoMemory then
       (if displayWarnings then
           'Not enough memory to hold bitmap file '->puttext;
           filename[] -> putline;
       if)
    // XBitmapSuccess then
       bmap.value -> bitmap;
   if);
   (XtNiconPixmap, bitmap) -> topwindow.setIntegerresource;
   
   (* Specify fonts for Text submenus *)
   ('-*-COURIER-MEDIUM-R-*-120-*-ISO8859-1',   'courier')   -> menufonts.addText;
   ('-*-TIMES-MEDIUM-R-*-120-*-ISO8859-1',     'times')     -> menufonts.addText;
   ('-*-HELVETICA-MEDIUM-R-*-120-*-ISO8859-1', 'helvetica') -> menufonts.addText;
   ('-*-MEDIUM-R-*-120-*-ISO8859-1', 'plain')  -> menufonts.addText;
   ('-*-BOLD-R-*-120-*-ISO8859-1', 'bold')     -> menufonts.addText;
   ('-*-MEDIUM-I-*-120-*-ISO8859-1', 'italic') -> menufonts.addText;
   menulabel.init;
   topwindow.themenubar -> menubar[];
   
   ('Plain', 'plain') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.iplain.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Bold', 'bold') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.ibold.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Italic', 'italic') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.iitalic.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Times', 'times') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.itimes.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Helvetica', 'helvetica') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.ihelvetica.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Courier', 'courier') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.icourier.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);

   
#)

--InitCursors: descriptor--
(# 
do maincanvas.theCursor -> savedcursor.c[];
   (THIS(GuiENV).private.display,XC_ul_angle) -> XCreateFontCursor 
     -> ulcornercursor.ulc.private.id;
   (THIS(GuiENV).private.display,XC_lr_angle) -> XCreateFontCursor 
     -> lrcornercursor.lrc.private.id;
   (THIS(GuiENV).private.display,XC_hand2) -> XCreateFontCursor 
     -> lefthandcursor.lhc.private.id;
#)
