ORIGIN 'printdialog';
INCLUDE '~beta/guienv/utils/private/promptsbody';
INCLUDE '~beta/guienv/controls';

--PrintDialogPrivate: descriptor--
(# dlg: @PromptForText
     (# validate::<
          (# n: ^text;
          do (if value then
                 (* validation of usertext OK *)
                 usertext.copy -> printcommand[];
                 (* validate numCopies *)
                 numcopies.contents->n[]; 
                 n.reset;
                 n.getInt(# syntaxerror::
                              (# do false->THIS(validate).value; true->continue #)
                         #)->THIS(PrintDialog).numCopies;
             if);
          #);
        ok::<
          (# 
          do THIS(PrintDialog).ok;
          #);
        AskNumCopies: @staticText
          (# eventhandler::<
               (# onLabelChanged::<
                    (# p,s: @point;
                    do (labelWidth,labelHeight)->size;
                       THIS(Prompt).private.theText.position -> p;
                       THIS(Prompt).private.theMessage.size -> s;
                       (p.h, p.v+s.v+14+2*THIS(Prompt).private.vSep) 
                         -> position;
                    #);
                  onFatherFrameChanged::
                    (# (* NYI *) #)
               #);
             open::
               (# 
               do 'Number of Copies:' -> label;
                  
               #);
          #);
        NumCopies: @private.DialogText
          (# open:: 
               (# t: @styledtext;
                  p,s: @point;
               do AskNumCopies.position -> p;
                  AskNumCopies.size -> s;
                  (p.h+s.h+THIS(Prompt).private.hSep, p.v-7) 
                    -> position;
                  (30,s.v+14)->size;
                  '1' -> t;
                  t[] -> numCopies.contents;
               #);
          #);
        PaperSize: @optionButton
          (# eventHandler::
               (# onCurrentItemChanged::
                    (# 
                    do (if currentItem
                        //1 then 'A4' -> THIS(PrintDialog).papersize[];
                        //2 then 'Letter' -> THIS(PrintDialog).papersize[];
                        //3 then 'Legal' -> THIS(PrintDialog).papersize[];
                        //4 then 'Executive' -> THIS(PrintDialog).papersize[];
                       if);
                    #);
               #);
             aMenu: @menu
               (# addMenuitem:
                    (# itsName: ^text;
                    enter itsName[]
                    do (# theMenuItem: ^menuItem;
                       do &menuItem[] -> theMenuItem[];
                          theMenuItem.open;
                          itsName[] -> theMenuItem.name;
                          theMenuItem[] -> append;
                       #);
                    #);
                  open::
                    (#
                    do 'A4' -> addMenuitem;
                       'Letter' -> addMenuItem;
                       'Legal' -> addMenuItem;
                       'Executive' -> addMenuItem;
                    #);
               #);
             open::
               (# p,s: @point;
               do AskNumCopies.position -> p;
                  AskNumCopies.size -> s;
                  (p.h-3, p.v+s.v+THIS(Prompt).private.vSep) 
                    -> position;
                  aMenu.open;
                  aMenu[] -> popupMenu;
                  'Paper size:' -> label;
                  fitToContents;
                  'A4'->THIS(PrintDialog).papersize[];
               #);
          #);
        extra_vspace::<
          (# s: @point;
          do AskNumCopies.size -> s;
             s.v+2*THIS(Prompt).private.vSep -> value;
             PaperSize.size -> s;
             value + s.v+THIS(Prompt).private.vSep -> value;
             (* To give a little space before the buttons: *)
             value+20->value;
          #);
        extra_hspace::<
          (# 
          do 25 -> value;
          #);
        open::<
          (# 
          do this(prompt).contents->AskNumCopies.open;
             this(prompt).contents->NumCopies.open;
             this(prompt).contents->PaperSize.open;
          #);
     #);
#)

--PrintDialogPopup: dopart--
do (father[], title[], 'Print Command:           ', printcommand[]) 
     -> private.dlg.popup;
