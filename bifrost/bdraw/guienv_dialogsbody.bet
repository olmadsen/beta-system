ORIGIN 'guienv_dialogs';
INCLUDE '~beta/guienv/v1.2/fields';

--PromptForTextOk: dopart--
do private.theText.contents->usertext[];
   (if private.validatevalue then 
       INNER; 
       THIS(PromptForText).hide 
    else
       (* bell *)
       private.theText.all -> private.theText.selection;
   if)
   
--PromptForTextValidate: dopart--
do usertext.length>0 -> value; INNER
   
--PromptForTextPopup: dopart--
do (if private.dialogopened//false then
       open;
   if);
   titleText[] -> title;
   msgText[] -> private.theMessage.label;
   private.theText.clear;
   defaultText[] -> private.theText.insert;
   (* private.theText.all-> private.theText.selection;*)
   father.size -> private.fathersize;
   size -> private.mysize;
   father.position -> private.fatherpos;
   (private.fatherpos.v + (private.fathersize.v-private.mysize.v) div 2,
   private.fatherpos.h + (private.fathersize.h-private.mysize.h) div 2) 
     -> position;
   showmodal;
   
--PromptForTextOpen: dopart--
do (if private.dialogopened//false then
       true->private.dialogopened;
       hide;
       private.theMessage.open;
       private.theText.open;
       private.okButton.open;
       private.cancelButton.open;
       private.okButton[] -> defaultButton;
       (230,120) -> size;
       INNER;
   if);
   
--PromptForTextPrivate: descriptor--
(# fatherpos, fathersize, mysize: @point;
   dialogopened: @boolean;
   validatevalue: @validate;
   theMessage: @staticText
     (# open::
          (# 
          do (20,10) -> position;
             (200,30) -> size;
          #);
     #);
   DialogText: textField
     (# eventhandler::
          (# onBeforeChange:: 
               (# txt: ^text
               do (if length=1 then
                      theText -> txt[];
                      (if 1->txt.inxGet
                       // ascii.nl
                       // ascii.cr then 
                          okButton.theEventhandler.onMouseup;
                          false -> allow;
                      if);
                  if)
               #);
             onKeyDown::
               (# 
               do (if ch=ascii.esc then 
                      cancelButton.theEventhandler.onMouseup;
                  if)
               #);
          #);
     #);
   theText: @DialogText
     (# open::
          (# 
          do (20,40) -> Position;
             (200,30) -> Size;
          #);
     #);
   okButton: @pushButton
     (# eventHandler:: (# onMouseUp:: (# do ok #)#);
        open::
          (# 
          do (180,80) -> position;
             (40,30) -> size;
             'OK' -> label;
             true -> border.visible;
          #);
     #);
   cancelButton: @pushButton
     (# eventHandler:: 
          (# onMouseUp:: (# do cancel; THIS(PromptForText).hide #)#);
        open::
          (# 
          do (120,80) -> position;
             (50,30) -> size;
             'Cancel' -> label;
             true -> border.visible;
          #);
     #);
#)

--PromptForIntegerValidate: dopart--
do usertext.reset; 
   usertext.getint(# syntaxerror::
                       (# do false->THIS(validate).value; true->continue #)
                  #)
     -> userinteger;
