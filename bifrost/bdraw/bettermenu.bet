ORIGIN '~beta/guienv/v1.2/guienv';

-- MenuBarLib: attributes --

bettermenu: menu
  (* Special menu type with interdependant check marks, some other 
   * extra features, and a few code saving patterns.
   *)
  (# item: menuitem
       (# enabled: @boolean;
          onSelect:< object;
          eventhandler::<
            (# onSelect::<
                 (# do THIS(item).onSelect; INNER #);
               onStatus::<
                 (# do enabled->value #);
            #);
          new:
            (# n: ^text;
            enter n[]
            do true -> enabled; 
               open; 
               (if n[]<>NONE then n[]->name if);
               THIS(item)[] -> append;
               INNER;
            #);
          newkey: new(# k: @char enter k do k->key #);
       #);
     radioitem: item
       (# checkgroup: @integer
            (* The group of items THIS(item) is dependant on. default 1. *);
          open::< (# do 1 -> checkgroup; INNER; #);
          onSelect::< (# do THIS(radioitem)[] -> changechecked; INNER; #);
       #);
     toggleitem: item
       (# onSelect::< (# do not checked->checked; INNER; #)#);
     
     oldChecked: [3]^radioitem; (* Up to 3 independent groups of items *)
     changechecked:
       (# newitem: ^radioitem
       enter newitem[]
       do (if oldChecked[newitem.checkgroup][]<>newitem[] then
              (if oldChecked[newitem.checkgroup][]<>NONE then
                  false->oldChecked[newitem.checkgroup].checked;
              if);
              newitem[]->oldChecked[newitem.checkgroup][];
              true->newitem.checked;
          if)
       #);
     newseparator: 
       (# s: ^separator;
       do &separator[]->s[];
          s.open;
          s[] -> append;
       #);
     new:
       (# n: ^text;
       enter n[]
       do open; 
          (if n[]<>NONE then n[]->name if);
          THIS(bettermenu)[] -> THIS(menubar).append;
       #)
  #);
