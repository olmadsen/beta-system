ORIGIN 'bdrawenv';
INCLUDE '../Palette';

-- NewState: dopart --
do s -> thepalette.selection 
   
-- State: dopart --
do thepalette.selection -> s
   
---- PaletteBody: descriptor ----
Palette
(# shiftmodified: @boolean;
   eventhandler::
     (# onMouseDown::
          (# do shiftmodified -> thePalette.shiftmodified #)
     #);
   changed::
     (#
     do 
        (if selection
         // SelectMode then
            Select;
            LefthandCursor;
            'Choose object from the palette' -> inform;
         else
            theSelection.clear;
            shiftmodified -> holdobjecttype;
            (if selection
             // RectObject then
                'Rectangle' -> Inform;
                ulcornercursor;
             // EllipseObject then
                'Ellipse' -> Inform;
                crosscursor;
             // UserDefObject then
                'User Defined Object' -> Inform;
                crosscursor;
             // PieObject then
                'Pie Slice' -> Inform;
                crosscursor;
             // LineObject then
                'Line' -> Inform;
                crosscursor;
             // MultiLineObject then
                'MultiLine (Polygon)' -> Inform;
                crosscursor;
             // TextObject then
                'Text' -> Inform;
                beamcursor;
             // StrokedEllipseObject then
                'Stroked Ellipse' -> Inform;
                crosscursor;
             // StrokedRectObject then
                'Stroked Rectangle' -> Inform;
                ulcornercursor
             // ArcObject then
                'Arc' -> Inform;
                crosscursor;
             else
                LefthandCursor;
                'Object?' -> inform;
            if);
        if);
     #);
   
   open::
     (#
        R: ^Rect;
        E: ^Ellipse;
        G: ^GraphicalObject;
        n: ^NonCircularSplineSegment;
        T: ^GraphicText;
        P: ^PieSlice;
        A: ^Arc;
        L: ^Line;
        M: ^Multiline;
     do		 
        blackpaint[] -> framepaint -> GoPaint;
	
        &GraphicalObject[] -> G[];
        G.init;
        (  0, 32) -> G.theShape.open;
        (  0, 10) -> G.theShape.lineto;
        (  5, 15) -> G.theShape.lineto;
        ( 11,  0) -> G.theShape.lineto;
        ( 15,  2) -> G.theShape.lineto;
        (  9, 16) -> G.theShape.lineto;
        ( 16, 16) -> G.theShape.lineto;
        G.theShape.close;
        G[] -> append;
        
        &Rect[] -> R[];
        R.init;
        ((0,30),(50,0)) -> R.corners;
        R[] -> append;
        
        &Ellipse[] -> E[];
        E.init;
        ((-10,200), 20, 30) -> E.geometry;
        E[] -> append;
        
        &NonCircularSplineSegment[] -> n[];
        (20,40) -> n.open;
        (20,25) -> n.addcontrol;
        (40,30) -> n.addcontrol;
        (50,10) -> n.addcontrol;
        (25, 0) -> n.addcontrol;
        0.5 -> n.smoothness;
        &GraphicalObject[] -> G[];
        G.init;
        (0, 15) -> G.theShape.open;
        (20,40) -> G.theShape.lineto;
        n[] -> G.theShape.addspline;
        (0 ,15) -> G.theShape.lineto;
        G.theShape.close;
        G[] -> append;
        
        &PieSlice[] -> P[];
        P.init;
        (0,0) -> P.center;
        40 -> P.horizontalradius;
        25 -> P.verticalradius;
        340 -> P.angle1;
        420 -> P.angle2;
        P[] -> append;
        
        &GraphicText[] -> T[];
        T.init;
        ((100,20),Times,Bold,34,false,'T') -> T.inittext;
        T[] -> append;
        
        &Line[] -> L[];
        L.init;
        ((0,0), (50,30)) -> L.coordinates;
        2 -> L.width;
        L[] -> append;
        
        &MultiLine[] -> M[];
        M.init;
        (0,20) ->  M.theShape.addpoint;
        (15,30) -> M.theShape.addpoint;
        (35,0) ->  M.theShape.addpoint;
        (40,18) -> M.theShape.addpoint;
        2 -> M.width;
        M[] -> append;      
        
        R.copy -> R[];
        true -> R.theshape.stroked;
        2 -> R.theshape.strokewidth;
        R[] -> append;
        
        E.copy -> E[];
        true -> E.theshape.stroked;
        2 -> E.theshape.strokewidth;
        E[] -> append;
        
        &Arc[] -> A[];
        A.init;
        (0,0) -> A.center;
        40 -> A.horizontalradius;
        25 -> A.verticalradius;
        340  -> A.angle1;
        420 -> A.angle2;
        2 -> A.arcwidth;
        A[] -> append;  
     #);
#)
