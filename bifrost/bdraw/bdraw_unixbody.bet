ORIGIN 'bdrawenv';
INCLUDE '../private/X11/CanvasXtImpl';
INCLUDE '~beta/guienv/v1.3/private/X11/guienv_unixbody';
INCLUDE '~beta/sysutils/v1.4/envstring';
INCLUDE 'menus';
INCLUDE 'dialogs';
INCLUDE 'cursors';

--LIB: attributes--
(* Process/v1.4 does not handle pipes too well, so we do it manually... *)
fputc: external
  (# c: @char;
     file: @integer;
     status: @integer;
  enter (c,file)
  exit status
  #);
popen: external
  (# command: [1]@char;
     type: [1]@char;
     file: @integer;
  enter (command,type)
  exit file
  #);
pclose: external
  (# file: @integer;
     status: @integer;
  enter file
  exit status
  #);
SIGPIPE: (# exit 13 #);
signal: external
  (# sig: @integer;
     handler: ##external;
  enter (sig, handler##)
  #);
EOF: 
  (# exit -1 #);
pipe: Stream
  (# command: ^text;
     file: @integer;
     open: 
       (# error:< exception;
       enter command[]
       do (command, 'w')->popen->file;
          (if file//0 then error if);
       #);
     close: 
       (# error:< exception;
       do (if file->pclose//-1 then error if) 
       #);
     putError:< exception;
     put:: (# do (if (ch,file)->fputc//EOF then putError if) #);
  #);

-- BdrawPrint: descriptor--
(# A4: (# exit (0, 0, 595, 822) #);
   oldSelection: @picture;
   printDialog: @PromptForText
     (# ok:: (# do usertext[]->extensions.printcommand[]; printEPS #)#);
   
   printEPS:
     (# 
     do SaveCursor;
        WatchCursor;
        print: 
          (# out: @pipe(# putError:: (# do forget #)#);
             forget:
               (# msg: ^text;
               do 'Bdraw: Print: Failed to pipe EPS to '''-> msg[];
                  extensions.printcommand[]->msg.puttext;
                  ''''->msg.puttext;
                  msg[] -> Note;
                  leave print;
               #);
          do false -> extensions.pipe_broken;
             extensions.printcommand[]->out.open(# error:: (# do forget #)#);
             
             (* If the selection has been moved or otherwise transformed, this 
              * transformation is only registered by the selection TM matrix.
              * Temporarily remove the GOs in theSelection to make the GOs
              * incorporate the transformations.
              *)
             oldSelection.init;
             theSelection.scanGOs(# do go[] -> oldSelection.add #);
             theSelection.clear;
             
             (* Produce the postscript on the stream 'out' *)
             (A4, true, 1, out[]) -> maincanvas.writeEPS;
             (* Restore selection *)
             oldSelection.scanGOs(# do go[] -> theSelection.add #);
             
             out.close(# error:: (# do forget #)#);
             
             (if extensions.pipe_broken then forget if);
             
          #);
        RestoreCursor;
     #);
do (if extensions.printcommand[]//NONE then
       '$(BDRAWPRINT)' -> expandEnvVar -> extensions.printcommand[];
       (if extensions.printcommand.length//0 then 
           'lpr' -> extensions.printcommand[] 
       if);
   if);
   (topwindow[], 'Bdraw Print', 'Print Command:', extensions.printcommand[]) 
     -> printdialog.popup;
#)

-- BdrawExtensions: descriptor --
(# printcommand: ^text;
   pipe_broken: @boolean;
   handleSigPipe: external
     (# sig: @integer;
     enter sig
     do CExternalEntry;
        (* Be carefull not to cause GC here! *)
        true->pipe_broken;
     #);
   
   dpy, root, bitmap, status: @integer;
   filename: ^text;
   width, height, bmap: @integerref;
   doiconify: 
     (# do (XtNiconic, true) -> topWindow.setIntegerResource #);
   menufonts: @MotifFontList
     (# addText:
          (* Add one entry to THIS(MotifFontList) *)
          (# font, charset: ^text;
             fontstruct: @integer;
          enter (font[], charset[])
          do (xdisplay, font) -> XLoadQueryFont -> fontstruct;
             (if value
              // 0 then
                 (fontstruct, @@charset.T[1])
                   -> XmFontListCreate -> value
              else
                 (value, fontstruct, @@charset.T[1])
                   -> XmFontListAdd -> value
             if)
          #);
     #);     
   menulabel: @MotifString
     (# setT:
          (# string, charset: ^text;
          enter (string[], charset[])
          do (string[], charset[], XmSTRING_DIRECTION_L_TO_R)  -> setTextSegment;
          #)
     #);
   menubar: ^menubarpattern;
do 
   (* Specify signal handler for SIGPIPE *)
   (SIGPIPE, handleSigPipe##) -> signal;

   (* Add iconify pattern *)
   doiconify## -> iconify##;
   
   (* Specify icon for toplevel *)
   ('~beta/bifrost/v2.0/bitmaps/mjoelner.xbm', '') 
     -> bifrostprivate.PH.convertFilePath
     -> filename[];
   xdisplay -> dpy -> XDefaultRootWindow -> root;
   (dpy,root,filename,width,height,bmap,0,0)
     -> XReadBitmapFile -> status;
   (if status
    // XBitmapOpenFailed then
       'Cannot open bitmap file '->puttext; filename[]->putline; 
    // XBitmapFileInvalid then
       filename[]->puttext;
       ' is not a valid bitmap file' -> putline;
    // XBitmapNoMemory then
       'Not enough memory to hold bitmap file '->puttext;
       filename[] -> putline;
    // XBitmapSuccess then
       bmap.value -> bitmap;
   if);
   (XtNiconPixmap, bitmap) -> topwindow.setIntegerresource;
   
   (* Specify fonts for Text submenus *)
   ('-*-COURIER-MEDIUM-R-*-120-*-ISO8859-1',   'courier')   -> menufonts.addText;
   ('-*-TIMES-MEDIUM-R-*-120-*-ISO8859-1',     'times')     -> menufonts.addText;
   ('-*-HELVETICA-MEDIUM-R-*-120-*-ISO8859-1', 'helvetica') -> menufonts.addText;
   ('-*-MEDIUM-R-*-120-*-ISO8859-1', 'plain')  -> menufonts.addText;
   ('-*-BOLD-R-*-120-*-ISO8859-1', 'bold')     -> menufonts.addText;
   ('-*-MEDIUM-I-*-120-*-ISO8859-1', 'italic') -> menufonts.addText;
   menulabel.init;
   topwindow.themenubar -> menubar[];
   
   ('Plain', 'plain') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.iplain.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Bold', 'bold') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.ibold.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Italic', 'italic') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.iitalic.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Times', 'times') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.itimes.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Helvetica', 'helvetica') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.ihelvetica.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Courier', 'courier') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.icourier.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);

   
#)

--InitCursors: descriptor--
(# 
do maincanvas.theCursor -> savedcursor.c[];
   (xdisplay,XC_ul_angle) -> XCreateFontCursor 
     -> ulcornercursor.ulc.private.id;
   (xdisplay,XC_lr_angle) -> XCreateFontCursor 
     -> lrcornercursor.lrc.private.id;
   (xdisplay,XC_hand2) -> XCreateFontCursor 
     -> lefthandcursor.lhc.private.id;
#)
