ORIGIN 'bdrawenv';
INCLUDE '../private/X11/CanvasXtImpl';
INCLUDE '~beta/process/v1.4/processmanager';
INCLUDE '~beta/process/v1.4/private/communication_unixbody.bet';
INCLUDE '~beta/sysutils/v1.4/envstring';
INCLUDE 'menus';

--LIB: attributes--
pipeWrapper: Stream
  (* Process/v1.4 does not handle pipes too well... *)
  (# myPipe: ^pipe;
     init: (# enter myPipe[] #);
     put:: (# do ch->myPipe.writeEnd.put #);
     close: (# do myPipe.private.writeCh.close #);
  #);

-- BdrawPrint: descriptor--
(# proc: @process;
   outpipe: ^pipe;
   out: @pipeWrapper;
   
   cmd: ^text;
   command: [5]^text; 
   argc: @integer;
   pos: @integer;
   A4: (# exit (0, 0, 595, 822) #);
   oldSelection: @picture;
do print: 
     (# forget:
          (# 
          do 'Bdraw: Print: Failed to start process '''->cmd.prepend;
             ''''->cmd.append;
             cmd[] -> Note;
             leave print;
          #);
     do
        '$(BDRAWPRINT)' -> expandEnvVar -> cmd[];
        (if cmd.length<>0 then
            1->pos; 1->argc;
            ' '->cmd.findCh
            (# 
            do (*inx -> putint; newline;*)
               (if inx>pos then
                   (if inx>command.range then command.range->command.extend if);
                   (pos,inx-1)->cmd.sub->command[argc][];
                   argc+1->argc;
               if);
               inx+1->pos;
            #);
            (if cmd.length>pos then (* No blanks *)
                (pos, cmd.length) -> cmd.sub ->command[argc][]
            if);
         else
            'lpr' -> command[1][];
        if);
        
        (* Set up a pipe. Not handled too well by process/v1.4... *)
        'Opening pipe to program '''->puttext;
        command[1][]->puttext;
        ''' with arguments'->putline;
        (for i:command.range-1 repeat
             (if command[i+1][]<>NONE then
                 command[i+1][]->putline;
             if);
        for);
        
        
        (* create someone to talk to *)
        command[1][] -> proc.init;
        (for i:command.range-1 repeat
             (if command[i+1][]<>NONE then
                 command[i+1][]->proc.argument.append;
             if);
        for);
        &pipe[]->outpipe[];
        outpipe.init;
        outpipe.readEnd[]->proc.redirectFromChannel(# error:: (# do forget #)#);
        proc.start
        (# error:: (# do forget #);
           twoCurrent:: (# do forget #);
        #);
        
        (* create a stream-type channel to it *)
        outpipe[] -> out.init;
        
        (* If the selection has been moved or otherwise transformed, this transformation
         * is only registered by the selection TM matrix.
         * Temporarily remove the GOs in theSelection to make the GOs
         * incorporate the transformations.
         *)
        oldSelection.init;
        theSelection.scanGOs(# do go[] -> oldSelection.add #);
        theSelection.clear;
        (* Produce the postscript on the stream 'out' *)
        (A4, true, 1, out[]) -> maincanvas.writeEPS;
        (* Restore selection *)
        oldSelection.scanGOs(# do go[] -> theSelection.add #);
        
        out.close;
        
        'EPS output piped to command ''' -> cmd.prepend;
        ''''->cmd.append;
        cmd[] -> Note;
     #);
#)

-- BdrawExtensions: descriptor --
(# dpy, root, bitmap, status: @integer;
   filename: ^text;
   width, height, bmap: @integerref;
   doiconify: 
     (# do (XtNiconic, true) -> topWindow.setIntegerResource #);
   menufonts: @MotifFontList
     (# addText:
          (* Add one entry to THIS(MotifFontList) *)
          (# font, charset: ^text;
             fontstruct: @integer;
          enter (font[], charset[])
          do (xdisplay, font) -> XLoadQueryFont -> fontstruct;
             (if value
              // 0 then
                 (fontstruct, @@charset.T[1])
                   -> XmFontListCreate -> value
              else
                 (value, fontstruct, @@charset.T[1])
                   -> XmFontListAdd -> value
             if)
          #);
     #);     
   menulabel: @MotifString
     (# setT:
          (# string, charset: ^text;
          enter (string[], charset[])
          do (string[], charset[], XmSTRING_DIRECTION_L_TO_R)  -> setTextSegment;
          #)
     #);
   menubar: ^menubarpattern;
do 
   (* Add iconify pattern *)
   doiconify## -> iconify##;
   
   (* Specify icon for toplevel *)
   ('~beta/bifrost/v2.0/bitmaps/mjoelner.xbm', '') 
     -> bifrostprivate.PH.convertFilePath
     -> filename[];
   xdisplay -> dpy -> XDefaultRootWindow -> root;
   (dpy,root,filename,width,height,bmap,0,0)
     -> XReadBitmapFile -> status;
   (if status
    // XBitmapOpenFailed then
       'Cannot open bitmap file '->puttext; filename[]->putline; 
    // XBitmapFileInvalid then
       filename[]->puttext;
       ' is not a valid bitmap file' -> putline;
    // XBitmapNoMemory then
       'Not enough memory to hold bitmap file '->puttext;
       filename[] -> putline;
    // XBitmapSuccess then
       bmap.value -> bitmap;
   if);
   (XtNiconPixmap, bitmap) -> topwindow.setIntegerresource;
   
   (* Specify fonts for Text submenus *)
   ('-*-COURIER-MEDIUM-R-*-120-*-ISO8859-1',   'courier')   -> menufonts.addText;
   ('-*-TIMES-MEDIUM-R-*-120-*-ISO8859-1',     'times')     -> menufonts.addText;
   ('-*-HELVETICA-MEDIUM-R-*-120-*-ISO8859-1', 'helvetica') -> menufonts.addText;
   ('-*-MEDIUM-R-*-120-*-ISO8859-1', 'plain')  -> menufonts.addText;
   ('-*-BOLD-R-*-120-*-ISO8859-1', 'bold')     -> menufonts.addText;
   ('-*-MEDIUM-I-*-120-*-ISO8859-1', 'italic') -> menufonts.addText;
   menulabel.init;
   topwindow.themenubar -> menubar[];
   
   ('Plain', 'plain') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.iplain.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
    
   ('Bold', 'bold') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.ibold.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
    
   ('Italic', 'italic') -> menulabel.setT;
   menubar.menus.TextMenu.istyle.Stylemenu.iitalic.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
    
   ('Times', 'times') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.itimes.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
    
   ('Helvetica', 'helvetica') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.ihelvetica.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);
   
   ('Courier', 'courier') -> menulabel.setT;
   menubar.menus.TextMenu.ifont.Fontmenu.icourier.scanMotifmenuItems
   (# 
   do (XmNfontList, menufonts)    -> current.setIntegerResource;
      (XmNlabelString, menulabel) -> current.setIntegerResource;
   #);

   
#)
