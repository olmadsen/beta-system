ORIGIN 'bdrawenv';
INCLUDE 'thepalette';
BODY 'actions' 'version' 'dialogs' 'canvasses' 'menus' 'cursors';
MDBODY mac 'bdraw_macbody' default 'bdraw_unixbody';

-- BdrawVariant: descriptor --
(# 
do '
   ******************************************************
   *  Bifrost Drawing Tool (Lidskjalv)         ' -> puttext;
   version;
   '  *' -> putline;   
#)
-- bdrawattributes: attributes --
ParseArguments:
  (* Parse command-line arguments *)
  (# n: @Integer;
     theArg: ^Text;
  do 2 -> n;
     loop: 
       (# getArg:
            (# another: @Boolean; 
               (* true means another argument is expected *)
            enter another
            do
               (if (n <= NoOfArguments) 
                // false then
                   (if another // true then
                       (failure, 'ERROR: Not enough arguments')
                         -> Stop;
                   if);
                   leave loop;
                else
                   n -> Arguments -> theArg[];
                   n+1 -> n;   
               if);
            #);
       do false -> getArg;
          (if true 
           // ('-w' -> theArg.equal) then
              (* line width specification *)
              true -> getArg;
              0 -> theArg.SetPos;
              theArg.getint -> myLineWidth; 
              (* Change menu *)
           // ('-d' -> theArg.equal) then
              true -> debuggraphic;
              'Debug output enabled (type M-D to disable)' -> putline;
              '------------------------------------------' -> putline;
          if);
          restart loop;  
       #)
  #);

-- OpenBdraw: descriptor --
(# logosize, colorsize, infosize, palettesize, mainsize: @point;
do
   'Bdraw Drawing Tool' -> topwindow.title;
   mySolidColor.init;
   
   Logo.open;
   (10,10) -> Logo.position;
   Logo.size -> logosize;
   
   ColorCanvas.open;
   (10, logosize.y+20) -> Colorcanvas.position;
   ColorCanvas.size -> colorsize;
   
   (NONE, 80, 50, true)->thepalette.open;
   (10, logosize.y+colorsize.y+30) -> thepalette.position;
   SelectMode -> State;
   thepalette.size -> palettesize;
   
   Inform.open;
   (palettesize.x+20, 4) -> Inform.position;
   Inform.size -> infosize;
   
   maincanvas.open;
   1 -> maincanvas.borderwidth;
   (palettesize.x+20, infosize.y+5) -> maincanvas.position;
   (infosize.x,logosize.y+colorsize.y+palettesize.y-infosize.y+30)
     -> mainsize -> maincanvas.size;
   
   (mainsize.x+palettesize.x+25, infosize.y+mainsize.y+10) -> topwindow.size; 
   
   &SelectionPicture[] -> theSelection[];
   maincanvas[] -> theSelection.init;
   &Picture -> clipboard[]; clipBoard.init;
   
   InitCursors;
   
   InitDialogs;
   
   Extensions;
   
   ParseArguments;
   
#)

-- PROGRAM: descriptor --
(# bdraw: @bdrawenv;
do bdraw;
#)
