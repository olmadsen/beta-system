ORIGIN 'bdrawenv';
INCLUDE 'thepalette';
INCLUDE '../private/Impl/debug';
BODY 'private/actions';
BODY 'private/version';
BODY 'private/dialogs';
BODY 'private/canvasses';
BODY 'private/menus';
BODY 'private/cursors';
MDBODY ppcmac  'private/Macintosh/bdraw_macbody'
       nti     'private/WinNT/bdraw_ntibody'
       default 'private/X11/bdraw_unixbody';

-- BdrawVariant: descriptor --
(# 
do newline;
   '   ******************************************************' -> putline;
   '   *  Bifrost Drawing Tool (Lidskjalv)         ' -> puttext;
   version;
   '  *' -> putline;   
#)
-- bdrawattributes: attributes --
ParseArguments:
  (* Parse command-line arguments *)
  (# usage:
       (# 
       do 'Usage:' -> putline;
          '  bdraw [-w linewidth] [filename]' -> putline;
          '        -w linewidth  Specifies initial line width' -> putline;
          stop;
       #);
     n: @Integer;
     theArg: ^Text;
  do 2 -> n;
     loop: 
       (# getArg:
            (# another: @Boolean; 
               (* true means another argument is expected *)
            enter another
            do
               (if (n > NoOfArguments) 
                   then
                   (if another then
                       'Missing argument. '->putText;
                       usage;
                   if);
                   leave loop;
                else
                   n -> Arguments -> theArg[];
                   n+1 -> n;   
               if);
            #);
       do false -> getArg;
          (if true 
           // ('-w' -> theArg.equal) then
              (* line width specification *)
              true -> getArg;
              0 -> theArg.SetPos;
              theArg.getint -> myLineWidth; 
              (* Change menu *)
	   else
	      theArg[] -> currentFileName[]
          if);
          restart loop;  
       #);
  #)

-- OpenBdraw: descriptor --
(# logosize, colorsize, palettesize, palettepos: @point;
do
   'Bdraw Drawing Tool' -> topwindow.setTitle;
   mySolidColor.init;
   
   Logo.open;
   (10,10) -> Logo.position;
   Logo.size -> logosize;
   
   ColorCanvas.open;
   (10, logosize.y+20) -> Colorcanvas.position;
   ColorCanvas.size -> colorsize;
   
   (NONE, 80, 50, true)->thepalette.open;
   (10, logosize.y+colorsize.y+30) -> thepalette.position;
   SelectMode -> State;
   thepalette.size -> palettesize;
   thepalette.position -> palettepos;
   
   (* Setting size of main window + canvas + scroller *)
   (765,720) -> topwindow.size;
   
   mainscroll.open
   (# 
   do (660,palettepos.y+palettesize.y) -> mainscroll.size;
      (palettepos.x+palettesize.x+10, 0) -> mainscroll.position;
   #);
   
   Inform.open;
   (* Position is calculated in Inform.eventhandler.onFrameChanged *)
   
   &MySelectionPicture[] -> theSelection[];
   maincanvas[] -> theSelection.init;
   &Picture -> clipboard[]; clipBoard.init;
   
   InitCursors;
   
   InitDialogs;
   
   Extensions;
   
   ParseArguments;
   1->maincanvas.borderwidth;
#)

--PositionMainCanvas: descriptor--
(# ss, cs, cp: @point;
do mainscroll.contents.size -> ss;
   maincanvas.size -> cs;
   (0,0) -> cp;
   (if ss.x>cs.x then (ss.x-cs.x) div 2 -> cp.x if);
   (if ss.y>cs.y then (ss.y-cs.y) div 2 -> cp.y if);
   cp -> maincanvas.position;
   'maincanvas: new position: ' -> puttext;
   cp -> putpoint;
#)

-- PROGRAM: descriptor --
(# bdraw: @bdrawenv;
do bdraw;
#)
