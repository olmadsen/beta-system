ORIGIN 'bdrawenv';
INCLUDE 'thepalette';
BODY 'private/actions';
BODY 'private/version';
BODY 'private/dialogs';
BODY 'private/canvasses';
BODY 'private/menus';
BODY 'private/cursors';
MDBODY ppcmac  'private/Macintosh/bdraw_macbody'
       nti     'private/WinNT/bdraw_ntibody'
       default 'private/X11/bdraw_unixbody';

-- BdrawVariant: descriptor --
(# 
do newline;
   '   ******************************************************' -> putline;
   '   *  Bifrost Drawing Tool (Lidskjalv)         ' -> puttext;
   version;
   '  *' -> putline;   
#)
-- bdrawattributes: attributes --
ParseArguments:
  (* Parse command-line arguments *)
  (# usage:
       (# 
       do 'Usage:' -> putline;
          '  bdraw [-w linewidth] [-d] [filename]' -> putline;
          '        -w linewidth  Specifies initial line width' -> putline;
          '        -d            Enables (volumenous) debug output' -> putline;
          stop;
       #);
     n: @Integer;
     theArg: ^Text;
  do 2 -> n;
     loop: 
       (# getArg:
            (# another: @Boolean; 
               (* true means another argument is expected *)
            enter another
            do
               (if (n > NoOfArguments) 
                   then
                   (if another then
                       'Missing argument. '->putText;
                       usage;
                   if);
                   leave loop;
                else
                   n -> Arguments -> theArg[];
                   n+1 -> n;   
               if);
            #);
       do false -> getArg;
          (if true 
           // ('-w' -> theArg.equal) then
              (* line width specification *)
              true -> getArg;
              0 -> theArg.SetPos;
              theArg.getint -> myLineWidth; 
              (* Change menu *)
           // ('-d' -> theArg.equal) then
              true -> debuggraphic;
              'Debug output enabled (type M-D to disable)' -> putline;
              '------------------------------------------' -> putline;
	   else
	      theArg[] -> currentFileName[]
          if);
          restart loop;  
       #);
  #)

-- OpenBdraw: descriptor --
(# logosize, colorsize, infosize, infopos, 
   palettesize, palettepos, mainsize: @point;
do
   'Bdraw Drawing Tool' -> topwindow.setTitle;
   mySolidColor.init;
   
   Logo.open;
   (10,10) -> Logo.position;
   Logo.size -> logosize;
   
   ColorCanvas.open;
   (10, logosize.y+20) -> Colorcanvas.position;
   ColorCanvas.size -> colorsize;
   
   (NONE, 80, 50, true)->thepalette.open;
   (10, logosize.y+colorsize.y+30) -> thepalette.position;
   SelectMode -> State;
   thepalette.size -> palettesize;
   thepalette.position -> palettepos;
   
   (* Setting size og main window + canvas + scroller *)
   (765,720) -> mainsize -> topwindow.size;
   
   mainscroll.open
   (# 
   do (660,palettepos.y+palettesize.y)
        ->mainscroll.size
        ->mainscroll.contents.size;
      (palettepos.x+palettesize.x+10, 0) -> mainscroll.position;
   #);
   mainscroll.contents[]->maincanvas[];
   
   Inform.open;
   (palettepos.x, palettepos.y+palettesize.y+10) -> Inform.position;
   Inform.size -> infosize;
   Inform.position -> infopos;
   
   &MySelectionPicture[] -> theSelection[];
   maincanvas[] -> theSelection.init;
   &Picture -> clipboard[]; clipBoard.init;
   
   InitCursors;
   
   InitDialogs;
   
   Extensions;
   
   ParseArguments;
   maincanvas.TM->originalTM.set;
   1->maincanvas.borderwidth;
   100->myZoomFactor;
   ZoomFactorChanged;
#)

-- PROGRAM: descriptor --
(# bdraw: @bdrawenv;
do bdraw;
#)
