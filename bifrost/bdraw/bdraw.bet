ORIGIN 'bdrawenv';
INCLUDE 'thepalette';
BODY 'actions' 'version' 'dialogs' 'canvasses' 'menus' 'cursors';
MDBODY mac     'bdraw_macbody'
ppc     'bdraw_macbody'
ppcmac  'bdraw_macbody'
nti     'bdraw_ntibody'
default 'bdraw_unixbody';

-- BdrawVariant: descriptor --
(# 
do newline;
   '   ******************************************************' -> putline;
   '   *  Bifrost Drawing Tool (Lidskjalv)         ' -> puttext;
   version;
   '  *' -> putline;   
#)
-- bdrawattributes: attributes --
ParseArguments:
  (* Parse command-line arguments *)
  (# usage:
       (# 
       do 'Usage:' -> putline;
          '  bdraw [-w linewidth] [-d] [filename]' -> putline;
          '        -w linewidth  Specifies initial line width' -> putline;
          '        -d            Enables (volumenous) debug output' -> putline;
          stop;
       #);
     n: @Integer;
     theArg: ^Text;
  do 2 -> n;
     loop: 
       (# getArg:
            (# another: @Boolean; 
               (* true means another argument is expected *)
            enter another
            do
               (if (n > NoOfArguments) 
                   then
                   (if another then
                       'Missing argument. '->putText;
                       usage;
                   if);
                   leave loop;
                else
                   n -> Arguments -> theArg[];
                   n+1 -> n;   
               if);
            #);
       do false -> getArg;
          (if true 
           // ('-w' -> theArg.equal) then
              (* line width specification *)
              true -> getArg;
              0 -> theArg.SetPos;
              theArg.getint -> myLineWidth; 
              (* Change menu *)
           // ('-d' -> theArg.equal) then
              true -> debuggraphic;
              'Debug output enabled (type M-D to disable)' -> putline;
              '------------------------------------------' -> putline;
	   else
	      theArg[] -> currentFileName[]
          if);
          restart loop;  
       #);
  #)

-- OpenBdraw: descriptor --
(# logosize, colorsize, infosize, palettesize, mainsize: @point;
do
   'Bdraw Drawing Tool' -> topwindow.setTitle;
   mySolidColor.init;
   
   Logo.open;
   (10,10) -> Logo.position;
   Logo.size -> logosize;
   
   ColorCanvas.open;
   (10, logosize.y+20) -> Colorcanvas.position;
   ColorCanvas.size -> colorsize;
   
   (NONE, 80, 50, true)->thepalette.open;
   (10, logosize.y+colorsize.y+30) -> thepalette.position;
   SelectMode -> State;
   thepalette.size -> palettesize;
   
   Inform.open;
   (palettesize.x+20, 4) -> Inform.position;
   Inform.size -> infosize;
   
   (* Setting size og main window + canvas + scroller *)
   (*(infosize.x,logosize.y+colorsize.y+palettesize.y-infosize.y+30)
     -> mainsize;*)
   (780,710) -> mainsize -> topwindow.size;
   
   mainscroll.open
   (# 
   do (660,660)->mainscroll.size->mainscroll.contents.size;
      (palettesize.x+20, infosize.y+5) -> mainscroll.position;
   #);
   mainscroll.contents[]->maincanvas[];
   
   &MySelectionPicture[] -> theSelection[];
   maincanvas[] -> theSelection.init;
   &Picture -> clipboard[]; clipBoard.init;
   
   InitCursors;
   
   InitDialogs;
   
   Extensions;
   
   ParseArguments;
   maincanvas.TM->originalTM.set;
   3->maincanvas.borderwidth;
#)

-- PROGRAM: descriptor --
(# bdraw: @bdrawenv;
do bdraw;
#)
