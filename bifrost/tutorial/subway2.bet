ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/PredefinedGO';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* Add the map *)
   
   theWindow: @window
     (# 
        aarhus: @TiledSolidColor
          (# bits: @bitMap
               (# 
               do 'Reading in map ... '->puttext;
                  '~beta/bifrost/v2.0/bitmaps/Aarhus.pbm'->readFromPBMfile;
                  'done'->putline;
                  (0,0)->hotspot;
               #);   
             
             init:: 
               (# 
               do black->name;
                  bits->thetile;
               #)
          #);
        
        myCanvas: @BifrostCanvas
          (# 
             map: @Rect
               (# 
                  init:: 
                    (# 
                    do (* Make THIS(Rect) the size of the
                        * aarhus-bitmap
                        *)
                       aarhus.init;
                       (0,aarhus.bits.height)->upperleft;
                       aarhus.bits.width->width;
                       aarhus.bits.height->height;
                       aarhus[]->setpaint;
                    #)
               #);
             
             open:: 
               (# 
               do (* Make THIS(BifrostCanvas) the size of the map *)
                  map.init;
                  (map.width, map.height)->Size->theWindow.size;
               #);
             
             eventhandler:: 
               (# 
                  onOpen:: (# do map[]->draw; #);
                  
                  onMouseDown:: (# (* ... *) #);
                  
                  onKeyDown:: 
                    (# 
                    do (if ch
                        //'Q' then Terminate
                       if)
                    #);
               #);
          #);
        open:: 
          (# 
          do (* Initialize and open the BifrostCanvas *)
             myCanvas.open
          #)
     #) (* theWindow *)
do theWindow.open;
#)
