ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/PredefinedGO';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This example shows how to produce hatching/patterning effects using
    * TiledSolidColor.
    * The Graphictext "Dimmed" appears dimmed because only half the pixels
    * are setin the bitmap of the TiledSolidColor used. Of course the proper
    * way to obtain this effect is by decreasing the saturation of a SolidColor
    * but on e.g. a Black & White output device, the approach shown here will
    * often be more useful.
    *) 
   theWindow: @window
     (# myCanvas: @BifrostCanvas
          (# go1, go2: @Ellipse;
             t1, t2: ^GraphicText;
	     aSolidColor: ^SolidColor;
             aTiledSolidColor: ^TiledSolidColor;

             bitmap1: @bitMap
               (# mybit: @pixel;
	          putbit: @PutPixel;
               do (8,8)->init;
                  true->mybit;
                  (for i: 4 repeat
                       (for j: 4 repeat
 		            (  i,   j, mybit[])->Putbit;
		            (i+4, j+4, mybit[])->Putbit
 		       for)
                  for);

                  false->mybit;
                  (for i: 4 repeat
                       (for j: 4 repeat
 		            (i+4,   j, mybit[])->Putbit;
		            (  i, j+4, mybit[])->Putbit; 
		       for)
                  for);
               #);

             bitmap2: @bitMap
               (# mybit: @pixel;
               do (2,2)->init;
                  true->mybit; 
                  (1, 1, mybit[])->PutPixel;
                  (2, 2, mybit[])->PutPixel;

                  false->mybit;
                  (1, 2, mybit[])->PutPixel;
                  (2, 1, mybit[])->PutPixel;
               #);
             eventhandler:: 
               (# onOpen:: 
	            (# 
	            do &SolidColor[]->aSolidColor[];
                       aSolidColor.init;
	               red->aSolidColor.Name;

                       go1.init;
                       ((150,150),100,75)->go1.geometry;
                       aSolidColor[]->go1.setPaint;
                       go1[]->draw;

                       &TiledSolidColor[]->aTiledSolidColor[];
                       aTiledSolidColor.init;
	               green->aTiledSolidColor.Name;
                       bitmap1->aTiledSolidColor.theTile;

                       go2.init;
                       ((150,150),50,100)->go2.geometry;
                       aTiledSolidColor[]->go2.setPaint;
                       go2[]->draw;

                       &SolidColor[]->aSolidColor[];
                       aSolidColor.init;
	               magenta->aSolidColor.Name;
                       
	               &GraphicText[]->t1[];
	               t1.init;
	               aSolidColor[]->t1.setpaint;
	               ((50,255), Times, Italic, 34, false, 'Bright')->t1.inittext;
	               t1[]->draw;

                       &TiledSolidColor[]->aTiledSolidColor[];
                       aTiledSolidColor.init;
	               magenta->aTiledSolidColor.Name;
                       bitmap2->aTiledSolidColor.theTile;

	               &GraphicText[]->t2[];
	               t2.init;
	               aTiledSolidColor[]->t2.setpaint;
	               ((150,255), Times, Italic, 34, false, 'Dimmed')->t2.inittext;
	               t2[]->draw;
                    #);
                  onMouseDown::
                    (# do terminate #);
               #) (* eventhandler *);
          #) (* myCanvas *);
        open:: 
          (# 
          do 4->myCanvas.Borderwidth;
             myCanvas.open;
             ((300,300)->myCanvas.Size, (8,8))->addpoints->size;
          #);
     #) (* theWindow *);
do theWindow.open;
#)

