ORIGIN '~beta/bifrost/v2.2/Bifrost';
INCLUDE '~beta/bifrost/v2.2/ColorNames';
INCLUDE '~beta/bifrost/v2.2/PredefinedGO';
-- program: descriptor --
bifrost
(# (* Simple demo showing some manipulation with the
    * predefined graphical object Line.
    *)
   theWindow: @window
     (# bluepaint, redpaint: @solidcolor;
	theCanvas: @BifrostCanvas
	  (# eventhandler::
	       (# ML: @MultiLine; p: ^pointArray;
		  onKeyDown::
		    (# do terminate #);
		  onMouseDown::
		    (# exactPos: ^point;
		    do mousePos -> DeviceToCanvas -> mousePos;
		       (ML[], mousePos)-> HitControl -> exactpos[];
		       (if exactpos[] <> NONE then
			   ML[] -> unhilite;
			   (ML[], exactPos, ShiftModifier)
			     -> interactiveReshape;
			   ML[] -> hilite;
			else
			   (if (theCanvas[],mousePos) -> ML.containsPoint then
			       (if doubleClick then
				   ML[] -> unhilite;
				   mousepos->p.addPoint;
				   p[]->ML.points;
				   ML[]->draw;
				   ML[] -> hilite;
				else
				   ML[] -> unhilite;
				   (ML[], mousePos, NoModifier)
				     -> interactiveMove;
				   ML[] -> hilite;
			       if)
			   if)
		       if)
		    #);
		  onOpen::
		    (#
		    do ML.init;
		       &pointArray[]->p[];
		       3->p.initPoints;
		       (0,0) -> p.addPoint;
		       (20,100) -> p.addPoint;
		       (150,150) -> p.addPoint;
		       p[] -> ML.points;
		       8 -> ML.width;
		       bluepaint[] -> ML.setpaint;
		       ML[] -> hilite;
		       ML[] -> draw;
		    #)
	       #);
	     open::
	       (#
	       do (200,200) -> size;
		  true->bindright;
		  true->bindbottom;
		  true->bindtop;
		  true->bindleft;
	       #);
	  #);
	eventhandler:: (# onAboutToClose:: (# do terminate #) #);
	open::
	  (#
	  do (200,200)->size;
	     redpaint.init;
	     red -> redpaint.name;
	     bluepaint.init;
	     blue -> bluepaint.name;
	     theCanvas.open;
	  #);
     #);
do theWindow.open;
   1->Arguments->theWindow.title;
#)
