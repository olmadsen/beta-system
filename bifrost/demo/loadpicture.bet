ORIGIN '~beta/bifrost/v2.2/Bifrost';
INCLUDE '~beta/bifrost/v2.2/PredefinedGO'
	'~beta/bifrost/v2.2/EPSfile'
        '~beta/basiclib/v1.6/file';

(* Example showing how to to read a PostScript file (saved by
 * storepicture.bet) into a Picture.
 *)

--- PROGRAM: descriptor ---
bifrost
  (# 
     theWindow: @window
       (# 
	  myCanvas: @BifrostCanvas
	    (# 
               square: Rect
                 (# 
                    readUserData::
                      (#
                      do 'userdata is: \'' -> screen.puttext;
                         userdata.getLine -> screen.puttext;
                         '\'' -> screen.putline;
                      #)
                 #);
	       open:: 
		 (# 
		 do (700, 700) -> size -> theWindow.size;
		 #);
	       eventHandler::
		 (# 
		    onKeyDown::
		      (# 
                         aFile: @file;
                      do 
			 (if ch
                          // 'R' then 
			     'image.ps' -> aFile.name;
			     aFile.openRead;
                             aFile.skipEPSheaders;
                             aFile[] -> 
                             loadPicture
			       (# 
				  createGO::
				    (#
				    do (if true
					// 'image' -> patternName.equal then
					   &picture[] -> GO[]
					// 'square' -> patternName.equal then
					   &square[] -> GO[]
				       if)
				    #);
   
			       #) -> thePicture[];
                             myCanvas[] -> thePicture.draw;
                             aFile.close;
                          // 'Q' then
			     Terminate 
			 if) 
		      #)
		 #)
	    #);
	  open:: 
	    (# 
	    do myCanvas.open;
	    #)
       #)
  do theWindow.open;
     'Type \'R\' to load image.ps or \'Q\' to quit' -> screen.putline;
  #)
