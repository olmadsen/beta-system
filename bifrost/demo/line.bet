ORIGIN '~beta/bifrost/v2.2/Bifrost';
INCLUDE '~beta/bifrost/v2.2/ColorNames';
INCLUDE '~beta/bifrost/v2.2/PredefinedGO';
-- program: descriptor --
bifrost
(# (* Simple demo showing some manipulation with the
    * predefined graphical object Line.
    *)
   theWindow: @window
     (# bluepaint, redpaint: @solidcolor;
        theCanvas: @BifrostCanvas
          (# eventhandler::
               (# L: @Line;
                  onKeyDown::
                    (# do terminate #);
                  onMouseDown::
                    (# exactPos: ^point;
                    do mousePos -> DeviceToCanvas -> mousePos;
                       (L[], mousePos)-> HitControl -> exactpos[];
                       (if exactpos[] <> NONE then
                           L[] -> unhilite;
                           (L[], exactPos, ShiftModifier)
                             -> interactiveReshape;
                           L[] -> hilite;
                        else
                           (if (theCanvas[],mousePos) -> L.containsPoint then
                               L[] -> unhilite;
                               (L[], mousePos, NoModifier)
                                 -> interactiveMove;
                               L[] -> hilite;
                           if);
                       if);
                    #);
                  onOpen::
                    (# 
                    do L.init;
                       (20,20) -> l.begin;
                       (180,180) -> l.end;
                       8 -> l.width;
                       bluepaint[] -> l.setpaint;
                       l[] -> hilite;
                       l[] -> draw;
                    #)
               #);
             open:: 
               (# 
               do (200,200) -> size;
                  true->bindright;
                  true->bindbottom;
                  true->bindtop;
                  true->bindleft;
               #);
          #);
        open::
          (# 
          do (208,208)->size;
             4->theCanvas.borderwidth;
             redpaint.init;
             red -> redpaint.name;
             bluepaint.init;
             blue -> bluepaint.name;
             redpaint -> thecanvas.borderpaint;
             theCanvas.open;
          #);
     #);
do theWindow.open;
   1->Arguments->theWindow.title;
#)
