ORIGIN '~beta/bifrost/v2.1/Bifrost';
INCLUDE '~beta/bifrost/v2.1/PredefinedGO';
INCLUDE '~beta/bifrost/v2.1/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This is the program used to produce the frontpage of the thesis
    * 
    * "Bifrost 
    *   - An Interactive Object Oriented Device Independent Graphics System"
    * (DAIMI IR-100 by Peter Andersen, Kim Jensen Moller, Jorgen Rask).
    * 
    * The program uses many different graphical objects, shows various 
    * transformations of these, and it also shows how to use milimeters as
    * the drawing unit in the BifrostCanvas.
    *) 
   
   theWindow: @window
     (# canvasbackgroundcolor: @SolidColor;

        myCanvas: @BifrostCanvas
          (# anArc: ^Arc;
	     delta: @point;
             anEllipse: ^Ellipse;
             aSolidColor: ^SolidColor;
	     center,radius, p: @Point;
	     a1, a2: @real;
	     c1, c2: ^GraphicalObject;
	     h,s,v: @integer;
	     r: @rectangle;
 	     gt: ^GraphicText;          

	     cloud: @Shape
	       (# s: ^NonCircularSplineSegment;
	       do &NonCircularSplineSegment[]->s[];
                  (25,30)->s.open;
	          ( 0,65)->s.addcontrol;
	          (30,75)->s.addcontrol;
	          s[]->addspline;
	          &NonCircularSplineSegment[]->s[];
	          (30, 75)->s.open;
	          (35,100)->s.addcontrol;
	          (60,105)->s.addcontrol;
	          (75, 80)->s.addcontrol;
	          s[]->addspline;
	          &NonCircularSplineSegment[]->s[];
                  ( 75, 80)->s.open;
	          ( 90,100)->s.addcontrol;
	          (110, 90)->s.addcontrol;
	          (105, 70)->s.addcontrol;
	          s[]->addspline;
	          &NonCircularSplineSegment[]->s[];
                  (105,70)->s.open;
	          (140,50)->s.addcontrol;
	          (115,25)->s.addcontrol;
	          s[]->addspline;
	          &NonCircularSplineSegment[]->s[];
                  (115,25)->s.open;
	          (110,10)->s.addcontrol;
	          ( 75,20)->s.addcontrol;
	          s[]->addspline;
	          &NonCircularSplineSegment[]->s[];
                  (75,20)->s.open;
	          (60, 0)->s.addcontrol;
	          (25, 0)->s.addcontrol;
	          (25,30)->s.addcontrol;
	          s[]->addspline;
	          close;
               #);
             eventhandler:: 
               (# 
                  onOpen:: 
                    (# 
                    do (* The rainbow *)
	               (150,120)->MMtoPixel->center;
	               (120,120)->MMtoPixel->radius;
	               (1,1)->MMtoPixel->delta;
 	               90->a1;
	               190->a2;
	               
	               (for h: 34 repeat
	                    &Arc[]->anArc[];
                            anArc.init;
	                    center->anArc.center;
	                    radius.x->anArc.horizontalradius;
	                    radius.y->anArc.verticalradius;
	                    delta.x->anArc.arcwidth;
	                    a1->anArc.angle1;
	                    a2->anArc.angle2;
	                    &SolidColor[]->aSolidColor[]->anArc.setPaint;
	                    (10*h-6,DefaultMaxSat,DefaultMaxVal)
                              ->aSolidColor.HSVvalues;
	                    anArc[]->draw;
	                    (radius, delta)->subpoints->radius;
	               for);
	               
	               (* The clouds *)
	               &GraphicalObject[]->c1[]; c1.init;
	               &SolidColor[]->aSolidColor[]->c1.setPaint;
	               white->aSolidColor.Name;
	               cloud->c1.setShape;
                       (* The cloud shape is defined using pixels.
                        * Scale it to be 25 mm high
                        *)
                       (# ratio: @real;
                       do c1.getbounds->r;
                          (r.width,r.height)->PixelToMM->p;
                          25.0/p.x->ratio;
                          (ratio, ratio)->c1.scale;
                       #);
                       (15,205)->MMToPixel->c1.move;
	               c1[]->draw;

                       c1.copy -> c2[];
                       (* Move it back *)
                       (-15,-205)->MMToPixel->c2.move;
                       (* Scale up *)
                       (3.0, 2.5)->c2.scale;
                       (* Move to right place *)
                       (110,195)->MMToPixel->c2.move;
	               c2[]->draw;

                       (* The Earth *)
	               &Ellipse[]->anEllipse[];
	               anEllipse.init;
	               (100,-105)->MMtoPixel->center;
	               (230,230)->MMtoPixel->radius;
	               (center, radius.x, radius.y)->anEllipse.theShape.geometry;
	               &SolidColor[]->aSolidColor[]->anEllipse.setPaint;
	               saddlebrown->aSolidColor.Name;
	               aSolidColor.hsvvalues->(h,s,v);
	               (h,s div 3,v)->aSolidColor.hsvvalues;
	               anEllipse[]->draw;
	               
	               (* Title *)
	               &SolidColor[]->aSolidColor[];
	               black->aSolidColor.Name;
	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[]->gt.setpaint;
	               ((135,180)->MMtoPixel, Times, Bold, 34, false, 'Bifrost')
                         ->gt.inittext;
	               gt.getbounds->r;
	               (-r.width div 2 , 0)->gt.move;
	               gt[]->draw;
                       
	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[]->gt.setpaint;
	               ((135,170)->MMtoPixel, Times, Bold, 20, false, 'An')
                         -> gt.inittext;
	               gt.getbounds->r;
	               (-r.width div 2 , 0)->gt.move;
	               gt[]->draw;
   	               
	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[]->gt.setpaint;
	               ((135,162)->MMtoPixel, Times, Bold, 20, false, 
                       'Interactive') -> gt.inittext;
	               gt.getbounds->r;
	               (-r.width div 2 , 0)->gt.move;
	               gt[]->draw;
   	               
	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[] -> gt.setpaint;
	               ((135,154)->MMtoPixel, Times, Bold, 20, false, 
                       'Object Oriented') -> gt.inittext;
	               gt.getbounds->r;
	               (-r.width div 2 , 0)->gt.move;
	               gt[]->draw;
	               
	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[]->gt.setpaint;
	               ((135,146)->MMtoPixel, Times, Bold, 20, false, 
                       'Device Independent') -> gt.inittext;
	               gt.getbounds->r;
	               (-r.width div 2 , 0)->gt.move;
	               gt[]->draw;
	               
	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[]->gt.setpaint;
	               ((135,138)->MMtoPixel, Times, Bold, 20, false, 
                       'Graphics System') -> gt.inittext;
	               gt.getbounds->r;
	               (-r.width div 2 , 0)->gt.move;
	               gt[]->draw;

                       (* Authors *)
	               &SolidColor[]->aSolidColor[];
	               gold->aSolidColor.Name;
	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[]->gt.setpaint;
	               ((100,80)->MMtoPixel, Times, Bold, 25, false, 
                       'Peter Andersen') -> gt.inittext;
	               gt.getbounds->r;
	               (-(r.width div 2), 0)->gt.move;
	               gt[]->draw;
	   	       
	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[]->gt.setpaint;
	               ((100,67)->MMtoPixel, Times, Bold, 25, false, 
                       'Kim Jensen M\370ller')
	               (* \370: Latin-1 encoding of danish oring *)
	                 -> gt.inittext;
	               gt.getbounds->r;
	               (-(r.width div 2), 0)->gt.move;
	               gt[]->draw;

	               &GraphicText[]->gt[];
	               gt.init;
	               aSolidColor[]->gt.setpaint;
	               ((100,54)->MMtoPixel, Times, Bold, 25, false, 
                       'J\370rgen Rask')
                       (* \370: Latin-1 encoding of danish oring *)
	                 -> gt.inittext;
	               gt.getbounds->r;
	               (-(r.width div 2), 0)->gt.move;
	               gt[]->draw;
                    #);

                  onMouseDown:: 
                    (# do terminate #);
               #) (* eventhandler *)
          #) (* myCanvas *);
        open:: 
          (# do lightblue->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->myCanvas.backgroundpaint;

             myCanvas.open;
             (200,250)->myCanvas.MMtoPixel->size->myCanvas.Size;
          #);
     #) (* theWindow *)
do theWindow.open;
   1->Arguments->theWindow.title;
#)

