ORIGIN '~beta/bifrost/v2.1/Bifrost';
INCLUDE '~beta/bifrost/v2.1/PredefinedGO';
INCLUDE '~beta/bifrost/v2.1/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This demo shows how to make a colorscale, using the HSV color model.
    * This is done by stacking thin Lines upon each other, having hues 
    * 1, 2, ..., DefaultMaxHue respectively (i.e. DefaultMaxHue Lines in all).
    * Full Saturation and Value are used.
    *) 
   theWindow: @window
     (# canvasbackgroundcolor, canvasbordercolor: @SolidColor;

        rgbcmyCanvas: @BifrostCanvas
          (# aLine: ^Line;
	     aSolidColor, anotherSolidColor: ^SolidColor;
             eventhandler:: 
               (# onOpen:: 
                    (# puti: @(# enter screen.putint do ' '->screen.put #);
                    do (for h: (DefaultMaxHue div 2) repeat
		            &Line[]->aLine[];
                            aLine.init;
		            2->aLine.width;
		            ((10, 10+2*h),(110,10+2*h))->aLine.Coordinates;
		            &SolidColor[]->aSolidColor[]->aLine.thePaint[];
                            
		            (2*h, DefaultMaxSat, DefaultMaxVal)
                              -> aSolidColor.HSVvalues;
		            aLine[]->draw;
                            
		            &Line[]->aLine[];
                            aLine.init;
                            2->aLine.width;
                            ((120, 10+2*h),(220,10+2*h))->aLine.Coordinates;
                            &SolidColor[]->anotherSolidColor[]->aLine.setPaint;
                            
                            aSolidColor.RGBvalues->anotherSolidColor.CMYvalues;
                            aLine[]->draw;
                       for);
                    #);

                  onMouseDown:: 
                    (# do terminate #);
               #) (* eventhandler *);
          #) (* rgbcmyCanvas *);
        open:: 
          (# 
          do 2->rgbcmyCanvas.Borderwidth;
             lightblue->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->rgbcmyCanvas.backgroundpaint;
             violet->canvasbordercolor.Name;
             canvasbordercolor[]->rgbcmyCanvas.borderpaint;

             rgbcmyCanvas.open;
             ((230,380)->size, (4,4))->SubPoints->rgbcmyCanvas.Size;
          #);
     #) (* theWindow *);
do theWindow.open;
   1->Arguments->theWindow.title;
#)

