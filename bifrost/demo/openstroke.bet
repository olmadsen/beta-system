ORIGIN '~beta/bifrost/v2.1/Bifrost';
INCLUDE '~beta/bifrost/v2.1/PredefinedGO';
INCLUDE '~beta/bifrost/v2.1/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This example show how the Stroke operation of Shape works on an
    * open Shape
    *) 
   theWindow: @window
     (# canvasbackgroundcolor: @SolidColor;

        myCanvas: @BifrostCanvas
          (# aSolidColor: @SolidColor;

             myStroke: @Shape
               (# 
               do (100,100)->open;
                  (150, 50)->lineTo;
                  (200,100)->lineTo;
                  (250, 50)->lineTo;
                  (300,100)->splineTo;
                  (350, 50)->splineTo;
                  (400,100)->splineTo;
                  (20,CapButt,JoinMiter)->stroke;
               #);
             eventhandler:: 
               (# onOpen:: 
	            (# myGO: @GraphicalObject;
	            do aSolidColor.init;
                       blue->aSolidColor.Name;
                       myGO.init;
                       aSolidColor->myGO.setPaint;
                       myStroke->myGO.setShape;
                       myGO[]->draw;
                    #);
                  onMouseDown:: 
                    (# do terminate #);
               #) (* eventhandler *);
          #) (* myCanvas *);
        open:: 
          (# 
          do 4->myCanvas.Borderwidth;
             canvasbackgroundcolor.init;
             orange->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->myCanvas.backgroundPaint;
             myCanvas.open;
             ((500,150)->size, (8,8)) -> SubPoints -> myCanvas.Size;
          #);
     #)
do theWindow.open;
#)

