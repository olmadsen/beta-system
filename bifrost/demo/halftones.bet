ORIGIN '~beta/bifrost/v2.1/Bifrost';
INCLUDE '~beta/bifrost/v2.1/RasterGrays';
INCLUDE '~beta/bifrost/v2.1/PredefinedGO';
INCLUDE '~beta/bifrost/v2.1/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This is a simple example of using RasterGrays to obtain several gray 
    * tones even on Black & White devices.
    *)
   theWindow: @window
     (# grays: @RasterGrays;  
        myCanvas: @BifrostCanvas
          (# eventhandler:: 
               (# onOpen:: 
                    (# e, border: ^Ellipse;
                       blackpaint: @SolidColor;
                    do blackpaint.init;
                       black->blackpaint.name (* for the borders *);
                       (for i: 11 repeat
                            &Ellipse[]->e[]; e.init;
                            &Ellipse[]->border[]; border.init;
                            (((i-1)*50+30,100),20,20)
                              -> e.geometry
                              -> border.geometry;
                            true->border.theshape.stroked;
                            10*(i-1)->grays->e.setPaint;
                            blackpaint->border.setpaint;
                            e[]->draw;
                            border[]->draw;
                       for);
                    #);
                  onMouseDown::
                    (# do terminate #);
               #) (* eventhandler *)
          #) (* myCanvas *);
        open:: 
          (# 
          do grays.init;
             4->myCanvas.Borderwidth;
             myCanvas.open;
             ((560,200)->myCanvas.size, (8,8)) -> addpoints -> size;
          #) 
     #)(* theWindow *)
do theWindow.open;
   1->Arguments->theWindow.title;
#)

