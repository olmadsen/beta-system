ORIGIN '~beta/bifrost/v2.2/Bifrost';
INCLUDE '~beta/bifrost/v2.2/PredefinedGO'
	'~beta/bifrost/v2.2/EPSfile'
        '~beta/basiclib/v1.6/file';

(* Example showing how to to save a Picture on file for later
 * restoring (see loadpicture.bet).
 *)
--- PROGRAM: descriptor ---
bifrost
  (# 
     theWindow: @window
       (# 
	  image: @Picture
	    (# 
	       init::
		 (#
                    aGO: ^Ellipse;
		    aSolidColor: ^solidColor;
		 enter size
		 do 
		    square.init;
		    square[] -> add;
                    &ellipse[] -> aGO[];
                    aGO.init;
		    &SolidColor[] -> aSolidColor[];
		    (65280, 0, 0) -> aSolidColor.RGBvalues;
		    aSolidColor[] -> aGO.setPaint;
                    true->aGO.theShape.stroked;
                    (350,350) -> aGO.center;
                    300 -> aGO.horizontalRadius;
                    200 -> aGO.verticalRadius;
                    aGO[] -> add;
		 #)
	    #);
	  square: @Rect
	    (# 
               writeUserData::
                 (# 
                 do 'Adding userdata \'% USERDATA\'' -> screen.putline;
                    '% USERDATA' -> userdata.putLine;
                 #);
	       init::
		 (#
		    aSolidColor: ^solidColor;
		 do
		    &SolidColor[] -> aSolidColor[];
		    (65280, 0, 0) -> aSolidColor.RGBvalues;
		    aSolidColor[] -> setPaint;
		    (10, 110) -> upperleft; 
		    100 -> width; 
		    100 -> height
		 #)
	    #);
	  myCanvas: @BifrostCanvas
	    (# 
	       open:: 
		 (# 
		 do (700, 700) -> size -> theWindow.size ->
		    image.init;
		 #);
	       eventHandler::
		 (# 
		    onOpen::
		      (# 
		      do image[] -> draw;
		      #);
		    onKeyDown::
		      (# 
                         A4: (# exit (0, 0, 595, 822) #);
                         aFile: @file;
                      do 
			 (if ch='Q' then 
			     'image.ps' -> aFile.name;
			     aFile.openWrite;
                             aFile.startEPSfile;
                             aFile[] -> thePicture.writePS;
                             aFile.endEPSfile;
                             aFile.close;
			     Terminate 
			 if) 
		      #);
		 #);
	    #);
	  open:: 
	    (# 
	    do myCanvas.open;
	    #)
       #)
  do theWindow.open;
     'Type \'Q\' to quit the application and write PostScript to image.ps'
       -> putline;
  #)
