ORIGIN '~beta/bifrost/v2.2/Bifrost';
INCLUDE	'~beta/bifrost/v2.2/EPSfile'
        '~beta/basiclib/v1.6/file'
	'squarelib';

(* Example showing how to to save a Picture on file for later
 * restoring (see loadpicture.bet).
 *)
--- PROGRAM: descriptor ---
bifrost
  (#
     theWindow: @window
       (#
	  theImage: ^image;
	  myCanvas: @BifrostCanvas
	    (#
	       open::
		 (#
		 do
		    &image[] -> theImage[];
		    (700, 700) -> size -> theWindow.size
		      -> theImage.init
		 #);
	       eventHandler::
		 (#
		    onOpen::
		      (#
		      do theImage[] -> draw;
		      #);
		    onKeyDown::
		      (#
                         A4: (# exit (0, 0, 595, 822) #);
                         aFile: @file;
                      do
			 (if ch='q' then
			     'image.ps' -> aFile.name;
			     aFile.openWrite;
                             aFile.startEPSfile;
                             ((0,0,595,822),true,1,aFile[])
			       -> myCanvas.writeEPS;
                             aFile.endEPSfile;
                             aFile.close;
			     Terminate
			 if)
		      #);
		 #);
	    #);
	  open::
	    (#
	    do myCanvas.open;
	    #)
       #)
  do theWindow.open;
     'Type \'q\' to quit the application' -> putText;
     ' and write PostScript to image.ps' -> putline;
  #)
