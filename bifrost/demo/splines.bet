ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* Example splines from section "3.4.3 SplineSegment" from the Bifrost thesis *)
   theWindow: @window
     (# 
        canvasbordercolor: @SolidColor;
        canvasbackgroundcolor: @SolidColor;

        myCanvas: @BifrostCanvas
          (# 
             go1, go2, go3, go4: @GraphicalObject;
	     aSolidColor: @SolidColor;
             aBow: @NonCircularSplineSegment
               (# 
               do (  0,100)->open;
                  (100,100)->AddControl;
                  (100,  0)->AddControl;
                  (  0,  0)->AddControl;
               #);
             aDrop: @NonCircularSplineSegment
               (# 
               do (  0,100)->open;
                  (100,100)->AddControl;
                  (100,  0)->AddControl;
                  (  0,  0)->AddControl;
                  close;
               #);
             aSpline: @CircularSplineSegment
               (# 
               do (  0,100)->open;
                  (100,100)->AddControl;
                  (100,  0)->AddControl;
               #);
             anotherSpline: @CircularSplineSegment
               (# 
               do (  0,100)->open;
                  (100,100)->AddControl;
                  (100,  0)->AddControl;
                  (  0,  0)->AddControl;
               #);
             eventhandler:: 
               (# 
	          onOpen:: 
	            (# 
	            do aSolidColor.init;
	               pink->aSolidColor.Name;
	               go1.init;
	               aBow->go1.theShape.addSpline;
                       (* A shape must be closed before it can be filled*)
	               go1.theshape.close;
	               aSolidColor[]->go1.setpaint;
                       (30,160)->go1.move;
	               go1[]->draw;
	               go1[]->hilite;

	               go2.init;
	               aDrop->go2.theShape.addSpline;
                       (* Since aDrop is closed, go2.theShape is also closed *)
	               aSolidColor[]->go2.setpaint;
                       (160,160)->go2.move;
	               go2[]->draw;
	               go2[]->hilite;

	               go3.init;
	               aSpline->go3.theShape.addSpline;
	               aSolidColor[]->go3.setpaint;
                       (30,30)->go3.move;
	               go3[]->draw;
	               go3[]->hilite;

	               go4.init;
	               anotherSpline->go4.theShape.addSpline;
	               aSolidColor[]->go4.setpaint;
                       (160,30)->go4.move;
	               go4[]->draw;
	               go4[]->hilite;
                    #);
	          onMouseDown:: 
                    (# do terminate #);
               #) (* eventhandler *);
          #) (* myCanvas *);
        open:: 
          (# 
          do 4->myCanvas.Borderwidth;
             aliceblue->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->myCanvas.backgroundpaint;
             violet->canvasbordercolor.Name;
             canvasbordercolor[]->myCanvas.borderpaint;
             myCanvas.open;
             ((300,300)->myCanvas.Size, (8,8))->addpoints->size;
          #);
     #) (* theWindow *)
do theWindow.open
#)

