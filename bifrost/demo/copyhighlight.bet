ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/PredefinedGO';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This example shows how to utilize the deep copy function of graphical 
    * objects, and how to use two of the different highlighting approaches 
    * supported by bifrost
    *) 
   theWindow: @window
     (# 
        myCanvas: @BifrostCanvas
          (# 
             go1, go2: ^GraphicalObject;
             go3, go4: ^graphictext;
	     aSolidColor: @SolidColor;

	     aShape: @Shape
	       (# 
	       do (50,100)->open;
	          (50,150)->lineto;
	          (75,200)->splineto;
	          (150,170)->splineto;
	          (145,100)->splineto;
	          close;
               #);
             eventhandler:: 
               (# 
	          onOpen:: 
	            (# 
	            do aSolidColor.init;
	               pink->aSolidColor.Name;
                       &GraphicalObject[]->go1[];
                       go1.init;
	               aShape->go1.setShape;
                       aSolidColor[]->go1.setPaint;
	               go1[]->draw;
                       go1[]->hilite;

                       go1.copy->go2[];
                       (150,0)->go2.move;
	               go2[]->draw;
                       go2[]->hilite;

                       &GraphicText[]->go3[];
                       go3.init;
                       &go3.theShape.hiliteBound[]->go3.theShape.drawhilite[];
                       ((50,30),Times,Bold,34,false,'Hi!')->go3.inittext;
                       go3[]->draw;
                       go3[]->hilite;

                       go3.copy->go4[];
                       (100,0)->go4.move;
                       'Hi again!'->go4.theText;
                       go4[]->draw;
                       go4[]->hilite;
                    #);
                  onMouseDown:: (# do terminate #);
               #) (* eventhandler *);
          #) (* myCanvas *);
        open:: 
          (# 
          do 4->myCanvas.Borderwidth;
             myCanvas.open;
             ((350,250)->size, (8,8))->SubPoints->myCanvas.Size;
          #);
     #) (* theWindow *)
do theWindow.open;
#)
