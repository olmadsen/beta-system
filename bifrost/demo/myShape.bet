ORIGIN '~beta/bifrost/v2.2/Bifrost';
INCLUDE '~beta/bifrost/v2.2/private/Impl/debug';
INCLUDE '~beta/bifrost/v2.2/ColorNames';

-- program: descriptor --
bifrost
(# 
   (* This is a small example showing how to make a simple Shape consisting
    * of Line- and SplineSegments, and then filling the Shape with a pink
    * SolidColor.
    * Also this example shows how to reshape the graphical object 
    * interactively, if one of the hotspots were hit.
    *)
   
   theWindow: @window
     (# theCanvas: @BifrostCanvas
          (# aGO: @GraphicalObject;
             aSolidColor: @SolidColor;
             
             myShape: @Shape
               (#
               do (100,100) -> open;
                  (100,150) -> lineto;
                  (125,200) -> splineto;
                  (200,170) -> splineto;
                  (195,100) -> splineto;
                  close;
               #);
             
             eventhandler::
               (# 
                  onOpen::
                    (#
                    do aSolidColor.init;
                       pink -> aSolidColor.name;
                       aGO.init;
                       myShape -> aGO.setShape;
                       aSolidColor[]-> aGO.setpaint;
                       aGO[] -> draw;
                       aGO[] -> hilite;
                    #);
                  onMouseDown::
                    (# exactPos: ^point;
                    do mousePos -> DeviceToCanvas -> mousePos;
                       (aGO[], mousePos)-> HitControl -> exactpos[];
                       (if exactpos[] <> NONE then
                           aGO[] -> unhilite;
                           (aGO[], exactPos, NoModifier)
                             -> interactiveReshape;
                           aGO[] -> hilite;
                        else
                           (if (theCanvas[],mousePos) -> aGO.containsPoint then
                               aGO[] -> unhilite;
                               (aGO[], mousePos, NoModifier)
                                 -> interactiveMove;
                               aGO[] -> hilite;
                           if);
                       if);
                    #);
                  onKeyDown:: (# do terminate #);
               #);
             open::
               (# do theWindow.size->size; #);
          #);
        open::
          (# do (300,300)->size; theCanvas.open; #);
     #);
do theWindow.open;
   1->Arguments->theWindow.title;
#)
