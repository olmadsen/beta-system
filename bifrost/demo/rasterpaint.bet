ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/ColorNames';
-- PROGRAM: descriptor --

bifrost
(# 
   (* This example shows how to create a Pixmap, and use it in a RasterPaint.
    * It also illustrates the difference in behaviour depending on whether 
    * the PaddingSolidColor attribute of the RasterPaint is set or not.
    * The Pixmap defined contains 6 rectangular areas with pixels of six
    * different colors.
    *) 
   theWindow: @window
     (# 
        canvasbordercolor: @SolidColor;
        canvasbackgroundcolor: @SolidColor;

        myCanvas: @BifrostCanvas
          (# 
             go1, go2: @GraphicalObject;
	     aSolidColor: @SolidColor;
             RasterPaint1, RasterPaint2: ^RasterPaint;

	     aShape: @Shape
	       (# 
	       do (50,100)->open;
	          (50,150)->lineto;
	          (75,200)->splineto;
	          (150,170)->splineto;
	          (145,100)->splineto;
	          close;
                  (* Lower left corner of the mypixmap (ie. default raster hotspot)
 	           * will be positioned in hotspot when this shape is filled with
                   * Rasterpaint1
                   *)
                  (75,125)->hotspot;
               #);

             mypixmap: @PixMap
               (# mypix: @pixel;
	          putpix: @PutPixel;
               do (30,30,MaxRGB)->init;
                  (MaxRGB,0,0)->mypix;
                  (for i: 10 repeat
                       (for j: 15 repeat (i,j,mypix[])->PutPix; for)
                  for);
                  (MaxRGB,MaxRGB,0)->mypix;
                  (for i: 10 repeat
                       (for j: 15 repeat (i+10,j,mypix[])->PutPix; for)
                  for);
                  (0,MaxRGB,MaxRGB)->mypix;
                  (for i: 10 repeat
                       (for j: 15 repeat (i+20,j,mypix[])->PutPix; for)
                  for);
                  (0,MaxRGB,0)->mypix;
                  (for i: 10 repeat
                       (for j: 15 repeat (i,j+15,mypix[])->PutPix; for)
                  for);
                  (MaxRGB,0,MaxRGB)->mypix;
                  (for i: 10 repeat
                       (for j: 15 repeat (i+10,j+15,mypix[])->PutPix; for)
                  for);
                  (0,0,MaxRGB)->mypix;
                  (for i: 10 repeat
                       (for j: 15 repeat (i+20,j+15,mypix[])->PutPix; for)
                  for);
               #);
	     
	     open:: 
	       (# 
	       do &RasterPaint[]->RasterPaint1[];
                  'Initializing pixmap ... '->puttext;
                  mypixmap->RasterPaint1.thePixMap;
                  'done.'->putline;
	          
	          go1.init;
	          aShape->go1.setShape;
                  RasterPaint1->go1.setPaint;
	          
	          aSolidColor.init;
	          pink->aSolidColor.Name;
	          
                  Rasterpaint1.copy->RasterPaint2[];
                  aSolidColor[]->RasterPaint2.paddingSolidColor[];
	          
                  go2.init;
	          aShape.copy->go2.setShape;
	          
                  (150,0)->go2.move; 
                  RasterPaint2->go2.setPaint;
               #);
             eventhandler:: 
               (# 
	          onOpen:: 
	            (# 
	            do go1[]->draw;
                       go1[]->hilite;
	               
                       go2[]->draw;
                       go2[]->hilite;
                    #);

                  onMouseDown:: (# do terminate #);
               #) (* eventhandler *);
          #) (* theCanvas *);
        open:: 
          (# 
          do 4->myCanvas.Borderwidth;
             canvasbordercolor.init;
             violet->canvasbordercolor.Name;
             canvasbordercolor[]->myCanvas.borderpaint;
             canvasbackgroundcolor.init;
             aliceblue->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->myCanvas.backgroundPaint;
             myCanvas.open;
             ((350,300)->myCanvas.Size, (8,8))->AddPoints->size;
          #);
     #) (* theWindow *)
do theWindow.open;
#)

