ORIGIN '~beta/bifrost/Bifrost';
INCLUDE '~beta/bifrost/ColorNames';
INCLUDE '~beta/bifrost/PredefinedGO';

-- PROGRAM: descriptor --

(* A simple styled text editor build using Bifrost and Lidskjalv.
 * A BifrostCanvas is opened, in which text can be displayed: When the mouse is
 * pressed, an insertionmark appears, and text can be entered using the
 * keyboard. The text entering is ended by typing <Return>, or by clicking
 * the mouse. Then text can be entered another place.
 * When text is not being entered, a few characteristics of the next text
 * can be set using the menus.
 *)

guienv
(# mainwindow: @window
     (# menubarType::
          (# mymenu: menu
               (* Special menu type with interdependant check marks *)
               (# oldChecked: ^radioitem;
                  radioitem: menuitem
                    (# eventhandler::<
                         (# 
                            onSelect::<
                              (# 
                              do (if oldChecked[]<>THIS(radioitem)[] then
                                     (if oldChecked[]<>NONE then
                                         false->oldChecked.checked;
                                     if);
                                     THIS(radioitem)[]->oldChecked[];
                                     true->checked;
                                     INNER;
                                 if)
                              #);
                         #);
                    #);
                  toggleitem: menuitem
                    (# eventhandler::<
                         (# 
                            onSelect::<
                              (# 
                              do not checked->checked;
                                 INNER;
                              #);
                         #);
                    #);
               #);
             mFile: @menu
               (# iQuit: @menuitem
                    (# eventHandler:: 
                         (# onSelect:: (# do terminate #)#);
                       open::
                         (# do 'Quit' -> name #);
                    #);
                  open::
                    (# 
                    do 'File' -> name;
                       iQuit.open;
                       iQuit[] -> append;
                    #);
               #);
             mFont: @mymenu
               (# 
                  iTimes: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do Times->textfont #)#);
                       open::
                         (# do 'Times' -> name #);
                    #);
                  iCourier: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do Courier->textfont #)#);
                       open::
                         (# do 'Courier' -> name #);
                    #);
                  iHelvetica: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do Helvetica->textfont #)#);
                       open::
                         (# do 'Helvetica' -> name #);
                    #);
                  open::
                    (# 
                    do 'Font' -> name;
                       iTimes.open;
                       iTimes[] -> append;
                       iCourier.open;
                       iCourier[] -> append;
                       iHelvetica.open;
                       iHelvetica[] -> append;
                       iTimes.theEventhandler.onSelect;
                    #);
               #);
             mSize: @mymenu
               (# 
                  sizeitem: radioitem
                    (# size: @integer;
                       eventHandler:: 
                         (# onSelect:: 
                              (# do size->textsize #)#);
                       open::
                         (# s: ^text do &text[]->s[]; size->s.putint; s[] -> name #);
                       new:
                         (# enter size do open; THIS(sizeitem)[]->Append #);
                    #);
                  i6, i8, i10, i12, i14, i18, i20, i24, i36, i48, i72: @sizeitem;
                  open::
                    (# 
                    do 'Size' -> name;
                       6  -> i6.new;
                       8  -> i8.new;
                       10 -> i10.new;
                       12 -> i12.new;
                       14 -> i14.new;
                       18 -> i18.new;
                       20 -> i20.new;
                       24 -> i24.new;
                       36 -> i36.new;
                       48 -> i48.new;
                       72 -> i72.new;
                       i18.theEventhandler.onSelect;
                    #);
               #);
             mColor: @mymenu
               (# iRed: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do red->color.name #)#);
                       open::
                         (# do 'Red' -> name #);
                    #);
                  iGreen: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do green->color.name #)#);
                       open::
                         (# do 'Green' -> name #);
                    #);
                  iBlue: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do blue->color.name #)#);
                       open::
                         (# do 'Blue' -> name #);
                    #);
                  open::
                    (# 
                    do 'Color' -> name;
                       iRed.open;
                       iRed[] -> append;
                       iGreen.open;
                       iGreen[] -> append;
                       iBlue.open;
                       iBlue[] -> append;
                       iBlue.theEventhandler.onSelect;
                    #);
               #);
             mStyle: @mymenu
               (# iItalic: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do Italic->textstyle #)#);
                       open::
                         (# do 'Italic' -> name #);
                    #);
                  iBold: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do Bold->textstyle #)#);
                       open::
                         (# do 'Bold' -> name #);
                    #);
                  iPlain: @radioitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do Plain->textstyle #)#);
                       open::
                         (# do 'Plain' -> name #);
                    #);
                  iUnderline: @toggleitem
                    (# eventHandler:: 
                         (# onSelect:: 
                              (# do not underline -> underline #)
                         #);
                       open::
                         (# do 'Underline' -> name #);
                    #);
                  open::
                    (# 
                    do 'Style' -> name;
                       iItalic.open;
                       iItalic[] -> append;
                       iBold.open;
                       iBold[] -> append;
                       iPlain.open;
                       iPlain[] -> append;
                       iUnderline.open;
                       iUnderline[] -> append;
                       iPlain.theEventhandler.onSelect;
                    #);
               #);
             open::
               (# 
               do mFile.open;
                  mFile[] -> append;
                  mFont.open;
                  mFont[] -> append;
                  mStyle.open;
                  mStyle[] -> append;
                  mSize.open;
                  mSize[] -> append;
                  mColor.open;
                  mColor[] -> append;
               #);
          #);
        
        T: ^Graphictext;
        textfont: @FontName;
        textstyle: @Style;
        textsize: @integer;
        underline: @boolean;
        color: @SolidColor;
        
        TextCanvas: @BifrostCanvas
          (# eventhandler::
               (# onMouseDown::
                    (# go: ^AbstractGraphicalObject;
                    do mousePos 
                         -> DeviceToCanvas (* Transform to Canvas coords *)
                         -> mousePos
                         -> lastContaining (* Check if any object hit *)
                         -> go[];
                       (if go[]=NONE then
                           (* No existing object hit; create one *)
                           &GraphicText[] -> T[];
                           T.init;
                           textsize -> T.size;
                           textfont -> T.theFontName;
                           textstyle -> T.theStyle;
                           underline -> T.underline;
                           color.copy -> T.setpaint;
                           (T[], mousepos, NoModifier)
                             -> interactiveCreateShape;
                           T[] -> draw;
                        else
                           (* Hit a graphical object; move it *)
                           (go[], mousePos, NoModifier)
                             -> interactiveMove;
                       if);
                    #);
               #);
             open::
               (# 
               do color.init;
                  mainwindow.size->size;
                  True -> bindBottom;
                  True -> bindRight;
               #);
          #);
        open::
          (# 
          do (300,300) -> Size;
             TextCanvas.open;
          #);
     #);
do mainwindow.open;
#)
