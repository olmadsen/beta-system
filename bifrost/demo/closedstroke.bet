ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/PredefinedGO';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This example shows how the Stroke operation of Shape works on a
    * closed Shape
    *) 
   theWindow: @window
     (# canvasbackgroundcolor: @SolidColor;

        myCanvas: @BifrostCanvas
          (# aSolidColor: @SolidColor;

             myStroke: @Shape
               (# 
               do ( 50,100)->open;
                  (200,100)->lineTo;
                  (200, 50)->lineTo;
                  ( 50, 50)->lineTo;
                  close;
                  (10,CapButt,JoinMiter)->stroke;
               #);
             eventhandler::
               (# onOpen:: 
                    (# myGO: @GraphicalObject;
                    do aSolidColor.init;
                       orange->aSolidColor.Name;
                       myGO.init;
                       aSolidColor->myGO.setPaint;
                       myStroke->myGO.setShape;
                       myGO[]->draw;
                    #);
                  onMouseDown::
                    (# do terminate #);
               #);
          #) (* myCanvas *);
        open::
          (# 
          do 4->myCanvas.Borderwidth;
             canvasbackgroundcolor.init;
             lightblue->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->myCanvas.backgroundPaint;
             myCanvas.open;
             ((250,150)->size, (8,8)) -> SubPoints -> myCanvas.Size;
          #);
     #);
do theWindow.open;
#)

