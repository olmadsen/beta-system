ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/PredefinedGO';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This example shows how to use different colorspaces. Like "colorscale",
    * the HSV colorspace is used to draw a column containing a colorscale.
    * The column is built up of thin Lines.
    * Another column is drawn in complementary colors by using the RGB values
    * of the first column as CMY values in the second.
    * The program thus uses both the HSV, RGB and CMY colorspaces.
    *) 
   theWindow: @window
     (# canvasbackgroundcolor, canvasbordercolor: @SolidColor;
        
        rgbcmyCanvas: @BifrostCanvas
          (# aLine: ^Line;
             aSolidColor, anotherSolidColor: ^SolidColor;
             eventhandler:: 
               (# onOpen:: 
        	    (# r,g,b: @integer
        	    do (for h: 90 repeat
			    &Line[]->aLine[];
                	    aLine.init;
			    4->aLine.width;
			    ((10, 10+4*h),(110,10+4*h))->aLine.Coordinates;
			    &SolidColor[]->aSolidColor[]->aLine.thePaint[];
			    (4*h, DefaultMaxSat-360*h, DefaultMaxVal) 
	        	      -> aSolidColor.HSVvalues;
			    aLine[]->draw;
			    
			    &Line[]->aLine[];
                	    aLine.init;
			    4->aLine.width;
			    ((120, 10+4*h),(220,10+4*h))->aLine.Coordinates;
			    &SolidColor[]->anotherSolidColor[]->aLine.setPaint;
			    aSolidColor.RGBvalues->(r,g,b);
                	    (r,g,b-360*h)->anotherSolidColor.CMYvalues;
			    aLine[]->draw;
           	       for);
                    #);
                  onMouseDown::
                    (# do terminate #);
               #) (* eventhandler *)
          #) (* rgbcmyCanvas *);
        open:: 
          (# 
          do 2->rgbcmyCanvas.Borderwidth;
      	     lightblue->canvasbackgroundcolor.Name;
      	     canvasbackgroundcolor[]->rgbcmyCanvas.backgroundpaint;
      	     violet->canvasbordercolor.Name;
      	     canvasbordercolor[]->rgbcmyCanvas.borderpaint;
      	     rgbcmyCanvas.open;
      	     ((230,380)->rgbcmyCanvas.Size, (4,4))->addpoints->size;
          #);
     #) (* theWindow *)
do theWindow.open;
#)

