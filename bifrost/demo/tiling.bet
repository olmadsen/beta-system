ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/PredefinedGO';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This example shows how to read a small Bitmap from a PBM file, and then
    * tiling a circular shape with it, using a TiledSolidColor.
    *) 
   theWindow: @window
     (# 
        canvasbackgroundcolor: @SolidColor;

        myCanvas: @BifrostCanvas
          (# go1: @Ellipse;
             aTiledSolidColor: ^TiledSolidColor;

             hobbes: @bitMap
               (# 
               do '~beta/bifrost/v2.0/bitmaps/hobbes.pbm'->readFromPBMfile; 
                  (128,136)->hotspot; (* Center of this bitmap *)
               #);
             eventhandler:: 
               (# onOpen:: 
	            (# 
	            do &TiledSolidColor[]->aTiledSolidColor[];
                       aTiledSolidColor.init;
	               black->aTiledSolidColor.Name;
                       hobbes->aTiledSolidColor.theTile;

                       go1.init;
                       ((125,125),125,125)->go1.geometry;
                       (125,125)->go1.theShape.hotspot; (* Center *)
                       aTiledSolidColor[]->go1.setPaint;
                       go1[]->draw;
                    #);
                  onMouseDown::
                    (# do terminate #);
               #) (* eventhandler *)
          #) (* myCanvas *);
        open:: 
          (# do 4->myCanvas.Borderwidth;
             canvasbackgroundcolor.init;
             orange->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->myCanvas.backgroundPaint;
             myCanvas.open;
             ((250,250)->myCanvas.Size, (8,8))->addpoints->size;
          #);
     #) (* theWindow *)
do theWindow.open;
#)
