ORIGIN '~beta/bifrost/Bifrost';
INCLUDE '~beta/bifrost/PredefinedGO';
-- BifrostAttributes: attributes --
image: Picture
  (#
     init::
       (#
	  aGO: ^Ellipse;
	  aSolidColor: ^solidColor;
	  aSquare: ^square;
          go: @GraphicalObject;
          aSpline: @CircularSplineSegment
            (# 
            do (  0,100)->open;
               (100,100)->AddControl;
               (100,  0)->AddControl;
            #);
       enter size
       do
	  &square[] -> aSquare[];
	  aSquare.init;
	  aSquare[] -> add;
	  &ellipse[] -> aGO[];
	  aGO.init;
	  &SolidColor[] -> aSolidColor[];
	  (65280, 0, 0) -> aSolidColor.RGBvalues;
	  aSolidColor[] -> aGO.setPaint;
	  true->aGO.theShape.stroked;
	  (350,350) -> aGO.center;
	  300 -> aGO.horizontalRadius;
	  200 -> aGO.verticalRadius;
	  aGO[] -> add;
          
          go.init;
          aSpline->go.theShape.addSpline;
          aSolidColor[]->go.setpaint;
          (100,100)->go.move;
          go[]->add;

       #)
  #);
square: Rect
  (#
     writeUserData::
       (#
	  (* Notice that we only use one line to store userdata.
	   * It's illegal to put more than one line in userdata
           *)
       do 'Adding userdata \'% USERDATA\'' -> screen.putline;
	  '% USERDATA' -> userdata.putText;
       #);
     readUserData::
       (#
       do 'userdata is: \'' -> screen.puttext;
	  userdata.getLine -> screen.puttext;
	  '\'' -> screen.putline;
       #);
     init::
       (#
	  aSolidColor: ^solidColor;
       do
	  &SolidColor[] -> aSolidColor[];
	  (65280, 0, 0) -> aSolidColor.RGBvalues;
	  aSolidColor[] -> setPaint;
	  (10, 110) -> upperleft;
	  100 -> width;
	  100 -> height
       #)
  #)
