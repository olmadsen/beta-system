ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/PredefinedGO';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# 
   (* In this example the HSV color model is used to vary the saturation and
    * brightness (value). Two columns are shown: in the first one the 
    * saturation is decreased going from bottom to top - thus making the color
    * lighter - and in the second column the value is decreased - thus making
    * the color darker.
    * Each column is composed of 90 thin Lines stacked upon each other.
    *) 
   theWindow: @window
     (# canvasbackgroundcolor, canvasbordercolor: @SolidColor;

        rgbcmyCanvas: @BifrostCanvas
          (# aLine: ^Line;
	     SolidColor1, SolidColor2, SolidColor3: ^SolidColor;
             
             eventhandler:: 
               (# onOpen:: 
                    (# r,g,b: @integer
                    do (for n: 90 repeat
		            &Line[]->aLine[];
                            aLine.init;
		            4->aLine.width;
		            ((10, 10+4*n),(110,10+4*n))->aLine.Coordinates;
		            &SolidColor[]->SolidColor1[]->aLine.thePaint[];
		            (120, DefaultMaxSat-360*n, DefaultMaxVal)
                              ->SolidColor1.HSVvalues;
		            aLine[]->draw;

		            &Line[]->aLine[];
                            aLine.init;
		            4->aLine.width;
		            ((120, 10+4*n),(220,10+4*n))->aLine.Coordinates;
		            &SolidColor[]->SolidColor2[]->aLine.setPaint;
                            (120, DefaultMaxSat, DefaultMaxVal-360*n)
                              ->SolidColor2.HSVvalues;
		            aLine[]->draw;
                            &Line[]->aLine[];
                            aLine.init;
                            4->aLine.width;
                            ((230, 10+4*n),(330,10+4*n))->aLine.Coordinates;
                            &SolidColor[]->SolidColor3[]->aLine.setPaint;
                            (120, DefaultMaxSat-360*n, DefaultMaxVal-360*n)
                              -> SolidColor3.HSVvalues;
                            aLine[]->draw;
                       for);
                    #);
                  onMouseDown::
                    (# do terminate #);
               #) (* eventhandler *);
          #) (* rgbcmyCanvas *);
        open:: 
          (# 
          do 2->rgbcmyCanvas.Borderwidth;
             lightblue->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->rgbcmyCanvas.backgroundpaint;
             violet->canvasbordercolor.Name;
             canvasbordercolor[]->rgbcmyCanvas.borderpaint;
             rgbcmyCanvas.open;
             ((230,380)->rgbcmyCanvas.Size, (4,4))->addpoints->size;
          #);
     #) (* theWindow *)
do theWindow.open;
#)

