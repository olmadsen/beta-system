ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE '~beta/bifrost/v2.0/PredefinedGO';
INCLUDE '~beta/bifrost/v2.0/ColorNames';

-- PROGRAM: descriptor --

bifrost
(# (* This small example shows two different backgrounds of a BifrostCanvas.
    * When opened the background is a TiledSolidColor containing a
    * repeated pattern of a small Bitmap read from a PBM file.
    * If a mouse button is pressed the background is changed to a RasterPaint
    * containing 8 different colors in a hatching pattern.
    * Subsequent mouse button activations makes the background change between
    * these two Paints.
    *) 
   theWindow: @window
     (# canvasTiledBackground: @TiledSolidColor;
        canvasRasterBackground: @RasterPaint;
        raster: @boolean;
        
        backgroundBitmap: @BitMap
          (# do '~beta/bifrost/v2.0/bitmaps/hobbes.pbm'->readFromPBMfile #);
        
        backgroundPixmap: @PixMap
          (# putone: @
               (# mypix: @pixel;
                  PutPix: @PutPixel;
                  i,j: @integer;
               enter mypix
               do (for n: 16 repeat
                       (n-1) mod 32 + 1->j;
                       ((i+n-1) mod 16 + 1, j, mypix[])->PutPix;
                       ((i+n)   mod 16 + 1, j, mypix[])->PutPix;
                  for);
                  i+2->i;
               #);
             
          do (16,16,MaxRGB)->init;
             (* A red stripe *)
             (MaxRGB,      0,      0)->putone;
             (* A yellow stripe *)
             (MaxRGB, MaxRGB,      0)->putone;
             (* A cyan stripe *)
             (     0, MaxRGB, MaxRGB)->putone;
             (* A black stripe *)
             (     0,      0,      0)->putone;
             (* A green stripe *)
             (     0, MaxRGB,      0)->putone;
             (* A magenta stripe *)
             (MaxRGB,      0, MaxRGB)->putone;
             (* A blue stripe *)
             (     0,      0, MaxRGB)->putone;
             (* A white stripe *)
             (MaxRGB, MaxRGB, MaxRGB)->putone;
          #);

        myCanvas: @BifrostCanvas
          (# open:: 
               (# 
               do 4->borderwidth;
                  (theWindow.size, (8,8)) -> SubPoints -> size;
                  
                  canvasTiledBackground.init;
                  violet->canvasTiledBackground.Name;
                  backgroundBitmap->canvasTiledBackground.theTile;
                  
                  canvasRasterBackground.init;
                  backgroundPixmap->canvasRasterBackground.thePixmap;
                  
                  canvasTiledBackground[]->BackgroundPaint;
                  'Press any button to change background.'->putline;
                  'Type ''Q'' to quit.'->putline;
               #);
             
             eventhandler:: 
               (# onMouseDown:: 
                    (# 
                    do (if raster
                        //true  then canvasTiledBackground[]->BackgroundPaint;
                        //false then canvasRasterBackground[]->BackgroundPaint;
                       if);
                       not raster->raster;
                    #);
                  onKeyDown::
                    (# do (if ch//'Q' then terminate if) #);
               #)
          #) (* theCanvas *);
        open::
          (# 
          do (250,250)->Size;
             myCanvas.open;
          #)
     #) (* theWindow *);
do theWindow.open;
#)
