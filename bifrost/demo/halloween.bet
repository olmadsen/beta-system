(* ORIGIN '~beta/bifrost/PredefinedGO';
 * INCLUDE '~beta/bifrost/ColorNames';
 *)
ORIGIN '../PredefinedGO';
INCLUDE '../ColorNames';
INCLUDE '~beta/basiclib/file';

-- PROGRAM: descriptor --

bifrost
(# 
   (* This example shows a way to obtain nice effects using TiledSolidColor.
    * A BifrostCanvas with blue background is opened. In this an orange circle (the
    * color of the moon: -) is drawn. Then a large Bitmap is read from a PBM
    * file, and this Bitmap is used in a black TiledSolidColor to fill a
    * slightly bigger circle. This circle is small enough that the Bitmap will
    * only be replicated once. The hotspots of the circle and the Bitmap is
    * used to make sure the Bitmap is centered in the shape of the circle.
    *) 
   theWindow: @window
     (# canvasbackgroundcolor: @SolidColor; (* The sky *)
        myCanvas: @BifrostCanvas
          (# go1, go2: @Ellipse;
             aTiledSolidColor: @TiledSolidColor;
             aSolidColor: @SolidColor;
             
             witch: @bitMap
               (# 
               do '~beta/bifrost/bitmaps/witch.pbm'->readFromPBMfile;
                  (127,127)->hotspot;
               #);
             open:: 
               (# 
               do aSolidColor.init;
                  orange->aSolidColor.Name;
                  
                  go1.init; (* The moon *)
                  ((125,125),100,100)->go1.geometry;
                  aSolidColor[]->go1.setPaint;
                  
                  aTiledSolidColor.init;
                  black->aTiledSolidColor.Name;
                  witch->aTiledSolidColor.theTile;
                  
                  go2.init; (* The witch *)
                  ((125,125),110,110)->go2.geometry;
                  (125,125)->go2.theShape.hotspot; (* Center *)
                  aTiledSolidColor[]->go2.setPaint;
               #);
             eventhandler:: 
               (# onOpen:: (# do go1->draw; go2[]->draw; #);
                  onMouseDown:: (# do terminate #);
                  onKeyDown::
                    (# out: @file;
                       A4: (# exit (0, 0, 595, 822) #);
                    do (if ch='p' then
                           'halloween.eps' -> out.name;
                           'Writing EPS to halloween.eps ... ' -> puttext;
                           out.openwrite;
                           (A4, true, 1, out[]) -> myCanvas.writeEPS;
                           out.close;
                           'done' -> putline;
                       if);
                    #);
               #) (* eventhandler *);
          #) (* myCanvas *);
        open:: 
          (# 
          do 4->myCanvas.Borderwidth;
             canvasbackgroundcolor.init;
             lightblue->canvasbackgroundcolor.Name;
             canvasbackgroundcolor[]->myCanvas.backgroundPaint;
             myCanvas.open;
             ((250,250)->myCanvas.Size, (8,8))->addpoints->size;
          #);
     #) (* theWindow *)
do true->debuggraphic;
   theWindow.open;
   1->Arguments->theWindow.title;
#)

