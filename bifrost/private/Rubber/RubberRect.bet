ORIGIN '../../Bifrost';

-- CanvasAttributes: attributes --

     RubberRect: InteractionHandler
      (# anchorpoint, movingpoint: @point; (* BifrostCanvas coords *)
	 theModifier: @Modifier;
	 r: @Rectangle;
 	 stopinteraction: @boolean;
	 ibox: @immediaterect;
	 myabs: @Abs;
	 mymin: @Min;
	 
	 Initialize::<
	    (#
	    do (* Transform to device coords *)
	       anchorpoint -> canvastodevice -> anchorpoint;
	       movingpoint -> canvastodevice -> movingpoint;

	       (anchorpoint.x,movingpoint.x) -> mymin -> r.x;
	       (anchorpoint.y,movingpoint.y) -> mymin -> r.y;
	       (anchorpoint.x-movingpoint.x) -> myabs -> r.width;
	       (anchorpoint.y-movingpoint.y) -> myabs -> r.height;
	       r -> ibox;
	       INNER;
	    #);
	 Motion::<
	    (#
	    do
	       r -> ibox;
	       INNER;
	       GetPointerLocation -> movingpoint;
	       (if themodifier -> IsModifierOn
		// true then
		   ((movingpoint.x-anchorpoint.x) -> myabs, 
		   (movingpoint.y-anchorpoint.y) -> myabs)
 		     -> mymin -> r.width -> r.height;
		   (if movingpoint.x > anchorpoint.x // true then
		       (if movingpoint.y > anchorpoint.y
			// true  then 
			   anchorpoint
			   -> (r.x,r.y);
			// false then 
			   (anchorpoint.x,(anchorpoint.y-r.height))
			   -> (r.x,r.y);
		       if);
		    // false then
		       (if (movingpoint.y > anchorpoint.y)
			// true then
			   ((anchorpoint.x-r.width), anchorpoint.y)
			   -> (r.x,r.y);
			// false then 
			   ((anchorpoint.x-r.width),
			   (anchorpoint.y-r.height))
			   -> (r.x,r.y);
		       if);
		   if);
	       else (* unconstrained rectangle *)
		   (anchorpoint.x,movingpoint.x) -> mymin -> r.x;
		   (anchorpoint.y,movingpoint.y) -> mymin -> r.y;
		   (anchorpoint.x-movingpoint.x) -> myabs -> r.width;
		   (anchorpoint.y-movingpoint.y) -> myabs -> r.height;
	       if);
	       r -> ibox;
	       INNER;
	    #);
	 ButtonPress::<
	    (#
	    do true -> stopinteraction;
	    #);
	 TerminateCondition::<
	    (# 
	    do stopinteraction -> value;        
	    #);
	 Terminated::<
	    (# 
	    do (* erase feedback *)
	       r -> ibox;
	       (* tranform to BifrostCanvas coords *)
	       
	       r -> TM.inversetransformrectangle -> r;

	       INNER;
	    #);
	 
      enter (anchorpoint, movingpoint, theModifier) (* BifrostCanvas coords *)
      exit r                                        (* BifrostCanvas coords *)
      #)

