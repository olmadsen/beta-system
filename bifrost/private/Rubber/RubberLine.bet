ORIGIN '../../Bifrost';

-- CanvasAttributes: attributes --

RubberLine: InteractionHandler
  (# mousePoint,anchorPoint: @Point; (* Device coords *)
     mouse, anchor: @point;          (* BifrostCanvas coords *)
     themodifier: @modifier;
     stopinteraction: @boolean;
     myabs: @abs;
     iline: @immediateLine;
     x,y: @integer;
     
     Initialize::<
       (#
       do (anchorpoint, mousePoint) -> iline;
          anchorpoint -> devicetocanvas -> anchor;
       #);
     
     Motion::<
       (#
       do (anchorpoint, mousePoint) -> iline;
          GetPointerLocation -> mousePoint;
          (if themodifier->isModifierOn
           // true then
              (mousepoint.x-anchorpoint.x) -> myabs -> x;
              (mousepoint.y-anchorpoint.y) -> myabs -> y;
              (if y > 2*x
               // true then (* 90 degrees *)
                  anchorpoint.x -> mousepoint.x
               // false then (* 45 degrees *)
                  (if 2*y > x
                   // true then
                      mousepoint -> devicetocanvas -> mouse;
                      
                      (if y > x
                       // true  then (* change mouse.y *)
                          (if mouse.y > anchor.y
                           // true  then anchor.y + x -> mouse.y
                           // false then anchor.y - x -> mouse.y
                          if);
                       // false then (* change mouse.x *)
                          (if mouse.x > anchor.x 
                           // true  then anchor.x + y -> mouse.x
                           // false then anchor.x - y -> mouse.x
                          if);
                      if);
                      
                      mouse -> canvastodevice -> mousepoint;
                   // false then (* 0 degrees *)
                      anchorpoint.y -> mousepoint.y
                  if)
              if);
          if);
          (anchorpoint, mousePoint) -> iline;
       #);
     ButtonPress::<
       (# do true -> stopinteraction; #);
     TerminateCondition::<
       (# do stopinteraction -> value; #);
     Terminated::<
       (# (* erase feedback *)
       do (anchorpoint, mousePoint) -> iline;
       #);
     
  enter (anchorpoint, mousepoint, themodifier)
  exit mousepoint
  #);

