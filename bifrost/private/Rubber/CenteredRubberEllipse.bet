ORIGIN '../../Bifrost';

-- CanvasAttributes: attributes --

     CenteredRubberEllipse: InteractionHandler
     (# stopinteraction: @boolean;
	themodifier: @modifier;
	center, movingPoint: @Point; (* BifrostCanvas coords *)
	width, height: @integer;
	iarc: @immediatearc;
	myabs: @Abs;
	mymin: @Min;

	Initialize::<
	   (#
	   do (* Transform to device coords *)
	       center -> canvastodevice -> center;
	       movingpoint -> canvastodevice -> movingpoint;
	      (center.x,center.y,width,height,0,360) -> iarc;
	      INNER;
	   #);
	Motion::<
	   (#
	   do
	      (center.x,center.y,width,height,0,360) -> iarc;
	      INNER;
	      GetPointerLocation -> movingpoint;
	      (if themodifier -> IsModifierOn then
		  ((movingpoint.x-center.x) -> myabs, 
		  (movingpoint.y-center.y) -> myabs) 
		    -> mymin -> width -> height;
	      else (* unconstrained ellipse *)
		  (center.x-movingpoint.x) -> myabs -> width;
		  (center.y-movingpoint.y) -> myabs -> height;
	      if);
	      (center.x,center.y,width,height,0,360) -> iarc;
	      INNER;
	   #);
	ButtonPress::<
	   (#
	   do true -> stopinteraction;
	   #);
	TerminateCondition::<
	   (# 
	   do stopinteraction -> value;
	   #);
	Terminated::<
	   (#
	   do (* erase feedback *)
	      (center.x,center.y,width,height,0,360) -> iarc;
	      INNER;
	   #);

     enter (center, movingpoint, themodifier)
     exit (width, height)
     #)
