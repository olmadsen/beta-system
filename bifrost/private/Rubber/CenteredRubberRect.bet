ORIGIN '../../Bifrost';

-- CanvasAttributes: attributes --

     CenteredRubberRect: InteractionHandler
      (# center, movingpoint: @point; (* BifrostCanvas coords *)
	 devicecenter: @point;        (* Device coords *)
	 theModifier: @Modifier;
	 r: @Rectangle;
	 halfwidth, halfheight: @integer;
	 dr: @rectangle;
 	 stopinteraction: @boolean;
	 ibox: @immediaterect;
	 myabs: @Abs;
	 mymin: @Min;
	 
	 Initialize::<
	    (#
	    do (* Transform to device coords *)
	       center -> canvastodevice -> devicecenter;
	       
	       (* BifrostCanvas coords *)
  	       (center.x-movingpoint.x) -> myabs -> halfwidth; 
	       (center.y-movingpoint.y) -> myabs -> halfheight;
	       center.x-halfwidth  -> r.x;
	       center.y+halfheight -> r.y;
	       2*halfwidth  -> r.width;                      
	       2*halfheight -> r.height;
	       
	       r -> TM.transformrectangle -> dr;
	       (* Device coords *)	       
	       dr -> ibox;
	       
	       INNER;
	    #);
	 Motion::<
	    (#
	    do
	       dr -> ibox;
	       INNER;
	       GetPointerLocation -> devicetocanvas -> movingpoint;
	       (if themodifier -> IsModifierOn then
		   (* BifrostCanvas coords *)
  	       	   ( (center.x-movingpoint.x) -> myabs, 
	       	     (center.y-movingpoint.y) -> myabs)
		     -> mymin -> halfwidth -> halfheight;  
	       	   center.x-halfwidth  -> r.x;
	       	   center.y+halfheight -> r.y;
	       	   2*halfwidth  -> r.width -> r.height;  
	       	   r -> TM.transformrectangle -> dr;
	       else (* unconstrained rectangle *)
		   (* BifrostCanvas coords *)
  	       	   (center.x-movingpoint.x) -> myabs -> halfwidth; 
	       	   (center.y-movingpoint.y) -> myabs -> halfheight;
	       	   center.x-halfwidth  -> r.x;
	       	   center.y+halfheight -> r.y;
	       	   2*halfwidth  -> r.width;                       
	       	   2*halfheight -> r.height;
	       	   r -> TM.transformrectangle -> dr;
	       if);
	       (* Device coords *)
	       dr -> ibox;
	       INNER;
	    #);
	 ButtonPress::<
	    (#
	    do true -> stopinteraction;
	    #);
	 TerminateCondition::<
	    (# 
	    do stopinteraction -> value;        
	    #);
	 Terminated::<
	    (# 
	    do (* erase feedback *)
	       dr -> ibox;
	       INNER;
	    #);
	 
      enter (center, movingpoint, theModifier) (* BifrostCanvas coords *)
      exit r                                   (* BifrostCanvas coords *)
      #)

