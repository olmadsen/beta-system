ORIGIN '../MacBifrost';
BODY 'CanvasWindowImpl';


[[

  (*
   * CanvasWindow:
   *                                          
   *   A Macintosh Window with a Canvas (theCanvas) for drawing in.
   *
   *)

-- INCLUDE 'MacBifrostEnv'
-- INCLUDE 'DatatypesMacImpl'

-- GraphicsAttributes: attributes --

CanvasWindow: env.theMacEnv.colorwindow (* a MacEnv Window with color *)
(# 
   theCanvases: @doublelinkedlist
   (# element::< Canvas;
      append: insertWithNoCheck(##);
      hitcanvas:
      (# p: ^MacPoint; c: ^Canvas;
         macR: @macRectangle;
      enter p[]
      do
         scanner: scan
	 (# 
	 do ((thisElm.position.x,thisElm.position.y,
	      thisElm.Size.width,thisElm.Size.height),macR[]) -> MakeMacRect;
	    (if (p,macR[]) -> PtInRect
	     //mactrue then thisElm[] -> c[]; leave scanner;
	    if);
	 #);
	 (if c[]//NONE then theCanvas[] -> c[] if); (* exit InsideCanvas by default *)
       exit c[]
       #);
   #);
   
   position: @Point;
   leftMargin: @Integer;
      
   size: @
     (#  width: @Integer;
	 height: @Integer;
     enter (width,height)
     exit  (width,height)
     #);
	
   setVisualClip:
     (# portRect : @macrectangle;
     do 
        winptr.portRect -> portRect;
	portRect[] -> ClipRect;
     #);
	
   duringOpen: @Boolean;
   createdByCanvas: @Boolean;

   init:<
   (# 
   do
      (if theCanvas[]//NONE then &InsideCanvas[] -> theCanvas[] if);
      THIS(CanvasWindow)[] -> env.currentWindow[];
      &env.theMacEnv.Cursor[] -> env.watch[];
      watchCursor -> env.watch.get;
      INNER;
      theCanvas[] -> theCanvases.append;
      theCanvas.Init;
   #);

   theCanvas: ^InsideCanvas;
   InsideCanvas:< Canvas;
      
   Open::< (# do <<SLOT OpenWindow: descriptor>>; INNER; #);
      
   Close::< (# do theCanvases.scan (# do thisElm.close #) #);
      
   EventHandler::<
     (#
        activate::< (# do THIS(CanvasWindow)[] -> env.currentWindow[] #);
	mouseDown::<
	(# UpdateCanvasSize: (# do <<SLOT UpdateCanvasSize: descriptor>> #);
	   inGrow::<    (# do UpdateCanvasSize #);
	   inZoomIn::<  (# do UpdateCanvasSize #);
	   inZoomOut::< (# do UpdateCanvasSize #);
	   inContent::<  (# do  <<SLOT InContent :descriptor>> #);
	   doubleClick::< (# do INNER #);
	#);
	refresh::< (# do <<SLOT RefreshWindow: descriptor>> #);
	keyDown::< (# do <<SLOT KeyDown :descriptor>> #);
     #);
#);

--]]
