ORIGIN '../Impl/PredefinedImpl';

(********* Fonts and styles ***********)

-- CourierBody: dopart --
do 1->value;
   
   
-- TimesBody: dopart --
do 2->value;
   
   
-- HelveticaBody: dopart --
do 3->value;
   
   
-- PlainBody: dopart --
do 1->value;
   
   
-- ItalicBody: dopart --
do 2->value;
   
   
-- BoldBody: dopart --
do 3->value;
   
   
   
   (************ TextShape **********************)
   
-- BifrostAttributes: attributes --

TextShapeGetWidthHeight:
  (# theLines: ^TextList;
     nam: @Fontname;
     sty: @Style;
     siz: @Integer;
     width,height: @Integer;
  enter (theLines[], nam, sty, siz)
  do <<SLOT TextShapeGetWidthHeight: descriptor >>;
  exit (width,height)
  #);

TextShapeMaxDescent:
  (# nam: @Fontname;
     sty: @Style;
     siz: @Integer;
     desc: @Integer;
  enter (nam, sty, siz)
  <<SLOT TextShapeMaxDescent: dopart>>
  exit desc
  #);
TextShapeMaxAscent:
  (# nam: @Fontname;
     sty: @Style;
     siz: @Integer;
     asc: @Integer;
  enter (nam, sty, siz)
  <<SLOT TextShapeMaxAscent: dopart>>
  exit asc
  #);

TextShapeUnderlineDepth:
  (# nam: @Fontname;
     sty: @Style;
     siz: @Integer;
     depth: @Integer;
  enter (nam, sty, siz)
  do <<SLOT TextShapeUnderlineDepth: descriptor >>;
  exit depth
  #);


-- TextShapeChangePosBody: dopart --
do p -> TextPrivate.position;
   Invalidate;
   
   
-- TextShapePosBody: dopart --
do TextPrivate.position -> p;
   
   
-- TextShapeChangeNameBody: dopart --
do nam -> TextPrivate.theFontName;
   Invalidate;
   
   
-- TextShapeTheFontNameBody: dopart --
do TextPrivate.theFontName -> nam;
   
   
-- TextShapeChangeStyleBody: dopart --
do sty -> TextPrivate.theStyle;
   Invalidate;
   
   
-- TextShapeTheStyleBody: dopart --
do TextPrivate.theStyle -> sty;
   
   
-- TextShapeChangeSizeBody: dopart --
do siz -> TextPrivate.theSize;
   Invalidate;
   
   
-- TextShapeSizeBody: dopart --
do TextPrivate.theSize -> siz;
   
   
-- TextShapeChangeUnderlineBody: dopart --
do ul -> TextPrivate.underline;
   Invalidate;
   
   
-- TextShapeUnderlineBody: dopart --
do TextPrivate.underline -> ul;
   
   
-- TextShapeChangeTextBody: dopart --
do (if t[]<>NONE then
       TextPrivate.theStrings.clear;
       t.scanall
       (# x: @text;
          i: @integer;
       do i+1->i;
          (if ch
           // ascii.nl
           // ascii.cr then
              x.copy -> TextPrivate.theStrings.append;
              x.clear;
           else
              ch -> x.put;
              (if i=t.lgth then
                  (* end of text reached *)
                  x.copy -> TextPrivate.theStrings.append;
              if);
          if);
       #);
       Invalidate;
   if)
   
   
-- TextShapeTheTextBody: dopart --
do &text[]->t[]; 
   TextPrivate.theStrings.scan
   (# firstdone: @boolean
   do (if firstdone then
          t.newline;
       else
          true -> firstdone;
      if);          
      current.copy -> t.append;
   #)
   
--TextShapeTheLinesBody: dopart-- 
do TextPrivate.thestrings.copy -> l[];
   
--TextShapeChangeLinesBody: dopart--
do (if l[]<>NONE then
       TextPrivate.thestrings.clear;
       l.scan
       (# 
       do current[] -> TextPrivate.thestrings.append;
       #);
       Invalidate;
   if)
   
--TextShapeNumberOfLinesBody: dopart--
do TextPrivate.thestrings.size-> value;
   
-- TextShapePrivatePart: descriptor --
(# thestrings: @TextList;
   theFontName: @FontName;
   theStyle: @Style;
   theSize: @Integer;
   underline: @Boolean;
   position: @Point;
#)

-- TextShapeGetBoundsBody: descriptor --
(# asc: @integer;
do (if privatepart.boundInvalid then
       
       (* Find width and height *)
       ((theLines, theFontName, theStyle, size) -> TextShapeGetWidthHeight)
         -> (privatepart.bound.width,privatepart.bound.height);
       
       (* Find x *)
       TextPrivate.position.x -> privatepart.bound.x;
       
       (* Find y *)
       (theFontName, theStyle, size) -> TextShapeMaxAscent -> asc;
       TextPrivate.position.y + asc (* Sum is top of top line *)
         -> privatepart.bound.y;
       
       (* Small adjustment *)
       privatepart.bound.width+1 -> privatepart.bound.width;
       privatepart.bound.height+1 -> privatepart.bound.height;
       
       (* Transform to canvas coords *)
       (privatepart.bound.x,privatepart.bound.y)
         -> privatepart.go2canvasTM.m.transformPoint
         -> (privatepart.bound.x,privatepart.bound.y);
       
       false -> privatepart.boundinvalid;
   if);
#)

-- TextShapeContainsPoint: dopart --
do (thePoint, getbounds) -> PointInRect -> value;
   INNER
   
-- TextShapeCopyBody: descriptor --
(# ts: ^TextShape;
do (if aCopy[]=NONE then (* Not done in inner *)
       &TextShape[] -> ts[];
       position -> ts.position;
       theFontName -> ts.theFontName;
       theStyle -> ts.theStyle;
       size -> ts.size;
       underline -> ts.underline;
       TextPrivate.theStrings.scan
       (# 
       do current[] -> ts.TextPrivate.theStrings.append;
       #);
       ts[] -> aCopy[];
   if)
#)

-- TextShapeTransformBody: dopart --
do TextPrivate.position -> M.transformPoint -> TextPrivate.position;
   INNER;
   
-- TextShapeCalculate: dopart --
do 'TextShape: CalculateShape not implemented' -> putline;
   INNER;
   
-- TextShapeGetControlsBody: descriptor --
(# r: @Rectangle;
do
   getBounds -> r; (* bound in BifrostCanvas coordinates *)
   (r.x, r.y)  -> spots.addpoint;
   (r.x+r.width, r.y) -> spots.addpoint;
   (r.x+r.width, r.y-r.height) -> spots.addpoint;
   (r.x, r.y-r.height) -> spots.addpoint;
#)
