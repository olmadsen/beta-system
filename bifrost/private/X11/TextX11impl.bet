ORIGIN '../Predefined/TextImpl';
LIB_ITEM 'bifrost';
INCLUDE 'X11fonts';

-- TextListGetWidthHeight: descriptor --
(# xf: ^THIS(guienv).private.xfont;
do (* Find the xfont specified by nam, sty and siz *)
   (nam,sty,siz) -> XCheckFontLoaded -> (xf[],nam,sty,siz);
   
   (* Now obtain the width *)
   0 -> width;
   (if xf.fixedwidth then 
       theLines.scan
       (# w: @integer
       do current.length*xf.width -> w;
          (if w>width then w->width if);
       #);
    else
       theLines.scan
       (# w: @integer
       do (xf.fi, current.t, current.t.range) -> XTextWidth -> w;
          (if w>width then w->width if);
       #);
   if);
   
   (* now obtain the height *)
   theLines.size*xf.lineheight -> height;
   
#)
-- TextGetWidth: descriptor --
(# xf: ^THIS(guienv).private.xfont;
do (* Find the xfont specified by nam, sty and siz *)
   (nam,sty,siz) -> XCheckFontLoaded -> (xf[],nam,sty,siz);
   
   (* Now obtain the width *)
   (if xf.fixedwidth then 
       T.length*xf.width ->value
    else
       (xf.fi, T.t, T.t.range) -> XTextWidth -> value
   if);
#)
-- TextGetHeight: descriptor --
(# xf: ^THIS(guienv).private.xfont;
do (* Find the xfont specified by nam, sty and siz *)
   (nam,sty,siz) -> XCheckFontLoaded -> (xf[],nam,sty,siz);
   
   xf.lineheight -> value;
#)
-- TextLineHeight: dopart --
do (NONE,nam,sty,siz) -> TextGetHeight -> value;

-- TextMaxDescent: dopart --
do (# xf: ^THIS(guienv).private.xfont;
   do (nam,sty,siz) -> XCheckFontLoaded -> (xf[],nam,sty,siz);
      THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz].maxdescent
        -> value
   #)
-- TextMaxAscent: dopart --
do (# xf: ^THIS(guienv).private.xfont;
   do (nam,sty,siz) -> XCheckFontLoaded -> (xf[],nam,sty,siz);
      THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz].maxascent
        -> value
   #);
   
-- TextUnderlineDepth: descriptor --
(# t: @integer; xf: ^THIS(guienv).private.xfont;
do (nam,sty,siz) -> XCheckFontLoaded -> (xf[],nam,sty,siz);
   THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz].upos->value;
   THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz].uthick -> t;
   (t+1) div 2 + value -> value
#)

(* TextShapeInteractiveCreate *)
--TextShapeInteractiveCreateBodyPrivate: descriptor--
(# xf: ^THIS(guienv).private.xfont; #)
--TextShapeInteractiveCreateBodyDrawInsertionMark: descriptor--
(#
do theCanvas.prepareInteraction;
   ((base_x,base_y + icprivate.xf.maxdescent), (base_x,uppery)) 
     -> theCanvas.immediateline;
   theCanvas.endInteraction;
   theCanvas[] -> thecanvas.privatepart.interactionpaint.setcanvaspaint;
#)
--TextShapeInteractiveCreateBodyLetterWidth: dopart--
do (ch -> icprivate.xf.letterwidth) -> value
--TextShapeInteractiveCreateBodyDrawCh: descriptor--
(#
do
   (* Cannot use XDrawImageString with TiledSolidColor,
    * so simulate it instead
    *)
   (THIS(guienv).private.display, 
   theCanvas.implpart.xdrawable,
   base_x, uppery, ChWidth, lineheight, 0) -> XClearArea;
   
   theCh -> chArray[1];
   (THIS(guienv).private.display, 
   theCanvas.implpart.xdrawable, 
   theCanvas.implpart.xgc,
   base_x, base_y,
   chArray, 1) -> XDrawString;
   
   (if textprivate.underline then	    
       (THIS(guienv).private.display,
       theCanvas.implpart.xdrawable, 
       theCanvas.implpart.xgc,
       base_x,
       base_y + THIS(guienv).private.fonts.
       xfonts[textprivate.theFontName].
       style[textprivate.theStyle].
       size[textprivate.theSize].upos,
       theCh -> letterwidth,
       THIS(guienv).private.fonts.
       xfonts[textprivate.theFontName].
       style[textprivate.theStyle].
       size[textprivate.theSize].uthick)
         -> XFillRectangle;
   if);
#)
--TextShapeInteractiveCreateBodyClearPrevCh: descriptor--
(# 
do (THIS(guienv).private.display, 
   theCanvas.implpart.xdrawable,
   base_x, uppery, prevChWidth, lineheight, 0) -> XClearArea;
#)
--TextShapeInteractiveCreateBodySetFontAndLine: descriptor--
(# nam: @Fontname;
   sty: @Style;
   siz: @Integer;
do (theCanvas[], textprivate.theFontName, textprivate.theStyle, textprivate.theSize)
     -> SetXFont
     -> (icprivate.xf[],nam,sty,siz);
   
   (THIS(guienv).private.display, 
   theCanvas.implpart.xgc,
   0, 0, 0, 0) -> XSetLineAttributes;
#)
--TextShapeInteractiveCreateBodyFontAscent: dopart--
do icprivate.xf.maxascent->value
--TextShapeInteractiveCreateBodyCleanUp: descriptor--
(# #)

-- TextShapeHiliteOutlineBody: descriptor --
(# pos: @point;
   itext: @theCanvas.immediateText;
   lineheight: @integer;
do 
   (if TM[]=NONE then
       textprivate.position -> pos;
    else
       textprivate.position -> TM.transformpoint -> pos;
   if);
   pos -> Privatepart.Go2CanvasTM.m.transformPoint
     -> theCanvas.CanvasToDevice -> pos;
   (textprivate.theFontName, textprivate.theStyle, textprivate.theSize)
     -> TextLineHeight
     -> theCanvas.ZoomDim 
     -> lineheight;
   
   textprivate.thestrings.scan
   (# 
   do (pos, 
      textprivate.theFontName,
      textprivate.theStyle,
      textprivate.theSize,
      underline,
      current[]) -> itext;
      pos.y+lineheight -> pos.y (* device coords *);
   #);
#)
