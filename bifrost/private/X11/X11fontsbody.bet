ORIGIN 'X11fonts';
LIB_ITEM 'bifrost';
-- Fontname2textDoPart: dopart --
do (if i
    //Courier then
       'courier'->t[];
    //Times then 
       'times'->t[];
    //Helvetica then 
       'helvetica'->t[];
    else
       '*'->t[];
       (* FIXME: announce a warning *)
   if);
   
-- Fontstyle2textDoPart: dopart --
do (if i
    //Plain then
       'medium-r'->t[];
    //Bold then 
       'bold-r'->t[];
    //Italic then 
       'medium-i'->t[];
    else
       '*-*'->t[];
       (* FIXME: announce a warning *)
   if);
-- buildFontNamesTryOblique: doPart --
do
   (* FIXME: This is a special case *)
   (* Should Fontstyle2Text be changed? *)
   names->XFreeFontNames;
   fname.clear;

   tmpname[]->fname.putFormat
     (#
     do
	nam->Fontname2Text->s;
	(* Substitute italic with oblique *)
	'medium-o' -> s
     #);

   (xdisplay, fname,maxnames,@@count) -> XListFonts -> names

-- buildFontNamesListFonts: doPart --
do
   fname.clear;
   tmpname[]->fname.putFormat
     (#
     do
	nam->Fontname2Text->s;
	sty->Fontstyle2Text->s
     #);

   (xdisplay, fname,maxnames,@@count) -> XListFonts -> names

-- buildFontNames: doPart--
do
   (* We first try to find scalable fonts if available else we find non scalables *)
   (* FIXME: the below code must be polished *)
   
   (* First try scalable *)
   &text[] ->fname[];
   (if onlyAdobeFonts then
       '-adobe-%s-%s-normal-*-0-0-*-*-*-*-iso8859-1' -> tmpname[];
    else
       '-*-%s-%s-normal-*-0-0-*-*-*-*-iso8859-1' -> tmpname[];
   if);

   listFonts;
   (if (count = 0) and (sty = italic) then tryOblique if);

   (if count=0 then
       (* Try non-scalable *)
       names->XFreeFontNames;
       (if onlyAdobeFonts then
           '-adobe-%s-%s-normal-*-*-*-*-*-*-*-iso8859-1' -> tmpname[];
        else
           '-*-%s-%s-normal-*-*-*-*-*-*-*-iso8859-1' -> tmpname[];
       if);
       listFonts;
       (if (count = 0) and (sty = italic) then tryOblique if)
   if);

   (if count=0 then
       names->XFreeFontNames;
       putline;
       'Could not find any fonts of type: '->puttext; textname[]->putline;
       'Please consider installing it. '->puttext;
       'Substituting...'->puttext;
       fname.clear;
       '-*-*-%s-normal-*-*-*-*-*-*-*-iso8859-1'
	 -> fname.putFormat(# do sty->Fontstyle2Text->s #);
       (xdisplay, fname,maxnames,@@count) -> XListFonts -> names;
       (if count=0 then
           warntxt.putline;
           'WARNING: Could not find any substitution for'->warntxt.putline;
           textname[]->warntxt.putline;
           'WARNING: too few fonts available'->warntxt.putline;
           warntxt.putline;
        else
           'DONE!'->putline;
       if);
   if);
   myloop:
     (for i:count repeat
          %getLongAt (names+4*(i-1)) -> xname;
          xname.get->realname[]->getfontsize->size;
          (* NB: realname is changed by getfontsize to include a "%d" *)

          scalableLoop:
            (if size=0 then (* JUBIIII: we have found a scalable font *)
                fname.clear;
                realname[]->fname.putformat
                (#
                do 1->d;
                #);
                fname->THIS(guienv).private.fonts.xfonts[nam].style[sty].size[1].name;
                THIS(guienv).private.fonts.xfonts[nam].style[sty].size[1][]
                  ->XLoadFontFromName;
                (if not(THIS(guienv).private.fonts.xfonts[nam].style[sty].size[1].loaded)
                    then leave scalableLoop;
                if);
                true->THIS(guienv).private.fonts.scalableExists;
                &text[]->THIS(guienv).private.fonts.scalableName[];
                realname->THIS(guienv).private.fonts.scalableName;
                THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range ->tmprange;
                (for fsize: (tmprange-1) repeat
                     fname.clear;
                     realname[]->fname.putformat
                     (#
                     do (fsize+1)->d;
                     #);
                     fname->THIS(guienv).private.fonts.xfonts[nam].style[sty].size[fsize+1].name;
                for);
                THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range->totalfonts;
                leave myloop
            if);
          (if (size<=60)and(size>0) then
              totalfonts+1->totalfonts;
              realname->THIS(guienv).private.fonts.xfonts[nam].style[sty].size[size].name;
          if);
     for);
   names->XFreeFontNames;

-- tryToFindScalableFont: descriptor --
(#
   fname, scalableName: ^text;
   maxnames: (# exit 100 #);
   names, count: @integer;
   xname: @cstring;
   nam: @FontName;
   sty: @Style;
   warntxt: ^text;
enter warntxt[]
do
   (if not THIS(guienv).private.fonts.scalableExists then
       '-*-*-*-normal-*-0-0-*-*-*-*-iso8859-1' -> fname[];
       (xdisplay, fname,maxnames,@@count) -> XListFonts -> names;
       myloop:
         (for i:count repeat
              %getLongAt (names+4*(i-1)) -> xname;
              (if (xname.get->scalableName[]->getfontsize) = 0 then (* JUBIIII: we have found a scalable font *)
                  (* NB: realname is changed by getfontsize to include a "%d" *)
                  true->THIS(guienv).private.fonts.scalableExists;
                  scalableName[]->THIS(guienv).private.fonts.scalableName[];
                  leave myloop;
              if);
         for);
       (if count=0 then
           'WARNING:'->warntxt.putline;
           'There is no scalable fonts available on the system.'->warntxt.putline;
           'Zoom will not function properly!!!'->warntxt.putline;
           warntxt.putline;
       if);
       names->XFreeFontNames;
   if);
#)

-- getfontsize: doPart --
do -1 ->mysize;
   (if t[]<>NONE then
       t.reset;
       myscan:
         t.scan
         (# getinteger: @
              t.getint(# syntaxerror::(# do -1->value;true->continue#)#);
         do
            (if ch='-' then
                (if (mypos+1->mypos)=7 then
                    (t.position,t.position+4)->t.sub->temp[];
                    (if ('-0-0-' -> temp.equal) then
                        (t.position,t.position+1)->t.delete;
                        ('-%d',t.position+1) -> t.insert;
                        0->mysize;
                     else
                        ;
                        (if (getinteger->mysize) = 0 then
                            -1 -> mysize;
                           (* workaround for a bug in X-Win32 *)
                        if);
                    if);
                    (if false then
                        getinteger->mysize;
                        (if mysize=0 then
                            t.position->sizepos;
                            t.get;
                            getinteger->mysize;
                            (if mysize=0 then
                                sizepos->t.position;
                                (t.position,t.position+1)->t.delete;
                                ('%d-',t.position+1) -> t.insert;
                             else -1 ->mysize;
                            if);
                        if);
                    if);
                    leave myscan
                if)
            if)
         #)
   if)

-- initBifrostFonts: doPart --
do
   (if debugGraphic then 'InitFonts ... ' -> putwarntext if);
   (for nam: THIS(guienv).private.fonts.xfonts.range repeat
        &THIS(guienv).private.fonts.stylearray[]
          -> THIS(guienv).private.fonts.xfonts[nam][];

        (for sty: stylepattern.style.range repeat
             &THIS(guienv).private.fonts.sizearray[]
               -> THIS(guienv).private.fonts.xfonts[nam].style[sty][];

             (for siz: sizepattern.size.range repeat
                  &THIS(guienv).private.xfont[]
                    -> THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz][];
                  false ->
                  THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz].loaded;

             for);
        for);
   for);

   InitXFontNames;
   (if THIS(guienv).private.fonts.scalableExists then
       THIS(guienv).private.fonts.defaultfont.name.clear;
       THIS(guienv).private.fonts.scalableName[]
         -> THIS(guienv).private.fonts.defaultfont.name.putformat
       (#
       do 1->d;
       #);
    else
       10->smallest;
       (for nam: THIS(guienv).private.fonts.xfonts.range repeat
            (for sty: THIS(guienv).private.fonts.xfonts[nam].style.range repeat
                 THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range->sizerange;
                 (if sizerange>10 then smallest->sizerange; if);
                 loop:
                   (for siz: sizerange repeat
                        (if not(THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz].name.empty) then
                            (if smallest>siz then
                                siz->smallest;
                                THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz][]->df[];
                            if);
                            leave loop;
                        if);
                 for);
            for);
       for);
       (if df[]=NONE then
           '8x13' -> THIS(guienv).private.fonts.defaultfont.name;
        else
           df.name -> THIS(guienv).private.fonts.defaultfont.name;
       if);
   if);
   THIS(guienv).private.fonts.defaultfont[] -> XLoadFontFromName;
   (if not THIS(guienv).private.fonts.defaultfont.loaded then
       '8x13' -> THIS(guienv).private.fonts.defaultfont.name;
       THIS(guienv).private.fonts.defaultfont[] -> XLoadFontFromName;
   if);
   (if not THIS(guienv).private.fonts.defaultfont.loaded then
       'FATAL ERROR: Failed to open defaultfont '->warntxt.puttext;
       THIS(guienv).private.fonts.defaultfont.name[]->warntxt.putline;
       (NONE, warntxt[], 'FATAL ERROR') ->alertUser;
       -1 -> result;
   if);
   true->THIS(guienv).private.fonts.initialized;
   (if debugGraphic then 'done' -> putwarnline if);

-- XCheckFontLoaded: doPart --
do
   (if debugGraphic then
       'XCheckFontLoaded: Name style size ' -> putwarntext;
       (nam,6) -> putwarnintwidth;
       (sty,6) -> putwarnintwidth;
       (siz,6) -> putwarnintwidthline;
   if);
   siz -> origsiz;
   nam -> orignam;
   sty -> origsty;
   false -> subst;

   (* range checks *)
   true -> legalsiz -> legalnam -> legalsty;
   (if ((nam<1) or (nam>THIS(guienv).private.fonts.xfonts.range)) then
       (* no such fontname *)
       true -> subst;
       Courier -> nam;
       false -> legalnam;
   if);
   (if ((sty<1) or (sty>THIS(guienv).private.fonts.xfonts[nam].style.range)) then
       true -> subst;
       Plain -> sty;
       false -> legalsty;
   if);
   (if (siz<1) then
       true -> subst;
       1->siz;
       false -> legalsiz;
   if);
   THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range -> range;
   (if siz>range then
       (* Due to zoom, this may often happen. Extend array to allow
        * caching of substitution font.
        *)
       true -> subst;
       siz-range -> THIS(guienv).private.fonts.xfonts[nam].style[sty].size.extend;
       (for i:(siz-range) repeat
            &THIS(guienv).private.xfont[]
              -> THIS(guienv).private.fonts.xfonts[nam].style[sty].size[range+i][];
            false ->
            THIS(guienv).private.fonts.xfonts[nam].style[sty].size[range+i].loaded;
       for);
       (if debugGraphic then
           'XCheckFontLoaded: Extended to Name style size ' -> putwarntext;
           (nam,6) -> putwarnintwidth;
           (sty,6) -> putwarnintwidth;
           (THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range,6)
             -> putwarnintwidthline;
       if);
       true -> legalsiz (* Can now be used as index *);
   if);
   (* check that it is loaded, and if not, do it *)
   THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz][]->xf[];
   (if (not xf.loaded) and xf.name.empty and THIS(guienv).private.fonts.scalableExists then
       true -> subst;
       THIS(guienv).private.fonts.scalableName[] -> xf.name.putformat
       (#
       do siz->d;
       #);
       xf[] -> XLoadFontFromName;
    else
       siz->tmpsiz;
       findfont:
         (#
         do
            THIS(guienv).private.fonts.xfonts[nam].style[sty].size[tmpsiz][]->xf[];
            (if not THIS(guienv).private.fonts.xfonts[nam].style[sty].size[tmpsiz].loaded then
                (if xf.name.empty then
                    true -> subst;
                    (if tmpsiz>1 then
                        tmpsiz-1->tmpsiz;
                        restart findfont;
                    if);
                    THIS(guienv).private.fonts.xfonts[nam].style[sty].size[tmpsiz][]
                      -> xf[];
                if);
                xf[] -> XLoadFontFromName;
                tmpsiz->siz;
            if)
         #);
   if);
   (if subst then
       'Sorry, the X server has no font corresponding to '->putwarntext;
       (if orignam
        // Helvetica then 'Helvetica-' -> putwarntext;
        // Times then 'Times-' -> putwarntext;
        // Courier then 'Courier-' -> putwarntext;
        else
           '<' -> putwarntext; orignam->putwarnint; '>' -> putwarntext;
       if);
       (if origsty
        // Plain then 'Plain-' -> putwarntext;
        // Bold then 'Bold-' -> putwarntext;
        // Italic then 'Italic-' -> putwarntext;
        else
           '<' -> putwarntext; origsty->putwarnint; '>' -> putwarntext;
       if);
       origsiz -> putwarnintline;
   if);
   (* here kan vi være sikker på at  "xf[] -> XLoadFontFromName;" er kaldt *)
   (if not xf.loaded then
       'Sorry, the X server has no font matching \''->putwarntext;
       xf.name[] -> putwarntext;
       '\''->putwarnline;
       true -> subst;
       THIS(guienv).private.fonts.defaultFont[] -> xf[];
   if);

   (if subst then
       'Substituting with font: \'' -> putwarntext;
       xf.name[] -> putwarntext;
       '\''->putwarnline;
       (if legalnam and legalsty and legalsiz then
           (* cache it to avoid work on next call of XCheckFontLoaded *)
           (if debugGraphic then
               'XCheckFontLoaded: Caching ' -> putwarntext;
               (nam, 6) -> putwarnintwidth;
               (sty, 6) -> putwarnintwidth;
               (siz, 6) -> putwarnintwidth;
               xf.name[] -> putwarnline;
           if);
           true ->
           THIS(guienv).private.fonts.xfonts[orignam]
           .style[origsty].size[origsiz].loaded;
           xf[] -> THIS(guienv).private.fonts.xfonts[orignam]
           .style[origsty].size[origsiz][];
       if)
   if);

   (if debugGraphic then
       'Font to be used: ' -> putwarntext;
       xf.name[] -> putwarntext;
       (xf.xfontid, 10) -> putwarnintwidthline
   if);

-- InitX11r3: descriptor --
(#
   warntxt: @text;
do
   warntxt[]->tryToFindScalableFont;
   (Times,Plain,warntxt[],'Times,Plain')->buildFontNames;
   (Times,Bold,warntxt[],'Times,Bold')->buildFontNames;
   (Times,Italic,warntxt[],'Times,Italic')->buildFontNames;

   (Helvetica,Plain,warntxt[],'Helvetica,Plain')->buildFontNames;
   (Helvetica,Bold,warntxt[],'Helvetica,Bold')->buildFontNames;
   (Helvetica,Italic,warntxt[],'Helvetica,Italic')->buildFontNames;

   (Courier,Plain,warntxt[],'Courier,Plain')->buildFontNames;
   (Courier,Bold,warntxt[],'Courier,Bold')->buildFontNames;
   (Courier,Italic,warntxt[],'Courier,Italic')->buildFontNames;
   (if displaywarnings and (not warntxt.empty) then
       warntxt[] -> putwarnline;
   if);
#)
