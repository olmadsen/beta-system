ORIGIN '../Impl/BifrostImpl';

BODY 'AbstractShapeX11impl';
BODY 'TextX11impl';
BODY 'PaintX11impl';
BODY 'RasterX11impl';
BODY 'CanvasX11impl';
BODY 'ShapeX11impl';

INCLUDE 'DatatypesX11impl';
INCLUDE '../Impl/RectangleList';
INCLUDE '~beta/guienv/v1.4/private/X11/guienv_unixprivate';
INCLUDE '~beta/guienv/v1.4/private/X11/callbacks';

(***************  Cap and join constants *****************)

-- CapButtBody: dopart --
do 1->s 

-- CapRoundedBody: dopart --
do 2->s 

-- CapSquareBody: dopart --
do 3->s (* CapProjecting *) 


-- JoinMiterBody: dopart --
do 0->s 

-- JoinRoundBody: dopart --
do 1->s 

-- JoinBevelBody: dopart --
do 2->s 


(**** ABSTRACT SHAPE *****)

-- AShapeImplPrivatePart: descriptor --
(#
   XPoints: ^XPointDesc;
   noOfPoints: @Integer;
   HilitePoints: ^XPointDesc ; (* used to draw outline hilite *)
#)


(****** TEXT *****)


-- BifrostAttributes: attributes --

xdisplay: (# exit THIS(guienv).private.display #); 

SetXFont:
  (# theCanvas: ^BifrostCanvas;
     nam: @FontName;
     sty: @Style;
     siz: @Integer;
  enter (theCanvas[], nam, sty, siz)
  do 
     (* find the xfont to be used from nam, sty, siz and
      * set it as current font in theCanvas.
      *)
     <<SLOT SetXFontBody: descriptor>>;
  exit (nam, sty, siz)
  #);

(********** RASTER ***********)

-- BitMapPrivatePart: descriptor --
(# xpixmap: @integer;
   data: [1]@integer;
   realwidth: @integer; (* multiple of 8 *)
   padright: @integer;
   (* Number of bits needed to make width multiple of 8 *)
#)

-- GrayMapPrivatePart: descriptor --
(# #)

-- PixMapPrivatePart: descriptor --
(# ximage: @integer;
   pixels: @integer;
   maxRGB: @Integer;
#)

(********** BifrostCanvas ***********)

-- CanvasAttributes: attributes --

SetCanvasBorderPixel: 
  (# p: @Integer; 
  enter p
  do p     -> implpart.borderpixel;
     true  -> implpart.borderpixelset;
     false -> implpart.borderpixmapset;
  #);

SetCanvasBackgroundPixel: 
  (# p: @Integer; 
  enter p
  do p     -> implpart.backGroundpixel;
     true  -> implpart.backGroundPixelSet;
     false -> implpart.backGroundPixmapSet;
  #);

SetCanvasBorderPixmap: 
  (# p: @Integer; 
  enter p
  do p     -> implpart.borderpixmap;
     true  -> implpart.borderpixmapset;
     false -> implpart.borderpixelset;
  #);

SetCanvasBackgroundPixmap: 
  (# p: @Integer; 
  enter p
  do p     -> implpart.backGroundpixmap;
     true  -> implpart.backGroundPixmapSet;
     false -> implpart.backGroundPixelSet;
  #);

-- canvasPrivateimplPart: descriptor --
(# 
   xgc: @Integer;
   xdrawable: @Integer;
   xcolormap: @Integer;
   
   borderwidth: @integer;
   
   args: @ArgList;
   
   xevent: @XAnyEvent;     (* used for XNextEvent in interaction *)
   xbutton: @XButtonEvent; (* cast of xevent, used in interaction *)
   xkey: @XKeyEvent;       (* cast of xevent, used in interaction *)
   lastbut, prevbut, dbltime: @integer
     (* Used to detect doubleclicks *);
   
   XPoints: ^XpointDesc;   (* used in immediatemultiline *)
   clipRegion: @Integer;
   
   currentXfont: ^THIS(guienv).private.xfont;
   
   opened: @boolean;
   
   canvasbutton: @XButtonEvent;  (* Last buttonevent processed by eventProcessor *)
		
   DamagedList: ^RectangleList;
   
   defaultbackground: @boolean;
   
   backGroundpixel, borderpixel: @integer;
   backGroundPixelSet, borderpixelset: @boolean;
   
   backGroundpixmap, borderpixmap: @integer;
   backGroundPixmapSet, borderpixmapset: @boolean;
   
   OpenSelector: exposeEventSelector
     (* Handles first expose *)
     (# cwatt: @XSetWindowAttributes;
        cwmask: @integer;
     <<SLOT CanvasOpenHandler: dopart>>
     #);
   OpenHandler: eventProcessor
      (#
      do &OpenSelector[] -> es[];
      #);
   openHandlerAddr: @integer;
#)
