ORIGIN 'BifrostX11impl';
INCLUDE 'CanvasXtImpl';


--CanvasAttributes: attributes--
XtRemoveEventHandler  : external
  (# w : @integer;
     eventMask :@integer;
     nonmaskable :@integer;
     callBack :@ integer; (* difference from the one in xtlib *)
     data : @integer
  enter (w,eventMask,nonmaskable,callBack,data)
  do callC
  #);

-- CanvasOpenHandler: dopart --
do (if opened//false then
       (* getwindowID *)
       WindowId -> xdrawable;
       (* Use SouthWest bit_gravity: matches our coordinatesystem *)
       SouthWestGravity -> cwatt.bitgravity;
       CWBitGravity -> cwmask;
       (xdisplay,
       xdrawable,
       cwmask,
       cwatt.address) -> XChangeWindowAttributes;
       
       (if borderwidth//0 then else
           true->SetAnswer; (* guienv hack *)
           (WidgetID,XtNborderWidth,borderwidth) -> IntegerResourceSet;
           false->SetAnswer; (* guienv hack *)
       if);
       
       (if borderpixelset//true then
           (xdisplay, 
           xdrawable,
           borderpixel) -> XSetWindowBorder;
       if);
       (if borderpixmapset//true then
           (xdisplay, 
           xdrawable,
           borderpixmap) -> XSetWindowBorderPixmap;
       if);
       (if defaultbackground//false then
           (if backgroundpixelset//true then
               (xdisplay, 
               xdrawable,
               backgroundpixel) -> XSetWindowBackGround;
               
               (xdisplay, 
               xdrawable,
               0, 0, 0, 0, 0) -> XClearArea;
           if);
           (if backgroundpixmapset//true then
               (xdisplay, 
               xdrawable,
               backgroundpixmap) -> XSetWindowBackGroundPixmap;
               
               (xdisplay, 
               xdrawable,
               0, 0, 0, 0, 0) -> XClearArea;
           if);
           (if (backgroundpixmapset or backgroundpixelset)//false then
               (xdisplay, 
               xdrawable,
               THIS(guienv).private.whitepixelvalue) -> XSetWindowBackGround;
               (xdisplay, 
               xdrawable,
               0, 0, 0, 0, 0) -> XClearArea;
           if);
       if);
       &RectangleList[] -> damagedlist[];
       damagedlist.init;
       UpdateCoordinateSystem;
       true -> opened;
       theEventhandler.onOpen;
       (WidgetID, XExposureMask, false, openHandlerAddr, 0)
         -> XtRemoveEventhandler;
    else
       'ERROR: Openhandler called again' -> putline;
   if);
 
