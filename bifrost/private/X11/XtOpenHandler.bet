ORIGIN 'BifrostX11impl';
LIB_ITEM 'bifrost';
INCLUDE 'CanvasXtImpl';


--CanvasAttributes: attributes--
XtRemoveEventHandler: external
  (# w: @integer;
     eventMask: @integer;
     nonmaskable: @integer;
     callBack: @integer; (* difference from the one in xtlib *)
     data: @integer
  enter (w,eventMask,nonmaskable,callBack,data)
  do callC
  #);

-- CanvasOpenHandler: dopart --
do (if not opened then
       (* getwindowID *)
       WindowId -> xdrawable;
       (* Use SouthWest bit_gravity: matches our coordinatesystem *)
       SouthWestGravity -> cwatt.bitgravity;
       CWBitGravity -> cwmask;
       (THIS(guienv).private.display,
       xdrawable,
       cwmask,
       cwatt.address) -> XChangeWindowAttributes;
       
       (if borderwidth//0 then else
           true->SetAnswer; (* guienv hack *)
           (XtNborderWidth,borderwidth) -> SetIntegerResource;
           false->SetAnswer; (* guienv hack *)
       if);
       
       (if borderpixelset then
           (THIS(guienv).private.display, 
           xdrawable,
           borderpixel) -> XSetWindowBorder;
       if);
       (if borderpixmapset then
           (THIS(guienv).private.display, 
           xdrawable,
           borderpixmap) -> XSetWindowBorderPixmap;
       if);
       (if not defaultbackground then
           (if backgroundpixelset then
               (THIS(guienv).private.display, 
               xdrawable,
               backgroundpixel) -> XSetWindowBackGround;
               
               (THIS(guienv).private.display, 
               xdrawable,
               0, 0, 0, 0, 0) -> XClearArea;
           if);
           (if backgroundpixmapset then
               (THIS(guienv).private.display, 
               xdrawable,
               backgroundpixmap) -> XSetWindowBackGroundPixmap;
               
               (THIS(guienv).private.display, 
               xdrawable,
               0, 0, 0, 0, 0) -> XClearArea;
           if);
           (if not (backgroundpixmapset or backgroundpixelset) then
               (THIS(guienv).private.display, 
               xdrawable,
               THIS(guienv).private.whitepixelvalue) -> XSetWindowBackGround;
               (THIS(guienv).private.display, 
               xdrawable,
               0, 0, 0, 0, 0) -> XClearArea;
           if);
       if);
       &RectangleList[] -> damagedlist[];
       damagedlist.init;
       UpdateCoordinateSystem;
       true -> opened;
       theEventhandler.onOpen;
       (WidgetID, XExposureMask, 0, openHandlerAddr, 0)
         -> XtRemoveEventhandler;
    else
       'ERROR: Openhandler called again' -> putwarnline;
   if);
 
