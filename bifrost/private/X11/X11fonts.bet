ORIGIN 'BifrostX11impl';
-- CanvasImmediateTextBody: descriptor --
(# (* Need to be in this fragmentgroup to see private attributes of 
    * font info
    *)
   width: @integer;
   xf: ^THIS(guienv).private.xfont;
   nam: @FontName;
   sty: @Style;
   siz: @Integer;
do
   (THIS(BifrostCanvas)[], theFontName, theStyle, theSize) 
     -> SetXFont
     -> (xf[], nam, sty, siz);
   
   (xdisplay, 
   implpart.xdrawable, 
   implpart.xgc,
   pos.x,
   pos.y,
   thestring,
   thestring.lgth) -> XDrawString;
   
   (if underline then
       (if xf.fixedwidth then 
           thestring.length*xf.width -> width;
        else
           (xf.fi, thestring.t, thestring.t.range) -> XTextWidth -> width;
       if);
       
       (xdisplay,
       implpart.xdrawable, 
       implpart.xgc,
       pos.x,
       pos.y + xf.upos,
       width,
       xf.uthick)
         -> XFillRectangle;
   if);
#)

-- SetXFontBody: descriptor --
(# 
   UseScalableFonts: (# exit false #);
   fname:@text;
   succes: @Boolean;
   
do siz -> theCanvas.ZoomDim -> siz;
   false->succes;
   (if UseScalableFonts then
       '-*-%s-%s-normal-*-%d-*-*-*-p-*-iso8859-1' ->
       fname.putFormat
       (# 
       do nam->Fontname2Text->s; sty->Fontstyle2Text->s; siz->d;
       #);
       &THIS(guienv).private.xfont[]->xf[];
       fname->xf.name;
       false->xf.loaded;
       'Trying to load: ' ->puttext; fname[] -> putline;
       xf[]->XLoadFontFromName;
       xf.loaded->succes;
   if);
   (if not succes then
       (nam,sty,siz) -> XCheckFontLoaded -> (xf[],nam,sty,siz);
   if);
   (* Set font in GC *)
   (xdisplay, theCanvas.implpart.xgc, xf.XfontId) -> XSetFont;
   
   (* Temporarily store the current font in privatePart of theCanvas *)
   xf[] -> theCanvas.implpart.currentxfont[];
#)
                                  
-- BifrostAttributes: attributes -- 
initBifrostFonts: 
  (# d: @Text;
     stylepattern: @THIS(guienv).private.fonts.stylearray;
     sizepattern:  @THIS(guienv).private.fonts.sizearray;
  do 
     (if debugGraphic then 'InitFonts ... ' -> putwarntext if);
     (for nam: THIS(guienv).private.fonts.xfonts.range repeat
          &THIS(guienv).private.fonts.stylearray[] 
            -> THIS(guienv).private.fonts.xfonts[nam][];
          
          (for sty: stylepattern.style.range repeat
               &THIS(guienv).private.fonts.sizearray[] 
                 -> THIS(guienv).private.fonts.xfonts[nam].style[sty][];
               
               (for siz: sizepattern.size.range repeat
                    &THIS(guienv).private.xfont[] 
                      -> THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz][];
                    false ->
                    THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz].loaded;
                    
               for);
          for);
     for);
     
     InitXFontNames;
     (if THIS(guienv).private.fonts.defaultfont.name.empty then
         '-*-*-*-normal-*-1-*-*-*-*-*-iso8859-1' -> THIS(guienv).private.fonts.defaultfont.name;
     if);
     THIS(guienv).private.fonts.defaultfont[] -> XLoadFontFromName;
     (if not THIS(guienv).private.fonts.defaultfont.loaded then
         '8x13' -> THIS(guienv).private.fonts.defaultfont.name;
         THIS(guienv).private.fonts.defaultfont[] -> XLoadFontFromName;
     if);
     (if not THIS(guienv).private.fonts.defaultfont.loaded then
         'FATAL ERROR: Failed to open defaultfont '->puttext;
         THIS(guienv).private.fonts.defaultfont.name[]->putline;
         stop; (* FIXME: Should call bifrost.fatalerror instead *)
     if);
     true->THIS(guienv).private.fonts.initialized;
     (if debugGraphic then 'done' -> putwarnline if);
  #);

XCheckFontLoaded:
  (# nam, orignam: @FontName;
     sty, origsty: @Style;
     siz, origsiz, tmpsiz, range: @integer;
     legalsiz, legalnam, legalsty, subst: @boolean;
     xf: ^THIS(guienv).private.xfont;
  enter (nam,sty,siz)
  do 
     (if debugGraphic then 
         'XCheckFontLoaded: Name style size ' -> putwarntext;
         (nam,6) -> putwarnintwidth;
         (sty,6) -> putwarnintwidth;
         (siz,6) -> putwarnintwidthline;
     if);
     siz -> origsiz;
     nam -> orignam;
     sty -> origsty;
     false -> subst;
     
     (* range checks *)
     true -> legalsiz -> legalnam -> legalsty;
     (if ((nam<1) or (nam>THIS(guienv).private.fonts.xfonts.range)) then
         (* no such fontname *)
         true -> subst;
         Courier -> nam;
         false -> legalnam;
     if);
     (if ((sty<1) or (sty>THIS(guienv).private.fonts.xfonts[nam].style.range)) then
         true -> subst;
         Plain -> sty;
         false -> legalsty;
     if);
     (if (siz<1) then 
         true -> subst;
         1->siz; 
         false -> legalsiz;
     if);
     THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range -> range;
     (if siz>range then
         (* Due to zoom, this may often happen. Extend array to allow
          * caching of substitution font.
          *)
         true -> subst;
         siz-range -> THIS(guienv).private.fonts.xfonts[nam].style[sty].size.extend;
         (for i:(siz-range) repeat
              &THIS(guienv).private.xfont[] 
                -> THIS(guienv).private.fonts.xfonts[nam].style[sty].size[range+i][];
              false ->
              THIS(guienv).private.fonts.xfonts[nam].style[sty].size[range+i].loaded;
         for);
         (if debugGraphic then 
             'XCheckFontLoaded: Extended to Name style size ' -> putwarntext;
             (nam,6) -> putwarnintwidth;
             (sty,6) -> putwarnintwidth;
             (THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range,6)
               -> putwarnintwidthline;
         if);
         true -> legalsiz (* Can now be used as index *);
     if);
     (if not THIS(guienv).private.fonts.xfonts[nam].style[sty].size[siz].loaded then
         siz->tmpsiz;
         THIS(guienv).private.fonts.xfonts[nam].style[sty].size[tmpsiz][]->xf[];
         (* check that it is loaded, and if not, do it *)
         findfont:
           (if not THIS(guienv).private.fonts.xfonts[nam].style[sty].size[tmpsiz].loaded then
               (if xf.name.empty then
                   true -> subst;
                   (if tmpsiz>1 then
                       tmpsiz-1->tmpsiz;
                       restart findfont;
                   if);
                   THIS(guienv).private.fonts.xfonts[nam].style[sty].size[tmpsiz][]
                     -> xf[];
               if);
               xf[] -> XLoadFontFromName;
           if)
     if);
     (if subst then
         'Sorry, the X server has no font corresponding to '->putwarntext;
         (if orignam
          // Helvetica then 'Helvetica-' -> putwarntext;
          // Times then 'Times-' -> putwarntext;
          // Courier then 'Courier-' -> putwarntext;
          else 
             '<' -> putwarntext; orignam->putwarnint; '>' -> putwarntext;
         if);
         (if origsty
          // Plain then 'Plain-' -> putwarntext;
          // Bold then 'Bold-' -> putwarntext;
          // Italic then 'Courier-' -> putwarntext;
          else 
             '<' -> putwarntext; origsty->putwarnint; '>' -> putwarntext;
         if);
         origsiz -> putwarnintline;
     if);
     
     (if not xf.loaded then
         'Sorry, the X server has no font matching \''->putwarntext;
         xf.name[] -> putwarntext;
         '\''->putwarnline;
         true -> subst;
         (if THIS(guienv).private.fonts.scalableExists then
             origsiz->tmpsiz;
             (if tmpsiz>7 then tmpsiz-1->tmpsiz 
              else (if tmpsize<1 then 1->tmpsize; if);
             if); 
             NONE->xf[];
             findfont:
               (for i:THIS(guienv).private.fonts.xfonts.range repeat
                      (for j: THIS(guienv).private.fonts.xfonts[nam].style.range repeat
                           (if not 
                               THIS(guienv).private.fonts.xfonts[i].style[j].size[tmpsiz].name.empty
                               then
                               THIS(guienv).private.fonts.xfonts[i].style[j].size[tmpsiz][]->xf[];
                               leave findfont;
                           if)
                      for)
               for);
             (if xf[]=NONE then
                 THIS(guienv).private.fonts.defaultFont[] -> xf[];
              else 
                 (if not xf.loaded then
                     xf[] -> XLoadFontFromName; 
                     (if not xf.loaded then 
                         THIS(guienv).private.fonts.defaultFont[] -> xf[];
                     if);
                 if);
             if);
         if);
     if);
         
     (if subst then
         'Substituting with font: \'' -> putwarntext;
         xf.name[] -> putwarntext;
         '\''->putwarnline;
         (if legalnam and legalsty and legalsiz then
             (* cache it to avoid work on next call of XCheckFontLoaded *)
             (if debugGraphic then 
                 'XCheckFontLoaded: Caching ' -> putwarntext;
                 (nam, 6) -> putwarnintwidth;
                 (sty, 6) -> putwarnintwidth;
                 (siz, 6) -> putwarnintwidth;
                 xf.name[] -> putwarnline;
             if);
             true ->
             THIS(guienv).private.fonts.xfonts[orignam]
             .style[origsty].size[origsiz].loaded;
             xf[] -> THIS(guienv).private.fonts.xfonts[orignam]
             .style[origsty].size[origsiz][];
         if)
     if);
         
     (if debugGraphic then
         'Font to be used: ' -> putwarntext;
         xf.name[] -> putwarntext;
         (xf.xfontid, 10) -> putwarnintwidthline
     if);
     
  exit (xf[], nam, sty, siz)
  #);


XLoadFontFromName:
  (# xf: ^THIS(guienv).private.xfont;
  enter xf[]
  do
     (if ((xdisplay, xf.name) -> XLoadQueryFont -> xf.fi)
      //0 then
         (* font does not exist *)
         false -> xf.loaded;
         false -> xf.fixedWidth;
         -1-> xf.height
           -> xf.width
           -> xf.baseLine
           -> xf.firstCharInFont
           -> xf.lastCharInFont;
      else
         xf.fi.id
           -> xf.XFontId;
         
         (xf.fi.maxBounds.width =
         xf.fi.minBounds.width)
           -> xf.fixedWidth;
         xf.fi.maxBounds.width
           -> xf.width;
         xf.fi.maxBounds.ascent 
         + xf.fi.maxBounds.descent
           -> xf.height;
         xf.fi.ascent
           -> xf.baseLine;
         xf.fi.minCharOrByte2
           -> xf.firstCharInFont;
         xf.fi.maxCharOrByte2
           -> xf.lastCharInFont;
         
         true -> xf.loaded;
         xf.fi.maxbounds.descent -> xf.maxdescent;
         xf.fi.maxbounds.ascent -> xf.maxascent;
     if);
  #);


InitXFontNames:
  (# revision: @Integer;
     t: @text;
  do
     xdisplay -> XVendorRelease -> revision;
     (*(if revision >= 1000 then revision div 1000 -> revision if);*)
     (if revision
      // 0 // 1 // 2 then (* X11, X11.1, X11.2 *)
         &InitX11r0;
      else (* X11.{3,4,...}: Using adobe fonts *)
         &InitX11r3
     if);
  #);


InitX11r0:
  (#
  do (NONE, 
     'X Font Initialization: X11 servers prior to X11R3 not supported',
     'Unsupported X version') -> AlertUser;
  #);

InitX11r3:
  (#
  do 
     (Times,Plain,'Times,Plain')->buildFontNames;
     (Times,Bold,'Times,Bold')->buildFontNames;
     (Times,Italic,'Times,Italic')->buildFontNames;
     
     (Helvetica,Plain,'Helvetica,Plain')->buildFontNames;
     (Helvetica,Bold,'Helvetica,Bold')->buildFontNames;
     (Helvetica,Italic,'Helvetica,Italic')->buildFontNames;
     
     (Courier,Plain,'Courier,Plain')->buildFontNames;
     (Courier,Bold,'Courier,Bold')->buildFontNames;
     (Courier,Italic,'Courier,Italic')->buildFontNames;

  #); 

buildFontNames:
  (# 
     onlyAdobeFonts: (# exit false #);
     fname: @text;
     textname,tmpname,realname,scaleName: ^text;
     maxnames: (# exit 100 #);
     scaleExist: @Boolean;
     names, count, tmprange, size, totalfonts: @integer;
     xname: @cstring;
     nam: @FontName;
     sty: @Style;
     getfontsize:
       (# mypos,mysize,sizepos: @integer;
          t: ^text;
       enter t[]
       do -1 ->mysize;
          (if t[]<>NONE then
              t.reset;
              myscan:
                t.scan
                (# 
                do 
                   (if ch='-' then
                       (if (mypos+1->mypos)=7 then
                           t.getint->mysize;
                           (if mysize=0 then
                               t.position->sizepos;
                               t.get;t.getint->mysize;
                               (if mysize=0 then
                                   sizepos->t.position;
                                   (t.position,t.position+1)->t.delete;
                                   ('%d-',t.position+1) -> t.insert;
                                else -1 ->mysize;
                               if);
                           if);
                           leave myscan
                       if)
                   if)
                #)
          if)
       exit mysize
       #);
  enter (nam,sty,textname[])
  do
     (if onlyAdobeFonts then
         '-adobe-%s-%s-normal-*-*-*-*-*-*-*-iso8859-1' -> tmpname[];
      else
         '-*-%s-%s-normal-*-*-*-*-*-*-*-iso8859-1' -> tmpname[];
     if);
     
     tmpname[]->fname.putFormat
     (# 
     do nam->Fontname2Text->s; sty->Fontstyle2Text->s;
     #);
     
     (xdisplay, fname,maxnames,@@count) -> XListFonts -> names;
     (if count=0 then
         putline;
         'Coud not find any fonts of type: '->puttext; textname[]->putline;
         'Please consider installing the above type of fonts.'->putline;
         'Substituting it with something else...'->putline;
         '-*-*-%s-normal-*-*-*-*-*-*-*-iso8859-1' -> tmpname[];
         fname.clear;
         tmpname[]->fname.putFormat
         (# 
         do sty->Fontstyle2Text->s;
         #);
         (xdisplay, fname,maxnames,@@count) -> XListFonts -> names;
         (if count=0 then
             'WARNING: too few fonts available'->putline;
             'WARNING: too few fonts available'->putline;
         if);
     if);
     myloop:
       (for i:count repeat
            %getLongAt (names+4*(i-1)) -> xname;
            xname.get->realname[]->getfontsize->size;
            (* NB: realname is changed by getfontsize to include a "%d" *)
            
            (if size=0 then (* JUBIIII: we have found a scalable font *)
                true->scaleExist;
                realname[]->scaleName[];
                THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range->tmprange;
                (for fsize: tmprange repeat
                     fname.clear;
                     realname[]->fname.putformat
                     (# 
                     do fsize->d;
                     #);
                     fname->THIS(guienv).private.fonts.xfonts[nam].style[sty].size[fsize].name;
                for);
                THIS(guienv).private.fonts.xfonts[nam].style[sty].size.range->totalfonts;
                leave myloop
            if);
	    (if (size<=60)and(size>0) then
                totalfonts+1->totalfonts;
                realname->THIS(guienv).private.fonts.xfonts[nam].style[sty].size[size].name;
            if);
       for);
     names->XFreeFontNames;
     (if true then
         '%d fonts of type %s is found'->putFormat
         (# 
         do totalfonts->d; fname[]->s;
         #);
         putline;
     if);
     (if scaleExist then 
         true->THIS(guienv).private.fonts.scalableExists;
         fname.clear;
         scaleName[]->fname.putformat
         (# 
         do 1->d;
         #);
         THIS(guienv).private.fonts.defaultfont.name.clear;
         fname->THIS(guienv).private.fonts.defaultfont.name;
     if);
  #);
