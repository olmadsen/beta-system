ORIGIN '../../SelectionPicture';
INCLUDE 'BifrostImpl';

--SelectionPictureInit: dopart--
do (if theCanvas[]=NONE then
       'SelectionPicture.init: theCanvas is NONE' -> putwarnline;
   if);
   true -> private.hilited
   
--SelectionPictureCopy: descriptor--
(# p: ^SelectionPicture;
do INNER copy;
   (if aCopy[] = NONE then
       &SelectionPicture[] -> p[] -> aCopy[];
   if);
   aCopy[] -> p[];
   thecanvas[] -> p.thecanvas[];
#)

--SelectionPictureDraw: dopart--
do (if not doneInInner then
       'Error to draw a SelectionPicture!' -> putwarnline;
       'The GOs should already be in a Canvas!' -> putwarnline;
       true -> doneInInner;
   if);
   
--SelectionPictureErase: dopart--
do (if not doneInInner then
       'Error to erase a SelectionPicture!' -> putwarnline;
       true -> doneInInner;
   if);
   
--SelectionPictureAdd: dopart--
do (if debugGraphic then
       'SelectionPicture.add: GOs: ' -> putwarntext;
       NoOfGOs -> putwarnintline;
   if);
   (if NoOfGOs
    // 1 then (* only one selected; hilite controls of it *)
       go.theShape.hc[] -> go.theShape.drawHilite[];
       theCanvas[] -> go.hilite;
       onOneGO;
    // 2 then (* two selected; start to hilite outline *)
       theShape.ho[] -> theShape.drawhilite[];
       go.theShape.ho[] -> go.theShape.drawHilite[];
       theCanvas[] -> go.hilite;
       
       firstGO -> go[];
       theCanvas[] -> go.unhilite;
       go.theShape.ho[] -> go.theShape.drawHilite[];
       theCanvas[] -> go.hilite;
       onTwoGOs;
    else 
       (* more than two selected; hilite outline *)
       go.theShape.ho[] -> go.theShape.drawHilite[];
       theCanvas[] -> go.hilite;
   if);
   
--SelectionPictureDelete: dopart--
do (if debugGraphic then
       'SelectionPicure.delete: GOs: ' -> putwarntext; 
       NoOfGOs -> putwarnintline;
   if);
   theCanvas[] -> go.unhilite;
   go.theShape.hc[] -> go.theShape.drawHilite[];
   (if NoOfGOs
    // 0 then (* no more *)
       onEmpty;
    // 1 then (* only one remaining; hilite controls *)
       firstGO -> go[];
       theCanvas[] -> go.unhilite;
       go.theShape.hc[] -> go.theShape.drawHilite[];
       theCanvas[] -> go.hilite;
       theShape.hc[] -> theShape.drawhilite[];
       onOneGO;
   if);
   
--SelectionPictureClear: dopart--
do scanGOs(# do go[] -> delete #);
   idmatrix -> TM;
