ORIGIN 'BifrostImpl';
BODY '../Predefined/TextImpl'
'../Predefined/RectImpl'
'../Predefined/EllipseImpl'
'../Predefined/PieImpl'
'../Predefined/LineImpl'
'../Predefined/MultilineImpl'
'../Predefined/ArcImpl';
(*********************** PREDEFINED SHAPE ********************************)

-- PrePrivate: descriptor --
(#
   calculatedShape: ^Shape; (* Caching of calculated approximating Shape*)
#)

-- PreShapeInvalidate: dopart --
do INNER;
   
-- PreShapeExitShape: dopart --
do preprivate.calculatedshape.copy -> s[] 
   
-- PreShapeContainsPoint: dopart --
do INNER;
   
-- PreShapeTransform: descriptor --
(# b: @boolean;
do 
   (* (if m[]<>IDmatrix then *)
   (m.a = 1.0) -> b;
   (m.b = 0.0) and b -> b;
   (m.c = 0.0) and b -> b;
   (m.d = 1.0) and b -> b;
   (if not b then
       (if displaywarnings then
           'Warning: Attempt to rotate/scale a PredefinedShape (' -> puttext;
           THIS(PredefinedShape)[] -> getpatternname -> puttext;
           ').' -> putline;
           'PredefinedShapes are only guarantied to be moveable!' -> putline;
       if);
   if);
#)


(*********************** STROKEABLE SHAPE ********************************)

-- StrokeableGetBounds: dopart --
do INNER;
   (if privatepart.boundinvalid then
       (if stroked then
           (privatepart.bound, (strokewidth+1) div 2) -> ExpandRectangle 
             -> privatepart.bound;
       if);
       false -> privatepart.boundinvalid;
   if);
   
   
-- StrokeableCopyBody: descriptor --
(# str: ^StrokeableShape;
do (if aCopy[]//NONE then (* Not done in inner *)
       (if displayWarnings then
           'Copy: Attempt to instantiate StrokeableShape'
             -> putline;
       if);
    else
       aCopy[] -> str[];
       stroked -> str.stroked;
       strokewidth -> str.strokewidth;
   if);
#)
