ORIGIN '../../PredefinedGO';
INCLUDE 'BifrostImpl';

(******************** PREDEFINED GRAPHICAL OBJECTS *************************)

-- PredefinedGOenterTM: descriptor -- 
(# b: @boolean;
do 
   (* (if m[]<>IDmatrix//true then *)
   (m.a = 1.0) -> b;
   (m.b = 0.0) and b -> b;
   (m.c = 0.0) and b -> b;
   (m.d = 1.0) and b -> b;
   (if b
    // false then
       (if displaywarnings // true then
           'Warning: Attempt to rotate/scale a PredefinedGraphicalObject (' -> puttext;
           THIS(PredefinedGraphicalObject)[] -> getpatternname -> puttext;
           ').' -> putline;
           'PredefinedGraphicalObjects are only guarantied to be moveable!' -> putline;
       if);
   if);
#)
-- PGOinitBody: dopart --
do theShape.open;
   INNER;

(******************************* Lines **************************************)


-- LineDrawBody: dopart --
do (theCanvas[],theShape[]) -> thePaint.FillLine; 
   INNER;

-- LineCopyBody: descriptor --
(# l: ^Line;
do (if aCopy[]//NONE then (* Not done in inner *)
       &Line[]-> l[];
       l[] -> aCopy[];
   if);
#)

(****************************** MultiLine *********************************)


-- MultiLinedrawBody: dopart --
do (theCanvas[],theShape[]) -> thePaint.fillMultiLine;


-- MultiLineCopyBody: descriptor --
(# l: ^MultiLine;
do (if aCopy[]//NONE then (* Not done in inner *)
       &MultiLine[]-> l[];
       l[] -> aCopy[];
   if);
#)

(***************************** Arc ***************************)


-- ArcCopyBody: descriptor --
(# a: ^Arc;
do (if aCopy[]//NONE then (* Not done in inner *)
       &Arc[] -> a[];
       a[] -> aCopy[];
   if);
#)

-- ArcDrawBody: dopart --
do (theCanvas[],theShape[]) -> thePaint.FillArc;
   INNER;

(***************************** PieSlice ***************************************)

-- PieCopyBody: descriptor --
(# p: ^PieSlice;
do (if aCopy[]//NONE then (* Not done in inner *)
       &PieSlice[] -> p[];
       p[] -> aCopy[];
   if);
#)


-- PieDrawBody: dopart --
do (theCanvas[],theShape[]) -> thepaint.fillPie;
   INNER;


(******************************** Rects ***********************************)


-- RectDrawBody: dopart --
do (theCanvas[],theShape[]) -> thePaint.FillRect;
   INNER

-- RectCopyBody: descriptor --
(# r: ^Rect;
do (if aCopy[]//NONE then (* Not done in inner *)
       &Rect[] -> r[];
       r[] -> aCopy[];
   if);
#)

(***************************** Ellipses ***********************************)


-- EllipseDrawBody: dopart --
do (if thecanvas[]//none then 'EllipseDrawBody: canvas is none'->putline if);
   (theCanvas[],theShape[]) -> thePaint.FillEllipse;
   INNER

-- EllipseCopyBody: descriptor --
(# e: ^Ellipse;
do (if aCopy[]//NONE then (* Not done in inner *)
       &Ellipse[] -> e[];
       e[] -> aCopy[];
   if);
#)


(*************** GraphicText *******************)

-- GTextDrawBody: dopart --
do (theCanvas[],theShape[]) -> thePaint.FillText; INNER;


-- GTextCopyBody: dopart --
do INNER;
   (if aCopy[]//NONE then (* Not done in inner *)
       &Graphictext[] -> aCopy[];
   if);


-- GTextInteractiveCreateShape: dopart --
do
   thecanvas.endInteraction;  
   (* we like to draw the chars in their right color  *)
   thePaint[] -> thecanvas.privatePart.interactionpaint[];
   (theCanvas[], startPoint, theModifier) -> theShape.interactiveCreate -> lastCh;
   true -> doneininner;


-- GTextInteractiveReshape: dopart --
do thecanvas.endInteraction;  
   (* we like to draw the chars in their right color  *)
   thePaint[] -> thecanvas.privatePart.interactionpaint[];
   (theCanvas[], startPoint, theModifier) -> theShape.interactiveReshape -> lastCh;
   true -> doneininner;

