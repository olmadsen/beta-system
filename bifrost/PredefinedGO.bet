(************************************************************)
(************** PredefinedGraphicalObject *******************)
(************************************************************)

ORIGIN 'Bifrost';
BODY 'private/Impl/PredefinedGoImpl'

(*
 * COPYRIGHT 
 *       Copyright Mjolner Informatics, 1990-94 
 *       All rights reserved.
 *)

-- BifrostAttributes: attributes --

PredefinedGraphicalObject: AbstractGraphicalObject
  (# shapeDesc::<
       PredefinedShape;
     TMDesc::< 
       (# enterTM::< (# do <<SLOT PredefinedGOenterTM: descriptor>>; INNER #)#);
     init::< 
       (# <<SLOT PGOinitBody: dopart>> #);
     getShape::< 
       (# do theShape.calculateShape -> s[] #);
  do INNER;
  #);

(************************************************************)
(************************* Line *****************************)
(************************************************************)

Line: PredefinedGraphicalObject
  (# shapeDesc::< LineShape;
     
     begin: (# enter theShape.begin  exit theShape.begin #);
     end: (# enter theShape.end    exit theShape.end #);
     width: (# enter theShape.width  exit theShape.width #);
     dashes: (# enter theShape.dashes exit theShape.dashes #);
     cap: (# enter theShape.cap    exit theShape.cap  #);
     
     coordinates: 
       (# enter theShape.coordinates exit theShape.coordinates #);
     draw::< 
       (# <<SLOT LinedrawBody: dopart>> #);
     copy::<
       (# do INNER; <<SLOT LineCopyBody: descriptor>> #);
  do INNER;
  #);


(************************************************************)
(********************** Multiline ***************************)
(************************************************************)

MultiLine: PredefinedGraphicalObject
  (# shapeDesc::< MultiLineShape;
     
     width: (# enter theShape.width  exit theShape.width #);
     points: (# enter theShape.points exit theShape.points #);
     addPoint:
       (# p: @point
       enter p
       do p->(TM).inversetransformpoint->theShape.addPoint
       #);
     insertPoint:
       (# i: @integer; p: @point
       enter (p,i)
       do (p->(TM).inversetransformpoint,i)->theShape.insertPoint
       #);
     deletePoint:
       (# p: @point
       enter p
       do p->(TM).inversetransformpoint->theShape.deletePoint
       #);
     getPoint:
       (# i: @Integer;
          p: @Point;
       enter i
       do i->theShape.getPoint->p
       exit p
       #);
     setPoint:
       (# i: @Integer;
          p: @Point;
       enter (i,p)
       do (p->(TM).inversetransformpoint,i)->theShape.setPoint
       #);
     closestLineSegment:
       (# p: @point; i: @integer
       enter p
       do p->(TM).inversetransformpoint->theShape.closestLineSegment->i
       exit i
       #);
     dashes: (* Not Yet Implemented *)
       (# enter theShape.dashes exit theShape.dashes #);
     cap: (# enter theShape.cap    exit theShape.cap  #);
     join: (# enter theShape.join   exit theShape.join #);
     draw::< 
       (# <<SLOT MultiLinedrawBody: dopart>> #);
     copy::< 
       (# do INNER; <<SLOT MultiLineCopyBody: descriptor>> #);
  do INNER;
  #);


(************************************************************)
(*********************** GraphicText ************************)
(************************************************************)

GraphicalText: GraphicText(# #); (* Alias *)

GraphicText: PredefinedGraphicalObject
  (# shapeDesc::< TextShape;
     
     inittext: (# enter theShape.inittext #);
     
     position: 
       (# enter theShape.position
       exit theShape.position 
       #);
     thefontname: 
       (# 
       enter theShape.thefontname
       exit theShape.thefontname
       #);
     theStyle: 
       (# 
       enter theShape.theStyle
       exit theShape.theStyle
       #);
     size: 
       (# 
       enter theShape.size
       exit theShape.size
       #);
     underline: 
       (# 
       enter theShape.underline
       exit theShape.underline
       #);
     theText: 
       (# 
       enter theShape.theText
       exit theShape.theText
       #);
     
     draw::< 
       (# <<SLOT GTextDrawBody: dopart>> #);
     copy::< 
       (# <<SLOT GTextCopyBody: dopart>> #);
     interactiveCreateShape::< 
       (# lastCh: @Char; (* Last character typed in interaction *)
       <<SLOT GTextInteractiveCreateShape: dopart>> 
       exit lastCh
       #);
     interactiveReshape::< 
       (# lastCh: @Char; (* Last character typed in interaction *)
       <<SLOT GTextInteractiveReshape: dopart>> 
       exit lastCh
       #);
  do INNER;
  #);

(************************************************************)
(************************* Arc ******************************)
(************************************************************)

Arc: PredefinedGraphicalObject
  (# shapeDesc::< ArcShape;
     
     center: (# enter theShape.center   exit theShape.center #);
     horizontalRadius:
       (#
       enter theShape.horizontalradius
       exit  theShape.horizontalradius
       #);
     verticalRadius:
       (# 
       enter theShape.verticalradius
       exit  theShape.verticalradius
       #);
     angle1:
       (# 
       enter theShape.angle1 
       exit theShape.angle1
       #);
     angle2: 
       (# 
       enter theShape.angle2 
       exit theShape.angle2
       #);
     arcWidth: 
       (# 
       enter theShape.arcWidth
       exit theShape.arcWidth
       #);
     
     draw::< 
       (# <<SLOT ArcDrawBody: dopart>> #);
     copy::< 
       (# do INNER; <<SLOT ArcCopyBody: descriptor>> #);
  do INNER
  #);

(************************************************************)
(*********************** PieSlice ***************************)
(************************************************************)

PieSlice: PredefinedGraphicalObject
  (# shapeDesc::< PieShape;
     
     center: 
       (# 
       enter theShape.center
       exit theShape.center
       #);
     horizontalRadius: 
       (# 
       enter theShape.horizontalradius
       exit theShape.horizontalradius
       #);
     verticalRadius: 
       (# 
       enter theShape.verticalradius
       exit theShape.verticalradius
       #);
     angle1: 
       (# 
       enter theShape.angle1
       exit theShape.angle1
       #);
     angle2: 
       (# 
       enter theShape.angle2
       exit theShape.angle2
       #);
     draw::< 
       (# <<SLOT PieDrawBody: dopart>> #);
     copy::< 
       (# do INNER; <<SLOT PieCopyBody: descriptor>> #);
  do INNER
  #);


StrokeAblePredefinedGraphicalObject: PredefinedGraphicalObject
  (# 
     shapeDesc::< StrokeableShape
  do INNER
  #);

(************************************************************)
(************************* Rect *****************************)
(************************************************************)

Rect: StrokeAblePredefinedGraphicalObject
  (# shapeDesc::< RectShape;
     
     upperleft:
       (# 
       enter theShape.upperleft 
       exit theShape.upperleft
       #);
     width: 
       (# 
       enter theShape.width
       exit theShape.width
       #);
     height: 
       (#
       enter theShape.height
       exit theShape.height
       #);
     corners: 
       (# 
       enter theShape.corners
       exit theShape.corners
       #);
     
     draw::< (# <<SLOT RectDrawBody: dopart>> #);
     copy::< (# do INNER; <<SLOT RectCopyBody: descriptor>> #);
  do INNER;
  #);


(************************************************************)
(************************* Ellipse **************************)
(************************************************************)

Ellipse: StrokeAblePredefinedGraphicalObject
  (# shapeDesc::< EllipseShape;
     
     center: 
       (# 
       enter theShape.center
       exit theShape.center
       #);      
     horizontalradius: 
       (# 
       enter theShape.horizontalradius
       exit theShape.horizontalradius
       #);
     verticalradius: 
       (#
       enter theShape.verticalradius 
       exit theShape.verticalradius 
       #);
     geometry: 
       (# 
       enter theShape.geometry 
       exit theShape.geometry
       #);
     
     draw::< (# <<SLOT EllipseDrawBody: dopart>> #);
     copy::< (# do INNER; <<SLOT EllipseCopyBody: descriptor>> #);
  do INNER;
  #);
