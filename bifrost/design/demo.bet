ORIGIN '~beta/bifrost/v2.0/Bifrost';
INCLUDE 'designlib';
INCLUDE '~beta/guienv/v1.3.1/utils/simplemenu';

--- PROGRAM: descriptor ---
bifrost
(#
   theWindow: @Window
     (# nodestate:      (# exit 0 #);
        connectstate:   (# exit 1 #);
        movestate:      (# exit 2 #);
        state: @integer;
        NodeType: ##AbstractGraphicalObject (* Type of next node to create *);

        menubarType::
          (# FileMenu: @simplemenu
               (# iquit: @item(# onSelect:: (# do terminate #)#);
                  open:: 
                    (# 
                    do ('Quit',           'q') -> iquit.newkey; 
                    #)
               #);
             DesignMenu: @simplemenu
               (# inode: @radioitem(# onSelect:: (# do nodestate->state #)#);
                  iconn: @radioitem(# onSelect:: (# do connectstate->state#)#);
                  imove: @radioitem(# onSelect:: (# do movestate->state #)#);
                  open:: 
                    (# 
                    do ('Make Node',      'n') -> inode.newkey;
                       ('Make Connector', 'c') -> iconn.newkey;
                       ('Move Node',      'm') -> imove.newkey;
                    #)
               #);
             ObjectMenu: @simplemenu
               (# ielli: @radioitem(#onSelect::(# do Ellipse##->NodeType###)#);
                  irect: @radioitem(#onSelect::(# do Rect##->NodeType###)#);
                  open:: 
                    (# 
                    do ('Ellipse',      'e') -> ielli.newkey;
                       ('Rectangle',    'r') -> irect.newkey;
                    #)
               #);
             open::
               (# 
               do 'File' -> FileMenu.new;
                  'Design' -> DesignMenu.new;
                  'Object' -> ObjectMenu.new;
                  ObjectMenu.irect.onSelect;
                  DesignMenu.inode.onSelect;
               #);
          #);
        myCanvas: @BifrostCanvas
          (# redcolor, greycolor, blackcolor: @SolidColor
               (* The color used nodes *); 
             MakeNode: @interactiveCreateNode;
             
             open::
               (# 
               do (* The first Node will have label "A" *)
                  'A' -> MakeNode.ch;
                  redcolor.init; IndianRed->redcolor.name;
                  greycolor.init; Gray->greycolor.name;
                  blackcolor.init; Black->blackcolor.name;
                  true->bindright;
                  true->bindbottom;
               #);
             eventhandler::
	       (# onMouseDown::
                    (# hitNode: ^Node;
                    do mousepos -> devicetocanvas -> mousepos;
                       (if state
                        // nodestate then
                           (mousepos, nodetype##, greycolor[], redcolor[])
                             -> MakeNode;
                        // connectstate then
                           (if (mousepos->findNode->hitNode[]) <> NONE then
                               (* We hit a Node *)
                               (hitNode[], blackcolor[]) 
                                 -> interactiveCreateConnector;
                           if);
                        // movestate then
                           (if (mousepos->findNode->hitNode[]) <> NONE then
                               (* We hit a Node *)
                               (hitNode[],mousepos,NoModifier)->interactivemove
                           if);
                       if);
                    #);
               #) (* Eventhandler *);
          #); (* myCanvas *)       
        open::
          (# 
          do (* Open the Canvas *)
             myCanvas.open;
	     (400, 600) -> myCanvas.size -> size;
             'Design' -> title;
          #);
     #)
do theWindow.open;
#)

