ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/containers/list' '~beta/mps/astlevel' '~beta/basiclib/file';
(*
 * It is assumed that projects have distinct names, and that groups within
 * a single project have distinct names. *)
-- lib: Attributes --
projectInfo:
  (#
     project:<
       (#
          isDirectory,isDotDotDirectory,isProjectFile: @boolean;
          isRoot,isExtent,isDomain: @boolean;
          hasSubprojects: @boolean;
          showTxtFiles: @boolean;
          
          name: ^Text;
          (* Textual name of project. *)
          
          location: @text;
          scanGroups:
            (#
               forEach:<
                 (# current: ^group;  enter current[] do INNER ;  #);
               project: ^Text;
               
            enter project[]
            do THIS(scanGroups)[]->scanGroupsImpl; 
            #);
          scanGroupsImpl:<
            (# thescan: ^scanGroups;  enter thescan[] do INNER #);
          lastgroupSelected: ^group;
          groups: @groupList
       #);
     group:<
       (#
          name: ^Text;
          (* Textual name of group. *)
          location: @text;
          txtFile: @boolean;
          getfg:<
            (# diskfile,textfile: @file; recheck:< object; 
            do
               (if fg[] = none then
                   INNER getfg
                else
               (* added by JLK *)
                   recheck; 
               if);
               
            exit fg[]
            #);
          close:< (# theForm: ^astInterface.fragmentForm
                  enter theForm[] 
                  do INNER
                  #);
          lastFormSelected: ^astInterface.fragmentForm;
          (* private *)
          fg: ^astInterface.fragmentGroup;
          
       #);
     groupList: list (# element:: group #);
     scanProjects:
       (# forEach:< (# current: ^project enter current[] do INNER #); 
       do THIS(scanProjects)[]->scanProjectsImpl; 
       #);
     scanProjectsImpl:<
       (# thescan: ^scanProjects;  enter thescan[] do INNER #);
     init:< Object
  #);
  
