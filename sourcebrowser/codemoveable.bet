ORIGIN '~beta/guienv/v1.3.1/guienv';
BODY 'private/codemoveablebody';
INCLUDE '~beta/objectbrowser/v2.0/UI/moveable'
        '~beta/objectbrowser/v2.0/UI/icons'
        '~beta/objectbrowser/v2.0/options'
        '~beta/editor/v5.0/codeeditor'
        '~beta/guienv/v1.3.1/utils/guienvadds';
(* windowItem.{PreferredSize,BringToFront} *)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- guienvlib: Attributes --
codemoveableEditorList: List
  (# (*element:: window.codemoveableEditor;*)
     onInsert:<
       (# theCodemoveableEditor: ^window.codemoveableEditor; 
       enter theCodemoveableEditor[]
       do INNER
       #);
     onDelete:<
       (# theCodemoveableEditor: ^window.codemoveableEditor; 
       enter theCodemoveableEditor[]
       do INNER
       #);
     newscan: scan
       (# cur: ^window.codemoveableEditor; 
       do current[]->cur[]; INNER ; 
       #);
     
  #);
getcodemoveableEditorList: objectPool.get
  (# type::< codemoveableEditorList; init::  (#  do obj.init #) #);
  

-- windowLib: Attributes --
codemoveableEditor: sifTextEditor
  (#
     <<SLOT codemoveableEditorLib:Attributes>>;
     isolated:< booleanValue;
     open::< 
       (#
          defaultSize:: 
            (# 
            do mysize->value
            #);
          mysize: @Point;
          
       enter mysize
       do
          <<SLOT codemoveableEditorOpen:Descriptor>>
       #);
     close::<  (#  do <<SLOT codemoveableEditorClose:Descriptor>> #);
     cmeprivate: @<<SLOT codemoveableEditorPrivate:Descriptor>>;
     contentsType::< 
       (#
          eventHandler::< 
            (#
               onMouseDown::< 
                 (# 
                 <<SLOT codemoveableEditorOnMouseDown:DoPart>>
                 #);
               onKeyDown::< 
                 (#  <<SLOT codemoveableEditorOnKeyDown:DoPart>> #);
               
            #);
          
       #);
     codeViewerType::< codeeditor
       (#
          selectNodeInFormEditor::< 
            (# 
            do
               (theEditorRoot[],
                node[],separate)->THIS(codemoveableEditor).selectNode
            #)
       #);
     selectNode:<
       (# theEditorRoot,node: ^astInterface.ast; separate: @boolean
       enter (theEditorRoot[],node[],separate)
       do INNER
       #);
     select:
       (# node: ^astInterface.ast; 
       enter node[]
       <<SLOT codemoveableEditorSelect:DoPart>>
       #);
     update:
       (# 
       <<SLOT codemoveableEditorUpdate:DoPart>>
       #);
     editorEncloserType:< codemoveableEditorEncloser;
     encl: ^editorEncloserType;
     getencloser:<
       (# theEncloser: ^editorEncloserType; 
       do INNER ; theEncloser[]->encl[]; 
       exit theEncloser[]
       #);
     
  #);
codemoveableEditorEncloser:
  (#
     tryclose:< (* Called when this codeview should be closed. *) Object;
     onHighLight:< (* Called when this codeview should attract attention. *)
      Object;
     getAST:< (* Should return the astInterface used. *)
       (# AST: ^astInterface;  do INNER exit AST[] #);
     getBETACFL:< (* Should return the BETA grammar used. *)
       (# BETACFL: ^astInterface.beta;  do INNER exit BETACFL[] #);
     
  #);
codemoveable: moveable
  (#
     contentsType::< 
       (#
          initialNode: ^astInterface.ast;
          editorType:< codeMoveableEditor
            (#
               charWidthType::  (#  do moveableHeader.style->ts[] #);
               getencloser::  (#  do editorEncloser[]->theEncloser[] #);
               
            #);
          editor: @editorType;
          open::<  (#  do <<SLOT codemoveableContentsOpen:Descriptor>> #);
          dobindings::< 
            (# 
            <<SLOT codemoveabledobindings:DoPart>>
            #);
          
       #);
     editorEncloserType:< codemoveableEditorEncloser
       (#
          onHighLight::  (#  do wriggle #);
          tryclose::  (#  do THIS(codemoveable).close #);
          
       #);
     editorEncloser: @editorEncloserType;
     moveableMenu::< 
       (#
          open::< 
            (# mi: ^actionedMenuItem; 
            do
               &actionedMenuItem[]->mi[];
               (codemoveableFitAction##,none ,none ,'Fit to contents')->mi.open;
               INNER ;
               
            #);
          
       #);
     codemoveableFitAction: actionedMenuItemAction
       (#  do contents.editor.preferredSize->newPreferredContentsSize;  #);
     codemoveableOpenInWindowAction: actionedMenuItemAction
       (#  <<SLOT codemoveableOpenInWindowAction:DoPart>> #);
     open::< 
       (# 
       enter
       contents.initialnode[]
       do
          <<SLOT codemoveableopen:Descriptor>>
       #);
     close::<  (#  do <<SLOT codemoveableclose:Descriptor>> #);
     icon:: 
       (# 
       do
          'CodeView_Icon'
            ->(getIconDB).getIcon
            ->theIcon[];
          
       #);
     
  #);
  

