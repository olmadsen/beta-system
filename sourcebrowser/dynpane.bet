ORIGIN '~beta/guienv/v1.3/guienv';
BODY 'private/dynpanebody';
--- windowLib:attributes ---
DynPane: Canvas
  (* Initialization:
   *   1. Call open.
   *   2. From within INNER open, call appendMember repeatedly until all 
   *      initial members have been appended.
   *   3. From within INNER open, call appendsDone.
   *
   * Extension:
   *   1. Call appendMember repeatedly until all new members have
   *      been appended.
   *   2. Call appendsDone.
   * 
   * Deletion:
   *   1. Call deleteMember.
   *
   *
   * If the application allows interactive change of the size of the DynPane,
   * the members are not automatically extended. This must be ensured by
   * the user of DynPane. Consider using onFatherFrameResizeRelative from
   * ~beta/objectbrowser/v2.0/UI/obguienvadds.bet. However, notice that
   * this action should not be installed in members until all members have 
   * been appended to the DynPane, and that the action will have to be 
   * temporarily disabled during appendMember and deleteMember sessions, 
   * since these change the size of the DynPane.
   *)
  (# <<SLOT DynPaneLib:attributes>>;

     open::<
       (* verticalStacking: Whether members are stacked vert. or horiz.
        * minsize: Minimal height (or width) of members after interaction.
        * panewidth: Width of the separators between members. Minimum is 2. *)
       (# setDefaults:<
            (* used to set default values for verticalStacking,
             * minsize, and panewidt
             *)
            (# do INNER #);
          verticalStacking: @Boolean;
          minsize, panewidth: @Integer;
       enter (verticalStacking,minsize,panewidth)
       <<SLOT dynpaneopen:dopart>>
       #);

     appendMember: 
       (# member: ^Canvas;
       enter member[]
       <<SLOT dynpaneappendmember:dopart>>
       #);
     appendsDone: (# <<SLOT dynpaneappenddone:dopart>> #);

     deleteMember:
       (# member: ^Canvas;
       enter member[]
       <<SLOT dynpanedeletemember:dopart>>
       #);
     
     dppriv: @<<SLOT dynpaneprivate:descriptor>>;
  #)
