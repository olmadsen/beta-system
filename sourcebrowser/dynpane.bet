ORIGIN '~beta/guienv/v1.3.1/guienv';
BODY 'private/dynpanebody';
--- windowLib:attributes ---
DynPane: Canvas
  (* 
   * Initialization:
   *   1. Call open.
   *   2. From within INNER open, call appendMember repeatedly until all 
   *      initial members have been appended.
   *   3. From within INNER open, call appendsDone.
   *
   * Extension:
   *   1. Call appendMember repeatedly until all new members have
   *      been appended.
   *   2. Call appendsDone.
   * 
   * Deletion:
   *   1. Call deleteMember.
   * 
   * If the application allows interactive change of the size of the DynPane,
   * the members are automatically extended, since correct operation of
   * the dynpane depends on the assumption that members fill out the
   * dynpane. DO NOT INTERFERE WITH AUTOMATIC MEMBER RESIZING! E.g., do
   * not bind members, and do not install actions that resize members. This is
   * taken care of by dynpane.
   *)
  (# <<SLOT DynPaneLib:attributes>>;

     open::<
       (* verticalStacking: Whether members are stacked vert. or horiz.
        * minsize: Minimal height (or width) of members after interaction.
        * panewidth: Width of the separators between members. Minimum is 2. *)
       (# setDefaults:<
            (* May be used to set default values for verticalStacking,
             * minsize, and panewidth. *)
            (# do INNER #);
          verticalStacking: @Boolean;
          minsize, panewidth: @Integer;
       enter (verticalStacking,minsize,panewidth)
       <<SLOT dynpaneopen:dopart>>
       #);

     appendMember: 
       (# member: ^Canvas;
       enter member[]
       <<SLOT dynpaneappendmember:dopart>>
       #);
     appendsDone: (# <<SLOT dynpaneappenddone:dopart>> #);

     fixedSize:
       (* Will not respond to automatic resizing in vertical or
        * horizontal direction (depending on the stacking order of
        * this pane
        *)
       (# member: ^Canvas;
       enter member[]
       <<SLOT dynpaneFixedSize: dopart>>
       #);
     
     deleteMember:
       (# member: ^Canvas;
       enter member[]
       <<SLOT dynpanedeletemember:dopart>>
       #);
     
     onMemberResize:< IntegerValue
       (* If onMemberResize=dynpaneFixed (default), then dragging a
        * separator will resize both of its surrounding members, and keep the 
        * size of the dynpane fixed.
        * If onMemberResize=dynpaneResizable, then, in addition, dragging a 
        * separator with the Shift key down will resize the member immediately
        * before the dragged separator, and keep the rest of the member 
        * sizes unchanged. As a result, the dynpane itself is resized. *);
     dynpaneFixed: IntegerValue (# do 0->value #);
     dynpaneResizable: IntegerValue (# do 1->value #);
     
     fitToFather:
       (* Changes the size of this dynpane to fit the size of its 
        * father frame. 
        * If fitHoriz is TRUE, the dynpane is fitted horizontally. 
        * Default of fitHoriz is verticalStacking.
        * If fitVert is TRUE, the dynpane is fitted vertically.
        * Default of fitVert is not verticalStacking.
        * Further bind fatherSize in case "father.size" is not the size
        * to be fitted against. *)
       (# fitHoriz:< BooleanObject
            (# do <<SLOT dynpanefithorizdefault:descriptor>>; INNER #);
          fitVert:< BooleanObject
            (# do <<SLOT dynpanefitvertdefault:descriptor>>; INNER #);
          fatherSize:<
            (# sz: @Point;
            do INNER; (if (0,0)->sz.isEqual then father.size->sz if);
            exit sz
            #);
       do <<SLOT dynpanefittofather:descriptor>>
       #);
     
     dppriv: @<<SLOT dynpaneprivate:descriptor>>;
     
     eventHandler::<
       (# onFrameChanged::<
            (* Resizes members to maintain invariant that the dynpane 
             * is filled by its members. *)
            (# do <<SLOT dynpaneonframechanged:descriptor>> #);
       #);
  #)
