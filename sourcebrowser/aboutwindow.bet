ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/graphics';
INCLUDE '~beta/toollibs/utils/startBrowser';
-- guienvLib: Attributes --
aboutwindow: window
  (#
     pm: @pixmap;
     open:: 
       (# 
       do
          'mjolner-about'->pm.read;
	  (pm.width,pm.height)->size;
          logo.open;
          ok.open;
          'About the Mjølner System'->title
       #);
     OK: @pushbutton
       (#
          open:: (# do (60,20)->size; (255,5)->position; 'OK'->label #);
          eventhandler:: 
            (# onMouseUp::  (#  do THIS(window).close #);  #);
          
       #);
     logo: @windowItem
       (# urlframe: @rectangle;
          open::
            (# 
            do (0,0) -> position;
               (pm.width,pm.height) -> size;
	       (195,20)->urlframe.size;
               (5,190)->urlframe.offset;
            #); 
          eventHandler::
            (# onRefresh::
                 (# 
                 do 
                    graphics
                    (# 
                    do (pm[], (0, 0), (0, 0), pm.width,pm.height)
                         -> drawRaster
                    #)
                 #);
               onMouseUp::
                 (#
		 do (if localposition->urlframe.containspoint then
			 cursors.watch[]->mouse.busyCursor;
			'http://www.mjolner.com'->startBrowser;
			 none->mouse.busyCursor;
		    if)
		 #)
            #)
       #);
  #)  

