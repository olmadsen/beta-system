ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls'
'~beta/guienv/graphics'
'~beta/toollibs/utils/startBrowser';
-- guienvLib: Attributes --
aboutwindow: window
  (#
     pm: @pixmap;
     open:: 
       (# 
       do
          (360,215)->size;
          'mjolner'->pm.read;
          (0xffff,0xffff,0xffff)->pm.transparentcolor;
          legend.open;
          url.open;
          ok.open;
          logo.open;
          'About the Mjølner System'->title
       #);
     legend: @statictext
       (#
          open:: 
            (# 
            do
               'The Mjølner System\n'
               'Release 5.0, September 1999\n'
               'Copyright (C) 1994-1999 Mjølner Informatics\n'
               '\n'
               'Mjølner Informatics\n'
               'Science Park Aarhus\n'
               'Gustav Wieds Vej 10\n'
               'DK-8000 Århus C, Denmark\n'
               'Phone: +45 86 20 20 00\n'
               'Fax: +45 86 20 12 22\n'
               'E-mail: info@mjolner.com'->label;
               (250,170)->size;
               (150,25)->position;
            #)
       #);
     url: @windowItem
       (#
          eventhandler::
            (# onRefresh::
                 (# 
                 do graphics
                    (# 
                    do (0,0,0xffff)->pen.foregroundColor;
                       (0,10)->moveto;
                       'URL: http://www.mjolner.com'->drawText;
                    #)
                 #);
               onMouseUp::
                 (# do 'http://www.mjolner.com'->startBrowser #)
            #);
          open:: 
            (# 
            do (200,15)->size;
               (15,180)->position;
            #)
       #);
     OK: @pushbutton
       (#
          open:: (# do (60,20)->size; (275,180)->position; 'OK'->label #);
          eventhandler:: 
            (# onMouseUp::  (#  do THIS(window).close #);  #);
          
       #);
     logo: @windowItem
       (# open::
            (# 
            do (0,0) -> position;
               (pm.width,pm.height) -> size
            #); 
          transparentbackground:: (# do true->value #);
          eventHandler::
            (# onRefresh::
                 (# 
                 do 
                    graphics
                    (# 
                    do (pm[], (0, 0), (0, 0), pm.width,pm.height)
                         -> drawRaster
                    #)
                 #);
            #)
       #);
  #)  

