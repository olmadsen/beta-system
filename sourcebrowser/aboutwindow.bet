ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/graphics';
INCLUDE '~beta/toollibs/utils/startBrowser';
-- guienvLib: Attributes --
aboutwindow: window
  (#
     pm: ^pixmap;
     wwwframe, mailframe: @rectangle;
     open:: 
       (# 
       do &pixmap[]->pm[];
          load: 'mjolner-about'->pm.read
          (# error:: (# do none->pm[]; msg[]->screen.putline; leave load #)
          #);
	  (if pm[]<>none then
	      (pm.width,pm.height)->size;
	   else
	      (322, 274)->size
	  if);
          logo.open;
          ok.open;
          'About the Mjølner System'->title
       #);
     close::
       (# 
       do updateCursor.end; 
       #);
     OK: @pushbutton
       (#
          open:: (# do (60,20)->size; (250,250)->position; 'OK'->label #);
          eventhandler:: 
            (# onMouseUp::  (#  do THIS(window).close #);  #);
          
       #);
     updateCursor: @timer
       (# usingHand: @boolean;
          action::
            (# localposition: (# exit mouse.globalposition->globalToLocal #);
            do (if (localposition->mailframe.containspoint) or
                   (localposition->wwwframe.containspoint) then
                   (if not usingHand then
                       cursors.hand[] -> mouse.busycursor;
                       true -> usingHand;
                   if);
                else
                   clear;
               if);
            #);
          clear:
            (# 
            do (if usingHand then
                   none -> mouse.busycursor;
                   false -> usingHand;
               if);
            #);
          end:
            (# 
            do stop; clear;
            #);
       #);
     Logo: @windowItem
       (# open::
            (# 
            do (0,0) -> position;
               (if pm[]<>none then
		   (pm.width,pm.height) -> size;
	        else
	           (322, 274)->size
	       if);
	       (124,16)->mailframe.size;
               (70,240)->mailframe.offset;
	       (130,16)->wwwframe.size;
               (70,256)->wwwframe.offset;
            #); 
          eventHandler::
            (# onRefresh::
                 (# 
                 do graphics
                    (# 
                    do (if pm[]<>none then
                           (pm[], (0, 0), (0, 0), pm.width,pm.height)
                             -> drawRaster
                        else
                           (70, 240)->moveto;
			   'Click here to mail to support@mjolner.com'->drawtext;
                           (70, 256)->moveto;
			   'Click here to go to http://www.mjolner.com'->drawtext;
                       if)
                    #)
                 #);
               onMouseEnter::
                 (* Mouse enters About window *)
                 (# 
                 do 100 -> updateCursor.start;
                 #);
               onMouseLeave::
                 (* Mouse leaves About window *)
                 (# 
                 do updateCursor.end;
                 #);
               onMouseUp::
                 (#
		 do (if true
                     // localposition->mailframe.containspoint then
                        cursors.watch[]->mouse.busyCursor;
			'mailto:support@mjolner.com'->startBrowser;
                        none->mouse.busyCursor;
                     // localposition->wwwframe.containspoint then
                        cursors.watch[]->mouse.busyCursor;
			'http://www.mjolner.com'->startBrowser;
                        none->mouse.busyCursor;
		    if)
		 #)
            #)
       #);
  #)  

