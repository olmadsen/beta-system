ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/graphics';
INCLUDE '~beta/toollibs/utils/startBrowser';
-- guienvLib: Attributes --
aboutwindow: window
  (#
     pm: ^pixmap;
     open:: 
       (# 
       do &pixmap[]->pm[];
          load: 'mjolner-about'->pm.read
            (# error:: (# do none->pm[]; msg[]->screen.putline; leave load #)
            #);
	  (if pm[]<>none then
	      (pm.width,pm.height)->size;
	   else
	      (322, 274)->size
	  if);
          logo.open;
          ok.open;
          'About the Mjølner System'->title
       #);
     OK: @pushbutton
       (#
          open:: (# do (60,20)->size; (255,5)->position; 'OK'->label #);
          eventhandler:: 
            (# onMouseUp::  (#  do THIS(window).close #);  #);
          
       #);
     logo: @windowItem
       (# urlframe: @rectangle;
          open::
            (# 
            do (0,0) -> position;
               (if pm[]<>none then
		   (pm.width,pm.height) -> size;
	        else
	           (322, 274)->size
	       if);
	       (195,20)->urlframe.size;
               (5,190)->urlframe.offset;
            #); 
          eventHandler::
            (# onRefresh::
                 (# 
                 do graphics
                    (# 
                    do (if pm[]<>none then
                           (pm[], (0, 0), (0, 0), pm.width,pm.height)
                             -> drawRaster
                        else
                           (5, 200)->moveto;
			   'Click here to go to Mjølner URL'->drawtext
                        if)
                    #)
                 #);
               onMouseUp::
                 (#
		 do (if localposition->urlframe.containspoint then
			 cursors.watch[]->mouse.busyCursor;
			'http://www.mjolner.com'->startBrowser;
			 none->mouse.busyCursor;
		    if)
		 #)
            #)
       #);
  #)  

