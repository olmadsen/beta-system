ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/graphics';
INCLUDE '~beta/toollibs/utils/startBrowser';
-- guienvLib: Attributes --
aboutwindow: window
  (#
     pm: ^pixmap;
     open:: 
       (# 
       do &pixmap[]->pm[];
          load: 'mjolner-about'->pm.read
            (# error:: (# do none->pm[]; msg[]->screen.putline; leave load #)
            #);
	  (if pm[]<>none then
	      (pm.width,pm.height)->size;
	   else
	      (322, 274)->size
	  if);
          logo.open;
          ok.open;
          'About the Mjølner System'->title
       #);
     OK: @pushbutton
       (#
          open:: (# do (60,20)->size; (250,250)->position; 'OK'->label #);
          eventhandler:: 
            (# onMouseUp::  (#  do THIS(window).close #);  #);
          
       #);
     logo: @windowItem
       (# wwwframe, mailframe: @rectangle;
          open::
            (# 
            do (0,0) -> position;
               (if pm[]<>none then
		   (pm.width,pm.height) -> size;
	        else
	           (322, 274)->size
	       if);
	       (124,16)->mailframe.size;
               (70,240)->mailframe.offset;
	       (130,16)->wwwframe.size;
               (70,256)->wwwframe.offset;
            #); 
          eventHandler::
            (# onRefresh::
                 (# 
                 do graphics
                    (# 
                    do (if pm[]<>none then
                           (pm[], (0, 0), (0, 0), pm.width,pm.height)
                             -> drawRaster
                        else
                           (70, 240)->moveto;
			   'Click here to mail to support@mjolner.com'->drawtext;
                           (70, 256)->moveto;
			   'Click here to go to http://www.mjolner.com'->drawtext;
                        if)
                    #)
                 #);
               onMouseUp::
                 (#
		 do (if true
                     // localposition->mailframe.containspoint then
                        cursors.watch[]->mouse.busyCursor;
			'mailto:support@mjolner.com'->startBrowser;
                        none->mouse.busyCursor;
                     // localposition->wwwframe.containspoint then
                        cursors.watch[]->mouse.busyCursor;
			'http://www.mjolner.com'->startBrowser;
                        none->mouse.busyCursor;
		    if)
		 #)
            #)
       #);
  #)  

