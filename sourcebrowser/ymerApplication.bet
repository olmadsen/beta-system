ORIGIN '~beta/basiclib/v1.5/basicsystemenv';
INCLUDE '~beta/guienv/v1.4/guienvsystemenv';
INCLUDE 'ymer';
INCLUDE 'toolsMenu';
INCLUDE '~beta/editor/v5.1/ymercall';
BODY '~beta/editor/v5.1/private/nouser'
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- program: descriptor ---
systemenv
(# fragmentBrowser: @ymerApplication
     (# startBrowser: @ymerBrowserWindow;
        editMode:: readonlyMode;
        ymerBrowserWindow::
          (# browserTitle:: (# do 'Ymer: '->title.prepend #);
             workspace::
               (# menubarType::
                    (# viewMenu: @codeEditorViewMenu
                         (# formEditorExists::
                              (# do this(workspace).cfe[]->cfe[] #);
                            setup:
                              (#
                              do true->BETAsif; open;
                                 this(viewMenu)[]->this(menubarType).append
                              #)
                         #);
                       open::
                         (#
                         do fileMenu.setup;
                            viewMenu.setup;
                            importMenu.setup;
                            scrollMenu.setup;
                            closeMenu.setup;
                         #)
                    #)
               #);
             codeViewWindow::
               (# menubarType::
                    (# viewMenu: @codeEditorViewMenu
                         (# formEditorExists::
                              (# do fe[]->cfe[] #);
                            setup:
                              (#
                              do true->BETAsif; open;
                                 this(viewMenu)[]->this(menubarType).append
                              #)
                         #);
                       open::
                         (#
                         do fileMenu.setup;
                            viewMenu.setup;
                            buffersMenu.setup
                         #)
                    #)
               #);
             menubarType::
               (# testMenu: @Menu
                    (# hideTextEditor: @menuItem
                         (# eventhandler::
                              (# onselect::
                                   (# do(* browser.sbprivate.down.defaultView.hide)* #) #)
                         #);
                       showTextEditor: @menuItem
                         (# eventhandler::
                              (# onselect::
                                   (# do (*browser.sbprivate.down.defaultView.show*) #) #)
                         #);
                       open::
                         (#
                         do hideTextEditor.open; hideTextEditor[]->append;
                            (*showTextEditor.open; showTextEditor[]->append;*)
                         #);
                       setup:
                         (#
                         do open;
                            this(testMenu)[]->this(menubarType).append
                         #)
                    #);
                  txtMenu: @menu
                    (# iCut: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE) and browser.cte.cutStatus->value #);
                                 onselect::
                                   (# do browser.cte.doCut #) #)
                         #);
                       iCopy: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE) and browser.cte.copyStatus->value #);
                                 onselect::
                                   (# do browser.cte.doCopy #) #)
                         #);
                       iPaste: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE) and browser.cte.pasteStatus->value #);
                                 onselect::
                                   (# do browser.cte.doPaste #) #)
                         #);
                       iClear: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE)->value #);
                                 onselect::
                                   (# do browser.cte.doClear #) #)
                         #);
                       iSave: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE)->value #);
                                 onselect::
                                   (# do browser.cte.save #) #)
                         #);
                       iSaveAs: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE)->value #);
                                 onselect::
                                   (# do browser.cte.saveAs #) #)
                         #);
                       iReload: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE)->value #);
                                 onselect::
                                   (# do browser.cte.reload #) #)
                         #);
                       iSearch: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE)->value #);
                                 onselect::
                                   (# do browser.cte.search #) #)
                         #);
                       iReplace: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE)->value #);
                                 onselect::
                                   (# do browser.cte.replace #) #);
                         #);
                       iToggle: @menuItem
                         (# eventhandler::
                              (# onstatus::
                                   (#  do (browser.cte[]<>NONE)->value #);
                                 onselect::
                                   (#
                                   do browser.cte.toggleAllowEdit;
                                      not checked->checked
                              #) #);
                            open::
                              (# do 'Txt Write Protection'->name; true->checked #);
                         #);
                       eventhandler::
                         (# onSelect::
                              (#
                              do (if browser.cte[]<>NONE then
                                     not browser.cte.allowEdit->iToggle.checked
                                 if)
                              #)
                         #);
                       open::
                         (# sep: ^separator;
                         do iCut.open; iCut[]->append;
                            iCopy.open; iCopy[]->append;
                            iPaste.open; iPaste[]->append;
                            iClear.open; iClear[]->append;
                            &separator[]->sep[]; sep.open; sep[]->append;
                            iSave.open; iSave[]->append;
                            iSaveAs.open; iSaveAs[]->append;
                            iReload.open; iReload[]->append;
                            &separator[]->sep[]; sep.open; sep[]->append;
                            iSearch.open; iSearch[]->append;
                            iReplace.open; iReplace[]->append;
                            &separator[]->sep[]; sep.open; sep[]->append;
                            iToggle.open; iToggle[]->append;
                         #);
                       setup:
                         (#
                         do open; this(txtMenu)[]->this(menubarType).append
                         #)
                    #);
                  viewMenu: @codeEditorViewMenu
                    (# formEditorExists::
                         (# do browser.cfe[]->cfe[] #);
                       setup:
                         (#
                         do true->BETAsif; open;
                            this(viewMenu)[]->this(menubarType).append
                         #)
                    #);
                  open::
                    (#
                    do fileMenu.setup;
                       projectMenu.setup;
                       viewMenu.setup;
                       (&toolsMenu[]).setup;
                       historyMenu.setup;
                       windowsMenu.setup;
                       txtMenu.setup;
                       testMenu.setup;
                    #);
               #);
          #);
        init::
          (#
          do startBrowser.open;
             &ymerCall(# getFragmentGroup:: 
                           (# 
                           do name[]
                                ->mps.fragmentGroupTable.getFragmentGroup
                                ->fg[]
                           #)
                      #)[]->startBrowser.edenv.ymerCallback[];
             startBrowser.loadSettings
          #);
     #);
   
   setWindowEnv:: (# do fragmentBrowser[]->theWindowEnv[] #);
     
do 'Mjolner BETA Source Browser - version '->puttext;
   fragmentBrowser.ymerVersion->putline;
   fragmentBrowser.init;
   (for i: noOfArguments-1 repeat
        i+1->arguments->fragmentbrowser.startBrowser.appendProject
   for);
   (if noOfArguments-1>0 then fragmentbrowser.startBrowser.appendDone if);
#)
