ORIGIN '~beta/basiclib/v1.5/basicsystemenv';

INCLUDE '~beta/guienv/v1.4/guienvsystemenv';

INCLUDE 'ymer';

INCLUDE 'toolsMenu';

INCLUDE '~beta/editor/v5.1/ymercall';

BODY '~beta/editor/v5.1/private/nouser'
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- program: descriptor ---
systemenv
(# fragmentBrowser: @ymerApplication
     (# startBrowser: @ymerBrowserWindow;
        editMode:: readonlyMode;
        ymerBrowserWindow::
          (# browserTitle:: (# do 'Ymer: '->title.prepend #);
             workspace::
               (# menubarType::
                    (# viewMenu: @codeEditorViewMenu
                         (# formEditorExists::
                              (# do this(workspace).cfe[]->cfe[] #);
                            setup:
                              (#
                              do true->BETAsif; open;
                                 this(viewMenu)[]->this(menubarType).append
                              #)
                         #);
                       open::
                         (#
                         do fileMenu.setup;
                            viewMenu.setup;
                            importMenu.setup;
                            scrollMenu.setup;
                            closeMenu.setup;
                         #)
                    #)
               #);
             codeViewWindow::
               (# menubarType::
                    (# viewMenu: @codeEditorViewMenu
                         (# formEditorExists::
                              (# do fe[]->cfe[] #);
                            setup:
                              (#
                              do true->BETAsif; open;
                                 this(viewMenu)[]->this(menubarType).append
                              #)
                         #);
                       open::
                         (#
                         do fileMenu.setup;
                            viewMenu.setup;
                            buffersMenu.setup
                         #)
                    #)
               #);
             menubarType::
               (# (* testMenu: @Menu
                    (# groupClose: @menuItem
                         (# eventhandler::
                              (# onselect::
                                   (#
                                   do (if browser.currentgroup[]<>none then
                                          browser.currentgroup.close
                                      if)
                              #)#)
                         #);
                       open:: (# do groupClose.open; groupClose[]->append #);
                       setup:
                         (#
                         do open;
                            this(testMenu)[]->this(menubarType).append
                         #)
                    #);*)
                  viewMenu: @codeEditorViewMenu
                    (# formEditorExists::
                         (# do this(ymerBrowserWindow).cfe[]->cfe[] #);
                       setup:
                         (#
                         do true->BETAsif; open;
                            this(viewMenu)[]->this(menubarType).append
                         #)
                    #);
                  open::
                    (#
                    do fileMenu.setup;
                       projectMenu.setup;
                       viewMenu.setup;
                       (&toolsMenu[]).setup;
                       historyMenu.setup;
                       windowsMenu.setup;
                       (*testMenu.setup*)
                    #);
               #);
          #);
        init::
          (#
          do startBrowser.open;
             &ymerCall(# getFragmentGroup:: 
                           (# 
                           do name[]
                                ->mps.fragmentGroupTable.getFragmentGroup
                                ->fg[]
                           #)
                      #)[]->startBrowser.edenv.ymerCallback[];
             startBrowser.loadSettings
          #);
     #);
   
   setWindowEnv:: (# do fragmentBrowser[]->theWindowEnv[] #);
     
do fragmentBrowser.init;
   (for i: noOfArguments-1 repeat
        i+1->arguments->fragmentbrowser.startBrowser.appendProject
   for);
   (if noOfArguments-1>0 then fragmentbrowser.startBrowser.appendDone if);
#)
