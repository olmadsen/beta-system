ORIGIN '~beta/basiclib/v1.4/basicsystemenv';

INCLUDE '~beta/guienv/v1.3.1/guienvsystemenv';

INCLUDE 'ymer';

INCLUDE 'toolsMenu';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- program: descriptor ---
systemenv
(# fragmentBrowser: @ymerApplication
     (# startBrowser: @ymerBrowserWindow;
        editMode:: readonlyMode;
        ymerBrowserWindow::
          (# browserTitle:: (# do 'Ymer: '->title.prepend #);
             workspace::
               (# menubarType::
                    (# viewMenu: @codeEditorViewMenu
                         (# formEditorExists::
                              (# do this(workspace).cfe[]->cfe[] #);
                            setup:
                              (# do open; this(viewMenu)[]->this(menubarType).append #)
                         #);
                       open::
                         (#
                         do fileMenu.setup;
                            viewMenu.setup;
                            importMenu.setup;
                            scrollMenu.setup;
                            closeMenu.setup;
                         #)
                    #)
               #);
             codeViewWindow::
               (# menubarType::
                    (# viewMenu: @codeEditorViewMenu
                         (# formEditorExists::
                              (# do fe[]->cfe[] #);
                            setup:
                              (# do open; this(viewMenu)[]->this(menubarType).append #)
                         #);
                       open::
                         (#
                         do fileMenu.setup;
                            viewMenu.setup;
                            buffersMenu.setup
                         #)
                    #)
               #);
             menubarType::
               (# viewMenu: @codeEditorViewMenu
                    (# formEditorExists::
                         (# do this(ymerBrowserWindow).cfe[]->cfe[] #);
                       setup:
                         (# do open; this(viewMenu)[]->this(menubarType).append #)
                    #);
                  open::
                    (#
                    do fileMenu.setup;
                       projectMenu.setup;
                       viewMenu.setup;
                       (&toolsMenu[]).setup;
                       historyMenu.setup;
                       windowsMenu.setup
                    #);
               #);
          #);
        init::
          (# do startBrowser.open; startBrowser.loadSettings #);
     #);
   
   setWindowEnv:: (# do fragmentBrowser[]->theWindowEnv[] #);
     
do fragmentBrowser.init;
   (for i: noOfArguments-1 repeat
        i+1->arguments->fragmentbrowser.startBrowser.appendProject
   for);
   (if noOfArguments-1>0 then fragmentbrowser.startBrowser.appendDone if);
#)
