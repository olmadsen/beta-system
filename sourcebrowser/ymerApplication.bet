ORIGIN '~beta/basiclib/v1.4/basicsystemenv';

INCLUDE '~beta/guienv/v1.3.1/guienvsystemenv';

INCLUDE 'ymer';

INCLUDE 'toolsMenu';

INCLUDE 'private/ymerBody';

--- program: descriptor ---
systemenv
(# fragmentBrowser: @ymerApplication
     (# ymerBrowserWindow::
          (# eventhandler::
               (# #);
             open::
               (# #);
             menubarType::
               (# open::
                    (# m: ^menu do &toolsMenu[]->m[]; m.open; m[]->append #)
               #)
          #);
        init::
          (# #);
     #);
   
   setWindowEnv:: (# do fragmentBrowser[]->theWindowEnv[] #);
   t: ^text; i: @integer;
   newProj: ^fragmentbrowser.ymerBrowser.browser.projects.project;
     
do fragmentBrowser.init;
   (for i: noOfArguments-1 repeat
        i+1->arguments->t[];
        
'Argument: '->puttext; t[]->putline;
        (#
        do t[]->fragmentbrowser.mps.AST.expandToFullPath->t[];
           &fragmentbrowser.ymerbrowser.browser.projects.project[]->newProj[];
           t->newProj.location;
           newProj[]->fragmentbrowser.ymerbrowser.browser.projects.setupProject;
           newProj[]->fragmentbrowser.ymerbrowser.browser.projects.theProjects.append;
        #)
   for);
   (if newProj[]<>NONE then
       fragmentbrowser.ymerbrowser.browser.projectsChanged;
       newProj[]->fragmentbrowser.ymerbrowser.browser.setProject;
       (if fragmentbrowser.ymerbrowser.browser.currentProject.groups.size=1 then
           (* automatically select it, and the first fragment form in it *)
           l: ((fragmentbrowser.ymerbrowser.browser.currentProject.groups.head).elm.getfg).fragmentList.scan
             (# ff: ^fragmentbrowser.mps.AST.fragmentForm;
                (* find first FF to select it automatically *)
             do (if current.type
                 // fragmentbrowser.mps.AST.formType then
                    current.open->ff[];
                    (fragmentbrowser.ymerbrowser.browser.currentProject[],
                    (fragmentbrowser.ymerbrowser.browser.currentProject.groups.head).elm[],
                    ff[])
                      ->fragmentbrowser.ymerbrowser.browser.setFragmentForm;
                    leave l
                if)
             #)
       if)
   if)
#)
