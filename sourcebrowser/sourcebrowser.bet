ORIGIN '~beta/guienv/v1.6/guienv';
BODY 'private/sourcebrowserbody';
INCLUDE '~beta/mps/v5.2/astlevel'
        '~beta/betaast/v5.2/gram'
        '~beta/objectbrowser/v2.2/UI/listmoveable'
        '~beta/objectbrowser/v2.2/UI/labelled'
        'codemoveable'
        'asciiEditor';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
(*
 * It is assumed that projects have distinct names, and that groups within
 * a single project have distinct names. *)
-- lib: Attributes --
projectInfo:
  (#
     project:<
       (#
          name: ^Text;
          (* Textual name of project. *)
          location: @text;
          scanGroups:
            (#
               forEach:<
                 (# current: ^group;  enter current[] do INNER ;  #);
               project: ^Text;
               
            enter project[]
            do THIS(scanGroups)[]->scanGroupsImpl; 
            #);
          scanGroupsImpl:<
            (# thescan: ^scanGroups;  enter thescan[] do INNER #);
          lastgroupSelected: ^group;
       #);
     group:<
       (#
          name: ^Text;
          (* Textual name of group. *)
          location: @text;
          txtFile: @boolean;
          getfg:<
            (# diskfile, textfile: @file;
               recheck:< object;
            do
               (if fg[] = none then
                   INNER getfg
                else (* added by JLK *) 
                   recheck;
               if);
            exit fg[]
            #);
          close:<
            (# do INNER #);
          lastFormSelected: ^astInterface.fragmentForm;
          (* private *)
          fg: ^astInterface.fragmentGroup;
          
       #);
     scanProjects:
       (# forEach:< (# current: ^project enter current[] do INNER #); 
       do THIS(scanProjects)[]->scanProjectsImpl; 
       #);
     scanProjectsImpl:<
       (# thescan: ^scanProjects;  enter thescan[] do INNER #);
     init:< Object;
     
  #);
  

-- windowlib: Attributes --
sourceBrowser: canvas (* ABSTRACT PATTERN! *)
  (# <<SLOT sourcebrowserLib: attributes>>;
  (* projectInfoType
   * ===============
   * Must be further bound in specializations. *)
     projectInfoType:< projectInfo;
     projects: @projectInfoType;
     groups: @List
       (#
          <<SLOT sourcebrowsergroupslib:Attributes>>;
          element:: 
            (#
               index: @Integer;
               g: ^projects.group;
               
            #);
          getGroupByIndex:
            (#
               index: @Integer;
               g: ^projects.group;
               
            enter index
            <<SLOT groupsGetGroupByIndex:DoPart>>
            exit g[]
            #);
          getIndexByGroup:
            (# index: @Integer; g: ^projects.group; 
            enter g[]
            <<SLOT groupGetIndexByGroup:DoPart>>
            exit index
            #);
          
       #);
     cge: (* current group editor *)
       ^editorEnv.groupEditor;
     cfe: (* current form editor *)
       ^codeEditor;
     cte: (* current form editor *)
       ^asciiEditor;
     edenv: ^editorEnv;
     (* FUNCTIONALITY CONCERNING OPENING:
      * ================================= *)
     open::<  (#  <<SLOT sourcebrowseropen:DoPart>> #);
     onProjectListOpen:<
     (* Called when the project list view is opened. Use it to
      * install actions for the projectlist. *)
       (# theprojectlist: ^listmoveablelist; 
       enter theprojectlist[]
       do INNER
       #);
     onGroupListOpen:<
     (* Called when the group list view is opened. Use it to install
      * actions for the grouplist view. *)
       (# thegrouplist: ^listmoveablelist; 
       enter thegrouplist[]
       do INNER
       #);
     onFragmentgroupViewOpen:<
     (* Called when the fragmentgroup view is opened. Use it to install
      * actions for the fragmentgroup view. *)
       (# thefragmentgroupview: ^listmoveablelist; 
       enter thefragmentgroupview[]
       do INNER
       #);
     onCodeViewOpen:<
     (* Called when the code view is opened. Use it to install
      * actions for the codeview. *)
       (# thecodeview: ^codemoveableEditor; 
       enter thecodeview[]
       do INNER
       #);
     (* CODEVIEWER ENCLOSER (See newcodemoveable.bet)
      * =============================================
      * MUST be further bound in specializations of sourcebrowser. *)
     editorEncloserType:< codemoveableEditorEncloser;
     editorEncloser: @editorEncloserType;
     currentProject: ^projects.project;
     currentGroup: ^projects.group;
     currentForm: ^astInterface.fragmentForm;
     propertySelected: @boolean;
     projectsLabel:<
     (* Further bind to change label on projects list in UI. Default is
      * 'Directories'. *)
       (# t: ^Text;  do 'Directories'->t[]; INNER exit t[] #);
     emptyProjectListLabel:<
     (* Further bind to change label on empty group list in UI. Default
      * is 'No directory selected'. *)
       (# t: ^Text;  do 'No directory selected'->t[]; INNER exit t[] #);
     emptyGroupViewLabel:<
     (* Further bind to change label on empty fragment view in UI. Default
      * is 'No fragment group selected'. *)
       (# t: ^Text; 
       do 'No fragment group selected'->t[]; INNER
       exit t[]
       #);
     emptyCodeViewLabel:<
     (* Further bind to change label on empty fragment view in UI. Default
      * is 'No fragment selected'. *)
       (# t: ^Text;  do 'No fragment selected'->t[]; INNER exit t[] #);
     followLink:<
       (* Called when user double-clicks link in displayed fragment group.
        * Further bind to call setProject. *)
       (# groupname: ^Text; (* Name of fragment group pointed out by link. *)
       enter groupname[]
       do INNER
       #);
     fileEdit:<
       (* Called when user double-clicks make property in displayed
        * fragment group.
        *)
       (# filename: ^Text; (* Name of file to be edited *)
       enter filename[]
       do INNER
       #);
     selectnode:<
       (* Called when user double-clicks on an ast node. *)
       (# theEditorRoot,node: ^astInterface.ast; (* ast node double-clicked at. *)
          separate: @boolean
       enter (theEditorRoot[],node[],separate)
       do INNER
       #);
 
     onCurrentProjectChange:<
       (* Called by setProject. *)
       (# newProject: ^projects.project;
       enter newProject[]
       do INNER
       #);
     onCurrentGroupChange:<
       (* Called by setGroup after calling setProject. *)
       (# newGroup: ^projects.group;
       enter newGroup[]
       do (if newGroup[]<>none then
              newGroup[]->currentProject.lastGroupSelected[]
          if);
          INNER
       #);
     onCurrentFormChange:<
     (* Called by setFragmentForm after calling setGroup. *)
       (# newForm: ^astInterface.fragmentForm; 
       enter newForm[]
       do (if newForm[]<>NONE then
              newForm[]->currentGroup.lastFormSelected[]
          if);
          INNER
       #);
     projectsChanged: (* Call to notify changes in the project list. *)
       (#  <<SLOT sourcebrowserProjectsChanged:DoPart>> #);
     setProject:
     (* Call to change the project shown. If doscan is TRUE, the list
      * of projects is updated using projects.scanProjects. *)
       (#
          p: ^projects.project;
          setGroupDoneInInner,setFormDoneInInner: @Boolean;
          
       enter p[]
       <<SLOT sourcebrowserSetProject:DoPart>>
       #);
     setGroup: setProject
       (# g: ^projects.group; 
       enter g[]
       <<SLOT sourcebrowserSetGroup:DoPart>>
       #);
     setFragmentForm: setGroup
       (# ff: ^astInterface.fragmentForm; 
       enter ff[]
       <<SLOT sourcebrowserSetFragmentForm:DoPart>>
       #);
     viewHTML:<
       (# theme, location: ^text
       enter (theme[],location[])
       do INNER
       #);
     sbprivate: @<<SLOT sourcebrowserprivate:Descriptor>>;
     locationView: @labelled
       (# eventhandler::
            (# onMouseDown::
                 (# <<SLOT sourcebrowserLocationViewMouseDown: dopart>> #)
            #);
          private: @<<SLOT sourcebrowserLocationViewPrivate: descriptor>>;
          display:
            (# m: ^text
            enter m[]
            <<SLOT sourcebrowserLocationViewDisplay: dopart>>
            #);
          isChanged, isChecked, isLocked: @boolean;
          changed:
            (# status: @boolean
            enter status
            <<SLOT sourcebrowserLocationViewChanged: dopart>>
            #);
          checked:
            (# status: @boolean
            enter status
            <<SLOT sourcebrowserLocationViewChecked: dopart>>
            #);
          locked:
            (# status: @boolean
            enter status
            <<SLOT sourcebrowserLocationViewLocked: dopart>>
            #);
          msg:
            (# msg: ^text
            enter msg[]
            <<SLOT sourcebrowserLocationViewMsg: dopart>>
            #);
          contentsType:: staticText
            (# open::  (#  do ''->label #) #);
          open:: 
            (# sz: @point; 
            enter sz
            <<SLOT sourcebrowserLocationViewOpen: dopart>>
            #)
       #);
     state: @
       (# curList: @stack(# element:: cursor #);
          busy: @
            (#
            do mouse.busyCursor->curList.push;
               cursors.watch[]->mouse.busyCursor
            #);
          normal: @
            (#
            do (if curList.empty then
                   none->mouse.busyCursor
                else curList.pop->mouse.busyCursor
               if);
            #)
       #);
     infoView: @
       (# msgList: @list(# element:: text #);
          msgPush: @
            (# do m[]->msgList.prepend; display #);
          msgPop: @
            (#
            do (if msgList.size>0 then
                   (msgList.head).elm[]->done;
                   msgList.head->msgList.delete
               if)
            #);
          m: ^text;
          txt: @text;
          settext: @
            (# msg: ^text
            enter msg[]
            do 'Info: '->m[]; msg[]->m.append
            #);
          append: @
            (# msg: ^text
            enter msg[]
            do msg[]->m.append
            #);
          display: @
            (#
            do m[]->locationView.display;
            #);
          msg: @
            (#
            enter settext
            do display
            #);
          done: @
            (#
            enter m[]
            do 'done'->append; display
            #);
          open: (#  do 'Info: '->locationView.contents.label #)
       #)
  #)


