ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/mps/astlevel'
        '~beta/betaast/gram'
        '~beta/toollibs/UI/listmoveable'
        '~beta/guienv/utils/labelled'
        '~beta/toollibs/utils/mpsinterface'
        'codemoveable'
        'projects'
        'ymerInterface'
        'asciiEditor'
        '~beta/guienv/utils/toolbar';
BODY 'private/sourcebrowserbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- windowlib: Attributes --
sourceBrowser: canvas (* ABSTRACT PATTERN! *)
  (#
     <<SLOT sourcebrowserLib:Attributes>>;
     Interface: ^ymerBrowserInterface;
     browserEditMode:< BooleanValue;
     (* projectInfoType
      * ===============
      * Must be further bound in specializations. *)
     projectInfoType:< projectInfo;
     projects: @projectInfoType;
     mps: ^mpsInterface;
     cge: (* current group editor *)
       (#  enter Interface.cge[] exit Interface.cge[] #);
     cfe: @ (* current code editor *)
       (# 
       enter Interface.cfe[]
       (*do
        this was a try to select the codeeditor when 
        browsing in the sourcebrowser, so that textediting
        could be done immidiately, but this has the sideeffect
        that the source browser is raised when selecting 
        diagram from different files in the worksheet of 
        freja! we have to find another solution,
        maybe we should turn off the auto selection in 
        sif and freja?
        
        (if not (Interface.cfe[] = none )
        then
        Interface.cfe.theSifTexteditor[]->target
        if)*)
       exit Interface.cfe[]
       #);
     cte: (* current form editor *) ^asciiEditor;
     edenv: ^editorEnv;
     (* FUNCTIONALITY CONCERNING OPENING:
      * ================================= *)
     open::<  (#  <<SLOT sourcebrowseropen:DoPart>> #);
     onProjectListOpen:<
     (* Called when the project list view is opened. Use it to
      * install actions for the projectlist. *)
       (# theprojectlist: ^listmoveablelist; 
       enter theprojectlist[]
       do INNER
       #);
     onGroupListOpen:<
     (* Called when the group list view is opened. Use it to install
      * actions for the grouplist view. *)
       (# thegrouplist: ^listmoveablelist; 
       enter thegrouplist[]
       do INNER
       #);
     onFragmentgroupViewOpen:<
     (* Called when the fragmentgroup view is opened. Use it to install
      * actions for the fragmentgroup view. *)
       (# thefragmentgroupview: ^listmoveablelist; 
       enter thefragmentgroupview[]
       do INNER
       #);
     onCodeViewOpen:<
     (* Called when the code view is opened. Use it to install
      * actions for the codeview. *)
       (# thecodeview: ^codemoveableEditor; 
       enter thecodeview[]
       do INNER
       #);
     (* CODEVIEWER ENCLOSER (See newcodemoveable.bet)
      * =============================================
      * MUST be further bound in specializations of sourcebrowser. *)
     editorEncloserType:< codemoveableEditorEncloser;
     editorEncloser: @editorEncloserType;
     propertySelected: @boolean;
     projectsLabel:<
     (* Further bind to change label on projects list in UI. Default is
      * 'Directories'. *)
       (# t: ^Text;  do 'Directories'->t[]; INNER exit t[] #);
     emptyProjectListLabel:<
     (* Further bind to change label on empty group list in UI. Default
      * is 'No directory selected'. *)
       (# t: ^Text;  do 'No directory selected'->t[]; INNER exit t[] #);
     emptyGroupViewLabel:<
     (* Further bind to change label on empty fragment view in UI. Default
      * is 'No fragment group selected'. *)
       (# t: ^Text; 
       do 'No fragment group selected'->t[]; INNER
       exit t[]
       #);
     emptyCodeViewLabel:<
     (* Further bind to change label on empty fragment view in UI. Default
      * is 'No fragment selected'. *)
       (# t: ^Text;  do 'No fragment selected'->t[]; INNER exit t[] #);
     followLink:<
     (* Called when user double-clicks link in displayed fragment group.
      * Further bind to call setProject. *)
       (#
          groupname: ^Text; (* Name of fragment group pointed out by link. *) 
       enter groupname[]
       do INNER
       #);
     fileEdit:<
     (* Called when user double-clicks make property in displayed
      * fragment group.
      *)
       (# filename: ^Text; (* Name of file to be edited *) 
       enter filename[]
       do INNER
       #);
     selectnode:< (* Called when user double-clicks on an ast node. *)
       (#
          theEditorRoot,node: ^astInterface.ast;
          (* ast node double-clicked at. *)
          separate: @boolean
       enter (theEditorRoot[],node[],separate)
       do INNER
       #);
     onCurrentProjectChange:< (* Called by setProject. *)
       (# newProject: ^projects.project;  enter newProject[] do INNER #);
     onCurrentGroupChange:< (* Called by setGroup after calling setProject. *)
       (# newGroup: ^projects.group enter newGroup[] do INNER #);
     onCurrentFormChange:<
     (* Called by setFragmentForm after calling setGroup. *)
       (# newForm: ^astInterface.fragmentForm; 
       enter newForm[]
       do
          (if newForm[] <> none then
          (*newForm[]->currentGroup.lastFormSelected[]*)
              
          if);
          INNER
       #);
     projectsChanged: (* Call to notify changes in the project list. *)
       (#  <<SLOT sourcebrowserProjectsChanged:DoPart>> #);
     selectFragmentGroup:
       (# fgName: ^text
       enter fgName[]
       <<SLOT selectFragmentGroup:DoPart>>
       #);
     selectFragmentForm:
       (# fgName: ^text; formName: ^text
       enter (fgName[],formName[])
       <<SLOT selectFragmentForm:DoPart>>
       #);
     viewHTML:<
       (# theme,location: ^text
       enter (theme[],location[])
       do INNER
       #);
     sbprivate:
       @<<SLOT sourcebrowserprivate:Descriptor>>;
     locationView: @sblabelled
       (#
          eventhandler:: 
            (#
               onMouseDown:: 
                 (#  <<SLOT sourcebrowserLocationViewMouseDown:DoPart>> #)
            #);
          private:
            @<<SLOT sourcebrowserLocationViewPrivate:Descriptor>>;
          display:
            (# m: ^text
            enter m[]
            <<SLOT sourcebrowserLocationViewDisplay:DoPart>>
            #);
          clear:
            (# 
            <<SLOT sourcebrowserLocationViewClear:DoPart>>
            #);
          isChanged,isChecked,isLocked: @boolean;
          changed::  (#  <<SLOT sourcebrowserLocationViewChanged:DoPart>> #);
          checked::  (#  <<SLOT sourcebrowserLocationViewChecked:DoPart>> #);
          locked::  (#  <<SLOT sourcebrowserLocationViewLocked:DoPart>> #);
          msg:
            (# msg: ^text
            enter msg[]
            <<SLOT sourcebrowserLocationViewMsg:DoPart>>
            #);
          contentsType:: staticText (# open::  (#  do ''->label #) #);
          open:: 
            (# sz: @point; 
            enter sz
            <<SLOT sourcebrowserLocationViewOpen:DoPart>>
            #)
       #);
     state: @
       (#
          curList: @stack
            (# element:: cursor
            #);
          busy: @
            (# 
            do
               (if not
               infoView.silent then
                   mouse.busyCursor->curList.push;
                   cursors.watch[]->mouse.busyCursor
               if)
            #);
          normal: @
            (# 
            do
               (if not infoView.silent then
                   (if curList.empty then
                       none ->mouse.busyCursor
                    else
                       curList.pop->mouse.busyCursor
                   if)
               if)
            #)
       #);
     infoView: @sbInfoView
       (#
          msgPush:: 
            (# 
            do (if not silent then m[]->msgList.prepend; display if)
            #);
          msgPop:: 
            (# 
            do
               (if not silent then
                   (if msgList.size > 0 then
                       (msgList.head).elm[]->done; msgList.head->msgList.delete
                   if)
               if)
            #);
          txt: @text;
          append: @
            (# msg: ^text
            enter msg[]
            do (if not silent then msg[]->m.append if)
            #);
          display: @
            (#  do (if not silent then m[]->locationView.display if) #);
          msg::  (#  do display #);
          msgClear:: 
            (#  do (if not silent then locationView.clear if) #);
          done::  (#  do 'done'->append; display #);
          open:
            (# 
            do (if not silent then 'Info: '->locationView.contents.label if)
            #)
       #);
     setupToolbar:< (# toolb: ^toolbar;  enter toolb[] do INNER #);
     
  #)  

