ORIGIN '~beta/guienv/v1.6/guienv';
INCLUDE '~beta/guienv/v1.6/utils/group';
INCLUDE '~beta/guienv/v1.6/utils/private/groupbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- windowLib: attributes ---
envGroup: group
  (# contentsType:< windowItem;
     innerFrame: @rectangle;
     contents: @contentsType;
     open::<
       (#
       do INNER;
          calcInnerFrame;
          this(envGroup)[]->contents.open;
          innerFrame->contents.frame;
          true->contents.bindRight->contents.bindTop
       #);
     eventhandler::<
       (# onFatherFrameChanged::<
            (# do calcInnerFrame; innerFrame->contents.frame; INNER #) 
       #);
     labelHeight: (* Private *)
       (# ts: ^textStyle;
       do private.theLabel.style->ts[];
       exit ts.lineHeight+3
       #);
     calcInnerFrame: (* Private *)
       (# p: @point
       do (3,labelHeight)->innerFrame.topleft;
          size->p; (3,3)->p.subtract; p->innerFrame.bottomright
       #)
  #);
