ORIGIN '~beta/basiclib/basicsystemenv';
INCLUDE '~beta/guienv/guienvsystemenv'
        '../ymer'
        '~beta/editor/ymercall'
        'mytxtmenu';
BODY '~beta/editor/private/nouser';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- program: Descriptor --
(#
   thesystemenv: @|systemenv
     (# setWindowEnv::  (#  do gui[]->theWindowEnv[] #); 
     do gui[]->ymer.init;
        myCallBack[]->ymer.newBrowserCallBack[];
        &myfilemenuacc[]->ymer.fileMenuActionList.append; 
        setupMenus;
        ymer.newbrowser
     #);
   gui: @guienv;
   myCallBack:@gui.BrowserCallBack
     (# 
     do
        &ymer.mycall[]->ymerCallBack[];
     #);
   ymer: @ymerEnv
     (#
        editMode:: readonlyMode;
        mycall: ymerCall
          (#
             getFragmentGroup:: 
               (# 
               do name[]->mps.fragmentGroupTable.getFragmentGroup->fg[]
               #)
          #)
     #);
   (* ViewMenu *)
   SetUpMenus:
     (# 
     do &gui.MenubarAppendMenuAction
        (# viewMenu:theBrowser.ymerBrowser.codeEditorViewMenu
             (# browser:^gui.ymerbrowserwindow;
                formEditorExists::
                  (# do browser.browser.cfe->cfe[]
                  #);
             #);
           txtMenu:theBrowser.ymerBrowser.txtmenu
             (# browser:^gui.ymerbrowserwindow;
                getcte::
                  (# do browser.browser.cte[]->cte[];
                  #);
             #); 
           vm:^viewMenu;
           tm:^txtMenu;
        do &viewMenu[]->vm[];
           vm.open; vm[]->themenubar.append;
           theBrowser.ymerBrowser[]->vm.browser[];
           
           &txtMenu[]->tm[];
           theBrowser.ymerBrowser[]->tm.browser[];
           tm.open; tm[]->themenubar.append;
        #)[]->ymer.menuActionList.append
     #);

   (* just example, put in filemenu *)
   myfilemenuacc: gui.menuAppendItemAction
     (# myitem: ^fmenuitem;
        fmenuitem:themenu.menuitem
          (# open::(# do 'fgc'->name; #);
             eventhandler::
               (# onSelect::(# do 'fgc'->screen.putline#);
               #);
          #);
     do &fmenuitem[]->myitem[]; myitem.open; myitem[]->themenu.append; 
     #);
   
do
   'Mjolner Source Browser - version '->puttext;
   ymer.ymerVersion->putline;
   thesystemenv;
   
#)  

