ORIGIN  '../ymer';

INCLUDE '~beta/guienv/v1.3.1/fields';
INCLUDE '~beta/guienv/v1.3.1/stddialogs';

INCLUDE '~beta/guienv/v1.3.1/utils/prompts';

INCLUDE '~beta/objectbrowser/v2.0.1/UI/obguienvadds';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- browserLib: attributes ---
streamEditor: window
  (# strm: ^stream;
     setTitle:< (# t: ^text do INNER exit t[] #);
     openRead:< booleanValue(# do true->value; INNER #);
     openWrite:< booleanValue(# do true->value; INNER #);
     closeStream:< object;
     loadStream:
       (# 
       enter strm[]
       do (if strm[]<>NONE then
              (* load stream 'strm' *)
              strm[]->contents.loadStream;
              setTitle->title;
          if)
       #);
     menubarType::
       (# fileMenu: @menu
            (# revertItem: @menuItem
                 (# eventHandler::
                      (# onSelect::
                           (#
                           do strm[]->loadStream;
                           #)
                      #);
                    open:: (# do 'Revert'->name #)
                 #);
               saveItem: @menuItem
                 (# eventHandler::
                      (# onSelect::
                           (#
                           do (if contents.changed then
                                  contents.saveStream;
                                  false->contents.changed
                              if)
                           #)
                      #);
                    open:: 
                      (# do 'Save'->name #)
                 #);
               quitItem: @menuItem
                 (# inx: @integer;
                    eventHandler:: 
                      (# closeEditor: @boolean;
                         onSelect::
                           (# askuser: promptForBoolean
                                (# ok::
                                     (#
                                     do this(streamEditor).contents.saveStream;
                                        true->closeEditor
                                     #);
                                   notok:: (# do true->closeEditor #)
                                #)
                           do (if contents.changed then
                                  (this(streamEditor)[],'Quit','Save before close?')
                                    ->(&askUser[]).popup;
                                  (if closeEditor then this(streamEditor).close if)
                               else
                                  this(streamEditor).close
                              if)
                           #)
                      #);
                    open:: 
                      (# do 'w'->key; 'Close'->name #)
                 #);
               open::
                 (#
                 do 'File'->name;
                    revertItem.open; revertItem[]->append;
                    saveItem.open; saveItem[]->append;
                    quitItem.open; quitItem[]->append
            #)#);
          editMenu: @menu
            (# clipboard: @text;
               cutItem: @menuItem
                 (# eventHandler::
                      (# onStatus::
                           (#
                           do (contents.contents.selection.start<>contents.contents.selection.end)->value #);
                         onSelect::
                           (#
                           do clipboard.clear;
                              contents.contents.selection.contents->clipboard.append;
                              contents.contents.delete
                           #)
                      #);
                    open:: (# do 'x'->key;  'Cut'->name #)
                 #);
               copyItem: @menuItem
                 (# eventHandler::
                      (# onStatus::
                           (# do (contents.contents.selection.start<>contents.contents.selection.end)->value #);
                         onSelect::
                           (#
                           do clipboard.clear;
                              contents.contents.selection.contents->clipboard.append;
                           #)
                      #);
                    open:: 
                      (# do 'v'->key;  'Copy'->name #)
                 #);
               pasteItem: @menuItem
                 (# inx: @integer;
                    eventHandler:: 
                      (#  onStatus::
                           (# do (clipboard.length<>0)->value #);
                         onSelect::
                           (#
                           do clipboard[]
                                ->contents.contents.insert
                           #)
                      #);
                    open:: 
                      (# do  'v'->key; 'Paste'->name #)
                 #);
               open::
                 (#
                 do 'Edit'->name;
                    cutItem.open; cutItem[]->append;
                    copyItem.open; copyItem[]->append;
                    pasteItem.open; pasteItem[]->append
            #)#);
          open:: 
            (#
            do fileMenu.open; fileMenu[]->append;
               editMenu.open; editMenu[]->append
            #)
       #); (* menubarType *)
     contents: @textEditor
       (# changed, loading: @boolean;
          contentsType::
            (# eventhandler::
                 (# onTextChanged:: (# do not loading->changed #) #)
            #);
          strm: ^stream;
          loadStream:
            (# t: ^text;
               theText: @StyledText;
               pos, lineInx: @integer;
            enter strm[]
            do (if openRead then
                   true->loading;
                   strm.reset;
                   strm.scan
                   (# while:: (# do true->value #);
                   do ch->theText.put;
                   #);
                   theText[]->contents.contents;
                   false->loading;
                   closeStream
               if)
            #);
          saveStream:
            (# t: ^text
            do (if openWrite then
                   true->loading;
                   strm.reset;
                   contents.contents->strm.puttext;
                   false->loading;
                   closeStream
               if)
            #);
          dobind:
            (#
            do TRUE->bindTop->bindLeft->bindRight->bindBottom;
            #);
          open:: (# do this(streamEditor).size->size; doBind #);
       #);
     open::<
       (#
       do 'Stream Editor'->title;
          INNER;
          contents.open;
       #)
  #)
