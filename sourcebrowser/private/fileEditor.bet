ORIGIN  '../ymer';

INCLUDE '~beta/guienv/v1.3.1/fields';
INCLUDE '~beta/guienv/v1.3.1/stddialogs';

INCLUDE '../xprompts';
INCLUDE 'ymerFileUtils';

INCLUDE '~beta/basiclib/v1.4/file';

INCLUDE '../dynpane';
INCLUDE '~beta/objectbrowser/v2.0/UI/obguienvadds';
INCLUDE '~beta/objectbrowser/v2.0/UI/labelled';

--- browserLib: attributes ---
fileEditor: window
  (# path: ^text;
     loadFile:
       (# 
       enter path[]
       do (if path[]<>NONE then
              (* load text file *)
              path[]->contents.loadFile;
              path[]->fragmentDefaultName->(title).copyAppend->title;
          if)
       #);
     menubarType::
       (# fileMenu: @menu
            (# revertItem: @menuItem
                 (# eventHandler::
                      (# onSelect::
                           (#
                           do path[]->loadFile;
                           #)
                      #);
                    open:: (# do 'Revert'->name #)
                 #);
               saveItem: @menuItem
                 (# eventHandler::
                      (# onSelect::
                           (#
                           do (if contents.changed then
                                  contents.saveFile;
                                  false->contents.changed
                              if)
                           #)
                      #);
                    open:: 
                      (# do 'Save'->name #)
                 #);
               quitItem: @menuItem
                 (# inx: @integer;
                    eventHandler:: 
                      (# onSelect::
                           (# askuser: promptForBoolean
                                (# ok::
                                     (#
                                     do this(fileEditor).contents.saveFile;
                                        this(fileEditor).close
                                     #);
                                   notok:: (# do this(fileEditor).close #)
                                #)
                           do (if contents.changed then
                                  (this(fileEditor)[],'Quit','Save before close?')
                                ->(&askUser[]).popup
                               else
                                  this(fileEditor).close
                              if)
                           #)
                      #);
                    open:: 
                      (# do 'Close'->name #)
                 #);
               open::
                 (#
                 do 'File'->name;
                    revertItem.open; revertItem[]->append;
                    saveItem.open; saveItem[]->append;
                    quitItem.open; quitItem[]->append
            #)#);
          editMenu: @menu
            (# clipboard: @text;
               cutItem: @menuItem
                 (# eventHandler::
                      (# onStatus::
                           (#
                           do (contents.contents.selection.start<>contents.contents.selection.end)->value #);
                         onSelect::
                           (#
                           do clipboard.clear;
                              contents.contents.selection.contents->clipboard.append;
                              contents.contents.delete
                           #)
                      #);
                    open:: (# do 'Cut'->name #)
                 #);
               copyItem: @menuItem
                 (# eventHandler::
                      (# onStatus::
                           (# do (contents.contents.selection.start<>contents.contents.selection.end)->value #);
                         onSelect::
                           (#
                           do clipboard.clear;
                              contents.contents.selection.contents->clipboard.append;
                           #)
                      #);
                    open:: 
                      (# do 'Copy'->name #)
                 #);
               pasteItem: @menuItem
                 (# inx: @integer;
                    eventHandler:: 
                      (#  onStatus::
                           (# do (clipboard.length<>0)->value #);
                         onSelect::
                           (#
                           do clipboard[]
                                ->contents.contents.insert
                           #)
                      #);
                    open:: 
                      (# do 'Paste'->name #)
                 #);
               open::
                 (#
                 do 'Edit'->name;
                    cutItem.open; cutItem[]->append;
                    copyItem.open; copyItem[]->append;
                    pasteItem.open; pasteItem[]->append
            #)#);
          open:: 
            (#
            do fileMenu.open; fileMenu[]->append;
               editMenu.open; editMenu[]->append
            #)
       #); (* menubarType *)
     contents: @textEditor
       (# changed, loading: @boolean;
          contentsType::
            (# eventhandler::
                 (# onTextChanged:: (# do not loading->changed #) #)
            #);
          path: ^text;
          textFile: @file;
          loadFile:
            (# t: ^text;
               theText: @StyledText;
               pos, lineInx: @integer;
            enter path[]
            do true->loading;
               path[]->textFile.name;
               (if textFile.entry.exists
                   and textfile.entry.readable then
                   textFile.openRead;
                   textFile.scan
                   (# while:: (# do true->value #);
                   do ch->theText.put;
                   #);
                   theText[]->contents.contents;
                   textFile.close;
                else
                   'No read permissions to the file:\n     '->t[];
                   textFile.name->t.puttext;
                   (this(fileEditor)[], t[],'Read Error')
                     ->alertUser;
                   'no file'->theText.puttext;
                   theText[]->contents.contents;
               if);
               false->loading;
            #);
          saveFile:
            (# t: ^text
            do true->loading;
               (if textFile.entry.exists
                   and textfile.entry.writeable then
                   textFile.openWrite;
                   contents.contents->textFile.puttext;
                   textFile.close;
                else
                   'No write permissions to the file:\n     '->t[];
                   textFile.name->t.puttext;
                   (this(fileEditor)[], t[],'Write Error')
                     ->alertUser
               if);
               false->loading;
            #);
          dobind:
            (#
            do TRUE->bindTop->bindLeft->bindRight->bindBottom;
            #);
          open:: (# do this(fileEditor).size->size; doBind #);
       #);
     open::<
       (#
       do 'File Editor for: '->title;
          INNER;
          contents.open;
       #)
  #)
