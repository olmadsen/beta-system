ORIGIN '~beta/objectbrowser/v2.0.2/mpsinterface';
INCLUDE '~beta/mps/v5.0.1/astlevel'
--- mpsinterfacelib: Attributes ---
reload:
  (# fg: ^AST.fragmentGroup; fgf: ^AST.fragmentGroup;
     groupName: ^text; de: @diskEntry; donecheckOk: @Boolean; oldCatcher: ^AST.handler;
     reloadHandler: @AST.handler (* Private *)
       (#
       do (if no
           // AST.notificationNumbers.startingParsing then
              (if not (groupName[]->onOldGroup) then
              if);
           // AST.errorNumbers.notExisting then
              groupName[]->onGroupNotFound
           // AST.errorNumbers.arrayTooBig then
              'mps.arrayTooBig:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errorNumbers.grammarNotFound then
              'Grammar not found:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errorNumbers.badFormat then
              'Bad AST format:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errorNumbers.parseErrors then
              groupName[]->onParseErrors;
           // AST.errorNumbers.doubleFormDeclaration then
              'Two forms are declared with the same name: \n'->msg.prepend;
	      msg[]->onOtherError;
           // AST.errornumbers.noReadAccess then
              'Read access error:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errornumbers.noWriteAccess then
              'No write access:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errornumbers.writeAccessOnLstFileError then
              'Access error on .lst file:'->msg.prepend;msg[]->onOtherError;
           // AST.errornumbers.EOSError then
              'EOSError:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errornumbers.noSuchFile then
              'noSuchFile:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errornumbers.fileExists then
              'fileExists:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errornumbers.noSpaceLeft then
              'noSpaceLeft:\n'->msg.prepend; msg[]->onOtherError;
           // AST.errornumbers.otherFileError then
              'Other file error:\n'->msg.prepend; msg[]->onOtherError; 
           else
              (if true
               // no->AST.errorNumbers.fatalParseError then
                  no-100->onFatalParseError;
              if)
          if)  
       #)
  enter fg[]
  do fg.fullname->groupName[];
     groupName[]->onOpenGroup;
     (*fg.close; fg.init;*) fg[]->fgf[];
     (*(groupName[],screen[])*)
     (if AST.theCatcher[]
      // none then
         AST.theCatcher[]->oldCatcher[]; reloadHandler[]->AST.theCatcher[]
     if);
     screen[]->fg.unpack;
     (if AST.theCatcher[]
      // reloadHandler[] then oldCatcher[]->AST.theCatcher[]
     if);
     (if fg[]<>fgf[] then 'HOV HOV'->putline if);
     checkcheck: fg.prop.scanProp
       (#
	  doProp:: 
	    (# 
	    do
	       (if ('donecheck'->prop.equalNCS) then
		   TRUE->doneCheckOk; leave checkcheck; 
	       if);

	    #);

       #);
     (if not doneCheckOk then
	 (if not ((groupName[],fg[])->onUncheckedGroup) then
	     none ->fg[]
	 if)
     if);
     fg.diskFileName->de.path;
     (if not (de.exists and ((fg[],de.modtime)->modtimeOk)) then
	 (if not (groupName[]->onNewGroup) then
	     none ->fg[]
	 if)
     if);
     (groupName[],fg[])->onOpenGroupDone;
     fg[]->fgf[];
     (* Setup origin chain. *)
     loop:
       (if fgf[] <> none then
	   (if fgf.origin = none then
	       fgf[]->mySetupOrigin; fgf.origin->fgf[]; restart loop; 
	   if)
       if)
  #)
