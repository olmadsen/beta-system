ORIGIN '~beta/guienv/utils/treeview';
INCLUDE '~beta/mps/astlevel'
        'fileUtils'
        '~beta/mps/fragmentscanner';
BODY 'hierarchybody';
BODY 'hierarchysearchbody';
-- windowLib: Attributes --
hierarchy: treeview
  (#
     hirfolder: folder
       (#
          findFgandSelect:
            (# fgname: ^text; found: @boolean; grp: ^locationfolder
            enter fgname[]
            <<SLOT HIRfindFGAndSelect:DoPart>>
            exit (found,grp[])
            #)
       #);
     dependencyCache: folder
       (# formCache: @boolean;  #);
     locationfolder: hirfolder
       (#
          location: ^text;
          fg: ^astInterface.fragmentGroup;
          lfMenu: @menu
            (#
               openSeparate: @menuitem
                 (#
                    open:: 
                      (# 
                      do
                         'Open Separate'->name
                      #)
                 #);
               open::  (#  do openSeparate.open; openSeparate[]->append #)
            #)
       #);
     project: locationfolder
       (#
          <<SLOT projectlib:Attributes>>;
          hasSubprojects,isLoaded: @boolean;
          superProject: ^project;
          setup:< (#  do INNER #);
          setupListView:<
            (#  do INNER setupListView #);
          init:< Object;
          onProjectChange:< Object
       #);
     element: item
       (#
          location: ^text;
          setupListView:<
            (#  do INNER setupListView #)
       #);
     directoryProject:< project
       (#
          showTxtFiles: @boolean;
          onCollapse::< 
            (#  do items.clear; INNER #)
       #);
     DPproject:< project;
     projectFile:< project;
     temporaryProject:< project;
     rootProject:< project;
     group:< project
       (#
          hasFormCache,hasGroupCache,isSubGroup,hasNoDiskFile:
            @boolean;
          formCache,groupCache: ^dependencyCache;
          lastSelectedFf: ^astInterface.fragmentForm;
          theProject: ^project;
          init::<  (#  enter theProject[] do INNER #);
          onCollapse::<  (#  do items.clear; INNER #)
       #);
     fileItem:< element;
     form:< element (# ff: ^astInterface.fragmentForm #);
     properties:< hirfolder
       (#
          setup:< (#  do INNER #);
          setupListView:< (#  do INNER setupListView #)
       #);
     propertyElement: locationfolder
       (#
          setupListView:< (#  do INNER setupListView #);
          onExpand::<  (#  do THIS(folder)[]->onPropertyExpand #);
          onCollapse::<  (#  do items.clear #)
       #);
     origin:< propertyElement (#  #);
     include:< propertyElement (#  #);
     body:< propertyElement (#  #);
     build:< element;
     property:< element;
     txtfile:< element;
     onPropertyShowEditor:<
       (# lf: ^locationFolder enter lf[] do INNER #);
     onPropertyExpand:< (# pe: ^propertyElement enter pe[] do INNER #);
     
  #)  

-- projectlib: Attributes --
SaveProjectFileAs:
  (# filename: ^text
  enter filename[]
  <<SLOT temporaryProjectSavePrjFileAs:DoPart>>
  #);
  

