ORIGIN '../ymer';
INCLUDE '~beta/guienv/v1.3.1/utils/scrolleradds'
        'ymerBody';
-- fragmentBrowserlib: Attributes --
workspace: ymerLocalWindow
  (#
     cfe: ^browser.sbprivate.down.fragmentView.contents.codeViewerType;
     g: ^browser.projects.group;
     geList: @list
       (# element:: (# ge: ^editorEnv.groupEditor #);
          closeGroups: scan
            (# okToClose: @boolean
            do (if not (current.ge.closegroup->okToClose) then
                   leave closeGroups
               if)
            exit okToClose
            #);
       #);
     menubarType:: 
       (#
          fileMenu: @menu
            (#
               quitItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::
                           (#
                           do (if geList.closeGroups then
                                  THIS(workspace).close
                              if)
                           #)
                      #);
                    open::  (#  do 'w'->key;  'Close'->name #)
                 #);
               open:: 
                 (#  do 'File'->name; quitItem.open; quitItem[]->append #)
            #);
          importMenu: @menu
            (#
               iImportCurrentGroup: @menuItem
                 (#
                    eventhandler:: 
                      (#
                         onStatus:: 
                           (# t: ^text
                           do
                              (browser.currentGroup[] <> none )->value;
                              (if value then
                                  browser.currentGroup.name.copy->t[];
                                  '-**all**'->t.puttext;
                                  t[]->name
                               else
                                  'no group selected'->name
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              browser.currentGroup.getfg
                                ->panes.contents.importFragmentGroup
                           #)
                      #);
                 #);
                iImportCurrentForm: @menuItem
                 (#
                    eventhandler:: 
                      (#
                         onStatus:: 
                           (# t: ^text
                           do
                              (browser.currentForm[] <> none )->value;
                              (if value then
                                  browser.currentGroup.name.copy->t[];
                                  '-'->t.put;
                                  browser.currentForm.name->t.append;
                                  t[]->name
                               else
                                  'no fragment form selected'->name
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              browser.currentForm[]
                                ->panes.contents.importFragmentForm;
                              
                           #);
                         
                      #)
                 #);
               ImportForm: menuItem
                 (# ff: ^mps.AST.fragmentForm;
                    eventhandler:: 
                      (# onSelect:: 
                           (# 
                           do ff[]->panes.contents.importFragmentForm
                           #);
                         
                      #);
                    setName:
                      (# t: ^text
                      do (if (ff[] <> none ) then
                             browser.currentGroup.name.copy->t[];
                             '-'->t.put;
                             ff.name->t.append;
                             t[]->name
                         if)
                      #)
                 #);
              eventhandler::
                 (# onSelect::
                      (#
                         importItem: ^ImportForm
                      do (if browser.currentGroup[]<>g[] then
                             browser.currentGroup[]->g[];
                             clear;
                             iImportCurrentGroup[]->append;
                             (*iImportCurrentForm[]->append;*)
                             sep[]->append;
                             (if browser.currentGroup[] <> none then
                                 browser.currentGroup.fg.fragmentList.scan
                                 (#
                                 do (if current.type
                                     // mps.AST.formType then
                                        &ImportForm[]->importItem[];
                                        current.open->importItem.ff[];
                                        importItem.open;
                                        importItem[]->append;
                                        importItem.setName;
                                    if)
                                 #);
                             if)
                         if)
                      #)
                 #);
               sep: ^menuItem;
               open:: 
                 (#
                 do 'Import'->name;
                    iImportCurrentGroup.open;
                    iImportCurrentGroup[]->append;
                    (*iImportCurrentForm.open;
                    iImportCurrentForm[]->append;*)
                    &separator[]->sep[]; sep.open;
                 #)
            #);
          closeMenu: @menu
            (#
               importItem:
                 (# newIform: ^iForm; member: ^labelled; 
                 enter member[]
                 do
                    &iForm[]->newIform[];
                    member[]->newIform.member[];
                    newIform.open;
                    newIform[]->append;
                    newIform.setName;
                    
                 #);
               iForm: menuItem
                 (#
                    member: ^labelled;
                    eventhandler:: 
                      (#
                         onSelect:: 
                           (# 
                           do
                              member[]->panes.contents.deleteMember;
                              THIS(iForm)[]->delete
                           #);
                         
                      #);
                    setName: (#  do member.label->name #)
                 #);
               open::  (#  do 'Close'->name #)
            #);
          scrollMenu: @menu
            (#
               importItem:
                 (# newIform: ^iForm; member: ^labelled; 
                 enter member[]
                 do
                    &iForm[]->newIform[];
                    member[]->newIform.member[];
                    newIform.open;
                    newIform[]->append;
                    newIform.setName;
                    
                 #);
               iForm: menuItem
                 (#
                    member: ^labelled;
                    eventhandler:: 
                      (#
                         onSelect:: 
                           (#  do member.position->panes.theScroll #);
                         
                      #);
                    setName: (#  do member.label->name #)
                 #);
               open::  (#  do 'Scroll'->name #)
            #);
          Edit: @codeEditorEditMenu
            (#
               formEditorExists::  (#  do THIS(workspace).cfe[]->cfe[] #);
               writeStatus::  (#  do  #)
            #);
          View: @codeEditorViewMenu
            (# formEditorExists::  (#  do THIS(workspace).cfe[]->cfe[] #); 
            #);
          SLOTs: @codeEditorSLOTsMenu
            (# formEditorExists::  (#  do THIS(workspace).cfe[]->cfe[] #); 
            #);
          open:: 
            (# 
            do
               fileMenu.open;
               fileMenu[]->append;
               importMenu.open;
               importMenu[]->append;
               scrollMenu.open;
               scrollMenu[]->append;
               closeMenu.open;
               closeMenu[]->append;
               edit.open;
               edit[]->append;
               view.open;
               view[]->append;
               slots.open;
               slots[]->append;
               
            #)
       #);
     eventhandler:: 
       (# onAboutToClose:: 
            (# do (if geList.closeGroups->okToClose then
                      close
                  if)
            #)
       #);
     panes: @scroller
       (#
          contentsType:: pane
            (#
               onMemberResize:: paneResizable;
               changing: @boolean;
               FFVlist: @list (# element:: fragmentFormView #);
               fragmentFormView: labelled
                 (#
                    ff: ^mps.AST.fragmentForm;
                    onFatherFrameResizeRelative: @action
                      (#
                         w,w1,h,h1: @integer;
                         eventType:: theEventHandler.fatherFrameChanged;
                         
                      do
                         (if not changing then
                             size->(w,h); panes.size->(w1,h1); (w1-35,h)->size
                         if);
                         
                      #);
                    installResizeRelativeAction:
                      (#  do onFatherFrameResizeRelative[]->appendAction #);
                    dobind:
                      (# 
                      do installResizeRelativeAction; contents.dobind
                      #);
                    contentsType:: codemoveableEditor
                      (#
                         fe:
                           ^browser.sbprivate.down.fragmentView.contents.
                              codeViewerType;
                         dobind:
                           (# 
                           do
                              true->bindRight->bindBottom
                                ->bindLeft (*->bindTop*)
                           #);
                         selectNode:: 
                           (# ffV: ^fragmentFormView
                           do
                              node.frag[]->importFragmentForm->ffV[];
                              node[]->FFV.contents.SifViewer.setNode;
                              
                           #);
                         contentsType:: 
                           (#
                              eventHandler:: 
                                (# onMouseDown::  (#  do fe[]->cfe[] #)
                                #)
                           #);
                         open:: 
                           (# t: ^text; geElm: ^geList.element;
                              ge: ^editorEnv.groupEditor
                           do
                              THIS(fragmentFormView).contents[]
                                ->browser.onCodeViewOpen;
                              (none ,ff.father)->edenv.findOrCreateGroupEditor
                                ->ge[];
                              &geList.element[]->geElm[]; ge[]->geElm.ge[];
                              geElm[]->geList.append;
                              (ff.root[],none ,
                               THIS(fragmentFormView).contents[],ge[])
                                ->edenv.findOrCreateFormEditor->fe[];
                              (ff.father).fullName->fragmentDefaultName->t[];
                              '-'->t.put;
                              ff.name->t.append;
                              t[]->label
                           #);
                         charWidthType:: 
                           (# 
                           do
                              THIS(fragmentFormView).private.theLabel.style
                                ->ts[]
                           #);
                         getEncloser:: 
                           (#  do ffEncloser[]->theEncloser[] #);
                         
                      #);
                    ffEncloser: @codemoveableEditorEncloser
                      (#
                         getAST::  (#  do mps.AST[]->AST[] #);
                         getBETACFL::  (#  do mps.BETACFL[]->BETACFL[] #);
                         
                      #);
                    open:: 
                      (#
                         sz: @point;
                         openContents:: 
                           (# 
                           do (none ,sz)->contents.open; true->doneInInner
                           #)
                      enter sz
                      do hide; sz->size
                      #)
                 #);
               importFragmentForm:
                 (#
                    ffV: ^fragmentFormView;
                    w,h,pw,ph: @integer;
                    ff: ^mps.AST.fragmentForm
                 enter ff[]
                 do
                    l: FFVlist.scan
                      (# 
                      do
                         (if ff[] = current.ff[] then
                             current[]->ffV[]; leave l; 
                         if)
                      #);
                    (if ffV[] = none then (* not already in workspace *)
                        panes.size->(w,h);
                        &fragmentFormView[]->ffV[];
                        ff[]->ffV.ff[];
                        (THIS(contentsType)[],(w-35,150))->ffV.open;
                        true->changing;
                        ffV[]->THIS(contentsType).appendMember;
                        THIS(contentsType).appendsDone;
                        ffV.dobind;
                        ffV[]->FFVlist.append;
                        false->changing;
                        ffV.show;
                        ffV[]->(theMenubar).scrollMenu.importItem;
                        ffV[]->(theMenubar).closeMenu.importItem;
                        
                    if);
                    ffV.position->panes.theScroll;
                    
                 exit ffV[]
                 #);
               importFragmentGroup:
                 (#
                    ffV: ^fragmentFormView;
                    newFFVs: @list (# element:: fragmentFormView #);
                    w,h,pw,ph: @integer;
                    ff: ^mps.AST.fragmentForm;
                    fg: ^mps.AST.fragmentGroup;
                    
                 enter fg[]
                 do
                    panes.size->(w,h);
                    true->changing;
                    fg.fragmentList.scan
                      (# 
                      do
                         (if current.type
                          // mps.AST.formType then
                             current.open->ff[];
                             none ->FFV[];
                             l: FFVlist.scan
                               (# 
                               do
                                  (if ff[] = current.ff[] then
                                      current[]->ffV[]; leave l; 
                                  if)
                               #);
                             (if ffV[] = none then
                             (* not already in workspace *)
                                 &fragmentFormView[]->ffV[];
                                 ff[]->ffV.ff[];
                                 (THIS(contentsType)[],(w-35,150))->ffV.open;
                                 ffV[]->THIS(contentsType).appendMember;
                                 ffV[]->newFFVs.append;
                                 ffV[]->FFVlist.append;
                                 
                             if)
                         if);
                         
                      #);
                    THIS(contentsType).appendsDone;
                    l: newFFVs.scan
                      (# 
                      do
                         current.dobind;
                         current.show;
                         current[]->(theMenubar).scrollMenu.importItem;
                         current[]->(theMenubar).closeMenu.importItem;
                         
                      #);
                    
                 #);
               dobind:
                 (#  do TRUE->bindTop->bindLeft->bindRight->bindBottom #);
               open:: 
                 (#
                    setDefaults:: 
                      (# 
                      do true->verticalStacking; 50->minsize; 2->panewidth
                      #);
                    
                 do doBind
                 #);
               
            #);
          open::  (#  do TRUE->bindTop->bindLeft->bindRight->bindBottom #);
          
       #);
     open:: 
       (# t: ^text
       do
          'Workspace'->t[];
          ymerPrivate.workspaceCount+1->ymerPrivate.workspaceCount;
          (if ymerPrivate.workspaceCount > 1 then
              '<'->t.put; ymerPrivate.workspaceCount->t.putint; '>'->t.put; 
          if);
          t[]->title;
          panes.open;
          (500,700)->size;
          
       #);
     
  #)  

