ORIGIN '../ymer';

INCLUDE 'ymerBody';

--- fragmentBrowserlib: attributes ---
workspace: ymerLocalWindow
     (# menubarType::
          (# addMenu: @menu
               (# iAddCurrentForm: @menuItem
                    (# eventhandler::
                         (# onStatus::
                              (# t: ^text
                              do (browser.currentForm[]<>NONE)->value;
                                 (if value then
                                     browser.currentGroup.name.copy->t[];
                                     '-'->t.put;
                                     browser.currentForm.name->t.append;
                                     t[]->name
                                  else 'no fragment form selected'->name
                                 if)
                              #);
                            onSelect::
                              (#
                              do browser.currentForm[]
                                   ->panes.contents.addFragmentForm;
                              #);
                         #)
                    #);
                  iAddCurrentGroup: @menuItem
                    (# eventhandler::
                         (# onStatus::
                              (# t: ^text
                              do (browser.currentGroup[]<>NONE)->value;
                                 (if value then
                                     browser.currentGroup.name.copy->t[];
                                     '-**all**'->t.puttext;
                                     t[]->name
                                  else 'no group selected'->name
                                 if)
                              #);
                            onSelect::
                              (#            
                              do browser.currentGroup.getfg
                                   ->panes.contents.addFragmentGroup
                                 (*(browser.currentGroup.getfg).fragmentList.scan
                                 (#
                                 do (if current.type
                                     // mps.AST.formType then
                                        (#
                                        do current.open
                                             ->panes.contents.addFragmentForm;
                                        #);
                                    if);
                                  #)
                                  * *)
                              #)
                         #)
                    #);
                  open::
                    (#
                    do 'Add'->name;
                       iAddCurrentForm.open; iAddCurrentForm[]->append;
                       iAddCurrentGroup.open; iAddCurrentGroup[]->append;
                    #)
               #);
             removeMenu: @menu
               (# addItem:
                    (# newIform: ^iForm;
                       member: ^labelled;
                    enter member[]
                    do &iForm[]->newIform[];
                       member[]->newIform.member[];
                       newIform.open; newIform[]->append; newIform.setName;
                    #);
                  iForm: menuItem
                    (# member: ^labelled;
                       eventhandler::
                         (# onSelect::
                              (#
                              do member[]->panes.contents.deleteMember;
                                 this(iForm)[]->delete
                              #);
                         #);
                       setName: (# do member.label->name #)
                    #);
                  open::
                    (# do 'Remove'->name #)
               #);
             showMenu: @menu
               (# addItem:
                    (# newIform: ^iForm;
                       member: ^labelled;
                    enter member[]
                    do &iForm[]->newIform[];
                       member[]->newIform.member[];
                       newIform.open; newIform[]->append; newIform.setName;
                    #);
                  iForm: menuItem
                    (# member: ^labelled;
                       eventhandler::
                         (# onSelect::
                              (# w,h,pw,ph: @integer;
                              do member.position->(w,h);
(*w->putint;' '->put;h->putint;newline; *)
                                 panes.contents.position->(pw,ph);
(*pw->putint;' '->put;ph->putint;newline; *)
                                 (*(pw,h)->panes.contents.position*) (* panes.scrollIntoView *);
                                 (0,h)->panes.scroll; (* doesn't work - not implemented :-( *)
                              #);
                         #);
                       setName: (# do member.label->name #)
                    #);
                  open::
                    (# do 'Show'->name #)
               #);
             open::
               (#
               do addMenu.open; addMenu[]->append;
                  showMenu.open; showMenu[]->append;
                  removeMenu.open; removeMenu[]->append;
               #)
          #);
        panes: @scroller
          (# contentsType:: dynpane
               (# onMemberResize:: dynpaneResizable;
                  changing: @boolean;
                  FFVlist: @list(# element:: fragmentFormView #);
                  fragmentFormView: labelled
                    (# ff: ^mps.AST.fragmentForm;
                       onFatherFrameResizeRelative: @action
                         (# w,w1,h,h1: @integer;
                            eventType:: theEventHandler.fatherFrameChanged;
                         do (if not changing then
                                size->(w,h);
                                panes.size->(w1,h1);
                                (w1-35,h)->size
                            if);
                         #);
                       installResizeRelativeAction:
                         (# do onFatherFrameResizeRelative[]->appendAction #);
                       dobind:
                         (#
                         do installResizeRelativeAction;
                            contents.dobind
                         #);
                       contentsType:: codemoveableEditor
                         (# dobind:
                              (# do true->bindRight->bindBottom->bindLeft(*->bindTop*) #);
                            selectNode:: 
                              (# ffV: ^fragmentFormView
                              do node.frag[]->addFragmentForm->ffV[];
                                 (node[],1,0,0)
                                   ->FFV.contents.SifViewer.setFocus;
                              #);
                            open::
                              (# t: ^text;
                              do this(fragmentFormView).contents[]
                                   ->browser.onCodeViewOpen;
                                 (ffEncloser.getAST,ffEncloser.getBETACFL,
                                 ff[],NONE)
                                   ->newFragment;
                                 (ff.father).fullName->fragmentDefaultName->t[];
                                 '-'->t.put;ff.name->t.append;
                                 t[]->label
                              #); 
                            charWidthType::
                              (#
                              do this(fragmentFormView).private.theLabel.style
                                   ->ts[]
                              #);
                            getEncloser:: (# do ffEncloser[]->theEncloser[] #);
                         #); 
                       ffEncloser: @codemoveableEditorEncloser
                         (# getAST:: (# do mps.AST[]->AST[] #);
                            getBETACFL:: (# do mps.BETACFL[]->BETACFL[] #);
                         #);
                       open::
                         (# sz: @point;
                            openContents::
                              (#
                              do (none,sz)->contents.open; true->doneInInner
                              #)
                         enter sz
                         do hide; sz->size
                         #)
                    #);
                  addFragmentForm:
                    (# ffV: ^fragmentFormView;
                       w,h,pw,ph: @integer;
                       ff: ^mps.AST.fragmentForm
                    enter ff[]
                    do l: FFVlist.scan
                         (#
                         do (if ff[]=current.ff[] then
                                current[]->ffV[]; leave l;
                            if)
                         #);
                       (if ffV[]=NONE then (* not already in workspace *)
                           panes.size->(w,h);
                           &fragmentFormView[]->ffV[];
                           ff[]->ffV.ff[];
                           (this(contentsType)[], (w-35,150))->ffV.open;
                           
                           true->changing;
                           ffV[]-> this(contentsType).appendMember;
                           this(contentsType).appendsDone; ffV.dobind;
                           ffV[]->FFVlist.append;
                           false->changing;
                           
                           ffV.show;
                           
                           ffV[]->(theMenubar).removeMenu.addItem;
                       if);
                       ffV.position->(w,h);
                       panes.contents.position->(pw,ph);
                       (pw,-h)->panes.contents.position (* panes.scrollIntoView *);
                    exit ffV[]
                    #);
                  addFragmentGroup:
                    (# ffV: ^fragmentFormView;
                       newFFVs: @list(# element:: fragmentFormView #);
                       w,h,pw,ph: @integer;
                       ff: ^mps.AST.fragmentForm;
                       fg: ^mps.AST.fragmentGroup;
                    enter fg[]
                    do panes.size->(w,h);
                       true->changing;
                       fg.fragmentList.scan
                       (#
                       do (if current.type
                           // mps.AST.formType then
                              current.open->ff[];
                              NONE->FFV[];
                              l: FFVlist.scan
                                (#
                                do (if ff[]=current.ff[] then
                                       current[]->ffV[]; leave l;
                                   if)
                                #);
                              (if ffV[]=NONE then
                                  (* not already in workspace *)
                                  &fragmentFormView[]->ffV[];
                                  ff[]->ffV.ff[];
                                  (this(contentsType)[], (w-35,150))
                                    ->ffV.open;
                                  ffV[]-> this(contentsType).appendMember;
                                  ffV[]->newFFVs.append;
                                  ffV[]->FFVlist.append;
                              if)
                          if);
                       #);
                       this(contentsType).appendsDone;
                       l: newFFVs.scan
                         (#
                         do current.dobind; current.show;
                            current[]->(theMenubar).removeMenu.addItem;
                            current[]->(theMenubar).showMenu.addItem;
                         #);
                    #);
                  dobind:
                    (# do TRUE->bindTop->bindLeft->bindRight->bindBottom #);
                  open:: 
                    (# setDefaults:: 
                         (#
                         do true->verticalStacking;
                            50->minsize;
                            2->panewidth
                         #);
                    do doBind
                    #);
               #);
             open::
               (# do TRUE->bindTop->bindLeft->bindRight->bindBottom #);
          #);
        open::
          (# t: ^text
          do 'Workspace'->t[];
             ymerPrivate.workspaceCount+1->ymerPrivate.workspaceCount; 
             (if ymerPrivate.workspaceCount>1 then
                 '<'->t.put; ymerPrivate.workspaceCount->t.putint; '>'->t.put;
             if);
             t[]->title; panes.open;
          #);
     #)
