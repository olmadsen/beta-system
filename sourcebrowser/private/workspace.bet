ORIGIN '../ymer';
INCLUDE '~beta/guienv/v1.3.1/utils/scrolleradds'
'ymerBody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
--- workspacePrivate: descriptor ---
(# g: ^browser.projects.group;
   geList: @list
     (# element:: (# ge: ^editorEnv.groupEditor #);
        closeGroups:
          (# okToClose: @boolean
          do true->okToClose;
             l: scan
               (#
               do (if not (current.ge.closegroup->okToClose) then
                      leave l
                  if)
               #)
          exit okToClose
          #);
     #);
#)

--- workspaceFileQuitSelect: dopart ---
do (if workspacePrivate.geList.closeGroups then
       THIS(workspace).close
   if)
--- workspaceFileOpen: dopart ---
do 'File'->name; quitItem.open; quitItem[]->append

--- workspaceImportAllStatus: dopart ---
do (browser.currentGroup[] <> none )->value;
   (if value then
       '-**all**'->(browser.currentGroup.name.copy).append->name
    else
       'no group selected'->name
   if)
--- workspaceImportAllSelect: dopart ---   
do browser.currentGroup.getfg->UI.panes.contents.importFragmentGroup
--- workspaceImportItems: attributes ---   
ImportForm: menuItem
  (# ff: ^mps.AST.fragmentForm;
     eventhandler:: 
       (# onSelect:: 
            (# 
            do ff[]->UI.panes.contents.importFragmentForm;
               this(ImportForm)[]->delete
            #);
          
       #);
     setName:
       (# t: ^text
       do (if (ff[] <> none ) then
              browser.currentGroup.name.copy->t[];
              '-'->t.put;
              ff.name->t.append;
              t[]->name
          if)
       #)
  #);
--- workspaceRefreshImportMenu: descriptor ---
(# importItem: ^ImportForm; sep: ^menuItem;
do (if browser.currentGroup[]<>workspacePrivate.g[] then
       browser.currentGroup[]->workspacePrivate.g[];
       clear;
       iImportCurrentGroup[]->append;
       &separator[]->sep[]; sep.open;
       sep[]->append;
       (if browser.currentGroup[] <> none then
           browser.currentGroup.fg.fragmentList.scan
           (#
           do (if current.type
               // mps.AST.formType then
                  &ImportForm[]->importItem[];
                  current.open->importItem.ff[];
                  importItem.open;
                  importItem[]->append;
                  importItem.setName;
              if)
           #);
       if)
   if)
#)
--- workspaceImportOpen: dopart ---
do 'Import'->name;
   iImportCurrentGroup.open;
   iImportCurrentGroup[]->append;
   
--- workspaceCloseItems: attributes ---
closeItem:
  (# newCform: ^cForm;
     member: ^UI.panes.contents.fragmentFormView; 
  enter member[]
  do
     &cForm[]->newCform[];
     member[]->newCform.member[];
     newCform.open;
     newCform[]->append;
     newCform.setName;
     
  #);
cform: menuItem
  (# member: ^UI.panes.contents.fragmentFormView;
     eventhandler:: 
       (# onSelect:: 
            (# 
            do member[]->UI.panes.contents.deleteMember;
               member[]->UI.panes.contents.FFVlist.at
                 ->UI.panes.contents.FFVlist.delete;
               l: scrollMenu.scan
                 (# mi: ^scrollMenu.sForm;
                 do  current[]->mi[];
                    (if mi.member[]=member[] then
                        current[]->scrollMenu.delete;
                        leave l
                    if);
                 #);
               THIS(cform)[]->delete;
            #)
       #);
     setName: (#  do member.label->name #)
  #);

--- workspaceScrollItems: attributes ---
scrollItem:
  (# newSform: ^sForm;
     member: ^UI.panes.contents.fragmentFormView; 
  enter member[]
  do &sform[]->newSform[];
     member[]->newSform.member[];
     newSform.open;
     newSform[]->append;
     newSform.setName
  #);
sform: menuItem
  (# member: ^UI.panes.contents.fragmentFormView;
     eventhandler:: 
       (# onSelect:: 
            (#  do member.position->UI.panes.theScroll #);
       #);
     setName: (# do member.label->name #)
  #);

--- workspaceUI: descriptor ---
(# panes: @scroller
     (#
        contentsType:: pane
          (#
             onMemberResize:: paneResizable;
             eventhandler:: 
               (# onFatherFrameChanged::
                    (# w,w1,h,h1: @integer;
                    do (if not changing then
                           size->(w,h); UI.panes.size->(w1,h1); (w1-35,h)->size
                       if);
                    #)
               #);
             changing: @boolean;
             FFVlist: @list (# element:: fragmentFormView #);
             fragmentFormView: labelled
               (#
                  ff: ^mps.AST.fragmentForm;
                  dobind:
                    (# do contents.dobind #);
                  contentsType:: codemoveableEditor
                    (# isolated:: trueObject;
                       fe:
                         ^browser.sbprivate.down.fragmentView.contents.
                         codeViewerType;
                       dobind:
                         (# 
                         do
                            true->bindRight->bindBottom
                              ->bindLeft->bindTop
                         #);
                       selectNode:: 
                         (# ffV: ^fragmentFormView
                         do (if true
                             // (theEditorRoot.frag.root[]->theEditorRoot.equal) and separate then
                                (theEditorRoot.frag[],node[])->viewSeparate
                             // (theEditorRoot.frag.root[]->theEditorRoot.equal) and not separate then
                                theEditorRoot.frag[]->importFragmentForm->ffV[];
                                node[]->FFV.contents.SifViewer.setNode
                             // (not (theEditorRoot.frag.root[]->theEditorRoot.equal)) and separate then
                                (theEditorRoot[],node[])->fe.openSubeditor
                             else
                                'workspace.selectnode: theEditorRoot[]<> theEditorRoot.frag.root[] and not separate'
                                  ->putLine
                            if)
                         #);
                       contentsType:: 
                         (#
                            eventHandler:: 
                              (# onMouseDown::  (#  do fe[]->cfe[] #)
                              #)
                         #);
                       open:: 
                         (# t: ^text; geElm: ^workspacePrivate.geList.element;
                            ge: ^editorEnv.groupEditor
                         do
                            THIS(fragmentFormView).contents[]
                              ->browser.onCodeViewOpen;
                            (none ,ff.father, editMode)->edenv.findOrCreateGroupEditor
                              ->ge[];
                            &workspacePrivate.geList.element[]->geElm[]; ge[]->geElm.ge[];
                            geElm[]->workspacePrivate.geList.append;
                            (ff[],ff.root[],none ,
                            THIS(fragmentFormView).contents[],ge[])
                              ->edenv.findOrCreateFormEditor->fe[]->cfe[];
                            (ff.father).fullName->fragmentDefaultName->t[];
                            '-'->t.put;
                            ff.name->t.append;
                            t[]->label;
                            this(fragmentFormView).contents[]->onCodeViewOpen
                         #);
                       charWidthType:: 
                         (# 
                         do
                            THIS(fragmentFormView).private.theLabel.style
                              ->ts[]
                         #);
                       getEncloser:: 
                         (#  do ffEncloser[]->theEncloser[] #);
                       
                    #);
                  ffEncloser: @codemoveableEditorEncloser
                    (#
                       getAST::  (#  do mps.AST[]->AST[] #);
                       getBETACFL::  (#  do mps.BETACFL[]->BETACFL[] #);
                       
                    #);
                  open:: 
                    (#
                       sz: @point;
                       openContents:: 
                         (# 
                         do (none ,sz)->contents.open; true->doneInInner
                         #)
                    enter sz
                    do hide; sz->size
                    #)
               #);
             importFragmentForm:
               (#
                  ffV: ^fragmentFormView;
                  w,h,pw,ph: @integer;
                  ff: ^mps.AST.fragmentForm
               enter ff[]
               do
                  l: FFVlist.scan
                    (# 
                    do
                       (if ff[] = current.ff[] then
                           current[]->ffV[]; leave l; 
                       if)
                    #);
                  (if ffV[] = none then (* not already in workspace *)
                      UI.panes.size->(w,h);
                      &fragmentFormView[]->ffV[];
                      ff[]->ffV.ff[];
                      (THIS(contentsType)[],(w-35,150))->ffV.open;
                      true->changing;
                      ffV[]->THIS(contentsType).appendMember;
                      THIS(contentsType).appendsDone;
                      ffV.dobind;
                      ffV[]->FFVlist.append;
                      false->changing;
                      ffV.show;
                      ffV[]->(theMenubar).scrollMenu.scrollItem;
                      ffV[]->(theMenubar).closeMenu.closeItem;
                      
                  if);
                  ffV.position->UI.panes.theScroll;
                  
               exit ffV[]
               #);
             importFragmentGroup:
               (#
                  ffV: ^fragmentFormView;
                  newFFVs: @list (# element:: fragmentFormView #);
                  w,h,pw,ph: @integer;
                  ff: ^mps.AST.fragmentForm;
                  fg: ^mps.AST.fragmentGroup;
                  
               enter fg[]
               do
                  UI.panes.size->(w,h);
                  true->changing;
                  fg.fragmentList.scan
                  (# 
                  do
                     (if current.type
                      // mps.AST.formType then
                         current.open->ff[];
                         none ->FFV[];
                         l: FFVlist.scan
                           (# 
                           do
                              (if ff[] = current.ff[] then
                                  current[]->ffV[]; leave l; 
                              if)
                           #);
                         (if ffV[] = none then
                             (* not already in workspace *)
                             &fragmentFormView[]->ffV[];
                             ff[]->ffV.ff[];
                             (THIS(contentsType)[],(w-35,150))->ffV.open;
                             ffV[]->THIS(contentsType).appendMember;
                             ffV[]->newFFVs.append;
                             ffV[]->FFVlist.append;
                             
                         if)
                     if);
                     
                  #);
                  THIS(contentsType).appendsDone;
                  l: newFFVs.scan
                    (# 
                    do
                       current.dobind;
                       current.show;
                       current[]->(theMenubar).scrollMenu.scrollItem;
                       current[]->(theMenubar).closeMenu.closeItem;
                       
                    #);
                  false->changing;
                  
               #);
             dobind:
               (#  do (*TRUE->bindTop->bindLeft->bindRight->bindBottom*) #);
             open:: 
               (#
                  setDefaults:: 
                    (# 
                    do true->verticalStacking; 50->minsize; 2->panewidth
                    #);
                  
               do doBind
               #);
             
          #);
        open::  (#  do father.size->size; TRUE->bindTop->bindLeft->bindRight->bindBottom #);
        
     #);
   
#)  

--- workspaceOnAboutToClose: dopart ---
do (if workspacePrivate.geList.closeGroups->okToClose then
       close
   if)
--- workspaceOpen: dopart ---
do (# t: ^text
   do 'Workspace'->t[];
      ymerPrivate.workspaceCount+1->ymerPrivate.workspaceCount;
      (if ymerPrivate.workspaceCount > 1 then
          '<'->t.put; ymerPrivate.workspaceCount->t.putint; '>'->t.put; 
      if);
      t[]->title;
      UI.panes.open;
      (500,700)->size;
      INNER open
   #);
   
