ORIGIN '../ymer';
INCLUDE '~beta/guienv/v1.6/utils/scrolleradds'
	'ymerBody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- workspacePrivate: Descriptor --
(#
   g: ^browser.projects.group;
   geList: @list
     (#
	element::  (# ge: ^editorEnv.groupEditor #);
	closeGroups:
	  (# okToClose: @boolean
	  do
	     true->okToClose;
	     l: scan
	       (#
	       do
		  (if not (current.ge.closegroup->okToClose) then leave l if)
	       #)
	  exit okToClose
	  #);

     #);

#)

-- workspaceFileCloseSelect: DoPart --
do (if workspacePrivate.geList.closeGroups then THIS(workspace).close if)

-- workspaceFileOpen: DoPart --
do 'File'->name; closeItem.open; closeItem[]->append

-- workspaceImportAllStatus: DoPart --
do
   (browser.currentGroup[] <> none )->value;
   (if value then
       '-**all**'->(browser.currentGroup.name.copy).append->name
    else
       'no group selected'->name
   if)

-- workspaceImportAllSelect: DoPart --
do browser.currentGroup.getfg->UI.panes.contents.importFragmentGroup;

-- workspaceImportItems: Attributes --
ImportForm: menuItem
  (#
     ff: ^mps.AST.fragmentForm;
     eventhandler::
       (#
	  onSelect::
	    (#
	    do
	       ff[]->UI.panes.contents.importFragmentForm;
	       THIS(ImportForm)[]->delete
	    #);

       #);
     setName:
       (# t: ^text
       do
	  (if (ff[] <> none ) then
	      browser.currentGroup.name.copy->t[];
	      '-'->t.put;
	      ff.name->t.append;
	      t[]->name
	  if)
       #)
  #);


-- workspaceRefreshImportMenu: Descriptor --
(# importItem: ^ImportForm; sep: ^menuItem;
do
   (if browser.currentGroup[] <> workspacePrivate.g[] then
       browser.currentGroup[]->workspacePrivate.g[];
       clear;
       iImportCurrentGroup[]->append;
       &separator[]->sep[];
       sep.open;
       sep[]->append;
       (if browser.currentGroup[] <> none then
	   browser.currentGroup.fg.fragmentList.scan
	     (#
	     do
		(if current.type
		 // mps.AST.formType then
		    &ImportForm[]->importItem[];
		    current.open->importItem.ff[];
		    importItem.open;
		    importItem[]->append;
		    importItem.setName;

		if)
	     #);

       if)
   if)
#)

-- workspaceImportOpen: DoPart --
do 'Import'->name; iImportCurrentGroup.open; iImportCurrentGroup[]->append;

-- workspaceCloseItems: Attributes --
closeItem:
  (# newCform: ^cForm; member: ^UI.panes.contents.fragmentFormView;
  enter member[]
  do
     &cForm[]->newCform[];
     member[]->newCform.member[];
     newCform.open;
     newCform[]->append;
     newCform.setName;

  #);
cform: menuItem
  (#
     member: ^UI.panes.contents.fragmentFormView;
     eventhandler::
       (#
	  onSelect::
	    (#
	    do
	       member[]->UI.panes.contents.deleteMember;
	       member[]->UI.panes.contents.FFVlist.at
		 ->UI.panes.contents.FFVlist.delete;
	       l: scrollMenu.scan
		 (# mi: ^scrollMenu.sForm;
		 do
		    current[]->mi[];
		    (if mi.member[] = member[] then
			current[]->scrollMenu.delete; leave l
		    if);

		 #);
	       THIS(cform)[]->delete;

	    #)
       #);
     setName: (#  do member.label->name #)
  #);


-- workspaceScrollItems: Attributes --
scrollItem:
  (# newSform: ^sForm; member: ^UI.panes.contents.fragmentFormView;
  enter member[]
  do
     &sform[]->newSform[];
     member[]->newSform.member[];
     newSform.open;
     newSform[]->append;
     newSform.setName
  #);
sform: menuItem
  (#
     member: ^UI.panes.contents.fragmentFormView;
     eventhandler::
       (# onSelect::  (#  do member.position->UI.panes.theScroll #);
       #);
     setName: (#  do member.label->name #)
  #);


-- workspaceUI: Descriptor --
(#
   panes: @scroller
     (#
	contentsType:: pane
	  (#
	     onMemberResize:: paneResizable;
	     fatherFrame::
	       (# do ((0,0),panes.viewSize)->fr #);
	     eventhandler::
	       (#
		  onFatherFrameChanged::
		    (# w,w1,h,h1: @integer;
		    do
		       (if not changing then
			   size->(w,h); UI.panes.viewsize->(w1,h1);
			   (w1,h)->size
		       if);

		    #)
	       #);
	     changing: @boolean;
	     FFVlist: @list (# element:: fragmentFormView #);
	     fragmentFormView: labelled
	       (# zoomed: @boolean;
		  eventhandler::
		    (# onMouseDown::
			 (#
			 do (if doubleclick then
				(if zoomed then
				    false->zoomed;
				    panes.contents.unzoom
				 else
				    true->zoomed;
				    this(labelled)[]
				      ->panes.contents.zoomMember
				if)
			    if)
			 #)
		    #);
		  ff: ^mps.AST.fragmentForm;
		  dobind: (#  do contents.dobind #);
		  contentsType:: codemoveableEditor
		    (#
		       isolated:: trueObject;
		       fe: ^codeeditor;
		       dobind:
			 (#
			 do true->bindRight->bindBottom->bindLeft->bindTop
			 #);
		       selectNode::
			 (# ffV: ^fragmentFormView
			 do
			    (if true
			     //
			     (theEditorRoot.frag.root[]->theEditorRoot.equal)
			     and
			     separate then
				(theEditorRoot.frag[],node[])->viewSeparate
			     //
			     (theEditorRoot.frag.root[]->theEditorRoot.equal)
			     and
			     not
			     separate then
				theEditorRoot.frag[]->importFragmentForm->ffV[];
				node[]->FFV.contents.SifViewer.setNode
			     //
			     (not
			      (theEditorRoot.frag.root[]->theEditorRoot.equal))
			     and
			     separate then
				(theEditorRoot[],node[])->fe.openSubeditor
			     //
			     (not
			      (theEditorRoot.frag.root[]->theEditorRoot.equal))
			     and
			     not
			     separate then
				theEditorRoot.frag[]->importFragmentForm->ffV[];
				node[]->FFV.contents.SifViewer.setNode
			    if)
			 #);
		       contentsType::
			 (#
			    eventHandler::
			      (# onMouseDown::  (#  do fe[]->cfe[] #) #)
			 #);
		       open::
			 (#
			    t: ^text;
			    geElm: ^workspacePrivate.geList.element;
			    ge: ^editorEnv.groupEditor
			 do
			    THIS(fragmentFormView).contents[]
			      ->browser.onCodeViewOpen;
			    (THIS(workspace)[],ff.father,editMode)
			      ->edenv.findOrCreateGroupEditor->ge[];
			    &workspacePrivate.geList.element[]->geElm[];
			    ge[]->geElm.ge[];
			    geElm[]->workspacePrivate.geList.append;
			    (ff[],ff.root[],none ,
			     THIS(fragmentFormView).contents[],ge[])
			      ->edenv.findOrCreateFormEditor->fe[];
			    (ff.father).fullName->fragmentDefaultName->t[];
			    '-'->t.put;
			    ff.name->t.append;
			    t[]->label;
			    THIS(fragmentFormView).contents[]->onCodeViewOpen
			 #);
		       charWidthType::
			 (#
			 do THIS(fragmentFormView).private.theLabel.style->ts[]
			 #);
		       getEncloser::  (#  do ffEncloser[]->theEncloser[] #);

		    #);
		  ffEncloser: @codemoveableEditorEncloser
		    (#
		       getAST::  (#  do mps.AST[]->AST[] #);
		       getBETACFL::  (#  do mps.BETACFL[]->BETACFL[] #);

		    #);
		  open::
		    (#
		       sz: @point;
		       openContents::
			 (#
			 do (none ,sz)->contents.open; true->doneInInner
			 #)
		    enter sz
		    do hide; sz->size
		    #)
	       #);
	     importFragmentForm:
	       (#
		  ffV: ^fragmentFormView;
		  w,h,pw,ph: @integer;
		  ff: ^mps.AST.fragmentForm
	       enter ff[]
	       do cursors.watch[]->mouse.busyCursor;
		  l: FFVlist.scan
		    (#
		    do
		       (if ff[] = current.ff[] then
			   current[]->ffV[];
			   leave l;
		       if)
		    #);
		  (if ffV[] = none then (* not already in workspace *)
		      UI.panes.viewsize->(w,h);
		      &fragmentFormView[]->ffV[];
		      ff[]->ffV.ff[];
		      (THIS(contentsType)[],(w,150))->ffV.open;
		      true->changing;
		      ffV[]->THIS(contentsType).appendMember;
		      THIS(contentsType).appendsDone;
		      ffV.dobind;
		      ffV[]->FFVlist.append;
		      false->changing;
		      ffV.show;
		      ffV[]->(theMenubar).scrollMenu.scrollItem;
		      ffV[]->(theMenubar).closeMenu.closeItem;
		  if);
		  ffV.contents.fe[]->cfe[];
		  ffV.position->UI.panes.theScroll;
		  NONE->mouse.busyCursor;

	       exit ffV[]
	       #);
	     importFragmentGroup:
	       (#
		  ffV: ^fragmentFormView;
		  newFFVs: @list (# element:: fragmentFormView #);
		  w,h,pw,ph: @integer;
		  ff: ^mps.AST.fragmentForm;
		  fg: ^mps.AST.fragmentGroup;

	       enter fg[]
	       do cursors.watch[]->mouse.busyCursor;
		  UI.panes.viewsize->(w,h);
		  true->changing;
		  fg.fragmentList.scan
		    (#
		    do
		       (if current.type
			// mps.AST.formType then
			   current.open->ff[];
			   none ->FFV[];
			   l: FFVlist.scan
			     (#
			     do
				(if ff[] = current.ff[] then
				    current[]->ffV[]; leave l;
				if)
			     #);
			   (if ffV[] = none then (* not already in workspace *)
			       &fragmentFormView[]->ffV[];
			       ff[]->ffV.ff[];
			       (THIS(contentsType)[],(w,150))->ffV.open;
			       ffV[]->THIS(contentsType).appendMember;
			       ffV[]->newFFVs.append;
			       ffV[]->FFVlist.append;

			   if)
		       if);

		    #);
		  THIS(contentsType).appendsDone;
		  l: newFFVs.scan
		    (#
		    do
		       current.dobind;
		       current.show;
		       current[]->(theMenubar).scrollMenu.scrollItem;
		       current[]->(theMenubar).closeMenu.closeItem;

		    #);
		  false->changing;
		  NONE->mouse.busyCursor;

	       #);
	     dobind:
	       (#
	       do (*TRUE->bindTop->bindLeft->bindRight->bindBottom*)
	       #);
	     open::
	       (#
		  setDefaults::
		    (#
		    do true->verticalStacking; 50->minsize; 2->panewidth
		    #);

	       do doBind
	       #);

	  #);
	open::
	  (#
	  do father.size->size; TRUE->bindTop->bindLeft->bindRight->bindBottom
	  #);

     #);

#)

-- workspaceOnAboutToClose: DoPart --
do workspacePrivate.geList.closeGroups->okToClose

-- workspaceOpen: DoPart --
do
     (# t: ^text
     do
	'Workspace'->t[];
	ymerPrivate.workspaceCount+1->ymerPrivate.workspaceCount;
	(if ymerPrivate.workspaceCount > 1 then
	    '<'->t.put; ymerPrivate.workspaceCount->t.putint; '>'->t.put;
	if);
	t[]->title;
	UI.panes.open;
	(500,700)->size;
	INNER open
     #);
