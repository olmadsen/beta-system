ORIGIN  '~beta/guienv/v1.3/guienv';

INCLUDE '~beta/guienv/v1.3/fields';
INCLUDE '~beta/guienv/v1.3/stddialogs';

INCLUDE '../xprompts';

INCLUDE '~beta/basiclib/v1.4/file';

INCLUDE '../dynpane';
INCLUDE '~beta/objectbrowser/v2.0/UI/obguienvadds';
INCLUDE '~beta/objectbrowser/v2.0/UI/labelled';

--- guienvlib: attributes ---
parseEditor: window
  (# path: ^text;
     loadFiles:
       (# 
       enter path[]
       do (if path[]<>NONE then
              (* load BETA text file *)
              '.bet'
                ->path.copyAppend
                ->contents.theTextEditor.contents.loadFile;
              
              (* load LST text file *)
              '.lst'
                ->path.copyAppend
                ->contents.theLSTView.contents.loadFile;
          if)
       #);
     menubarType::
       (# fileMenu: @menu
            (# revertItem: @menuItem
                 (# eventHandler::
                      (# onSelect::
                           (#
                           do path[]->loadFiles;
                           #)
                      #);
                    open:: (# do 'Revert'->name #)
                 #);
               saveItem: @menuItem
                 (# eventHandler::
                      (# onSelect::
                           (# do contents.theTextEditor.contents.saveFile #)
                      #);
                    open:: 
                      (# do 'Save'->name #)
                 #);
               quitItem: @menuItem
                 (# inx: @integer;
                    eventHandler:: 
                      (# onSelect::
                           (# askuser: promptForBoolean
                                (# ok::
                                     (#
                                     do this(parseEditor).contents.theTextEditor.contents.saveFile;
                                        this(parseEditor).close
                                     #);
                                   notok:: (# do this(parseEditor).close #)
                                #)
                           do (this(parseEditor)[],'Quit','Save before close?')
                                ->(&askUser[]).popup
                           #)
                      #);
                    open:: 
                      (# do 'Close'->name #)
                 #);
               open::
                 (#
                 do 'File'->name;
                    revertItem.open; revertItem[]->append;
                    saveItem.open; saveItem[]->append;
                    quitItem.open; quitItem[]->append
            #)#);
          editMenu: @menu
            (# clipboard: @text;
               cutItem: @menuItem
                 (# eventHandler::
                      (# onStatus::
                           (#
                           do (contents.theTextEditor.contents.contents.selection.start<>contents.theTextEditor.contents.contents.selection.end)->value #);
                         onSelect::
                           (#
                           do clipboard.clear;
                              contents.theTextEditor.contents.contents.selection.contents->clipboard.append;
                              contents.theTextEditor.contents.contents.delete
                           #)
                      #);
                    open:: (# do 'Cut'->name #)
                 #);
               copyItem: @menuItem
                 (# eventHandler::
                      (# onStatus::
                           (# do (contents.theTextEditor.contents.contents.selection.start<>contents.theTextEditor.contents.contents.selection.end)->value #);
                         onSelect::
                           (#
                           do clipboard.clear;
                              contents.theTextEditor.contents.contents.selection.contents->clipboard.append;
                           #)
                      #);
                    open:: 
                      (# do 'Copy'->name #)
                 #);
               pasteItem: @menuItem
                 (# inx: @integer;
                    eventHandler:: 
                      (#  onStatus::
                           (# do (clipboard.length<>0)->value #);
                         onSelect::
                           (#
                           do clipboard[]
                                ->contents.theTextEditor.contents.contents.insert
                           #)
                      #);
                    open:: 
                      (# do 'Paste'->name #)
                 #);
               open::
                 (#
                 do 'Edit'->name;
                    cutItem.open; cutItem[]->append;
                    copyItem.open; copyItem[]->append;
                    pasteItem.open; pasteItem[]->append
            #)#);
          open:: 
            (#
            do fileMenu.open; fileMenu[]->append;
               editMenu.open; editMenu[]->append
            #)
       #); (* menubarType *)
     contents: @dynpane
       (# dobind:
            (# 
            do TRUE->bindTop->bindLeft->bindRight->bindBottom;
               thetextEditor.dobind;
               theLSTView.dobind;
            #);
          fileEditor: labelled
            (# loading: @boolean;
               contentsType::< textEditor
                 (# path: ^text;
                    textFile: @file;
                    lineStarts: [1000]@integer;
                    lineMax: @integer;
                    loadFile:
                      (#  t: ^text;
                         theText: @StyledText;
                         pos, lineInx: @integer;
                      enter path[]
                      do true->loading;
                         path[]->textFile.name;
                         (if textFile.entry.exists
                             and textfile.entry.readable then
                             textFile.openRead;
                             1->pos->lineInx->lineStarts[1];
                             textFile.scan
                             (# while:: (# do true->value #);
                             do pos+1->pos;
                                ch->theText.put;
                                (if ch=ascii.newLine then
                                    lineInx+1->lineInx;
                                    (if lineInx=lineStarts.range then
                                        1000->lineStarts.extend if);
                                    pos->lineStarts[lineInx];
                                if)
                             #);
                             lineInx+1->lineInx;
                             (if lineInx=lineStarts.range then
                                 1000->lineStarts.extend if);
                             pos->lineStarts[lineInx];
                             lineInx->lineMax;
                             theText[]->contents.contents;
                             textFile.close;
                          else
                             'No read permissions to the file:\n     '->t[];
                             textFile.name->t.puttext;
                             (this(parseEditor)[], t[],'Read Error')
                               ->alertUser;
                             'no file'->theText.puttext;
                             1->pos->lineInx->lineStarts[1];
                             9->lineStarts[2];
                             theText[]->contents.contents;
                         if);
                         false->loading;
                      #);
                    saveFile:
                      (# t: ^text
                      do true->loading;
                         (if textFile.entry.exists
                             and textfile.entry.writeable then
                             textFile.openWrite;
                             contents.contents->textFile.puttext;
                             textFile.close;
                          else
                             'No write permissions to the file:\n     '->t[];
                             textFile.name->t.puttext;
                             (this(parseEditor)[], t[],'Write Error')
                               ->alertUser
                         if);
                         false->loading;
                      #);
                    selectLine:
                      (# lineInx: @integer
                      enter lineInx
                      do (lineStarts[lineInx]-1,lineStarts[lineInx+1]-1)
                           ->contents.selection;
                         contents.selection.scrollIntoView;
                      #);
                    locateLine:
                      (# pos, lineInx: @integer
                      enter pos
                      do loop:
                           (if lineStarts[lineInx+1->lineInx]<pos then
                               restart loop
                           if)
                      exit lineInx-1
                      #);
                    dobind:
                      (#
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom;
                      #);
                 #);
               dobind:
                 (# do installResizeRelativeAction #);
               open::<
                 (#
                 do INNER;
                    contents.dobind;
                 #)
            #);
          theTextEditor: @fileEditor
            (# open::
                 (# w,h: @integer
                 do 'BETA Text File'->label;
                    this(parseEditor).size->(w,h);
                    (w,((h*2)/3)-1)->size;
                 #)
            #);
          theLSTView: @fileEditor
            (# contentsType::
                 (# contentsType::
                      (# eventhandler::
                           (# onBeforeChange:: (# do loading->allow #);
                              lineInx: @integer;
                              selectLine:
                                (# lineInx: @integer;
                                   t: @text;
                                enter lineInx
                                do (lineStarts[lineInx]-1,lineStarts[lineInx+1]-1)
                                     ->scanText(# do ch->t.put #);
                                   t.reset;
                                   l: (if t.get='#' then
                                          t.getInt(# syntaxError::
                                                       (#
                                                       do lineInx-1->lineInx;
                                                          (if lineInx>0 then
                                                              restart l
                                                           else leave l
                                                       if);
                                               #) #)
                                         ->theTextEditor.contents.selectLine;
                                    else lineInx-1->lineInx;
                                       (if lineInx>0 then restart l if);
                                   if)
                                #);
                              onKeyDown::
                                (# 
                                do (if ch
                                    // ascii.cr
                                    // ascii.nl then
                                       (if lineInx=0 then
                                           1->lineInx
                                        else
                                           lineInx+1->lineInx;
                                           (if lineInx>lineMax then 1->lineInx if)
                                       if);
                                       (lineStarts[lineInx]-1,lineStarts[lineInx+1]-1)
                                         ->selection;
                                       lineInx->selectLine
                                   if)
                                #);
                              onMouseDown::
                                (#
                                do localPosition
                                     ->ptToPos
                                     ->locateLine
                                     ->lineInx
                                     ->selectLine;
                                #)
                           #)
                      #);
                 #);
               open:: 
                 (# w,h: @integer;
                 do 'Parse Errors'->label;
                    this(parseEditor).size->(w,h);
                    (w,(h/3)-1)->size;
                 #)
            #);
          open::
            (# setDefaults:: (# do true->verticalStacking #)
            do theTextEditor.open; theLSTView.open;
               theLSTView[]->appendMember; theTextEditor[]->appendMember;
               appendsDone; dobind
            #)
       #);
     open::<
       (#
       do 'Parse Errors Editor'->title;
          INNER;
          contents.open
       #)
  #)
