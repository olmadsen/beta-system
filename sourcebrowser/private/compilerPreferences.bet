ORIGIN '../ymer';

INCLUDE 'ymerBody';

INCLUDE '~beta/basiclib/v1.4/basicsystemenv';

INCLUDE '~beta/guienv/v1.2/controls';
INCLUDE '~beta/guienv/v1.2/fields';

INCLUDE '~beta/unixlib/v1.4/iostate';

INCLUDE '~beta/process/v1.4/processmanager';
INCLUDE '~beta/process/v1.4/private/communication_unixbody';
INCLUDE '~beta/process/v1.4/private/processmanager_unixbody';
INCLUDE '~beta/process/v1.4/private/fdStreamBody';

--- showPreferencesDialog: descriptor ---
(# do cPrivate.preferenceDialog.showmodal #)

--- fragmentBrowserLib: attributes ---

compilerPrefDialog: ymerWindow
  (# switchWidth: @integer;
     height, distance,
     windowWidth,windowHeight: @integer;
     setFrame:
       (#
       do recalc;
          (if more then
              8*height+4*distance->windowHeight;
              (windowWidth,windowHeight)->size;
           else
              4*height+2*distance->windowHeight;
              (windowWidth,windowHeight)->size;
          if);
       #);
     recalc:
       (#
       do 3->distance;
          (switchWidth*3)+distance*4->windowWidth;
       #);
     
     changed:
       (# cmd: ^text
       do &text[]->cmd[];
          (if all then '--all'->cmd.puttext; ' '->cmd.put if);
          (if repeating then '--repeat'->cmd.puttext; ' '->cmd.put if);
          (if nolink then '--nolink'->cmd.puttext; ' '->cmd.put if);
          (if nocode then '--nocode'->cmd.puttext; ' '->cmd.put if);
          (if noexec then '--noexec'->cmd.puttext; ' '->cmd.put if);
          (if nolist then '--nolist'->cmd.puttext; ' '->cmd.put if);
          (if noquawarn then '--noquawarn'->cmd.puttext; ' '->cmd.put if);
          (if nowarnings then '--nowarnings'->cmd.puttext; ' '->cmd.put if);
          (if verbose then '--verbose'->cmd.puttext; ' '->cmd.put if);
          (if quiet then '--quiet'->cmd.puttext; ' '->cmd.put if);
          (if mute then '--mute'->cmd.puttext; ' '->cmd.put if);
          (if static then '--static'->cmd.puttext; ' '->cmd.put if);
          (if debug then '--debug'->cmd.puttext; ' '->cmd.put if);
          (if preserve then '--preserve'->cmd.puttext; ' '->cmd.put if);
          (if not (switchEdit.contents.contents).empty then
              '--switch '->cmd.puttext;
              switchEdit.contents.contents->cmd.puttext;
              ' '->cmd.put; '0 '->cmd.puttext if);
          cmd[]->compilerSwitches[]
       #);
     
     more, all, repeating,
     nolink, debug, nocode, noexec, nolist, noquawarn, nowarnings,
     verbose, quiet, mute,
     static, preserve: @boolean;
     (* -------------------------------------------------- *)
     bCancel: @pushButton
       (# setFrame:
            (#
            do recalc;
               (distance,distance)->position;
               ((windowWidth-3*distance)/2,height)->size
            #);
          open:: (# do setFrame; 'Cancel'->label #);
          eventhandler::
            (# onMouseUp:: (# do this(compilerPrefDialog).hide #);
               onFatherFrameChanged::
                 (# do setFrame #)
            #)
       #);
     bOk: @pushButton
       (# setFrame:
            (#
            do recalc;
               (distance+(windowWidth-3*distance)/2,distance)->position;
               ((windowWidth-3*distance)/2,height)->size
            #);
          open:: (# do setFrame; 'OK'->label #);
          eventhandler::
            (# onMouseUp:: (# do changed; this(compilerPrefDialog).hide #);
               onFatherFrameChanged::
                 (# do setFrame #)
            #)
       #);
     switchesFrame: @envGroup
       (# contentsType:: canvas
            (# nolistBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         (0, distance)->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'nolist'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not nolist->nolist #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               nolinkBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         (0, (height+distance))->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'nolink'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not nolink->nolink #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               nocodeBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         (0, (height+distance)*2)->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'nocode'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not nocode->nocode #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               (* -------------------------------------------------- *)
               verboseBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         (switchWidth+distance, distance)->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'verbose'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not verbose->verbose #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               quietBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         (switchWidth+distance, (height+distance))->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'quiet'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not quiet->quiet #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               muteBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         (switchWidth+distance, (height+distance)*2)->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'mute'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not mute->mute #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               (* -------------------------------------------------- *)
               moreBotton: @pushButton
                 (# setFrame:
                      (#
                      do recalc;
                         ((switchWidth+distance)*2,distance)->position;
                         (switchWidth-distance*3,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'more choices'->label #);
                    eventhandler:: 
                      (# onMouseUp::
                           (#
                           do not more->more;
                              (if more then
                                  'less choices'->label;
                                  this(compilerPrefDialog).setFrame;
                                  switchesFrame.setFrame;
                                  debugBox.show;
                                  staticBox.show;
                                  noexecBox.show;
                                  preserveBox.show;
                                  allBox.show;
                                  repeatingBox.show;
                                  switchEdit.show;
                               else
                                  'more choices'->label;
                                  debugBox.hide;
                                  staticBox.hide;
                                  noexecBox.hide;
                                  preserveBox.hide;
                                  allBox.hide;
                                  repeatingBox.hide;
                                  switchEdit.hide;
                                  switchesFrame.setFrame;
                                  this(compilerPrefDialog).setFrame;
                              if);
                              true->this(compilerPrefDialog).update;
                            #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #); 
               noquawarnBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         ((switchWidth+distance)*2, (height+distance))->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'noquawarn'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not noquawarn->noquawarn #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               nowarningsBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         ((switchWidth+distance)*2, (height+distance)*2)->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; 'nowarnings'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not nowarnings->nowarnings #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               (* -------------------------------------------------- *)
               debugBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         (0, (height+distance)*3)->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; hide; 'debug'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not debug->debug #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               staticBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         (0, (height+distance)*4)->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; hide; 'static'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not static->static #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               (* -------------------------------------------------- *)
               noexecBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         ((switchWidth+distance), (height+distance)*3)
                           ->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; hide; 'noexec'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not noexec->noexec #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               preserveBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         ((switchWidth+distance), (height+distance)*4)
                           ->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; hide; 'preserve'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not preserve->preserve #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               (* -------------------------------------------------- *)
               allBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         ((switchWidth+distance)*2, (height+distance)*3)
                           ->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; hide; 'all'->label #);
                    eventhandler:: 
                      (# onMouseUp:: (# do not all->all #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               repeatingBox: @checkBox 
                 (# setFrame:
                      (#
                      do recalc;
                         ((switchWidth+distance)*2, (height+distance)*4)
                           ->position;
                         (switchWidth,height)->size
                      #);
                    open:: 
                      (# do setFrame; hide; 'repeating'->label #);
                    eventhandler:: 
                      (# onMouseUp::
                           (#
                           do not repeating->repeating;
                              not cPrivate.repeating->cPrivate.repeating;
                              changed
                           #);
                         onFatherFrameChanged::
                           (# do setFrame #)
                      #)
                 #);
               open::
                 (#
                 do nolistBox.open;
                    nolinkBox.open;
                    nocodeBox.open;
                    
                    verboseBox.open;
                    quietBox.open;
                    muteBox.open;
                    
                    noquawarnBox.open;
                    nowarningsBox.open;
                    moreBotton.open;
                    
                    debugBox.open;
                    staticBox.open;
                    
                    noexecBox.open;
                    preserveBox.open;
                    
                    allBox.open;
                    repeatingBox.open;
                 #)
            #);
          setFrame:
            (#
            do recalc;
               (if more then
                   (windowWidth-2*distance, height*5+distance)->size;
                else
                   (windowWidth-2*distance, height*5+distance)->size;
               if);
               (distance, distance+height)->position
            #);
          open::
            (# do setFrame; 'Switches'->label #);
          eventhandler::
            (# onFatherFrameChanged::
                 (# do setFrame #)
            #)
       #);
     switchEdit: @envGroup 
       (# contentsType:: editText;
          setFrame:
            (#
            do recalc;
               (distance, height*6+distance*3)->position;
               (windowWidth-2*distance,height*2)->size
            #);
          open:: 
            (# do setFrame; hide; 'Numeric switches'->label #);
          eventhandler::
            (# onFatherFrameChanged::
                 (# do setFrame #)
            #)
       #);
     (* -------------------------------------------------- *)
     tst: @pushButton
       (# open::
            (# ts: ^textStyle;
               buttonLabel: (# exit 'more choises' #);
               buttonWidth: @integer
            do buttonLabel->label;
               style->ts[];
               ((buttonLabel->ts.widthOfText)+5,ts.lineHeight+3)
                 ->(switchWidth,height);
               switchWidth+8->switchWidth;
               height+8->height;
            #)
       #);
     eventhandler::
       (# onAboutToClose::
            (# do false->okToClose; hide #)
       #);
     open::< 
       (# 
       do tst.open; (* just to get the width and height of fonts *)
          tst.hide; tst.close;
          
          setFrame; hide;
          
          this(compilerprefdialog).contents->bCancel.open;
          this(compilerprefdialog).contents->bOk.open;
                    
          this(compilerprefdialog).contents->switchesFrame.open;
          this(compilerprefdialog).contents->switchEdit.open;
          'Compiler Preferences'->setName;
          INNER
       #)
  #);

executeWindow: ymerWindow
  (# textMsg:
       (# value: @text
       do INNER
       exit (# msg: ^text
            do '--- '->msg[]; value[]->msg.puttext; ' ---\n'->msg.append
            exit msg[]
            #)
       #);
     executeStartMsg:< textMsg
       (# do 'Execution start'->value; INNER #);
     executeRestartMsg:< textMsg
       (# do 'Execution restart'->value; INNER #);
     executeAbortMsg:< textMsg
       (# do 'Execution ABORT'->value; INNER #);
     executeEndMsg:< textMsg
       (# do 'Execution terminated'->value; INNER #);
     repeatLabel:<
       (# value: ^text
       do 'Reexecute'->value[]; INNER
       exit value[]
       #);
     onAbort:<
       (# do INNER #);
     program: ^process;
     programTerminated: @boolean;
     programOutputPipe: @pipe;
     programInputPipe: @pipe;
     programOutputBuffer: @fd_buffer;
     programOutputProcessor: ^|outputProcessor;
     outputProcessor: theSystemEnv.system
       (# l: @integer;
       do cycle:
            (# t: @text;ch: @char;
            do (if not programTerminated(*program.stillRunning*)then
                   programOutputBuffer.getBufferContents
                   (# error::
                        (#
                        do true->programTerminated;
                           leave cycle
                        #);
                      endOfFile::
                        (#
                        do true->programTerminated;
                           leave cycle
                        #)
                   #)
                    ->terminal.contents.insert;
                   terminal.contents.length->l;
                   (l,l)->terminal.contents.selection.set;
                   terminal.contents.selection.scrollIntoView;
                   restart cycle
               if)
            #);
          program.private.pid -> awaitNotExecuting;
          executeEndMsg->terminal.contents.insert;
          terminal.contents.length->l;
          (l,l)->terminal.contents.selection.set;
          terminal.contents.selection.scrollIntoView;
       #);
     repeatButton: @pushButton
       (# setFrame:
            (#
            do recalc;
               (distance, distance)->position;
               ((windowWidth-distance*3)/2, height+distance)->size
            #);
          open:: 
            (# do setFrame; repeatLabel->label #);
          eventhandler:: 
            (# onMouseUp:: (# do doRepeat #);
               onFatherFrameChanged:: (# do setFrame #)
            #)
       #);
     abortButton: @pushButton
       (# setFrame:
            (#
            do recalc;
               ((windowWidth-distance*3)/2+distance*2, distance)->position;
               ((windowWidth-distance*3)/2, height+distance)->size
            #);
          open:: 
            (# do setFrame; 'Abort'->label #);
          eventhandler:: 
            (# onMouseUp::
                 (# l: @integer
                 do (if not programTerminated(*program.stillRunning*) then
                        program.private.pid -> stopUnixProcess;
                        program.private.pid -> awaitNotExecuting;
                        (* should be: program.stop; *)
                    if);
                    (if programOutputProcessor.shstatus<>SE_KILLED then
                        programOutputProcessor[]->theSystemEnv.kill
                    if);
                    executeAbortMsg->terminal.contents.insert;
                    terminal.contents.length->l;
                    (l,l)->terminal.contents.selection.set;
                    terminal.contents.selection.scrollIntoView;
                    onAbort
                 #);
               onFatherFrameChanged::
                 (# do setFrame #)
            #)
       #);
     terminal: @textEditor
       (# contentsType::
            (# eventhandler::
                 (# t: @text;
                    onKeydown::
                      (#
                      do (if not programTerminated(*program.stillRunning*) then
                             (if ch
                              // ascii.bs then
                                 (if t.length>0 then
                                     (t.length,t.length)->t.delete
                                 if);
                              // ascii.cr then
                                 t[]->programInputPipe.writeend.puttext;
                                 programInputPipe.writeend.newline;
                                 programInputPipe.private.writech.flush;
                                 t.clear;
                              else
                                 ch->t.put;
                             if)
                         if)
                      #)
                 #)
            #);
          setFrame:
            (#
            do recalc;
               (distance, height+distance*2)->position;
               (windowWidth-distance*2,windowHeight-height-distance*3)-> size
            #);
          open:: 
            (#
            do setFrame;
               true -> bindBottom -> bindRight;
            #);
          eventhandler::
            (# onFatherFrameChanged::
                 (# do setFrame #)
            #)
       #);
     doExecute:<
       (# l: @integer;
       do executeStartMsg->terminal.contents.insert;
          terminal.contents.length->l;
          (l,l)->terminal.contents.selection.set;
          terminal.contents.selection.scrollIntoView;
          
          setupNewProcess;
         
          INNER;
       #);
     doRepeat:<
       (# l: @integer;
       do executeRestartMsg->terminal.contents.insert;
          terminal.contents.length->l;
          (l,l)->terminal.contents.selection.set;
          terminal.contents.selection.scrollIntoView;
          INNER;
       #);
     eventhandler::
       (# onAboutToClose:: 
            (#
            do (if not programTerminated(*program.stillRunning*) then
                   program.private.pid -> stopUnixProcess;
                   program.private.pid -> awaitNotExecuting;
                   (* should be: program.stop; *)
               if);
               (if programOutputProcessor.shstatus<>SE_KILLED then
                   programOutputProcessor[]->theSystemEnv.kill
               if);
            #)
       #);
     height, distance: @integer;
     windowWidth, windowHeight: @integer;
     
     BETAprogram: ^text;
     arguments: ^text;
     
     recalc:
       (# do size->(windowWidth,windowHeight) #);
     tst: @pushButton
       (# open::
            (# ts: ^textStyle;
               buttonLabel: (# exit 'more choises' #);
            do buttonLabel->label;
               style->ts[];
               ts.lineHeight+3->height;
               height+8->height;
            #)
       #);
     setupNewProcess:
       (# fds: ^fdStream;
          setbuf: external
            (# stdout, state: @integer 
            enter (stdout, state)
            #);
       do &process[]->program[];
          BETAprogram[]->program.init;
          
          programInputPipe.init;
          programInputPipe.readEnd[]
            ->program.redirectFromChannel;
          programOutputPipe.init;
          programOutputPipe.writeEnd[]
            ->program.redirectToChannel;
          
          (if arguments[]<>NONE then
              arguments.reset; arguments.scanwhitespace;
              loop: (if not arguments.eos then
                        arguments.getatom->program.argument.append;
                        arguments.scanwhitespace; restart loop
                    if);
          if);
          false->programTerminated;
          program.start;
                    
          programInputPipe.writeEnd[]->fds[];
          (fds.private.unixStream, 0)->setbuf;
          
          programOutputPipe.readEnd[]->fds[];
          (fds.private.unixStream, 0)->setbuf;
          (fds.private.index,FD_READ)->programOutputBuffer.init;
          
          &|outputProcessor[]->programOutputProcessor[];
          programOutputProcessor[]->theSystemEnv.fork;
       #);
     open::<
       (# t: @text
       do tst.open; (* just to get the width and height of fonts *)
          tst.hide; tst.close;
          3->distance;
          (600,400) -> size;
          repeatButton.open;
          abortButton.open;
          terminal.open;
          
          INNER;
            
          BETAprogram->t; ' '->t.put;
          (if arguments[]<>NONE then arguments[]->t.puttext; ' '->t.put if);
          t.copy->setName;
        
       #)
  #);

compileWindow: executeWindow
  (# executeStartMsg::
       (# do 'Compilation start'->value #);
     executeRestartMsg::
       (# do 'Recompilation start'->value #);
     executeAbortMsg::
       (# do 'Compilation ABORT'->value #);
     executeEndMsg::
       (# do 'Compilation terminated'->value #);
     repeatLabel::
       (# do 'Recompile'->value #);
     doExecute::
       (#
       do switches[]->programInputPipe.writeend.puttext;
          ' '->programInputPipe.writeend.puttext;
          files[]->programInputPipe.writeend.puttext;
          programInputPipe.writeend.newline;
          programInputPipe.private.writech.flush;
       #);
     doRepeat::<
       (#
       do INNER;
          switches[]->programInputPipe.writeend.puttext;
          ' '->programInputPipe.writeend.puttext;
          files[]->programInputPipe.writeend.puttext;
          programInputPipe.writeend.newline;
          programInputPipe.private.writech.flush;
       #);
     files, switches: ^text
  #);

singleCompileWindow: compileWindow
  (# doRepeat::
       (# do setupNewProcess #);
  #);

repeatingCompileWindow: compileWindow
  (# open:: (# do '--repeat'->arguments.append #);
     onAbort::<
       (# do NONE->cPrivate.recWindow[] #);
  #);

--- compilePrivate: descriptor ---
(# recWindow: ^repeatingCompileWindow;
   preferenceDialog: @compilerPrefDialog;
   repeating: @boolean;
do preferenceDialog.open
#)

--- iCompile: descriptor ---
(# win: ^compileWindow;
   t: @text
do (if BETAexecutable[]=NONE then
       BETAlib.copy->BETAexecutable[];
       '/bin/beta+'->BETAexecutable.puttext;
   if);
   l: (if (BETAexecutable[]->fileExists) and
          (BETAexecutable[]->fileReadable) and
          (BETAexecutable[]->fileExecutable) then
          (if cPrivate.repeating then
              (if cPrivate.recWindow[]=NONE then
                  (* should have been: not cPrivate.recWindow.compiler.stillRunning *)
                  &repeatingCompileWindow[]->cPrivate.recWindow[]->win[];
                  &text[]->win.arguments[];
                  BETAexecutable.copy->win.BETAprogram[];
                  win.open;
               else
                  cPrivate.recWindow[]->win[]
              if)
           else
              &singleCompileWindow[]->win[];
              &text[]->win.arguments[];
              BETAexecutable.copy->win.BETAprogram[];
              win.open;
          if);
          compilerSwitches.copy->win.switches[];
          (if true
           // browser.currentGroup[]<>NONE then
              browser.currentGroup.location.copy->win.files[];
           // browser.currentProject[]<>NONE then
              (if true
               // browser.currentProject.isRoot
               // browser.currentProject.isExtent
               // browser.currentProject.isDomain then
                  browser.currentProject.location.copy->win.files[];
               // browser.currentProject.isDirectory then
                  (# d: @directory
                  do (if browser.currentProject.location.empty then
                         ph.currentDirectory->browser.currentProject.location.puttext;
                     if);
                     (browser.currentProject.location[],ph.currentDirectory)
                       ->ph.convertfilepath->d.name;
                     &text[]->win.files[];
                     d.scanEntries
                     (#
                     do select
                        (# whenFile::
                             (# f: ^file; ext: ^text; location: @text
                             do thefile->f[];
                                f.entry.path.name.suffix
                                  ->ext[];
                                f.entry.path.name
                                  ->location.puttext;
                                (if true
                                 // '.bet'->ext.equal then
                                    browser.currentProject.location[]->location.prepend;
                                    location[]->win.files.puttext; ' '->win.files.put;
                                if);
                             #)
                        #)
                     #)
                  #)
              if)
          if);
          win.doExecute;
       else
          'BETA compiler\n     "%s"\nnot available'
            ->t.putformat(# do BETAexecutable[]->s #);
          (NONE, t[] ,'Compile')->alertUser;
          &fileSelectionDialog(# do 'Select BETA compiler executable'->dialogTitle[] #)
            ->BETAexecutable[];
          (if BETAexecutable[]<>NONE then
              restart l
          if)
      if)
#)

--- iExecute: descriptor ---
(# win: @executeWindow
     (# doRepeat::
          (# do setupNewProcess #);
     #);
   t: @text
do &text[]->win.arguments[];
   (if true
    // browser.currentGroup[]<>NONE then
       browser.currentGroup.location.copy->win.BETAprogram[];
    // browser.currentProject[]<>NONE then
       (if true
        // browser.currentProject.isRoot
        // browser.currentProject.isExtent
        // browser.currentProject.isDomain then
           browser.currentProject.location.copy->win.BETAprogram[];
       if)
   if);
   (if (win.BETAprogram[]->fileExists) and
       (win.BETAprogram[]->fileReadable) and
       (win.BETAprogram[]->fileExecutable) then
       win.open;
       win.doExecute
    else
       'Selected executable\n     "%s"\nnot available'
         ->t.putformat(# do win.BETAprogram[]->s #);
       (NONE, t[],'Execute')->alertUser
   if)
#)

--- iDebug: descriptor ---
(# win: @executeWindow
     (# doRepeat::
          (# do setupNewProcess #);
     #);
   t: @text
do &text[]->win.arguments[];
   (if VALHALLAexecutable[]=NONE then
       BETAlib.copy->VALHALLAexecutable[];
       '/bin/valhalla2.0'->VALHALLAexecutable.puttext;
   if);
   VALHALLAexecutable.copy->win.BETAprogram[];
   l: (if (win.BETAprogram[]->fileExists) and
          (win.BETAprogram[]->fileReadable) and
          (win.BETAprogram[]->fileExecutable) then
          (if true
           // browser.currentGroup[]<>NONE then
              browser.currentGroup.location.copy->win.arguments.puttext;
           // browser.currentProject[]<>NONE then
              (if true
               // browser.currentProject.isRoot
               // browser.currentProject.isExtent
               // browser.currentProject.isDomain then
                  browser.currentProject.location.copy->win.arguments.puttext;
              if);
          if);
          win.arguments[]->putline;
          (if (win.arguments[]->fileExists) and
              (win.arguments[]->fileReadable) and
              (win.arguments[]->fileExecutable) then
              win.open;
              win.doExecute
           else
              'Selected executable\n     "%s"\nnot available'
                ->t.putformat(# do win.arguments[]->s #);
              (NONE, t[],'Debug')->alertUser
          if)
       else
          'Debugger\n     "%s"\nnot available'
            ->t.putformat(# do win.BETAprogram[]->s #);
          (NONE, t[] ,'Debug')->alertUser;
          &fileSelectionDialog(# do 'Select Valhalla executable'->dialogTitle[] #)
            ->VALHALLAexecutable[];
          (if VALHALLAexecutable[]<>NONE then
              VALHALLAexecutable.copy->win.BETAprogram[];
              restart l
          if)
      if)
#)

--- iSif: descriptor ---
(# win: @executeWindow
     (# doRepeat::
          (# do setupNewProcess #);
        open:: (# do hide #)
     #);
   t: @text;
do &text[]->win.arguments[];
   (if SIFexecutable[]=NONE then
       BETAlib.copy->SIFexecutable[];
       '/bin/sif'->SIFexecutable.puttext;
   if);
   SIFexecutable.copy->win.BETAprogram[];
   l: (if (win.BETAprogram[]->fileExists) and
          (win.BETAprogram[]->fileReadable) and
          (win.BETAprogram[]->fileExecutable) then
          (if true
           // browser.currentGroup[]<>NONE then
              browser.currentGroup.location.copy->win.arguments.puttext;
           // browser.currentProject[]<>NONE then
              (if true
               // browser.currentProject.isRoot
               // browser.currentProject.isExtent
               // browser.currentProject.isDomain then
                  browser.currentProject.location.copy->win.arguments.puttext;
              if)
          if);
          win.open;
          win.doExecute
       else
          'Sif editor\n     "%s"\nnot available'
            ->t.putformat(# do win.BETAprogram[]->s #);
          (NONE, t[] ,'Edit: Sif')->alertUser;
          &fileSelectionDialog(# do 'Select Sif editor executable'->dialogTitle[] #)
            ->SIFexecutable[];
          (if SIFexecutable[]<>NONE then
              SIFexecutable.copy->win.BETAprogram[];
              restart l
          if)
      if);
#)

--- iFreja: descriptor ---
(# win: @executeWindow
     (# doRepeat::
          (# do setupNewProcess #);
        open:: (# do hide #)
     #);
   t: @text;
do &text[]->win.arguments[];
   (if FREJAexecutable[]=NONE then
       BETAlib.copy->FREJAexecutable[];
       '/bin/freja'->FREJAexecutable.puttext;
   if);
   FREJAexecutable.copy->win.BETAprogram[];
   l: (if (win.BETAprogram[]->fileExists) and
          (win.BETAprogram[]->fileReadable) and
          (win.BETAprogram[]->fileExecutable) then
          (if true
           // browser.currentGroup[]<>NONE then
              browser.currentGroup.location.copy->win.arguments.puttext;
           // browser.currentProject[]<>NONE then
              (if true
               // browser.currentProject.isRoot
               // browser.currentProject.isExtent
               // browser.currentProject.isDomain then
                  browser.currentProject.location.copy->win.arguments.puttext;
              if)
          if);
          win.open;
          win.doExecute
       else
          'Freja editor\n     "%s"\nnot available'
            ->t.putformat(# do win.BETAprogram[]->s #);
          (NONE, t[] ,'Edit: Freja')->alertUser;
          &fileSelectionDialog(# do 'Select Freja editor executable'->dialogTitle[] #)
            ->FREJAexecutable[];
          (if FREJAexecutable[]<>NONE then
              FREJAexecutable.copy->win.BETAprogram[];
              restart l
          if)
      if);
#)

--- iInterfacebuilder: descriptor ---
(# win: @executeWindow
     (# doRepeat::
          (# do setupNewProcess #);
        open:: (# do hide #)
     #);
   t: @text;
do &text[]->win.arguments[];
   (if INTERFACEBUILDERexecutable[]=NONE then
       BETAlib.copy->INTERFACEBUILDERexecutable[];
       '/bin/interfacebuilder'->INTERFACEBUILDERexecutable.puttext;
   if);
   INTERFACEBUILDERexecutable.copy->win.BETAprogram[];
   l: (if (win.BETAprogram[]->fileExists) and
          (win.BETAprogram[]->fileReadable) and
          (win.BETAprogram[]->fileExecutable) then
          (if true
           // browser.currentGroup[]<>NONE then
              browser.currentGroup.location.copy->win.arguments.puttext;
           // browser.currentProject[]<>NONE then
              (if true
               // browser.currentProject.isRoot
               // browser.currentProject.isExtent
               // browser.currentProject.isDomain then
                  browser.currentProject.location.copy->win.arguments.puttext;
              if)
          if);
          win.open;
          win.doExecute
       else
          'Interfacebuilder editor\n     "%s"\nnot available'
            ->t.putformat(# do win.BETAprogram[]->s #);
          (NONE, t[] ,'Edit: Interfacebuilder')->alertUser;
          &fileSelectionDialog(# do 'Select Interfacebuilder editor executable'->dialogTitle[] #)
            ->INTERFACEBUILDERexecutable[];
          (if INTERFACEBUILDERexecutable[]<>NONE then
              INTERFACEBUILDERexecutable.copy->win.BETAprogram[];
              restart l
          if)
      if);
#)

--- iTextEdit: descriptor ---
(# win: @executeWindow
     (# doRepeat::
          (# do setupNewProcess #);
        open:: (# do hide #)
     #);
   t: @text;
do &text[]->win.arguments[];
   (if TEXTexecutable[]=NONE then
       '$(EDITOR)'
         ->expandEnvVar(# defaultValue::
                            (# do '/usr/local/bin/emacs'->envvarvalue[] #)#)
         ->TEXTexecutable[];
   if);
   TEXTexecutable.copy->win.BETAprogram[];
   l: (if (win.BETAprogram[]->fileExists) and
          (win.BETAprogram[]->fileReadable) and
          (win.BETAprogram[]->fileExecutable) then
          (if true
           // browser.currentGroup[]<>NONE then
              browser.currentGroup.location.copy->win.arguments.puttext;
           // browser.currentProject[]<>NONE then
              (if true
               // browser.currentProject.isRoot
               // browser.currentProject.isExtent
               // browser.currentProject.isDomain then
                  browser.currentProject.location.copy->win.arguments.puttext;
              if)
          if);
          '.bet'->win.arguments.puttext;
          win.open;
          win.doExecute;
       else
          'Text Editor\n     "%s"\nnot available'
            ->t.putformat(# do win.BETAprogram[]->s #);
          (NONE, t[] ,'Edit: Text Editor')->alertUser;
          &fileSelectionDialog(# do 'Select TEXT EDITOR executable'->dialogTitle[] #)
            ->TEXTexecutable[];
          (if TEXTexecutable[]<>NONE then
              TEXTexecutable.copy->win.BETAprogram[];
              restart l
          if)
      if)
#)

--- iAnalyze: descriptor ---
(# win: @executeWindow
     (# executeStartMsg::
          (# do 'Analysis start'->value #);
        executeRestartMsg::
          (# do 'Analysis restart'->value #);
        executeAbortMsg::
          (# do 'Analysis ABORT'->value #);
        executeEndMsg::
          (# do 'Analysis terminated'->value #);
        repeatLabel::
          (# do 'Analyze again'->value #);
        doRepeat::
          (# do setupNewProcess #)
     #);
   t: @text;
do &text[]->win.arguments[];
   (if BETAWCexecutable[]=NONE then
       BETAlib.copy->BETAWCexecutable[];
       '/bin/betawc'->BETAWCexecutable.puttext;
   if);
   BETAWCexecutable.copy->win.BETAprogram[];
   l: (if (win.BETAprogram[]->fileExists) and
          (win.BETAprogram[]->fileReadable) and
          (win.BETAprogram[]->fileExecutable) then
          (if true
           // browser.currentGroup[]<>NONE then
              browser.currentGroup.location.copy->win.arguments.puttext;
           // browser.currentProject[]<>NONE then
              (if true
               // browser.currentProject.isRoot
               // browser.currentProject.isExtent
               // browser.currentProject.isDomain then
                  browser.currentProject.location.copy->win.arguments.puttext;
              if)
          if);
          (win.arguments[],ph.currentDirectory)
            ->ph.convertfilepath->win.arguments[];
          win.open;
          win.doExecute;
       else
          'Analysis tool betawc\n     "%s"\nnot available'
            ->t.putformat(# do win.BETAprogram[]->s #);
          (NONE, t[] ,'Analyze')->alertUser;
          &fileSelectionDialog(# do 'Select Analysis executable'->dialogTitle[] #)
            ->BETAWCexecutable[];
          (if BETAWCexecutable[]<>NONE then
              BETAWCexecutable.copy->win.BETAprogram[];
              restart l
          if)
      if)
#)


--- windowsMenuInsertWindow: descriptor ---
(# wmi: ^windowMenuItem
do &windowMenuItem[]->wmi[];
   wmi.open;
   wmi[]->append;
   win[]->wmi.ww[];
   win.name.copy->wmi.name;
#)   
--- windowsMenuDeleteWindow: dopart ---
do scan
   (# wmi: ^windowMenuItem
   do current[]->wmi[];
      (if wmi.ww[]=win[] then current[]->delete if)
   #)

--- windowsMenuPrivate: attributes ---
windowMenuItem: menuItem
     (# ww: ^ymerWindow;
        eventhandler::
          (# onStatus:: 
               (# do ww.name.copy->name #);
             onSelect:: 
               (# do ww.show; ww.wriggle #)
          #);
     #);
