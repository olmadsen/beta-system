ORIGIN '../xprompts';
INCLUDE '~beta/guienv/v1.3.1/fields';
INCLUDE 'buttonadds';

(******** Prompt ********)

--xPromptPrivate: descriptor--
(# fatherpos, fathersize, mysize: @point;
   dialogopened: @boolean;
   popdown: @boolean;
   hSep: (# exit 14 #);
   vSep: (# exit 14 #);
   recalc:
     (# x,y,h,v,t: @integer; fr: @rectangle; p: @point;
     do cancelButton.size->okButton.size;
        (if this(prompt)##<=promptForBoolean## then
            cancelButton.size->notOkButton.size;
            notOkButton.size->p; p.h+hSep+h->h;
        if);
        okButton.size->p; p.h+hSep+h->h;
        cancelButton.size->p; p.h+hSep+h->h;
        theMessage.size->p; (hSep+p.h+hSep,hSep+h)->max->h;
        
        theMessage.size->p; p.v+vSep+v->v;
        (if this(prompt)##<=promptForText## then
            theMessage.size->p;
            (p.h,(p.v div linesInMessage)*linesInText+14)->theText.size;
            theText.size->p; p.v+vSep+v->v;
        if);
        okButton.size->p; p.v+vSep+v->v;
        v+vSep->v;
            
        (h,v)->THIS(prompt).size;
        
        hSep->x; vSep->y;
        (x,y)->theMessage.position;
        theMessage.size->p; p.v+vSep+y->y;
        (if this(prompt)##<=promptForText## then
            (x,y)->theText.position;
            theText.size->p; p.v+vSep+y->y;
        if);
        
        cancelButton.size->p; p.h->t;
        h-t-hSep->x;
        (x,y)->cancelButton.position;
        
        okButton.size->p; p.h->t;
        x-t-hSep->x;
        (x,y)->okButton.position;
        
        (if this(prompt)##<=promptForBoolean## then
            notOkButton.size->p; p.h->t;
            x-t-hSep->x;
            (x,y)->notOkButton.position;
        if);
     #);
   linesInMessage,linesInText: @integer;
   theMessage: @staticText
     (# eventhandler::<
          (# onLabelChanged::<
               (# w,h: @integer;
               do (labelWidth,labelHeight*linesinMessage)->size;
               #)
          #);
        open::
          (# do (hSep,vSep)->position #);
     #);
   promptButton: pushButton
     (# eventhandler::<
          (# onLabelChanged::<
               (# w,h: @integer
               do (labelWidth+14,labelHeight+14)->size;
               #)
          #)
     #);
   notOkButton: @promptButton
     (# eventHandler:: 
          (# onMouseUp:: 
               (# 
               do false->THIS(prompt).private.popdown;
                  notok;
                  (if THIS(prompt).private.popdown then THIS(Prompt).hide if)
               #)
          #);
        open::
          (#
          do 'NOT OK'->label;
             (110,80)->position;
          #);
     #);
   okButton: @promptButton
     (# eventHandler:: 
          (# onMouseUp:: 
               (# 
               do false->THIS(prompt).private.popdown;
                  ok;
                  (if THIS(prompt).private.popdown then THIS(Prompt).hide if)
               #)
          #);
        open::
          (#
          do 'OK'->label;
             (170,80)->position;
          #);
     #);
   cancelButton: @promptButton
     (* only valid for promptForBoolean *)
     (# eventHandler:: 
          (# onMouseUp:: (# do cancel; THIS(Prompt).hide #)#);
        open::
          (#
          do 'Cancel'->label;
             (230,80)->position;
          #);
     #);
   DialogText: textField
     (* only valid for promptForText *)
     (# eventhandler::
          (# onBeforeChange:: 
               (# txt: ^text
               do (if length=1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl
                       // ascii.cr then 
                          THIS(Prompt).private.okButton.theEventhandler.onMouseup;
                          false->allow;
                      if);
                  if)
               #);
             onKeyDown::
               (# 
               do (if ch=ascii.esc then 
                      THIS(Prompt).private.cancelButton.theEventhandler.onMouseup;
                  if)
               #);
          #);
     #);
   theTextSize: @point;
   theText: @DialogText
     (* only valid for promptForText *)
     (# open::
          (# do (hSep,40)->position #);
     #);
#)

--xPromptPopup: dopart--
do (if not private.dialogopened then
       open;
   if);
   titleText[]->title;
   1->private.linesInMessage;
   msgText.scanAll
   (#
   do (if ch=ascii.newline then
          private.linesInMessage+1->private.linesInMessage;
      if)
   #);
   msgText[]->private.theMessage.label;
   INNER;
   (if father[] <> NONE then
       father.size->private.fathersize;
       size->private.mysize;
       father.position->private.fatherpos;
       (private.fatherpos.v + (private.fathersize.v-private.mysize.v) div 2,
       private.fatherpos.h + (private.fathersize.h-private.mysize.h) div 2) 
         ->position;
   if);
   private.recalc;
   showmodal;
   
--xPromptOpen: dopart--
do (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       private.theMessage.open;
       private.okButton.open;
       private.cancelButton.open;
       (*private.okButton[]->defaultButton;*)
       INNER;
   if);
   
(********* PromptForText **********)
   
--xPromptForTextOpen: dopart--
do private.theText.open;
   linesInUsertext->private.linesInText;
   INNER;
   
--xPromptForTextOk: dopart--
do private.theText.contents->usertext[];
   (if textprivate.validatevalue then 
       INNER; 
       true->THIS(prompt).private.popdown
    else
       system.beep;
       (if textprivate.hilite then private.theText.all 
             ->private.theText.selection
       if);
   if)
   
--xPromptForTextValidate: dopart--
do true->hiliteOnError;
   usertext.length>0->value; 
   INNER;
   hiliteOnError->textprivate.hilite;
   
--xPromptForTextPopup: dopart--
do private.theText.all->private.theText.selection;
   private.theText.delete;
   defaultText[]->private.theText.insert;
   (* private.theText.all->private.theText.selection;*)
   INNER;
   
--xPromptForTextPrivate: descriptor--
(# hilite: @boolean;
   validatevalue: @validate;
#)


(********* PromptForInteger **********)

--xPromptForIntegerValidate: dopart--
do usertext.reset;
   usertext.getInt(# syntaxerror::
                       (# do false->THIS(validate).value; true->continue #)
                  #)->userinteger;
   INNER;
   
(******* PromptForBoolean ************)
   
--xPromptForBooleanOk: dopart--
do true->THIS(prompt).private.popdown;
   INNER
   
--xPromptForBooleanNotOk: dopart--
do true->THIS(prompt).private.popdown;
   INNER
   
--xPromptForBooleanOpen: dopart--
do private.notokButton.open;
   'No'->private.notokButton.label;
   'Yes'->private.okButton.label;
   INNER;
