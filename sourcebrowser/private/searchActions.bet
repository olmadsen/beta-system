ORIGIN '~beta/guienv/v1.3.1/scrolllists'

--- textScrollListLib: attributes ---

searchAction: action
  (# getContext:< (# context: ^object do INNER exit context[] #);
     select:< (# inx: @integer enter inx do INNER #);
     info:< (# msg: ^text enter msg[] do INNER #);
     
     eventtype:: theeventhandler.keydown;
     ch: @char;
     inx,i: @integer;
     searching, skip, purePrefix: @boolean;
     prefix,msg: @text; name: ^text;
     context, lastContext: ^object;
  do theevent.ch->ch;
     (if ch
      // ascii.ht (* <ctrl>i / ^i *) then
         getContext->context[];
         (if context[] <> lastContext[] then
             context[]->lastContext[]; 1->inx
          else
             (if inx=-1 then (* wrapped search *)
                 0->inx;
              else
                 selection.first+1->inx;
             if)
         if);
         true->searching; false->skip;
      // ascii.nl // ascii.cr then
         false->searching; false->skip; false->purePrefix;
         prefix.clear; 0->inx;
         ''->info; 
      // ascii.bs then
         (if prefix.length>0 then
             (prefix.length,prefix.length)->prefix.delete
         if)
      else
         (if searching then
             (if true
              // ch->ascii.isWhiteSpace then
                 true->skip
              // ch='^' then
                 (if prefix.length=0 then
                     true->purePrefix;
                     false->skip;
                 if)
              else
                 false->skip;
                 ch->prefix.put
             if)
         if);
     if);
     (if searching and not skip then
         (if inx = 0 then
             'Wrapped search for "'->msg; 
          else
             'Search for "'->msg; 
         if);
         (if not purePrefix then '*'->msg.put else '^'->msg.put if);
         prefix[]->msg.puttext;
         '*"'->msg.puttext;
         msg[]->info;
         0->i;
         l:
           (# 
           do scan
              (# n: ^text;
              do (if (i+1->i) >= inx then
                     current->gettext->name[];
                     (if purePrefix then
                         (1,prefix.length)->name.sub->n[];
                         (if prefix[]->n.equalNCS then
                             current->inx->select;
                             leave l;
                         if)
                      else
                         (for j: name.length repeat
                              (j,j+prefix.length-1)->name.sub->n[];
                              (if prefix[]->n.equalNCS then
                                  current->inx->select;
                                  leave l;
                              if)
                         for)
                     if)
                 if);
              #);
              (* wrap search from the beginning *)
              -1->inx;
              'Failing search for "'->msg;
              (if not purePrefix then '*'->msg.put else '^'->msg.put if);
              prefix[]->msg.puttext;
              '*"'->msg.puttext;
              msg[]->info;
           #)
     if);
  #)
