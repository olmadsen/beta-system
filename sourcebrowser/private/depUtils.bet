ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/mps/v5.2/astlevel';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- lib: Attributes --
scanLocalDependency:
  (# doProperty:<
       (# prop,s,m: ^text enter (prop[],s[],m[]) do INNER #);
     doFragmentLink:<
       (# fullname: ^text enter fullname[] do INNER #);
     doFragmentForm:<
       (# fle: ^g.fragmentListElement enter fle[] do INNER #);
     doFragmentGroup:<
       (# fle: ^g.fragmentListElement enter fle[] do INNER #);
     g: ^astInterface.fragmentGroup;
     inx: @integer;
     mps: ^astInterface;
     latestName: @text;
     machineType: ^text;
     
  enter (mps[],g[],machineType[])
  do g.prop.scanProp
     (# doProp::< 
	  (# 
	  do scanParameters
	     (# doName::< 
		  (# 
		  do n->latestName;
		  #);
		doString::< 
		  (# 
		  do (if true
		      // ('ORIGIN'->prop.equalNCS)
		      // ('BODY'->prop.equalNCS) then
			 (prop[],s[],none)->doProperty;
			 
		      // ('MDBODY'->prop.equalNCS)
		      // ('OBJFILE'->prop.equalNCS)
		      // ('BETARUN'->prop.equalNCS)
		      // ('MAKE'->prop.equalNCS)
		      // ('LIBFILE'->prop.equalNCS) then
			 (if (machineType[]->latestName.equalNCS) or
			     ('default'->latestName.equalNCS) then
			     (prop[],s[],machineType[])->doProperty
			 if)
		     if)
		  #)
	     #)
	  #)
     #);
     g.scanIncludes
       (# do current.fullname->doFragmentLink #);
     g.fragmentList.scan
     (# t: ^text; help: ^text; 
     do (if current.type
	 // mps.formType then
	     current[]->doFragmentForm;
	 // mps.groupType then
	     current[]->doFragmentForm;
	if)
     #)
  #);
