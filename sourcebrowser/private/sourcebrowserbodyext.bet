ORIGIN 'sourcebrowserbody';
INCLUDE 'setupProjects';


-- selectFragmentGroup: DoPart --
do (if debugSourcebrw then 
       'Select fragmentgroup on:%s\n'
         ->putformat (#  do fgname[]->s #)
   if);
   
   (# findInSubDir:
        (# localName,dirName,rest:^text;
           theProject:^sbprivate.hierarchyView.hir.project;
        enter (localName[],theProject[])
        do
           ll:'/'->localname.findAll
           (# 
           do (1,inx-1)->localname.sub->dirName[];
              (inx+1,localname.length)->localname.sub->rest[];
              leave ll
           #);
           (if dirName[]<>none then
               theProject.items.find
               (#
                  predicate::
                    (# do (current.isFolder and (current.label[]->dirName.equalNcs))->value #)
               do (rest[],current[])->findInSubDir
               #)               
            else
               (localname[],theproject.location[])->mps.ast.thepathhandler.convertfilepath->dirName[];
               (dirName[],theProject[])->findInCurrentDirProject
           if)
        #);
      findInCurrentDirProject:
        (# theProject:^sbprivate.hierarchyView.hir.directoryProject;
           fld:^hierarchy.folder;
           selectedGroup:^sbprivate.hierarchyView.hir.group;
           fg:^astInterface.fragmentgroup
        enter (fgName[],theProject[])
        do fgName[]->mps.AST.expandToFullPath->mps.fragmentGroupTable.getFragmentGroup
             ->fg[];
           (if not theProject.isExpanded then theProject.onExpand if);
           theProject.items.find
           (#
              predicate:: 
                (# g: ^sbprivate.hierarchyView.hir.group; 
                do (if current##=sbprivate.hierarchyView.hir.group## then
                       current[]->g[];
                       (if g.fg[] = none then
                           g.findfg
                       if);
                       (g.fg[] = fg[])->value
                    else false->value
                   if)
                #);
              notFound::
                (#
                   newGroup: ^sbprivate.hierarchyView.hir.group;
                   ff: ^astInterface.fragmentForm;
                   catname,t: ^text
                do
                   &sbprivate.hierarchyView.hir.group[]->newGroup[];
                   fgName.copy->newGroup.location[];
                   (* ess: this caused troubles when opening files 
                    * (via opendialog or commandline) with parse errors  
                    *  mps.ast.astFileExtension->newGroup.location.append;
                    *)
                   fgName[]->fragmentDefaultName->newGroup.label[];
                   newGroup[]->theProject.additem;
                   fg[]->newGroup.fg[];

                   theProject[]->newGroup.init;
                   newGroup.theProjectInfoGroup[]
                     ->theProject.theProjectInfoProject.groups.append;
                   newGroup.select;
                   sbprivate.hierarchyView.theScroller[]
                     ->newGroup.scrollIntoView
                #)
           do current.select;
              sbprivate.hierarchyView.theScroller[]->current.scrollIntoView
           #)
        #);
      found:@boolean;
   do
      (if sbprivate.rootProjectID = sbprivate.RecentPrjId then
          (fgName[],true)->AddToUsedGroups;
          true->found
       else
          (fgName[],false)->AddToUsedGroups
      if);

      (if sbprivate.rootProjectID = sbprivate.DotPrjID then
          (# localpath:^text;
          do (fgname[],currentDirectory)->mps.ast.thepathhandler.localpath->localpath[];
             localpath.reset;
             (if localpath.get
              // '/' then false->found
              // '.' then false->found
              // '~' then false->found
              else 
                 ll: '/'->localpath.find
                 (# 
                 do (localpath.copy,sbprivate.theActiveRootProject[])->findinsubdir;
                    leave ll
                 #);
                 (fgname[],sbprivate.theActiveRootProject[])->findInCurrentDirProject;
                 true->found;
             if);
          #);
          (if not found then
              sbprivate.StdLibPrjID -> sbprivate.rootProjectID;
              sbprivate.stdlibprj[]->sbprivate.theActiveRootProject[]
          if)
      if);
      StdLibIf:
        (if not found and (sbprivate.rootProjectID = sbprivate.StdLibPrjID) then
            (if sbprivate.theActiveRootProject.items.empty then 
                (sbprivate.hierarchyView.hir[], sbprivate.theActiveRootProject[])->sbprivate.setupProjectFile 
            if);
            sbprivate.theActiveRootProject.items.scan
            (# theProject:^sbprivate.hierarchyView.hir.project;
               localpath:^text;
            do current[]->theProject[];
               (fgname[],theProject.location[]->mps.ast.expandtofullpath)->mps.ast.thepathhandler.localpath->localpath[];
               localpath.reset;
               (if localpath.get
                // '/' then false->found
                // '.' then false->found
                // '~' then false->found
                else 
                   ll: '/'->localpath.find
                   (# 
                   do (localpath.copy,theProject[])->findinsubdir;
                      leave ll
                   #);
                   (fgname[],theProject[])->findInCurrentDirProject;
                   true->found;
                   leave StdLibIf
               if)
            #)
        if);
      (if not found and (sbprivate.rootProjectID = sbprivate.HomedirPrjID) then
          (* (fgname[],'~')->mps.ast.thepathhandler.localpath->putline; *)
      if);
      (if not found then
          (fgName[],true)->AddToUsedGroups
      if)
   #)
   
-- selectFragmentForm: DoPart --
do (if debugSourcebrw then
       'Select fragmentform on:%s-%s\n'
         ->putformat (#  do fgname[]->s; formname[]->s #)
   if);
   
   fgName[]->selectFragmentGroup;
   
   (* 'selectFragmentForm - GroupAdded'->putline; *)
   
   (if not
       ((sbprivate.formView.selectedFormName[] <> none ) and
       (formName[]->sbprivate.formView.selectedFormName.equal)) then
       (sbprivate.formView.root).items.scan
         (# form: ^sbprivate.formView.form; 
         do (if current## = sbprivate.formView.form## then
                current[]->form[];
                (if form.ff.name->formName.equalNCS then
                    form.select;
                    formName.copy->sbprivate.formView.selectedFormName[];
                    sbprivate.formview.theScroller[]->form.scrollIntoView
                if)
            if)
         #)
    else
       (if sbprivate.formView.selectedFormName[] <> none then
           (if debugSourcebrw then
               sbprivate.formView.selectedFormName[]->putline
           if)
       if)
   if)  
