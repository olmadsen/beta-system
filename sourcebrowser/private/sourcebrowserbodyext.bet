ORIGIN 'sourcebrowserbody';
INCLUDE 'setupProjects';

-- selectFragmentGroup: DoPart --
do 'Select fragmentgroup on:%s\n'
     ->putformat (#  do fgname[]->s #); 
   (fgName[],true)->AddToUsedGroups;   
   
   (fgname[],'.')->mps.ast.thepathhandler.localpath->putline;     
   
-- selectFragmentForm: DoPart --
do
   'Select fragmentform on:%s-%s\n'
     ->putformat (#  do fgname[]->s; formname[]->s #);  
   
   (# findInSubDir:
        (# localName,dirName,rest:^text;
           theProject:^sbprivate.hierarchyView.hir.project;
        enter (localName[],theProject[])
        do 'findInSubDir'->puttext; localname[]->putline;
           ll:'/'->localname.findAll
           (# 
           do (1,inx-1)->localname.sub->dirName[];
              (inx+1,localname.length)->localname.sub->rest[];
              leave ll
           #);
           (if dirName[]<>none then
               theProject.items.find
               (#
                  predicate::
                    (# do (current.isFolder and (current.label[]->dirName.equalNcs))->value #)
               do (rest[],current[])->findInSubDir
               #)               
            else
               'dirnone'->putline;
               localname[]->putline;
               theproject.location[]->putline;
               (localname[],theproject.location[])->mps.ast.thepathhandler.convertfilepath->dirName[];
               dirName[]->putline;
               (dirName[],theProject[])->findInCurrentDirProject
           if)
        #);
      findInCurrentDirProject:
        (# theProject:^sbprivate.hierarchyView.hir.project;
           fld:^hierarchy.folder;
           selectedGroup:^sbprivate.hierarchyView.hir.group;
           fg:^astInterface.fragmentgroup
        enter (fgName[],theProject[])
        do 'findInCurrentDirProject'->putline;
           fgName[]->mps.AST.expandToFullPath->mps.fragmentGroupTable.getFragmentGroup
             ->fg[];
           (if not theProject.isExpanded then theProject.onExpand if);
           theProject.items.find
           (#
              predicate:: 
                (# g: ^sbprivate.hierarchyView.hir.group; 
                do (if current##=sbprivate.hierarchyView.hir.group## then
                       current[]->g[];
                       (if g.fg[] = none then
                           g.findfg
                       if);
                       (g.fg[] = fg[])->value
                    else false->value
                   if)
                #)
           do 'findincurrentproject FOUND'->putline;
              current.select;
              sbprivate.hierarchyView.theScroller[]->current.scrollIntoView
           #)
        #);
      found:@boolean;
   do
      (if sbprivate.rootProjectID = sbprivate.RecentPrjId then
          (fgName[],true)->AddToUsedGroups;
          true->found
      if);
      (fgName[],false)->AddToUsedGroups;
      (if sbprivate.rootProjectID = sbprivate.DotPrjID then
          (# localpath:^text;
          do (fgname[],currentDirectory)->mps.ast.thepathhandler.localpath->localpath[];
             'DOTPRJ inserting: %s\n'->putformat(# do localpath[]->s #);
             localpath.reset;
             (if localpath.get
              // '/' then false->found
              // '.' then false->found
              // '~' then false->found
              else 
                 ll: '/'->localpath.find
                 (# 
                 do (localpath.copy,sbprivate.theActiveRootProject[])->findinsubdir;
                    leave ll
                 #);
                 (fgname[],sbprivate.theActiveRootProject[])->findInCurrentDirProject;
                 true->found;
             if);
          #);
          (if not found then
              sbprivate.StdLibPrjID -> sbprivate.rootProjectID;
              sbprivate.stdlibprj[]->sbprivate.theActiveRootProject[]
          if)
      if);
      StdLibIf:
        (if not found and (sbprivate.rootProjectID = sbprivate.StdLibPrjID) then
            'StdLibIf'->putline; fgname[]->putline;
            (if sbprivate.theActiveRootProject.items.empty then 
                (sbprivate.hierarchyView.hir[], sbprivate.theActiveRootProject[])->sbprivate.setupProjectFile 
            if);
            sbprivate.theActiveRootProject.items.scan
            (# theProject:^sbprivate.hierarchyView.hir.project;
               localpath:^text;
            do current[]->theProject[];
               'project: '->puttext;theproject.label[]->puttext;
               (fgname[],theProject.location[]->mps.ast.expandtofullpath)->mps.ast.thepathhandler.localpath->localpath[];
               ' path: '->puttext;localpath[]->putline;
               localpath.reset;
               (if localpath.get
                // '/' then false->found
                // '.' then false->found
                // '~' then false->found
                else 
                   ll: '/'->localpath.find
                   (# 
                   do (localpath.copy,theProject[])->findinsubdir;
                      leave ll
                   #);
                   (fgname[],theProject[])->findInCurrentDirProject;
                   true->found;
                   leave StdLibIf
               if)
            #)
        if);
      (if not found and (sbprivate.rootProjectID = sbprivate.HomedirPrjID) then
          '~'->putline;
          (fgname[],'~')->mps.ast.thepathhandler.localpath->putline;
      if);
      (if not found then
          (fgName[],true)->AddToUsedGroups
      if)
   #);

   (* 'selectFragmentForm - GroupAdded'->putline; *)
   
   (if not
       ((sbprivate.formView.selectedFormName[] <> none ) and
       (formName[]->sbprivate.formView.selectedFormName.equal)) then
       (sbprivate.formView.root).items.scan
         (# form: ^sbprivate.formView.form; 
         do (if current## = sbprivate.formView.form## then
                current[]->form[];
                (if form.ff.name->formName.equalNCS then
                    form.select;
                    formName.copy->sbprivate.formView.selectedFormName[];
                    sbprivate.formview.theScroller[]->form.scrollIntoView
                if)
            if)
         #)
    else
       (if sbprivate.formView.selectedFormName[] <> none then
           sbprivate.formView.selectedFormName[]->putline;
       if)
   if)  
