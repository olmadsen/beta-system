ORIGIN 'ymerBody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- ymerLoadSettings: dopart --
do browser.reloadsettings
   
--- menuFileSaveSettingsOnselect: dopart ---
do browser.saveSettings
   
--- menuFileReloadSettingsOnselect: dopart ---
do browser.reloadSettings

--- browserLib: attributes ---

saveSettings:
(# settings: @file; 

   saveProjects:
     (# level: @integer; projLst: ^projects.projectList
     enter (level,projLst[])
     do projLst.scan
        (#
        do (if current.level=0 then
               (for level repeat '\t'->settings.puttext for);
               'PROJECT'->settings.puttext; ' #'''->settings.puttext;
               current.name[]->settings.puttext; ''' '->settings.puttext;
               (if true
                // current.isDirectory then
                   ''''->settings.put;
                   current.location[]->settings.puttext;
                   ''''->settings.put;
                // current.isProjectFile then
                   ''''->settings.put;
                   current.location[]->settings.puttext;
                   ''''->settings.put;
                // current.isRoot then
                   ''''->settings.put;
                   current.location[]->settings.puttext;
                   ''''->settings.put;
                // current.hasSubprojects then
                   settings.newline;
                   (level+1, current.subProjects[])->saveProjects;
                   (for level+1 repeat '\t'->settings.puttext for);
                   '-------'->settings.puttext;
                // current.groups.size<>0 then
                   current.groups.scan
                   (#
                   do (if not current.temporary then
                          settings.newline;
                          (for level+1 repeat '\t'->settings.puttext for);
                          'GROUP'->settings.puttext; '#'''->settings.puttext; 
                          current.name[]->settings.puttext; ''' '->settings.puttext;
                          ''''->settings.put;
                          current.location[]->settings.puttext;
                          ''''->settings.put;
                      if)
                   #)
                else 'Something wrong'->screen.putline
               if);
               settings.newline;
           if)
        #)
     #);
   
   putOption:
     (# name, value: ^text
     enter (name[], value[])
     do name[]->settings.puttext;
       '='''->settings.puttext;
       value[]->settings.puttext;
        ''''->settings.putline;
     #);
        
enter settings[]
do '~/.ymer.rc'
     ->mps.ast.expandtofullpath->settings.name;
   (if settings.entry.writeable then
       settings.openwrite;
       
       ('BETAexecutable', BETAexecutable[])->putOption;
       ('SIFexecutable', SIFexecutable[])->putOption;
       ('FREJAexecutable', FREJAexecutable[])->putOption;
       ('INTERFACEBUILDERexecutable', INTERFACEBUILDERexecutable[])->putOption;
       ('VALHALLAexecutable', VALHALLAexecutable[])->putOption;
       ('TEXTexecutable', TEXTexecutable[])->putOption;
       ('BETAWCexecutable', BETAWCexecutable[])->putOption;
       settings.close;
    else
       (NONE, '~/.ymer.rc: no write access', 'Write access error')->alertUser
   if);
   
   '~/.ymer.pjt'
     ->mps.ast.expandtofullpath->settings.name;
   (if settings.entry.writeable then
       settings.openwrite;
       (0, projects.theProjects[])->saveProjects;
       settings.close;
    else
       (NONE, '~/.ymer.pjt: no write access', 'Write access error')->alertUser
   if)
#);

reloadSettings:
  (# settings: @file; newProj: ^projects.project;
     getOption:
       (# name, value: ^text; tmp: @text;
       enter name[]
       do settings.scanwhitespace;
          settings.scan(# while:: (# do (ch<>'=')->value #) do ch->tmp.put #);
          (if tmp[]->name.equal then
              settings.get; (*skip '='*)
              settings.scanwhitespace;
              (if settings.get='''' (*skip ''''*) then 
                  settings.scanwhitespace; &text[]-> value[];
                  settings.scan(# while:: (# do (ch<>'''')->value #) do ch->value.put#);
                  settings.get; (*skip ''''*)
              if)
          if)
       exit value[]
       #);
     user: ^text;
  enter settings[]
  do '~/.ymer.rc'
       ->mps.ast.expandtofullpath->settings.name;
     (if settings.entry.readable then
         settings.openread;
         
         'BETAexecutable'->getOption->BETAexecutable[];
         'SIFexecutable'->getOption->SIFexecutable[];
         'FREJAexecutable'->getOption->FREJAexecutable[];
         'INTERFACEBUILDERexecutable'->getOption->INTERFACEBUILDERexecutable[];
         'VALHALLAexecutable'->getOption->VALHALLAexecutable[];
         'TEXTexecutable'->getOption->TEXTexecutable[];
         'BETAWCexecutable'->getOption->BETAWCexecutable[];
         
         settings.close;
     if);
     
     '~/.ymer.pjt'
       ->mps.ast.expandtofullpath->settings.name;
     (if settings.entry.readable then
         projects.theProjects.clear;
         &browser.projects.project[]->newProj[];
         (settings.name, newProj[])->projects.theProjects.parseProjectFile;
         newProj.subprojects.scan
         (#
         do current[]->projects.theProjects.append;
            NONE->current.superProject[]
         #);
     if);

     browser.projects.theProjects.find
     (# loc: ^text;
        start:: (#
                do '~beta/configuration/ymer.pjt'
                     (*->mps.ast.expandtofullpath*)
                     ->loc[];
                #);
        predicate:: (# do loc[]->current.location.equal->value #);
        notFound::
          (# 
          do (if loc[]->mps.ast.expandtofullpath->fileReadable then
                 &browser.projects.project[]->newProj[];
                 true->newProj.isProjectFile;
                 'Std. Libraries/'->newProj.name[];
                 loc[]->newProj.location.puttext;
                 (loc[],newProj[])->projects.theProjects.parseProjectFile;
                 newProj[]->browser.projects.theProjects.append;
             if)
          #)
     #);

     browser.projects.theProjects.find
     (# loc: ^text;
        start::
          (#
          do '~'->loc[]; mps.ast.thepathhandler.directorychar->loc.put;
          #);
        predicate:: (# do loc[]->current.location.equal->value #);
        notFound::
          (# 
          do (if '~'->mps.ast.expandtofullpath->fileReadable then
                 &browser.projects.project[]->newProj[];
                 '~'->newProj.location.puttext;
                 '~/'->newProj.name[];
                 newProj[]->browser.projects.setupProject;
                 newProj[]->browser.projects.theProjects.append;
             if)
          #)
     #);
     
     browser.projects.theProjects.find
     (# loc: ^text;
        start::
          (#
          do '.'->loc[]; mps.ast.thepathhandler.directorychar->loc.put;
          #);
        predicate:: (# do loc[]->current.location.equal->value #);
        notFound::
          (# 
          do (if '.'->mps.ast.expandtofullpath->fileReadable then
                 &browser.projects.project[]->newProj[];
                 '.'->newProj.location.puttext;
                 './'->newProj.name[];
                 newProj[]->browser.projects.setupProject;
                 newProj[]->browser.projects.theProjects.append;
             if)
          #)
     #);
     browser.projectsChanged;
  #);
