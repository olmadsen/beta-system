ORIGIN 'ymerBody';
INCLUDE 'setupProjects'
        '~beta/sysutils/envstring';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- ymerLoadSettings: DoPart --
do browser.reloadsettings  

-- menuFileSaveSettingsOnselect: DoPart --
do browser.saveSettings  

-- menuFileReloadSettingsOnselect: DoPart --
do browser.reloadSettings  

-- browserLib: Attributes --
saveSettings:
  (#
     settings: @file;
     saveProjects:
       (#
          level: @integer;
          projLst: ^browser.sbprivate.hierarchyView.hir.project
       enter (level,projLst[])
       do
          projLst.items.scan
            (# p: ^browser.sbprivate.hierarchyView.hir.project
            do
               current[]->p[];
               (if true (*current.level=0*) then
                   (for level repeat '\t'->settings.puttext for);
                   'PROJECT'->settings.puttext;
                   ' #\''->settings.puttext;
                   p.label[]->settings.puttext;
                   '\' '->settings.puttext;
                   (if true
                    //
                    p##
                    =
                    browser.sbprivate.hierarchyView.hir.directoryProject## then
                       '\''->settings.put;
                       p.location[]->settings.puttext;
                       '\''->settings.put;
                       
                    // p## = browser.sbprivate.hierarchyView.hir.projectFile##
                    then
                       '\''->settings.put;
                       p.location[]->settings.puttext;
                       '\''->settings.put;
                       
                    // p## = browser.sbprivate.hierarchyView.hir.rootProject##
                    then
                       '\''->settings.put;
                       p.location[]->settings.puttext;
                       '\''->settings.put;
                       
                    // p.hasSubprojects then
                       settings.newline;
                       (level+1,p[])->saveProjects;
                       (for level+1 repeat '\t'->settings.puttext for);
                       '-------'->settings.puttext;
                       
                    // p.items.size <> 0 then
                       p.items.scan
                         (# g: ^browser.sbprivate.hierarchyView.hir.group
                         do
                            current[]->g[];
                            (if true (*not g.temporary*) then
                                settings.newline;
                                (for level+1 repeat
                                  '\t'->settings.puttext
                                for);
                                'GROUP'->settings.puttext;
                                '#\''->settings.puttext;
                                g.label[]->settings.puttext;
                                '\' '->settings.puttext;
                                '\''->settings.put;
                                g.location[]->settings.puttext;
                                '\''->settings.put;
                                
                            if)
                         #)
                    else
                       'Something wrong'->screen.putline
                   if);
                   settings.newline;
                   
               if)
            #)
       #);
     putOption:
       (# name,value: ^text
       enter (name[],value[])
       do
          name[]->settings.puttext;
          '=\''->settings.puttext;
          value[]->settings.puttext;
          '\''->settings.putline;
          
       #);
     
  do
     '~/.ymer.pjt'->THIS(ymerBrowserWindow).mps.ast.expandtofullpath
       ->settings.name;
     (if settings.entry.writeable then
         settings.openwrite;
         (0,browser.sbprivate.rootProject[])->saveProjects;
         settings.close;
         
      else
         (none ,'~/.ymer.pjt: no write access','Write access error')->alertUser
     if)
  #);
reloadSettings:
  (#
     settings: @file;
     newProj: ^browser.sbprivate.hierarchyView.hir.project;
     newTmpProj: ^browser.sbprivate.hierarchyView.hir.temporaryProject;
     newGrp: ^browser.sbprivate.hierarchyView.hir.group;
     newProjFile: ^browser.sbprivate.hierarchyView.hir.projectFile;
     fg: ^mps.ast.fragmentgroup;
     getOption:
       (# name,value: ^text; tmp: @text; 
       enter name[]
       do
          settings.scanwhitespace;
          settings.scan
            (# while::  (#  do (ch <> '=')->value #) do ch->tmp.put #);
          (if tmp[]->name.equal then
              settings.get;
              (*skip '='*)
              settings.scanwhitespace;
              (if settings.get = '\'' (*skip '\''*) then
                  settings.scanwhitespace;
                  &text[]->value[];
                  settings.scan
                    (# while::  (#  do (ch <> '\'')->value #)
                    do ch->value.put
                    #);
                  settings.get;
                  (*skip '\''*)
                  
              if)
          if)
       exit value[]
       #);
     user: ^text;
     mjolnerpath: ^text;
     
  do
     '~/.ymer.pjt'->THIS(ymerBrowserWindow).mps.ast.expandtofullpath
       ->settings.name;
     (if settings.entry.readable then
         browser.sbprivate.rootProject.items.clear;
         &browser.sbprivate.hierarchyView.hir.projectFile[]->newProjFile[];
         settings.name->newProjFile.location[];
         newProjFile.setup;
         newProjFile.items.scan
           (# f: ^browser.sbprivate.hierarchyView.hir.project
           do
              current[]->f[];
              f[]->browser.sbprivate.rootProject.addItem;
              none ->f.superProject[]
           #);
         
     if);
     browser.sbprivate.rootProject.items.find
       (#
          loc: ^text;
          start::  (#  do '~beta/configuration/ymer.pjt'->loc[] #);
          predicate:: 
            (# p: ^browser.sbprivate.hierarchyView.hir.project
            do current[]->p[]; loc[]->p.location.equal->value
            #);
          notFound:: 
            (# 
            do
               (if
               loc[]->THIS(ymerBrowserWindow).mps.ast.expandtofullpath
                 ->fileReadable then
                   &browser.sbprivate.hierarchyView.hir.projectFile[]
                     ->newProj[];
                   'Std. Libraries/'->newProj.label[];
                   loc[]->newProj.location[];
                   newProj.init;
                   newProj[]->browser.sbprivate.rootProject.addItem;
                   newProj[]->browser.sbprivate.stdlibprj[]
               if)
            #)
       #);
     browser.sbprivate.rootProject.items.find
       (#
          loc: ^text;
          start::  (#  do '~'->loc[] #);
          predicate:: 
            (# p: ^browser.sbprivate.hierarchyView.hir.project
            do current[]->p[]; loc[]->p.location.equal->value
            #);
          notFound:: 
            (# 
            do
               (if
               '~'->THIS(ymerBrowserWindow).mps.ast.expandtofullpath
                 ->fileReadable then
                   &browser.sbprivate.hierarchyView.hir.directoryProject[]
                     ->newProj[];
                   '~'->newProj.location[];
                   '~/'->newProj.label[];
                   (*newProj[]->browser.sbprivate.hierarchyView.hir.setupProject;*)
                   newProj.init;
                   newProj[]->browser.sbprivate.rootProject.addItem;
                   newProj[]->browser.sbprivate.homeprj[]
               if)
            #)
       #);
     browser.sbprivate.rootProject.items.find
       (#
          loc: ^text;
          start::  (#  do '.'->loc[] #);
          predicate:: 
            (# p: ^browser.sbprivate.hierarchyView.hir.project
            do current[]->p[]; loc[]->p.location.equal->value
            #);
          notFound:: 
            (# 
            do
               (if
               '.'->THIS(ymerBrowserWindow).mps.ast.expandtofullpath
                 ->fileReadable then
                   &browser.sbprivate.hierarchyView.hir.directoryProject[]
                     ->newProj[];
                   '.'->newProj.location[];
                   './'->newProj.label[];
                   (*newProj[]->browser.sbprivate.hierarchyView.hir.setupProject;*)
                   newProj.init;
                   newProj[]->browser.sbprivate.rootProject.addItem;
                   newProj[]->browser.sbprivate.dotprj[]
               if);
               
            #)
       #);
     &browser.sbprivate.hierarchyView.hir.temporaryProject[]->newTmpProj[];
     'Recent Files'->newTmpProj.label[];
     '$(MJOLNERPATH)'->expandEnvVar->mjolnerpath[];
     (if not ((mjolnerpath.length > 0) and (mjolnerpath[]->isDirectory)) then
         ''->mjolnerpath[]
     if);
     '/.recentProject.pjt'->mjolnerpath.append;
     mjolnerpath[]->THIS(ymerBrowserWindow).mps.ast.expandtofullpath
       ->newTmpProj.location[];
     (browser.sbprivate.prjicon[],browser.sbprivate.prjopenicon[])
       ->newTmpProj.icon;
     newTmpProj.init;
     newTmpProj[]->browser.sbprivate.rootProject.addItem;
     newTmpProj[]->browser.sbprivate.recentprj[];
     newTmpProj[]->browser.sbprivate.hierarchyView.hir.usedGroups[];
     (if
     mjolnerpath[]->THIS(ymerBrowserWindow).mps.ast.expandtofullpath
       ->fileReadable then
         &browser.sbprivate.hierarchyView.hir.projectFile[]->newProjFile[];
         newTmpProj.location.copy->newProjFile.location[];
         newProjFile.setup;
         (browser.sbprivate.hierarchyView.hir[],newProjFile[])
           ->browser.sbprivate.setupProjectFile;
         newProjFile.items.scan
           (# f,ngrp: ^browser.sbprivate.hierarchyView.hir.group
           do
              (if current## = browser.sbprivate.hierarchyView.hir.group## then
                  current[]->f[];
                  &browser.sbprivate.hierarchyView.hir.group[]->ngrp[];
                  f.location.copy->ngrp.location[];
                  f.label.copy->ngrp.label[];
                  newTmpProj[]->ngrp.init;
                  ngrp[]->newTmpProj.additem;
                  ngrp.theProjectInfoGroup[]
                    ->newTmpProj.theProjectInfoProject.groups.append
               else
                  current[]->newTmpProj.additem
              if)
           #)
     if);
     browser.projectsChanged
  #);
  

