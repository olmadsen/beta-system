ORIGIN 'ymerBody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- ymerLoadSettings: dopart --
do browser.reloadsettings
   
--- menuFileSaveSettingsOnselect: dopart ---
do browser.saveSettings
   
--- menuFileReloadSettingsOnselect: dopart ---
do browser.reloadSettings

--- browserLib: attributes ---

saveSettings:
(# settings: @file; 

   saveProjects:
     (# level: @integer;
        projLst: ^browser.sbprivate.hierarchyView.hir.project
     enter (level,projLst[])
     do projLst.items.scan
        (# p: ^browser.sbprivate.hierarchyView.hir.project
        do current[]->p[];
           (if true(*current.level=0*) then
               (for level repeat '\t'->settings.puttext for);
               'PROJECT'->settings.puttext; ' #\''->settings.puttext;
               p.label[]->settings.puttext; '\' '->settings.puttext;
               (if true
                // p##=browser.sbprivate.hierarchyView.hir.directoryProject## then
                   '\''->settings.put;
                   p.location[]->settings.puttext;
                   '\''->settings.put;
                // p##=browser.sbprivate.hierarchyView.hir.projectFile## then
                   '\''->settings.put;
                   p.location[]->settings.puttext;
                   '\''->settings.put;
                //  p##=browser.sbprivate.hierarchyView.hir.rootProject## then
                   '\''->settings.put;
                   p.location[]->settings.puttext;
                   '\''->settings.put;
                // p.hasSubprojects then
                   settings.newline;
                   (level+1, p[])->saveProjects;
                   (for level+1 repeat '\t'->settings.puttext for);
                   '-------'->settings.puttext;
                // p.items.size<>0 then
                   p.items.scan
                   (# g: ^browser.sbprivate.hierarchyView.hir.group
                   do current[]->g[];
                      (if true(*not g.temporary*) then
                          settings.newline;
                          (for level+1 repeat '\t'->settings.puttext for);
                          'GROUP'->settings.puttext; '#\''->settings.puttext; 
                          g.label[]->settings.puttext; '\' '->settings.puttext;
                          '\''->settings.put;
                          g.location[]->settings.puttext;
                          '\''->settings.put;
                      if)
                   #)
                else 'Something wrong'->screen.putline
               if);
               settings.newline;
           if)
        #)
     #);
   
   putOption:
     (# name, value: ^text
     enter (name[], value[])
     do name[]->settings.puttext;
       '=\''->settings.puttext;
       value[]->settings.puttext;
        '\''->settings.putline;
     #);
        
do '~/.ymer.rc'
     ->this(ymerBrowserWindow).mps.ast.expandtofullpath->settings.name;
   (if settings.entry.writeable then
       settings.openwrite;
       
(*       ('BETAexecutable', BETAexecutable[])->putOption;
       ('SIFexecutable', SIFexecutable[])->putOption;
       ('FREJAexecutable', FREJAexecutable[])->putOption;
       ('INTERFACEBUILDERexecutable', INTERFACEBUILDERexecutable[])->putOption;
       ('VALHALLAexecutable', VALHALLAexecutable[])->putOption;
       ('TEXTexecutable', TEXTexecutable[])->putOption;
 ('BETAWCexecutable', BETAWCexecutable[])->putOption;*)
       settings.close;
    else
       (NONE, '~/.ymer.rc: no write access', 'Write access error')->alertUser
   if);
   
   '~/.ymer.pjt'
     ->this(ymerBrowserWindow).mps.ast.expandtofullpath->settings.name;
   (if settings.entry.writeable then
       settings.openwrite;
       (0, browser.sbprivate.rootProject[])->saveProjects;
       settings.close;
    else
       (NONE, '~/.ymer.pjt: no write access', 'Write access error')->alertUser
   if)
#);

reloadSettings:
  (# settings: @file;
     newProj: ^browser.sbprivate.hierarchyView.hir.project;
     newProjFile: ^browser.sbprivate.hierarchyView.hir.projectFile;
     getOption:
       (# name, value: ^text; tmp: @text;
       enter name[]
       do settings.scanwhitespace;
          settings.scan(# while:: (# do (ch<>'=')->value #) do ch->tmp.put #);
          (if tmp[]->name.equal then
              settings.get; (*skip '='*)
              settings.scanwhitespace;
              (if settings.get='\'' (*skip '\''*) then 
                  settings.scanwhitespace; &text[]-> value[];
                  settings.scan(# while:: (# do (ch<>'\'')->value #) do ch->value.put#);
                  settings.get; (*skip '\''*)
              if)
          if)
       exit value[]
       #);
     user: ^text;
  do '/home/gram/beta/r5.2/sourcebrowser/private/settings.bet: Line 130'->screen.putline;
     '~/.ymer.rc'
       ->this(ymerBrowserWindow).mps.ast.expandtofullpath->settings.name;
     (if settings.entry.readable then
         settings.openread;
         
(*         'BETAexecutable'->getOption->BETAexecutable[];
         'SIFexecutable'->getOption->SIFexecutable[];
         'FREJAexecutable'->getOption->FREJAexecutable[];
         'INTERFACEBUILDERexecutable'->getOption->INTERFACEBUILDERexecutable[];
         'VALHALLAexecutable'->getOption->VALHALLAexecutable[];
         'TEXTexecutable'->getOption->TEXTexecutable[];
         'BETAWCexecutable'->getOption->BETAWCexecutable[];
 *)       
         settings.close;
     if);
     
     '~/.ymer.pjt'
       ->this(ymerBrowserWindow).mps.ast.expandtofullpath->settings.name;
     (if settings.entry.readable then
         browser.sbprivate.rootProject.items.clear;
         &browser.sbprivate.hierarchyView.hir.projectFile[]->newProjFile[];
         settings.name->newProjFile.location[];
         newProjFile.setup;
         newProjFile.items.scan
         (# f: ^browser.sbprivate.hierarchyView.hir.project
         do current[]->f[];
            f[]->browser.sbprivate.rootProject.addItem;
            NONE->f.superProject[]
         #);
     if);
     
     (if '.recentProject.pjt'->this(ymerBrowserWindow).mps.ast.expandtofullpath->fileReadable then
         &browser.sbprivate.hierarchyView.hir.projectFile[]->newProj[];
         'Last Used'->newProj.label[];
         '.recentProject.pjt'->newProj.location[];
         newProj.init;
         newProj[]->browser.sbprivate.rootProject.addItem
     if);
     
     browser.sbprivate.rootProject.items.find
     (# loc: ^text;
        start:: (# do '~beta/configuration/ymer.pjt'->loc[] #);
        predicate::
          (# p: ^browser.sbprivate.hierarchyView.hir.project
          do current[]->p[];
             loc[]->p.location.equal->value
          #);
        notFound::
          (# 
          do (if loc[]->this(ymerBrowserWindow).mps.ast.expandtofullpath->fileReadable then
                 &browser.sbprivate.hierarchyView.hir.projectFile[]->newProj[];
                 'Std. Libraries/'->newProj.label[];
                 loc[]->newProj.location[];
                 newProj.init;
                 newProj[]->browser.sbprivate.rootProject.addItem
             if)
          #)
     #);

     browser.sbprivate.rootProject.items.find
     (# loc: ^text;
        start:: (# do '~'->loc[] #);
        predicate:: 
          (# p: ^browser.sbprivate.hierarchyView.hir.project
          do current[]->p[];
             loc[]->p.location.equal->value
          #);
        notFound::
          (# 
          do (if '~'->this(ymerBrowserWindow).mps.ast.expandtofullpath->fileReadable then
                 &browser.sbprivate.hierarchyView.hir.directoryProject[]->newProj[];
                 '~'->newProj.location[];
                 '~/'->newProj.label[];
                 (*newProj[]->browser.sbprivate.hierarchyView.hir.setupProject;*)
                 newProj.init;
                 newProj[]->browser.sbprivate.rootProject.addItem
             if)
          #)
     #);

     browser.sbprivate.rootProject.items.find
     (# loc: ^text;
        start:: (# do '.'->loc[] #);
        predicate::
          (# p: ^browser.sbprivate.hierarchyView.hir.project
          do current[]->p[];
             loc[]->p.location.equal->value
          #);
        notFound::
          (# 
          do (if '.'->this(ymerBrowserWindow).mps.ast.expandtofullpath->fileReadable then
                 &browser.sbprivate.hierarchyView.hir.directoryProject[]->newProj[];
                 '.'->newProj.location[];
                 './'->newProj.label[];
                 (*newProj[]->browser.sbprivate.hierarchyView.hir.setupProject;*)
                 newProj.init;
                 newProj[]->browser.sbprivate.rootProject.addItem
             if);
          #)
     #);
     
     browser.projectsChanged;
  #);
