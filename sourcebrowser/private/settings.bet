ORIGIN '../ymer'

--- browserLib: attributes ---

saveSettings:
(# settings: @file; 

   saveProjects:
     (# level: @integer; projLst: ^projects.projectList
     enter (level,projLst[])
     do projLst.scan
        (#
        do (for level repeat '\t'->settings.puttext for);
           'PROJECT'->settings.puttext; ' '->settings.put;
           current.name[]->settings.puttext; ' '->settings.put;
           (if true
            // current.isDirectory then
               ''''->settings.put;
               current.location[]->settings.puttext;
               ''''->settings.put;
            // current.isProjectFile then
               ''''->settings.put;
               current.location[]->settings.puttext;
               ''''->settings.put;
            // current.isRoot then
               ''''->settings.put;
               current.location[]->settings.puttext;
               ''''->settings.put;
            // current.hasSubprojects then
               settings.newline;
               (level+1, current.subProjects[])->saveProjects;
               (for level+1 repeat '\t'->settings.puttext for);
               '-------'->settings.puttext;
            // current.groups.size<>0 then
               current.groups.scan
               (#
               do settings.newline;
                  (for level+1 repeat '\t'->settings.puttext for);
                  current.name[]->settings.puttext; ' '->settings.put;
                  ''''->settings.put;
                  current.location[]->settings.puttext;
                  ''''->settings.put;
               #)
            else 'Something wrong'->screen.putline
           if);
           settings.newline;
        #)
     #);
   
   putOption:
     (# name, value: ^text
     enter (name[], value[])
     do name[]->settings.puttext;
       '='''->settings.puttext;
       value[]->settings.puttext;
        ''''->settings.putline;
     #);
        
enter settings[]
do ('~/.ymer.rc',ph.currentDirectory)
     ->ph.convertfilepath->settings.name;
   (if settings.entry.writeable then
       settings.openwrite;
       
       ('BETAlib', BETAlib[])->putOption;
       ('BETAexecutable', BETAexecutable[])->putOption;
       ('SIFexecutable', SIFexecutable[])->putOption;
       ('VALHALLAexecutable', VALHALLAexecutable[])->putOption;
       ('TEXTexecutable', TEXTexecutable[])->putOption;
       ('BETAWCexecutable', BETAWCexecutable[])->putOption;
       settings.close;
    else
       (NONE, '~/.ymer.rc: no write access', 'Write access error')->alertUser
   if);
   
   ('~/.ymer.pjt',ph.currentDirectory)
     ->ph.convertfilepath->settings.name;
   (if settings.entry.writeable then
       settings.openwrite;
       (0, projects.theProjects[])->saveProjects;
       settings.close;
    else
       (NONE, '~/.ymer.pjt: no write access', 'Write access error')->alertUser
   if)
#);

loadSettings:
  (# settings: @file; newProj: ^projects.project;
     getOption:
       (# name, value: ^text; tmp: @text;
       enter name[]
       do settings.scanwhitespace;
          settings.scan(# while:: (# do (ch<>'=')->value #) do ch->tmp.put #);
          (if tmp[]->name.equal then
              settings.get; (*skip '='*)
              settings.scanwhitespace;
              (if settings.get='''' (*skip ''''*) then 
                  settings.scanwhitespace; &text[]-> value[];
                  settings.scan(# while:: (# do (ch<>'''')->value #) do ch->value.put#);
                  settings.get; (*skip ''''*)
              if)
          if)
       exit value[]
       #)
  enter settings[]
  do ('~/.ymer.rc',ph.currentDirectory)
       ->ph.convertfilepath->settings.name;
     (if settings.entry.readable then
         settings.openread;
         
         'BETAlib'->getOption->BETAlib[];
         'BETAexecutable'->getOption->BETAexecutable[];
         'SIFexecutable'->getOption->SIFexecutable[];
         'VALHALLAexecutable'->getOption->VALHALLAexecutable[];
         'TEXTexecutable'->getOption->TEXTexecutable[];
         'BETAWCexecutable'->getOption->BETAWCexecutable[];
         
         settings.close;
     if);
     
     ('~/.ymer.pjt',ph.currentDirectory)
       ->ph.convertfilepath->settings.name;
     (if settings.entry.readable then
         &projects.theProjects.element[]->newProj[];
         (settings.name, newProj[])->projects.theProjects.parseProjectFile;
         projects.theProjects.clear;
         newProj.subprojects.scan
         (# do current[]->projects.theProjects.append #);
      else
         projects.theProjects.parseProjectsRC;
     if);
     browser.projectsChanged;
  #);
