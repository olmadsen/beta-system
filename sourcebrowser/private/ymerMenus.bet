ORIGIN '../ymer';
INCLUDE 'ymerBody'
        'semanticEditor'
        'workspace'
        'help'
        'separateCodeEditor';
-- menuFileHelp: DoPart --
do
     (#
        help: @helpViewer
          (#
             open:: 
               (# 
               do
                  ymerPrivate.helpDocuments.scan
                    (# 
                    do (current.title[],current.path[])->registerTheme
                    #)
               #)
          #)
     do help.open
     #)  

-- menuFileCloseOnselect: DoPart --
do quit  

-- menuFileQuitOnselect: DoPart --
do
   l:
     (# 
     do
        browsers.scan
          (#  do (if not current.closeGroups then leave l if) #);
        browsers.scan
          (#
             where:: 
               (#  do current[] <> THIS(ymerBrowserWindow)[]->value #)
          do current.quit
          #);
        THIS(ymerBrowserWindow).quit;
        
     #);
   (* If we get to this point, not all browsers have been closed *)
   (none ,'Quit','Modified buffers still exists.\nQuit anyway?')
     ->promptForBoolean (# ok::  (#  do terminate #) #)  

-- menuProjectsLoadOnselect: DoPart --
do
     (# f: @file; ext: ^text; 
     do
        fileSelectionDialog (#  do 'Load Project File'->label[] #)->t[];
        (if t[] <> none then
            &browser.projects.project[]->newProj[];
            t->newProj.location;
            newProj[]->browser.projects.setupProject;
            newProj[]->browser.projects.theProjects.append;
            browser.projectsChanged;
            newProj[]->browser.setProject;
            (if browser.currentProject.groups.size = 1 then
            (* automatically select it, and the first fragment form in it *)
                (if (browser.currentProject.groups.head).elm.getfg <> none then
                    l:
                    (browser.currentProject.groups.head).elm.fg.fragmentList.
                      scan
                      (#
                         ff: ^mps.AST.fragmentForm;
                         (* find first FF to select it automatically *)
                         
                      do
                         (if current.type
                          // mps.AST.formType then
                             current.open->ff[];
                             (browser.currentProject[],
                              (browser.currentProject.groups.head).elm[],ff[])
                               ->browser.setFragmentForm;
                             leave l
                         if)
                      #)
                if)
            if);
            
        if)
     #)  

-- menuProjectsRenameOnselect: Descriptor --
(# t: ^text; cp: ^browser.projects.project; ch: @char
do
   fileCreationDialog (#  do 'New Project Location'->label[] #)->t[];
   (if t[] <> none then
       browser.currentProject[]->cp[];
       t->cp.location;
       cp[]->browser.projects.setupProject;
       browser.projectsChanged;
       cp[]->browser.setProject;
       
   if)
#)  

-- menuProjectsRelocateOnselect: DoPart --
do
   browser.currentProject[]->cp[];
   fileSelectionDialog (#  do 'Specify new location'->label[] #)->t[];
   (if true
    // (t[] = none ) then (* ignore: cancel pressed *)
       
    // t[]->fileAvailable then
       t->cp.location;
       cp[]->browser.projects.setupProject;
       (if cp.unfolded then
           cp[]->browser.foldSubprojects; cp[]->browser.unfoldSubprojects; 
       if);
       browser.projectsChanged;
       cp[]->browser.setProject
   if)  

-- menuProjectsDeleteOnselect: DoPart --
do
   browser.currentProject[]->cp[];
   (if cp.unfolded then cp[]->browser.foldSubprojects;  if);
   cp[]->browser.projects.theProjects.at->browser.projects.theProjects.delete;
   browser.projectsChanged  

-- menuGroupsNewOnselect: DoPart --
do
(*fileSelectionDialog(# do 'New group'->label[] #)->t[];
 &browser.projects.group[]->newGroup[];
 newGroup[]->browser.currentProject.subprojects.append;
 t->newGroup.location;
 t[]->newGroup.name[];
 browser.projectsChanged*)   

-- menuGroupsAddOnselect: DoPart --
do   

-- windowsMenuOnSelect: DoPart --
do
   l: scan
     (# wmi: ^windowMenuItem
     do
        (if current## <= windowMenuItem## then
            current[]->wmi[]; wmi.ww.title->wmi.name; 
        if)
     #)  

-- menuGroupsDeleteOnselect: DoPart --
do   

-- windowsMenuInsertWindow: Descriptor --
(# wmi: ^windowMenuItem
do
   &windowMenuItem[]->wmi[];
   win[]->wmi.ww[];
   wmi.open;
   wmi[]->append;
   win.title->wmi.name;
   
#)  

-- windowsMenuDeleteWindow: DoPart --
do
   l: scan
     (# wmi: ^windowMenuItem
     do
        (if current## <= windowMenuItem## then
            current[]->wmi[];
            (if wmi.ww[] = win[] then wmi[]->delete; leave l if)
        if)
     #)  

-- windowsMenuPrivate: Attributes --
windowMenuItem: menuItem
  (#
     ww: ^window;
     eventhandler::  (# onSelect::  (#  do ww.show; ww.wriggle #) #);
     
  #);
  

-- windowsMenuSeparateBrowser: DoPart --
do (&ymerBrowserWindow[]).open  

-- windowsMenuSeparateCodeView: DoPart --
do (#  do (browser.currentForm[],none )->viewSeparate #)  

-- windowsMenuWorkspace: DoPart --
do (&workspace[]).open  

-- windowsMenuSemanticViewerOnselect: DoPart --
do
     (#
        sv: @browser.semanticViewer
          (#
             open:: 
               (# 
               do
                  browser.currentGroup.fg[]->sv.fg[];
                  browser.currentGroup.name[]->sv.fgName[];
                  
               #)
          #)
     do
        (if (browser.currentGroup[] <> none ) and
        (browser.currentGroup.getfg <> none ) then
            sv.open; 
        if)
     #)  

-- windowsMenuSemanticEditorOnselect: DoPart --
do
     (#
        se: @browser.semanticEditor
          (#
             open:: 
               (# 
               do
                  browser.currentGroup.fg[]->fg[];
                  browser.currentGroup.name[]->fgName[];
                  
               #)
          #);
        
     do
        (if (browser.currentGroup[] <> none ) and
        (browser.currentGroup.getfg <> none ) then
            se.open
        if)
     #)  

