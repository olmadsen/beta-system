ORIGIN '../ymer';
INCLUDE 'ymerBody'
        'semanticEditor'
        'workspace'
        'help'
        'depGraph'
        'separateCodeEditor';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- menuFileHelp: DoPart --
do browser.viewHTML 

-- menuFileCloseOnselect: DoPart --
do (if ymerBrowserPrivate.geList.closeGroups and
       browser.sbprivate.down.aeList.closeAsciiEditors then
       quit
   if)

-- menuFileQuitOnselect: DoPart --
do
   l:
     (# leaveQuit, closeAll: @boolean
     do ll:
          (# 
          do true->closeAll; false->leaveQuit;
             (if browsers.size>1 then
                 (none ,'Quit','More browsers exists.\nQuit other browsers?')
                   ->promptForBoolean
                 (# ok:: (# do false->leaveQuit; true->closeAll #);
                    notok:: (# do false->leaveQuit; false->closeAll #);
                    cancel:: (# do true->leaveQuit; false->closeAll #)
                 #);
                 (if leaveQuit then leave l if);
                 (if closeAll then
                     browsers.scan
                     (# where::
                          (# do current[]<>THIS(ymerBrowserWindow)[]->value #)
                     do (if not current.closeGroups then leave ll if);
                        (if not current.browser.sbprivate.down.aeList.closeAsciiEditors then leave ll if);
                     #);
                     browsers.scan
                     (# where::
                          (# do current[]<>THIS(ymerBrowserWindow)[]->value #)
                     do current.quit
                     #)
                 if)
             if);
             (if not THIS(ymerBrowserWindow).closeGroups then leave ll if);
             (if not THIS(ymerBrowserWindow).browser.sbprivate.down.aeList.closeAsciiEditors then leave ll if);
             THIS(ymerBrowserWindow).quit;
          #);
        (* If we get to this point, not all browsers have been closed *)
        (if closeAll and onTerminateApplication then
            (none ,'Quit','Modified buffers still exists.\nQuit anyway?')
              ->promptForBoolean(# ok:: (# do false->leaveQuit #);
                                   notok:: (# do true->leaveQuit #);
                                   cancel:: (# do true->leaveQuit #)
                                #);
            (if not leaveQuit then terminate if)
        if)
     #)
   
-- menuProjectsLoadOnselect: DoPart --
do
     (# f: @file; ext: ^text; proj: ^browser.projects.project
     do
        fileSelectionDialog (#  do 'Load Project File'->label[] #)->t[];
        (if t[] <> none then
            &browser.projects.project[]->newProj[];
            t[]->newProj.location.puttext;
            newProj[]->browser.projects.setupProject->proj[];
            (if proj[]=NONE then
                newProj[]->proj[]->browser.projects.theProjects.append;
                browser.projectsChanged
            if);
            proj[]->browser.setProject;
        if)
     #)  

-- menuProjectsRenameOnselect: Descriptor --
(# t: ^text; cp: ^browser.projects.project; ch: @char
do
   browser.currentProject[]->cp[];
   (none, 'Project Rename', 'New project name', cp.name[])
     ->promptForText(# ok:: (# do usertext[]->t[] #) #);
   (if t[] <> none then
       t->cp.name;
       browser.projectsChanged
   if)
#)  

-- menuProjectsSaveAsOnselect: Descriptor --
(#
do browser.currentProject[]->cp[];
   fileSelectionDialog (#  do 'New Project Location'->label[] #)
     ->t[];
   (if true
    // (t[] = none ) then (* ignore: cancel pressed *)
       
    // t[]->fileAvailable then
       t->cp.location;
       cp[]->browser.projects.setupProject;
       (if cp.unfolded then
           cp[]->browser.foldSubprojects; cp[]->browser.unfoldSubprojects 
       if);
       browser.projectsChanged;
       cp[]->browser.setProject
   if)  
#)

-- menuProjectsReloadOnselect: Descriptor --
(#
do browser.currentProject[]->cp[];
   cp.groups.clear; cp.subprojects.clear;
   (if cp.unfolded then
       cp[]->browser.foldSubprojects; cp[]->browser.unfoldSubprojects
   if);
   browser.projectsChanged;
   cp[]->browser.setProject
#)

-- menuProjectsDeleteOnselect: DoPart --
do
   browser.currentProject[]->cp[];
   (if cp.unfolded then cp[]->browser.foldSubprojects;  if);
   cp[]->browser.projects.theProjects.at->browser.projects.theProjects.delete;
   browser.projectsChanged  

-- menuGroupsNewOnselect: DoPart --
do
(*fileSelectionDialog(# do 'New group'->label[] #)->t[];
 &browser.projects.group[]->newGroup[];
 newGroup[]->browser.currentProject.subprojects.append;
 t->newGroup.location;
 t[]->newGroup.name[];
 browser.projectsChanged*)   

-- menuGroupsAddOnselect: DoPart --
do   

-- windowsMenuOnSelect: DoPart --
do
   l: scan
     (# wmi: ^windowMenuItem
     do
        (if current## <= windowMenuItem## then
            current[]->wmi[]; wmi.ww.title->wmi.name; 
        if)
     #)  

-- menuGroupsDeleteOnselect: DoPart --
do   

-- windowsMenuInsertWindow: Descriptor --
(# wmi: ^windowMenuItem
do
   &windowMenuItem[]->wmi[];
   win[]->wmi.ww[];
   wmi.open;
   wmi[]->append;
   win.title->wmi.name;
   
#)  

-- windowsMenuDeleteWindow: DoPart --
do
   l: scan
     (# wmi: ^windowMenuItem
     do
        (if current## <= windowMenuItem## then
            current[]->wmi[];
            (if wmi.ww[] = win[] then wmi[]->delete; leave l if)
        if)
     #)  

-- windowsMenuPrivate: Attributes --
windowMenuItem: menuItem
  (#
     ww: ^window;
     eventhandler::  (# onSelect::  (#  do ww.show; ww.wriggle #) #);
     
  #);
  

-- windowsMenuSeparateBrowser: DoPart --
do (&ymerBrowserWindow[]).open  

-- windowsMenuGraphBrowser: DoPart --
do (# cp: ^browser.projects.project;
      cpl: ^list.theCellType;
      loc, t: ^text;
      fgWindow: @graphBrowser
        (# fgChanged::
             (#
             do cp[]->browser.setProject;
                location[]->browser.selectFragmentGroup;
             #)
        #);
   do (if browser.currentGroup.getfg<>none then
          browser.currentGroup.location[]->loc[]->addProject;
          browser.currentProject[]->cp[];
          (if not cp.unfolded then
              cp[]->browser.unfoldSubprojects;
              browser.projectsChanged;
              cp.subprojects.head->cpl[];
              cpl.succ[]->cpl[]; cpl.elm[]->cp[]; (* select [extent] project *)
              cp[]->browser.setProject;
          if);
          fgWindow.open;
          'Dependency Graph Browser:\n'->t[];
          '   Experimental version - use with care'->t.putline;
          '   May contain bugs and inconveniences'->t.puttext;
          (none, t[], 'Warning')->alertUser;
          (loc[], mps.AST[])->fgWindow.display;
      if);
   #)

-- windowsMenuSeparateCodeView: DoPart --
do (#  do (browser.currentForm[],none )->viewSeparate #)  

-- windowsMenuWorkspace: DoPart --
do (&workspace[]).open  

-- windowsMenuSemanticViewerOnselect: DoPart --
do
     (#
        sv: @browser.semanticViewer
          (#
             open:: 
               (# 
               do
                  browser.currentGroup.fg[]->sv.fg[];
                  browser.currentGroup.name[]->sv.fgName[];
                  
               #)
          #)
     do
        (if (browser.currentGroup[] <> none ) and
        (browser.currentGroup.getfg <> none ) then
            sv.open; 
        if)
     #)  

-- windowsMenuSemanticEditorOnselect: DoPart --
do
     (#
        se: @browser.semanticEditor
          (#
             open:: 
               (# 
               do
                  browser.currentGroup.fg[]->fg[];
                  browser.currentGroup.name[]->fgName[];
                  
               #)
          #);
        
     do
        (if (browser.currentGroup[] <> none ) and
        (browser.currentGroup.getfg <> none ) then
            se.open
        if)
     #)  

