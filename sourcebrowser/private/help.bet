ORIGIN '../ymer';

INCLUDE '~beta/basiclib/v1.6/regexp';

INCLUDE '~beta/guienv/v1.6/fields';
INCLUDE '~beta/guienv/v1.6/stddialogs';
INCLUDE '~beta/guienv/v1.6/utils/pane';
INCLUDE '~beta/guienv/v1.6/utils/listview';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- fragmentBrowserLib: attributes ---
helpViewer: ymerLocalWindow
  (# aspect:< integerValue(# do 6->value; INNER #);
     menubarType:: 
       (# fileMenu: @menu
            (# loadItem: @menuItem
                 (# eventHandler:: 
                      (# onSelect::
                           (# f: @file; ext, t: ^text;
                           do fileSelectionDialog
                              (# do 'Load Help File'->title[] #)->t[];
                              (if t[] <> none then
                                  (t[], t[])->registerTheme
                              if)
                           #)
                      #);
                    open:: 
                      (# do 'l'->key;  'Load'->name #)
                 #);
               quitItem: @menuItem
                 (# eventHandler:: 
                      (# onSelect::
                           (# do this(helpViewer).close #)
                      #);
                    open:: 
                      (# do 'w'->key;  'Close'->name #)
                 #);
               open::
                 (#
                 do 'File'->name;
                    loadItem.open; loadItem[]->append;
                    quitItem.open; quitItem[]->append
            #)#);
          themeMenu: @menu
            (# themeItem: menuItem
                 (# theme, path: ^text;
                    eventHandler:: 
                      (# onSelect::
                           (# t: ^text
                           do (path[],theme[])->contents.help.contents.loadFile;
                              'Help: '->t[]; theme[]->t.append; t[]->title;
                           #)
                      #);
                    open::
                      (#
                      enter (theme[], path[])
                      #)
                 #);
               open:: (# do 'Manuals'->name #)
            #);
          open:: 
            (# 
            do fileMenu.open; fileMenu[]->append;
               themeMenu.open; themeMenu[]->append;
            #)
       #);
     registerTheme:
       (# theme, path: ^text;
          mBar: ^menubarType; mi: ^menubarType.themeMenu.themeItem;
       enter (theme[], path[])
       do (if path[]<>NONE then
              (* load HELP text file *)
              theMenubar->mBar[];
              &mBar.themeMenu.themeItem[]->mi[];
              (theme[], path[]->mps.AST.expandToFullPath)
                ->mi.open;
              mi[]->mBar.themeMenu.append; theme[]->mi.name;
          if)
       #);
     contents: @pane
       (# dobind:
            (# 
            do TRUE->bindTop->bindLeft->bindRight->bindBottom;
               headers.dobind;
               help.dobind;
            #);
          fatherFrame::
            (# do ((0,0),this(helpViewer).size)->fr #);
          headers: @labelled
            (# dobind: (# do contents.dobind #);
               zoomed: @boolean;
               eventhandler::
                 (# onMouseDown::
                      (#
                      do (if doubleclick then
                             (if zoomed then
                                 false->zoomed;
                                 this(helpViewer).contents.unzoom
                              else
                                 true->zoomed;
                                 this(labelled)[]
                                   ->this(helpViewer).contents.zoomMember
                             if)
                         if)
                      #)
                 #);
               contentsType:: listView
                 (# current: ^listitems.theCellType;
                    display:
                      (#
                      do INNER;
                         (if current[]<>NONE then
                             current.elm.select;
                             selection.scrollIntoView;
                         if)
                      #);
                    next: display
                      (#
                      do (if current[]=NONE then
                             listitems.head->current[];
                          else
                             current.succ[]->current[];
                             (if current[]=NONE then
                                 listitems.head->current[]
                             if);
                         if)
                      #);
                    prev: display
                      (#
                      do (if current[]=NONE then
                             listitems.last->current[];
                          else
                             current.pred[]->current[];
                             (if current[]=NONE then
                                 listitems.last->current[]
                             if);
                         if)
                      #);
                    first: display
                      (# do listitems.head->current[] #);
                    last: display
                      (# do listitems.last->current[] #);
                    dobind:
                      (# do TRUE->bindTop->bindLeft->bindRight->bindBottom #);
                    eventhandler::
                      (# onKeyDown::
                           (#
                           do (if ch
                               // ascii.cr
                               // ascii.nl then next
                              if)
                           #)
                      #);
                    prevHeader: ^headerItem;
                    registerHeader:
                      (# pos: @integer;
                         title: ^text; level: @integer;
                         newHeader: ^headerItem;
                      enter (pos, title[], level)
                      do (if prevHeader[]<>NONE then
                             pos->prevHeader.limit
                         if);
                         &headerItem[]->newHeader[]->prevHeader[];
                         newHeader.init; pos->newHeader.pos;
                         title.reset;
                         (for i: title.length repeat
                              (if title.get//ascii.cr//ascii.nl then
                                  (' ',i)->title.inxput
                              if)
                         for);
                         (for i: level-1 repeat '  '->title.prepend for);
                         title[]->newHeader.name;
                      #);
                    headerItem: listItem
                      (# headerText: ^text;
                         pos, limit: @integer;
                         onSelect::
                           (#
                           do this(headerItem)[]->listitems.at->current[];
                              (pos,limit)->help.displaySection;
                           #);
                      #);
                 #);
               open:: 
                 (# w,h: @integer;
                 do 'Subjects'->label;
                    this(helpViewer).size->(w,h);
                    (w,(h div aspect)*2)->size;
                 #)
            #);
          help: @labelled
            (# dobind: (# do contents.dobind #);
               zoomed: @boolean;
               eventhandler::
                 (# onMouseDown::
                      (#
                      do (if doubleclick then
                             (if zoomed then
                                 false->zoomed;
                                 this(helpViewer).contents.unzoom
                              else
                                 true->zoomed;
                                 this(labelled)[]
                                   ->this(helpViewer).contents.zoomMember
                             if)
                         if)
                      #)
                 #);
               forwardLabel: @staticText
                 (# width: @integer;
                    eventhandler::
                      (# onFatherFrameChanged::
                           (# w,h: @integer
                           do help.size->(w,h); (w-width-5,0)->position
                           #);
                          onMouseDown::
                           (#
                           do (if doubleclick then
                                  headers.contents.first
                               else headers.contents.next
                              if)
                           #)
                      #);
                    open::
                      (# w,h,y: @integer; p: @point
                      do '>'->label;
                         preferredSize->size->(width,y); help.size->(w,h);
                         true->border.visible; borderStyles.etchedIn->border.style;
                         (w-width-5,0)->position
                      #)
                 #);
               backwardLabel: @staticText
                 (# calcNewPosition:
                      (# w,h: @integer; p: @point
                      do forwardLabel.position->p; forwardLabel.size->(w,h);
                         (w+5,0)->p.subtract; p->position
                      #);
                    eventhandler::
                      (# onFatherFrameChanged::
                           (# do calcNewPosition #);
                         onMouseDown::
                           (#
                           do (if doubleclick then
                                  headers.contents.last
                               else headers.contents.prev
                              if)
                           #)
                      #);
                    open::
                      (# w,h: @integer; p: @point
                      do '<'->label; preferredSize->size;
                         true->border.visible; borderStyles.etchedIn->border.style;
                         calcNewPosition;
                      #)
                 #);
               displaySection:
                 (# start, end: @integer;
                    sec: @styledText
                 enter (start, end)
                 do (start,end)->contents.theText.sub->sec.puttext;
                    true->contents.loading;
                    sec[]->contents.contents.contents;
                    false->contents.loading;
                 #);
               contentsType:: textEditor
                 (# loading: @boolean;
                    dobind:
                      (# do true->bindRight->bindLeft->bindTop->bindBottom #);
                    theText: @StyledText;
                    loadFile:
                      (# path, theme: ^text;
                         textFile: @file;
                         tag, header: @text;
                         txt: ^text;
                         closeTag, validTag, getHeader: @boolean;
                         handleAttributes: @
                           (# 
                           do (* ignore the html tag and attributes *)
                              ll: (if textFile.peek
                                   // '>' then textFile.get;
                                   else textFile.get; restart ll
                                  if);
                           #);
                         handleHeader: @
                           (# level, pos: @integer
                           enter level
                           do textFile.get;
                              (if closeTag then
                                  false->getHeader; theText.length->pos;
                                  header[]->theText.puttext; theText[]->txt[];
                                  (pos,header[],level)
                                    ->headers.contents.registerHeader
                               else
                                  true->getHeader; header.clear;
                                  header[]->txt[]
                              if)
                           #);
                         title: ^text;
                      enter (path[], theme[])
                      do true->loading;
                         theText.clear; theText[]->contents.contents;
                         false->loading;
                         headers.contents.listItems.scanReverse
                         (# do current.delete #);
                         headers.contents.listItems.clear;
                         '---------- '->title[]; theme[]->title.append;
                         ' ----------'->title.append;
                         (1,title[],1)->headers.contents.registerHeader;
                         path[]->textFile.name;
			 (if textFile.entry.exists
			     and textfile.entry.readable then
                             cursors.watch[]->mouse.busycursor;
			     textFile.openRead;
			     textFile.reset;
			     theText[]->txt[];
			     l: (if not textFile.eos then
				    (if textFile.peek
				     // '<' then
					textFile.get;
					(textFile.peek='/')->closeTag;
					(if closeTag then textFile.get if);
					tag.clear; 
					ll: (if textFile.peek
					     // ' '
					     // ascii.nl
					     // ascii.cr
					     // '>' then
					     else
                                                textFile.get->tag.put;
                                                restart ll
					    if);
					(if true
					 // 'H1'->tag.equalNCS then
                                            1->handleHeader
					 // 'H2'->tag.equalNCS then
                                            2->handleHeader
					 // 'H3'->tag.equalNCS then
                                            3->handleHeader
					 // 'H4'->tag.equalNCS then
                                            4->handleHeader
					 // 'H5'->tag.equalNCS then
                                            5->handleHeader
					 // 'H6'->tag.equalNCS then
                                            6->handleHeader
					 // 'hr'->tag.equalNCS then
					    textFile.get; (for 70 repeat '-'->txt.put for)
					 // 'pre'->tag.equalNCS
					 // 'ol'->tag.equalNCS
					 // 'ul'->tag.equalNCS
					 // 'dl'->tag.equalNCS
					 // 'dt'->tag.equalNCS
					 // 'dd'->tag.equalNCS
					 // 'lh'->tag.equalNCS
					 // 'li'->tag.equalNCS
					 // 'p'->tag.equalNCS
					 // 'br'->tag.equalNCS
                      
					 // 'em'->tag.equalNCS
					 // 'b'->tag.equalNCS
					 // 'i'->tag.equalNCS
					 // 'tt'->tag.equalNCS
					 // 'u'->tag.equalNCS
					 // 's'->tag.equalNCS
					 // 'strong'->tag.equalNCS
					 // 'big'->tag.equalNCS
					 // 'small'->tag.equalNCS
					 // 'sub'->tag.equalNCS
					 // 'sup'->tag.equalNCS
					 // 'code'->tag.equalNCS
                      
					 // 'html'->tag.equalNCS
					 // 'head'->tag.equalNCS
					 // 'body'->tag.equalNCS
                      
					 // 'base'->tag.equalNCS
					 // 'isindex'->tag.equalNCS
					 // 'link'->tag.equalNCS
					 // 'title'->tag.equalNCS
                      
					 // 'a'->tag.equalNCS
                      
					 // 'img'->tag.equalNCS
					 // 'fig'->tag.equalNCS
                      
					 // 'table'->tag.equalNCS
					 // 'caption'->tag.equalNCS
					 // 'tr'->tag.equalNCS
					 // 'th'->tag.equalNCS
					 // 'td'->tag.equalNCS
                      
					 // 'math'->tag.equalNCS
					 // 'note'->tag.equalNCS
					 // 'fn'->tag.equalNCS
					 // 'bq'->tag.equalNCS
					 // 'cite'->tag.equalNCS
					 // 'blockquote'->tag.equalNCS
					 // 'address'->tag.equalNCS
					 // 'form'->tag.equalNCS then
					    handleAttributes
					 else
					    '<'->txt.put;
					    (if closeTag then '/'->txt.put if);
					    tag[]->txt.puttext;
					    textFile.get->txt.put;
					if);
					restart l
				     // '&' then
					textFile.get;
					tag.clear; 
					ll: (if textFile.peek=';' then 
                                                textFile.get
					     else
                                                textFile.get->tag.put;
                                                restart ll
					    if);
					(if true
					 // 'oslash'->tag.equalNCS then
                                            'o'->txt.put
					 // 'lt'->tag.equalNCS then
                                            '<'->txt.put
					 // 'gt'->tag.equalNCS then 
                                            '>'->txt.put
					 // 'quot'->tag.equalNCS then
                                            '"'->txt.put
					 // 'amp'->tag.equalNCS then
                                            '&'->txt.put
					 // 'ouml'->tag.equalNCS then
                                            'o'->txt.put
					 else
					    '&'->txt.put; tag[]->txt.puttext;
					    ';'->txt.put;
					if);
					restart l
				     else
					(if not textFile.eos then
					    textFile.get->txt.put;
					    restart l
					if)
				    if)
				if);
			     textFile.close;
                             none->mouse.busycursor;
			  else
			     'No help file for:\n   "'->theText.puttext;
                             theme[]->theText.puttext;
			     '"\n   Cannot read the file:\n      '
                               ->theText.puttext;
			     textFile.name->theText.puttext;
			     theText[]->contents.contents;
			 if);
                         theText.length->headers.contents.prevHeader.limit;
                         (headers.contents.listItems.head).elm.select;
                         INNER
                      #);
                    contentsType::
                      (# eventhandler::
                           (# onBeforeChange:: (# do loading->allow #) #);
                      #);
                 #);
               open::
                 (# w,h: @integer;
                 do 'Help text'->label;
                    this(helpViewer).size->(w,h);
                    (w,(h div aspect)*4)->size;
                    forwardLabel.open; backwardLabel.open;
                 #)
            #);
	  open::
            (# setDefaults::
                 (#
                 do true->verticalStacking;
                    25->minsize;
                    2->panewidth
                 #)
            do help.open; headers.open;
               headers[]->appendMember;
               help[]->appendMember; 
               appendsDone;
               dobind;
            #)
       #);
     open::<
       (#
       do 'Help'->title;
          hide;
          (500,400)->size;
          contents.open;
          INNER;
          show;
       #)
  #)
