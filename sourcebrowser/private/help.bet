ORIGIN '../ymer';

INCLUDE '~beta/guienv/v1.3.1/fields';
INCLUDE '~beta/guienv/v1.3.1/stddialogs';

--- fragmentBrowserLib: attributes ---
helpViewer: ymerLocalWindow
  (# path: ^text;
     loading: @boolean;
     loadFile:
       (# 
       enter path[]
       do (if path[]<>NONE then
              (* load HELP text file *)
              path[]
                ->contents.loadFile;
          if)
       #);
     contents: @textEditor
       (# dobind:
            (# 
            do TRUE->bindTop->bindLeft->bindRight->bindBottom;
            #);
          loadFile:
            (# textFile: @file;
               theText: @StyledText; t: ^text;
            enter path[]
            do true->loading;
               path[]->textFile.name;
               'Mjølner Fragment Browser and Editor, version 0.3'
                 ->theText.putline;
               '================================================'
                 ->theText.putline;
               theText.newline;
               (if textFile.entry.exists
                   and textfile.entry.readable then
                   textFile.openRead;
                   textFile.scan
                   (# while:: (# do true->value #);
                   do ch->theText.put;
                   #);
                   theText[]->contents.contents;
                   textFile.close;
                else
                   'No read permissions to the file:\n     '->t[];
                   textFile.name->t.puttext;
                   (this(helpViewer)[], t[],'Read Error')
                     ->alertUser;
                   'no help file'->theText.puttext;
                   theText[]->contents.contents;
               if);
               false->loading;
               INNER
            #);
          open::
            (# do this(helpViewer).size->size #);
          contentsType::
            (# eventhandler::
                 (# onBeforeChange:: (# do loading->allow #) #);
            #);
       #);
     open::<
       (#
       do 'Short Help'->title;
          (500,300)->size;
          contents.open; contents.dobind;
          INNER;
       #)
  #)
