ORIGIN '../ymer';

INCLUDE '~beta/basiclib/v1.4/regexp';

INCLUDE '~beta/guienv/v1.3.1/fields';
INCLUDE '~beta/guienv/v1.3.1/stddialogs';
INCLUDE '~beta/guienv/v1.3.1/utils/pane';
INCLUDE '~beta/guienv/v1.3.1/utils/listview';

--- fragmentBrowserLib: attributes ---
helpViewer: ymerLocalWindow
  (# menubarType:: 
       (# fileMenu: @menu
            (# quitItem: @menuItem
                 (# eventHandler:: 
                      (# onSelect::
                           (# do this(helpViewer).close #)
                      #);
                    open:: 
                      (# do 'Close'->name #)
                 #);
               open::
                 (#
                 do 'File'->name;
                    quitItem.open; quitItem[]->append
            #)#);
          open:: 
            (# 
            do fileMenu.open; fileMenu[]->append;
            #)
       #);
     registerTheme:
       (# theme, path: ^text
       enter (theme[], path[])
       do (if path[]<>NONE then
              (* load HELP text file *)
              (theme[], (path[],ph.currentDirectory)->ph.convertfilepath)
                ->contents.themes.contents.registerTheme;
          if)
       #);
     contents: @pane
       (# dobind:
            (# 
            do TRUE->bindTop->bindLeft->bindRight->bindBottom;
               themes.dobind;
               headers.dobind;
               help.dobind;
            #);
          themes: @labelled
            (# dobind: (# do contents.dobind #);
               contentsType:: listView
                 (# current: ^listitems.theCellType;
                    dobind:
                      (# do TRUE->bindTop->bindLeft->bindRight->bindBottom #);
                    eventhandler::
                      (# onKeyDown::
                           (#
                           do (if ch
                               // ascii.cr
                               // ascii.nl then
                                  (if current[]=NONE then
                                      listitems.head->current[];
                                   else
                                      current.succ[]->current[];
                                      (if current[]=NONE then
                                          listitems.head->current[]
                                      if);
                                  if);
                                  (if current[]<>NONE then
                                      current.elm.select;
                                      selection.scrollIntoView;
                                  if)
                              if)
                           #)
                      #);
                    registerTheme:
                      (# title, path: ^text; level: @integer;
                         newTheme: ^themeItem;
                      enter (title[], path[])
                      do &themeItem[]->newTheme[];
                         newTheme.init;
                         path[]->newTheme.path[];
                         title[]->newTheme.name;
                      #);
                    themeItem: listItem
                      (# path: ^text;
                         onSelect::
                           (#
                           do this(themeItem)[]->listitems.at->current[];
                              path[]->help.contents.loadFile;
                           #);
                      #);
                 #);
               open:: 
                 (# w,h: @integer;
                 do 'Themes'->label;
                    this(helpViewer).size->(w,h);
                    (w,20)->size;
                 #)
            #);
          headers: @labelled
            (# dobind: (# do contents.dobind #);
               contentsType:: listView
                 (# current: ^listitems.theCellType;
                    dobind:
                      (# do TRUE->bindTop->bindLeft->bindRight->bindBottom #);
                    eventhandler::
                      (# onKeyDown::
                           (#
                           do (if ch
                               // ascii.cr
                               // ascii.nl then
                                  (if current[]=NONE then
                                      listitems.head->current[];
                                   else
                                      current.succ[]->current[];
                                      (if current[]=NONE then
                                          listitems.head->current[]
                                      if);
                                  if);
                                  (if current[]<>NONE then
                                      current.elm.select;
                                      selection.scrollIntoView;
                                  if)
                              if)
                           #)
                      #);
                    registerHeader:
                      (# pos: @integer; title: ^text; level: @integer;
                         newHeader: ^headerItem;
                      enter (pos, title[], level)
                      do &headerItem[]->newHeader[];
                         newHeader.init;
                         pos->newHeader.position;
                         (for i: level-1 repeat '  '->title.prepend for);
                         title[]->newHeader.name;
                      #);
                    headerItem: listItem
                      (# headerText: ^text;
                         position: @integer;
                         onSelect::
                           (#
                           do this(headerItem)[]->listitems.at->current[];
                              position->help.selectLine;
                           #);
                      #);
                 #);
               open:: 
                 (# w,h: @integer;
                 do 'Subjects'->label;
                    this(helpViewer).size->(w,h);
                    (w,30)->size;
                 #)
            #);
          help: @labelled
            (# dobind: (# do contents.dobind #);
               selectLine:
                 (# pos: @integer
                 enter pos
                 do (lgt,lgt)->contents.contents.selection;
                    contents.contents.selection.scrollIntoView;
                    (pos,pos)->contents.contents.selection;
                    contents.contents.selection.scrollIntoView;
                 #);
               lgt: @integer;
               contentsType:: textEditor
                 (# loading: @boolean;
                    dobind:
                      (# do true->bindRight->bindLeft->bindTop->bindBottom #);
                    loadFile:
                      (# path: ^text;
                         textFile: @file;
                         theText: @StyledText; t: ^text; txt: @text;
                      enter path[]
                      do true->loading;
                         path[]->textFile.name;
                         (if textFile.entry.exists
                             and textfile.entry.readable then
                             textFile.openRead;
                             textFile.scan
                             (# while:: (# do true->value #);
                             do ch->txt.put;
                             #);
                             textFile.close;
                             headers.contents.listItems.scanReverse
                             (# do current.delete #);
                             headers.contents.listItems.clear;
                             txt.reset;
                             l: '<[hH]\\([1-9]\\)>\\(.*\\)</[hH][1-9]>'
                               ->txt.regexp_search
                             (# posToMatchEnd:: trueObject;
                                t: ^text; level: @integer
                             do ((1->regs.start)+1,1->regs.end)->txt.sub->t[];
                                t.reset; t.getInt->level;
                                ((2->regs.start)+1,2->regs.end)->txt.sub->t[];
                                ((2->regs.end)+1,(2->regs.end)+5)->txt.delete;
                                ((0->regs.start)+1,(0->regs.start)+4)->txt.delete;
                                ((0->regs.start),t[],level)
                                  ->headers.contents.registerHeader;
                                restart l
                             #);
                             txt.length->lgt;
                             txt[]->theText.puttext;
                             theText[]->contents.contents;
                          else
                             'No read permissions to the file:\n     '->t[];
                             textFile.name->t.puttext;
                             (this(helpViewer)[], t[],'Read Error')
                               ->alertUser;
                             'no help file'->theText.puttext;
                             theText[]->contents.contents;
                         if);
                         false->loading;
                         INNER
                      #);
                    contentsType::
                      (# eventhandler::
                           (# onBeforeChange:: (# do loading->allow #) #);
                      #);
                 #);
               open::
                 (# w,h: @integer;
                 do 'Help text'->label;
                    this(helpViewer).size->(w,h);
                    (w,50)->size;
                 #)
            #);
          open::
            (# setDefaults::
                 (#
                 do true->verticalStacking;
                    50->minsize;
                    2->panewidth
                 #)
            do help.open; headers.open; themes.open;
               themes[]->appendMember;
               headers[]->appendMember;
               help[]->appendMember; 
               appendDone;
               dobind;
            #)
       #);
     open::<
       (#
       do 'Help'->title;
          contents.open;
          (500,400)->size;
          INNER;
       #)
  #)
