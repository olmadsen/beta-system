ORIGIN '../sourcebrowser';
INCLUDE '~beta/objectbrowser/v2.0/UI/obguienvadds';
INCLUDE '~beta/objectbrowser/v2.0/UI/listmoveable';
INCLUDE '~beta/objectbrowser/v2.0/UI/labelled';

INCLUDE '../dynpane';

INCLUDE '~beta/basiclib/v1.4/formatio';
INCLUDE '~beta/basiclib/v1.4/file';

INCLUDE '~beta/editor/v5.0/fragmentscanner';

--- sourcebrowsergroupslib:attributes ---

appendGroup:
  (# g: ^projects.group; index: @Integer; new: ^element; 
  enter (g[],index)
  do &element[]->new[]; (g[],index)->(new.g[],new.index); new[]->append; 
  #);
  

-- groupsGetGroupByIndex: DoPart --
do
   doscan: scan
     (# where::  (#  do (current.index = index)->value #)
     do current.g[]->g[]; leave doscan; 
     #);
     

-- groupGetIndexByGroup: DoPart --
do
   doscan: scan
     (# where::  (#  do (current.g[] = g[])->value #)
     do current.index->index; leave doscan; 
     #);
     

-- sourcebrowserprivate: Descriptor --
(#
   minHorizControlWidth: (#  exit 100 #);
   initialSize: (#  exit (200,150) #);
   outerctrl: @dynpane (* Vertical control containing upctrl and down. *)
     (# eventhandler::
          (# onFatherFrameChanged:: (# do father.size->size #) #);
        dobind:
          (# 
          do
             upctrl.dobind;
             down.dobind;
             
          #);
        open:: 
          (# setDefaults::
               (#
               do TRUE->verticalStacking;
                  50->minsize;
                  2->panewidth;
               #)
          do
             (outerctrl[],false,50,2)->upctrl.open;
             outerctrl[]->down.open;
             upctrl[]->appendMember;
             down[]->appendMember;
             appendsDone;
             doBind;
          #);
        
     #);
   upctrl: @dynpane
   (* Horizontal group containing projectList, groupList and FragmentGroupView *)
     (#
        dobind:
          (# 
          do
             projectList.dobind;
             grouplist.dobind;
             FragmentGroupView.dobind;
          #);
        open:: 
          (# setDefaults::
               (#
               do FALSE->verticalStacking;
                  minHorizControlWidth div 2->minsize;
               #)
          do             
             upctrl[]->projectList.open;
             upctrl[]->grouplist.open;
             upctrl[]->FragmentGroupView.open;
             projectList[]->appendMember;
             grouplist[]->appendMember;
             FragmentGroupView[]->appendMember;
             appendsDone;
             doBind;
          #)
     #);
   projectList: @labelled
     (#
        dobind: (#  do contents.dobindings #);
        shownprojects: @List
          (#
             element::  (# index: @Integer; p: ^projects.project;  #);
             getProjectByIndex:
               (# index: @Integer; p: ^projects.project; 
               enter index
               do
                  doscan: scan
                    (# where::  (#  do (current.index = index)->value #)
                    do current.p[]->p[]; leave doscan; 
                    #);
                  
               exit p[]
               #);
             getIndexByProject:
               (# index: @Integer; p: ^projects.project; 
               enter p[]
               do
                  doscan: scan
                    (# where::  (#  do (current.p[] = p[])->value #)
                    do current.index->index; leave doscan; 
                    #);
                  
               exit index
               #);
             appendProject:
               (# p: ^projects.project; index: @Integer; new: ^element; 
               enter (p[],index)
               do
                  &element[]->new[];
                  (p[],index)->(new.p[],new.index);
                  new[]->append;
                  
               #);
             
          #);
        contentsType:: listmoveableList
          (#
             eventHandler:: 
               (#
                  onMouseUp:: 
                    (# newp: ^projects.project; 
                    do
                       (if not doubleClick then
                           selection.first->getSelectedProject->newp[];
                           (if newp[] <> none then
                               newp[]->THIS(sourcebrowser).setProject
                           if);
                       if);
                       
                    #);
               #);
             getSelectedProject:
               (# value: ^projects.project; 
                  inx: @integer
               enter inx
               do
                  (if inx <> 0 then
                      inx->shownprojects.getProjectByIndex->value[];
                      
                  if);
                  
               exit value[]
               #);
             setProject:
               (# p: ^projects.project; index: @Integer; 
               enter p[]
               do
                  p[]->shownprojects.getIndexByProject->index;
                  (if index <> 0 then
                      (index,FALSE)->selection.select; 
                      selection.scrollIntoView;
                   else
                      selection.clear; 
                  if);
                  
               #);
             scanProjects:
               (# first: ^projects.project; 
               do
                  shownprojects.clear;
                  projects.scanProjects
                    (#
                       index: @Integer;
                       forEach:: 
                         (# 
                         do
                            index+1->index;
                            (if index = 1 then current[]->first[] if);
                            (current[],index)->shownprojects.appendProject;
                            
                         #);
                       
                    #);
                  shownprojects.size->setNumberOfItems;
                  shownprojects.scan
                    (#  do (current.index,current.p.name[])->settext #);
                  projectsLabel->label;
                  
               exit first[]
               #);
             open:: 
               (# first: ^projects.project; 
               do scanProjects; projectlist.contents[]->onProjectListOpen; 
               #);
             
          #);
        open:: 
          (#
             openContents:: 
               (# 
               do (none ,initialSize)->contents.open; TRUE->doneInInner; 
               #);
             
          do
             shownprojects.init;
             projectsLabel->label;
             initialSize->size;
             
          #);
        
     #);
   grouplist: @labelled
     (#
        dobind: (#  do contents.dobindings #);
        clear: (#  do emptyProjectListLabel->label; groups.clear;  #);
        contentsType:: listmoveableList
          (#
             clear:
               (# 
               do
                  groups.clear;
                  0->setNumberOfItems;
                  emptyProjectListLabel->label;
                  
               #);
             setgroup:
               (# g: ^projects.group; index: @Integer; 
               enter g[]
               do
                  (if g[] = none then
                      selection.clear; 
                   else
                      g[]->groups.getIndexByGroup->index;
                      (if index <> 0 then
                          (index,FALSE)->selection.select;
                          selection.scrollIntoView;
                          g[]->fragmentgroupView.contents.setgroup;
                          
                      if);
                      
                  if);
                  
               #);
             eventHandler:: 
               (#
                  onMouseUp:: 
                    (# 
                    do
                       (if (not doubleClick) and (selection.first<>0) then
                           (currentProject[],
                           selection.first->groups.getGroupByIndex)
                             ->THIS(sourcebrowser).setGroup;
                           
                       if);
                    #);
                  
               #);
             open::
               (# 
               do grouplist.contents[]->onGroupListOpen;
               #);
             addGroup:
               (# p: ^projects.project;
                  g: ^projects.group;  index: @Integer;
               enter (p[], g[])
               do groups.clear;
                  p.scanGroups
                  (# foreach::
                       (#
                       do index+1->index;
                          (current[],index)->groups.appendGroup;
                       #)
                  #);
                  index+1->index;
                  (g[],index)->groups.appendGroup;
                  groups.size->setNumberOfItems;
                  groups.scan
                  (# 
                  do (current.index,current.g.name[])->settext;
                  #);
               #);
             scanGroups:
               (# p: ^projects.project; first: ^projects.group; 
               enter p[]
               do
                  groups.clear;
                  p.scanGroups
                    (#
                       index: @Integer;
                       foreach:: 
                         (# 
                         do
                            index+1->index;
                            (current[],index)->groups.appendGroup;
                            (if index = 1 then current[]->first[] if);
                            
                         #)
                    #);
                  groups.size->setNumberOfItems;
                  groups.scan
                    (#  do (current.index,current.g.name[])->settext;  #);
                  p.name[]->label;
                  
               exit first[]
               #);
             
          #);
        open:: 
          (#
             openContents:: 
               (# 
               do (none ,initialSize)->contents.open; TRUE->doneInInner; 
               #);
             
          do
             groups.init;
             emptyProjectListLabel->label;
             initialSize->size;
             
          #);
        
     #);
   fragmentGroupView: @labelled
     (#
        dobind: (#  do contents.dobindings #);
        contentsType:: listmoveableList
          (#
             fgs: @fragmentGroupScanner;
             g: ^projects.group;
             AST: ^astInterface;
             getFragmentFormByIndex: (* added by JLK *)
               (# index: @Integer; ff: ^astInterface.fragmentForm;
               enter index
               do doscan: scan
                    (# lc: @integer
                    do (if (lc+1->lc)=index then
                           (index->gettext,screen[])->(g.getfg).open->ff[];
                           leave doscan;
                       if)
                    #);
               exit ff[]                  
               #);
             getIndexByFragmentForm: (* added by JLK *)
               (# index: @Integer; fg: ^astInterface.fragmentGroup;
                  ff: ^astInterface.fragmentForm;
               enter ff[]
               do (if g[]=NONE then (*try currentGroup*)
                      currentGroup[]->g[]
                  if);
                  (if g[]<>NONE then
                      g.getfg->fg[];
                      (if fg[]<>NONE then
                          (AST[],fg[])->fgs;                        
                          find: fgs.scanPropsAndFragsForText
                            (# myAddText::
                                 (# n: ^text
                                 do (ff.name).copy->n[]; ':'->n.append;n.reset;
                                    (0,n.length)->t.sub->t[];
                                    (if t[]->n.equalNCS then
                                        no->index;
                                        leave find;
                                    if)
                                 #)
                            #)
                      if)
                  if)                   
               exit index  
               #);
             setgroup:
               (# inx,lc: @Integer; fg: ^astInterface.fragmentGroup; 
               enter g[]
               do g.getfg->fg[];
                  (if fg[]<>NONE then
                      (AST[],fg[])->fgs;                        
                      fgs.scanPropsAndFragsForText 
                      (# myAddText:: (# do lc+1->lc #)#);
                      lc->setNumberOfItems;
                      fgs.scanPropsAndFragsForText
                        (#
                           myAddText:: 
                             (#  do inx+1->inx; (inx,t[])->setText #);
                           
                        #);
                      g.name[]->fragmentGroupView.label;
                      selection.clear;
                      INNER ;
                      
                  if);
                  
               #);
             setFragmentForm: (* added by JLK *)
               (# ff: ^astInterface.fragmentForm; index: @Integer;
               enter ff[]
               do (if ff[]=NONE then
                      selection.clear
                   else
                      ff[]->getIndexByFragmentForm->index;
                      (if index<>0 then
                          (index,FALSE)->selection.select;
                          selection.scrollIntoView;
                          ff[]->down.fragmentView.newFragment;
                      if);                      
                  if);
               #);
             clear:
               (# 
               do
                  emptyGroupViewLabel->fragmentGroupView.label;
                  none ->g[];
                  0->setNumberOfItems;
                  
               #);
             eventHandler:: 
               (#
                  onMouseUp:: 
                    (# inx: @Integer; 
                    do
                       (if (selection.first->inx)<>0 then
                           find:
                             (g.getfg,inx)
                             ->fgs.scanPropsAndFrags
                           (#
                              doProperty:: 
                                (# t: ^Text; 
                                do (* ORIGIN,BODY, MDBODY or MAKE *)
                                   (if doubleClick then
                                       (if true
                                        // 'ORIGIN'->prop.equalNCS
                                        // 'BODY'->prop.equalNCS
                                        // 'MDBODY'->prop.equalNCS then
                                           s[]->followLink; 
                                        // 'MAKE'->prop.equalNCS then
                                           s[]->makeFileEdit;
                                       if);
                                       
                                   if);
                                   leave find;
                                   
                                #);
                              doFragmentLink:: 
                                (# fl: ^astInterface.FragmentLink; 
                                do (* INCLUDE *)
                                   (if doubleClick then
                                       fle.open->fl[];
                                       fl.name->followLink;
                                       leave find;
                                       
                                   if);
                                   
                                #);
                              doFragmentForm::
                                (# 
                                do (currentProject[],currentGroup[],
                                   fle.open)->this(sourceBrowser).setFragmentForm;
                                   leave find;
                                #);
                           #);
                       if);   
                    #)
               #);
             followLink:
               (# groupName: ^Text; s,t: ^Text; 
               enter s[]
               do
                  (g.getfg).fullName->AST.stripPathName->t[];
                  (if t[]=NONE then
                      (s.copy,AST.thePathHandler.currentDirectory)
                        ->AST.thePathHandler.convertFilePath
                        ->THIS(sourceBrowser).followLink;
                      
                   else
                      (s.copy,t[])->AST.thePathHandler.convertFilePath
                        ->THIS(sourceBrowser).followLink;
                      
                  if);
                  
               #);
             open:: 
               (# 
               do
                  editorencloser.getAST->AST[];
                  fragmentgroupview.contents[]->onFragmentgroupViewOpen;
                  
               #);
             
          #);
        open:: 
          (#
             openContents:: 
               (# 
               do (none ,initialSize)->contents.open; TRUE->doneInInner; 
               #);
             
          do
             emptyGroupViewLabel->label;
             initialSize->size;
          #);
        
     #);
   down: @Canvas
     (#
        dobind:
          (#  do fragmentView.dobind #);
        open:: 
          (# sz: @Point; 
          do
             initialSize->sz;
             (sz.h*3+4, 2*sz.v)->size;
             (down[],size)->fragmentView.open;
          #);
        fragmentView: @labelled
          (#
             dobind:
               (#  do contents.dobind; TRUE->bindRight->bindBottom;  #);
             lastFragment: ^astInterface.fragmentForm;
             clearing: @Boolean;
             clear:
               (# 
               do
                  TRUE->clearing;
                  contents.contents.all->contents.contents.selection.set;
                  contents.contents.delete;
                  none ->newfragment;
                  none ->lastFragment[];
                  FALSE->clearing;
                 
               #);
             newfragment:
               (# ff: ^astInterface.fragmentForm; 
               enter ff[]
               do
                  (if true
                   // ff[] = none then
                      (* jlk: changed from
                       * (none ,none ,none ,none )->contents.newfragment;
                       *)
                      emptyCodeViewLabel->fragmentView.label;
                   // lastFragment[] = none
                   // not (lastFragment.root[]->ff.root.equal) then
                      ff[]->lastFragment[];
                      
                      (editorencloser.getAST,editorencloser.getBETACFL,ff[],none)
                        ->contents.newfragment;
                      
                  if);
                  
               #);
             contentsType::< codemoveableEditor
               (# dobind: (# do TRUE->bindRight->bindBottom->bindLeft->bindTop #);
                  selectNode::
                    (#
                    do node[]->this(sourceBrowser).selectNode
                    #);
                  open:: 
                    (#  do fragmentView.contents[]->onCodeViewOpen;  #);
                  newfragment:: 
                    (# 
                    do 
                       (if ff[] <> none then
                           ff.name->fragmentView.label; 
                        else
                           emptyCodeViewLabel->fragmentView.label; 
                       if);
                       
                    #);
                  initialContractionLevel::  (#  do 1->value #);
                  charWidthType:: 
                    (# 
                    do
                    (* HACK NEEDED UNTIL textEditor.defaultStyle gets 
                     * implemented !!! *)
                       THIS(fragmentView).private.theLabel.style->ts[]; 
                    #);
                  contentsType:: 
                    (#
                       eventHandler:: 
                         (#
                            onBeforeChange:: 
                              (# 
                              do (if not allow then clearing->allow if)
                              #);
                            
                         #);
                       
                    #);
                  getEncloser::  (#  do editorEncloser[]->theEncloser[] #);
                  
               #);
             open:: 
               (#
                  sz: @Point;
                  openContents:: 
                    (# 
                    do (none ,sz)->contents.open; TRUE->doneInInner; 
                    #);
                  
               enter sz
               do sz->size; none ->lastFragment[]; none ->newfragment; 
               #);
             
          #);
        
     #);
   open:
     (#
     do
        projects.init;
        (THIS(sourcebrowser)[],true,50,2)->outerctrl.open;
        INNER ;
        outerctrl.size->THIS(sourcebrowser).size;
     #);
   
#)  

-- sourcebrowseropen: DoPart --
do sbprivate.open(# do INNER open #)

-- sourcebrowserProjectsChanged: DoPart --
do
   sbprivate.projectlist.contents.scanProjects;
   none ->sbprivate.projectlist.contents.setProject;
   none ->currentProject[]->onCurrentProjectChange;
   sbprivate.grouplist.contents.clear;
   none ->currentGroup[]->onCurrentGroupChange;
   sbprivate.down.fragmentView.clear;
   none ->currentForm[]->onCurrentFormChange;
     

-- sourcebrowserSetProject: DoPart --
do
   cursors.watch[]->mouse.busycursor;
   (if p[] <> currentProject[] then p[]->onCurrentProjectChange;  if);
   p[]->sbprivate.projectlist.contents.setProject;
   (if true
    // p[] = none then
       sbprivate.grouplist.contents.clear; 
    // p[] <> currentProject[] then
       p[]->sbprivate.grouplist.contents.scanGroups; 
   if);
   p[]->currentProject[];
   (*NONE->currentGroup[];*)
   (*NONE->currentForm[];*)
   INNER ;
   (if not setGroupDoneInInner then
       none ->onCurrentGroupChange;
       none ->currentGroup[];
       none ->sbprivate.grouplist.contents.setgroup;
       sbprivate.fragmentGroupView.contents.clear;
       
   if);
   (if not setFormDoneInInner then
       none ->onCurrentFormChange;
       none ->currentForm[];
       sbprivate.down.fragmentView.clear;
       
   if);
   none ->mouse.busycursor;
     

-- sourcebrowserSetGroup: DoPart --
do
   TRUE->setGroupDoneInInner;
   (if g[] <> currentGroup[] then
       g[]->onCurrentGroupChange;
       g[]->currentGroup[];
       g[]->sbprivate.grouplist.contents.setgroup;
       
   if);
   INNER ;
     

-- sourcebrowserSetFragmentForm: DoPart --
do
   TRUE->setFormDoneInInner;
   (if ff[] <> currentForm[] then
       ff[]->oncurrentFormChange;
       (*JLK: changed from ff[]->sbprivate.down.fragmentView.newfragment;*)
       ff[]->sbprivate.fragmentGroupView.contents.setfragmentForm;
       ff[]->currentForm[];
       
   if);
     

-- sourcebrowserGetFragmentFormEditor: DoPart --
do sbprivate.down.fragmentView.contents[]->theEditor[]  

