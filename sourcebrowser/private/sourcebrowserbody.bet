ORIGIN '../sourcebrowser';
INCLUDE '~beta/objectbrowser/v2.2/UI/obguienvadds'
	'~beta/objectbrowser/v2.2/UI/listmoveable'
	'~beta/objectbrowser/v2.2/UI/labelled'
	'~beta/guienv/v1.6/utils/pane'
	'~beta/basiclib/v1.6/formatio'
	'~beta/basiclib/v1.6/file'
	'~beta/betaast/v5.2/donecheck'
	'~beta/mps/v5.2/fragmentscanner'
	'~beta/dependency/v1.3/private/xmisk' (* only to get hold of expandMacror2 *)
	'fileUtils';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- sourcebrowsergroupslib: Attributes --
appendGroup:
  (# g: ^projects.group; index: @Integer; new: ^element;
  enter (g[],index)
  do &element[]->new[]; (g[],index)->(new.g[],new.index); new[]->append;
  #);


-- groupsGetGroupByIndex: DoPart --
do
   doscan: scan
     (# where::  (#  do (current.index = index)->value #)
     do current.g[]->g[]; leave doscan
     #)

-- groupGetIndexByGroup: DoPart --
do
   doscan: scan
     (# where::  (#  do (current.g[] = g[])->value #)
     do current.index->index; leave doscan
     #)

-- sourcebrowserprivate: Descriptor --
(#
   initialProjectListViewWidth: (#  exit 146 #);
   initialGroupListViewWidth: (#  exit 146 #);
   initialFormListViewWidth: (#  exit 212 #);
   initialListViewHeight: (#  exit 150 #);
   initialWindowWidth:
     (#
     exit initialProjectListViewWidth+initialGroupListViewWidth+
     initialFormListViewWidth+2*stdPaneWidth
     #);
   stdMinSize: (#  exit 50 #);
   stdPaneWidth: (#  exit 2 #);
   outerctrl: @pane
   (* Vertical pane containing upctrl, down and locationView. *)
     (#
	eventhandler::
	  (# onFatherFrameChanged::  (#  do father.size->size #) #);
	dobind: (#  do upctrl.dobind; down.dobind;  #);
	open::
	  (#
	     setDefaults::
	       (#
	       do
		  TRUE->verticalStacking;
		  stdMinSize->minsize;
		  stdPaneWidth->panewidth
	       #)
	  do
	     (outerctrl[],false,50,2)->upctrl.open;
	     outerctrl[]->down.open;
	     (outerctrl[],(initialWindowWidth,45))->locationView.open;
	     infoView.open;
	     upctrl[]->appendMember;
	     upctrl[]->fixedSize;
	     down[]->appendMember;
	     locationView[]->appendMember;
	     locationView[]->fixedSize;
	     appendsDone;
	     dobind
	  #)
     #);
   upctrl: @pane
   (* Horizontal pane containing projectList, groupList and fragmentGroupView *)
     (#
	fatherFrame::  (#  do upctrl.frame->fr #);
	dobind:
	  (#
	  do projectList.dobind; grouplist.dobind; fragmentGroupView.dobind
	  #);
	open::
	  (#
	     setDefaults::
	       (#
	       do
		  FALSE->verticalStacking;
		  stdMinSize->minsize;
		  stdPaneWidth->panewidth
	       #)
	  do
	     upctrl[]->projectList.open;
	     upctrl[]->grouplist.open;
	     upctrl[]->fragmentGroupView.open;
	     projectList[]->appendMember;
	     grouplist[]->appendMember;
	     fragmentGroupView[]->appendMember;
	     appendsDone;
	     dobind
	  #)
     #);
   projectList: @labelled (*newgroup*)
     (#
	zoomed: @boolean;
	eventhandler::
	  (#
	     onMouseDown::
	       (#
	       do
		  (if doubleclick then
		      (if zoomed then
			  false->zoomed; upctrl.unzoom
		       else
			  true->zoomed; projectList[]->upctrl.zoomMember
		      if)
		  if)
	       #)
	  #);
	dobind: (#  do contents.dobindings #);
	shownprojects: @List
	  (#
	     element::  (# index: @Integer; p: ^projects.project #);
	     getProjectByIndex:
	       (# index: @Integer; p: ^projects.project
	       enter index
	       do
		  doscan: scan
		    (# where::  (#  do (current.index = index)->value #)
		    do current.p[]->p[]; leave doscan
		    #)
	       exit p[]
	       #);
	     getIndexByProject:
	       (# index: @Integer; p: ^projects.project
	       enter p[]
	       do
		  doscan: scan
		    (# where::  (#  do (current.p[] = p[])->value #)
		    do current.index->index; leave doscan
		    #)
	       exit index
	       #);
	     appendProject:
	       (# p: ^projects.project; index: @Integer; new: ^element
	       enter (p[],index)
	       do
		  &element[]->new[];
		  (p[],index)->(new.p[],new.index);
		  new[]->append
	       #)
	  #);
	contentsType:: listmoveableList
	  (#
	     eventHandler::
	       (#
		  onMouseUp::
		    (# newp: ^projects.project;
		    do
		       (if not doubleClick then
			   selection.first->getSelectedProject->newp[];
			   (if newp[] <> none then
			       newp[]->THIS(sourcebrowser).setProject
			   if)
		       if)
		    #)
	       #);
	     getSelectedProject:
	       (# value: ^projects.project; inx: @integer
	       enter inx
	       do
		  (if inx <> 0 then
		      inx->shownprojects.getProjectByIndex->value[]
		  if)
	       exit value[]
	       #);
	     setProject:
	       (# p: ^projects.project; index: @Integer;
	       enter p[]
	       do
		  p[]->shownprojects.getIndexByProject->index;
		  (if index <> 0 then
		      (index,FALSE)->selection.select; selection.scrollIntoView
		   else
		      selection.clear
		  if)
	       #);
	     scanProjects:
	       (# first: ^projects.project;
	       do
		  shownprojects.clear;
		  projects.scanProjects
		    (#
		       index: @Integer;
		       forEach::
			 (#
			 do
			    index+1->index;
			    (if index = 1 then current[]->first[] if);
			    (current[],index)->shownprojects.appendProject
			 #)
		    #);
		  shownprojects.size->setNumberOfItems;
		  shownprojects.scan
		    (#  do (current.index,current.p.name[])->settext #);
		  projectsLabel->label
	       exit first[]
	       #);
	     open::
	       (# first: ^projects.project;
	       do scanProjects; projectlist.contents[]->onProjectListOpen
	       #);

	  #);
	open::
	  (#
	     openContents::
	       (#
	       do
		  (none ,(initialProjectListViewWidth,initialListViewHeight))
		    ->contents.open;
		  TRUE->doneInInner
	       #)
	  do
	     shownprojects.init;
	     projectsLabel->label;
	     (initialProjectListViewWidth,initialListViewHeight)->size
	  #)
     #);
   grouplist: @labelled
     (#
	zoomed: @boolean;
	eventhandler::
	  (#
	     onMouseDown::
	       (#
	       do
		  (if doubleclick then
		      (if zoomed then
			  false->zoomed; upctrl.unzoom
		       else
			  true->zoomed; grouplist[]->upctrl.zoomMember
		      if)
		  if)
	       #)
	  #);
	dobind: (#  do contents.dobindings #);
	clear: (#  do (*emptyProjectListLabel->label;*) groups.clear #);
	contentsType:: listmoveableList
	  (#
	     clear:
	       (#
	       do
		  groups.clear;
		  0->setNumberOfItems;
		  (*emptyProjectListLabel->label*)

	       #);
	     setgroup:
	       (# g: ^projects.group; index: @Integer;
	       enter g[]
	       do
		  sbprivate.fragmentGroupView.contents.clear;
		  (if g[] = none then
		      selection.clear
		   else
		      g[]->groups.getIndexByGroup->index;
		      (if index <> 0 then
			  (index,FALSE)->selection.select;
			  selection.scrollIntoView;
			  g[]->fragmentgroupView.contents.setgroup
		      if)
		  if)
	       #);
	     eventHandler::
	       (#
		  onMouseUp::
		    (# selectedGroup: ^projects.group
		    do
		       (if (not doubleClick) and (selection.first <> 0) then
			   selection.first->groups.getGroupByIndex
			     ->selectedGroup[];
			   (if selectedGroup.txtFile then
			       selectedGroup.location[]->down.newAsciiEditor
			    else
			       (currentProject[],
				selection.first->groups.getGroupByIndex)
				 ->THIS(sourcebrowser).setGroup
			   if)
		       if)
		    #)
	       #);
	     open::  (#  do grouplist.contents[]->onGroupListOpen #);
	     addGroup:
	       (#
		  p: ^projects.project; g: ^projects.group; index: @Integer;
	       enter (p[],g[])
	       do
		  groups.clear;
		  p.scanGroups
		    (#
		       foreach::
			 (#
			 do
			    index+1->index;
			    (current[],index)->groups.appendGroup
			 #)
		    #);
		  index+1->index;
		  (g[],index)->groups.appendGroup;
		  groups.size->setNumberOfItems;
		  groups.scan
		    (#  do (current.index,current.g.name[])->settext #)
	       #);
	     scanGroups:
	       (# p: ^projects.project; first: ^projects.group
	       enter p[]
	       do
		  groups.clear;
		  p.scanGroups
		    (#
		       index: @Integer;
		       foreach::
			 (#
			 do
			    index+1->index;
			    (current[],index)->groups.appendGroup;
			    (if index = 1 then current[]->first[] if)
			 #)
		    #);
		  groups.size->setNumberOfItems;
		  groups.scan
		    (#  do (current.index,current.g.name[])->settext #);
		  (*p.name[]->label*)

	       exit first[]
	       #)
	  #);
	open::
	  (#
	     openContents::
	       (#
	       do
		  (none ,(initialGroupListViewWidth,initialListViewHeight))
		    ->contents.open;
		  TRUE->doneInInner
	       #)
	  do
	     groups.init;
	     emptyProjectListLabel->label;
	     (initialGroupListViewWidth,initialListViewHeight)->size
	  #)
     #);
   fragmentGroupView: @labelled
     (#
	zoomed: @boolean;
	eventhandler::
	  (#
	     onMouseDown::
	       (#
	       do
		  (if doubleclick then
		      (if zoomed then
			  false->zoomed; upctrl.unzoom
		       else
			  true->zoomed; fragmentGroupView[]->upctrl.zoomMember
		      if)
		  if)
	       #)
	  #);
	dobind: (#  do contents.dobindings #);
	contentsType:: listmoveableList
	  (#
	     g: ^projects.group;
	     AST: ^astInterface;
	     getFragmentFormByIndex: (* added by JLK *)
	       (#
		  index: @Integer;
		  fg: ^astInterface.fragmentGroup;
		  ff: ^astInterface.fragmentForm
	       enter index
	       do
		  doscan: scan
		    (# lc: @integer
		    do
		       (if (lc+1->lc) = index then
			   g.getfg->fg[];
			   (if fg[] <> none then
			       (index->gettext,screen[])->fg.open->ff[]
			   if);
			   leave doscan
		       if)
		    #)
	       exit ff[]
	       #);
	     getIndexByFragmentForm: (* added by JLK *)
	       (#
		  index: @Integer;
		  fg: ^astInterface.fragmentGroup;
		  ff: ^astInterface.fragmentForm
	       enter ff[]
	       do
		  (if g[] = none then (*try currentGroup*)
		      currentGroup[]->g[]
		  if);
		  (if g[] <> none then
		      g.getfg->fg[];
		      (if fg[] <> none then
			  find: fg.scanPropsAndFragsForText
			    (#
			       displayText::
				 (# n: ^text
				 do
				    (ff.name).copy->n[];
				    ':'->n.append;
				    n.reset;
				    (0,n.length)->t.sub->t[];
				    (if t[]->n.equalNCS then
					inx->index; leave find
				    if)
				 #)
			    #)
		      if)
		  if)
	       exit index
	       #);
	     setgroup:
	       (#
		  ng: ^projects.group;
		  no,lc: @Integer;
		  fg: ^astInterface.fragmentGroup
	       enter ng[]
	       do
		  ng[]->g[];
		  ng.getfg->fg[];
		  (if fg[] <> none then
		      fg.scanPropsAndFragsForText
			(# displayText::  (#  do lc+1->lc #) #);
		      lc->setNumberOfItems;
		      fg.scanPropsAndFragsForText
			(#
			   displayText::
			     (#  do no+1->no; (no,t[])->setText #)
			#);
		      (*ng.name[]->fragmentGroupView.label;*)
		      selection.clear;
		      INNER
		  if)
	       #);
	     setFragmentForm: (* added by JLK *)
	       (# ff: ^astInterface.fragmentForm; index: @Integer;
	       enter ff[]
	       do
		  (if ff[] = none then
		      selection.clear
		   else
		      ff[]->getIndexByFragmentForm->index;
		      (if index <> 0 then
			  (index,FALSE)->selection.select;
			  selection.scrollIntoView
		      if)
		  if)
	       #);
	     clear:
	       (#
	       do (*emptyGroupViewLabel->fragmentGroupView.label;*)
		  none ->g[]; 0->setNumberOfItems
	       #);
	     scrollToFirstFF:
	       (#
	       do
		  find:
		  (if (g[] <> none ) and (g.fg[] <> none ) then
		      g.fg.scanPropsAndFrags
			  (#
			     doFragmentForm::
			       (#
			       do
				  (inx,FALSE)->selection.select;
				  selection.scrollIntoView;
				  inx->selection.deselect;
				  leave find
			       #)
			  #)
		  if)
	       #);
	     doSelect:
	       (# no: @integer; fg: ^astInterface.fragmentGroup
	       enter no
	       do
		  (if no <> 0 then
		      g.getfg->fg[];
		      find:
			no
			->fg.findPropsAndFrags
			  (#
			     doProperty::
			       (# t: ^Text;
			       do (* ORIGIN,BODY, MDBODY or MAKE *)
				  sbprivate.down.popdown;
				  none ->currentForm[];
				  none ->onCurrentFormChange;
				  (no,false)->selection.select;
				  selection.scrollIntoView;
				  true->propertySelected;
				  leave find
			       #);
			     doFragmentLink::
			       (#
			       do (* INCLUDE *)
				  (no,false)->selection.select;
				  sbprivate.down.popdown;
				  none ->currentForm[];
				  none ->onCurrentFormChange;
				  selection.scrollIntoView;
				  true->propertySelected;
				  leave find
			       #);
			     doFragmentForm::
			       (#
			       do
				  (currentProject[],currentGroup[],fle.open)
				    ->THIS(sourceBrowser).setFragmentForm;
				  leave find
			       #)
			  #)
		  if)
	       #);
	     eventHandler::
	       (#
		  onMouseUp::
		    (# no: @Integer; fg: ^astInterface.fragmentGroup
		    do
		       (if not doubleClick and not shiftKey and
		       ((selection.first->no) <> 0) then
			   true->propertySelected;
			   g.getfg->fg[];
			   find:
			     no
			     ->fg.findPropsAndFrags
			     (#
				  doProperty::
				    (#
				       t: ^text;
				       f: @file;
				       view: ^sbprivate.down.asciiEditorView;
;
				       mp: @astinterface;
				       dg: @mp.dependencyGraph;
				    do
				       (if true
					// 'MAKE'->prop.equalNCS then
					   currentGroup.location[]->f.name;
					   f.entry.path.head->t[];
					   ast.thepathhandler.directoryChar
					     ->t.put;
					   s[]->t.append;
					   t[]->sbprivate.down.newAsciiEditor
					// 'BUILD'->prop.equalNCS then
					   (if selector.lgth=0 then
					       (* the following three lines
						* (and the mp and dg instances
						* above) is _only_ here to
						* initialize mg and dg
						* properly, such that
						* expandMacro2 works properly,
						* i.e. substitute '$$' with
						* the proper directory name.
						*)
					       mp;dg.init;
					       machine_type
						 ->dg.TargetMachine[];
					       machine_type
						 ->dg.TargetDirectory[];
					       currentGroup.location[]->f.name;
					       f.entry.path.head->t[];
					       ast.thepathhandler.directoryChar
						 ->t.put;
					       s[]
						 ->t.append
						 ->dg.expandMacro2 (* to expand '$$' *)
						 ->sbprivate.down.newAsciiEditor
					   if)
				       if);
				       leave find
				    #);
				  doFragmentForm::
				    (#
				    do (currentProject[],currentGroup[],
					fle.open)
					 ->THIS(sourceBrowser).setFragmentForm;
				       leave find
				    #)
			       #)
		       if)
		    #);
		  onMouseDown::
		    (# no: @Integer; fg: ^astInterface.fragmentGroup
		    do
		       (if doubleClick and not shiftKey and
		       ((selection.first->no) <> 0) then
			   g.getfg->fg[];
			   find:
			   no
			     ->fg.findPropsAndFrags
			       (#
				  doProperty::
				    (# t: ^Text; f: @file;
				    do (* ORIGIN,BODY, MDBODY or MAKE *)
				       (if true
					// 'ORIGIN'->prop.equalNCS
					// 'BODY'->prop.equalNCS
					// 'MDBODY'->prop.equalNCS then
					   s[]->followLink;
				       if);
				       leave find
				    #);
				  doFragmentLink::
				    (#
				    do (* INCLUDE *)
				       include.fullname->followLink; leave find
				    #)
			       #)
		       if)
		    #)
	       #);
	     followLink:
	       (#
		  groupName: ^Text;
		  s,t: ^Text;
		  fg: ^astInterface.fragmentGroup
	       enter s[]
	       do
		  g.getfg->fg[];
		  (if fg[] <> none then
		      fg.fullName->AST.stripPathName->t[];
		      (if t[] = none then
			  s.copy
			  (*->AST.expandtofullpath ESS: not necessary any more *)
			  ->THIS(sourceBrowser).followLink
		       else
			  (s.copy,t[])->AST.thePathHandler.convertFilePath
			    ->THIS(sourceBrowser).followLink
		      if)
		  if)
	       #);
	     open::
	       (#
	       do
		  editorencloser.getAST->AST[];
		  fragmentgroupview.contents[]->onFragmentgroupViewOpen
	       #)
	  #);
	open::
	  (#
	     openContents::
	       (#
	       do
		  (none ,(initialFormListViewWidth,initialListViewHeight))
		    ->contents.open;
		  TRUE->doneInInner
	       #)
	  do
	     emptyGroupViewLabel->label;
	     (initialFormListViewWidth,initialListViewHeight)->size
	  #)
     #);
   down: @Canvas
     (#
	aeList: @list (* ascii editors active in this browser *)
	  (#
	     element::  (# ae: ^asciiEditor #);
	     findOrCreateAsciiEditor:
	       (# ae: ^asciiEditor; path: ^text; view: ^asciiEditorView
	       enter path[]
	       do
		  state.busy;
		  'Opening '->infoView.settext;
		  path[]->infoView.append;
		  '... '->infoView.append;
		  infoView.msgPush;
		  l: find
		    (#
		       predicate::
			 (#  do current.ae.path->path.equalNCS->value #);
		       notFound::
			 (# newElm: ^element
			 do
			    &asciiEditorView[]->view[];
			    (down[],down.size)->view.open;
			    path.copy->view.label;
			    path.copy->view.path[];
			    (if not view.ae.reload then leave l if);
			    view.ae[]->ae[];
			    &element[]->newElm[];
			    view.ae[]->ae[]->newElm.ae[];
			    newElm[]->append
			 #)
		    do current.ae[]->ae[]
		    #);
		  infoView.msgPop;
		  state.normal;

	       exit ae[]
	       #);
	     closeAsciiEditor:
	       (# path: ^text; okToClose: @boolean;
	       enter path[]
	       do
		  locate
		    (#
		       predicate::
			 (#  do current.elm.ae.path->path.equalNCS->value #)
		    do
		       (if position.elm.ae.close->okToClose then
			   position[]->delete
		       if)
		    #)
	       exit okToClose
	       #);
	     closeAsciiEditors:
	       (# okToClose: @boolean
	       do
		  true->okToClose;
		  l: iterate
		    (#
		    do
		       (if current.elm.ae.close->okToClose then
			   current[]->delete
			else
			   leave l
		       if)
		    #)
	       exit okToClose
	       #)
	  #);
	dobind: (#  do (*currentView.dobind*)  #);
	open::
	  (#
	  do
	     (down[],down.size)->defaultView.open;
	     defaultView.show;
	     defaultView[]->currentView[]
	  #);
	newfragment:
	  (#
	     ff: ^astInterface.fragmentForm;
	     view: ^fragmentViewType;
	     cfe: ^codeeditor
	  enter ff[]
	  do
	     &fragmentViewType[]->view[];
	     (down[],down.size)->view.open;
	     (ff[],ff.root[],none ,view.contents[],cge[])
	       ->edenv.findOrCreateFormEditor->cfe[];
	     ff.name->view.label
	  exit (cfe[],view[])
	  #);
	newAsciiEditor:
	  (# p: ^text
	  enter p[]
	  do p[]->aeList.findOrCreateAsciiEditor->cte[];
	     (if cte[]<>NONE then cte.show; none ->cfe[] if)
	  #);
	popdown: (#  do defaultView[]->popup #);
	popup:
	  (# view: ^overlayType
	  enter view[]
	  do
	     (if view[] <> none then view.bringToFront; view.show;  if);
	     (if (currentView[] <> none ) and (currentView[] <> view[]) then
		 currentView.hide
	     if);
	     view[]->currentView[];

	  #);
	overlayType: (* canvas*) labelled
	  (# (*contents: @contentsType*)
	     ;
	     (*contentsType:< windowItem*)
	     ;
	     zoomed: @boolean;
	     eventhandler::<
	       (#
		  onMouseDown::
		    (#
		    do
		       (if doubleclick then
			   (if zoomed then
			       false->zoomed; outerctrl.unzoom
			    else
			       true->zoomed; down[]->outerctrl.zoomMember
			   if)
		       if)
		    #);
		  onFatherFrameChanged::
		    (#
		    do
		       (if visible then
			   down.size->size (*->contents.size*)
		       if)
		    #);
		  onVisibleChanged::
		    (#
		    do
		       (if visible then
			   down.size->size (*->contents.size*)
		       if)
		    #);

	       #);
	     open::<
	       (#
		  sz: @point;
		  (*openContents:<
		   *  (# doneInINNER: @boolean
		   *  do INNER;
		   *     (if not doneInINNER then
		   *         this(overlayType)[]->contents.open;
		   *         true->doneInINNER
		   *     if)
		   *   #)
		   *)

	       enter sz
	       do hide; sz->size; (*openContents;*) INNER
	       #)
	  #);
	textView: overlayType
	  (#
	     changed,allowEdit,loading: @boolean;
	     contentsType::< textEditor
	       (#
		  contentsType::<
		    (#
		       eventhandler::<
			 (#
			    onTextChanged::
			      (#
			      do not loading->changed->locationView.changed
			      #);
			    onBeforeChange::
			      (#  do (loading or allowEdit)->allow #)
			 #)
		    #)
	       #)
	  #);
	asciiEditorView: textView
	  (#
	     path,name: ^text;
	     loadStream:
	       (# strm: ^stream; theText: @styledText;
	       enter strm[]
	       do
		  strm.reset;
		  strm.scan
		    (# while::  (#  do true->value #);
		    do ch->theText.put;
		    #);
		  true->loading;
		  theText[]->contents.contents.contents;
		  false->loading;

	       #);
	     saveStream:
	       (# strm: ^stream; theText: ^styledText;
	       enter strm[]
	       do
		  strm.reset;
		  contents.contents.contents->theText[];
		  theText[]->strm.puttext
	       #);
	     ae: @asciiEditor
	       (#
		  allowEdit::
		    (#  do THIS(asciiEditorView).allowEdit->value #);
		  toggleAllowEdit::
		    (#
		    do
		       not THIS(asciiEditorView).allowEdit
			 ->THIS(asciiEditorView).allowEdit;
		       not THIS(asciiEditorView).allowEdit->locationView.locked;

		    #);
		  changed::  (#  do THIS(asciiEditorView).changed->value #);
		  cutStatus::
		    (#
		    do
		       (contents.contents.selection.start <>
			contents.contents.selection.end)->value
		    #);
		  doCut::  (#  do contents.contents.cut #);
		  copyStatus::
		    (#
		    do
		       (contents.contents.selection.start <>
			contents.contents.selection.end)->value
		    #);
		  doCopy::  (#  do contents.contents.copy #);
		  pasteStatus::
		    (#  do (*clipboard.hasText*) true->value #);
		  doPaste::  (#  do contents.contents.paste #);
		  doClear::  (#  do contents.contents.clear #);
		  save::
		    (#
		    do
		       l:
			 (#
			    t: ^text;
			    f: @file
			      (#
				 cleanUp:
				   (# msg: ^text
				   enter msg[]
				   do
				      (none ,msg[],'Warning')->alertUser;
				      leave l
				   #);
				 accessError::  (#  do msg[]->cleanup #);
				 writeError::  (#  do msg[]->cleanup #);
				 readError::  (#  do msg[]->cleanup #);
				 EOSerror::  (#  do msg[]->cleanup #);
				 noSuchFileError::
				   (#  do msg[]->cleanup #);
				 fileExistsError::
				   (#  do msg[]->cleanup #);
				 noSpaceError::  (#  do msg[]->cleanup #);
				 otherError::  (#  do msg[]->cleanup #);

			      #);
			 do
			    path
			      ->fragmentGroupView.contents.AST.expandToFullPath
			      ->f.name;
			    (if f.name->fileExists then
				(if f.name->fileWriteable then
				    f.openWrite;
				    f[]->THIS(asciiEditorView).saveStream;
				    f.close
				 else
				    'File exception for '''->t[];
				    f.name->t.append;
				    t.newline;
				    'File is not writeable (Insufficient access privilegies)'
				      ->t.append;
				    (none ,t[],'Warning')->alertUser;
				    leave l
				if)
			     else
				'File exception for '''->t[];
				f.name->t.append;
				t.newline;
				'File does not exist'->t.append;
				(none ,t[],'Warning')->alertUser;
				leave l
			    if);
			    false->locationView.changed;

			 #)
		    #);
		  saveAs::  (#  do  #);
		  reload::
		    (# t: ^text; success: @boolean
		    do
		       l:
			 (#
			    f: @file
			      (#
				 cleanUp:
				   (# msg: ^text
				   enter msg[]
				   do
				      (none ,msg[],'Warning')->alertUser;
				      leave l
				   #);
				 accessError::  (#  do msg[]->cleanup #);
				 writeError::  (#  do msg[]->cleanup #);
				 readError::  (#  do msg[]->cleanup #);
				 EOSerror::  (#  do msg[]->cleanup #);
				 noSuchFileError::
				   (#  do msg[]->cleanup #);
				 fileExistsError::
				   (#  do msg[]->cleanup #);
				 noSpaceError::  (#  do msg[]->cleanup #);
				 otherError::  (#  do msg[]->cleanup #);

			      #)
			 do
			    false->success;
			    path
			      ->fragmentGroupView.contents.AST.expandToFullPath
			      ->f.name;
			    (if f.name->fileExists then
				(if f.name->fileReadable then
				    f.entry.writeable
				      ->THIS(asciiEditorView).allowEdit;
				    ''->t[];
				    t[]->loadStream;
				    f.openRead;
				    f[]->loadStream;
				    f.close;

				 else
				    'File exception for '''->t[];
				    f.name->t.append;
				    t.newline;
				    'File is not readable (Insufficient access privilegies)'
				      ->t.append;
				    (none ,t[],'Warning')->alertUser;
				    leave l
				if)
			     else
				'File exception for '''->t[];
				f.name->t.append;
				t.newline;
				'File does not exist'->t.append;
				(none ,t[],'Warning')->alertUser;
				leave l
			    if);
			    false->locationView.changed;
			    true->success
			 #)
		    exit success
		    #);
		  search::  (#  do  #);
		  replace::  (#  do  #);
		  show::
		    (#
		    do
		       THIS(asciiEditorView)[]->popup;
		    #);
		  close::
		    (# t: ^text
		    do
		       (if changed then
			   'Text file: '->t[];
			   path->t.putline;
			   'has been changed.'->t.putline;
			   'Save?'->t.puttext;
			   (none ,'Warning',t[])
			     ->promptForBoolean
			       (#
				  ok::  (#  do true->value; save #);
				  notok::  (#  do true->value #);
				  cancel::  (#  do false->value #)
			       #)
			else
			   true->value
		       if)
		    #);
		  path::  (#  do THIS(asciiEditorView).path.copy->path[] #);

	       #)
	  #);
	currentView: ^overlayType;
	defaultView: @textView
	  (# open::  (#  do emptyCodeViewLabel->label #) #);
	ffViewType: codemoveableEditor
	  (#
	     isolated:: trueObject;
	     dobind:
	       (#  do TRUE->bindRight->bindBottom->bindLeft->bindTop #);
	     selectNode::
	       (#
	       do
		  (theEditorRoot[],node[],separate)
		    ->THIS(sourceBrowser).selectNode
	       #);
	     open::  (#  do THIS(ffViewType)[]->onCodeViewOpen #);
	     newfragment::
	       (#
	       do
		  (if editorRoot[] <> none then
		      (if editorRoot.frag[] <> none then
		      (*editorRoot.frag.name->currentView.label*)

		       else
		      (*emptyCodeViewLabel->currentView.label*)

		      if)
		   else
		  (*emptyCodeViewLabel->currentView.label*)

		  if)
	       #);
	     initialContractionLevel::  (#  do 2->value #);
	     charWidthType::
	       (#
	       do
	       (* HACK NEEDED UNTIL textEditor.defaultStyle gets
		* implemented !!! *)
		  fragmentGroupView.private.theLabel.style->ts[]
	       #);
	     getEncloser::  (#  do editorEncloser[]->theEncloser[] #)
	  #);
	fragmentViewType: overlayType
	  (#
	     dobind:
	       (#  do contents.dobind; TRUE->bindRight->bindBottom #);
	     contentsType:: ffViewType;
	     open::
	       (#
		  openContents::
		    (#
		    do
		       (THIS(fragmentViewType)[],sz)->contents.open;
		       TRUE->doneInInner
		    #)
	       #)
	  #);

     #);
   open:
     (#
     do
	projects.init;
	(THIS(sourcebrowser)[],true,50,2)->outerctrl.open;
	INNER open;
	outerctrl.size->THIS(sourcebrowser).size
     #)
#)

-- sourcebrowserLocationViewMouseDown: DoPart --
do
   (if doubleclick then
       (if private.zoomed then
	   false->private.zoomed;
	   contents.show;
	   private.log.hide;
	   sbprivate.outerctrl.unzoom
	else
	   true->private.zoomed;
	   locationView[]->sbprivate.outerctrl.zoomMember;
	   contents.frame->private.log.frame;
	   private.log.show;
	   contents.hide;

       if)
   if)

-- sourcebrowserLocationViewDisplay: DoPart --
do
   m[]->contents.label;
   '\n'->((7,m.length)->m.sub).append->private.log.append;
   true->locationView.contents.update

-- sourcebrowserLocationViewPrivate: Descriptor --
(#
   zoomed: @boolean;
   log: @texteditor
     (#
	inserting: @boolean;
	contentsType::
	  (#
	     eventhandler::
	       (# onBeforeChange::  (#  do inserting->allow #) #)
	  #);
	append:
	  (# msg: ^text
	  enter msg[]
	  do
	     true->inserting;
	     (contents.length,contents.length)->contents.selection.set;
	     contents.selection.scrollIntoView;
	     msg[]->contents.insert;
	     false->inserting
	  #);
	open::
	  (#  do true->bindleft->bindtop->bindright->bindbottom; hide #)
     #);
   changedLabel: @staticText
     (#
	width: @integer;
	eventhandler::
	  (#
	     onFatherFrameChanged::
	       (# w,h: @integer
	       do locationView.size->(w,h); (w-width-5,0)->position
	       #)
	  #);
	open::
	  (# w,h,y: @integer; p: @point
	  do
	     '*'->label;
	     hide;
	     true->border.visible;
	     borderStyles.etchedIn->border.style;
	     preferredSize->size->(width,y);
	     locationView.size->(w,h);
	     (w-width-5,0)->position
	  #)
     #);
   checkedLabel: @staticText
     (#
	width: @integer;
	eventhandler::
	  (#
	     onFatherFrameChanged::
	       (# p: @point do changedLabel.position->position;  #)
	  #);
	open::
	  (# w,h: @integer; p: @point
	  do
	     'c'->label;
	     hide;
	     true->border.visible;
	     borderStyles.etchedIn->border.style;
	     preferredSize->size;
	     changedLabel.position->position
	  #)
     #);
   lockedLabel: @staticText
     (#
	calcNewPosition:
	  (# w,h: @integer; p: @point
	  do
	     changedLabel.position->p;
	     changedLabel.size->(w,h);
	     (w+5,0)->p.subtract;
	     p->position
	  #);
	eventhandler::
	  (# onFatherFrameChanged::  (#  do calcNewPosition #) #);
	open::
	  (# w,h: @integer; p: @point
	  do
	     '%'->label;
	     hide;
	     true->border.visible;
	     borderStyles.etchedIn->border.style;
	     preferredSize->size;
	     calcNewPosition;

	  #)
     #);

#)

-- sourcebrowserLocationViewChanged: DoPart --
do
   (if true
    // status then
       private.changedLabel.show
    // not status then
       private.changedLabel.hide
   if);
   status->isChanged

-- sourcebrowserLocationViewChecked: DoPart --
do
   (if true
    // status then
       private.checkedLabel.show
    // not status then
       private.checkedLabel.hide
   if);
   status->isChecked

-- sourcebrowserLocationViewLocked: DoPart --
do
   (if true
    // status then
       private.lockedLabel.show
    // not status then
       private.lockedLabel.hide
   if);
   status->isLocked

-- sourcebrowserLocationViewMsg: DoPart --
do
   'Location: '->(msg.copy).prepend->label;
   update;
   (if true
    // (cge[] <> none ) and (cge.touched > 0) then
       false->checked; true->changed
    //
    (cge[] <> none )
    and
    ((sbprivate.fragmentGroupView.contents.ast[],cge.fg[])
       ->getDoneCheckProperty) then
       false->changed; true->checked
    // (cte[] <> none ) and cte.changed then
       false->checked; true->changed
    else
       false->changed; false->checked;
   if);
   (if true
    // (cge[] <> none ) and cge.isReadOnly
    // (cte[] <> none ) and not cte.allowEdit then
       true->locked
    else
       false->locked
   if)

-- sourcebrowserLocationViewOpen: DoPart --
do
   'Location: '->label;
   sz->size;
   locationView[]->private.changedLabel.open;
   locationView[]->private.checkedLabel.open;
   locationView[]->private.lockedLabel.open;
   locationView[]->private.log.open;
   INNER open

-- sourcebrowseropen: DoPart --
do sbprivate.open (#  do INNER open #)

-- sourcebrowserProjectsChanged: DoPart --
do
   sbprivate.projectlist.contents.scanProjects;
   none ->sbprivate.projectlist.contents.setProject;
   none ->currentProject[];
   none ->onCurrentProjectChange;
   sbprivate.grouplist.contents.clear;
   none ->currentGroup[];
   none ->onCurrentGroupChange;
   sbprivate.fragmentGroupView.contents.clear;
   sbprivate.down.popdown;
   none ->currentForm[];
   none ->onCurrentFormChange

-- sourcebrowserSetProject: DoPart --
do
   cursors.watch[]->mouse.busycursor;
   (if p[] <> currentProject[] then
       p[]->sbprivate.projectlist.contents.setProject;
       p[]->sbprivate.grouplist.contents.scanGroups;
       p[]->currentProject[]->onCurrentProjectChange
   if);
   INNER ;
   (if not setGroupDoneInInner then
       (if (currentProject[] <> none ) then
	   (if groups.size = 1 then (* automatically select it *)
	       (if not (groups.head).elm.g.txtFile then
		   (groups.head).elm.g[]->currentProject.lastGroupSelected[]
	       if);

	   if);
	   currentProject.lastGroupSelected[]
	     ->sbprivate.grouplist.contents.setgroup;
	   currentProject.lastGroupSelected[]->currentGroup[];
	   currentProject.lastGroupSelected[]->onCurrentGroupChange
	else
	   none ->sbprivate.grouplist.contents.setgroup;
	   sbprivate.fragmentGroupView.contents.clear;
	   none ->currentGroup[];
	   none ->onCurrentGroupChange
       if)
   if);
   (if not setFormDoneInInner then
       (if (currentGroup[] <> none ) then
	   (if (currentGroup.lastFormSelected[] = none ) then
	       (if currentGroup.fg[] <> none then
		   l: currentGroup.fg.fragmentList.scan
		     (#
			ff:
			  ^astInterface.fragmentForm
			   (* find first FF to select it automatically *)
		     do
			(if current.type
			 // sbprivate.fragmentGroupView.contents.ast.formType
			 then
			    (if currentGroup.lastFormSelected[] = none then
				current.open->currentGroup.lastFormSelected[]
			     else
			    (* only select if only one ff in group *)
				none ->currentGroup.lastFormSelected[]; leave l
			    if)
			if)
		     #)
	       if)
	   if);
	   (if (currentGroup[] <> none ) and
	   (currentGroup.lastFormSelected[] <> none ) then
	       edenv.history.protect
		 (#
		 do
		    currentGroup.lastFormSelected[]
		      ->sbprivate.fragmentGroupView.contents.setfragmentForm;
		    currentGroup.lastFormSelected[]->currentForm[];
		    currentGroup.lastFormSelected[]->onCurrentFormChange;
		    false->propertySelected
		 #)
	    else
	       sbprivate.fragmentGroupView.contents.scrollToFirstFF;
	       sbprivate.down.popdown;
	       none ->currentForm[];
	       none ->onCurrentFormChange;
	       false->propertySelected
	   if)
	else
	   sbprivate.fragmentGroupView.contents.clear;
	   sbprivate.down.popdown;
	   none ->currentForm[];
	   none ->onCurrentFormChange;
	   false->propertySelected
       if)
   if);
   none ->mouse.busycursor

-- sourcebrowserSetGroup: DoPart --
do
   TRUE->setGroupDoneInInner;
   (if g[] <> currentGroup[] then
       g[]->sbprivate.grouplist.contents.setgroup;
       g[]->currentGroup[];
       g[]->onCurrentGroupChange
   if);
   INNER

-- sourcebrowserSetFragmentForm: DoPart --
do
   TRUE->setFormDoneInInner;
   (if ff[] <> currentForm[] then
       ff[]->sbprivate.fragmentGroupView.contents.setfragmentForm;
       ff[]->currentForm[];
       ff[]->oncurrentFormChange;

   if);
   false->propertySelected;
