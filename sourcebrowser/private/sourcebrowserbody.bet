ORIGIN '../sourcebrowser';
INCLUDE '~beta/guienv/utils/obguienvadds'
        '~beta/toollibs/UI/listmoveable'
        '~beta/guienv/utils/labelled'
        '~beta/guienv/utils/pane'
        '~beta/guienv/utils/scrolleradds'
        '~beta/basiclib/formatio'
        '~beta/basiclib/file'
        '~beta/betaast/donecheck'
        '~beta/mps/fragmentscanner'
        '~beta/dependency/private/xmisk'
        'fileUtils'
        'uniqNames'
        '~beta/guienv/utils/toolbar'
        'hierarchy';
BODY 'setupProjects';
BODY 'sourcebrowserbodyext';
(* only to get hold of expandMacror2 *)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- lib: Attributes --
debugSourcebrw: (#  exit true #);
nameToDirectoryName:
  (# filename: ^text; thenr: @integer; 
  do filename.reset; directoryChar->filename.findAll (#  do inx->thenr #); 
  #)  

-- sourcebrowserLocationViewMouseDown: DoPart --
do
   (if doubleclick then
       (if private.zoomed then
           false->private.zoomed;
           contents.show;
           private.log.hide;
           sbprivate.outerctrl.unzoom
        else
           true->private.zoomed;
           locationView[]->sbprivate.outerctrl.zoomMember;
           contents.frame->private.log.frame;
           private.log.show;
           contents.hide
       if)
   if)  

-- sourcebrowserLocationViewClear: DoPart --
do 'Info: '->contents.label; true->locationView.contents.update  

-- sourcebrowserLocationViewDisplay: DoPart --
do
   m[]->contents.label;
   '\n'->((7,m.length)->m.sub).append->private.log.append;
   true->locationView.contents.update  

-- sourcebrowserLocationViewPrivate: Descriptor --
(#
   zoomed: @boolean;
   log: @texteditor
     (#
        inserting: @boolean;
        contentsType:: 
          (#
             eventhandler:: 
               (# onBeforeChange::  (#  do inserting->allow #) #)
          #);
        append:
          (# msg: ^text
          enter msg[]
          do
             true->inserting;
             (contents.length,contents.length)->contents.selection.set;
             contents.selection.scrollIntoView;
             msg[]->contents.insert;
             false->inserting
          #);
        open:: 
          (#  do true->bindleft->bindtop->bindright->bindbottom; hide #)
     #);
   changedLabel: @staticText
     (#
        width: @integer;
        eventhandler:: 
          (#
             onFatherFrameChanged:: 
               (# w,h: @integer
               do locationView.size->(w,h); (w-width-5,0)->position
               #)
          #);
        open:: 
          (# w,h,y: @integer; p: @point
          do
             '*'->label;
             hide;
             true->border.visible;
             borderStyles.etchedIn->border.style;
             preferredSize->size->(width,y);
             locationView.size->(w,h);
             (w-width-5,0)->position
          #)
     #);
   checkedLabel: @staticText
     (#
        width: @integer;
        eventhandler:: 
          (#
             onFatherFrameChanged:: 
               (# p: @point do changedLabel.position->position;  #)
          #);
        open:: 
          (# w,h: @integer; p: @point
          do
             'c'->label;
             hide;
             true->border.visible;
             borderStyles.etchedIn->border.style;
             preferredSize->size;
             changedLabel.position->position
          #)
     #);
   lockedLabel: @staticText
     (#
        calcNewPosition:
          (# w,h: @integer; p: @point
          do
             changedLabel.position->p;
             changedLabel.size->(w,h);
             (w+5,0)->p.subtract;
             p->position
          #);
        eventhandler:: 
          (# onFatherFrameChanged::  (#  do calcNewPosition #) #);
        open:: 
          (# w,h: @integer; p: @point
          do
             '%'->label;
             hide;
             true->border.visible;
             borderStyles.etchedIn->border.style;
             preferredSize->size;
             calcNewPosition;
             
          #)
     #);
   
#)  

-- sourcebrowserLocationViewChanged: DoPart --
do
   (if status then
       private.changedLabel.show
    else
       private.changedLabel.hide
   if);
   status->isChanged  

-- sourcebrowserLocationViewChecked: DoPart --
do
   (if status then
       private.checkedLabel.show
    else
       private.checkedLabel.hide
   if);
   status->isChecked  

-- sourcebrowserLocationViewLocked: DoPart --
do
   (if status then private.lockedLabel.show else private.lockedLabel.hide if);
   status->isLocked  

-- sourcebrowserLocationViewMsg: DoPart --
do
   'Location: '->(msg.copy).prepend->label;
   update;
   (if true
    // (cge <> none ) and ((cge).touched > 0) then
       false->checked; true->changed
    //
    (cge <> none )
    and
    true
    (*(sbprivate.fragmentGroupView.contents.ast[],(cge).fg[])
     ->getDoneCheckProperty*)
    then
       false->changed; true->checked
    // (cte[] <> none ) and cte.changed then
       false->checked; true->changed
    else
       false->changed; false->checked; 
   if);
   (if true
    // (cge <> none ) and (cge).isReadOnly
    // (cte[] <> none ) and not cte.allowEdit then
       true->locked
    else
       false->locked
   if)  

-- sourcebrowserLocationViewOpen: DoPart --
do
   'Location: '->label;
   sz->size;
   locationView[]->private.changedLabel.open;
   locationView[]->private.checkedLabel.open;
   locationView[]->private.lockedLabel.open;
   locationView[]->private.log.open;
   INNER open  

-- sourcebrowseropen: DoPart --
do sbprivate.open (#  do INNER open #)  

-- sourcebrowserProjectsChanged: DoPart --
do sbprivate.hierarchyView.hir.changed  

-- sourcebrowserLib: Attributes --
findGroup: (* will not select group! *)
  (#
     recFind:
       (#
          itm: ^sbprivate.hierarchyView.hir.item;
          fld: ^sbprivate.hierarchyView.hir.locationfolder;
          grp,tmp: ^sbprivate.hierarchyView.hir.locationfolder;
          pp: ^sbprivate.hierarchyView.hir.properties;
          newGroup: ^sbprivate.hierarchyView.hir.group;
          found: @boolean;
          
       enter itm[]
       do
          (if itm[] = none then
              &sbprivate.hierarchyView.hir.group[]->newGroup[];
              fgName.copy->newGroup.location[];
              mps.ast.astFileExtension->newGroup.location.append;
              fgName[]->fragmentDefaultName->newGroup.label[];
              (* 'newgroup: fgname:%s, label:%s\n'->putformat 
               (# do fgname[]->s;newgroup.label[]->s #);*)
              '+'->newGroup.label.prepend;
              (*true->newGroup.temporary;*)
              newGroup.init;
              newGroup[]->(sbprivate.hierarchyView.hir.root).addItem;
              (*newGroup.select;*)
              newGroup[]->grp[]
           else
          (*'searching item: %s for%s\n'->putformat(# do itm.label[]->s; completeName[]->s #);  *)
              (if itm## = sbprivate.hierarchyView.hir.properties## then
                  itm.father[]->itm[]
              if);
              (if itm.isFolder then
                  itm[]->fld[];
                  (* first look in expanded properties *)
                  fld.items.scan
                    (# ppfound: @boolean
                    do
                       (if current## = sbprivate.hierarchyView.hir.properties##
                        then
                           current[]->pp[];
                           fgname[]->pp.findFgandSelect->(ppfound,grp[]);
                           (if ppfound then true->found;  if)
                       if);
                       
                    #);
                  (if found then leave recFind if);
                  (* if not found in those, then look upwards in farther chain.
                   * still not found then append at bottom *)
                  fld.items.find
                    (#
                       predicate:: 
                         (# 
                         do
                            (if current## <=
                            sbprivate.hierarchyView.hir.locationfolder## then
                                current[]->tmp[];
                                (*'found locfld: %s loc=%s\n'->putformat
                                 (# do current[]->getpatternname->s; tmp.location[]->s #); *)
                                completename[]->tmp.location.equal->value
                             else
                                false->value
                            if)
                         #);
                       notFound::  (#  do fld.father[]->recFind->grp[] #)
                    do (* current.select *) current[]->grp[]
                    #)
               else
                  itm.father[]->recFind->grp[]
              if)
          if)
       exit grp[]
       #);
     fgName,completename: ^text;
     group: ^sbprivate.hierarchyView.hir.locationfolder;
     sel: ^sbprivate.hierarchyView.hir.item;
     
  enter fgName[]
  do
     fgName.copy->completename[];
     mps.ast.astFileExtension->completename.append;
     sbprivate.hierarchyView.hir.selection[]->sel[];
     (if sel[] <> none then
         sel.father[]->recFind->group[]
      else
         recFind->group[]
     if)
  exit group[]
  #);
SelectFormNotInHistory:
  (# grp: ^sbprivate.hierarchyView.hir.locationfolder; fgName,formName: ^text
  enter (fgName[],formName[])
  do
     fgName[]->findGroup->grp[];
     grp.onCollapse;
     grp.items.clear;
     grp.onExpand;
     grp.items.find
       (#
          predicate:: 
            (# fr: ^sbprivate.hierarchyView.hir.form; 
            do
               (if (current## = sbprivate.hierarchyView.hir.form##)->value then
                   current[]->fr[]; fr.ff.name->formName.equal->value
               if);
               
            #);
          notFound:: 
            (# t: @text
            do
               'Selected fragment form:\n  "%s"\nnot in selected group:\n   "%s"'
                 ->t.putformat (#  do formName[]->s; fgName[]->s #);
               (none ,t[],'Warning')->alertUser
            #)
       do current.select
       #)
  #);
AddToFormDependencyCache:
  (#
     fgName,formName: ^text;
     sel: ^sbprivate.hierarchyView.hir.item;
     grp: ^sbprivate.hierarchyView.hir.group;
     cache: ^sbprivate.hierarchyView.hir.dependencyCache;
     fg: ^astInterface.fragmentGroup;
     
  enter (fgName[],formName[])
  do
     sbprivate.hierarchyView.hir.selection[]->sel[];
     l:
     (if not (sel## <= sbprivate.hierarchyView.hir.group##) then
         sel.father[]->sel[]; restart l
      else
         sel[]->grp[];
         (if grp.isSubGroup then sel.father[]->sel[]; restart l if)
     if);
     sel[]->grp[];
     (if not grp.hasFormCache then
         &sbprivate.hierarchyView.hir.dependencyCache[]->cache[];
         'Used Forms'->cache.label[];
         true->cache.formCache;
         cache[]->grp.items.prepend;
         cache[]->grp.formCache[];
         grp[]->cache.father[];
         true->grp.hasFormCache
      else
         grp.formCache[]->cache[]
     if);
     fgName[]->mps.AST.expandToFullPath->mps.fragmentGroupTable.getFragmentGroup
       ->fg[];
     cache.items.find
       (#
          predicate:: 
            (# fr: ^sbprivate.hierarchyView.hir.form; 
            do
               current[]->fr[];
               fr.ff.name->formName.equal->value;
               (if value then (fr.ff.father = fg[])->value if)
            #);
          notFound:: 
            (#
               fr: ^sbprivate.hierarchyView.hir.form;
               ff: ^astInterface.fragmentForm;
               catname,t: ^text;
               
            do
               (if debugSourcebrw then 'Formcache - notfound'->putline if);
               &sbprivate.hierarchyView.hir.form[]->fr[];
               fg.realopen;
               fg.scan
                 (# cff: ^astInterface.fragmentForm; 
                 do
                    (if current.type = mps.AST.formType then
                        current[]->cff[];
                        (if cff.name->formName.equal then cff[]->ff[] if)
                    if)
                 #);
               (if ff[] <> none then
                   sbprivate.fficon[]->fr.icon;
                   ff[]->fr.ff[];
                   (if debugSourcebrw then
                       '/users/gram/beta/r5.2/sourcebrowser/private/sourcebrowserbody.bet: Line 378'
                         ->screen.putline
                   if);
                   fg.name->t[];
                   (t.copy,false)->sbprivate.history.names.uniqName;
                   (t.copy,true)->sbprivate.history.names.uniqName->t[];
                   '-'->t.puttext;
                   ff.name->t.append;
                   t[]->fr.label[];
                   fr.ff.fullname->fr.location[];
                   fr[]->cache.addItem;
                   fr.select
               if)
            #)
       do current.select
       #)
  #);
AddToUsedGroups:
  (#
     fgName: ^text;
     fg: ^astInterface.fragmentGroup;
     usedGroups: ^sbprivate.hierarchyView.hir.temporaryProject;
     selectedGroup: ^sbprivate.hierarchyView.hir.group;
     fld: ^hierarchy.folder;
     doSelect: @boolean
  enter (fgName[],doSelect)
  do
  (* 'Addtousedgroups on %s, doSelect=%t\n'->putformat(# do fgName[]->s; doselect->tv #);  *)
     sbprivate.hierarchyView.hir.getUsedGroups->usedGroups[];
     fgName[]->mps.AST.expandToFullPath->mps.fragmentGroupTable.getFragmentGroup
       ->fg[];
     (if (fg[] <> none ) then
         (if fg[] = sbprivate.hierarchyView.hir.selectedFG[] then
             (if not DoSelect then leave AddToUsedGroups if)
         if);
         usedGroups.items.find
           (#
              predicate:: 
                (# g: ^sbprivate.hierarchyView.hir.group; 
                do
                   current[]->g[];
                   (if g.fg[] = none then g.findfg if);
                   (g.fg[] = fg[])->value
                #);
              notFound:: 
                (#
                   newGroup: ^sbprivate.hierarchyView.hir.group;
                   ff: ^astInterface.fragmentForm;
                   catname,t: ^text
                do
                   &sbprivate.hierarchyView.hir.group[]->newGroup[];
                   fgName.copy->newGroup.location[];
                   (* ess: this caused troubles when opening files 
                    * (via opendialog or commandline) with parse errors  
                    *  mps.ast.astFileExtension->newGroup.location.append;
                    *)
                   fgName[]->fragmentDefaultName->newGroup.label[];
                   newGroup[]->usedGroups.additem;
                   fg[]->newGroup.fg[];
                   usedGroups.onProjectChange;
                   true->newGroup.isSubGroup;
                   (* newGroup.onexpand; *)
                   usedGroups[]->newGroup.init;
                   newGroup.theProjectInfoGroup[]
                     ->usedGroups.theProjectInfoProject.groups.append;
                   (if doSelect then
                       (if not usedGroups.isExpanded then
                           usedGroups.onExpand
                       if);
                       (if newGroup.items.size > 1 then
                           (newGroup.items.head).succ.elm.select
                        else
                           newGroup.select
                       if);
                       sbprivate.hierarchyView.theScroller[]
                         ->newGroup.scrollIntoView
                    else
                       (if newGroup.items.size > 1 then
                           sbprivate.sourceView.popdown
                       if);
                       sbprivate.hierarchyView.hir.changed
                   if);
                   usedGroups.SaveProjectFile
                #)
           do
              (if doSelect then
                  (if current[] <> sbprivate.hierarchyView.hir.selection[] then
                      current.select
                  if);
                  sbprivate.hierarchyView.theScroller[]->current.scrollIntoView
               else
                  current[]->fld[];
                  (if fld.items.size > 1 then
                  (* popdown if group has more than one form. *)
                  (* seems to give problems with properties *)
                  (* sbprivate.sourceView.popdown  *)
                      
                  if)
              if)
           #);
         
     if)
  #);
AddToGroupDependencyCache:
  (#
     findGroupInProject:
       (# fgName: ^text; dp: ^sbprivate.hierarchyView.hir.directoryProject
       enter fgName[]
       do
          sel[]->dp[];
          l: dp.items.scan
            (# 
            do
               (if current## <= sbprivate.hierarchyView.hir.group## then
                   current[]->grp[];
                   (if grp.fg[] = none then
                       grp.location[]->mps.AST.expandToFullPath
                         ->mps.fragmentGroupTable.getFragmentGroup->grp.fg[]
                   if);
                   (if fgname[]->(grp.fg.fullname).equal then
                       grp.select; current[]->sel[]; leave l
                    else
                       none ->grp[]
                   if)
               if)
            #)
       #);
     fgName,formName: ^text;
     sel: ^sbprivate.hierarchyView.hir.item;
     grp: ^sbprivate.hierarchyView.hir.group;
     cache: ^sbprivate.hierarchyView.hir.dependencyCache;
     fg: ^astInterface.fragmentGroup;
     
  enter fgName[]
  do (* 'AddToGroupDependencyCache on :%s\n'->putformat(# do fgName[]->s #); *)
     sbprivate.hierarchyView.hir.selection[]->sel[];
     l:
     (if not (sel## <= sbprivate.hierarchyView.hir.group##) then
         (if sel## = sbprivate.hierarchyView.hir.directoryProject## then
             fgName[]->findGroupInProject; leave l
          else
             sel.father[]->sel[]; restart l
         if)
      else
         sel[]->grp[];
         (if grp.isSubGroup then sel.father[]->sel[]; restart l if)
     if);
     sel[]->grp[];
     (if debugSourcebrw then grp.location[]->putline if);
     (if grp.fg[] = none then
         grp.location[]->mps.AST.expandToFullPath
           ->mps.fragmentGroupTable.getFragmentGroup->grp.fg[]
     if);
     (if fgname[]->(grp.fg.fullname).equal then
         grp.select;
         sbprivate.hierarchyView.theScroller[]->grp.scrollIntoView;
         leave AddToGroupDependencyCache
     if);
     (if not grp.hasGroupCache then
         &sbprivate.hierarchyView.hir.dependencyCache[]->cache[];
         'Used Groups'->cache.label[];
         cache[]->grp.items.prepend;
         cache[]->grp.groupCache[];
         false->cache.formCache;
         grp[]->cache.father[];
         true->grp.hasGroupCache
      else
         grp.GroupCache[]->cache[]
     if);
     fgName[]->mps.AST.expandToFullPath->mps.fragmentGroupTable.getFragmentGroup
       ->fg[];
     (if debugSourcebrw then
         (if fg[] = none then
             'fg none'->putline
          else
             'fg.fulname:'->puttext; fg.fullname->putline
         if)
     if);
     (if debugSourcebrw then
         cache.items.scan
           (# g: ^sbprivate.hierarchyView.hir.group; 
           do
              current[]->g[];
              (if g.fg[] <> none then
                  g.fg.fullname->putline
               else
                  'wasnone'->puttext; g.location[]->putline
              if)
           #)
     if);
     cache.items.find
       (#
          predicate:: 
            (# g: ^sbprivate.hierarchyView.hir.group; 
            do
               current[]->g[];
               (g.fg[] = fg[])->value;
               (* (if g.fg[]=none then
                'g.fg none '->puttext;
                g.location[]->putline;
                if)*)
               
            #);
          notFound:: 
            (#
               newGroup: ^sbprivate.hierarchyView.hir.group;
               ff: ^astInterface.fragmentForm;
               catname,t: ^text
            do
               &sbprivate.hierarchyView.hir.group[]->newGroup[];
               fgName.copy->newGroup.location[];
               mps.ast.astFileExtension->newGroup.location.append;
               fgName[]->fragmentDefaultName->newGroup.label[];
               fg[]->newGroup.fg[];
               newGroup[]->cache.additem;
               true->newGroup.isSubGroup;
               (* newGroup.onexpand; *)
               newGroup.init;
               (if newGroup.items.size > 1 then
                   (newGroup.items.head).succ.elm.select
                else
                   newGroup.select; 
               if);
               sbprivate.hierarchyView.theScroller[]->newGroup.scrollIntoView
            #)
       do
          current.select;
          sbprivate.hierarchyView.theScroller[]->current.scrollIntoView
       #)
  #)  

-- sourcebrowserprivate: Descriptor --
(#
   rootProject: ^treeview.folder;
   rootProjectID: @integer;
   StdLibPrjID: (#  exit 1 #);
   HomedirPrjID: (#  exit 2 #);
   DotPrjID: (#  exit 3 #);
   RecentPrjId: (#  exit 4 #);
   initialHierarchyViewWidth: (#  exit 250 #);
   initialWindowWidth: (#  exit 480 (*510*) #);
   stdMinSize: (#  exit 50 #);
   stdPaneWidth: (#  exit 3 #);
   fficon,prjicon,prjopenicon,fgicon,includeicon,bodyicon,originicon: @pixmap;
   outerctrl: @pane (* Vertical pane containing upctrl and locationView. *)
     (#
        fatherFrame::  (#  do THIS(sourcebrowser).frame->fr #);
        eventhandler:: 
          (# onFatherFrameChanged::  (#  do father.size->size #) #);
        open:: 
          (#
             setDefaults:: 
               (# 
               do
                  TRUE->verticalStacking;
                  stdMinSize->minsize;
                  stdPaneWidth->panewidth
               #)
          do
             (outerctrl[])->tb.open;
             (*tb.size->(putint,putint);*)
             (outerctrl[],false,50,2)->upctrl.open;
             (outerctrl[],(initialWindowWidth,45))->locationView.open;
             infoView.open;
             tb[]->appendMember;
             tb[]->fixedsize;
             upctrl[]->appendMember;
             locationView[]->appendMember;
             locationView[]->fixedSize;
             appendsDone
          #)
     #);
   tb: @toolbar
     (#
        open:: 
          (# 
          do (300,45)->size; tb[]->Interface.theToolbar[]; tb[]->setupToolbar
          #)
     #);
   upctrl: @pane (* Horizontal pane containing hierarchyView and sourceView *)
     (#
        fatherFrame::  (#  do outerctrl.frame->fr #);
        open:: 
          (#
             setDefaults:: 
               (# 
               do
                  FALSE->verticalStacking;
                  stdMinSize->minsize;
                  stdPaneWidth->panewidth
               #)
          do
             (upctrl[],false,50,2)->projectpane.open;
             upctrl[]->sourceView.open;
             projectPane[]->AppendMember;
             projectPane[]->fixedsize;
             sourceView[]->appendMember;
             appendsDone
          #)
     #);
   setupDirectory: <<SLOT setupDirectory:Descriptor>>;
   parseProjectFile:
    <<SLOT parseProjectFile:Descriptor>>;
   setupProjectFile: <<SLOT setupProjectFile:Descriptor>>;
   setupGroup:
    <<SLOT setupGroup:Descriptor>>;
   getfg:
     (#
        grp: ^hierarchy.group;
        doRealOpen:
          (# g: ^astInterface.fragmentGroup
          enter g[]
          <<SLOT groupDoRealOpen:DoPart>>
          #);
        recheck:
         <<SLOT groupRecheck:Descriptor>>
     enter grp[]
     <<SLOT groupGetFg:DoPart>>
     #);
   formView: @hierarchy
     (#
        hideRoot:: trueObject;
        rootProject: ^treeview.folder;
        theScroller: ^scroller;
        theGroup: ^hierarchy.group;
        selectedFormName: ^text;
        clear:
          (# 
          do
             (root).items.clear;
             ''->(root).label[];
             none ->theGroup[];
             changed
          #);
        form:: 
          (#
             onSelected:: 
               (# 
               <<SLOT formViewOnSelected:DoPart>>
               #)
          #);
        eventhandler:: 
          (# onUpdate::  (#  do true->updateDoneInInner;  #) #);
        open::< 
          (# 
          do
             &project[]->rootproject[];
             'Fragment Forms'->rootProject.label[];
             rootProject[]->root;
             (initialHierarchyViewWidth,150)->size;
             (0xffff,0,0xffff)->backgroundcolor;
             INNER
          #)
     #);
   updateFolderView:
     (# g: ^hierarchy.group; didSelect: @boolean
     enter g[]
     <<SLOT updateFolderView:DoPart>>
     #);
   theActiveRootProject,groupProject,stdlibprj,
     homeprj,dotprj,recentprj: ^hierarchy.project;
   hierarchyView: @labelled
     (#
        theScroller: ^scroller;
        cp: ^projects.project;
        setCP: @
          (# newp: ^projects.project; fp,last: ^hierarchy.folder; 
          enter newp[]
          do
             (if debugsourcebrw then
                 'setcp on %s \n'->putformat (#  do newp.name[]->s #)
             if);
             (if newp[] <> cp[] then
                 newp[]->cp[];
                 newP[]->Interface.currentProject[];
                 newp[]->onCurrentProjectChange
             if);
             hir.selection[]->fp[];
             (if fp[] = none then leave setcp if);
             l:
             (if fp.father[] <> none then
                 fp[]->last[]; fp.father[]->fp[]; restart l
              else
                 last[]->theActiveRootProject[];
                 (if last[]
                  // stdlibprj[] then
                     StdLibPrjID->rootProjectID
                  // homeprj[] then
                     HomedirPrjID->rootProjectID
                  // recentprj[] then
                     RecentPrjId->rootProjectID
                  // dotprj[] then
                     DotPrjID->rootProjectID
                  else
                     'unknown rootproject'->putline;
                     last[]->getPatternName->putline;
                     last.label[]->putline;
                     
                 if)
             if)
          #);
        hir: @hierarchy
          (#
             refreshcount: @integer;
             selectedFG: ^astInterface.fragmentGroup;
             findCP: @
               (#
                  sel: ^item;
                  dp: ^directoryProject;
                  pf: ^projectFile;
                  tp: ^temporaryProject;
                  dpp: ^DPproject
               do
                  selection[]->sel[];
                  (if debugsourcebrw then
                      'findcp on %s\n'->putformat (#  do sel.label[]->s #)
                  if);
                  (if sel[] = none then
                      (if debugsourcebrw then
                          'leaving, sel is none'->putline
                      if);
                      leave findCP
                  if);
                  l:
                  (if sel## = group## then
                      sel.father[]->sel[]; restart l
                   else
                      (if sel## = project## then sel.father[]->sel[] if)
                  if);
                  (if sel##
                   // directoryProject## then
                      sel[]->dp[]; dp.theProjectInfoProject[]->setCP; 
                   // projectFile## then
                      sel[]->pf[]; pf.theProjectInfoProject[]->setCP
                   // temporaryProject## then
                      sel[]->tp[]; tp.theProjectInfoProject[]->setCP
                   // DPproject## then
                      sel[]->dpp[]; dpp.theProjectInfoProject[]->setCP
                   else
                      'project not found'->putline; none ->setCP
                  if)
               #);
             hideRoot:: trueObject;
             usedGroups: ^temporaryProject;
             getUsedGroups: @ (#  exit usedGroups[] #);
             setUsedGroups: (#  enter usedGroups[] #);
             directoryProject:: 
               (#
                  theProjectInfoProject: ^projects.project;
                  onSelected:: 
                    (# 
                    do
                       sourceView.popdown;
                       formView.clear;
                       (if theProjectInfoProject[] <> none then
                           theProjectInfoProject.scangroups
                       if);
                       theProjectInfoProject[]->onCurrentProjectChange
                    #);
                  onExpand:: 
                    (#  <<SLOT directoryProjectOnExpand:DoPart>> #);
                  setupListView::< 
                    (#
                    (* not used anymore !! *) 
                    <<SLOT directoryProjectSetupListView:DoPart>>
                    #);
                  init:: 
                    (# 
                    <<SLOT directoryProjectinit:DoPart>>
                    #)
               #);
             projectFile:: 
               (#
                  theProjectInfoProject: ^projects.project;
                  isUsedGroup: @boolean;
                  onSelected::  (#  do projectFileSetupListView #);
                  onExpand::  (#  <<SLOT projectFileOnExpand:DoPart>> #);
                  readFile:
                    (# 
                    <<SLOT projectFileReadFile:DoPart>>
                    #);
                  SelectAsUsedGroups:
                    (#  <<SLOT SelectAsUsedGroups:DoPart>> #);
                  projectFilesetupListView:
                    (# 
                    <<SLOT projectFileSetupListView:DoPart>>
                    #);
                  init:: 
                    (# lfmenuitem: ^lfmenu.menuitem
                    <<SLOT ProjectFileinit:DoPart>>
                    #)
               #);
             temporaryProject:: 
               (#
                  originalproject: ^text;
                  hasChanged: @boolean;
                  theProjectInfoProject:
                    ^projects.project;
                  SaveProjectFile:
                    (# 
                    <<SLOT temporaryProjectSavePrjFile:DoPart>>
                    #);
                  onSelected:: 
                    (#  <<SLOT temporaryProjectonSelect:DoPart>> #);
                  init:: 
                    (#
                       lfmenuitem:
                         ^lfmenu.
                            menuitem
                    <<SLOT temporaryProjectInit:DoPart>>
                    #);
                  onProjectChange:: 
                    (# 
                    <<SLOT temporaryProjectChange:DoPart>>
                    #)
               #);
             DomainKind: (#  exit 1 #);
             ExtentKind: (#  exit 2 #);
             SubProjectKind: (#  exit 3 #);
             DPProject:: 
               (#
                  theProjectInfoProject: ^projects.project;
                  onSelected::  (#  do theProjectInfoProject[]->setCP #);
                  kind: @integer;
                  isScanned: @boolean;
                  init:: 
                    (# lfmenuitem: ^lfmenu.menuitem; 
                    enter kind
                    <<SLOT DPProjectInit:DoPart>>
                    #);
                  onExpand:: 
                    (# 
                    <<SLOT DDProjectExpand:DoPart>>
                    #)
               #);
             properties:: 
               (#
                  onSelected:: 
                    (# grp: ^group; pe: ^propertyElement; 
                    do
                       (if cge <> none then
                           (if true
                            // father## <= group## then
                               father[]->grp[];
                               grp.fg[]
                                 ->sourceview.pelist.findorcreatePropertyEditor
                            // father## < propertyElement## then
                               father[]->pe[];
                               pe.fg[]
                                 ->sourceview.pelist.findorcreatePropertyEditor
                           if)
                        else
                           'cge none'->putline
                       if)
                    #)
               #);
             group:: 
               (#
                  HasDomain,hasExtent,expanding: @boolean;
                  theProjectInfoGroup: ^projects.group;
                  findfg:
                    (# 
                    do
                       location[]->mps.AST.expandToFullPath
                         ->mps.fragmentGroupTable.getFragmentGroup->fg[]
                         ->theProjectInfoGroup.fg[]
                    #);
                  isInUsedGroups: (#  exit theProject[] = UsedGroups[] #);
                  InsertDomain: (#  <<SLOT groupInsertDomain:DoPart>> #);
                  InsertExtent:
                    (# 
                    <<SLOT groupInsertExtent:DoPart>>
                    #);
                  onSelected:: 
                    (# 
                    do
                       groupsetupListView;
                       (* sourceView.listView[]->sourceView.popup *)
                       
                    #);
                  onExpand::  (#  <<SLOT groupOnExpand:DoPart>> #);
                  groupSetupListView:
                    (# 
                    <<SLOT groupSetupListView:DoPart>>
                    #);
                  init:: 
                    (# lfmenuitem: ^lfmenu.menuitem
                    <<SLOT groupinit:DoPart>>
                    #)
               #);
             form:: 
               (#
                  onSelected:: 
                    (# 
                    <<SLOT formOnSelected:DoPart>>
                    #)
               #);
             onPropertyShowEditor:: 
               (# 
               do
                  (if cge <> none then
                      lf.select;
                      (if lf.fg[] = none then
                          lf.location[]->mps.AST.expandToFullPath
                            ->mps.fragmentGroupTable.getFragmentGroup->lf.fg[]
                      if);
                      lf.fg[]->sourceview.pelist.findorcreatePropertyEditor
                   else
                      'cge none'->putline
                  if);
                  none ->selectedFG[];
                  none ->formview.selectedFormName[]
               #);
             onPropertyExpand::  (#  <<SLOT includeOnExpand:DoPart>> #);
             origin:: 
               (#
                  onSelected:: 
                    (# 
                    do originSetupListView
                    #);
                  originSetupListView:
                    (# 
                    <<SLOT originSetupListView:DoPart>>
                    #)
               #);
             include:: 
               (#
                  onSelected::  (#  do includeSetupListView #);
                  includeSetupListView:
                    (#  <<SLOT includeSetupListView:DoPart>> #);
                  
               #);
             body:: 
               (#
                  onSelected:: 
                    (# 
                    do bodysetupListView
                    #);
                  bodySetupListView:
                    (# 
                    <<SLOT bodySetupListView:DoPart>>
                    #)
               #);
             eventhandler:: 
               (# (* for debugging refreshes *)
               (*
                onrefresh::
                (# 
                do refreshcount+1 -> refreshcount;
                'hir refresh: %d\n'->putformat(# do refreshcount -> d #);
                'rectangle: %d, %d, %d, %d\n'->putformat
                (# r:^rectangle;
                do updaterect->r[];
                r.left->d; r.top->d; r.right->d; r.bottom->d;
                #);
                (* (dumpstack,'')->stop;  * )
                #);*)
                  onUpdate:: 
                    (#
                       treewidth,treeheight,scrollerwidth,scrollerheight:
                         @integer;
                       
                    <<SLOT hirOnUpdate:DoPart>>
                    #);
                  
               #);
             open:: 
               (# 
               do
               (*(initialHierarchyViewWidth,3000)
                ->size; *) 
               #)
          #);
        contentsType:: scroller
          (#
             <<SLOT hierarchyViewLib:Attributes>>;
             eventhandler:: 
               (#
                  onFatherFrameChanged:: 
                    (# 
                    do
                       contents.
                         resizeContents;
                       
                    #);
                  
               #);
             contentsType:: 
               (#
                  resizeContents:
                    (#
                       hirWidth,hirHeight,width,height,scrollerWidth,
                         scrollerHeight: @integer;
                       
                    do
                       THIS(scroller).size->(scrollerWidth,scrollerHeight);
                       THIS(labelled).size->(width,height);
                       hir.size->(hirWidth,hirHeight);
                       ((hirWidth,scrollerWidth)->max,
                        (hirHeight,scrollerHeight)->max)->size;
                       
                    #);
                  eventHandler:: 
                    (#
                       onChildFrameChanged:: 
                         (#
                            hirWidth,hirHeight,width,height,scrollerWidth,
                              scrollerHeight: @integer;
                            
                         do
                            THIS(scroller).size->(scrollerWidth,scrollerHeight);
                            THIS(labelled).size->(width,height);
                            hir.size->(hirWidth,hirHeight);
                            ((hirWidth,scrollerWidth)->max,
                             (hirHeight,scrollerHeight)->max)->size;
                            
                         #)
                    #);
                  open:: 
                    (# 
                    do
                       THIS(scroller)[]->hierarchyView.theScroller[];
                       contents[]->hir.open;
                       hir.update;
                       (0xffff,0xffff,0xffff)->contents.backgroundcolor;
                       (0xffff,0xffff,0xffff)->hir.backgroundcolor;
                       
                    #)
               #)
          #);
        open::  (# sz: @point enter sz do sz->size; projectsLabel->label #)
     #);
   ProjectPane: @pane
     (#
        open:: 
          (#
             setDefaults::  (#  do TRUE->verticalStacking;  #);
             fx,fy: @integer;
             
          do
             fitToFather;
             (THIS(pane)[],(initialHierarchyViewWidth,445))->hierarchyView.open;
             hierarchyView[]->AppendMember;
             FormLabelled.open;
             (initialHierarchyViewWidth,200)->formLabelled.size;
             FormLabelled[]->appendMember;
             FormLabelled[]->fixedsize;
             appendsDone
          #);
        FormLabelled: @labelled
          (#
             open::  (#  do 'Fragment Forms:'->label #);
             contentsType::< scroller
               (#
                  contentsType:: 
                    (#
                       eventHandler:: 
                         (#
                            onChildFrameChanged:: (*childFrame = hir*) 
                              (#
                                 formWidth,formHeight,scrollerWidth,
                                   scrollerHeight: @integer
                              do
                                 THIS(scroller).size
                                   ->(scrollerWidth,scrollerHeight);
                                 formView.size->(formWidth,formHeight);
                                 ((scrollerWidth,formWidth)->max,
                                  (scrollerHeight,formHeight)->max)->size;
                                 
                              #)
                         #);
                       open:: 
                         (# 
                         do
                            THIS(scroller)[]->formview.theScroller[];
                            contents[]->formView.open;
                            formView.update;
                            (0xffff,0xffff,0xffff)->contents.backgroundcolor;
                            (0xffff,0xffff,0xffff)->formView.backgroundcolor;
                            
                         #)
                    #)
               #)
          #)
     #);
   sourceView: @Canvas
     (#
        propEditor:
          (# fg: ^astInterface.fragmentGroup; view: ^overlaytype;  #);
        ProperTyViewType: overlayType
          (#
             contentsType:: canvas
               (# open::  (#  do father.size->size #) #);
             dobind: (#  do TRUE->bindRight->bindBottom #);
             open:: 
               (#
                  openContents:: 
                    (# 
                    do
                       (THIS(ProperTyViewType)[])->contents.open;
                       TRUE->doneInInner
                    #)
               do 'Properties'->label
               #)
          #);
        peList: @list (* property editors *)
          (#
             element::  (# pe: ^propEditor #);
             findOrCreatePropertyEditor:
               (# fg: ^astInterface.fragmentGroup; peView: ^ProperTyViewType
               enter fg[]
               do
                  find
                    (#
                       predicate:: 
                         (#  do (current.pe.fg[] = fg[])->value #);
                       notFound:: 
                         (# ov: ^ProperTyViewType; e: ^element; 
                         do
                            &ProperTyViewType[]->ov[];
                            (sourceView[],sourceView.size)->ov.open;
                            fg[]->geList.findOrCreateGroupEditor->cge;
                            ov.contents[]->(cge).editPropertiesCanvas->cfe;
                            ov[]->peView[];
                            &element[]->e[];
                            e[]->append;
                            &propEditor[]->e.pe[];
                            ov[]->e.pe.view[];
                            fg[]->e.pe.fg[]
                         #)
                    do current.pe.view[]->peview[]
                    #);
                  peView[]->popup
               #)
          #);
        aeList: @list (* ascii editors active in this browser *)
          (#
             element::  (# ae: ^asciiEditor #);
             findOrCreateAsciiEditor:
               (# ae: ^asciiEditor; path: ^text; view: ^asciiEditorView
               enter path[]
               do
                  state.busy;
                  'Opening '->infoView.settext;
                  path[]->infoView.append;
                  '... '->infoView.append;
                  infoView.msgPush;
                  l: find
                    (#
                       predicate:: 
                         (#  do current.ae.path->path.equalNCS->value #);
                       notFound:: 
                         (# newElm: ^element
                         do
                            &asciiEditorView[]->view[];
                            (sourceView[],sourceView.size)->view.open;
                            path.copy->view.label;
                            path.copy->view.path[];
                            (if not view.ae.reload then leave l if);
                            view.ae[]->ae[];
                            &element[]->newElm[];
                            view.ae[]->ae[]->newElm.ae[];
                            newElm[]->append
                         #)
                    do current.ae[]->ae[]
                    #);
                  infoView.msgPop;
                  state.normal;
                  
               exit ae[]
               #);
             closeAsciiEditor:
               (# path: ^text; okToClose: @boolean; 
               enter path[]
               do
                  locate
                    (#
                       predicate:: 
                         (#  do current.elm.ae.path->path.equalNCS->value #)
                    do
                       (if position.elm.ae.close->okToClose then
                           position[]->delete
                       if)
                    #)
               exit okToClose
               #);
             closeAsciiEditors:
               (# okToClose: @boolean
               do
                  true->okToClose;
                  l: iterate
                    (# 
                    do
                       (if current.elm.ae.close->okToClose then
                           current[]->delete
                        else
                           leave l
                       if)
                    #)
               exit okToClose
               #)
          #);
        open:: 
          (# 
          do
             (sourceView[],sourceView.size)->defaultView.open;
             (sourceView[],sourceView.size)->listView.open;
             defaultView.show;
             defaultView[]->currentView[]
          #);
        newfragment:
          (#
             ff: ^astInterface.fragmentForm;
             view: ^fragmentViewType;
             cfe: ^codeeditor
          enter ff[]
          do
             &fragmentViewType[]->view[];
             (sourceView[],sourceView.size)->view.open;
             (ff[],ff.root[],none ,view.contents[],cge)
               ->edenv.findOrCreateFormEditor->cfe[];
             (if edenv.history.currentCell[] <> none then
                 edenv.history.currentCell.elm[]->edenv.history.remove
             if);
             ff.fullname->view.label;
             view[]->sourceView.popup
          exit (cfe[],view[])
          #);
        newAsciiEditor:
          (# p: ^text
          enter p[]
          do
             p[]->aeList.findOrCreateAsciiEditor->cte[];
             (if cte[] <> none then cte.show; none ->cfe if)
          #);
        popdown: (#  do none ->cfe; defaultView[]->popup #);
        popup:
          (# view: ^overlayType
          enter view[]
          do
             (if view[] = none then leave popup if);
             (if (currentView[] <> none ) then
                 (if (currentView[] <> view[]) then
                     currentView.hide; view.bringToFront; view.show
                  else
                 (* they are the same view => do nothing *)
                     
                 if)
              else
                 view.bringToFront; view.show
             if);
             (*  old impl 
              (if (currentView[] <> none ) and (currentView[] <> view[]) then
              currentView.hide
              else
              (if view[] <> none then 
              view.bringToFront; 
              view.show 
              if)
              if);
              *)
             view[]->currentView[]
          #);
        overlayType: labelled
          (#
             zoomed: @boolean;
             eventhandler::< 
               (#
                  onMouseDown:: 
                    (# 
                    do
                       (if doubleclick then
                           (if zoomed then
                               false->zoomed; upctrl.unzoom
                            else
                               true->zoomed; sourceView[]->upctrl.zoomMember
                           if)
                       if)
                    #);
                  onFatherFrameChanged:: 
                    (#  do (if visible then sourceView.size->size if) #);
                  onVisibleChanged:: 
                    (#  do (if visible then sourceView.size->size if) #)
               #);
             open::< 
               (# sz: @point;  enter sz do hide; sz->size; INNER #)
          #);
        textView: overlayType
          (#
             changed,allowEdit,loading: @boolean;
             contentsType::< textEditor
               (#
                  contentsType::< 
                    (#
                       eventhandler::< 
                         (#
                            onTextChanged:: 
                              (# 
                              do not loading->changed->locationView.changed
                              #);
                            onBeforeChange:: 
                              (#  do (loading or allowEdit)->allow #)
                         #)
                    #)
               #)
          #);
        asciiEditorView: textView
          (#
             path,name: ^text;
             loadStream:
               (# strm: ^stream; theText: @styledText; 
               enter strm[]
               do
                  strm.reset;
                  strm.scan
                    (# while::  (#  do true->value #); 
                    do ch->theText.put; 
                    #);
                  true->loading;
                  theText[]->contents.contents.contents;
                  false->loading;
                  
               #);
             saveStream:
               (# strm: ^stream; theText: ^styledText; 
               enter strm[]
               do
                  strm.reset;
                  contents.contents.contents->theText[];
                  theText[]->strm.puttext
               #);
             ae: @asciiEditor
               (#
                  allowEdit:: 
                    (#  do THIS(asciiEditorView).allowEdit->value #);
                  toggleAllowEdit:: 
                    (# 
                    do
                       not THIS(asciiEditorView).allowEdit
                         ->THIS(asciiEditorView).allowEdit;
                       not THIS(asciiEditorView).allowEdit->locationView.locked
                    #);
                  changed::  (#  do THIS(asciiEditorView).changed->value #);
                  cutStatus:: 
                    (# 
                    do
                       (contents.contents.selection.start <>
                        contents.contents.selection.end)->value
                    #);
                  doCut::  (#  do contents.contents.cut #);
                  copyStatus:: 
                    (# 
                    do
                       (contents.contents.selection.start <>
                        contents.contents.selection.end)->value
                    #);
                  doCopy::  (#  do contents.contents.copy #);
                  pasteStatus:: 
                    (#  do (*clipboard.hasText*) true->value #);
                  doPaste::  (#  do contents.contents.paste #);
                  doClear::  (#  do contents.contents.clear #);
                  save:: 
                    (# 
                    do
                       l:
                         (#
                            t: ^text;
                            f: @file
                              (#
                                 cleanUp:
                                   (# msg: ^text
                                   enter msg[]
                                   do
                                      (none ,msg[],'Warning')->alertUser;
                                      leave l
                                   #);
                                 accessError::  (#  do msg[]->cleanup #);
                                 writeError::  (#  do msg[]->cleanup #);
                                 readError::  (#  do msg[]->cleanup #);
                                 EOSerror::  (#  do msg[]->cleanup #);
                                 noSuchFileError:: 
                                   (#  do msg[]->cleanup #);
                                 fileExistsError:: 
                                   (#  do msg[]->cleanup #);
                                 noSpaceError::  (#  do msg[]->cleanup #);
                                 otherError::  (#  do msg[]->cleanup #);
                                 
                              #);
                            
                         do
                            path->mps.AST.expandToFullPath->f.name;
                            (if f.name->fileExists then
                                (if f.name->fileWriteable then
                                    f.openWrite;
                                    f[]->THIS(asciiEditorView).saveStream;
                                    f.close
                                 else
                                    'File exception for \''->t[];
                                    f.name->t.append;
                                    t.newline;
                                    'File is not writeable (Insufficient access privilegies)'
                                      ->t.append;
                                    (none ,t[],'Warning')->alertUser;
                                    leave l
                                if)
                             else
                                'File exception for \''->t[];
                                f.name->t.append;
                                t.newline;
                                'File does not exist'->t.append;
                                (none ,t[],'Warning')->alertUser;
                                leave l
                            if);
                            false->locationView.changed;
                            
                         #)
                    #);
                  saveAs::  (#  do  #);
                  reload:: 
                    (# t: ^text; success: @boolean
                    do
                       l:
                         (#
                            f: @file
                              (#
                                 cleanUp:
                                   (# msg: ^text
                                   enter msg[]
                                   do
                                      (none ,msg[],'Warning')->alertUser;
                                      leave l
                                   #);
                                 accessError::  (#  do msg[]->cleanup #);
                                 writeError::  (#  do msg[]->cleanup #);
                                 readError::  (#  do msg[]->cleanup #);
                                 EOSerror::  (#  do msg[]->cleanup #);
                                 noSuchFileError:: 
                                   (#  do msg[]->cleanup #);
                                 fileExistsError:: 
                                   (#  do msg[]->cleanup #);
                                 noSpaceError::  (#  do msg[]->cleanup #);
                                 otherError::  (#  do msg[]->cleanup #);
                                 
                              #)
                         do
                            false->success;
                            path->mps.AST.expandToFullPath->f.name;
                            (if f.name->fileExists then
                                (if f.name->fileReadable then
                                    f.entry.writeable
                                      ->THIS(asciiEditorView).allowEdit;
                                    ''->t[];
                                    t[]->loadStream;
                                    f.openRead;
                                    f[]->loadStream;
                                    f.close;
                                    
                                 else
                                    'File exception for \''->t[];
                                    f.name->t.append;
                                    t.newline;
                                    'File is not readable (Insufficient access privilegies)'
                                      ->t.append;
                                    (none ,t[],'Warning')->alertUser;
                                    leave l
                                if)
                             else
                                'File exception for \''->t[];
                                f.name->t.append;
                                t.newline;
                                'File does not exist'->t.append;
                                (none ,t[],'Warning')->alertUser;
                                leave l
                            if);
                            false->locationView.changed;
                            true->success
                         #)
                    exit success
                    #);
                  search::  (#  do  #);
                  replace::  (#  do  #);
                  show::  (#  do THIS(asciiEditorView)[]->popup;  #);
                  close:: 
                    (# t: ^text
                    do
                       (if changed then
                           'Text file: '->t[];
                           path->t.putline;
                           'has been changed.'->t.putline;
                           'Save?'->t.puttext;
                           (none ,'Warning',t[])
                             ->promptForBoolean
                               (#
                                  ok::  (#  do true->value; save #);
                                  notok::  (#  do true->value #);
                                  cancel::  (#  do false->value #)
                               #)
                        else
                           true->value
                       if)
                    #);
                  path::  (#  do THIS(asciiEditorView).path.copy->path[] #);
                  
               #)
          #);
        listView: @overlayType
          (#
             install:
               (# f: ^treeview.folder
               enter f[]
               do INNER ; f[]->contents.contents.root; f.onExpand
               #);
             contentstype:: scroller
               (#
                  contentsType:: hierarchy
                    (#
                       hideRoot:: trueObject;
                       empty: @project;
                       open:: 
                         (# 
                         do
                            ('root','')->(empty.label[],empty.location[]);
                            empty[]->install
                         #);
                       selectInHierarchyView:
                         (# f: ^hierarchy.folder; i: ^hierarchy.item
                         enter i[]
                         do
                            (if hierarchyView.hir.selection.isFolder then
                                hierarchyView.hir.selection[]->f[];
                                f.items.clear;
                                f.onExpand;
                                f.items.find
                                  (#
                                     predicate:: 
                                       (# 
                                       do current.label[]->i.label.equal->value
                                       #)
                                  do current.select
                                  #)
                             else
                                INNER
                            if)
                         #);
                       selectGroupInHierarchyView: selectInHierarchyView
                         (# e: ^hierarchy.element
                         do i[]->e[]; e.location[]->selectFragmentGroup
                         #);
                       selectFormInHierarchyView: selectInHierarchyView
                         (# f: ^hierarchy.form
                         do
                            i[]->f[];
                            ((f.ff.father).fullname,f.ff.name)
                              ->selectFragmentForm
                         #);
                       selectProjectInHierarchyView: selectInHierarchyView
                         (# p: ^hierarchy.project
                         do i[]->p[]; p.location[]->selectFragmentGroup
                         #);
                       form:: 
                         (#
                            onSelected:: 
                              (# 
                              do
                                 (if ev.doubleclick then
                                     THIS(form)[]->selectFormInHierarchyView
                                 if)
                              #)
                         #);
                       origin:: 
                         (#
                            onSelected:: 
                              (# 
                              do
                                 (if ev.doubleclick then
                                     THIS(origin)[]->selectGroupInHierarchyView
                                 if)
                              #)
                         #);
                       include:: 
                         (#
                            onSelected:: 
                              (# 
                              do
                                 (if ev.doubleclick then
                                     THIS(include)[]->selectGroupInHierarchyView
                                 if)
                              #)
                         #);
                       body:: 
                         (#
                            onSelected:: 
                              (# 
                              do
                                 (if ev.doubleclick then
                                     THIS(body)[]->selectGroupInHierarchyView
                                 if)
                              #)
                         #);
                       directoryProject:: 
                         (#
                            onSelected:: 
                              (# 
                              do
                                 (if ev.doubleclick then
                                     THIS(directoryProject)[]
                                       ->selectProjectInHierarchyView
                                 if)
                              #)
                         #);
                       
                    #)
               #)
          #);
        currentView: ^overlayType;
        defaultView: @textView
          (# open::  (#  do emptyCodeViewLabel->label #) #);
        ffViewType: codemoveableEditor
          (#
             isolated:: trueObject;
             dobind:
               (#  do TRUE->bindRight->bindBottom->bindLeft->bindTop #);
             selectNode:: 
               (# 
               do
                  (theEditorRoot[],node[],separate)
                    ->THIS(sourceBrowser).selectNode
               #);
             open::  (#  do THIS(ffViewType)[]->onCodeViewOpen #);
             initialContractionLevel::  (#  do 2->value #);
             charWidthType:: 
               (# 
               do
               (* HACK NEEDED UNTIL textEditor.defaultStyle gets
                * implemented !!!
                fragmentGroupView.private.theLabel.style->ts[] *) 
               #);
             getEncloser::  (#  do editorEncloser[]->theEncloser[] #)
          #);
        fragmentViewType: overlayType
          (#
             dobind:
               (#  do contents.dobind; TRUE->bindRight->bindBottom #);
             contentsType:: ffViewType;
             open:: 
               (#
                  openContents:: 
                    (# 
                    do
                       (THIS(fragmentViewType)[],sz)->contents.open;
                       TRUE->doneInInner
                    #)
               #)
          #);
        
     #);
   open:
     (# 
     do
        'tv-ff'->fficon.read;
        'tv-fg'->fgicon.read;
        'tv-origin'->originicon.read;
        'tv-include'->includeicon.read;
        'tv-body'->bodyicon.read;
        'tv-prjclosed'->prjicon.read;
        'tv-prjopen'->prjopenicon.read;
        &sbprivate.hierarchyView.hir.project[]->rootproject[];
        'Projects'->rootProject.label[];
        rootProject[]->sbprivate.hierarchyView.hir.root;
        (THIS(sourcebrowser)[],true,50,2)->outerctrl.open;
        sbprivate.hierarchyView.hir.changed;
        sbprivate.formview.changed;
        INNER open
     #);
   history: @list
     (#
        names: @nameTree;
        add:
          (# elm: ^element
          enter elm[]
          do elm[]->prepend; (elm.location[],false)->names.uniqName; 
          #);
        element:: 
          (#
             location: ^text;
             theProject: ^object;
             theGroup: ^projects.group;
             theForm: ^astInterface.fragmentForm;
             theCfe: ^codeeditor;
             theView: ^sbprivate.sourceview.overlayType;
             theHirItem: ^sbprivate.hierarchyView.hir.item;
             
          #)
     #);
   CVlist: @list
     (# element::  (# ff: ^astInterface.fragmentForm; cVW: ^window #)
     #);
   geList: @list (* group editors active in this browser *)
     (#
        element::  (# ge: ^editorEnv.groupEditor #);
        findOrCreateGroupEditor:
          (#
             fg: ^astInterface.fragmentGroup;
             ge: ^editorEnv.groupEditor;
             newElm: ^element;
             
          enter fg[]
          do
             find
               (#
                  predicate::  (#  do current.ge.fg[] = fg[]->value #);
                  notFound:: 
                    (# 
                    do
                       &element[]->newElm[];
                       (Interface.id (* ess 980602: was browser[]*) ,fg[],
                        browserEditMode)->edenv.findOrCreateGroupEditor
                         ->newElm.ge[]->ge[];
                       newElm[]->append
                    #)
               do current.ge[]->ge[]
               #)
          exit ge[]
          #);
        closeGroups:
          (# okToClose: @boolean; 
          do
             true->okToClose;
             l: iterate
               (# 
               do
                  (if current.elm.ge.closeGroup->okToClose then
                      current[]->delete
                   else
                      leave l
                  if)
               #)
          exit okToClose
          #);
        closeGroup:
          (# fg: ^astInterface.fragmentGroup; okToClose: @boolean; 
          enter fg[]
          do
             locate
               (#
                  predicate::  (#  do current.elm.ge.fg[] = fg[]->value #); 
               do
                  (if position.elm.ge.closeGroup->okToClose then
                      position[]->delete
                  if)
               #)
          exit okToClose
          #)
     #)
#)  

