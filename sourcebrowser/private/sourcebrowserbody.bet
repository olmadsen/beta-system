ORIGIN '../sourcebrowser';
INCLUDE '~beta/objectbrowser/v2.0/UI/obguienvadds'
        '~beta/objectbrowser/v2.0/UI/listmoveable'
        '~beta/objectbrowser/v2.0/UI/labelled'
        '~beta/guienv/v1.3.1/utils/pane'
        '~beta/basiclib/v1.4/formatio'
        '~beta/basiclib/v1.4/file'
        '~beta/editor/v5.0/fragmentscanner'
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- sourcebrowsergroupslib: Attributes --
appendGroup:
  (# g: ^projects.group; index: @Integer; new: ^element; 
  enter (g[],index)
  do &element[]->new[]; (g[],index)->(new.g[],new.index); new[]->append; 
  #);
  

-- groupsGetGroupByIndex: DoPart --
do
   doscan: scan
     (# where::  (#  do (current.index = index)->value #)
     do current.g[]->g[]; leave doscan; 
     #);
     

-- groupGetIndexByGroup: DoPart --
do
   doscan: scan
     (# where::  (#  do (current.g[] = g[])->value #)
     do current.index->index; leave doscan; 
     #);
     

-- sourcebrowserprivate: Descriptor --
(#
   minHorizControlWidth: (#  exit 100 #);
   initialProjectListViewWidth: (#  exit 146 #);
   initialGroupListViewWidth: (#  exit 146 #);
   initialFormListViewWidth: (#  exit 212 #);
   initialListViewHeight: (#  exit 150 #);
   initialWindowWidth:
     (# 
     exit initialProjectListViewWidth+initialGroupListViewWidth+
     initialFormListViewWidth
     #);
   outerctrl: @pane (* Vertical control containing upctrl and down. *)
     (#
        eventhandler:: 
          (# onFatherFrameChanged::  (#  do father.size->size #) #);
        dobind: (#  do upctrl.dobind; down.dobind;  #);
        open:: 
          (#
             setDefaults:: 
               (#  do TRUE->verticalStacking; 50->minsize; 2->panewidth;  #)
          do
             (outerctrl[],false,50,2)->upctrl.open;
             outerctrl[]->down.open;
             upctrl[]->appendMember;
             upctrl[]->fixedSize;
             down[]->appendMember;
             appendsDone;
             doBind;
             
          #);
        
     #);
   upctrl: @pane
   (* Horizontal group containing projectList, groupList and FragmentGroupView *)
     (#
        dobind:
          (# 
          do projectList.dobind; grouplist.dobind; FragmentGroupView.dobind; 
          #);
        open:: 
          (#
             setDefaults:: 
               (# 
               do
                  FALSE->verticalStacking; minHorizControlWidth div 2->minsize; 
               #)
          do
             upctrl[]->projectList.open;
             upctrl[]->grouplist.open;
             upctrl[]->FragmentGroupView.open;
             projectList[]->appendMember;
             grouplist[]->appendMember;
             FragmentGroupView[]->appendMember;
             appendsDone;
             doBind;
             
          #)
     #);
   projectList: @labelled
     (#
        dobind: (#  do contents.dobindings #);
        shownprojects: @List
          (#
             element::  (# index: @Integer; p: ^projects.project;  #);
             getProjectByIndex:
               (# index: @Integer; p: ^projects.project; 
               enter index
               do
                  doscan: scan
                    (# where::  (#  do (current.index = index)->value #)
                    do current.p[]->p[]; leave doscan; 
                    #);
                  
               exit p[]
               #);
             getIndexByProject:
               (# index: @Integer; p: ^projects.project; 
               enter p[]
               do
                  doscan: scan
                    (# where::  (#  do (current.p[] = p[])->value #)
                    do current.index->index; leave doscan; 
                    #);
                  
               exit index
               #);
             appendProject:
               (# p: ^projects.project; index: @Integer; new: ^element; 
               enter (p[],index)
               do
                  &element[]->new[];
                  (p[],index)->(new.p[],new.index);
                  new[]->append;
                  
               #);
             
          #);
        contentsType:: listmoveableList
          (#
             eventHandler:: 
               (#
                  onMouseUp:: 
                    (# newp: ^projects.project; 
                    do
                       (if not doubleClick then
                           selection.first->getSelectedProject->newp[];
                           (if newp[] <> none then
                               newp[]->THIS(sourcebrowser).setProject
                           if);
                           
                       if);
                       
                    #);
                  
               #);
             getSelectedProject:
               (# value: ^projects.project; inx: @integer
               enter inx
               do
                  (if inx <> 0 then
                      inx->shownprojects.getProjectByIndex->value[]; 
                  if);
                  
               exit value[]
               #);
             setProject:
               (# p: ^projects.project; index: @Integer; 
               enter p[]
               do
                  p[]->shownprojects.getIndexByProject->index;
                  (if index <> 0 then
                      (index,FALSE)->selection.select;
                      selection.scrollIntoView;
                      
                   else
                      selection.clear; 
                  if);
                  
               #);
             scanProjects:
               (# first: ^projects.project; 
               do
                  shownprojects.clear;
                  projects.scanProjects
                    (#
                       index: @Integer;
                       forEach:: 
                         (# 
                         do
                            index+1->index;
                            (if index = 1 then current[]->first[] if);
                            (current[],index)->shownprojects.appendProject;
                            
                         #);
                       
                    #);
                  shownprojects.size->setNumberOfItems;
                  shownprojects.scan
                    (#  do (current.index,current.p.name[])->settext #);
                  projectsLabel->label;
                  
               exit first[]
               #);
             open:: 
               (# first: ^projects.project; 
               do scanProjects; projectlist.contents[]->onProjectListOpen; 
               #);
             
          #);
        open:: 
          (#
             openContents:: 
               (# 
               do
                  (none ,(initialProjectListViewWidth,initialListViewHeight))
                    ->contents.open;
                  TRUE->doneInInner;
                  
               #);
             
          do
             shownprojects.init;
             projectsLabel->label;
             (initialProjectListViewWidth,initialListViewHeight)->size;
             
          #);
        
     #);
   grouplist: @labelled
     (#
        dobind: (#  do contents.dobindings #);
        clear: (#  do emptyProjectListLabel->label; groups.clear;  #);
        contentsType:: listmoveableList
          (#
             clear:
               (# 
               do
                  groups.clear;
                  0->setNumberOfItems;
                  emptyProjectListLabel->label;
                  
               #);
             setgroup:
               (# g: ^projects.group; index: @Integer; 
               enter g[]
               do
                  sbprivate.fragmentGroupView.contents.clear;
                  sbprivate.down.fragmentView.clear;
                  (if g[] = none then
                      selection.clear; 
                   else
                      g[]->groups.getIndexByGroup->index;
                      (if index <> 0 then
                          (index,FALSE)->selection.select;
                          selection.scrollIntoView;
                          g[]->fragmentgroupView.contents.setgroup;
                          
                      if);
                      
                  if);
                  
               #);
             eventHandler:: 
               (#
                  onMouseUp:: 
                    (# 
                    do (if (not doubleClick) and (selection.first <> 0) then
                           (currentProject[],
                            selection.first->groups.getGroupByIndex)
                             ->THIS(sourcebrowser).setGroup;
                           
                       if);
                       
                    #);
                  
               #);
             open::  (#  do grouplist.contents[]->onGroupListOpen;  #);
             addGroup:
               (#
                  p: ^projects.project; g: ^projects.group; index: @Integer; 
               enter (p[],g[])
               do
                  groups.clear;
                  p.scanGroups
                    (#
                       foreach:: 
                         (# 
                         do
                            index+1->index;
                            (current[],index)->groups.appendGroup;
                            
                         #)
                    #);
                  index+1->index;
                  (g[],index)->groups.appendGroup;
                  groups.size->setNumberOfItems;
                  groups.scan
                    (#  do (current.index,current.g.name[])->settext;  #);
                  
               #);
             scanGroups:
               (# p: ^projects.project; first: ^projects.group; 
               enter p[]
               do
                  groups.clear;
                  p.scanGroups
                    (#
                       index: @Integer;
                       foreach:: 
                         (# 
                         do
                            index+1->index;
                            (current[],index)->groups.appendGroup;
                            (if index = 1 then current[]->first[] if);
                            
                         #)
                    #);
                  groups.size->setNumberOfItems;
                  groups.scan
                    (#  do (current.index,current.g.name[])->settext;  #);
                  p.name[]->label;
                  
               exit first[]
               #);
             
          #);
        open:: 
          (#
             openContents:: 
               (# 
               do
                  (none ,(initialGroupListViewWidth,initialListViewHeight))
                    ->contents.open;
                  TRUE->doneInInner;
                  
               #);
             
          do
             groups.init;
             emptyProjectListLabel->label;
             (initialGroupListViewWidth,initialListViewHeight)->size;
             
          #);
        
     #);
   fragmentGroupView: @labelled
     (#
        dobind: (#  do contents.dobindings #);
        contentsType:: listmoveableList
          (#
             fgs: @fragmentGroupScanner;
             g: ^projects.group;
             AST: ^astInterface;
             getFragmentFormByIndex: (* added by JLK *)
               (# index: @Integer; ff: ^astInterface.fragmentForm; 
               enter index
               do
                  doscan: scan
                    (# lc: @integer
                    do
                       (if (lc+1->lc) = index then
                           (index->gettext,screen[])->(g.getfg).open->ff[];
                           leave doscan;
                           
                       if)
                    #);
                  
               exit ff[]
               #);
             getIndexByFragmentForm: (* added by JLK *)
               (#
                  index: @Integer;
                  fg: ^astInterface.fragmentGroup;
                  ff: ^astInterface.fragmentForm;
                  
               enter ff[]
               do
                  (if g[] = none then (*try currentGroup*)
                      currentGroup[]->g[]
                  if);
                  (if g[] <> none then
                      g.getfg->fg[];
                      (if fg[] <> none then
                          (AST[],fg[])->fgs;
                          find: fgs.scanPropsAndFragsForText
                            (#
                               myAddText:: 
                                 (# n: ^text
                                 do
                                    (ff.name).copy->n[];
                                    ':'->n.append;
                                    n.reset;
                                    (0,n.length)->t.sub->t[];
                                    (if t[]->n.equalNCS then
                                        no->index; leave find; 
                                    if)
                                 #)
                            #)
                      if)
                  if)
               exit index
               #);
             setgroup:
               (# inx,lc: @Integer; fg: ^astInterface.fragmentGroup; 
               enter g[]
               do
                  g.getfg->fg[];
                  (if fg[] <> none then
                      (AST[],fg[])->fgs;
                      fgs.scanPropsAndFragsForText
                        (# myAddText::  (#  do lc+1->lc #) #);
                      lc->setNumberOfItems;
                      fgs.scanPropsAndFragsForText
                        (#
                           myAddText:: 
                             (#  do inx+1->inx; (inx,t[])->setText #);
                           
                        #);
                      g.name[]->fragmentGroupView.label;
                      selection.clear;
                      INNER ;
                      
                  if);
                  
               #);
             setFragmentForm: (* added by JLK *)
               (# ff: ^astInterface.fragmentForm; index: @Integer; 
               enter ff[]
               do
                  (if ff[] = none then
                      selection.clear
                   else
                      ff[]->getIndexByFragmentForm->index;
                      (if index <> 0 then
                          (index,FALSE)->selection.select;
                          selection.scrollIntoView;
                          ff[]->down.fragmentView.newFragment;
                          
                      if);
                      
                  if);
                  
               #);
             clear:
               (# 
               do
                  emptyGroupViewLabel->fragmentGroupView.label;
                  none ->g[];
                  0->setNumberOfItems;
                  
               #);
             scrollToFirstFF:
               (# inx: @integer
               do find:
                    (if (g[]<>NONE) and (g.fg[]<>none) then
                        (AST[], g.(*get*)fg[])
                          ->scanPropsAndFragsNEW
                        (# doFragmentForm:: 
                             (#
                             do (inx,FALSE)->selection.select;
                                selection.scrollIntoView;
                                inx->selection.deselect;
                                leave find
                             #);
                        #); 
                    if)
               #);
             doSelect:
               (# inx: @integer
               enter inx
               do (if inx <> 0 then
                      find:
                        (g.getfg,inx)
                        ->fgs.scanPropsAndFrags
                      (#
                         doProperty:: 
                           (# t: ^Text; 
                           do (* ORIGIN,BODY, MDBODY or MAKE *)
                              sbprivate.down.fragmentView.clear;
                              none ->currentForm[]; none ->onCurrentFormChange;
                              (inx,false)->selection.select;
                              selection.scrollIntoView;
                              leave find;
                           #);
                         doFragmentLink:: 
                           (# 
                           do (* INCLUDE *)
                              (inx,false)->selection.select;
                              sbprivate.down.fragmentView.clear;
                              none ->currentForm[]; none ->onCurrentFormChange;
                              selection.scrollIntoView;
                              leave find;
                           #);
                         doFragmentForm:: 
                           (# 
                           do (currentProject[],currentGroup[],fle.open)
                                ->THIS(sourceBrowser).setFragmentForm;
                              leave find;
                           #);
                      #);
                  if)
               #);
             eventHandler:: 
               (# onMouseUp:: 
                    (# inx: @Integer; 
                    do (if not doubleClick and
                           not shiftKey and
                           ((selection.first->inx) <> 0) then
                           find:
                           (g.getfg,inx)
                             ->fgs.scanPropsAndFrags
                               (# doFragmentForm:: 
                                    (# 
                                    do (currentProject[],currentGroup[],fle.open)
                                         ->THIS(sourceBrowser).setFragmentForm;
                                       leave find;
                                    #);
                               #);
                       if);
                    #);
                  onMouseDown:: 
                    (# inx: @Integer; 
                    do (if doubleClick and
                           not shiftKey and
                           ((selection.first->inx) <> 0) then
                           find:
                           (g.getfg,inx)
                             ->fgs.scanPropsAndFrags
                               (# doProperty:: 
                                    (# t: ^Text; 
                                    do (* ORIGIN,BODY, MDBODY or MAKE *)
                                       (if true
                                        // 'ORIGIN'->prop.equalNCS
                                        // 'BODY'->prop.equalNCS
                                        // 'MDBODY'->prop.equalNCS then
                                           s[]->followLink; 
                                        // 'MAKE'->prop.equalNCS then
                                           s[]->makeFileEdit; 
                                       if);
                                       leave find;
                                    #);
                                  doFragmentLink:: 
                                    (# fl: ^astInterface.FragmentLink; 
                                    do (* INCLUDE *)
                                       fle.open->fl[]; fl.name->followLink; 
                                       leave find;
                                    #);
                               #);
                       if);
                    #);
               #);
             followLink:
               (# groupName: ^Text; s,t: ^Text; 
               enter s[]
               do
                  (g.getfg).fullName->AST.stripPathName->t[];
                  (if t[] = none then
                      s.copy
                        ->AST.expandtofullpath
                        ->THIS(sourceBrowser).followLink;
                      
                   else
                      (s.copy,t[])->AST.thePathHandler.convertFilePath
                        ->THIS(sourceBrowser).followLink;
                      
                  if);
                  
               #);
             open:: 
               (# 
               do
                  editorencloser.getAST->AST[];
                  fragmentgroupview.contents[]->onFragmentgroupViewOpen;
                  
               #);
             
          #);
        open:: 
          (#
             openContents:: 
               (# 
               do
                  (none ,(initialFormListViewWidth,initialListViewHeight))
                    ->contents.open;
                  TRUE->doneInInner;
                  
               #);
             
          do
             emptyGroupViewLabel->label;
             (initialFormListViewWidth,initialListViewHeight)->size;
             
          #);
        
     #);
   down: @Canvas
     (#
        dobind: (#  do fragmentView.dobind #);
        open:: 
          (# 
          do
             (initialWindowWidth,2*initialListViewHeight)->size;
             (down[],size)->fragmentView.open;
             
          #);
        fragmentView: @labelled
          (#
             dobind:
               (#  do contents.dobind; TRUE->bindRight->bindBottom;  #);
             lastFragment: ^astInterface.fragmentForm;
             clearing: @Boolean;
             clear:
               (# 
               do
                  TRUE->clearing;
                  contents.contents.all->contents.contents.selection.set;
                  contents.contents.delete;
                  none ->newfragment;
                  none ->lastFragment[];
                  FALSE->clearing;
                  
               #);
             newfragment:
               (# ff: ^astInterface.fragmentForm; 
               enter ff[]
               do
                  (if true
                   // ff[] = none then
                      emptyCodeViewLabel->fragmentView.label; 
                   // lastFragment[] = none
                   // not (lastFragment.root[]->ff.root.equal) then
                      ff[]->lastFragment[];
                      ff.name->fragmentView.label
                  if);
               #);
             contentsType:: codemoveableEditor
               (# isolated:: trueObject;
                  dobind:
                    (# 
                    do TRUE->bindRight->bindBottom->bindLeft->bindTop
                    #);
                  selectNode:: 
                    (#  do (theEditorRoot[],node[],separate)->THIS(sourceBrowser).selectNode #);
                  open:: 
                    (#  do fragmentView.contents[]->onCodeViewOpen;  #);
                  newfragment:: 
                    (# 
                    do
                       (if editorRoot[] <> none then
                           (if editorRoot.frag[] <> none then
                               editorRoot.frag.name->fragmentView.label; 
                            else
                               emptyCodeViewLabel->fragmentView.label; 
                           if)
                        else
                           emptyCodeViewLabel->fragmentView.label; 
                       if);
                       
                    #);
                  initialContractionLevel::  (#  do 1->value #);
                  charWidthType:: 
                    (# 
                    do
                    (* HACK NEEDED UNTIL textEditor.defaultStyle gets 
                     * implemented !!! *)
                       THIS(fragmentView).private.theLabel.style->ts[]; 
                    #);
                  contentsType:: 
                    (#
                       eventHandler:: 
                         (# onBeforeChange:: 
                              (# 
                              do (if not allow then clearing->allow if)
                              #);
                            
                         #);
                       
                    #);
                  getEncloser::  (#  do editorEncloser[]->theEncloser[] #);
                  
               #);
             open:: 
               (#
                  sz: @Point;
                  openContents:: 
                    (# 
                    do (none ,sz)->contents.open; TRUE->doneInInner; 
                    #);
                  
               enter sz
               do sz->size; none ->lastFragment[]; none ->newfragment; 
               #);
             
          #);
        
     #);
   open:
     (# 
     do
        projects.init;
        (THIS(sourcebrowser)[],true,50,2)->outerctrl.open;
        INNER ;
        outerctrl.size->THIS(sourcebrowser).size;
        
     #);
   
#)  

-- sourcebrowseropen: DoPart --
do sbprivate.open (#  do INNER open #)  

-- sourcebrowserProjectsChanged: DoPart --
do
   sbprivate.projectlist.contents.scanProjects;
   none ->sbprivate.projectlist.contents.setProject;
   none ->currentProject[];
   none ->onCurrentProjectChange;
   sbprivate.grouplist.contents.clear;
   none ->currentGroup[];
   none ->onCurrentGroupChange;
   sbprivate.fragmentGroupView.contents.clear;
   sbprivate.down.fragmentView.clear;
   none ->currentForm[];
   none ->onCurrentFormChange;
     

-- sourcebrowserSetProject: DoPart --
do
   cursors.watch[]->mouse.busycursor;
   p[]->sbprivate.projectlist.contents.setProject;
   (if true
    // p[] = none then
       sbprivate.grouplist.contents.clear;
       sbprivate.fragmentGroupView.contents.clear;
       sbprivate.down.fragmentView.clear;
       
    // p[] <> currentProject[] then
       p[]->sbprivate.grouplist.contents.scanGroups; 
   if);
   (if p[] <> currentProject[] then
       p[]->currentProject[]; p[]->onCurrentProjectChange; 
   if);
   INNER ;
   (if not setGroupDoneInInner then
       (if (currentProject[] <> none ) then
           (if groups.size = 1 then (* automatically select it *)
               (groups.head).elm.g[]->currentProject.lastGroupSelected[]
           if);
           currentProject.lastGroupSelected[]
             ->sbprivate.grouplist.contents.setgroup;
           currentProject.lastGroupSelected[]->currentGroup[];
           currentProject.lastGroupSelected[]->onCurrentGroupChange;
           
        else
           none ->sbprivate.grouplist.contents.setgroup;
           sbprivate.fragmentGroupView.contents.clear;
           none ->currentGroup[];
           none ->onCurrentGroupChange;
           
       if)
   if);
   (if not setFormDoneInInner then
       (if (currentGroup[] <> none ) then
           (if (currentGroup.lastFormSelected[] = none ) then
               (if currentGroup.fg[] <> none then
                   l: currentGroup.fg.fragmentList.scan
                     (#
                        ff: ^astInterface.fragmentForm;
                        (* find first FF to select it automatically *)
                        
                     do
                        (if current.type
                         // sbprivate.fragmentGroupView.contents.ast.formType
                            then
                            (if currentGroup.lastFormSelected[]=none then
                                current.open->currentGroup.lastFormSelected[];
                             else (* only select if only one ff in group *)
                                none->currentGroup.lastFormSelected[];
                                leave l
                            if)
                        if);
                        
                     #)
               if);
               
           if);
           (if (currentGroup[] <> none ) and
           (currentGroup.lastFormSelected[] <> none ) then
               edenv.history.protect
               (#
               do currentGroup.lastFormSelected[]
                    ->sbprivate.fragmentGroupView.contents.setfragmentForm;
                  currentGroup.lastFormSelected[]->currentForm[];
                  currentGroup.lastFormSelected[]->onCurrentFormChange;
               #)
            else
               sbprivate.fragmentGroupView.contents.scrollToFirstFF;
               sbprivate.down.fragmentView.clear;
               none ->currentForm[]; none ->onCurrentFormChange; 
           if)
        else
           sbprivate.fragmentGroupView.contents.clear;
           sbprivate.down.fragmentView.clear;
           none ->currentForm[]; none ->onCurrentFormChange;
           
       if)
   if);
   none ->mouse.busycursor;
     

-- sourcebrowserSetGroup: DoPart --
do
   TRUE->setGroupDoneInInner;
   (if g[] <> currentGroup[] then
       g[]->sbprivate.grouplist.contents.setgroup;
       g[]->currentGroup[];
       g[]->onCurrentGroupChange;
       
   if);
   INNER ;
     

-- sourcebrowserSetFragmentForm: DoPart --
do
   TRUE->setFormDoneInInner;
   (if ff[] <> currentForm[] then
       ff[]->sbprivate.fragmentGroupView.contents.setfragmentForm;
       ff[]->currentForm[];
       ff[]->oncurrentFormChange;
       
   if);
     

-- sourcebrowserGetFragmentFormEditor: DoPart --
do sbprivate.down.fragmentView.contents[]->theEditor[]  

