ORIGIN '../ymer';

INCLUDE 'executeWindow';
INCLUDE 'compilerPreferences';

--- fragmentBrowserLib: attributes ---
compileWindow: executeWindow
  (# preferenceDialog: @compilerPrefDialog;
     menubarType::<
       (# compileMenu: @menu
            (# iPreferences: @menuItem
                 (# eventhandler::
                      (# onSelect:: (# do preferenceDialog.showmodal #)
                      #);
                    open:: (# do 'Preferences'->name #)
                 #);
               open::
                 (# sep: ^menuItem;
                 do 'Compile'->name;
                    &separator[]->sep[]; sep.open; sep[]->append;
                    iPreferences.open; iPreferences[]->append;
                 #);
            #);
          open::<
            (#
            do compileMenu.open; compileMenu[]->append;
               INNER
            #)
       #);
     executeStartMsg::  (#  do 'Compilation start'->value #);
     executeRestartMsg::  (#  do 'Recompilation start'->value #);
     executeAbortMsg::  (#  do 'Compilation ABORT'->value #);
     executeEndMsg::  (#  do 'Compilation terminated'->value #);
     repeatLabel::  (#  do 'Recompile'->value #);
     doExecute:: 
       (# 
       do preferenceDialog.compilerSwitches[]
            ->programInputPipe.writeend.puttext;
          ' '->programInputPipe.writeend.puttext;
          files[]->programInputPipe.writeend.puttext;
          programInputPipe.writeend.newline;
          programInputPipe.private.writech.flush;
          
       #);
     doRepeat::< 
       (# 
       do INNER;
          preferenceDialog.compilerSwitches[]
            ->programInputPipe.writeend.puttext;
          ' '->programInputPipe.writeend.puttext;
          files[]->programInputPipe.writeend.puttext;
          programInputPipe.writeend.newline;
          programInputPipe.private.writech.flush;
          
       #);
     open::<
       (#
       do preferenceDialog.open;
          INNER #);
     files: ^text
  #);
singleCompileWindow: compileWindow
  (# doRepeat::  (#  do setupNewProcess #);  #);
repeatingCompileWindow: compileWindow
  (# open::  (#  do '--repeat'->arguments.append #);  #);
