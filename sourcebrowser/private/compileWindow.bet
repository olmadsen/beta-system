ORIGIN '../ymer';

INCLUDE 'executeWindow';
INCLUDE 'compilerPreferences';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- fragmentBrowserLib: attributes ---
compileWindow: executeWindow
  (# preferenceDialog: @compilerPrefDialog;
     menubarType::<
       (# compileMenu: @menu
            (# iPreferences: @menuItem
                 (# eventhandler::
                      (# onSelect:: (# do preferenceDialog.showmodal #)
                      #);
                    open:: (# do 'Preferences'->name #)
                 #);
               open::
                 (# sep: ^menuItem;
                 do 'Compile'->name;
                    &separator[]->sep[]; sep.open; sep[]->append;
                    iPreferences.open; iPreferences[]->append;
                 #);
            #);
          open::<
            (#
            do compileMenu.open; compileMenu[]->append;
               INNER
            #)
       #);
     executeStartMsg::  (#  do 'Compilation start'->value #);
     executeRestartMsg::  (#  do 'Recompilation start'->value #);
     executeAbortMsg::  (#  do 'Compilation ABORT'->value #);
     executeEndMsg::  (#  do 'Compilation terminated'->value #);
     repeatLabel::  (#  do 'Recompile'->value #);
     doExecute:: 
       (# t: ^text
       do &text[]->t[];
          preferenceDialog.compilerSwitches[]
            ->t.puttext;
          ' '->t.put; files[]->t.puttext;
          t[]->execSystem.puttext;
       #);
     doRepeat::< 
       (# t: ^text
       do INNER;
          &text[]->t[];
          preferenceDialog.compilerSwitches[]
            ->t.puttext;
          ' '->t.put; files[]->t.puttext;
          t[]->execSystem.puttext;
       #);
     open::<
       (#
       do preferenceDialog.open;
          INNER #);
     files: ^text
  #);
singleCompileWindow: compileWindow
  (# doRepeat::  (#  do setupNewProcess #);  #);
repeatingCompileWindow: compileWindow
  (# open::  (#  do '--repeat'->arguments.append #);  #);
