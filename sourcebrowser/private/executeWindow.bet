ORIGIN '../ymer';

INCLUDE '~beta/guienv/v1.4/guienvsystemenv';

INCLUDE 'executeSystem';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- fragmentBrowserLib: attributes ---
executeWindow: ymerLocalWindow
  (# theSystemEnv: ^systemEnv;
     executeSystem: theSystemEnv.executeSystem
       (# sink::
            (# l: @integer
            do t[]->terminal.contents.insert;
               terminal.contents.length->l;
               (l,l)->terminal.contents.selection.set;
               terminal.contents.selection.scrollIntoView;
            #)
       #);
     execSystem: ^|executeSystem;
     textMsg:
       (# value: @text
       do INNER
       exit
          (# msg: ^text
         do '--- '->msg[]; value[]->msg.puttext; ' ---\n'->msg.append
         exit msg[]
         #)
       #);
     executeStartMsg:< textMsg (#  do 'Execution start'->value; INNER #);
     executeRestartMsg:< textMsg
       (#  do 'Execution restart'->value; INNER #);
     repeatLabel:<
       (# value: ^text do 'Reexecute'->value[]; INNER exit value[] #);
     executeAbortMsg:< textMsg (#  do 'Execution ABORT'->value; INNER #);
     executeEndMsg:< textMsg
       (#  do 'Execution terminated'->value; INNER #);
     onAbort:< (# do INNER #);
     repeatButton: @pushButton
       (#
          setFrame:
            (# x,y,w,h: @integer 
            do
               recalc;
               distance->x;
               distance->y;
               (windowWidth-distance*3)/2->w;
               height+distance->h;
               ((x,y),(x+w,y+h))->frame;
            #);
          open::  (#  do setFrame; repeatLabel->label #);
          eventhandler:: 
            (#
               onMouseUp::  (#  do doRepeat #);
               onFatherFrameChanged::  (#  do setFrame #)
            #)
       #);
     abortButton: @pushButton
       (#
          setFrame:
            (#  x,y,w,h: @integer
            do
               recalc;
               (windowWidth-distance*3)/2+distance*2->x;
               distance->y;
               (windowWidth-distance*3)/2->w;
               height+distance->h;
               ((x,y),(x+w,y+h))->frame;
            #);
          open::  (#  do setFrame; 'Abort'->label #);
          eventhandler:: 
            (#
               onMouseUp:: 
                 (# l: @integer
                 do execSystem.abortSystem;
                    onAbort
                 #);
               onFatherFrameChanged::  (#  do setFrame #)
            #)
       #);
     terminal: @textEditor
       (#
          contentsType:: 
            (#
               eventhandler:: 
                 (#
                    t: @text;
                    onKeydown:: 
                      (# 
                      do (if ch
                          // ascii.bs then
                             (if t.length > 0 then
                                 (t.length,t.length)->t.delete
                             if)
                          // ascii.cr then
                             t[]->execSystem.puttext;
                             t.clear
                          else
                             ch->t.put; 
                         if)
                      #)
                 #)
            #);
          setFrame:
            (# x,y,w,h: @integer
            do
               recalc;
               distance->x;
               height+distance*2->y;
               windowWidth-distance*2->w;
               windowHeight-height-distance*3->h;
               ((x,y),(x+w,y+h))->frame;
            #);
          open::  (#  do setFrame; true->bindBottom->bindRight;  #);
          eventhandler:: 
            (# onFatherFrameChanged::  (#  do setFrame #) #)
       #);
     doExecute:<
       (# l: @integer; 
       do
          executeStartMsg->terminal.contents.insert;
          terminal.contents.length->l;
          (l,l)->terminal.contents.selection.set;
          terminal.contents.selection.scrollIntoView;
          setupNewProcess;
          INNER ;
          
       #);
     doRepeat:<
       (# l: @integer; 
       do
          executeRestartMsg->terminal.contents.insert;
          terminal.contents.length->l;
          (l,l)->terminal.contents.selection.set;
          terminal.contents.selection.scrollIntoView;
          INNER ;
          
       #);
     eventhandler:: 
       (#
          onAboutToClose:: 
            (# do execSystem.abortSystem #)
       #);
     height,distance: @integer;
     windowWidth,windowHeight: @integer;
     BETAprogram: ^text;
     arguments: ^text;
     recalc: (#  do size->(windowWidth,windowHeight) #);
     tst: @pushButton
       (#
          open:: 
            (# ts: ^textStyle; buttonLabel: (#  exit 'more choises' #); 
            do
               buttonLabel->label;
               style->ts[];
               ts.lineHeight+3->height;
               height+8->height;
               
            #)
       #);
     setupNewProcess:
       (#
       do getSystemEnv->theSystemEnv[];
          &|executeSystem[]->execSystem[]->execSystem.thisSystem[];
          executeAbortMsg->execSystem.abortMsg[];
          executeEndMsg->execSystem.endMsg[];
          (BETAprogram[]->mps.AST.expandToFullPath,arguments[])
            ->execSystem.forkSystem;
       #);
     open::< 
       (# t: @text
       do tst.open;
          (* just to get the width and height of fonts *)
          tst.hide;
          tst.close;
          3->distance;
          (600,400)->size;
          repeatButton.open;
          abortButton.open;
          terminal.open;
          INNER ;
          BETAprogram->t;
          ' '->t.put;
          (if arguments[] <> none then
              arguments[]->t.puttext; ' '->t.put
          if);
          t.copy->title;
       #)
  #)
