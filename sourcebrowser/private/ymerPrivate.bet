ORIGIN '../ymer';
INCLUDE 'sourcebrowserbody';
INCLUDE 'uniqNames';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- ymerBrowserPrivate: Descriptor --
(#
   history: @list
     (# names: @nameTree;
	add:
	  (# elm: ^element
	  enter elm[]
	  do elm[]->prepend;
	     (elm.theGroup.location[],false)->names.uniqName
	  #);
	element::
	  (#
	     theProject: ^browser.projects.project;
	     theGroup: ^browser.projects.group;
	     theForm: ^MPS.ast.fragmentForm;
	     theCfe: ^codeeditor;
	     theView: ^browser.sbprivate.down.overlayType;
	  #)
     #);
  geList: @list (* group editors active in this browser *)
     (#
	element::  (# ge: ^editorEnv.groupEditor #);
	findOrCreateGroupEditor:
	  (#
	     fg: ^MPS.ast.fragmentGroup;
	     ge: ^editorEnv.groupEditor;
	     newElm: ^element;

	  enter fg[]
	  do
	     find
	       (#
		  predicate::  (#  do current.ge.fg[] = fg[]->value #);
		  notFound::
		    (#
		    do
		       &element[]->newElm[];
		       (browser[],fg[], editMode)->edenv.createGroupEditor->newElm.ge[]
			 ->ge[];
		       newElm[]->append
		    #)
	       do current.ge[]->ge[]
	       #);

	  exit ge[]
	  #);
	closeGroups:
	  (# okToClose: @boolean;
	  do
	     true->okToClose;
	     l: iterate
	       (#
	       do (if current.elm.ge.closeGroup->okToClose then
		      current[]->delete
		   else
		      leave l
		  if)
	       #)
	  exit okToClose
	  #);
	closeGroup:
	  (# fg: ^MPS.ast.fragmentGroup;  okToClose: @boolean;
	  enter fg[]
	  do
	     locate
	       (#
		  predicate::  (#  do current.elm.ge.fg[] = fg[]->value #);
	       do (if position.elm.ge.closeGroup->okToClose then
		      position[]->delete
		  if)
	       #)
	  exit okToClose
	  #)
     #);
   CVlist: @list
     (#
	element::  (# ff: ^mps.AST.fragmentForm; cVW: ^ymerLocalWindow #);

     #);

#)

-- ymerPrivate: Descriptor --
(#
   workspaceCount: @integer;
   helpDocuments: @list (# element::  (# title,path: ^text #) #);
   edenv: ^object;
#)
