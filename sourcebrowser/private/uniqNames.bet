ORIGIN '~beta/basiclib/v1.4/betaenv';
INCLUDE '~beta/basiclib/v1.4/file';
INCLUDE '~beta/containers/v1.4/list';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- lib: attributes ---
nameElm: (# n: ^text; dirs: @nameTree #);
nameTree: list
  (# element:: nameElm;
     uniqName:
       (# fn, n, un: ^text; f: @file; level: ^nameTree;
          new: ^nameElm; uniq: @boolean
       enter (fn[],uniq)
       do uniqName:
            (#
            do fn[]->f.name; this(nameTree)[]->level[];
               l: (if (f.name).length>0 then
                      f.entry.path.name->n[];
                      level.find
                      (# predicate::
                           (# do n[]->current.n.equalNCS->value #);
                         notFound::
                           (#
                           do &nameElm[]->new[]; n[]->new.n[];
                              new[]->level.append; new.dirs[]->level[];
                           #)
                      do (if uniq then
                             (if un[]=NONE then
                                 &text[]->un[]
                              else
                                 '/'->un.prepend
                             if);
                             n[]->un.prepend;
                             (if current.dirs.size<=1 then
                                 leave uniqName
                             if)
                         if);
                         current.dirs[]->level[];
                      #);
                      f.entry.path.head->f.name;
                      restart l
                  if)
            #)
       exit un[]
       #)
  #);
(* 
 * --- program: descriptor ---
 * (# top: @nameTree
 * do
 *    ('~beta/containers/v1.4/list',false)->top.uniqName;
 *    ('~beta/containers/v1.4/recList',false)->top.uniqName;
 *    ('~beta/containers/v1.4.1/list',false)->top.uniqName;
 *    {*('~beta/basiclib/v1.4/list',false)->top.uniqName;*}
 *    ('~jlk/basiclib/v1.4/list',false)->top.uniqName;
 *    
 *    ('~beta/containers/v1.4/list',true)->top.uniqName->putline;
 *    ('~beta/containers/v1.4/recList',true)->top.uniqName->putline;
 *    ('~beta/containers/v1.4.1/list',true)->top.uniqName->putline;
 *    {*('~beta/basiclib/v1.4/list',true)->top.uniqName->putline;*}
 *    ('~jlk/basiclib/v1.4/list',true)->top.uniqName->putline;
 *    
 * #)
 *)
