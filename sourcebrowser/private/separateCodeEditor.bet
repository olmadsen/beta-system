ORIGIN '../ymer';
INCLUDE 'ymerPrivate'
'ymerFileUtils';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
---  codeViewWindowFileQuit: dopart ---
do (if codeViewWindowPrivate.ge.closeGroup then
       THIS(codeViewWindow).close;
   if)
   
--- codeViewWindowPrivate: descriptor ---
(# codeview: @codemoveableEditor
     (# isolated:: trueObject;
        selectNode:: 
          (# t: ^text
          do (if true
              // (theEditorRoot.frag.root[]->theEditorRoot.equal) and separate then
                 (theEditorRoot.frag[],node[])->viewSeparate
              // (theEditorRoot.frag.root[]->theEditorRoot.equal) and not separate then
                 theEditorRoot.frag[]->ff[];
                 (ff[],ff.root[],none ,codeViewWindowPrivate.codeview[],codeViewWindowPrivate.ge[])->edenv.CreateFormEditor->fe[];
                 (ff.father).fullname->fragmentDefaultName->t[];
                 '-'->t.append;
                 ff.name->t.append;
                 t[]->title;
                 node[]->fe.setNode
              // (not (theEditorRoot.frag.root[]->theEditorRoot.equal)) and separate then
                 (theEditorRoot[],node[])->fe.openSubeditor
              else
                 'separateCodeEditor.selectnode: theEditorRoot[]<> theEditorRoot.frag.root[] and not separate'
                   ->putLine
             if)  
          #);
        getEncloser::  (#  do codeviewEncloser[]->theEncloser[] #);
        open::  (# 
                do true->bindBottom->bindTop->bindRight->bindLeft;
                   codeViewWindowPrivate.codeview[]->onCodeViewOpen
                #);
        
     #);
   codeviewEncloser: @codemoveableEditorEncloser
     (#
        tryclose::
          (#
          do (if codeViewWindowPrivate.ge.closeGroup then
                 THIS(codeViewWindow).close;
             if)
          #);
        onHighLight::  (#  do THIS(codeViewWindow).wriggle #);
        getAST::  (#  do mps.AST[]->AST[] #);
        getBETACFL::  (#  do mps.BETACFL[]->BETACFL[] #);
        
     #);
   ge: ^editorEnv.groupEditor;
#)

--- codeViewWindowOnAboutToClose: dopart ---
do (if codeViewWindowPrivate.ge.closeGroup->okToClose then
       closeDown->okToClose
   if);
   INNER
   
--- codeViewWindowCloseDown: dopart ---
do ymerBrowserPrivate.CVlist.locate
   (# predicate:: 
        (# do current.elm.cVW[]=THIS(codeViewWindow)[]->value #)
   do position[]->ymerBrowserPrivate.CVlist.delete
   #);
   INNER
   
---  codeViewWindowOpen: dopart ---
do (# t: ^text
   do (THIS(codeViewWindow).contents,size)->codeViewWindowPrivate.codeview.open;
      (none ,ff.father, editMode)->edenv.findOrCreateGroupEditor->codeViewWindowPrivate.ge[];
      (ff[],ff.root[],none ,codeViewWindowPrivate.codeview[],codeViewWindowPrivate.ge[])->edenv.CreateFormEditor->fe[];
      (ff.father).fullname->fragmentDefaultName->t[];
      '-'->t.append;
      ff.name->t.append;
      t[]->title;
      INNER open
   #)
   
--- viewSeparate: dopart ---
do
   l: ymerBrowserPrivate.CVlist.scan
     (# 
     do (if current.ff[] = ff[] then current.cVW[]->cVW[]; leave l if)
     #);
   (if cVW[] = none then
       &codeViewWindow[]->cVW[];
       ff[]->cVW.ff[];
       cVW.open;
       (# newCVelement: ^ymerBrowserPrivate.CVlist.element
       do &ymerBrowserPrivate.CVlist.element[]->newCVelement[]
            ->ymerBrowserPrivate.CVlist.append;
          ff[]->newCVelement.ff[];
          cVW[]->newCVelement.cVW[]
       #)
    else
       cVW.wriggle; 
   if);
   (if (ff[] <> none ) and (node[] = none ) then ff.root[]->node[] if);
   node[]->cVW.fe.setNode
   

