ORIGIN '../ymer';
INCLUDE 'ymerPrivate'
        'ymerFileUtils';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- fragmentBrowserLib: Attributes --
codeViewWindow: ymerLocalWindow
  (#
     menubarType:: 
       (#
          fileMenu: @menu
            (#
               quitItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::
                           (#
                           do (if ge.closeGroup then
                                  THIS(codeViewWindow).close;
                              if)
                           #)
                      #);
                    open::  (#  do 'w'->key;  'Close'->name #)
                 #);
               open:: 
                 (#  do 'File'->name; quitItem.open; quitItem[]->append #)
            #);
          Edit: @codeEditorEditMenu
            (#
               formEditorExists::  (#  do fe[]->cfe[] #);
               writeStatus::  (#  do t[]->infoView.msg #)
            #);
          View: @codeEditorViewMenu
            (# formEditorExists::  (#  do fe[]->cfe[] #);  #);
          SLOTs: @codeEditorSLOTsMenu
            (# formEditorExists::  (#  do fe[]->cfe[] #);  #);
          open:: 
            (# 
            do
               fileMenu.open;
               fileMenu[]->append;
               edit.open; edit[]->append; 
               view.open;
               view[]->append;
               slots.open;
               slots[]->append;
               
            #)
       #);
     ff: ^mps.AST.fragmentForm;
     codeview: @codemoveableEditor
       (# isolated:: trueObject;
          selectNode:: 
            (# t: ^text
            do (if true
                // (theEditorRoot.frag.root[]->theEditorRoot.equal) and separate then
                   (theEditorRoot.frag[],node[])->viewSeparate
                // (theEditorRoot.frag.root[]->theEditorRoot.equal) and not separate then
                   theEditorRoot.frag[]->ff[];
                   (ff[],ff.root[],none ,codeview[],ge[])->edenv.CreateFormEditor->fe[];
                   (ff.father).fullname->fragmentDefaultName->t[];
                   '-'->t.append;
                   ff.name->t.append;
                   t[]->title;
                   node[]->fe.setNode
                // (not (theEditorRoot.frag.root[]->theEditorRoot.equal)) and separate then
                   (theEditorRoot[],node[])->fe.openSubeditor
                else
                   'separateCodeEditor.selectnode: theEditorRoot[]<> theEditorRoot.frag.root[] and not separate'
                     ->putLine
               if)  
            #);
          getEncloser::  (#  do codeviewEncloser[]->theEncloser[] #);
          open::  (# 
                  do true->bindBottom->bindTop->bindRight->bindLeft;
                     codeview[]->onCodeViewOpen
                  #);
          
       #);
     codeviewEncloser: @codemoveableEditorEncloser
       (#
          tryclose::
            (#
            do (if ge.closeGroup then
                   THIS(codeViewWindow).close;
               if)
            #);
          onHighLight::  (#  do THIS(codeViewWindow).wriggle #);
          getAST::  (#  do mps.AST[]->AST[] #);
          getBETACFL::  (#  do mps.BETACFL[]->BETACFL[] #);
          
       #);
     closeDown::
       (#
       do ymerBrowserPrivate.CVlist.locate
          (# predicate:: 
               (# do current.elm.cVW[]=THIS(codeViewWindow)[]->value #)
          do position[]->ymerBrowserPrivate.CVlist.delete
          #)
       #);
     eventhandler:: 
       (# onAboutToClose:: 
            (# do (if ge.closeGroup->okToClose then
                      closeDown->okToClose
                  if)
            #)
       #);
     ge: ^editorEnv.groupEditor;
     fe: ^codeeditor;
     open:: 
       (# t: ^text; 
       do
          (THIS(codeViewWindow).contents,size)->codeview.open;
          (none ,ff.father, editMode)->edenv.findOrCreateGroupEditor->ge[];
          (ff[],ff.root[],none ,codeview[],ge[])->edenv.CreateFormEditor->fe[];
          (ff.father).fullname->fragmentDefaultName->t[];
          '-'->t.append;
          ff.name->t.append;
          t[]->title;
          THIS(codeViewWindow)[]->onSeparateCodeEditorOpen;
       #);
     
  #);
viewSeparate:
  (#
     ff: ^mps.AST.fragmentForm;
     node: ^mps.AST.ast;
     cVW: ^codeViewWindow;
     newCVelement: ^ymerBrowserPrivate.CVlist.element
  enter (ff[],node[])
  do
     l: ymerBrowserPrivate.CVlist.scan
       (# 
       do (if current.ff[] = ff[] then current.cVW[]->cVW[]; leave l if)
       #);
     (if cVW[] = none then
         &codeViewWindow[]->cVW[];
         ff[]->cVW.ff[];
         cVW.open;
         &ymerBrowserPrivate.CVlist.element[]->newCVelement[]
           ->ymerBrowserPrivate.CVlist.append;
         ff[]->newCVelement.ff[];
         cVW[]->newCVelement.cVW[]
      else
         cVW.wriggle; 
     if);
     (if (ff[] <> none ) and (node[] = none ) then ff.root[]->node[] if);
     node[]->cVW.fe.setNode
  exit cVW[]
  #);
  

