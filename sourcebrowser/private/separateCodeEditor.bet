ORIGIN '../ymer';
INCLUDE 'ymerPrivate'
        'ymerFileUtils';
-- fragmentBrowserLib: Attributes --
codeViewWindow: ymerLocalWindow
  (#
     menubarType:: 
       (#
          Edit: @codeEditorEditMenu
            (#
               formEditorExists:: 
                 (#  do fe[]->cfe[] #);
               writeStatus::  (#  do  #)
            #);
          View: @codeEditorViewMenu
            (# 
               formEditorExists:: 
                 (#  do fe[]->cfe[] #);
            #);
          SLOTs: @codeEditorSLOTsMenu
            (#
               formEditorExists:: 
                 (#  do fe[]->cfe[] #);
            #);
          open:: 
            (# 
            do
               (if not codeview.readonly then
                   edit.open; edit[]->append;
               if);
               view.open; view[]->append;
               slots.open; slots[]->append;
            #)
       #);
     ff: ^mps.AST.fragmentForm;
     codeview: @codemoveableEditor
       (#
          selectNode:: 
            (# cVW: ^codeViewWindow
            do
               node.frag[]->viewSeparate->cVW[];
               node[]->cVW.codeview.SifViewer.setNode;
               
            #);
          getEncloser::  (#  do codeviewEncloser[]->theEncloser[] #);
          open::  (#  do true->bindBottom->bindTop->bindRight->bindLeft #);
          
       #);
     codeviewEncloser: @codemoveableEditorEncloser
       (#
          tryclose::  (#  do THIS(codeViewWindow).close #);
          onHighLight::  (#  do THIS(codeViewWindow).wriggle #);
          getAST::  (#  do mps.AST[]->AST[] #);
          getBETACFL::  (#  do mps.BETACFL[]->BETACFL[] #);
          
       #);
     eventhandler:: 
       (#
          onAboutToClose:: 
            (# 
            do
               (if (browser.getFragmentFormEditor).closeGroup->okToClose then
                   ymerBrowserPrivate.CVlist.locate
                     (#
                        predicate:: 
                          (# 
                          do position.elm.cVW[] = THIS(codeViewWindow)[]->value
                          #)
                     do position[]->ymerBrowserPrivate.CVlist.delete
                     #)
               if)
            #)
       #);
     ge: ^editorEnv.groupEditor;
     fe: ^browser.sbprivate.down.fragmentView.contents.codeViewerType;
     open:: 
       (# t: ^text; 
       do
          (THIS(codeViewWindow).contents,size)->codeview.open;
          ff.father->edenv.findOrCreateGroupEditor->ge[];
          (ff[],codeview[],ge[])->edenv.CreateFormEditor->fe[];
          (ff.father).fullname->fragmentDefaultName->t[];
          '-'->t.append;
          ff.name->t.append;
          t[]->title;
          
       #);
     
  #);
viewSeparate:
  (#
     ff: ^mps.AST.fragmentForm;
     cVW: ^codeViewWindow;
     newCVelement: ^ymerBrowserPrivate.CVlist.element
  enter ff[]
  do
     l: ymerBrowserPrivate.CVlist.scan
       (# 
       do (if current.ff[] = ff[] then current.cVW[]->cVW[]; leave l if)
       #);
     (if cVW[] = none then
         &codeViewWindow[]->cVW[];
         ff[]->cVW.ff[];
         cVW.open;
         &ymerBrowserPrivate.CVlist.element[]->newCVelement[]
           ->ymerBrowserPrivate.CVlist.append;
         ff[]->newCVelement.ff[];
         cVW[]->newCVelement.cVW[]
      else
         cVW.wriggle; 
     if)
  exit cVW[]
  #);
  

