ORIGIN '../ymer';
INCLUDE 'ymerPrivate'
        'ymerFileUtils';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- codeViewWindowFileQuit: DoPart --
do
   (if codeViewWindowPrivate.geList.closeGroups then
       THIS(codeViewWindow).close; 
   if)  

-- codeViewWindowBuffersMenu: Attributes --
bufferMenuItem: menuItem
  (#
     ge: ^editorEnv.groupEditor;
     ff: ^mps.AST.fragmentForm;
     eventhandler:: 
       (#
          onSelect:: 
            (# t: ^text
            do
               (if THIS(bufferMenuItem)[] <> codeViewWindowPrivate.current[]
                then
                   false->codeViewWindowPrivate.current.checked;
                   true->checked;
                   THIS(bufferMenuItem)[]->codeViewWindowPrivate.current[];
                   (ff[],ff.root[],none ,codeViewWindowPrivate.codeview[],ge[])
                     ->edenv.findOrCreateFormEditor->fe[];
                   (ff.father).fullname->fragmentDefaultName->t[];
                   '-'->t.append;
                   ff.name->t.append;
                   t[]->title
               if)
            #);
          
       #);
     setName:
       (# t: ^text; 
       do
          (ge.fg.name)->fragmentDefaultName->t[];
          '-'->t.append;
          ff.name->t.append;
          t[]->name;
          true->checked;
          
       #)
  #);
add:
  (#
     newBitem: ^bufferMenuItem;
     ge: ^editorEnv.groupEditor;
     ff: ^mps.AST.fragmentForm
  enter (ge[],ff[])
  do
     (if codeViewWindowPrivate.current[] <> none then
         false->codeViewWindowPrivate.current.checked
     if);
     &bufferMenuItem[]->newBitem[]->codeViewWindowPrivate.current[];
     newBitem.open;
     ge[]->newBitem.ge[];
     ff[]->newBitem.ff[];
     newBitem[]->append;
     newBitem.setName;
  exit newBitem[] 
  #);
  

-- codeViewWindowPrivate: Descriptor --
(#
   current: ^menubarType.buffersMenu.bufferMenuItem;
   buffer: @list
     (#
        element:: 
          (# ge: ^editorEnv.groupEditor;
             ff: ^mps.AST.fragmentForm; 
             bmi: ^menubarType.buffersMenu.bufferMenuItem;
          #);
        add:
          (#
             ge: ^editorEnv.groupEditor;
             ff: ^mps.AST.fragmentForm;
             newElm: ^element
          enter (ge[],ff[])
          do
             &element[]->newElm[];
             ge[]->newElm.ge[];
             ff[]->newElm.ff[];
             (ge[],ff[])->(theMenubar).buffersMenu.add->newElm.bmi[];
             newElm[]->append
          #);
        
     #);
   geList: @list (* group editors active in this codeView *)
     (#
        element::  (# ge: ^editorEnv.groupEditor #);
        findOrCreateGroupEditor:
          (#
             fg: ^MPS.ast.fragmentGroup;
             ge: ^editorEnv.groupEditor;
             newElm: ^element
          enter fg[]
          do
             find
               (#
                  predicate::  (#  do current.ge.fg[] = fg[]->value #);
                  notFound:: 
                    (# 
                    do
                       &element[]->newElm[];
                       (THIS(codeViewWindow)[],fg[],editMode)
                         ->edenv.findOrCreateGroupEditor->newElm.ge[]->ge[];
                       newElm[]->append;
                       
                    #)
               do current.ge[]->ge[]
               #);
             
          exit ge[]
          #);
        closeGroups:
          (# okToClose: @boolean; 
          do
             true->okToClose;
             l: iterate
               (# 
               do
                  (if current.elm.ge.closeGroup->okToClose then
                      current[]->delete
                   else
                      leave l
                  if)
               #)
          exit okToClose
          #);
        
     #);
   setupFF:
     (# t: ^text
     do
        ff.father->geList.findOrCreateGroupEditor->ge[];
        (ff[],ff.root[],none ,codeview[],ge[])->edenv.findOrCreateFormEditor
          ->fe[];
        (ff.father).fullname->fragmentDefaultName->t[];
        '-'->t.append;
        ff.name->t.append;
        t[]->title;
        buffer.find
          (#
             predicate:: 
               (# 
               do ((current.ge[] = ge[]) and (current.ff[] = ff[]))->value
               #);
             notFound:: 
               (# 
               do
                  (ge[],ff[])->buffer.add;
               #);
          do (if codeViewWindowPrivate.current[] <> none then
                 false->codeViewWindowPrivate.current.checked
             if);
             true->current.bmi.checked;
             current.bmi[]->codeViewWindowPrivate.current[]
          #)
     #);
   codeview: @codemoveableEditor
     (#
        isolated:: trueObject;
        selectNode:: 
          (# 
          do
             (if true
              // (theEditorRoot.frag.root[]->theEditorRoot.equal) and separate
              then
                 (theEditorRoot.frag[],node[])->viewSeparate
              //
              (theEditorRoot.frag.root[]->theEditorRoot.equal)
              and
              not
              separate then
                 l: ymerBrowserPrivate.CVlist.scan
                   (# 
                   do
                      (if current.ff[] = ff[] then
                          theEditorRoot.frag[]->current.ff[]; leave l
                      if)
                   #);
                 theEditorRoot.frag[]->ff[];
                 setupFF;
                 node[]->fe.setNode
              //
              (not (theEditorRoot.frag.root[]->theEditorRoot.equal))
              and
              separate then
                 (theEditorRoot[],node[])->fe.openSubeditor
              //
              (not (theEditorRoot.frag.root[]->theEditorRoot.equal))
              and
              not
              separate then
                 l: ymerBrowserPrivate.CVlist.scan
                   (# 
                   do
                      (if current.ff[] = ff[] then
                          theEditorRoot.frag[]->current.ff[]; leave l
                      if)
                   #);
                 theEditorRoot.frag[]->ff[];
                 setupFF;
                 node[]->fe.setNode
             if)
          #);
        getEncloser::  (#  do codeviewEncloser[]->theEncloser[] #);
        open:: 
          (# 
          do
             true->bindBottom->bindTop->bindRight->bindLeft;
             codeview[]->onCodeViewOpen
          #);
        
     #);
   codeviewEncloser: @codemoveableEditorEncloser
     (#
        tryclose:: 
          (# 
          do (if geList.closeGroups then THIS(codeViewWindow).close;  if)
          #);
        onHighLight::  (#  do THIS(codeViewWindow).wriggle #);
        getAST::  (#  do mps.AST[]->AST[] #);
        getBETACFL::  (#  do mps.BETACFL[]->BETACFL[] #);
        
     #);
   ge: ^editorEnv.groupEditor;
   
#)  

-- codeViewWindowOnAboutToClose: DoPart --
do
   (if codeViewWindowPrivate.geList.closeGroups->okToClose then
       closeDown->okToClose
   if);
   INNER  

-- codeViewWindowCloseDown: DoPart --
do
   ymerBrowserPrivate.CVlist.locate
     (#
        predicate:: 
          (#  do current.elm.cVW[] = THIS(codeViewWindow)[]->value #)
     do position[]->ymerBrowserPrivate.CVlist.delete
     #);
   INNER  

-- codeViewWindowOpen: DoPart --
do
     (# t: ^text
     do
        (THIS(codeViewWindow).contents,size)
          ->codeViewWindowPrivate.codeview.open;
        codeViewWindowPrivate.setupFF;
        INNER open
     #)  

-- viewSeparate: DoPart --
do
   l: ymerBrowserPrivate.CVlist.scan
     (# 
     do (if current.ff[] = ff[] then current.cVW[]->cVW[]; leave l if)
     #);
   (if cVW[] = none then
       &codeViewWindow[]->cVW[];
       ff[]->cVW.ff[];
       cVW.open;
         (# newCVelement: ^ymerBrowserPrivate.CVlist.element
         do
            &ymerBrowserPrivate.CVlist.element[]->newCVelement[]
              ->ymerBrowserPrivate.CVlist.append;
            ff[]->newCVelement.ff[];
            cVW[]->newCVelement.cVW[]
         #)
    else
       cVW.wriggle; 
   if);
   (if (ff[] <> none ) and (node[] = none ) then ff.root[]->node[] if);
   node[]->cVW.fe.setNode  

