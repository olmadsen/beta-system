ORIGIN '../ymer';

INCLUDE 'ymerPrivate';
INCLUDE 'ymerFileUtils';

--- fragmentBrowserLib: attributes ---
codeViewWindow: ymerLocalWindow
  (# menubarType::
       (# Edit: @codeEditorEditMenu
            (# writeStatus::  (#  do #) #);
          View: @codeEditorViewMenu(# #);
          SLOTs: @codeEditorSLOTsMenu(# #);
          open::
            (#
            do edit.open; view.open; slots.open;
               edit[]->append; view[]->append; slots[]->append;
            #)
       #);
     ff: ^mps.AST.fragmentForm;
     codeview: @codemoveableEditor
       (# selectNode:: 
            (# cVW: ^codeViewWindow
            do node.frag[]->viewSeparate->cVW[];
               (node[],1,0,0)->cVW.codeview.SifViewer.setFocus;
            #);
          getEncloser:: 
            (#  do codeviewEncloser[]->theEncloser[] #);
          open:: 
            (# do true->bindBottom->bindTop->bindRight->bindLeft #);
          
       #);
     codeviewEncloser: @codemoveableEditorEncloser
       (# tryclose::  (#  do THIS(codeViewWindow).close #);
          onHighLight::  (#  do THIS(codeViewWindow).wriggle #);
          getAST::  (#  do mps.AST[]->AST[] #);
          getBETACFL::  (#  do mps.BETACFL[]->BETACFL[] #);
       #);
     eventhandler:: 
       (# onAboutToClose:: 
            (# 
            do (if (browser.getFragmentFormEditor).closeGroup->okToClose then
                   ymerBrowserPrivate.CVlist.locate
                   (# predicate::
                        (# do position.elm.cVW[]=THIS(codeViewWindow)[]->value #)
                   do position[]->ymerBrowserPrivate.CVlist.delete
                   #)
               if)
            #)
       #);
     open:: 
       (# t: ^text; 
       do (THIS(codeViewWindow).contents,size)->codeview.open;
          (ff[],codeview[])->edenv.findOrCreateFormEditor;
          (ff.father).fullname->fragmentDefaultName->t[];
          '-'->t.append;
          ff.name->t.append;
          t[]->title;
       #);
  #);
viewSeparate:
  (# ff: ^mps.AST.fragmentForm; cVW: ^codeViewWindow; 
     newCVelement: ^ymerBrowserPrivate.CVlist.element
  enter ff[]
  do l: ymerBrowserPrivate.CVlist.scan
       (# 
       do (if current.ff[] = ff[] then current.cVW[]->cVW[]; leave l if)
       #);
     (if cVW[] = none then
         &codeViewWindow[]->cVW[];
         ff[]->cVW.ff[];
         cVW.open;
         
         &ymerBrowserPrivate.CVlist.element[]
           ->newCVelement[]
           ->ymerBrowserPrivate.CVlist.append;
         ff[]->newCVelement.ff[];
         cVW[]->newCVelement.cVW[]
      else
         cVW.wriggle; 
     if)
  exit cVW[]
  #);
