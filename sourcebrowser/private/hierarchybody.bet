ORIGIN 'sourcebrowserbody';
INCLUDE 'setupProjects';
-- updateFolderView: DoPart --
do
   'updatefolderview'->putline;
   (formview[],g[],true)->setupGroup;
   (formView.root).items.clear;
   g.items.scan
     (# 
     do
        (if current.isFolder then  else current[]->(formView.root).addItem if)
     #);
   ''->(formview.root).label[];
   (g.location[],'/fzvsvsvs')->mps.ast.thepathhandler.localpath
     ->fragmentDefaultName->(formview.root).label.append;
   (* (formView.root).onExpand; *)
   (if g.lastSelectedFf[] = none then
       (if (formview.root).items.size = 1 then
           ((formview.root).items.head).elm.Select;
           true->didSelect;
           formView.theScroller[]
             ->((formview.root).items.head).elm.scrollIntoView
        else
           (if not (formView.root).isexpanded then
               (formView.root).onExpand
           if);
           sbprivate.sourceView.popdown
       if)
    else
       (formview.root).items.scan
         (# form: ^formview.form; 
         do
            (if current## = formview.form## then
                current[]->form[];
                (if form.ff[] = g.lastSelectedFf[] then
                    form.select;
                    true->didSelect;
                    formView.theScroller[]->form.scrollIntoView
                if)
            if)
         #)
   if);
   (if not didSelect then formview.changed if)  

-- directoryProjectOnExpand: DoPart --
do
   (hir[],THIS(directoryProject)[],location[])->setupDirectory;
   (*do (this(contentsType)[],this(directoryProject)[],location[])->setupDirectory*)
   theProjectInfoProject[]->setCP;
   none ->Interface.CurrentGroup[];
   none ->Interface.CurrentForm[]  

-- projectFileOnExpand: DoPart --
do
   (hir[],THIS(projectFile)[])->setupProjectFile; theProjectInfoProject[]->setCp  

-- groupOnExpand: DoPart --
do
   (hir[],THIS(group)[],false)->setupGroup;
   fg[]->sbPrivate.geList.findOrCreateGroupEditor->cge;
     

-- includeOnExpand: DoPart --
do
   'INCLUDE-expand, loc:%s\n'->putformat (#  do pe.location[]->s #);
     (# g: @hir.group
     do
        (pe.label[],pe.location[])->(g.label[],g.location[]);
        'fgc'->g.label[];
        g[]->getfg;
        (hir[],g[],false)->setupGroup;
        g.items.scan
          (#  do current[]->pe.additem #);
        g.fg[]->pe.fg[]
     #)  

-- formOnSelected: DoPart --
do (* not used anymore, forms are solely in formview!! *)
   ff.father->sbPrivate.geList.findOrCreateGroupEditor->cge;
   (*
    (# o: ^object do ff[]->sbprivate.sourceview.newfragment->(cfe,o[]) #)
    *)
   ff[]->Interface.currentForm[];
   ff[]->THIS(SourceBrowser).onCurrentFormChange  

-- formViewOnSelected: DoPart --
do
   ff.name->sbprivate.formView.selectedFormName[];
   ff.father->sbPrivate.geList.findOrCreateGroupEditor->cge;
   (*
    (# o: ^object do ff[]->sbprivate.sourceview.newfragment->(cfe,o[]) #)
    *)
   ff[]->theGroup.lastSelectedFf[]->Interface.currentForm[];
   ff[]->THIS(SourceBrowser).onCurrentFormChange  

-- directoryProjectSetupListView: DoPart --
do
     (# dp: ^hierarchy.directoryProject
     do
        &sourceView.listView.contents.contents.directoryProject[]->dp[];
        (label[],location[])->(dp.label[],dp.location[]);
        (sourceView.listView.contents.contents[],dp[],location[])
          ->setupDirectory;
        dp[]->sourceView.listView.install
     #)  

-- projectFileSetupListView: DoPart --
do
     (# pf: ^hierarchy.projectFile
     do
        &sourceView.listView.contents.contents.projectFile[]->pf[];
        (label[],location[])->(pf.label[],pf.location[]);
        (sourceView.listView.contents.contents[],pf[])->setupProjectFile;
        pf[]->sourceView.listView.install
     #)  

-- groupSetupListView: DoPart --
do
   'groupSetupListView on %s\n'->putformat (#  do location[]->s #);
   findCP;
   (if fg[] = none then
       findfg
   if);
   (if fg[] <> selectedFG[] then
         (# g: ^hierarchy.group
         do
            &sourceView.listView.contents.contents.group[]->g[];
            (label[],location[],lastSelectedFF[])
              ->(g.label[],g.location[],g.lastSelectedFf[]);
            THIS(group)[]->formview.theGroup[];
            g[]->updateFolderView
         #)
   if);
   theProjectInfoGroup[]->Interface.currentGroup[];
   (if not isinusedGroups then 
       (fg.fullname,false)->addToUsedGroups
       
   if);
   fg[]->selectedFG[]  

-- originSetupListView: DoPart --
do
     (# g: ^hierarchy.group
     do
        &sourceView.listView.contents.contents.group[]->g[];
        (label[],location[])->(g.label[],g.location[]);
        g[]->getfg;
        (*(sourceView.listView.contents.contents[],g[],false)
         ->setupGroup;
         g[]->sourceView.listView.install; *)
        (* g[]->updateFolderView; *)
        (g.location[],true)-> (*addToGroupDependencyCache*) addToUsedGroups
     #)  

-- includeSetupListView: DoPart --
do
     (# g: ^hierarchy.group
     do
        &sourceView.listView.contents.contents.group[]->g[];
        (label[],location[])->(g.label[],g.location[]);
        g[]->getfg;
        (* (sourceView.listView.contents.contents[],g[],true)
         ->setupGroup;
         g[]->sourceView.listView.install *)
        g[]->updateFolderView;
        (g.location[],true)-> (*addToGroupDependencyCache*) addToUsedGroups
     #)  

-- bodySetupListView: DoPart --
do
     (# g: ^hierarchy.group
     do
        &sourceView.listView.contents.contents.group[]->g[];
        (label[],location[])->(g.label[],g.location[]);
        g[]->getfg;
        (* (sourceView.listView.contents.contents[],g[],true)
         ->setupGroup;
         g[]->sourceView.listView.install *)
        g[]->updateFolderView;
        (g.location[],true)-> (* addToGroupDependencyCache *) addToUsedGroups
     #)  

-- groupinit: DoPart --
do
   &projects.group[]->theProjectInfoGroup[];
   fgicon[]->icon;
   lfmenu.open;
   lfmenu[]->popupMenu;
   location.copy->theProjectInfoGroup.location.append;
   (if isinUsedGroups then
       &lfmenu.menuitem
         (#
            eventhandler:: 
              (#
                 onSelect:: 
                   (# 
                   do
                      THIS(group)[]->father.items.at->father.items.delete;
                      (if theProject[] <> none then
                          theProject.onProjectChange
                      if);
                      changed
                   #)
              #);
            open::  (#  do 'Remove'->name #)
         #)[]->lfmenuitem[]
    else
       &lfmenu.menuitem
         (#
            eventhandler:: 
              (#
                 onSelect:: 
                   (# 
                   do
                      THIS(group).father.onCollapse;
                      location[]->mps.AST.expandToFullPath
                        ->mps.fragmentGroupTable.getFragmentGroup->fg[];
                      (fg.name,true)->addToUsedGroups
                   #)
              #);
            open::  (#  do 'Open in "Recent Files"'->name #)
         #)[]->lfmenuitem[]
   if);
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append;
   &lfmenu.menuitem
   (#
      eventhandler:: 
        (#
           onStatus:: (# do not HasDomain->value #);
           onSelect:: (# do InsertDomain #)
        #);
      open::  (#  do 'Compute Domain"'->name #)
   #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append;

-- directoryProjectinit: DoPart --
do
   &projects.project[]->theProjectInfoProject[];
   showTxtFiles->theProjectInfoProject.showTxtFiles;
   hasSubprojects->theProjectInfoProject.hasSubprojects;
   location.copy->theProjectInfoProject.location.append;
   label.copy->theProjectInfoProject.name[];
   true->theProjectInfoProject.isDirectory;
   'DirectoryProjectinit os %s\n'->putformat (#  do label[]->s #)  

-- ProjectFileinit: DoPart --
do
   &projects.project[]->theProjectInfoProject[];
   hasSubprojects->theProjectInfoProject.hasSubprojects;
   (if location[] <> none then
       location.copy->theProjectInfoProject.location.append
   if);
   (if label[] <> none then label.copy->theProjectInfoProject.name[] if);
   true->theProjectInfoProject.isProjectFile;
   'ProjectFileinit os %s\n'->putformat (#  do label[]->s #);
   lfmenu.open;
   lfmenu[]->popupmenu;
   &lfmenu.menuitem
     (#
        eventhandler::  
          (# onSelect:: (# do SelectAsUsedGroups #);
             onStatus:: (# do not isUsedGroup->value #)
          #);
        open::  (#  do 'Select as Recent'->name #)
     #)[]->lfmenuitem[];
   lfmenuitem.open; 
   lfmenuitem[]->lfmenu.append


-- temporaryProjectInit: DoPart --
do
   &projects.project[]->theProjectInfoProject[];
   hasSubprojects->theProjectInfoProject.hasSubprojects;
   (if location[] <> none then
       location.copy->theProjectInfoProject.location.append
   if);
   (if label[] <> none then label.copy->theProjectInfoProject.name[] if);
   true->theProjectInfoProject.isProjectFile;
   'temporaryProjectInit os %s\n'->putformat (#  do label[]->s #);
   lfmenu.open;
   lfmenu[]->popupMenu;
   &lfmenu.menuitem
     (#
        eventhandler::  (# onSelect::  (#  do SaveProjectFileAs #) #);
        open::  (#  do 'Save Project As'->name #)
     #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append
   
-- temporaryProjectonSelect: DoPart --
do findcp  

-- temporaryProjectSavePrjFileAs: DoPart --
do
   l:(#  prjFile: @file; 
     do
        (if fileName[]=none then
            fileSelectionDialog->filename[];
            (if filename[]=none then leave l if);
            '.pjt'->filename.append
        if);
        filename[]->prjfile.name;
        prjfile.openWrite;
        items.scan
          (# grp: ^group
          do
             current[]->grp[];
             'group %s \'%s.bet\'\n'
               ->prjfile.putformat
                 (#  do grp.label[]->s; grp.location[]->s #)
          #);
        prjFile.close
     #)  

-- temporaryProjectChange: DoPart --
do saveProjectFile;
   '/home/gram/beta/r5.2/sourcebrowser/private/hierarchybody.bet: Line 319'->screen.putline;
   true->hasChanged
   
--DPProjectInit:DoPart--
do 
   &projects.project[]->theProjectInfoProject[];
   true->theProjectInfoProject.isDomain;
   location.copy->theProjectInfoProject.location.puttext;
   'Domain of '->theProjectInfoProject.name[];
   location.copy->theProjectInfoProject.name.append
   
--groupInsertDomain:dopart--
do (# dmp:@sbprivate.hierarchyView.hir.DPproject
   do
      true->hasdomain;
      'Domain'->dmp.label[];
      location.copy->dmp.location[];
      dmp.init;
      dmp[]->items.prepend;
      (if not isExpanded then
          onExpand
       else
          changed 
      if)
   #)
      
--temporaryProjectSavePrjFile:DoPart--
do location[]->putline;
   location[]->saveProjectFileAs
   
--SelectAsUsedGroups:dopart--
do (# usedGrp:^temporaryProject;
   do
      ReadFile;
      getUsedGroups->usedGrp[];
      usedGrp.items.clear;
      items.scan
      (# ngrp,grp:^group
      do (if current##=group## then
             current[]->grp[];
             &group[]->ngrp[];
             grp.location.copy->ngrp.location[];
             grp.label.copy->ngrp.label[];
             usedGrp[]->ngrp.init;
             ngrp[]->usedGrp.additem
         if)
      #);
      true->isUsedGroup;
      onCollapse;
      father.onCollapse;
      'Recent Files ('->usedGrp.label[];
      label[]->usedGrp.label.append;
      ')'->usedGrp.label.put;
      usedGrp.onExpand;
      usedGrp.onProjectChange;
      false->usedGrp.hasChanged;
      location.copy->usedGrp.originalproject[];
      changed
   #)
   
--projectFileReadFile:dopart--
do (hir[],THIS(projectFile)[])->setupProjectFile
   
