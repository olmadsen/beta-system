ORIGIN 'sourcebrowserbody';
INCLUDE 'setupProjects';
-- updateFolderView: DoPart --
do
   'updatefolderview'->putline;
   (formview[],g[],true)->setupGroup;
   (formView.root).items.clear;
   g.items.scan
   (# 
   do
      (if current.isFolder then  else current[]->(formView.root).addItem if)
   #);
   ''->(formview.root).label[];
   (g.location[],'/fzvsvsvs')->mps.ast.thepathhandler.localpath
     ->fragmentDefaultName->(formview.root).label.append;
   (* (formView.root).onExpand; *)
   (if g.lastSelectedFf[] = none then
       (if (formview.root).items.size = 1 then
           ((formview.root).items.head).elm.Select;
           true->didSelect;
           formView.theScroller[]
             ->((formview.root).items.head).elm.scrollIntoView
        else
           (if not (formView.root).isexpanded then
               (formView.root).onExpand
           if);
           none ->formView.selectedFormName[];
           sbprivate.sourceView.popdown
       if)
    else
       (formview.root).items.scan
       (# form: ^formview.form; 
       do
          (if current## = formview.form## then
              current[]->form[];
              (if form.ff[] = g.lastSelectedFf[] then
                  form.select;
                  true->didSelect;
                  formView.theScroller[]->form.scrollIntoView
              if)
          if)
       #)
   if);
   (if not didSelect then formview.changed if)  

-- directoryProjectOnExpand: DoPart --
do
   (hir[],THIS(directoryProject)[],location[])->setupDirectory;
   (*do (this(contentsType)[],this(directoryProject)[],location[])->setupDirectory*)
   theProjectInfoProject[]->setCP;
   none ->Interface.CurrentGroup[];
   none ->Interface.CurrentForm[]  

-- projectFileOnExpand: DoPart --
do
   (hir[],THIS(projectFile)[])->setupProjectFile; theProjectInfoProject[]->setCp  

-- groupOnExpand: DoPart --
do true->expanding;
   (hir[],THIS(group)[],false)->setupGroup;
   (if hasdomain then insertDomain if);
   (if hasExtent then insertExtent if);
   fg[]->sbPrivate.geList.findOrCreateGroupEditor->cge;   
   false->expanding

-- includeOnExpand: DoPart --
do
   'INCLUDE-expand, loc:%s\n'->putformat (#  do pe.location[]->s #);
   (# g: @hir.group
   do
      (pe.label[],pe.location[])->(g.label[],g.location[]);
      'fgc'->g.label[];
      g[]->getfg;
      (hir[],g[],false)->setupGroup;
      g.items.scan
      (#  do current[]->pe.additem #);
      g.fg[]->pe.fg[]
   #)  

-- formOnSelected: DoPart --
do (* not used anymore, forms are solely in formview!! *)
   ff.father->sbPrivate.geList.findOrCreateGroupEditor->cge;
   (*
    (# o: ^object do ff[]->sbprivate.sourceview.newfragment->(cfe,o[]) #)
    *)
   ff[]->Interface.currentForm[];
   ff[]->THIS(SourceBrowser).onCurrentFormChange  

-- formViewOnSelected: DoPart --
do
   ff.name->sbprivate.formView.selectedFormName[];
   ff.father->sbPrivate.geList.findOrCreateGroupEditor->cge;
   (*
    (# o: ^object do ff[]->sbprivate.sourceview.newfragment->(cfe,o[]) #)
    *)
   ff[]->theGroup.lastSelectedFf[]->Interface.currentForm[];
   ff[]->THIS(SourceBrowser).onCurrentFormChange  

-- directoryProjectSetupListView: DoPart --
do
   (# dp: ^hierarchy.directoryProject
   do
      &sourceView.listView.contents.contents.directoryProject[]->dp[];
      (label[],location[])->(dp.label[],dp.location[]);
      (sourceView.listView.contents.contents[],dp[],location[])
        ->setupDirectory;
      dp[]->sourceView.listView.install
   #)  

-- projectFileSetupListView: DoPart --
do
   (# pf: ^hierarchy.projectFile
   do
      &sourceView.listView.contents.contents.projectFile[]->pf[];
      (label[],location[])->(pf.label[],pf.location[]);
      (sourceView.listView.contents.contents[],pf[])->setupProjectFile;
      pf[]->sourceView.listView.install
   #)  

-- groupSetupListView: DoPart --
do
   'groupSetupListView on %s\n'->putformat (#  do location[]->s #);
   findCP;
   (if fg[] = none then findfg if);
   (if fg[] <> selectedFG[] then
       (# g: ^hierarchy.group
       do none->Interface.currentForm[];
          &sourceView.listView.contents.contents.group[]->g[];
          (label[],location[],lastSelectedFF[])
            ->(g.label[],g.location[],g.lastSelectedFf[]);
          THIS(group)[]->formview.theGroup[];
          theProjectInfoGroup[]->onCurrentGroupChange;
          g[]->updateFolderView
       #)
   if);
   theProjectInfoGroup[]->Interface.currentGroup[];
   (if not isinusedGroups then (fg.fullname,false)->addToUsedGroups if);
   fg[]->selectedFG[]  

-- originSetupListView: DoPart --
do
   (# g: ^hierarchy.group
   do
      &sourceView.listView.contents.contents.group[]->g[];
      (label[],location[])->(g.label[],g.location[]);
      g[]->getfg;
      (*(sourceView.listView.contents.contents[],g[],false)
       ->setupGroup;
       g[]->sourceView.listView.install; *)
      (* g[]->updateFolderView; *)
      (g.location[],true)-> (*addToGroupDependencyCache*) addToUsedGroups
   #)  

-- includeSetupListView: DoPart --
do
   (# g: ^hierarchy.group
   do
      &sourceView.listView.contents.contents.group[]->g[];
      (label[],location[])->(g.label[],g.location[]);
      g[]->getfg;
      (* (sourceView.listView.contents.contents[],g[],true)
       ->setupGroup;
       g[]->sourceView.listView.install *)
      g[]->updateFolderView;
      (g.location[],true)-> (*addToGroupDependencyCache*) addToUsedGroups
   #)  

-- bodySetupListView: DoPart --
do
   (# g: ^hierarchy.group
   do
      &sourceView.listView.contents.contents.group[]->g[];
      (label[],location[])->(g.label[],g.location[]);
      g[]->getfg;
      (* (sourceView.listView.contents.contents[],g[],true)
       ->setupGroup;
       g[]->sourceView.listView.install *)
      g[]->updateFolderView;
      (g.location[],true)-> (* addToGroupDependencyCache *) addToUsedGroups
   #)  

-- groupinit: DoPart --
do
   &projects.group[]->theProjectInfoGroup[];
   (if fg[]<>none then fg[]->theProjectInfoGroup.fg[] if);
   fgicon[]->icon;
   lfmenu.open;
   lfmenu[]->popupMenu;
   location.copy->theProjectInfoGroup.location.append;
   &lfmenu.menuitem
   (#
      eventhandler:: 
        (#
           onSelect:: 
             (#  do THIS(locationFolder)[]->onPropertyShowEditor #)
        #);
      open::  (#  do 'Show Properties'->name #)
   #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append;
   (if isinUsedGroups then
       &lfmenu.menuitem
       (#
          eventhandler:: 
            (#
               onSelect:: 
                 (# 
                 do
                    THIS(group)[]->father.items.at->father.items.delete;
                    (if theProject[] <> none then
                        theProject.onProjectChange
                    if);
                    changed
                 #)
            #);
          open::  (#  do 'Remove'->name #)
       #)[]->lfmenuitem[]
    else
       &lfmenu.menuitem
       (#
          eventhandler:: 
            (#
               onSelect:: 
                 (# 
                 do
                    THIS(group).father.onCollapse;
                    location[]->mps.AST.expandToFullPath
                      ->mps.fragmentGroupTable.getFragmentGroup->fg[];
                    (fg.name,true)->addToUsedGroups
                 #)
            #);
          open::  (#  do 'Open in "Recent Files"'->name #)
       #)[]->lfmenuitem[]
   if);
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append;
   &lfmenu.menuitem
   (#
      eventhandler:: 
        (#
           onStatus::  (#  do not HasDomain->value #);
           onSelect::  (#  do InsertDomain #)
        #);
      open::  (#  do 'Compute Domain"'->name #)
   #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append;
   &lfmenu.menuitem
   (#
      eventhandler:: 
        (#
           onStatus::  (#  do not HasExtent->value #);
           onSelect::  (#  do InsertExtent #)
        #);
      open::  (#  do 'Compute Extent"'->name #)
   #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append;
   

-- directoryProjectinit: DoPart --
do
   &projects.project[]->theProjectInfoProject[];
   showTxtFiles->theProjectInfoProject.showTxtFiles;
   hasSubprojects->theProjectInfoProject.hasSubprojects;
   location.copy->theProjectInfoProject.location.append;
   label.copy->theProjectInfoProject.name[];
   true->theProjectInfoProject.isDirectory;
   'DirectoryProjectinit os %s\n'->putformat (#  do label[]->s #)  

-- ProjectFileinit: DoPart --
do
   &projects.project[]->theProjectInfoProject[];
   hasSubprojects->theProjectInfoProject.hasSubprojects;
   (if location[] <> none then
       location.copy->theProjectInfoProject.location.append
   if);
   (if label[] <> none then label.copy->theProjectInfoProject.name[] if);
   true->theProjectInfoProject.isProjectFile;
   'ProjectFileinit os %s\n'->putformat (#  do label[]->s #);
   lfmenu.open;
   lfmenu[]->popupmenu;
   &lfmenu.menuitem
   (#
      eventhandler:: 
        (#
           onSelect::  (#  do SelectAsUsedGroups #);
           onStatus::  (#  do not isUsedGroup->value #)
        #);
      open::  (#  do 'Select as Recent'->name #)
   #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append  

-- temporaryProjectInit: DoPart --
do
   &projects.project[]->theProjectInfoProject[];
   hasSubprojects->theProjectInfoProject.hasSubprojects;
   (if location[] <> none then
       location.copy->theProjectInfoProject.location.append
   if);
   (if label[] <> none then label.copy->theProjectInfoProject.name[] if);
   true->theProjectInfoProject.isProjectFile;
   'temporaryProjectInit os %s\n'->putformat (#  do label[]->s #);
   lfmenu.open;
   lfmenu[]->popupMenu;
   &lfmenu.menuitem
   (#
      eventhandler::  (# onSelect::  (#  do SaveProjectFileAs #) #);
      open::  (#  do 'Save Project As'->name #)
   #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append;
   &lfmenu.menuitem
   (#
      eventhandler::  (# onSelect:: (# do items.clear; changed #) #);
      open::  (#  do 'Remove All'->name #)
   #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append;

-- temporaryProjectonSelect: DoPart --
do findcp  

-- temporaryProjectChange: DoPart --
do saveProjectFile; true->hasChanged  

-- DPProjectInit: DoPart --
do
   (if theProjectInfoProject[]=none then
       &projects.project[]->theProjectInfoProject[]
   if);
 
   location.copy->theProjectInfoProject.location.puttext;
   (if kind
    // domainKind then
       'Domain of '->theProjectInfoProject.name[];
       true->theProjectInfoProject.isDomain
    // ExtentKind then
       'Extent of '->theProjectInfoProject.name[];
       true->theProjectInfoProject.isExtent
   if);
   location.copy->theProjectInfoProject.name.append;
   lfmenu.open;
   lfmenu[]->popupmenu;
   &lfmenu.menuitem
   (# eventhandler:: 
        (# onSelect:: (# do SaveProjectFileAs #)
        #);
      open:: (#  do 'Save as Project File'->name #)
   #)[]->lfmenuitem[];
   lfmenuitem.open;
   lfmenuitem[]->lfmenu.append

-- groupInsertDomain: DoPart --
do
   (# dmp: @sbprivate.hierarchyView.hir.DPproject
   do 
      'Domain'->dmp.label[];
      location.copy->dmp.location[];
      DomainKind->dmp.init;
      dmp[]->items.prepend;
      false->hasdomain;
      (if (not expanding) and (not isExpanded) then onExpand else changed if); 
      true->hasdomain
   #)  
   
-- groupInsertExtent: DoPart --
do
   (# dmp: @sbprivate.hierarchyView.hir.DPproject
   do
      'Extent'->dmp.label[];
      location.copy->dmp.location[];
      ExtentKind->dmp.init;
      dmp[]->items.prepend;
      false->hasExtent;
      (if (not expanding) and (not isExpanded) then onExpand else changed if);
      true->hasextent
   #)  

-- temporaryProjectSavePrjFile: DoPart --
do location[]->putline; location[]->saveProjectFileAs  

-- projectFileReadFile: DoPart --
do (hir[],THIS(projectFile)[])->setupProjectFile  

-- DDProjectExpand: DoPart --
do
   (if items.size=0 then
       (if kind
        // DomainKind
        // ExtentKind then
           l:(if theProjectInfoProject.hasSubProjects then
                 theProjectInfoProject.subProjects.scan
                 (# dpp:^dpProject
                 do
                    &dPProject[]->dpp[];
                    current.name.copy->dpp.label[];
                    current.location.copy->dpp.location[];
                    current[]->dpp.theProjectInfoProject[];
                    subProjectKind->dpp.init;
                    dpp[]->addItem
                 #)
              else   
                 theProjectInfoProject.scanGroups; restart l
             if)
        // SubProjectKind then
           ll:(if theProjectInfoProject.groups.size > 0 then
                  theProjectInfoProject.groups.scan
                  (# g: ^group; 
                  do
                     &group[]->g[];
                     current.name.copy->g.label[];
                     current.location.copy->g.location[];
                     THIS(project)[]->g.init;
                     g[]->addItem
                  #)
               else
                  theProjectInfoProject.scanGroups; restart ll
              if)
       if)
   if)

-- SelectAsUsedGroups: DoPart --
do
   (# usedGrp: ^temporaryProject; 
   do
      ReadFile;
      getUsedGroups->usedGrp[];
      usedGrp.items.clear;
      items.scan
      (# ngrp,grp: ^group
      do
         (if current## = group## then
             current[]->grp[];
             &group[]->ngrp[];
             grp.location.copy->ngrp.location[];
             grp.label.copy->ngrp.label[];
             usedGrp[]->ngrp.init;
             ngrp[]->usedGrp.additem
         if)
      #);
      true->isUsedGroup;
      onCollapse;
      father.onCollapse;
      'Recent Files ('->usedGrp.label[];
      label[]->usedGrp.label.append;
      ')'->usedGrp.label.put;
      usedGrp.onExpand;
      usedGrp.onProjectChange;
      false->usedGrp.hasChanged;
      location.copy->usedGrp.originalproject[];
      changed
   #)  
