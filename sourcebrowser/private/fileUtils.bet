ORIGIN '../sourcebrowser';

INCLUDE '~beta/basiclib/v1.6/file';
INCLUDE '~beta/basiclib/v1.6/directory';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- guienvLib: attributes ---

fragmentDefaultName:
  (# f: @file
  enter f.name
  exit f.entry.path.name.prefix
  #);
fileAvailable:
  (# f: @file; avail: @boolean; t: ^text;
     required:< booleanValue;
  enter f.name
  do (if true
      // not f.entry.exists
	 (# error:: (# do (NONE, msg[], 'File.exists error')->alertUser #) #)
	 then
	 (if required then
	     'File: '->t[];
	     f.name->t.puttext;
	     '\ndoes not exist'->t.puttext;
	     (NONE, t[],'Not available')->alertUser
	 if)
      // not f.entry.readable
	 (# error:: (# do (NONE, msg[], 'File.readable error')->alertUser #) #)
	 then
	 (if required then
	     'No read permissions to the file:\n     '->t[];
	     f.name->t.puttext;
	     (NONE, t[],'Not available')->alertUser
	 if)
      else true->avail
     if);
  exit avail
  #);
fileRequired: fileAvailable
  (# required:: trueObject #);
fileExists:
  (# f: @file
  enter f.name
  exit f.entry.exists
     (# error:: (# do (NONE, msg[], 'File.exists error')->alertUser #) #)
  #);
fileReadable:
  (# f: @file
  enter f.name
  exit f.entry.readable
     (# error:: (# do (NONE, msg[], 'File.readable error')->alertUser #) #)
  #);
fileWriteable:
  (# f: @file
  enter f.name
  exit f.entry.writeable
     (# error:: (# do (NONE, msg[], 'File.writeable error')->alertUser #) #)
  #);
fileModtime:
  (# f: @file
  enter f.name
  exit f.entry.modtime
     (# error:: (# do (NONE, msg[], 'File.modtime error')->alertUser #) #)
  #);
isFile:
  (# f: @file
  enter f.name
  exit f.entry.isFile
     (# error:: (# do (NONE, msg[], 'File.isFile error')->alertUser #) #)
  #);
isDirectory:
  (# f: @file
  enter f.name
  exit f.entry.isDirectory
     (# error:: (# do (NONE, msg[], 'File.isDirectory error')->alertUser #) #)
  #);
