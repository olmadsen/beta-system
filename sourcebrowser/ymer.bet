ORIGIN '~beta/basiclib/v1.4/betaenv';

INCLUDE '~beta/basiclib/v1.4/basicsystemenv';

INCLUDE '~beta/guienv/v1.3/utils/prompts';
INCLUDE '~beta/guienv/v1.3/utils/pane';

INCLUDE '~beta/objectbrowser/v2.0/mpsinterface';
INCLUDE '~beta/objectbrowser/v2.0/betagrammarinit';

INCLUDE '~beta/dependency/v1.0/dependency';

INCLUDE 'sourcebrowser';

INCLUDE 'envgroup';

BODY 'private/parseProjects';
BODY 'private/splitDependency';
BODY 'private/compilerPreferences';

--- lib: attributes ---
ymerApplication: guienv
  (# <<SLOT ymerApplicationLib: attributes>>;
     mps: @mpsinterface
       (# onOldGroup::
            (# t: @text
            do 'Fragment not parsed.\nShould this be done?'->t;
               (ymerBrowser[],'Notification', t[])->
               promptForBoolean(# ok:: (# do true->continue #) #);
               true->continue (* to ignore faults *)
            #);
          onUncheckedGroup::
            (# do true->continue #);
          init::
            (# do betagrammarinit; dg.graphInit #);
          dg: @AST.dependencyGraph;
       #);
     ymerWindow: window
       (# name: ^text;
          setName:
            (#
            enter name[]
            do name[]->title;
            #);
          open::<
            (#
            do INNER;
               this(ymerWindow)[]
                ->(ymerBrowser.ymerMenubar).windowsMenu.insertWindow;
            #);
          eventhandler::<
            (# onAboutToClose::<
                 (#
                 do INNER;
                    (if okToClose then
                        this(ymerWindow)[]
                          ->(ymerBrowser.ymerMenubar).windowsMenu.deleteWindow
                    if);
                 #)
            #);
       #);
     ymerBrowser: @ymerBrowserWindow;
     ymerBrowserWindow:< window
       (# <<SLOT fragmentBrowserLib: attributes>>;
          ymerMenubar: (# mb: ^menubarType exit theMenubar->mb[] #);
          addProject:
            (# name, location: ^text;
               newProj: ^browser.projects.theProjects.element;
            enter (name[], location[])
            <<SLOT ymerAddProject: dopart>>
            #);
          compilerSwitches: ^text;
          ph: @pathHandler;
          browser: @sourcebrowser
            (# <<SLOT browserLib: attributes>>;
               selectNode::
                 (#
                 do (((node.frag.father).fullName).copy, (node.frag.name).copy)
                      ->selectFragmentForm;
                    (node[],1,0,0)->(getFragmentFormEditor).SifViewer.setFocus;
                 #);
               followLink::
                 (#
                 do groupname[]->selectFragmentGroup
                 #);
               projectInfoType::
                 (# <<SLOT projectInfoLib: attributes>>;
                    formList: list
                      (# element:: (# location: @text #) #);
                    groupList: list
                      (# gr: group(# #); element:: gr #);
                    projectList: list
                      (# pr: project(# #);
                         element:: pr;
                         <<SLOT parseDefn: attributes>>;
                         parseProjectFile:
                           (# projectFile: ^text; newProj: ^element
                           enter (projectFile[], newProj[])
                           <<SLOT ymerParseProjectFile: dopart>>
                           #);
                         parseProjectsRC:
                           <<SLOT ymerParseProjectsRC:descriptor>>;
                      #);
                    theProjects: @projectList;
                    project:: 
                      (# location: @text;
                         isDirectory, isProjectFile: @boolean;
                         isRoot, isExtent, isDomain: @boolean;
                         hasSubprojects: @boolean;
                         subprojects: @projectList;
                         level: @integer;
                         isOpen: @boolean;
                         groups: @groupList;
                         scanGroupsImpl::
                           (# newGroup: ^groups.element;
                              dependencyPrivate:
                                @<<SLOT dependencyPrivate: descriptor>>;
                              dependencyRegister:
                                (# fgName: ^text
                                enter fgName[]
                                <<SLOT dependencyRegister: dopart>>
                                #);
                              dependencyCreateSubprojects:
                                (# selectedProj: ^projects.theProjects.element;
                                do this(project)[]->selectedProj[];
                                   selectedProj.subProjects.clear;
                                   <<SLOT dependencyCreateSubprojects:descriptor>>
                                #);
                           <<SLOT scanGroupsImpl: dopart>>
                           #)
                      #);
                    group::
                      (# location: @text;
                         forms: @formList;
                         getfg::
                           (# doRealOpen:
                                (# g: ^mps.AST.FragmentGroup;
                                   isRealOpen:
                                     (# isOpen: @Boolean;
                                     do scanner: g.fragmentList.scan
                                          (#
                                          do (if current.type
                                              // mps.AST.formType then
                                                 (if current.f[]<>NONE then
                                                     true->isOpen;
                                                     leave scanner
                                          if)if)#)
                                     exit isOpen
                                     #)
                                enter g[]
                                do (if not isRealOpen then (* open g real *)
                                       g.realOpen;
                                   if);
                                #);
                           do (if not this(Group).location.empty then
                                  this(Group).location[]
                                    ->mps.fragmentGroupTable.getFragmentGroup
                                    ->fg[];
                                  (if fg[]<>NONE then fg[]->doRealOpen if)
                              if);
                           #);
                      #);
                    scanProjectsImpl::
                      (#
                      do theProjects.scan
                         (# do current[]->thescan.foreach #)
                      #);
                    init::
                      (# 
                      do ph.init;
                         theProjects.init;
                         theProjects.parseProjectsRC;
                      #);
                 #);
               projectsLabel::
                 (# do 'Projects'->t[] #);
               emptyProjectListLabel::
                 (# do 'No Project selected'->t[] #);
               onGroupListOpen::
                 (# <<SLOT onGroupListOpen: dopart>> #);
               onProjectListOpen::
                 (# <<SLOT onProjectListOpen: dopart>> #);
               onCurrentProjectChange::
                 (#
                 do (if newProject[]<>NONE then
                        newProject.location[]->locationView.contents.label
                    if)
                 #);
               onCurrentGroupChange::
                 (#
                 do (if newGroup[]<>NONE then
                        newGroup.location[]->locationView.contents.label
                    if)
                 #);
               selectFragmentGroup:
                 (# fgName: ^text
                 enter fgName[]
                   <<SLOT selectFragmentGroup: dopart>>
                 #);
               selectFragmentForm:
                 (# fgName: ^text;
                    formName: ^text
                 enter (fgName[],formName[])
                   <<SLOT selectFragmentForm: dopart>>
                 #);
               eventhandler::
                 (# onFatherFrameChanged::
                      (# h,w: @integer
                      do ymerBrowser.size->(w,h);
                         (w,h-60)->size;
                         (0,0)->position;
                      #)
                 #);
               open:: 
                 (# x,y,h,w: @integer
                 do size->(w,h);(w,h+h/2)->size;
                    (w,h+h/2+60)->ymerBrowser.size;
                    TRUE->bindRight->bindLeft->bindTop;
                 #);
               editorEncloserType::
                 (# tryclose:: (# do ymerBrowser.close #);
                    onHighLight:: (# do ymerBrowser.wriggle #);
                    getAST:: (# do mps.AST[]->AST[] #);
                    getBETACFL:: (# do mps.BETACFL[]->BETACFL[] #);
                 #);
            #);
          showPreferencesDialog:
            <<SLOT showPreferencesDialog: descriptor>>;
          locationView: @envGroup
            (# contentsType:: staticText(# open:: (# do ''->label #) #);
               eventhandler::
                 (# onFatherFrameChanged::
                      (# h,w: @integer
                      do ymerBrowser.size->(w,h);
                         (w/2,50)->size;
                         (0,h-60)->position;
                      #)
                 #);
               open::
                 (# h,w: @integer
                 do ymerBrowser.size->(w,h);
                    (w/2,50)->size;
                    (0,h-60)->position;
                    'Current location'->label;
                 #)
            #);
          infoView: @envGroup
            (# contentsType:: staticText(# open:: (# do ''->label #) #);
               eventhandler::
                 (# onFatherFrameChanged::
                      (# h,w: @integer
                      do ymerBrowser.size->(w,h);
                         (w/2,50)->size;
                         (w/2,h-60)->position;
                      #)
                 #);
               open::
                 (# h,w: @integer
                 do ymerBrowser.size->(w,h);
                    (w/2,50)->size;
                    (w/2,h-60)->position;
                    'Info'->label;
                 #)
            #);
          eventhandler::<
            (# onAboutToClose:: (# do INNER; terminate #) #);       
          open::<
            (#
            do 'Ymer Fragment Browser (v0.3)'->title;
               ''->compilerSwitches[]; cPrivate(* to initialize *);
               browser.open;
               locationView.open;
               infoView.open;
               INNER;
            #);
          cPrivate: @<<SLOT compilePrivate: descriptor>>;
          menubarType::< 
            (# fileMenu: @menu
                 (# iSaveSettings: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# <<SLOT menuFileSaveSettingsOnselect: dopart>> #) 
                           #);
                         open:: (# do 'Save settings'->name #)
                      #);
                    iReloadSettings: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# <<SLOT menuFileReloadSettingsOnselect: dopart>> #) 
                           #);
                         open:: (# do 'Reset settings'->name #)
                      #);
                    iQuit: @menuitem
                      (# eventhandler::
                           (# onSelect::
                                (# do terminate #)
                           #);
                         open:: (# do 'Quit Ymer'->name #)
                      #);
                    open::
                      (# sep: ^menuItem
                      do 'File'->name;
                         iSaveSettings.open; iSaveSettings[]->append;
                         iReloadSettings.open; iReloadSettings[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                         iQuit.open; iQuit[]->append;
                      #);
                  #);
               projectsMenu: @menu
                 (# iLoad: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# t: ^text;
                                   newProj: ^browser.projects.theProjects.element;
                                <<SLOT menuProjectsLoadOnselect: dopart>>
                                #) 
                           #);
                         open:: (# do 'Load existing project'->name #)
                      #);
                    iLoadStd: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# projectFile: ^text;
                                   newProj: ^browser.projects.theProjects.element;
                                <<SLOT menuProjectsLoadStdOnselect: dopart>>
                                #) 
                           #);
                         open:: (# do 'Load standard libraries'->name #)
                      #);
                   iNew: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do ((browser.currentProject[]=NONE)
                                   or browser.currentProject.hasSubProjects)
                                     -> value #);
                              onSelect::
                                (# cp: ^browser.projects.theProjects.element;
                                   newProjectWindow: @ymerWindow
                                     (# switchWidth: @integer;
                                        height, distance,
                                        windowWidth,windowHeight: @integer;
                                        setFrame:
                                          (#
                                          do recalc;
                                             8*height+4*distance->windowHeight;
                                             (windowWidth,windowHeight)->size;
                                          #);
                                        recalc:
                                          (#
                                          do 3->distance;
                                             (switchWidth*3)+distance*4->windowWidth;
                                          #);
                                        bCancel: @pushButton
                                          (# setFrame:
                                               (#
                                               do recalc;
                                                  (distance,distance)->position;
                                                  ((windowWidth-3*distance)/2,height)->size
                                               #);
                                             open:: (# do setFrame; 'Cancel'->label #);
                                             eventhandler::
                                               (# onMouseUp:: (# do newProjectWindow.hide #);
                                                  onFatherFrameChanged::
                                                    (# do setFrame #)
                                               #)
                                          #);
                                        bOk: @pushButton
                                          (# setFrame:
                                               (#
                                               do recalc;
                                                  (distance+(windowWidth-3*distance)/2,distance)->position;
                                                  ((windowWidth-3*distance)/2,height)->size
                                               #);
                                             open:: (# do setFrame; 'OK'->label #);
                                             eventhandler::
                                               (# onMouseUp::
                                                    (#
                                                    do switchEdit.contents.contents->cp.name[];
                                                       (if browser.currentProject[]=NONE then
                                                           cp[]->browser.projects.theProjects.append;
                                                        else
                                                           (if browser.currentProject.hasSubProjects then
                                                               cp[]->browser.currentProject.subProjects.append;   
                                                           if)
                                                       if);
                                                       browser.projectsChanged;
                                                       cp[]->browser.setProject;
                                                       newProjectWindow.close
                                                    #);
                                                  onFatherFrameChanged::
                                                    (# do setFrame #)
                                               #)
                                          #);
                                        switchesFrame: @envGroup
                                          (# contentsType:: canvas
                                               (# projectFileBox: @radioButton 
                                                    (# setFrame:
                                                         (#
                                                         do recalc;
                                                            (0, distance)->position;
                                                            (switchWidth,height)->size
                                                         #);
                                                       open:: 
                                                         (# do setFrame; 'project file'->label #);
                                                       eventhandler:: 
                                                         (# onMouseUp:: (# do true->cp.isProjectFile #);
                                                            onFatherFrameChanged::
                                                              (# do setFrame #)
                                                         #)
                                                    #);
                                                  rootBox: @radioButton 
                                                    (# setFrame:
                                                         (#
                                                         do recalc;
                                                            (0, (height+distance))->position;
                                                            (switchWidth,height)->size
                                                         #);
                                                       open:: 
                                                         (# do setFrame; 'root project'->label #);
                                                       eventhandler:: 
                                                         (# onMouseUp:: (# do true->cp.isRoot #);
                                                            onFatherFrameChanged::
                                                              (# do setFrame #)
                                                         #)
                                                    #);
                                                  subProjectBox: @radioButton 
                                                    (# setFrame:
                                                         (#
                                                         do recalc;
                                                            (0, (height+distance)*2)->position;
                                                            (switchWidth,height)->size
                                                         #);
                                                       open:: 
                                                         (# do setFrame; 'sub project'->label #);
                                                       eventhandler:: 
                                                         (# onMouseUp:: (# do true->cp.hasSubprojects #);
                                                            onFatherFrameChanged::
                                                              (# do setFrame #)
                                                         #)
                                                    #);
                                                  open::
                                                    (#
                                                    do projectFileBox.open;
                                                       rootBox.open;
                                                       subProjectBox.open;
                                                    #)
                                               #);
                                             setFrame:
                                               (#
                                               do recalc;
                                                  (windowWidth-2*distance, height*5+distance)->size;
                                                  (distance, distance+height)->position
                                               #);
                                             open::
                                               (# do setFrame; 'Project Type'->label #);
                                             eventhandler::
                                               (# onFatherFrameChanged::
                                                    (# do setFrame #)
                                               #)
                                          #);
                                        switchEdit: @envGroup 
                                          (# contentsType:: editText;
                                             setFrame:
                                               (#
                                               do recalc;
                                                  (distance, height*6+distance*3)->position;
                                                  (windowWidth-2*distance,height*2)->size
                                               #);
                                             open:: 
                                               (# do setFrame; 'Project name'->label #);
                                             eventhandler::
                                               (# onFatherFrameChanged::
                                                    (# do setFrame #)
                                               #)
                                          #);
                                        tst: @pushButton
                                          (# open::
                                               (# ts: ^textStyle;
                                                  buttonLabel: (# exit 'more choises' #);
                                                  buttonWidth: @integer
                                               do buttonLabel->label;
                                                  style->ts[];
                                                  ((buttonLabel->ts.widthOfText)+5,ts.lineHeight+3)
                                                    ->(switchWidth,height);
                                                  switchWidth+8->switchWidth;
                                                  height+8->height;
                                               #)
                                          #);
                                        open::< 
                                          (# 
                                          do tst.open; (* just to get the width and height of fonts *)
                                             tst.hide; tst.close;
                                             
                                             setFrame;
                                             
                                             newProjectWindow.contents->bCancel.open;
                                             newProjectWindow.contents->bOk.open;
                                             
                                             newProjectWindow.contents->switchesFrame.open;
                                             newProjectWindow.contents->switchEdit.open;
                                             'New Project'->setName; hide;
                                             INNER
                                          #)
                                     #);
                                do &browser.projects.theProjects.element[]
                                     ->cp[];
                                   newProjectWindow.open;
                                   newProjectWindow.showmodal;
                           #) #);
                         open:: (# do 'Create new project'->name #)
                      #);
                    iSave: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do false and (browser.currentProject[]<>NONE) -> value #);
                              onSelect:: (# do  #) #);
                         open:: (# do 'Save project'->name #)
                      #);
                    iRename: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do browser.currentProject[]<>NONE -> value #);
                              onSelect::
                                (#
                                do (ymerBrowser[],'Project Rename','New project name','')->
                                   promptForText
                                   (# ok::
                                        (# cp: ^browser.projects.project; ch: @char
                                        do browser.currentProject[]->cp[];
                                           usertext->cp.name;
                                           (if true
                                            // cp.isDirectory and 
                                               (cp.name.length>0) and
                                               ((cp.name.length->cp.name.inxGet)<>'/') then
                                               '/'->cp.name.append
                                            // cp.isRoot and 
                                               (cp.name.length>0) and
                                               ((cp.name.length->cp.name.inxGet)<>'*') then
                                               '*'->cp.name.put
                                           if);
                                           browser.projectsChanged;
                                           cp[]->browser.setProject;
                                   #) #);
                                #) 
                           #);
                         open:: (# do 'Rename project'->name #)
                      #);
                    iRelocate: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do browser.currentProject[]<>NONE -> value #);
                              onSelect::
                                (# cp: ^browser.projects.project; t: ^text
                                  <<SLOT menuProjectsRelocateOnselect: dopart>>
                                #) 
                           #);
                         open:: (# do 'Relocate project'->name #)
                      #);
                    iDelete: @menuitem
                      (# eventhandler::
                           (# onStatus::
                                (# do browser.currentProject[]<>NONE -> value #);
                              onSelect::
                                (# cp: ^browser.projects.project
                                  <<SLOT menuProjectsDeleteOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'Delete project'->name #)
                      #);
                    open::
                      (# sep: ^menuItem
                      do 'Projects'->name;
                         iNew.open; iNew[]->append;
                         iLoad.open; iLoad[]->append;
                         iLoadStd.open; iLoadStd[]->append;
                         iSave.open; iSave[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                         iRename.open; iRename[]->append;
                         iRelocate.open; iRelocate[]->append;
                         iDelete.open; iDelete[]->append;
                      #);
                 #);
               groupsMenu: @menu
                 (# iNew: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do (browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isProjectFile or
                                   browser.currentProject.isDirectory)
                                     ->value
                                #);
                              onSelect::
                                (# t: ^text;
                                   newGroup: ^browser.projects.group;
                                <<SLOT menuGroupsNewOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'New group'->name #)
                      #);
                    iAdd: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do (browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isProjectFile)
                                     ->value
                                #);
                              onSelect:: 
                                (#
                                <<SLOT menuGroupsAddOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'Add group'->name #)
                      #);
                    iDelete: @menuItem
                      (# eventhandler:: 
                           (# onStatus::
                                (#
                                do (browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isProjectFile or
                                   browser.currentProject.isDirectory)
                                     ->value
                                #);
                              onSelect::
                                (#
                                <<SLOT menuGroupsDeleteOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'Delete group'->name #)
                      #);
                    open::
                      (# sep: ^menuItem
                      do 'Groups'->name;
                         iNew.open; iNew[]->append;
                         iAdd.open; iAdd[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                         iDelete.open; iDelete[]->append;
                      #);
                 #);
               toolsMenu: @menu
                 (# iCompile: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do (browser.currentGroup[]<>NONE) or
                                   ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain or
                                   browser.currentProject.isDirectory))
                                     ->value
                                #);
                              onSelect::
                                (# compile: <<SLOT iCompile: descriptor>>
                                do compile
                                #)
                           #);
                         open:: (# do 'Compile'->name #)
                      #);
                    iExecute: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect::
                                (# execute: <<SLOT iExecute: descriptor>>
                                do execute
                                #)
                           #);
                         open:: (# do 'Execute'->name #)
                      #);
                    iDebug: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# debug: <<SLOT iDebug: descriptor>>
                                do debug
                                #)
                           #);
                         open:: (# do 'Debug'->name #)
                      #);
                    iSif: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# sif: <<SLOT iSif: descriptor>>
                                do sif
                                #)
                           #);
                         open:: (# do 'Edit: Sif'->name #)
                      #);
                    iFreja: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# sif: <<SLOT iFreja: descriptor>>
                                do sif
                                #)
                           #);
                         open:: (# do 'Edit: Freja'->name #)
                      #);
                    iTextEdit: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# textEdit: <<SLOT iTextEdit: descriptor>>
                                do textEdit
                                #)
                           #);
                         open:: (# do 'Edit: Text editor'->name #)
                      #);
                    iInterfaceBuilder: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# sif: <<SLOT iInterfaceBuilder: descriptor>>
                                do sif
                                #)
                           #);
                         open:: (# do 'Edit: InterfaceBuilder'->name #)
                      #);
                    iAnalyze: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# analyze: <<SLOT iAnalyze: descriptor>>
                                do analyze
                                #)
                           #);
                         open:: (# do 'Analyze: betawc'->name #)
                      #);
                    iPreferences: @menuItem
                      (# eventhandler::
                           (# onSelect:: (# do showPreferencesDialog #)
                           #);
                         open:: (# do 'Preferences'->name #)
                      #);
                    open::
                      (# sep: ^menuItem
                      do 'Tools'->name;
                         iCompile.open; iCompile[]->append;
                         iExecute.open; iExecute[]->append;
                         iDebug.open; iDebug[]->append;
                         iSif.open; iSif[]->append;
                         iFreja.open; iFreja[]->append;
                         iTextEdit.open; iTextEdit[]->append;
                         iInterfaceBuilder.open; iInterfaceBuilder[]->append;
                         iAnalyze.open; iAnalyze[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                         iPreferences.open; iPreferences[]->append;
                      #);
                 #);
               windowsMenu: @menu
                 (# <<SLOT windowsMenuPrivate: attributes>>;
                    insertWindow: (# win: ^ymerWindow enter win[] do <<SLOT windowsMenuInsertWindow: descriptor>> #);
                    deleteWindow: (# win: ^ymerWindow enter win[] <<SLOT windowsMenuDeleteWindow: dopart>> #);
                    open:: (# do 'Windows'->name #);
                 #);
               open::<
                 (#
                 do fileMenu.open; fileMenu[]->append;
                    projectsMenu.open; projectsMenu[]->append;
                    groupsMenu.open; groupsMenu[]->append;
                    toolsMenu.open; toolsMenu[]->append;
                    windowsMenu.open; windowsMenu[]->append;
                    INNER;
                 #)
            #);
       #);
     
     BETAlib, BETAexecutable,
     SIFexecutable, FREJAexecutable, INTERFACEBUILDERexecutable,
     VALHALLAexecutable, TEXTexecutable, BETAWCexecutable: ^text;
     machineType: @text;
     theSystemEnv: ^systemenv;
     
     init:<
       (# do <<SLOT ymerInit: descriptor>> #);
  #);
