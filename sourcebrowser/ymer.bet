ORIGIN '~beta/basiclib/v1.4/betaenv';

INCLUDE '~beta/basiclib/v1.4/basicsystemenv';

INCLUDE 'xprompts';
INCLUDE '~beta/guienv/v1.3.1/utils/pane';
INCLUDE '~beta/objectbrowser/v2.0/mpsinterface';
INCLUDE '~beta/objectbrowser/v2.0/betagrammarinit';
INCLUDE '~beta/objectbrowser/v2.0/UI/labelled';

INCLUDE '~beta/dependency/v1.0/dependency';

INCLUDE 'sourcebrowser';

BODY 'private/ymerBody';
BODY 'private/parseProjects';
BODY 'private/splitDependency';
BODY 'private/compilerPreferences';

--- lib: attributes ---
ymerApplication: guienv
  (# <<SLOT ymerApplicationLib: attributes>>;
     mps: @mpsinterface
       (# onOldGroup::
            (# t: @text
            do 'Fragment not parsed.\nShould this be done?'->t;
               (ymerBrowser[],'Notification', t[])->
               promptForBoolean
               (# ok:: (# do true->allowParsing #);
                  notok:: (# do false->allowParsing #);
                  cancel:: (# do false->allowParsing #);
               #);
               true->continue 
            #);
          onUncheckedGroup::
            (# do true->continue; true->allowChecking #);
          onParseErrors::
            (# <<SLOT ymerMPSparseErrors: dopart>> #);
          onNewGroup::
            (# do (*true->continue*) #);
          onOtherError::
            (#
            do (NONE, errorText[],'Error during opening of group')->alertUser;
               true->continue
            #);
          betaparserinit:
            <<SLOT ymerMPSbetaparserinit: descriptor>>;
          init::
            (#
            do betagrammarinit;
               betaparserinit;
               dg.graphInit
            #);
          dg: @AST.dependencyGraph;
       #);
     browsers: @list(# element:: ymerBrowserWindow #);
     ymerBrowser: @ymerBrowserWindow; ymerBrowserWindowCount: @integer;
     ymerBrowserWindow:< window
       (# <<SLOT fragmentBrowserLib: attributes>>;
          ymerLocalWindow: window
            (# open::<
                 (#
                 do INNER;
                    this(ymerLocalWindow)[]
                      ->(this(ymerBrowserWindow).theMenubar).windowsMenu.insertWindow;
                 #);
               eventhandler::<
                 (# onAboutToClose::<
                      (#
                      do INNER;
                         (if okToClose then
                             this(ymerLocalWindow)[]
                               ->(this(ymerBrowserWindow).theMenubar).windowsMenu.deleteWindow
                         if);
                      #)
                 #);
               close::<
                 (#
                 do this(ymerLocalWindow)[]
                      ->(this(ymerBrowserWindow).theMenubar).windowsMenu.deleteWindow
                 #)
            #);
          addProject:
            (# name, location: ^text;
               newProj: ^browser.projects.theProjects.element;
            enter (name[], location[])
            <<SLOT ymerAddProject: dopart>>
            #);
          compilerSwitches: ^text;
          ph: @pathHandler;
          browser: @sourcebrowser
            (# <<SLOT browserLib: attributes>>;
               selectNode::
                 (#
                 do (((node.frag.father).fullName).copy, (node.frag.name).copy)
                      ->selectFragmentForm;
                    (node[],1,0,0)->(getFragmentFormEditor).SifViewer.setFocus;
                 #);
               followLink::
                 (# do groupname[]->selectFragmentGroup #);
               makeFileEdit::
                 (# <<SLOT browserFileEdit: dopart>> #);
               projectInfoType::
                 (# <<SLOT projectInfoLib: attributes>>;
                    formList: list
                      (# element:: (# location: @text #) #);
                    groupList: list
                      (# element:: group #);
                    projectList: list
                      (# element:: project;
                         <<SLOT parseDefn: attributes>>;
                         parseProjectFile:
                           (# projectFile: ^text; newProj: ^element
                           enter (projectFile[], newProj[])
                           <<SLOT ymerParseProjectFile: dopart>>
                           #);
                         parseProjectsRC:
                           <<SLOT ymerParseProjectsRC:descriptor>>;
                      #);
                    theProjects: @projectList;
                    project:: 
                      (# location: @text;
                         isDirectory, isProjectFile: @boolean;
                         isRoot, isExtent, isDomain: @boolean;
                         hasSubprojects: @boolean;
                         subprojects: @projectList;
                         level: @integer;
                         unfolded: @boolean;
                         groups: @groupList;
                         scanGroupsImpl::
                           (# newGroup: ^groups.element;
                              dependencyPrivate:
                                @<<SLOT dependencyPrivate: descriptor>>;
                              dependencyRegister:
                                (# fgName: ^text
                                enter fgName[]
                                <<SLOT dependencyRegister: dopart>>
                                #);
                              dependencyCreateSubprojects:
                                (# selectedProj: ^projects.theProjects.element;
                                do this(project)[]->selectedProj[];
                                   selectedProj.subProjects.clear;
                                   <<SLOT dependencyCreateSubprojects:descriptor>>
                                #);
                           <<SLOT scanGroupsImpl: dopart>>
                           #)
                      #);
                    group::
                      (# location: @text;
                         temporary: @boolean;
                         forms: @formList;
                         getfg::
                           (# doRealOpen:
                                (# g: ^mps.AST.FragmentGroup;
                                   isRealOpen:
                                     (# isOpen: @Boolean;
                                     do scanner: g.fragmentList.scan
                                          (#
                                          do (if current.type
                                              // mps.AST.formType then
                                                 (if current.f[]<>NONE then
                                                     true->isOpen;
                                                     leave scanner
                                          if)if)#)
                                     exit isOpen
                                     #)
                                enter g[]
                                do (if not isRealOpen then (* open g real *)
                                       g.realOpen;
                                   if);
                                #);
                           do (if not this(Group).location.empty then
                                  this(Group).location[]
                                    ->mps.fragmentGroupTable.getFragmentGroup
                                    ->fg[];
                                  (if fg[]<>NONE then fg[]->doRealOpen if)
                              if);
                           #);
                      #);
                    scanProjectsImpl::
                      (#
                      do theProjects.scan
                         (# do current[]->thescan.foreach #)
                      #);
                    init::
                      (# 
                      do ph.init;
                         theProjects.init;
                         theProjects.parseProjectsRC;
                      #);
                 #);
               projectsLabel::
                 (# do 'Projects'->t[] #);
               emptyProjectListLabel::
                 (# do 'No Project selected'->t[] #);
               onGroupListOpen::
                 (# <<SLOT onGroupListOpen: dopart>> #);
               onProjectListOpen::
                 (# <<SLOT onProjectListOpen: dopart>> #);
               onCurrentProjectChange::
                 (#
                 do (if newProject[]<>NONE then
                        newProject.location[]->locationView.contents.label
                    if)
                 #);
               onCurrentGroupChange::
                 (#
                 do (if newGroup[]<>NONE then
                        newGroup.location[]->locationView.contents.label
                    if)
                 #);
               onCurrentFormChange::
                 (# <<SLOT onCurrentFormChange: dopart>> #);
               selectFragmentGroup:
                 (# fgName: ^text
                 enter fgName[]
                   <<SLOT selectFragmentGroup: dopart>>
                 #);
               selectFragmentForm:
                 (# fgName: ^text;
                    formName: ^text
                 enter (fgName[],formName[])
                   <<SLOT selectFragmentForm: dopart>>
                 #);
               eventhandler::
                 (# onFatherFrameChanged::
                      (# h,w: @integer
                      do this(ymerBrowserWindow).size->(w,h);
                         ((0,0),(w,h-60))->frame;
                      #)
                 #);
               open:: 
                 (# w,h: @integer
                 do size->(w,h);(w,h+h/2)->size;
                    (w,h+h/2+60)->this(ymerBrowserWindow).size;
                    TRUE->bindRight->bindLeft->bindTop;
                 #);
               editorEncloserType::
                 (# tryclose:: (# do this(ymerBrowserWindow).close #);
                    onHighLight:: (# do this(ymerBrowserWindow).wriggle #);
                    getAST:: (# do mps.AST[]->AST[] #);
                    getBETACFL:: (# do mps.BETACFL[]->BETACFL[] #);
                 #);
            #);
          showPreferencesDialog:
            <<SLOT showPreferencesDialog: descriptor>>;
          locationView: @labelled
            (# contentsType:: staticText(# open:: (# do ''->label #) #);
               eventhandler::
                 (# onFatherFrameChanged::
                      (# h,w: @integer
                      do this(ymerBrowserWindow).size->(w,h);
                         ((0,h-60),(0+w/2,h-60+50))->frame;
                      #)
                 #);
               open::
                 (# h,w: @integer
                 do this(ymerBrowserWindow).size->(w,h);
                    ((0,h-60),(0+w/2,h-60+50))->frame;
                    'Current location'->label;
                 #)
            #);
          infoView: @labelled
            (# contentsType:: staticText(# open:: (# do ''->label #) #);
               eventhandler::
                 (# onFatherFrameChanged::
                      (# h,w: @integer
                      do this(ymerBrowserWindow).size->(w,h);
                         ((w/2,h-60),(w/2+w/2,h-60+50))->frame;
                      #)
                 #);
               open::
                 (# x,y,w,h: @integer
                 do this(ymerBrowserWindow).size->(w,h);
                    w/2->x;
                    h-60->y;
                    w/2->w;
                    50->h;
                    ((x,y),(x+w,y+h))->frame;
                    'Info'->label;
                 #)
            #);
          eventhandler::<
            (# onAboutToClose::<
                 (#
                 do INNER;
                    (if okToClose then
                        this(ymerBrowserWindow)[]
                          ->browsers.at
                          ->browsers.delete;
                        (if browsers.empty then terminate
                         else
                            browsers.scan
                            (#
                            do this(ymerBrowserWindow)[]
                                 ->(current.theMenubar).windowsMenu.deleteWindow
                            #)
                        if)
                    if)           
                 #)
            #); 
          close::<
            (#
            do this(ymerBrowserWindow)[]
                 ->browsers.at
                 ->browsers.delete;
               (if browsers.empty then terminate
                else
                   browsers.scan
                   (#
                   do this(ymerBrowserWindow)[]
                        ->(current.theMenubar).windowsMenu.deleteWindow
                   #)
               if)
            #);
          open::<
            (# t: ^text
            do 'Ymer Fragment Browser (v0.3)'->t[];
               ymerBrowserWindowCount+1->ymerBrowserWindowCount;
               (if ymerBrowserWindowCount>1 then
                   '<'->t.put; ymerBrowserWindowCount->t.putint; '>'->t.put
               if);
               t[]->title;
               ''->compilerSwitches[]; cPrivate(* to initialize *);
               browser.open;
               locationView.open;
               infoView.open;
               INNER;
               browsers.scan
               (#
               do this(ymerBrowserWindow)[]
                    ->(current.theMenubar).windowsMenu.insertWindow;
                  current[]
                    ->(this(ymerBrowserWindow).theMenubar).windowsMenu.insertWindow;
               #);
               this(ymerBrowserWindow)[]->browsers.append;
            #);
          cPrivate: @<<SLOT compilePrivate: descriptor>>;
          ymerPrivate: @<<SLOT ymerPrivate: descriptor>>;
          menubarType::< 
            (# fileMenu: @menu
                 (# iSaveSettings: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# <<SLOT menuFileSaveSettingsOnselect: dopart>> #) 
                           #);
                         open:: (# do 'Save settings'->name #)
                      #);
                    iReloadSettings: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# <<SLOT menuFileReloadSettingsOnselect: dopart>> #) 
                           #);
                         open:: (# do 'Reset settings'->name #)
                      #);
                    iQuit: @menuitem
                      (# eventhandler::
                           (# onSelect::
                                (# cancelQuit: @boolean
                                  <<SLOT menuFileQuitOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'Quit Ymer'->name #)
                      #);
                    open::
                      (# sep: ^menuItem
                      do 'File'->name;
                         iSaveSettings.open; iSaveSettings[]->append;
                         iReloadSettings.open; iReloadSettings[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                         iQuit.open; iQuit[]->append;
                      #);
                  #);
               projectsMenu: @menu
                 (# iLoad: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# t: ^text;
                                   newProj: ^browser.projects.theProjects.element;
                                <<SLOT menuProjectsLoadOnselect: dopart>>
                                #) 
                           #);
                         open:: (# do 'Load existing project'->name #)
                      #);
                    iLoadStd: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# projectFile: ^text;
                                   newProj: ^browser.projects.theProjects.element;
                                <<SLOT menuProjectsLoadStdOnselect: dopart>>
                                #) 
                           #);
                         open:: (# do 'Load standard libraries'->name #)
                      #);
                   iNew: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do ((browser.currentProject[]=NONE)
                                   or browser.currentProject.hasSubProjects)
                                     -> value #);
                              onSelect::
                                (# cp: ^browser.projects.theProjects.element;
                                   newProjectWindow: @ymerLocalWindow
                                     (# switchWidth: @integer;
                                        height, distance,
                                        windowWidth,windowHeight: @integer;
                                        setFrame:
                                          (#
                                          do recalc;
                                             8*height+4*distance->windowHeight;
                                             (windowWidth,windowHeight)->size;
                                          #);
                                        recalc:
                                          (#
                                          do 3->distance;
                                             (switchWidth*3)+distance*4->windowWidth;
                                          #);
                                        bCancel: @pushButton
                                          (# setFrame:
                                               (# x,y,w,h: @integer
                                               do recalc;
                                                  distance->x;
                                                  distance->y;
                                                  (windowWidth-3*distance)/2->w;
                                                  height->h;
                                                  ((x,y),(x+w,y+h))->frame;
                                               #);
                                             open:: (# do setFrame; 'Cancel'->label #);
                                             eventhandler::
                                               (# onMouseUp:: (# do newProjectWindow.hide #);
                                                  onFatherFrameChanged::
                                                    (# do setFrame #)
                                               #)
                                          #);
                                        bOk: @pushButton
                                          (# setFrame:
                                               (# x,y,w,h: @integer
                                               do recalc;
                                                  distance+(windowWidth-3*distance)->x;
                                                  distance->y;
                                                  (windowWidth-3*distance)/2->w;
                                                  height->h;
                                                  ((x,y),(x+w,y+h))->frame;
                                               #);
                                             open:: (# do setFrame; 'OK'->label #);
                                             eventhandler::
                                               (# onMouseUp::
                                                    (#
                                                    do switchEdit.contents.contents->cp.name[];
                                                       (if browser.currentProject[]=NONE then
                                                           cp[]->browser.projects.theProjects.append;
                                                        else
                                                           (if browser.currentProject.hasSubProjects then
                                                               cp[]->browser.currentProject.subProjects.append;   
                                                           if)
                                                       if);
                                                       browser.projectsChanged;
                                                       cp[]->browser.setProject;
                                                       newProjectWindow.close
                                                    #);
                                                  onFatherFrameChanged::
                                                    (# do setFrame #)
                                               #)
                                          #);
                                        switchesFrame: @labelled
                                          (# contentsType:: canvas
                                               (# projectFileBox: @radioButton 
                                                    (# setFrame:
                                                         (# x,y,w,h: @integer
                                                         do recalc;
                                                            0->x;
                                                            distance->y;
                                                            switchWidth->w;
                                                            height->h;
                                                            ((x,y),(x+w,y+h))->frame;
                                                         #);
                                                       open:: 
                                                         (# do setFrame; 'project file'->label #);
                                                       eventhandler:: 
                                                         (# onMouseUp:: (# do true->cp.isProjectFile #);
                                                            onFatherFrameChanged::
                                                              (# do setFrame #)
                                                         #)
                                                    #);
                                                  rootBox: @radioButton 
                                                    (# setFrame:
                                                         (# x,y,w,h: @integer
                                                         do recalc;
                                                            0->x;
                                                            height+distance->y;
                                                            switchWidth->w;
                                                            height->h;
                                                            ((x,y),(x+w,y+h))->frame;
                                                         #);
                                                       open:: 
                                                         (# do setFrame; 'root project'->label #);
                                                       eventhandler:: 
                                                         (# onMouseUp:: (# do true->cp.isRoot #);
                                                            onFatherFrameChanged::
                                                              (# do setFrame #)
                                                         #)
                                                    #);
                                                  subProjectBox: @radioButton 
                                                    (# setFrame:
                                                         (# x,y,w,h: @integer
                                                         do recalc;
                                                            0->x;
                                                            (height+distance)*2->y;
                                                            switchWidth->w;
                                                            height->h;
                                                            ((x,y),(x+w,y+h))->frame;
                                                         #);
                                                       open:: 
                                                         (# do setFrame; 'sub project'->label #);
                                                       eventhandler:: 
                                                         (# onMouseUp:: (# do true->cp.hasSubprojects #);
                                                            onFatherFrameChanged::
                                                              (# do setFrame #)
                                                         #)
                                                    #);
                                                  open::
                                                    (#
                                                    do projectFileBox.open;
                                                       rootBox.open;
                                                       subProjectBox.open;
                                                    #)
                                               #);
                                             setFrame:
                                               (# x,y,w,h: @integer
                                               do recalc;
                                                  distance->x;
                                                  distance+height->y;
                                                  windowWidth-2*distance->w;
                                                  height*5+distance->h;
                                                  ((x,y),(x+w,y+h))->frame;
                                               #);
                                             open::
                                               (# do setFrame; 'Project Type'->label #);
                                             eventhandler::
                                               (# onFatherFrameChanged::
                                                    (# do setFrame #)
                                               #)
                                          #);
                                        switchEdit: @labelled 
                                          (# contentsType:: editText;
                                             setFrame:
                                               (# x,y,w,h: @integer
                                               do recalc;
                                                  distance->x;
                                                  height*6+distance*3->y;
                                                  windowWidth-2*distance->w;
                                                  height*2->h;
                                                  ((x,y),(x+w,y+h))->frame;
                                               #);
                                             open:: 
                                               (# do setFrame; 'Project name'->label #);
                                             eventhandler::
                                               (# onFatherFrameChanged::
                                                    (# do setFrame #)
                                               #)
                                          #);
                                        tst: @pushButton
                                          (# open::
                                               (# ts: ^textStyle;
                                                  buttonLabel: (# exit 'more choises' #);
                                                  buttonWidth: @integer
                                               do buttonLabel->label;
                                                  style->ts[];
                                                  ((buttonLabel->ts.widthOfText)+5,ts.lineHeight+3)
                                                    ->(switchWidth,height);
                                                  switchWidth+8->switchWidth;
                                                  height+8->height;
                                               #)
                                          #);
                                        open::< 
                                          (# 
                                          do tst.open; (* just to get the width and height of fonts *)
                                             tst.hide; tst.close;
                                             
                                             setFrame;
                                             
                                             newProjectWindow.contents->bCancel.open;
                                             newProjectWindow.contents->bOk.open;
                                             
                                             newProjectWindow.contents->switchesFrame.open;
                                             newProjectWindow.contents->switchEdit.open;
                                             'New Project'->title; hide;
                                             INNER
                                          #)
                                     #);
                                do &browser.projects.theProjects.element[]
                                     ->cp[];
                                   newProjectWindow.open;
                                   newProjectWindow.showmodal;
                           #) #);
                         open:: (# do 'Create new project'->name #)
                      #);
                    iSave: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do false and (browser.currentProject[]<>NONE) -> value #);
                              onSelect:: (# do  #) #);
                         open:: (# do 'Save project'->name #)
                      #);
                    iRename: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do browser.currentProject[]<>NONE -> value #);
                              onSelect::
                                (#
                                do (this(ymerBrowserWindow)[],'Project Rename','New project name','')->
                                   promptForText
                                   (# ok::
                                        (# cp: ^browser.projects.project; ch: @char
                                        do browser.currentProject[]->cp[];
                                           usertext->cp.name;
                                           (if true
                                            // cp.isDirectory and 
                                               (cp.name.length>0) and
                                               ((cp.name.length->cp.name.inxGet)<>'/') then
                                               '/'->cp.name.append
                                            // cp.isRoot and 
                                               (cp.name.length>0) and
                                               ((cp.name.length->cp.name.inxGet)<>'*') then
                                               '*'->cp.name.put
                                           if);
                                           browser.projectsChanged;
                                           cp[]->browser.setProject;
                                   #) #);
                                #) 
                           #);
                         open:: (# do 'Rename project'->name #)
                      #);
                    iRelocate: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do browser.currentProject[]<>NONE -> value #);
                              onSelect::
                                (# cp: ^browser.projects.project; t: ^text
                                  <<SLOT menuProjectsRelocateOnselect: dopart>>
                                #) 
                           #);
                         open:: (# do 'Relocate project'->name #)
                      #);
                    iDelete: @menuitem
                      (# eventhandler::
                           (# onStatus::
                                (# do browser.currentProject[]<>NONE -> value #);
                              onSelect::
                                (# cp: ^browser.projects.project
                                  <<SLOT menuProjectsDeleteOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'Delete project'->name #)
                      #);
                    open::
                      (# sep: ^menuItem
                      do 'Project'->name;
                         iNew.open; iNew[]->append;
                         iLoad.open; iLoad[]->append;
                         iLoadStd.open; iLoadStd[]->append;
                         iSave.open; iSave[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                         iRename.open; iRename[]->append;
                         iRelocate.open; iRelocate[]->append;
                         iDelete.open; iDelete[]->append;
                      #);
                 #);
               groupsMenu: @menu
                 (# iNew: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do (browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isProjectFile or
                                   browser.currentProject.isDirectory)
                                     ->value
                                #);
                              onSelect::
                                (# t: ^text;
                                   newGroup: ^browser.projects.group;
                                <<SLOT menuGroupsNewOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'New group'->name #)
                      #);
                    iAdd: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do (browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isProjectFile)
                                     ->value
                                #);
                              onSelect:: 
                                (#
                                <<SLOT menuGroupsAddOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'Add group'->name #)
                      #);
                    iDelete: @menuItem
                      (# eventhandler:: 
                           (# onStatus::
                                (#
                                do (browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isProjectFile or
                                   browser.currentProject.isDirectory)
                                     ->value
                                #);
                              onSelect::
                                (#
                                <<SLOT menuGroupsDeleteOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'Delete group'->name #)
                      #);
                    iSemantic: @menuItem
                      (# eventhandler:: 
                           (# onStatus::
                                (#
                                <<SLOT menuGroupsSemanticOnstatus: dopart>>
                                #);
                              onSelect::
                                (#
                                <<SLOT menuGroupsSemanticOnselect: dopart>>
                                #)
                           #);
                         open:: (# do 'Viev Semantic Errors'->name #)
                      #);
                    open::
                      (# sep: ^menuItem
                      do 'Group'->name;
                         iNew.open; iNew[]->append;
                         iAdd.open; iAdd[]->append;
                         iDelete.open; iDelete[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                         iSemantic.open; iSemantic[]->append;
                      #);
                 #);
               toolsMenu: @menu
                 (# iCompile: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do (browser.currentGroup[]<>NONE) or
                                   ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain or
                                   browser.currentProject.isDirectory))
                                     ->value
                                #);
                              onSelect::
                                (# compile: <<SLOT iCompile: descriptor>>
                                do compile
                                #)
                           #);
                         open:: (# do 'Compile'->name #)
                      #);
                    iExecute: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect::
                                (# execute: <<SLOT iExecute: descriptor>>
                                do execute
                                #)
                           #);
                         open:: (# do 'Execute'->name #)
                      #);
                    iDebug: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# debug: <<SLOT iDebug: descriptor>>
                                do debug
                                #)
                           #);
                         open:: (# do 'Debug'->name #)
                      #);
                    iSif: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# sif: <<SLOT iSif: descriptor>>
                                do sif
                                #)
                           #);
                         open:: (# do 'Edit: Sif'->name #)
                      #);
                    iFreja: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# sif: <<SLOT iFreja: descriptor>>
                                do sif
                                #)
                           #);
                         open:: (# do 'Edit: Freja'->name #)
                      #);
                    iTextEdit: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# textEdit: <<SLOT iTextEdit: descriptor>>
                                do textEdit
                                #)
                           #);
                         open:: (# do 'Edit: Text editor'->name #)
                      #);
                    iInterfaceBuilder: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# sif: <<SLOT iInterfaceBuilder: descriptor>>
                                do sif
                                #)
                           #);
                         open:: (# do 'Edit: InterfaceBuilder'->name #)
                      #);
                    iAnalyze: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (#
                                do ((browser.currentProject[]<>NONE) and
                                   (browser.currentProject.isRoot or
                                   browser.currentProject.isExtent or
                                   browser.currentProject.isDomain)) or
                                   (browser.currentGroup[]<>NONE)
                                     ->value
                                #);
                              onSelect:: 
                                (# analyze: <<SLOT iAnalyze: descriptor>>
                                do analyze
                                #)
                           #);
                         open:: (# do 'Analyze: betawc'->name #)
                      #);
                    iPreferences: @menuItem
                      (# eventhandler::
                           (# onSelect:: (# do showPreferencesDialog #)
                           #);
                         open:: (# do 'Preferences'->name #)
                      #);
                    open::
                      (# sep: ^menuItem
                      do 'Tools'->name;
                         iCompile.open; iCompile[]->append;
                         iExecute.open; iExecute[]->append;
                         iDebug.open; iDebug[]->append;
                         iSif.open; iSif[]->append;
                         iFreja.open; iFreja[]->append;
                         iTextEdit.open; iTextEdit[]->append;
                         iInterfaceBuilder.open; iInterfaceBuilder[]->append;
                         iAnalyze.open; iAnalyze[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                         iPreferences.open; iPreferences[]->append;
                      #);
                 #);
               historyMenu: @menu
                 (# addHistoryElement:
                      <<SLOT historyMenuAddHistoryElement: descriptor>>;
                    open:: (# do 'History'->name #);
                 #);
               windowsMenu: @menu
                 (# <<SLOT windowsMenuPrivate: attributes>>;
                    insertWindow: (# win: ^window enter win[] do <<SLOT windowsMenuInsertWindow: descriptor>> #);
                    deleteWindow: (# win: ^window enter win[] <<SLOT windowsMenuDeleteWindow: dopart>> #);
                    iSeparateBrowser: @menuItem
                      (# eventhandler::
                           (# onSelect::
                                (# <<SLOT windowsMenuSeparateBrowser: dopart>> #)
                           #);
                         open:: (# do 'Open Separate Browser'->name #)
                      #);
                    iSeparateCodeView: @menuItem
                      (# eventhandler::
                           (# onStatus::
                                (# do browser.currentForm[]<>NONE -> value #);
                              onSelect:: 
                                (# <<SLOT windowsMenuSeparateCodeView: dopart>> #)
                           #);
                         open:: (# do 'Open Separate Code Viewer'->name #)
                      #);
                    iWorkspace: @menuItem
                      (# eventhandler::
                           (# onSelect:: 
                                (# <<SLOT windowsMenuWorkspace: dopart>> #)
                           #);
                         open:: (# do 'Open Workspace'->name; #)
                      #);
                      
                    open::
                      (# sep: ^menuItem
                      do 'Windows'->name;
                         iWorkspace.open; iWorkspace[]->append;
                         iSeparateBrowser.open; iSeparateBrowser[]->append;
                         iSeparateCodeView.open; iSeparateCodeView[]->append;
                         &separator[]->sep[]; sep.open; sep[]->append;
                      #);
                 #);
               open::<
                 (#
                 do fileMenu.open; fileMenu[]->append;
                    projectsMenu.open; projectsMenu[]->append;
                    groupsMenu.open; groupsMenu[]->append;
                    historyMenu.open; historyMenu[]->append;
                    toolsMenu.open; toolsMenu[]->append;
                    windowsMenu.open; windowsMenu[]->append;
                    INNER;
                 #)
            #);
       #);
     
     BETAlib, BETAexecutable,
     SIFexecutable, FREJAexecutable, INTERFACEBUILDERexecutable,
     VALHALLAexecutable, TEXTexecutable, BETAWCexecutable: ^text;
     machineType: @text;
     theSystemEnv: ^systemenv;
     
     init:<
       (# do <<SLOT ymerInit: descriptor>> #);
  #);
