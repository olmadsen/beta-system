ORIGIN '~beta/guienv/v1.2/guienv';
INCLUDE 'sourcebrowser';

INCLUDE '~beta/basiclib/v1.4/formatio';
INCLUDE '~beta/basiclib/v1.4/file';
INCLUDE '~beta/basiclib/v1.4/directory';
INCLUDE '~beta/sysutils/v1.4/pathhandler';

INCLUDE '~beta/objectbrowser/v2.0/mpsinterface';
INCLUDE '~beta/objectbrowser/v2.0/betagrammarinit';

--- program:descriptor ---
guienv
(# mps: @mpsinterface
     (# onOpenGroup:: (# do 'Opening %s...'->putformat(# do groupname[]->s#)#);
        onOpenGroupDone:: (# do 'Done'->putline #);
        onOldGroup::
          (# do '%s needs parsing'->putformat(# do groupname[]->s#)#);
        init::
          (# 
          do betagrammarinit;
          #);
     #);
   browserwin: @Window
     (# browser: @sourcebrowser
          (# ph: @pathHandler;
             currentDir: ^Text;
             projectInfoType::
               (# 
                  scanFragmentGroups:
                    (# currentGroup: ^Text;
                       p: ^project;
                    enter p[]
                    do p.dir.scanEntries
                       (# 
                       do select
                          (# whenFile::
                               (# f: ^file; ext: ^Text;
                               do thefile->f[];
                                  f.entry.path.name.extension->ext[];
                                  '.'->ext.prepend;
                                  (if mps.AST.astfileExtension->ext.equal then
                                      foundFullPath->currentGroup[];
                                      (1,currentGroup.length-ext.length)
                                        ->currentGroup.sub
                                        ->currentGroup[];
                                      INNER scanFragmentGroups;
                                  if);
                               #);       
                          #);
                       #);
                    #);
                  
                  currentProjects: @List
                    (# element:: project;
                       setcurrentdir:
                         (* Enter NONE to set currentDir to the current
                          * directory of this process. *)
                         (# d: @Directory;
                         enter currentDir[]
                         do clear;
                            (if currentDir[]=NONE then
                                ph.currentDirectory->currentDir[];
                            if);
                            currentDir[]->d.name;
                            d.scanEntries
                            (# 
                            do select
                               (# whenDir::
                                    (# d: ^Directory; new: ^project;
                                    do thedir->d[];
                                       &project[]->new[];
                                       (d.name,currentDir[])
                                         ->ph.localPath
                                         ->new.name[];
                                       d[]->new.dir[];
                                       new[]->append;
                                    #);       
                               #);
                            #);
                         #);
                    #);
                  
                  project:: 
                    (# dir: ^Directory;
                       numberOfGroups::
                         (# 
                         do THIS(project)[]->scanFragmentGroups
                            (# 
                            do value+1->value
                            #);
                         #);
                       scanGroupsImpl::
                         (#
                         do THIS(project)[]->scanFragmentGroups
                            (# g: ^group;
                            do &group[]->g[];
                               currentGroup[]->g.fullName[];
                               (currentGroup[],dir.name)
                                 ->ph.localPath
                                 ->g.name[];
                               g[]->thescan.foreach;
                            #);
                         #);
                    #);
                  
                  group::
                    (# fullName: ^Text;
                       getfg::
                         (#
                         do fullName[]
                              ->mps.fragmentGroupTable.getFragmentGroup
                              ->fg[];
                         #);
                    #);
                  
                  numberOfProjects:: (# do currentProjects.size->value #);
                  
                  scanProjectsImpl::
                    (#
                    do currentProjects.scan (# do current[]->thescan.foreach #)
                    #);
                  
                  init::
                    (# 
                    do ph.init;
                       currentProjects.init;
                       NONE->currentProjects.setcurrentdir;
                    #);
               #);
             
             projectsLabel::
               (# 
               do currentDir[]->t[];
               #);
             
             onProjectListOpen::
               (# 
               do 
                  &theprojectlist.action
                  (# eventtype:: theprojectlist.theeventhandler.mousedown;
                     inx: @Integer;
                  do (if theEvent.doubleClick then
                         (if (theprojectlist.selection.first->inx)<>0 then
                             find: projects.currentProjects.scan
                               (# 
                               do inx-1->inx;
                                  (if inx=0 then
                                      (current.name[],currentDir[])
                                        ->ph.convertfilepath
                                        ->projects.currentprojects.setcurrentdir;
                                      projectsChanged;
                                      leave find; 
                                  if);
                               #);
                         if);
                     if);
                  #)[]->theprojectlist.appendAction;
               #);
             
             open:: 
               (#
               do size->browserwin.size;
                  TRUE->bindRight->bindLeft->bindTop->bindBottom;
               #);             
             
             editorEncloserType::
               (# tryclose:: (# do browserwin.close #);
                  onHighLight:: (# do browserwin.wriggle #);
                  getAST:: (# do mps.AST[]->AST[] #);
                  getBETACFL:: (# do mps.BETACFL[]->BETACFL[] #);
               #);
          #);
        open:: (# do browser.open #);
     #);
do mps.init;
   browserwin.open
#)
