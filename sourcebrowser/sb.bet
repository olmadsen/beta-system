ORIGIN 'ymer';

BODY 'private/createdialogbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- ymerApplicationLib:attributes ---
createDialog: window
  (# <<SLOT createDialogLib: attributes>>;
     create:< Object
       (* Called when the OK button is clicked *);
     cancel:< object 
       (* Called when the CANCEL button is clicked *);
     grammar, groupName, formName: ^text; (* The text entered by user *)
     popup:<
       (* Used to pop up the create dialog and wait for the
        * text to be typed
        *)
       (# titleText, groupText, formText: ^text;
          father: ^window;
       enter (father[], titleText[], groupText[], formText[])
       <<SLOT createPopup: dopart>>
       #);
     
     open::< (# <<SLOT createOpen: dopart>> #);
     private: @<<SLOT createPrivate: descriptor>>;
  enter popup
  #)

--- program: descriptor ---
ymerApplication
(# created: @createDialog
     (# create::
          (#
          do 'Create: '->puttext;
             grammar[]->putline;
             groupName[]->putline; 
             formName[]->putline;
          #);
        cancel::
          (#
          do 'Cancel: '->puttext;
             grammar[]->putline;
             groupName[]->putline; 
             formName[]->putline;
          #);
     #);
   init::
     (#
     do created.open;
        (none,'Create new group','untitled','untitled')->created.popup
     #)
do init
#)
