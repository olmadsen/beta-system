ORIGIN '~beta/freja/frejalib';
INCLUDE '~beta/interfacebuilder/friggapplication'
        '~beta/process/processmanager'
        'seticon';
MDBODY nti 'mjolner_ntibody'
       mac 'mjolner_macbody'
       ppc 'mjolner_macbody'
       ppcmac 'mjolner_macbody'
       default 'mjolner_unixbody';
-- frejalibdopart: Descriptor --
(# 
do
   &friggApplication
     (#
        ymerBrowserWindow::< 
          (#
             browserTitle::  (#  do 'Mjolner'->title #);
             externalTextedit:: 
               (#  do <<SLOT externalTextEdit:Descriptor>> #);
             onTerminateApplication::< 
               (# 
               do
               (* (if not sifHidden then
                true->sifHidden;
                theFragmentBrowser.ymerBrowser.hide;
                'Show Sif'
                ->
                theModel.theGraphicalInterface.theDesignInterface.theFileMenu.
                hideShowSifItem.rename
                if)*) 
               #);
             quit::  (#  #);
             menubarType:: 
               (#
                  frejaMenu: @simpleMenu
                    (#
                       iNewLib: @item
                         (# onSelect::  (#  do FrejaNewLib #) #);
                       iNewProgram: @item
                         (# onSelect::  (#  do FrejaNewProgram #) #);
                       iNewGraphicsPage: @item
                         (# onSelect::  (#  do FrejaNewGraphicsPage #)
                         #);
                       iOpen: @item
                         (# onSelect::  (#  do FrejaOpen #) #);
                       iShowClassDiag: @item
                         (# onSelect::  (#  do FrejaShowClassDiag #) #);
                       iShowObjectDiag: @item
                         (# onSelect::  (#  do FrejaShowObjectDiag #)
                         #);
                       iLoadGraphics: @item
                         (# onSelect::  (#  do FrejaLoadGraphics #) #);
                       open:: 
                         (# 
                         do
                            'New Diagram...'->iNewLib.new;
                            (* 'New Program...'->iNewProgram.new; *)
                            'New Graphics Page...'->iNewGraphicsPage.new;
                            'Open Diagram...'->iOpen.new;
                            newSeparator;
                            'Show Class Diagram...'->iShowClassDiag.new;
                            'Show Object Diagram...'->iShowObjectDiag.new;
                            newSeparator;
                            'Load Graphics...'->iLoadGraphics.new
                         #)
                    #);
                  open::  (#  do 'Diagrams'->frejaMenu.new #)
               #);
             open::  (#  do SetMjolnerIcon #)
          #);
        isBETAsif::<  (#  do true->value; INNER #);
        programs: @list (# element:: process #);
        executeProgram:: 
          (# p: @process
          do name[]->p.init; p.start; p[]->programs.append
          #);
        isProgramRunning:: 
          (# 
          do
             programs.scan
               (# 
               do
                  (if current.name[]->name.equal then
                      current.stillRunning->value;
                      (if not value then
                          current[]->programs.at->programs.delete
                      if)
                  if)
               #)
          #);
        killProgram:: 
          (# 
          do
             programs.scan
               (# 
               do
                  (if current.name[]->name.equal then
                      current.stop;
                      current.awaitStopped;
                      current[]->programs.at->programs.delete
                  if)
               #)
          #);
        askKillPrograms:: 
          (# msg: ^text
          do
             programs.scan
               (# 
               do
                  (if current.stillRunning then
                      'Kill '->msg[];
                      current.name[]->msg.putText;
                      '?'->msg.put;
                      (none ,'Kill program',msg[])
                        ->promptForBoolean
                          (#
                             ok::< 
                               (# 
                               do
                                  current.stop;
                                  current.awaitStopped;
                                  current[]->programs.at->programs.delete
                               #);
                             notOK::<  (#  do  #);
                             cancel::<  (#  do  #)
                          #)
                   else
                      current[]->programs.at->programs.delete
                  if)
               #)
          #);
        debugProgram:: 
          (# p: @process
          do debugger[]->p.init; program[]->p.argument.append; p.start
          #);
        useExternalTextEditor:: 
          (#  do true (*enable always useExtTextEd*) ->value #);
        frejaInclude::< 
          (# 
          do theModel.theGraphicalInterface.frejaAssociationsInclude->t[]
          #);
        init::< 
          (# 
          do
             ('Sif: Known Bugs and Inconveniences','~beta/editor/v5.2/doc/TODO')
               ->helpDocument;
             ('Freja: Known Bugs and Inconveniences',
              '~beta/freja/v3.1/FrejaSifStatus')->helpDocument;
             ('Freja FAQ','~beta/freja/doc/freja-faq.html')->helpDocument;
             
          #);
        fg: ^astInterface.fragmentGroup
     do
        <<SLOT mjolnerPlatformInit:Descriptor>>;
        'Mjolner Programming Environment version 6.0 (0) '->puttext;
        machine_type->putLine;
        'integrated with the BETA Compiler version '->putText;
        compilerVersion->putLine;
        (if noOfarguments > 1 then
            2->arguments->ymerBrowser.appendProject; ymerbrowser.appendDone
         else
            (ymerbrowser[],'untitled',false)->ymerbrowser.edenv.newBetaProgram
              ->fg[];
            (if fg[] <> none then fg.fullName->ymerbrowser.addproject if)
        if);
        theModel
     #)[]->theFragmentBrowser[]
#)  

