ORIGIN '~beta/freja/frejalib';
INCLUDE '~beta/interfacebuilder/friggapplication'
'~beta/process/processmanager'
'seticon'
'~beta/newdebugger/valhallaapplication';
INCLUDE '~beta/toollibs/preferences/cmdOptions';
INCLUDE '~beta/toollibs/preferences/preferencesDB';
INCLUDE 'mjolnerOptions';
INCLUDE '../prefDialog/prefDialog';
MDBODY nti 'mjolner_ntibody'
mac 'mjolner_macbody'
ppc 'mjolner_macbody'
ppcmac 'mjolner_macbody'
default 'mjolner_unixbody';
-- frejalibdopart: Descriptor --
(# 
do
   &valhallaApplication
   (# titleText:(# exit 'Mjolner' #);
      editMode::(# do false->value; #);
      valhallaMainWindow::<
        (# sifMenu:^codeEditorPopUpmenu;
           onshow::<
             (# t:@text;
             do titletext->t.append;
                ' -- Debugging '->t.append;
                t[]->ymerbrowser.title;
                '\n open Valhalla Window'->putline; 
                true->isDebugging;
                true->ymerbrowser.edenv.setGlobalWriteProtection;
                INNER;
             #);
           onHide::<
             (# 
             do objectviews.newscan
                (#  do cur.encl.tryclose #);
                stackviews.newscan
                (#  do cur.encl.tryclose #);
                
                (if debuggeeIsReady and (debuggee[] <> none ) then
                    debuggee.kill; 
                if);
                (if debuggee[]<>none then
                    (if not debuggee.terminated then
                        debuggee.kill; 
                    if);
                if);
                titletext->ymerbrowser.title; 
                false->isDebugging;
                false->ymerbrowser.edenv.setGlobalWriteProtection;
                ''->ymerbrowser.writeInfoView;
                INNER
             #);                      
        #);
      ymerBrowserWindow::< 
        (# 
           isEditor::<(# do not isDebugging->value; #);
           allowDebug::<
             (# touchedAcc:@integer
             do
                (* edenv.groupEditorList.scan(# do current.touched+touchedAcc->touchedAcc; #);
                 (touchedAcc>0)->value;
                 *)
             #);
           browserTitle::  (#  do titleText->title #);
           externalTextedit:: 
             (#  do <<SLOT externalTextEdit:Descriptor>> #);
           onTerminateApplication:: 
             (# 
             do
                (* (if not sifHidden then
                 true->sifHidden;
                 theFragmentBrowser.ymerBrowser.hide;
                 'Show Sif'
                 ->
                 theModel.theGraphicalInterface.theDesignInterface.theFileMenu.
                 hideShowSifItem.rename
                 if)*) 

                (if debuggeeIsReady and (debuggee[] <> none ) then
                    debuggee.kill; onTerminate
                if);
             #);
           quit::  
             (# 
             do
                (if debuggeeIsReady and (debuggee[] <> none ) then
                    debuggee.kill; onTerminate
                if)
             #);
           menubarType:: 
             (#
                filemenutype::<
                  (# iOptions:@item
                       (# prefwin:@preferencesDialog
                            (#
                               onCloseOK::
                                 (#
                                 do
                                    prefwin.tabcontrols.valhallaPreferences.pcanvas.booloptions.savestate;
                                    (mjolneroptionsfilename,valhallaprefs.sectionname)->valhallaprefs.save;
                                 #)
                            #);
                          onSelect::(# do
                                       valhallaPrefs[]->prefwin.tabcontrols.valhallaPreferences.pcanvas.Prefs[];
                                       compilerPrefs[]->prefwin.tabcontrols.compilerPreferences.pcanvas.Prefs[];
                                       prefwin.open; 
                                       prefwin.tabcontrols.valhallaPreferences[]->prefwin.tabcontrols.selection;
                                    #);
                       #);
                     open::<
                       (# 
                       do 'Options'->iOptions.new;
                          newseparator;
                       #);
                  #);
               
                frejaMenu: @simpleMenu
                  (#
                     iNewLib: @item
                       (# onSelect::  (#  do FrejaNewLib #) #);
                     iNewProgram: @item
                       (# onSelect::  (#  do FrejaNewProgram #) #);
                     iNewGraphicsPage: @item
                       (# onSelect::  (#  do FrejaNewGraphicsPage #)
                       #);
                     iOpen: @item
                       (# onSelect::  (#  do FrejaOpen #) #);
                     iShowClassDiag: @item
                       (# onSelect::  (#  do FrejaShowClassDiag #) #);
                     iShowObjectDiag: @item
                       (# onSelect::  (#  do FrejaShowObjectDiag #)
                       #);
                     iLoadGraphics: @item
                       (# onSelect::  (#  do FrejaLoadGraphics #) #);
                     open:: 
                       (# 
                       do
                          'New Diagram...'->iNewLib.new;
                          (* 'New Program...'->iNewProgram.new; *)
                          'New Graphics Page...'->iNewGraphicsPage.new;
                          'Open Diagram...'->iOpen.new;
                          newSeparator;
                          'Show Class Diagram...'->iShowClassDiag.new;
                          'Show Object Diagram...'->iShowObjectDiag.new;
                          newSeparator;
                          'Load Graphics...'->iLoadGraphics.new
                       #)
                  #);
                open::  (#  do 'Diagrams'->frejaMenu.new #)
             #);
           open::  (#  do SetMjolnerIcon #)
        #);
      isBETAsif::<  (#  do true->value; INNER #);
      programs: @list (# element:: process #);
      executeProgram:: 
        (# p: @process
        do name[]->p.init; p.start; p[]->programs.append
        #);
      isProgramRunning:: 
        (# 
        do
           programs.scan
           (# 
           do
              (if current.name[]->name.equal then
                  current.stillRunning->value;
                  (if not value then
                      current[]->programs.at->programs.delete
                  if)
              if)
           #)
        #);
      killProgram:: 
        (# 
        do
           programs.scan
           (# 
           do
              (if current.name[]->name.equal then
                  current.stop;
                  current.awaitStopped;
                  current[]->programs.at->programs.delete
              if)
           #)
        #);
      debugProgram:: 
        (#
           p: @process;
        do
           (*FGC, debugger[]->p.init; program[]->p.argument.append; p.start*)
           
           'Debugging: '->puttext; program[]->putline; newline;
           
           program[]->setEXECNAMEparam;
           
           (if debuggeeOutDated then
               'Debuggee Changed'->putline;
               (if main.opened then main.hide; if);
               leave debugProgram;
           if);

           (if not main.opened then
               main.open;
               
               main.theMenuBar->main.mainMenuBar[]; (* from valhallagui 731 *)
           if);

           main.onshow;
           main.show;
           main.bringtofront;
           
           (*FGC, from valhalla.bet *)
           

           
           ensureKilled;
           onTerminate;
           0->setPIDparam;
           FALSE->runNewDebuggee;
           (if debuggee[] <> none then
               ('program',debuggee.MPS.BETA.DescriptorForm)
                 ->debuggee.DBmanager.slotnameToGroups
               (#  do current[]->loadSources #);
           if);
           
        #);
      useExternalTextEditor:: 
        (#  do true (*enable always useExtTextEd*) ->value #);
      frejaInclude::< 
        (# 
        do theModel.theGraphicalInterface.frejaAssociationsInclude->t[]
        #);
      init::< 
        (# 
        do
           ('Sif: Known Bugs and Inconveniences','~beta/editor/v5.2/doc/TODO')
             ->helpDocument;
           ('Freja: Known Bugs and Inconveniences',
           '~beta/freja/v3.1/FrejaSifStatus')->helpDocument;
           ('Freja FAQ','~beta/freja/doc/freja-faq.html')->helpDocument;
           (* FGC, *)
           ymerbrowser[]->sources[];
        #);
      fg: ^astInterface.fragmentGroup;
      mjolnerPrefs:^mjolnerPreferences;
      compilerPrefs:^compilerPreferences;
   do
      'Mjolner Programming Environment version 6.0 (0) '->puttext;
      machine_type->putLine;
      'integrated with the BETA Compiler version '->putText;
      compilerVersion->putLine;
      'Warning: Extended with Valhalla, errors occur!!'->putline;
      
      
      objectpool.get(# type::<mjolnerPreferences;#)->mjolnerPrefs[];
      objectpool.get(# type::<valhallaPreferences;#)->valhallaPrefs[];
      objectpool.get(# type::<compilerPreferences;#)->compilerPrefs[];
      
      (if mjolnerprefs.noOfArgs>0 then
          mjolnerprefs.scanArguments
          (# fgname,ffname:^text;
             f:@file;
          do 
             current[]->analyzeArg->(fgname[],ffname[]);
             (if fgname.length > 0 then
                 (if ffname.length > 0 then
                     fgname[]->f.name;
                     (if f.entry.exists then
                         (if ffname.length > 0 then
                             fgname[]->ymerBrowser.appendProject;
                             (fgname[],ffname[])
                               ->ymerBrowser.browser.selectFragmentform
                          else
                             fgname[]->ymerbrowser.appendProject
                         if)
                     if)
                  else
                     fgname[]->ymerbrowser.appendProject
                 if)
             if);
             ymerbrowser.appendDone;
          #);
       else
          (ymerbrowser[],'untitled',false)->ymerbrowser.edenv.newBetaProgram
            ->fg[];
          (if fg[] <> none then fg.fullName->ymerbrowser.addproject if);
      if);
      
      theModel
   #)[]->theFragmentBrowser[];
#)  

