ORIGIN '../mjolnerpanel';
INCLUDE '~beta/win32lib/registry';


--- lib:attributes ---
RegistryKeyRoot:
  (#  
  exit 'SOFTWARE\\Mjolner Informatics\\Development Environment'
  #);

getValFromReg:
  (# var,val: ^text;
  enter var[]
  do (HKEY_LOCAL_MACHINE, RegistryKeyRoot, var[])
       ->readFromRegistry->val[]; 
  exit val[]
  #);
setValToReg:
  (# var,val: ^text;
  enter (var[],val[])
  do (HKEY_LOCAL_MACHINE, RegistryKeyRoot, 
     none, REG_SZ, var[], val[])->SetRegKeyValue;
  #);

--- MjolnerPanelDropListLoad: dopart ---
do CurKeyName->getValFromReg->cur[];
   ListKeyName->getValFromReg->list[];
   (if list[] = none then
       init;
    else
       list.reset;
       L:
         (if not list.eos then
             list.getAtom->t[]->append;
             (if (cur[] <> none) and (cur[]->t.equal) then
                 names.range->curidx
             if);
             restart L
         if)
   if);
   INNER               
   
--- MjolnerPanelDropListSave: dopart ---
do &Text[]->list[];
   list.reset;
   (for i: names.range repeat
        names[i][]->list.puttext;
        (if i <> names.range then ' '->list.put;  if)
   for);
   (ListKeyName,list[])->setValToReg;
   (CurKeyName, GetCur)->setValToReg;
   INNER;
   
--- MjolnerPanelBindIconDP: dopart ---
do BetalibList.GetCur -> doit[];
   '\\bin\\'->doit.putText;
   targetList.GetCur->doit.putText;
   '\\mjolnerdoit "OpenFile %1"'->doit.putText;
   (HKEY_CLASSES_ROOT, 
   'MjolnerBeta.BetaSource\\Shell\\open\\command',
   NONE, REG_SZ, NONE,
   doit[]) -> SetRegKeyValue;
   (HKEY_CLASSES_ROOT, 
   'MjolnerBeta.AbstractSyntaxTree\\Shell\\open\\command',
   NONE, REG_SZ, NONE,
   doit[]) -> SetRegKeyValue;
   (HKEY_CLASSES_ROOT, '.astL', NONE, REG_SZ, NONE,
   'MjolnerBeta.AbstractSyntaxTree') 
     -> SetRegKeyValue;
   (HKEY_CLASSES_ROOT, '.bet', NONE, REG_SZ, NONE,
   'MjolnerBeta.BetaSource') 
     -> SetRegKeyValue;
   BetalibList.Save;
   TargetList.Save;
   
