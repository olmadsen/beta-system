ORIGIN '~beta/basiclib/systemenv';
INCLUDE '~beta/process/streamsocket';
INCLUDE '~beta/process/processmanager';
INCLUDE '~beta/basiclib/mbs_version';

--- program:descriptor ---
systemEnv
(# 
   port: (# m: @mbs_version; 
         exit 6000+100*m.major+10*m.minor+m.revision
         #);
   host: (# exit 'localhost' #);
   
   s: @StreamSocket
     (# error::
          (# 
          do true->SocketFailedFlag;
             errCB_abortOperation->value;
          #)
     #);
   SocketFailedFlag: @Boolean;
   arg: ^Text;
   
   startNewMjolner:
     (# p: @Process;
     do 'mjolner'->p.init;
        arg[]->p.argument.append;
        p.start;
     #);
        
do (if noOfArguments<2 then
       'Usage: mjolnerdoit "<Command> <Arguments>+"'->screen.putLine;
       'Commands include "OpenFile <filename>"'->screen.putLine;
       (normal, NONE)->Stop;
   if);
   &Text[]->arg[];
   (for i: noOfArguments-1 repeat
        i+1->arguments->arg.putText;
        (if i<>noOfArguments-1 then
            ' '->arg.put;
        if)
   for);
   s.init;
   (2, host, port) -> s.connect;
   (if SocketFailedFlag then
       startNewMjolner;
    else
       arg[]->s.putLine;
       s.close;
   if);
#)
