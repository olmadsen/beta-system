ORIGIN '~beta/basiclib/basicsystemenv';
INCLUDE '~beta/guienv/guienvsystemenv'
        '~beta/editor/ymercall'
        '~beta/sourcebrowser/ymer'
        '~beta/process/processmanager'
        '~beta/editor/ymersiflib'
        '~beta/interfacebuilder/friggapplication'
        '~beta/freja/frejalib'
        '~beta/freja/frejaoptions'
        'private/mjolnerOptions'
        '~beta/debugger/valhallaapplication'
        'prefDialog/prefDialog'
        '~beta/toollibs/preferences/preferencesDB';
BODY '~beta/editor/private/nouser';
BODY '~beta/editor/private/compiler';
BODY '~beta/freja/private/frejaapplicationbody';
BODY '~beta/debugger/private/valhallabody';
BODY '~beta/debugger/private/breakpointMenus';
BODY '~beta/debugger/private/dynamic_compilation';
BODY '~beta/debugger/private/valhallabodyext';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-99
 *   All rights reserved.
 *   
 * NEW Integration Model!! 1.0
 *)
-- program: Descriptor --
(#
   mjolnerarguments,valhallaarguments,frejaarguments: ^text;
   readArguments:
   (* parse arguments in form:
    * mjolner -q -w filnavn --freja -s1231 --valhalla -bopt *)
     (#
        frejapos,valhallapos,mjolnerend: @integer;
        i: @integer;
        arguments: @text;
        
     do
        (for i: noOfArguments-1 repeat
          i+1->argumenthandler.getArgByNumber->arguments.append;
          ' '->arguments.put;
          
        for);
        'All args:   %s\n'->putformat (#  do arguments[]->s #);
        arguments.reset;
        '--freja'->arguments.findText (#  do inx->frejapos;  #);
        '--valhalla'->arguments.findText (#  do inx->valhallapos #);
        (if frejapos > 0 then
            (if valhallapos > 0 then
                (frejapos+7,valhallapos-1)->arguments.sub->frejaarguments[];
                (valhallapos,arguments.length)->arguments.sub
                  ->valhallaarguments[];
                
             else
                (frejapos+7,arguments.length)->arguments.sub->frejaarguments[]; 
            if);
            
        if);
        'Freja args: %s\n'->putformat (#  do frejaarguments[]->s #);
        'Valhalla args: %s\n'->putformat (#  do valhallaarguments[]->s #);
        (if frejapos > 0 then
            frejapos-1->mjolnerend; (* find out where mjolners options ends *) 
         else
            (if valhallapos > 0 then
                valhallapos-1->mjolnerend; 
             else
                arguments.length->mjolnerend; 
            if);
            
        if);
        (0,mjolnerend)->arguments.sub->mjolnerarguments[];
        
     #);
   theSystemEnv: @|systemenv
     (#
        setWindowEnv::  (#  do gui[]->theWindowEnv[] #);
        fg: ^ymerBrowser.mps.ast.fragmentGroup;
        
     do
        gui[]->ymer.init;
        setupValhalla;
        compilerPrefs.open
          (# 
          do
             compilerPrefs.registerCompileroptions;
             compilerPrefs.registerCompilerCmdoptions;
             
          #);
        compilerPrefs[]->objectpool.put;
        editorPrefs.open
          (#  do editorPrefs.registerEditoroptions;  #);
        editorPrefs[]->objectpool.put;
        frejaPrefs.open
          (#  do frejaPrefs.registerFrejaCmdOptions;  #);
        frejaPrefs[]->objectPool.put;
        readArguments;
        mjolnerarguments[]->mjolnerPrefs.readArguments;
        frejaarguments[]->frejaPrefs.readArguments;
        ymer.getInterface->sif.init;
        ymer.getInterface->frigg.init;
        ymer.getInterface->frej.init;
        Setup;
        setupMenus;
        ymer.newbrowser;
        (if mjolnerprefs.noOfArgs > 0 then
            mjolnerprefs.scanArguments
              (# fgname,ffname: ^text; f: @file; 
              do current[]->ExternalAppendProject; 
              #);
            
         else
            (if untitledExists then
                'untitled1'->ymerbrowser.appendProject; ymerbrowser.appendDone
             else
                (ymerbrowser[],'untitled1',false)
                  ->ymerbrowser.edenv.newBetaProgram->fg[];
                (if fg[] <> none then
                    fg.fullName->ymerbrowser.appendProject;
                    ymerbrowser.appendDone
                if)
            if);
            
        if);
        
     #);
   untitledExists: booleanValue
     (# f,astFile,textFile: @file; astfileName,textFileName: ^text
     do
        'untitled1'->f.name;
        'untitled1'->astFileName[];
        ymerBrowser.edenv.mps.astFileExtension->astFileName.append;
        astFileName[]->astFile.name;
        'untitled1'->textFileName[];
        '.bet'->textFileName.append;
        textFileName[]->textFile.name;
        (if f.entry.exists or astFile.entry.exists or textFile.entry.exists then
            true->value
        if)
     #);
   ExternalAppendProject:
     (# name,fgname,ffname: ^text; f: @file; 
     enter name[]
     do
        name[]->sif.analyzeArg->(fgname[],ffname[]);
        (if fgname.length > 0 then
            (if ffname.length > 0 then
                fgname[]->f.name;
                (if f.entry.exists then
                    (if ffname.length > 0 then
                        fgname[]->ymerBrowser.appendProject;
                        (fgname[],ffname[])->ymerBrowser.selectFragmentform
                     else
                        fgname[]->ymerbrowser.appendProject
                    if);
                    
                if)
             else
                fgname[]->ymerbrowser.appendProject
            if);
            ymerbrowser.appendDone
        if)
     #);
   ymerBrowser: ^ymerBrowserInterface;
   gui: @guienv (#  do  #);
   ymer: @ymerEnv (# editMode:: WriteMode;  #);
   sif: @sifapplication
     (#
        programs: @list (# element:: thesystemenv.process #);
        executeProgram:: 
          (# p: @thesystemenv.process
          do name[]->p.init; p.start; p[]->programs.append
          #);
        isProgramRunning:: 
          (# 
          do
             programs.scan
               (# 
               do
                  (if current.name[]->name.equal then
                      current.stillRunning->value;
                      (if not value then
                          current[]->programs.at->programs.delete
                      if)
                  if)
               #)
          #);
        killProgram:: 
          (# 
          do
             programs.scan
               (# 
               do
                  (if current.name[]->name.equal then
                      current.stop;
                      current.awaitStopped;
                      current[]->programs.at->programs.delete
                  if)
               #)
          #);
        askKillPrograms:: 
          (# msg: ^text
          do
             programs.scan
               (# 
               do
                  (if current.stillRunning then
                      current.name.copy->msg[];
                      ' is still running, quit?'->msg.append;
                      (none ,'Quit program',msg[])
                        ->gui.promptForBoolean
                          (#
                             ok::< 
                               (# 
                               do
                                  current.stop;
                                  current.awaitStopped;
                                  current[]->programs.at->programs.delete
                               #);
                             notOK::<  (#  do  #);
                             cancel::<  (#  do  #)
                          #)
                   else
                      current[]->programs.at->programs.delete
                  if)
               #)
          #);
        
     #);
   frigg: @friggapplication (#  #);
   frej: @freja (#  #);
   myCallBack: @gui.BrowserCallBack
     (# 
     do
        browserInt[]->ymerBrowser[];
        (* freja setup *)
        frej.theExternalInterface[]->browserInt.edenv.frejaExternalInterface[];
        (* sif setup *)
        &sif.sifBrowserWindowExt[]->lastWEXT[];
        lastWEXT.externalInterface[]->browserInt.sifExternalInterface[];
        BrowserInt[]->lastWEXT.init;
        lastWEXT.ymerCallBack[]->ymerCallBack[];
        (* frigginterface *)
        browserInt[]->frej.initFrejaWindow;
        &frigg.friggBrowserWindowExt[]->lastFriggExt[];
        frigg.externalInterface[]->browserInt.friggExternalInterface[];
        BrowserInt[]->lastFriggExt.init;
        (* valhalla *)
        (if not firstBrowser then
            BrowserInt[]->valhalla.sources[];
            valhalla.ExternalInterface[]
              ->browserInt.debuggerExternalInterface[];
            valhalla.valhallainit;
            true->firstBrowser;
            (* fileMenu *)
            lastWEXT.setupFileMenu;
            &gui.menuAppendItemAction
              (#
                 iOptions: theMenu.item
                   (#
                      prefwin: @gui.preferencesDialog
                        (#
                           isopened: @boolean;
                           onCloseOK:: 
                             (# 
                             do
                                prefwin.tabcontrols.valhallaPreferences.pcanvas.
                                  booloptions.savestate;
                                prefwin.tabcontrols.compilerPreferences.
                                  savestate;
                                prefwin.tabcontrols.editorPreferences.savestate;
                                (mjolneroptionsname,valhallaprefs.sectionname)
                                  ->valhallaprefs.save;
                                (mjolneroptionsname,compilerprefs.sectionname)
                                  ->compilerprefs.save;
                                (mjolneroptionsname,editorprefs.sectionname)
                                  ->editorprefs.save
                             #);
                           close::  (#  do false->isopened;  #)
                        #);
                      onSelect:: 
                        (# 
                        do
                           valhallaPrefs[]
                             ->
                               prefwin.tabcontrols.valhallaPreferences.pcanvas.
                                 Prefs[];
                           compilerPrefs[]
                             ->
                               prefwin.tabcontrols.compilerPreferences.pcanvas.
                                 Prefs[];
                           editorPrefs[]
                             ->
                               prefwin.tabcontrols.editorPreferences.pcanvas.
                                 Prefs[];
                           (if not prefwin.isopened then
                               prefwin.open; true->prefwin.isopened; 
                            else
                               prefwin.show; prefwin.bringtofront; 
                           if)
                        #)
                   #);
                 item: ^themenu.item;
                 
              do &iOptions[]->item[]; 'Options...'->item.new; 
              #)[]->ymer.fileMenuActionList.append
        if)
     #);
   firstBrowser: @boolean;
   lastWEXT: ^sif.sifBrowserWindowExt;
   lastFriggExt: ^frigg.friggBrowserWindowExt;
   Setup: (#  do myCallBack[]->ymer.newBrowserCallBack[] #);
   SetUpMenus:
     (# (* different ways to setup menu *) 
     do (* Fragmentbrowser, appends to list *)
        sif.setupMenus;
        &gui.MenubarAppendMenuAction
          (#  do theMenuBar[]->lastWEXT.setupMenus;  #)[]
          ->ymer.menuActionList.append;
        frigg.setupmenus;
        frej.setupmenus
     #);
   setupValhalla:
     (# 
     do
        &myvalhalla[]->valhalla[];
        valhallaPrefs.open
          (#  do valhallaprefs.registerVALHALLAoptions;  #);
        valhallaPrefs[]->objectpool.put;
        valhallaPrefs[]->valhalla.valhallaPrefs[];
        valhalla.globaloptions.init;
        valhalla.globaloptions[]->objectpool.put;
        valhallaprefs[]->obp.p[];
        obp[]->objectpool.put
     #);
   valhalla: ^myValhalla;
   myvalhalla: thesystemenv.valhallaapplication
     (#
        titleText: (#  exit 'Valhalla - Source Level Debugger ' #);
        editMode::  (#  do false->value;  #);
        (* ymerBrowserWindow::< 
         (#
         open::<  (#  do THIS(window).hide;  #);
         isEditor::<  (#  do false->value;  #);
         initCompiler::  (#  do dc[]->compiler[];  #);
         
         #); 
         *)
        valhallainit:
          (# 
          do
             ymer.gui[]->gui[];
             ymer.mps[]->mps[];
             false->isStandAloneValhalla->isDebugging;
             init;
             main.open;
             main.theMenuBar->main.mainMenuBar[];
             
          #)
     do true->isStandAloneValhalla; true->valhallaPrefs.dynamiccompilation; 
     #);
   valhallaPrefs: @valhallaPreferences;
   mjolnerPrefs: @mjolnerPreferences;
   compilerPrefs: @compilerPreferences;
   frejaPrefs: @frejaCMDPreferences;
   editorPrefs: @editorPreferences;
   obp: @objectPreferences;
   
do
   'Mjolner Source Browser - version '->puttext;
   ymer.ymerVersion->putline;
   thesystemenv;
   
#)  

