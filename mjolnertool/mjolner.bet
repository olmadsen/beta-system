ORIGIN '~beta/basiclib/basicsystemenv';
INCLUDE '~beta/guienv/guienvsystemenv'
        '~beta/guienv/utils/interfaceObjectAdds'
        '~beta/guienv/utils/streamwindow'
        '~beta/guienv/utils/simplemenu'
        '~beta/editor/ymercall'
        '~beta/sourcebrowser/ymer'
        '~beta/process/processmanager'
        '~beta/editor/ymersiflib'
        '~beta/interfacebuilder/friggapplication'
        '~beta/freja/frejalib'
        '~beta/freja/frejaoptions'
        'private/mjolnerOptions'
        '~beta/debugger/valhallaapplication'
        'prefDialog/prefDialog'
        '~beta/toollibs/preferences/preferencesDB'
        'seticon'
        '~beta/basiclib/mbs_version';
BODY '~beta/editor/private/nouser';
BODY '~beta/editor/private/compiler';
BODY '~beta/freja/private/frejaapplicationbody';
BODY '~beta/debugger/private/valhallabody';
BODY '~beta/debugger/private/breakpointMenus';
BODY '~beta/debugger/private/dynamic_compilation';
BODY '~beta/debugger/private/valhallabodyext';
BODY 'private/mjolnerbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-99
 *   All rights reserved.
 *   
 * NEW Integration Model!! 1.0
 *)
-- program: Descriptor --
(#
   mjolnerarguments,valhallaarguments,frejaarguments: ^text;
   readArguments:
   (* parse arguments in form:
    * mjolner -q -w filnavn --freja -s1231 --valhalla -bopt *)
     (#
        frejapos,valhallapos,mjolnerend: @integer;
        i: @integer;
        arguments: @text;
        
     <<SLOT readArguments:DoPart>>
     #);
   theSystemEnv: @|systemenv
     (#
        MjolnerDoitHandler: system
          (#
             port:
               (# m: @mbs_version; 
               exit 6000+100*m.major+10*m.minor+
               m.revision
               #);
             ClientSockType: StreamSocket
               (#
                  error:: 
                    (# 
                    do
                       true->SocketFailedFlag;
                       errCB_abortOperation->value;
                       
                    #)
               #);
             streamGen: @SocketGenerator;
             clientSock: ^ClientSockType;
             t,cmd: ^Text;
             SocketFailedFlag: @Boolean;
             
          <<SLOT MjolnerDoitHandler:DoPart>>
          #);
        setWindowEnv:: 
          (#  do gui[]->theWindowEnv[] #);
        fg:
          ^ymerBrowser.mps.ast.fragmentGroup;
        
     <<SLOT theSystemEnv:DoPart>>
     #);
   untitledExists:
    booleanValue
     (# f,astFile,textFile: @file; astfileName,textFileName: ^text
     <<SLOT untitledExists:DoPart>>
     #);
   ExternalAppendProject:
     (# name,fgname,ffname: ^text; f: @file; 
     enter name[]
     <<SLOT ExternalAppendProject:DoPart>>
     #);
   ymerBrowser: ^ymerBrowserInterface;
   gui: @guienv 
     (# 
        onQuit::  
          (#  
          do sif.askKillPrograms; 
             (if Valhalla.isDebugging then
                 Valhalla.stopDebugging
             if)
          #); 
        logwin: @streamwindow
          (#  
             open::
               (# 
               do (*hide;*)
                  'Mjolner: Log Window' -> title;
                  'mjolner.log' -> logfilename[];
                  (550,350) -> size;
                  THIS(streamwindow).thestream[] -> screen[];
                  true->hideOnCloseByUser;
               #);
             dummymenubar: @menubar
               (#
                  LogMenu: SimpleMenu 
                    (# ishow: @toggleitem
                         (#
                            onStatus::
                              (# 
                              do (if gui.logwin.isOpen then
                                     gui.logwin.visible->checked;
                                  else 
                                     false->checked;
                                 if);
                              #);
                            onSelect:: 
                              (# 
                              do (if gui.logwin.visible then
                                     gui.logwin.hide;
                                  else
                                     gui.logwin.show;
                                     gui.logwin.bringtofront;
                                 if); 
                              #)
                         #);
                       iflush: @item
                         (# onSelect:: 
                              (# do gui.logwin.flush; #)
                         #);
                       isaveas: @item
                         (# onSelect:: 
                              (# do gui.logwin.saveas; #)
                         #);
                       open:: 
                         (# 
                         do 'Show Log Window' -> ishow.new;
                            'Flush Log' -> iflush.new;
                            'Save Log As...' -> isaveas.new;
                         #);
                    #);
               #);
             SetUpMenus:
               (* Append Log menu to Windows menu *)
               (# m, l: ^menu;
                  i: ^menu.item;
                  s: ^menu.separator;
                  yi: ^ymerbrowserinterface
               enter yi[]
               do (if false then
                      yi.getWindowsMenu -> m[];
                      (if (m[]<>NONE) and (m.isOpen) then
                          &m.item[] -> i[];
                          i.open;
                          'Log' -> i.name;
                          i[] -> m.append;
                          &dummymenubar.logmenu[] -> l[];
                          l.open;
                          l[] -> i.submenu;
                          &m.separator[]->s[]; s.open; s[]->m.append;
                      if);
                  if)
               #)
          #);
     #);
   ymer: @ymerEnv (# editMode:: WriteMode;  #);
   sif: @sifapplication
     (#
        programs: @list (# element:: thesystemenv.process #);
        executeProgram:: 
          (# p: @thesystemenv.process <<SLOT executeProgram:DoPart>> #);
        isProgramRunning:: 
          (# 
          <<SLOT isProgramRunning:DoPart>>
          #);
        killProgram::  (#  <<SLOT killProgram:DoPart>> #);
        askKillPrograms:: 
          (# msg: ^text
          <<SLOT askKillPrograms:DoPart>>
          #);
        setCompilerOptions:: 
          (#
             sifcompiler: ^mps.ast.compiler;
             sw: ^sifcompiler.setswitch;
             swtext: ^text
          <<SLOT MjolnerSetCompilerOptions:DoPart>>
          #);
        externalTextEdit:: 
          (# 
          do
             <<SLOT externalTextEdit:Descriptor>>;
             
          #);
        useExternalTextEditor:: (#  do true->value #)
     #);
   frigg: @friggapplication (#  #);
   frej: @freja (#  #);
   myCallBack: @gui.BrowserCallBack (#  <<SLOT myCallBack:DoPart>> #);
   firstBrowser: @boolean;
   lastWEXT: ^sif.sifBrowserWindowExt;
   lastFriggExt: ^frigg.friggBrowserWindowExt;
   Setup:
     (# 
     do myCallBack[]->ymer.newBrowserCallBack[]
     #);
   SetUpMenus:
     (# (* different ways to setup menu *) 
     <<SLOT SetUpMenus:DoPart>>
     #);
   setupValhalla:
     (# 
     <<SLOT setupValhalla:DoPart>>
     #);
   valhalla: ^myValhalla;
   myvalhalla: thesystemenv.valhallaapplication
     (#
        titleText: (#  exit 'Valhalla - Source Level Debugger ' #);
        editMode::  (#  do false->value;  #);
        (* ymerBrowserWindow::< 
         (#
         open::<  (#  do THIS(window).hide;  #);
         isEditor::<  (#  do false->value;  #);
         initCompiler::  (#  do dc[]->compiler[];  #);
         
         #); 
         *)
        valhallainit: (#  <<SLOT valhallainit:DoPart>> #)
     <<SLOT myvalhalla:DoPart>>
     #);
   valhallaPrefs:
     @valhallaPreferences;
   mjolnerPrefs: @mjolnerPreferences;
   compilerPrefs: @compilerPreferences;
   frejaPrefs: @frejaCMDPreferences;
   editorPrefs: @editorPreferences;
   obp: @objectPreferences;
   
do
   'Mjolner Integrated Development Tool - release 5.0'->putLine;
   <<SLOT mjolnerplatforminit:Descriptor>>;
   thesystemenv;
   
#)  

