ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/freja/frejalib'
        '~beta/toollibs/preferences/cmdOptions'
        '~beta/toollibs/preferences/preferencesDB'
        'private/mjolnerOptions';

BODY 'private/mjolnerbody';

-- program: Descriptor --
(#
   optfile: @file;
   fgb: ^guienv;
   thesystemenv: @|systemenv
     (# setWindowEnv::  (#  do fgb[]->theWindowEnv[] #);  
     #);
   
   valhallaPrefs: @valhallaPreferences;
   mjolnerPrefs: @mjolnerPreferences;
   compilerPrefs: @compilerPreferences;
   editorPrefs: @editorPreferences;
do 
   &expandWildcardsArgumentHandler[]->argumentHandler[];
   mjolnerPrefs.open
     (#  do mjolnerPrefs.registerMJOLNERcmdOptions #);
   valhallaPrefs.open
     (#  do valhallaprefs.registerVALHALLAoptions;  #);
   compilerPrefs.open
     (# 
     do
        compilerprefs.registerCOMPILERoptions;
        compilerprefs.registerCOMPILERCmdOptions;
        
     #);
   editorPrefs.open
(# do editorprefs.registerEDITORoptions;
#);
   (mjolneroptionsname,valhallaPrefs.sectionname)
     ->valhallaPrefs.load
       (# Optionserror::  (#  do msg[]->screen.puttext;  #);  #);
   (mjolneroptionsname,compilerPrefs.sectionname)
     ->compilerPrefs.load
       (# Optionserror::  (#  do msg[]->screen.puttext;  #);  #);
   (mjolneroptionsname,editorPrefs.sectionname)
     ->editorPrefs.load
       (# Optionserror::  (#  do msg[]->screen.puttext;  #);  #);
   mjolnerPrefs.readArguments;
   (if mjolnerPrefs.help then mjolnerPrefs.dumpCmds; stop if);
   mjolnerPrefs[]->objectpool.put;
   valhallaPrefs[]->objectpool.put;
   compilerPrefs[]->objectpool.put;
   editorPrefs[]->objectPool.put;
   false->thesystemenv.freja (#  do thefragmentbrowser[]->fgb[] #);
   false->fgb.displayWarnings;
   
   thesystemenv
#)  

