ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/preliminary/tabControl/tabControl';
INCLUDE '~beta/guienv/graphics';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/newdebugger/UI/valhallaprefs';
INCLUDE 'compilerprefs';
-- guienvlib: Attributes --
preferencesDialog: window
  (#
     onCloseOK:<Object;
     tabControls: @tabControl
       (#
          preferenceTab: tab
            (#
               preferenceCanvas:< canvas;
               pCanvas: @preferenceCanvas;
               onSelect::<
                 (# width,height: @integer; 
                 do
                    tabControls.size->(width,height);
                    (width-4,height-24)->(width,height);
                    (width,height)->pCanvas.size;
                    
                 #);
               open::< (#  do pCanvas.open; pCanvas[]->page; INNER #)
            #);
          compilerPreferences: @preferenceTab
            (# preferenceCanvas::compilerPreferencesCanvas;
               open:: (#  do 'Compiler'->label;  #) 
            #);
          browserPreferences: @preferenceTab
            (# open:: (#  do 'Browser'->label;  #) #);
          sifPreferences: @preferenceTab
            (# open:: (#  do 'Sif'->label;  #) #);
          frejaPreferences: @preferenceTab
            (# open:: (#  do 'Freja'->label;  #) #);
          friggPreferences: @preferenceTab
            (# open:: (#  do 'Frigg'->label;  #) #);
          valhallaPreferences: @preferenceTab
            (#
               preferenceCanvas:: valhallaPreferencesCanvas;
               open:: (#  do 'Debugger'->label;  #)
            #);
          eventhandler::
            (#
               onFatherFrameChanged::
                 (# width,height: @integer; 
                 do
                    newframe->frame;
                    size->(width,height);
                    height-20->height;
                    (* space for buttons; *)
                    (width-4,height-24)->(width,height);
                    (width,height)->((selection).page).size;
                    (* tabs.scan(# ... #) nedenfor er her kun, fordi
                     * onSelect ikke bliver kaldt på UNIX
                     *)
                    tabs.scan
                      (#  do (width,height)->(current.page).size #)
                 #)
            #);
          open::
            (# 
            do 
               (518,330)->size;
               compilerPreferences.open;
               browserPreferences.open;
               sifPreferences.open;
               frejaPreferences.open;
               friggPreferences.open;
               valhallaPreferences.open;
               
            #)
       #);
     open::
       (# 
       do
          (520,360)->size;
          tabControls.open;
          cancelbutton.open;
          okbutton.open;
          
       #);
     okbutton: @pushbutton
       (# eventhandler::<(# onmouseup::(# do onCloseOK; this(window).close; #); #);
          open::< (#  do 'OK'->label; (60,20)->size; (455,335)->position; INNER #)
       #);
     cancelbutton: @pushbutton
       (#
          eventhandler::(# onmouseup::(# do this(window).close; #); #);
          open::<
            (# 
            do 'Cancel'->label; (60,20)->size; (380,335)->position; INNER
            #)
       #)
  #)  

