ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/tabControl'
        '~beta/guienv/graphics'
        '~beta/guienv/controls'
        '~beta/debugger/UI/valhallaprefs'
        'compilerprefs'
        'editorprefs';
-- guienvlib: Attributes --
preferencesDialog: window
  (#
     onCloseOK:< Object;
     eventhandler::<
       (# 
          onAboutToClose::<
            (# 
            do onCloseOK; THIS(window).hide; false->OKtoclose;
            #);
       #);
     tabControls: @tabControl
       (#
          preferenceTab: tab
            (#
               preferenceCanvas:< canvas;
               pCanvas: @preferenceCanvas;
               onSelect::< 
                 (# width,height: @integer; 
                 do
                    tabControls.size->(width,height);
                    (width-4,height-24)->(width,height);
                    (width,height)->pCanvas.size;
                    
                 #);
               open::<  (#  do pCanvas.open; pCanvas[]->page; INNER #)
            #);
          compilerPreferences: @preferenceTab
            (#
               preferenceCanvas:: compilerPreferencesCanvas;
               savestate: pcanvas.savestate (#  #);
               open::  (#  do 'Compiler'->label;  #)
            #);
          browserPreferences: @preferenceTab
            (# open::  (#  do 'View'->label;  #) #);
          editorPreferences: @preferenceTab
            (#
               preferenceCanvas:: editorPreferencesCanvas;
               savestate: pcanvas.savestate (#  #);
               open::  (#  do 'Edit'->label;  #)
            #);
          frejaPreferences: @preferenceTab
            (# open::  (#  do 'Diagram'->label;  #) #);
          friggPreferences: @preferenceTab
            (# open::  (#  do 'Gui'->label;  #) #);
          valhallaPreferences: @preferenceTab
            (#
               preferenceCanvas:: valhallaPreferencesCanvas;
               open::  (#  do 'Debugger'->label;  #)
            #);
          eventhandler:: 
            (#
               onFatherFrameChanged:: 
                 (# width,height: @integer; 
                 do
                    newframe->frame;
                    size->(width,height);
                    height-20->height;
                    (* space for buttons; *)
                    (width-4,height-24)->(width,height);
                    (width,height)->((selection).page).size;
                    (* tabs.scan(# ... #) nedenfor er her kun, fordi
                     * onSelect ikke bliver kaldt på UNIX
                     *)
                    tabs.scan
                      (#  do (width,height)->(current.page).size #)
                 #)
            #);
          open::
            (# 
            do
               (518,330)->size;
               compilerPreferences.open;
               valhallaPreferences.open;
               editorPreferences.open;
               (*browserPreferences.open;
                sifPreferences.open;
                frejaPreferences.open;
                friggPreferences.open;*)
               
            #)
       #);
     open:: 
       (# 
       do
          (520,360)->size;
          tabControls.open;
          cancelbutton.open;
          okbutton.open;
          'Preferences'->title          
       #);
     onHide:<Object;
     okbutton: @pushbutton
       (#
          eventhandler::< 
            (# onmouseup::  (#  do onCloseOK; THIS(window).hide;  #); 
            #);
          open::< 
            (# 
            do 'OK'->label; (60,20)->size; (455,335)->position; INNER
            #)
       #);
     cancelbutton: @pushbutton
       (#
          eventhandler:: 
            (# onmouseup::  (#  do THIS(window).hide;  #);  #);
          open::< 
            (# 
            do 'Cancel'->label; (60,20)->size; (380,335)->position; INNER
            #)
       #)
  #)  

