ORIGIN '~beta/toollibs/preferences/toolpreferences';
INCLUDE '~beta/toollibs/preferences/cmdOptions';

---compilerpreferenceslib:attributes---
registerCOMPILERoptions:
  (# 
  do
     'DebugInformation'
       ->define(# type::booleanPreference;
               #);
     
     'Warnings'
       ->define(# type::booleanPreference;
               #);
     'QuaCheck'
       ->define(# type::booleanPreference;
               #);
     
     'CmdOpt'
       ->define(# type::textPreference;
               #);
     
  #);

(* Short Cuts *)
debuginformation:getBoolean(# #);
warnings:getBoolean(# #);
quacheck:getBoolean(# #);
cmdopt:getText(# #);

registerCOMPILERCmdOptions: registerCmdOptions
(#
do 
   'link'
     ->define(# type:: cmdToggle
             do true->pref.value;
                'Link program'->pref.description[];
                'x'->pref.shortCut;
             #);
   'static'
     ->define(# type:: cmdOption
             do 'Use static linking'->pref.description[];
             #);
   'dynamic'
     ->define(# type:: cmdOption
                  (# 
                     default:: trueObject;
                  #)
             do 'Use dynamic linking'->pref.description[];
             #);
   'switch'
     ->define(# type:: cmdArgs
                  (# 
                     maxArgs::
                       (#
                       do (if (noOfArgs>0) and
                              ('0'->args[noOfArgs].equal) then
                              noOfArgs->value
                           else 0->value
                          if)
                       #);
                     legalArg::
                       (#
                       do arg.reset; true->value;
                          arg.getint
                          (# syntaxerror::
                               (# do false->this(legalArg).value;
                                  true->continue #)
                          #)
                       #)
                  #)
             do 'Set/unset one or more compiler switches'
                  ->pref.description[]; pref.description.newline;
                'See the Compiler Manual for details'
                  ->pref.description.puttext;
                's'->pref.shortCut;
             #);
   'linkOpts'
     ->define(# type::textPreference;
             do 'Specify text string to be appended to the link directive'
                  ->pref.description[];
             #);
   'betarun'
     ->define(# type::cmdArgs
                  (# 
                     maxArgs::(# do 1->value; #)
                  #);
             #);
#);


link:getcmdToggle(# #);
static :getcmdOption(# #);
dynamic :getcmdOption(# #);
switch:getcmdArgs(# #);
linkOpts :getText(# #);
betarun:getcmdArgs(# #);

(* section name used in disk representation *)
sectionname:(# exit 'compileroptions' #);


  
