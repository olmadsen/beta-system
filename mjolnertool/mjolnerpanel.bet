ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/interfacebuilder/support/groupbox'
        '~beta/guienv/controls';
MDBODY nti 'private/mjolnerpanel_nti';

-- guienvLib: Attributes --
AddBetalibDialog: window
  (#
     result:< (# t: ^Text;  enter t[] do INNER #);
     open::<
       (# 
       do
          'Add new BETALIB to list'->title;
          (245,90)->size;
          edittext1.open;
          pushbutton1.open;
          pushbutton2.open;
          edittext1[]->target;
          INNER
       #);
     edittext1: @edittext
       (# open::< (#  do (200,22)->size; (25,15)->position; INNER #)
       #);
     pushbutton1: @pushbutton
       (#
          open::<
            (# 
            do 'OK'->label; (60,20)->size; (25,50)->position; INNER
            #);
          eventHandler::<
            (#
               onMouseUp::<
                 (# 
                 do edittext1.contents->result; THIS(AddBetalibDialog).close; 
                 #)
            #)
       #);
     pushbutton2: @pushbutton
       (#
          open::<
            (# 
            do 'Cancel'->label; (60,20)->size; (165,50)->position; INNER
            #);
          eventHandler::<
            (# onMouseUp::< (#  do THIS(AddBetalibDialog).close;  #)
            #)
       #)
  #);
MjolnerPanel: window
  (#
     DropList:
       (# ListKeyName:< (# value: ^Text do INNER exit value[] #);
          CurKeyName:< (# value: ^Text do INNER exit value[] #);
          init:< (# do 1->curidx; INNER #);
          
          myOB: ^optionbutton;
          
          names: [0]^Text;
          curidx: @Integer;
          
          append:
            (# t: ^Text;
            enter t[]
            do 1->names.extend;
               t[]->names[names.range][];
            #);
          
          GetCur:
            (# exit names[curidx].copy #);
               
          Load:<
            (# list,cur,t: ^Text; 
               <<SLOT MjolnerPanelDropListLoad: dopart>>
            #);
          
          Save:<
            (# list: ^Text;   
            <<SLOT MjolnerPanelDropListSave: dopart>>
            #);

          Show:
            (# m: ^Menu; mi: ^Menu.MenuItem; 
            do
               myOB.popupMenu->m[];
               (if m[] <> none then 
                   none -> myOB.popupMenu; m.close;
               if);
               &Menu[]->m[];
               m.open;
               (for i: names.range repeat
                    &m.MenuItem[]->mi[];
                    mi.open;
                    names[i][]->mi.name;
                    mi[]->m.append;
                    
               for);
               m[]->myOB.popupMenu;
            #);
          
       #);
   
     BetalibList: @DropList
       (# 
          ListKeyName:: (# do 'BetalibList'->value[] #);
          CurKeyName:: (# do 'CurrentBetalib'->value[] #);
          init::(# do 'd:\\beta\\r5.0'->append #);
       #);
     
     TargetList:@DropList
       (# 
          ListKeyName:: (# do 'TargetList'->value[] #);
          CurKeyName:: (# do 'CurrentTarget'->value[] #);
          init::(# do 'nti_ms'->append; 'nti_gnu'->append #);
       #);
          
     BetalibChanged:
       (# i: @Integer; betalib: ^Text;
       enter i 
       do i->BetalibList.curidx;
          BetalibList.GetCur;  
          (* Do what with it? *)
          BetalibList.Save;
       #);
     
     TargetChanged:
       (# i: @Integer; target: ^Text;
       enter i
       do i->TargetList.curidx;
          TargetList.GetCur->target[];
          (* Do what with it? *)
          TargetList.Save;
       #);

     open::<
       (# 
       do
          (248,130)->size;
          'MjolnerPanel'->title;
          optionbutton1.open;
          optionbutton2.open;
          pushbutton1.open;
          pushbutton2.open;
          pushbutton3.open;
          
          optionbutton1[]->BetalibList.myOB[];
          BetalibList.load; BetalibList.show;
          optionbutton2[]->TargetList.myOB[];
          TargetList.load; TargetList.show;
          
          INNER
       #);
     optionbutton1: @optionbutton
       (#
          open::<
            (# 
            do 'Betalib:'->label; (224,32)->size; (16,8)->position; INNER
            #);
          eventHandler::
            (#
               onCurrentItemChanged::
                 (#  do currentItem->BetalibChanged;  #);
            #);
       #);
     optionbutton2: @optionbutton
       (#
          open::<
            (# 
            do 'Target'->label; (208,32)->size; (32,48)->position; INNER
            #);
          eventHandler::
            (#
               onCurrentItemChanged::
                 (#  do currentItem->TargetChanged;  #);
            #);
       #);
     pushbutton1: @pushbutton
       (#
          open::<
            (# 
            do 'Quit'->label; (64,20)->size; (176,96)->position; INNER
            #);
          eventHandler::< (# onMouseUp::< (#  do terminate #) #)
       #);
     pushbutton2: @pushbutton
       (#
          open::<
            (# 
            do 'Add Betalib'->label; (72,20)->size; (8,96)->position; INNER
            #);
          eventHandler::<
            (#
               onMouseUp::<
                 (#
                    diag: @AddBetalibDialog
                      (#
                         result::
                           (# 
                           do
                              t[]->BetalibList.append;
                              BetalibList.Save;
                           #)
                      #)
                 do diag.open; diag.showModal; 
                 #)
            #)
       #);
     pushbutton3: @pushbutton
       (#
          open::<
            (# 
            do
               'Bind Icon'->label; (80,20)->size; (88,96)->position; INNER
            #);
          eventHandler::<
            (#
               onMouseUp::<
                 (# doit: ^Text;
                 <<SLOT MjolnerPanelBindIconDP: dopart>>  
                 #)
            #)
       #)
  #)  

-- program: Descriptor --
guienv (# w: @MjolnerPanel;  do w.open #)  

