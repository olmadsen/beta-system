ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/containers/list';
INCLUDE '~beta/basiclib/file';
-- LIB: Attributes --

TextList: List
  (# Element:: Text; 
  #);

CommentScanner:
  (# input: ^Stream;
     ch: @char;
     EOS: (# exit 0 #);
     position: @integer;
     init:
       (#
       enter input[]
       do next;
       #);
     next:
       (#
       do (if input.eos then
              EOS -> ch;
           else
              input.get -> ch;
              position + 1 -> position;
          if);
       #);
     advance:
       (***
        * Assumption: When calling advance we are outside a string or comment. 
        * When advance is done we are still outside string or comment. 
        * We can not both be in a string and in a comment.
        ***)
       (# to: @integer;
          comment: ^Text;
          comments: ^TextList;
          error:< Exception;
       enter to
       do &TextList[] -> comments[];
          (if position < to then
              loop:
                (if position < to then
                    (if ch
                     //'\'' then
                        next;
                        skip:
                          (if ch
                           //'\\' then
                              next;
                              next;
                              restart skip;
                           //'\'' then
                              next;
                           else
                              next;
                              restart skip;
                          if);
                     //'(' then
                        next;
                        (if ch = '*' then
                            &Text[] -> comment[];
                            next;
                            read:
                              (if ch = '*' then
                                  next;
                                  (if ch = ')' then
                                      next;
                                      comment[] -> comments.append;
                                   else
                                      '*' -> comment.put;
                                      restart read;
                                  if);
                               else
                                  ch -> comment.put;
                                  next;
                                  restart read;
                              if);
                        if);
                     else
                        next;
                    if);
                    restart loop;
                if);
              (if to <> position then
                  (** error; **)
              if);
              forward:
                (if ch -> ascii.isSpace then
                    next;
                    restart forward;
                 else
                    (if ch = '(' then
                        next;
                        (if ch = '*' then
                            &Text[] -> comment[];
                            next;
                            read:
                              (if ch = '*' then
                                  next;
                                  (if ch = ')' then
                                      next;
                                      comment[] -> comments.append;
                                      restart forward;
                                   else
                                      '*' -> comment.put;
                                      restart read;
                                  if);
                               else
                                  ch -> comment.put;
                                  next;
                                  restart read;
                              if);
                        if);
                    if);
                if);
          if);
       exit comments[]
       #);
  #);

