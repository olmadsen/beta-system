------- CopyText: System ----

CopyT: public
  (#
     { Allocate a (byte)ValueRepetition and initialize it with some text.    }
     { stack on entry [return(0),offset(4),item(8),asciiz(12),...]     }
     
     { Used variable.  }
     stack:   StackArea  = SP;
     theText: CodeArea   = AddrReg1; { Address to the ascii text. }
     newTop:  DataArea   = AddrReg1;
     theCell: DataArea   = AddrReg1;
     newRep:  Repetition = AddrReg2;
     Range:   Long       = DataReg1; { The range of the valRep. }
     BodySize: Long      = DataReg2; { number of bytes to allocate }
     Value:   Long       = DataReg2;
     offset:  Long       = DataReg2
  do
     { Calculate the length of the text. }
     stack[12] -> theText;
     0 -> Range;
     goto LoopB;
     LoopA: 1  -> Range+;
     LoopB: (if theText[Range]!b <> 0 then goto LoopA if);
     
     (if MacOS then push AddrReg2 if);
     
     (if Range > 200 then 
         push DataRegA, DataRegB, RegObj, RegArg; { save registers }
         push Range; { Parameters to LVRAAlloc. }
         push -6; { Parameters to LVRAAlloc. }
         call LVRAAlloc;
         DataRegA -> newRep;
         (if newRep <> 0 then
             { Make the LVRA-cycle: theCell -> newRep.Age }
             (if MacOS then
                 stack[32  { 8 + 6*4 }] -> DataRegA; 4 -> DataRegA*;
                 stack[36 { 12 + 6*4 }] -> DataRegA+
              else
                 stack[28 { 4 + 6*4 }] -> DataRegA; 4 -> DataRegA*;
                 stack[32 { 8 + 6*4 }] -> DataRegA+
             if);
             DataRegA -> newRep.Age
         if);
         4 -> SP+;
         pop  Range;
         pop  RegArg, RegObj, DataRegB, DataRegA;
         (if newRep <> 0 then
             goto copyBody
         if)
     if);
     
     { Allocate a value repetition with bodysize = ((range+1+3)/4)*4 bytes }
     IOATop -> newRep;
     Range -> BodySize;
     4 -> BodySize+;
     4 -> BodySize/;
     4 -> BodySize*;
     newRep[16+BodySize][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part of the valrep.             }
     -6    -> newRep.ProtoType;
     1     -> newRep.Age;
     1     -> newRep.LowBorder;
     Range -> newRep.HighBorder;
     
     copyBody:
     
     (if MacOS then 
         stack[16] -> theText
      else
         stack[12] -> theText
     if);     
     
     { Assign the text to the body part of the repetition. }
     (if Range <> 0 then
         0 -> Value;
         4 -> Range+;
         4 -> Range/;
         1 -> Range-;
         4 -> Range*;
         loop:
         theText[Range] -> Value;
         Value -> newRep[16+Range];
         4 -> Range-;
         (if FLAGS >= 0 then goto loop if)
     if);
     
     (if MacOS then 
         stack[12] -> theCell;
         stack[8]  -> offset
      else
         stack[8] -> theCell;
         stack[4] -> offset
     if);
     theCell[offset*4][] -> theCell;
     newRep -> theCell[0];
     
     (if theCell outside IOA then  { The theCell resides in AOA. }
         push theCell;
         call ChkRA { No need to remove parameter. }
     if);
     
     (if MacOS then pop AddrReg2 if);
     return 12;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     4 -> BodySize/;
     BodySize -> ReqObjectSize+;
     (if MacOS then pop AddrReg2 if);
     call DoGC;              { Call the Garbage Collector.               }
     goto CopyT              { Go back to the beginning of this routine. }
  #)

