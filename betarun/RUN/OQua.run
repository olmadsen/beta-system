------- OQua: System ----
{ Qua check.
  The reference assignment src[]->dst[] has just been made. dst resides in theCell.
  Perform.
  1. CheckReferenceAssigment
  2. Qua Check: Check if the actual qualification of src is less than or equal to
  qualification of dst, as given by dstQuaProto and dstQuaOrigin.
}

{ OQua: Strict qua check, take origins into account }
OQua: public { *** Warning: Not ported to Intel *** }
  (#
     { input variables }
     dstQuaProto:  ProtoType = RegArg;
     dstQuaOrigin: Object    = DataReg1; { used to be RegAdr }
     theCell:      Long      = DataReg2;
     
     { used variables }
     src:          Object    = AddrReg1;
     srcStruc:     Structure = AddrReg1;
     srcComp:      Component = AddrReg1;
     srcProto:     ProtoType = AddrReg2;
     srcOrigin:    Long      = DataReg1;
     offset:       Word      = DataReg2;
     errorNo:      Long      = DataReg2;
     
     proto1:       ProtoType = AddrReg1;
     argument:     Structure = RegArg;
     newObject:    Object    = RegArg     
  do
     theCell -> src;
     src[0] -> src;
     (if src = 0 then return if);
     
     { 1. Check reference assignment }
     (if theCell outside IOA then
         push RegArg, RegAdr, AddrReg1, DataReg1;
         push theCell;
         call ChkRA; { No need to remove parameter }
         pop  DataReg1, AddrReg1, RegAdr, RegArg
     if);
     
     (if MacOS then push AddrReg2 if);
     
     { 2. Qua Check }
     src.ProtoType -> srcProto;
     (if srcProto = StructurePTValue then
         { It was a pattern variable assignment: src is a struc-object }
         srcStruc.iProto  -> srcProto;
         srcStruc.iOrigin -> srcOrigin; { invalidates theCell }
         goto eqCheck
     if);
     (if srcProto = ComponentPTValue then
         { It was a component-reference assignment; src points to a component }
         srcComp.Item[] -> src;
         src.ProtoType -> srcProto;
         srcProto.OrigOff -> offset;
         src[offset*4] -> srcOrigin; { invalidates theCell }
         goto eqCheck
     if);
     { It was a normal reference assignment: src is normal object }
     srcProto.OrigOff -> offset;
     src[offset*4] -> srcOrigin; { invalidates theCell }
     
     eqCheck: { Check for eqS }
     (if srcProto = dstQuaProto then
         (if srcOrigin = dstQuaOrigin then
             { Structures are identical. All is OK }
             goto success
         if)
     if);
     
     { Inlined version of ltS without struc objects }
     
     (if dstQuaProto = dstQuaProto.Prefix then
         { dstQuaProto is Object## }
         goto success
     if);
     
     { Prefix of srcProto is the first try }
     push src;
     srcProto -> proto1;
     proto1.Prefix -> proto1;
     loop:
     (if proto1 = proto1.Prefix then { proto1 = Object## }
         4->SP+ { pop src };
         QuaErr -> errorNo;
         goto failure 
     if); 
     (if proto1 = dstQuaProto then
         { Now there is some hope, now we need to check if origins are equal. }
         pop src; { proto 1 is no longer used }
         dstQuaProto.OrigOff -> offset;
         (if offset = srcProto.OrigOff then
             { The prototypes have same origin offset, so   }
             { the result is (srcOrigin = dstQuaOrigin).    }
             (if srcOrigin = dstQuaOrigin then goto success if);
             QuaOrigErr -> errorNo;
             goto failure
         if);
         
         { Src may have several origins (one per prefixlevel).
           We find the origin at the offset determined *by the prefix* (dstQuaProto)
           This should be the same as the origin of the qualification of dst.
         }
         dstQuaProto.OrigOff -> offset;
         
         (if src.ProtoType = StructurePTValue then
             push dstQuaOrigin; { save in case of GC }
             src -> argument; { invalidates dstQuaProto }
             call AlloSI;
             pop dstQuaOrigin
         if);
         
         newObject[offset*4] -> srcOrigin;
         (if dstQuaOrigin = srcOrigin then goto success if); 
         QuaOrigErr -> errorNo;
         goto failure
     if);
     proto1.Prefix -> proto1;
     goto loop;
     
     success:
     (if MacOS then pop AddrReg2 if);
     return;
     
     failure:
     (if MacOS then pop AddrReg2 if);
     { save registers in case of QuaCont }
     (if hp then
         (code '      movm.l  %a0-%a4,-(%sp)' code);
         (code '      movm.l  %d0-%d7,-(%sp)' code)
     if);
     (if MacOS then
         (code '	movem.l	a0-a4,-(sp)' code);
         (code '	movem.l	d0-d7,-(sp)' code)
     if);
     
     push RegObj;
     push errorNo;
     call BetaError;
     8 -> SP+;
     
     (if hp then
         (code '      movm.l  (%sp)+,%d0-%d7' code);
         (code '      movm.l  (%sp)+,%a0-%a4' code)
     if);
     (if MacOS then
         (code '	movem.l	(sp)+,d0-d7' code);
         (code '	movem.l	(sp)+,a0-a4' code)
     if);    
     
     return
  #)
