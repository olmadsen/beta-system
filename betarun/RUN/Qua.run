------- Qua: System ----
{ Qua check.
  The reference assignment src[]->dst[] has just been made. dst resides in theCell.
  Perform.
  1. CheckReferenceAssigment
  2. Qua Check: Check if the actual qualification of src is less than or equal to
  qualification of dst, as given by dstQuaProto and dstQuaOrigin.
}

{ Qua: Gentle qua check, don't worry about origins }
Qua: public
  (#
     { Input variables. }
     dstQuaProto:  ProtoType = RegCall;
     theCell:      Long      = DataReg2; {pReg2 in Intel version}
     
     { Used variables. }
     src:          Object    = AddrReg1;
     srcStruc:     Structure = AddrReg1;
     srcComp:      Component = AddrReg1;
     srcProto:     ProtoType = AddrReg2;
     proto1:       ProtoType = AddrReg1;
     argument:     Structure = RegCall;
     newObject:    Object    = RegCall     
  do
     {
       General stack layout:
       <Intel: AddrReg2>
       <Intel: AddrReg1>
       <Mac: AddrReg2>
       ...
     }
     
     (if debug then call Ck if);
     (if debug then 1 -> NumQua+ if); 
     
     push DataReg2; pReg2 -> DataReg2;
     push AddrReg1 {AddrReg2 coming up...};
     
     theCell -> src;
     src[0] -> src;
     (if src = 0 then 
         pop AddrReg1, DataReg2;
         return;
     if);
     
     { 1. Check reference assignment }
     (if theCell outside IOA then
         push RegCall, AddrReg1;
         push theCell;
         call ChkRA; { No need to remove parameter }
         pop  AddrReg1, RegCall
     if);
     
     {AddrReg1 already pushed} 
     push AddrReg2;
     
     (if src < -101 then
         { Dangling reference. Get prototype of referred object.}
         (if linux then
             (code '	pushl %eax' code);
             (code '	pushl %ecx' code);
             (code '	pushl %edx' code);
             (code '	pushl %ebx' code);
             (code '	pushl %ebp' code);
             (code '	pushl %edi' code);
             
             push src;
             (code '    movl findDanglingProto,%esi' code);
             (code '    call *%esi' code);
             DataRegA -> srcProto;
             pop src;
             
             (code '	popl %edi' code);
             (code '	popl %ebp' code);
             (code '	popl %ebx' code);
             (code '	popl %edx' code);
             (code '	popl %ecx' code);
             (code '	popl %eax' code);
         if);
         (if nti then
             (code '	push	eax' code);
             (code '	push	ecx' code);
             (code '	push	edx' code);
             (code '	push	ebx' code);
             (code '	push	ebp' code);
             (code '	push	edi' code);
             
             push src;
             (code 'extrn	_findDanglingProto:dword' code);
             (code '	mov	esi,_findDanglingProto' code);
             (code '	call	esi' code);
             DataRegA -> srcProto;
             pop src;
             
             (code '	pop	edi' code);
             (code '	pop	ebp' code);
             (code '	pop	ebx' code);
             (code '	pop	edx' code);
             (code '	pop	ecx' code);
             (code '	pop	eax' code);
         if);
         goto doCheck;
     if);
     
     { 2. Qua Check }
     src.ProtoType -> srcProto;
     
     doCheck:
     (if srcProto = StructurePTValue then
         { It was a pattern variable assignment: src is a struc-object }
         srcStruc.iProto  -> srcProto;
         goto eqCheck
     if);
     (if srcProto = ComponentPTValue then
         { It was a component-reference assignment; src points to a component }
         srcComp.Item[] -> src;
         src.ProtoType -> srcProto;
         goto eqCheck
     if);
     { It was a normal reference assignment: src is normal object }
     
     eqCheck: { Check for eqS }
     (if srcProto = dstQuaProto then
         { Structures are identical. All is OK }
         goto success
     if);
     
     { Inlined version of ltS without struc objects }
     
     (if dstQuaProto = dstQuaProto.Prefix then
         { dstQuaProto is Object## }
         goto success
     if);
     
     { Prefix of srcProto is the first try }
     srcProto -> proto1;
     proto1.Prefix -> proto1;
     loop:
     (if proto1 = proto1.Prefix then { proto1 = Object## }
         goto failure 
     if); 
     (if proto1 = dstQuaProto then
         { dstQuaProto is a prefix of srcProto; all is OK }
         goto success
     if);
     proto1.Prefix -> proto1;
     goto loop;
     
     success:
     pop AddrReg2, AddrReg1, DataReg2;
     return;
     
     failure:
     pop AddrReg2, AddrReg1;
     { save registers in case of QuaCont }
     (if linux then
         (code '	pushal' code);
     if);
     (if nti then
         (code '	pushad' code);
     if);
     
     push RegThis; push QuaErr; call BetaError;
     
     { in case of QuaCont: }
     8 -> SP+;
     
     (if linux then
         (code '	popal' code);
     if);
     (if nti then
         (code '	popad' code);
     if);
     
     pop DataReg2;
     
     (if debug then call Ck if);
     return
  #)
