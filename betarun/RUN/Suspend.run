------- Suspend: System ----
Susp: public
  (#
     { Suspends 'ActiveComponent'. }
     
     { Used variable.  }
     theComp: Component = AddrReg1;
     theCell: DataArea  = AddrReg1;
     caller:  Component = RegArg;
     theObj:  Item      = RegObj;
     stackPointer: StackArea = SP
  do
     { Before the block on the stack is packed, we must ensure  }
     { that ActiveComponent do not involve callBacks.           }
     { Suspending a component involving callBacks _must_ result }
     { in an error.                                             }
     (if ActiveCallBackFrame <> 0 then push theObj; 
         push -13; 
         call BetaError
     if);
     
     (if debug then call Ck if);
     
     push theObj;
     call PackCS; {GC registers ok; stack ok, too}
     
     { ActiveComponent.StackObj -> CheckReferenceAssignment. }
     ActiveComponent -> theCell; theCell[8][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA { No need to remove parameter }
     if);
     
     ActiveComponent -> theComp;
     
     theComp.CallerComp -> caller; 
     
     (if caller=0 then
         { This is the case if the basic component is suspended. }
         { Then leave the program in a nice way.                    }
         push 0; call BetaExit
     if);
     
     { Pop top block of the stack. }
     lastCompBlock -> stackPointer;
     pop ActiveCallBackFrame;
     pop lastCompBlock;
     4 -> stackPointer+;
     
     0 -> theComp.CallerComp;
     caller -> ActiveComponent;
     
     theComp.CallerObj -> theObj; 0 -> theComp.CallerObj;
     
     push theComp.CallerLSC;
     label SuspEnd; { For Valhalla }
     (if intel then
         0 -> AddrReg1; 0 -> AddrReg2; {Invalidate GC registers}
     if);
     return;
  #)

