------- AllocateRefRep: System ----
AlloRR: public
  (#
     { Allocate a reference repetition. }
     
     { Input variables. }
     theObj: Item     = RegThis;   { The object, which refer the valrep. }
     Offset: Long     = DataRegA;  { Offset in the theObj. } 
     Range:  Long     = DataRegB;  { The range of the valRep.            }
     
     { Used variables. }
     stack: StackArea = SP;
     newTop: DataArea   = AddrReg1;
     theCell: DataArea  = AddrReg1;
     theRep: Repetition = RegCall;
  do
     { 
       General stack layout:
       <Intel: AddrReg1>
       ... 
     }
     
     (if debug then 1 -> NumAlloRR+ if); 
     (if debug then call Ck if);

     (if intel then
         pReg1 -> DataRegA;
         pReg2 -> DataRegB;
     if);

     again:
     
     (if intel then push AddrReg1 if);
     
     (if Range < 0 then 0 -> Range if); { OBS: low>high confuses chk2 }
     IOATop -> theRep;
     theRep[16+Range*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part of the repetition. }
     RefRepPTValue    -> theRep.ProtoType;
     1     -> theRep.Age;
     1     -> theRep.LowBorder;
     Range -> theRep.HighBorder;
     
     theObj[Offset][] -> theCell;
     theRep -> theCell[0];
     
     (if theCell outside IOA then  { The theCell resides in AOA. }
         push theCell;
         call ChkRA { No need to remove parameter. }
     if);
     
     (if intel then pop AddrReg1 if);
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     Range -> ReqObjectSize+;
     (if intel then pop AddrReg1 if);
     call doGC;              { Call the Garbage Collector.               }
     goto again              { Go back to the beginning of this routine. }
  #)

