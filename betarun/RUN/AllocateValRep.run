------- AllocateValRep: System ----

AlloVR1: public
  (#
     { Allocate a byte repetition and assign it to the reference at theObj[offset] }
     
     { Input variables. }
     theObj: Item     = RegThis;   { The object, which refer the valrep. }
     Offset: Long     = DataReg1; { Offset in the theObj. } 
     Range:  Long     = DataReg2; { The range of the valRep.            }
     
     { Used variables. }
     BodySize:  Long    = DataReg1; { the number of bytes to allocate     }
     newTop: DataArea   = AddrReg1;
     theCell: DataArea  = AddrReg1;
     theRep: Repetition = AddrReg2;
     Value:  Long       = DataReg2;
  do
     (if debug then 1 -> NumAlloVR1+ if); 
     (if debug then call Ck if);

     { 
       General stack layout:
       <AddrReg2>
       <AddrReg1>
       ... 
     }
     
     push AddrReg1, AddrReg2;
     
     pReg1 -> DataReg1;
     pReg2 -> DataReg2;
     
     (if Range < 0 then 0 -> Range if);
     
     (if Range > LARGE_REP_SIZE then 
         push RegThis, RegCall, Offset; { save registers }
         push Range; { Parameters to LVRACAlloc. }
         push ByteRepPTValue; { Parameters to LVRACAlloc. }
         call LVRACAlloc;
         DataReg1 -> theRep;
         4 -> SP+;
         pop Range;
         pop Offset, RegCall, RegThis;
         (if theRep <> 0 then
             { Make the LVRA-cycle: theCell -> theRep.GCAttr }
             Offset -> Value; theObj -> Value+;
             Value -> theRep.GCAttr;
             theRep -> theObj[Offset]; 
             pop AddrReg2, AddrReg1;
             return
         if)
     if);
     
     IOAAlloc:
     
     (if debug then
         (if do_unconditional_GC then
             { Don't do this before AttBC }
             (if ActiveComponent<>0 then
                 { See general comment in AllocateItem.run }
                 push Offset; 
                 Range -> BodySize;
                 4 -> BodySize+;
                 4 -> BodySize/;
                 4 -> BodySize*;
                 goto GC;
             if);
         if);
     if);
     
     again: 
     
     { allocate 16+((range+1+3)/4)*4 bytes in IOA }
     IOATop -> theRep;
     
     push Offset; {BodySize assignment destroys Offset}
     
     Range -> BodySize;
     1 -> BodySize+; { NULL termination }
     ObjectAlign -> BodySize|;
     theRep[16+BodySize][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part of the repetition. }
     ByteRepPTValue -> theRep.ProtoType;
     (if IOAMinAge<>0 then
         IOAMinAge -> theRep.GCAttr;
     if);
     1     -> theRep.LowBorder;
     Range -> theRep.HighBorder;
     
     pop Offset; {We're finished using BodySize, so restore Offset}
     
     theObj[Offset][] -> theCell;
     theRep -> theCell[0];
     
     (if call_chkra_unconditionally then
         call ChkRA_EBP;
      else
         (if theCell outside IOA then  { The theCell resides in AOA. }
             push theCell;
             call ChkRA { No need to remove parameter. }
         if);
     if);
     
     pop AddrReg2, AddrReg1;
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     4 -> BodySize/;
     BodySize -> ReqObjectSize+;
     pop Offset; {Restore Offset}
     pop AddrReg2, AddrReg1;
     { Stack: as on entry - data registers are tagged properly. }
     call doGC;              { Call the Garbage Collector.               }
     push AddrReg1, AddrReg2;
     goto again
  #);



AlloVR2: public
  (#
     { Allocate a word repetition and assign it to the reference at theObj[offset] }
     
     { Input variables. }
     theObj: Item     = RegThis;   { The object, which refer the valrep. }
     Offset: Long     = DataReg1; { Offset in the theObj.               }
     Range:  Long     = DataReg2; { The range of the valRep.            }
     
     { Used variables. }
     BodySize:  Long    = DataReg1; { the number of bytes to allocate     }
     newTop: DataArea   = AddrReg1;
     theCell: DataArea  = AddrReg1;
     theRep: Repetition = AddrReg2;
     Value:  Long       = DataReg2;
  do
     (if debug then call Ck if);
     (if debug then 1 -> NumAlloVR2+ if); 
     
     push AddrReg1, AddrReg2;
     
     pReg1 -> DataReg1;
     pReg2 -> DataReg2;
     
     (if Range < 0 then 0 -> Range if);
     
     (if Range > LARGE_REP_SIZE then 
         push RegThis, RegCall, Offset; { save registers }
         push Range; { Parameter to LVRACAlloc. }
         push ShortRepPTValue;    { Parameter to LVRACAlloc. }
         call LVRACAlloc;
         DataReg1 -> theRep;
         4 -> SP+;
         pop Range;
         pop Offset, RegCall, RegThis;
         (if theRep <> 0 then
             { Make the LVRA-cycle: theCell -> theRep.GCAttr }
             Offset -> Value; theObj -> Value+;
             Value -> theRep.GCAttr;
             
             theRep -> theObj[Offset];
             pop AddrReg2, AddrReg1;
             return
         if)
     if);
     
     IOAAlloc:
     
     (if debug then
         (if do_unconditional_GC then
             { See general comment in AllocateItem.run }
             push Offset; 
             Range -> BodySize;
             2 -> BodySize*;
             3 -> BodySize+;
             4 -> BodySize/;
             4 -> BodySize*;
             goto GC;
         if);
     if);
     
     again:
     
     { allocate 16+((2*range+3)/4)*4 bytes in IOA }
     IOATop -> theRep;
     
     push Offset; {BodySize assignment destroys Offset}
     
     Range -> BodySize;
     2 -> BodySize*;
     3 -> BodySize+;
     4 -> BodySize/;
     4 -> BodySize*;
     theRep[16+BodySize][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part of the repetition. }
     ShortRepPTValue -> theRep.ProtoType;
     (if IOAMinAge<>0 then
         IOAMinAge -> theRep.GCAttr;
     if);
     1     -> theRep.LowBorder;
     Range -> theRep.HighBorder;
     
     pop Offset; {We're finished using BodySize, so restore Offset}
          
     theObj[Offset][] -> theCell;
     theRep -> theCell[0];
     
     (if call_chkra_unconditionally then
         call ChkRA_EBP;
      else
         (if theCell outside IOA then  { The theCell resides in AOA. }
             push theCell;
             call ChkRA { No need to remove parameter. }
         if);
     if);
     
     pop AddrReg2, AddrReg1;
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     4 -> BodySize/;
     BodySize -> ReqObjectSize+;
     pop Offset; {Restore Offset}
     pop AddrReg2, AddrReg1;
     { Stack: as on entry - data registers are tagged properly. }
     call doGC;              { Call the Garbage Collector.               }
     push AddrReg1, AddrReg2;
     goto again
  #);



AlloVR4: public
  (#
     { Allocate a value repetition of longs. }
     
     { Input variables. }
     theObj: Item  = RegThis;   { The object, which refer the valrep.}
     Offset: Long  = DataReg1; { Offset in the theObj.              }
     Range:  Long  = DataReg2; { The range of the valRep.           }
     
     { Used variables. }
     newTop: DataArea   = AddrReg1;
     theCell: DataArea  = AddrReg1;
     theRep: Repetition = AddrReg2;
     Value:  Long       = DataReg2;
  do
     (if debug then call Ck if);
     (if debug then 1 -> NumAlloVR4+ if); 
     
     push AddrReg1, AddrReg2;
     
     pReg1 -> DataReg1;
     pReg2 -> DataReg2;
     
     (if Range < 0 then 0 -> Range if);
     
     (if Range > LARGE_REP_SIZE then
         push RegThis, RegCall, Offset; { save registers }
         push Range; { Parameter to LVRACAlloc. }
         push LongRepPTValue;    { Parameter to LVRACAlloc. }
         call LVRACAlloc;
         DataReg1 -> theRep;
         4 -> SP+;
         pop Range;
         pop Offset, RegCall, RegThis;
         (if theRep <> 0 then
             { Make the LVRA-cycle: theCell -> theRep.GCAttr }
             Offset -> Value; theObj -> Value+;
             Value -> theRep.GCAttr;
             theRep -> theObj[Offset];  
             pop AddrReg2, AddrReg1;
             return
         if)
     if);
     
     IOAAlloc:
     
     (if debug then
         (if do_unconditional_GC then
             { See general comment in AllocateItem.run }
             goto GC;
         if);
     if);
     
     again:
     { allocate 16+Range*4 bytes in IOA }
     
     IOATop -> theRep;
     theRep[16+Range*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part of the repetition. }
     LongRepPTValue -> theRep.ProtoType;
     (if IOAMinAge<>0 then
         IOAMinAge -> theRep.GCAttr;
     if);
     1     -> theRep.LowBorder;
     Range -> theRep.HighBorder;
     
     theObj[Offset][] -> theCell;
     theRep -> theCell[0];
     
     (if call_chkra_unconditionally then
         call ChkRA_EBP;
      else
         (if theCell outside IOA then  { The theCell resides in AOA. }
             push theCell;
             call ChkRA { No need to remove parameter. }
         if);
     if);

     pop AddrReg2, AddrReg1;
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     Range -> ReqObjectSize+;
     pop AddrReg2, AddrReg1;
     { Stack: as on entry - data registers are tagged properly. }
     call doGC;              { Call the Garbage Collector.               }
     push AddrReg1, AddrReg2;
     goto again
  #);



AlloVR8: public
  (#
     { Allocate a double repetition and assign it to the reference at theObj[offset] }
     
     { Input variables. }
     theObj: Item     = RegThis;   { The object, which refer the valrep. }
     Offset: Long     = DataReg1; { Offset in the theObj.               }
     Range:  Long     = DataReg2; { The range of the valRep.            }
     
     { Used variables. }
     BodySize:  Long    = DataReg1; { the number of bytes to allocate     }
     newTop: DataArea   = AddrReg1;
     theCell: DataArea  = AddrReg1;
     theRep: Repetition = AddrReg2;
     Value:  Long       = DataReg2;
  do
     (if debug then call Ck if);
     (if debug then 1 -> NumAlloVR8+ if); 
     
     push AddrReg1, AddrReg2;
     
     pReg1 -> DataReg1;
     pReg2 -> DataReg2;
     
     (if Range < 0 then 0 -> Range if);
     
     (if Range > LARGE_REP_SIZE then 
         push RegThis, RegCall, Offset; { save registers }
         push Range; { Parameter to LVRACAlloc. }
         push DoubleRepPTValue;    { Parameter to LVRACAlloc. }
         call LVRACAlloc;
         DataReg1 -> theRep;
         4 -> SP+;
         pop Range;
         pop Offset, RegCall, RegThis;
         (if theRep <> 0 then
             { Make the LVRA-cycle: theCell -> theRep.GCAttr }
             Offset -> Value; theObj -> Value+;
             Value -> theRep.GCAttr;
             theRep -> theObj[Offset];
             pop AddrReg2, AddrReg1;
             return
         if)
     if);
     
     IOAalloc: 
     
     (if debug then
         (if do_unconditional_GC then
             { See general comment in AllocateItem.run }
             push Offset; 
             Range -> BodySize;
             8 -> BodySize*;
             goto GC;
         if);
     if);
     
     again:
     
     { allocate 16+range*8 bytes in IOA }
     IOATop -> theRep;
          
     push Offset; {BodySize assignment destroys Offset}
     
     Range -> BodySize;
     8 -> BodySize*;
     theRep[16+BodySize][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part of the repetition. }
     DoubleRepPTValue -> theRep.ProtoType;
     (if IOAMinAge<>0 then
         IOAMinAge -> theRep.GCAttr;
     if);
     1     -> theRep.LowBorder;
     Range -> theRep.HighBorder;
          
     pop Offset; {We're finished using BodySize, so restore Offset}
          
     theObj[Offset][] -> theCell;
     theRep -> theCell[0];

     (if call_chkra_unconditionally then
         call ChkRA_EBP;
      else
         (if theCell outside IOA then  { The theCell resides in AOA. }
             push theCell;
             call ChkRA { No need to remove parameter. }
         if);
     if);
     
     pop AddrReg2, AddrReg1;
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     4 -> BodySize/;    
     BodySize -> ReqObjectSize+;
     pop Offset; {Restore Offset}
     pop AddrReg2, AddrReg1;
     { Stack: as on entry - data registers are tagged properly. }
     call doGC;              { Call the Garbage Collector.               }
     push AddrReg1, AddrReg2;
     goto again
  #)
