------- CopyRefRep: System ----
CopyRR: public
  (#
     { Copy a reference repetition. }
     { stack on entry [return(0),Offset(4),Item(8),RefRep(12),....] }
     
     stack: StackArea = SP;
     
     { Used variables. }
     theRep: Repetition = AddrReg1;
     newTop: DataArea   = AddrReg2;
     newRep: Repetition = AddrReg2;
     theCell: DataArea  = AddrReg1;
     range:  Long       = DataReg1;
     tmp:    Long       = DataReg2;
     value:  Long       = DataReg2;
     offset: Long       = DataReg2;
  do
     (if debug then call Ck if);
     
     {
       General stack layout:
       <Intel: DataReg2>   Intel: +0
       <Intel: DataReg1>   Intel: +4
       <Intel: AddrReg2>   Intel: +8
       <Intel: AddrReg1>   Intel: +12
       <Mac: AddrReg2>                  Mac: +0    
       <return address>    Intel: +16   Mac: +4    Default: +0
       <offset>            Intel: +20   Mac: +8    Default: +4
       <item>              Intel: +24   Mac: +12   Default: +8
       <refrep>            Intel: +28   Mac: +16   Default: +12
     }
     
     (if MacOS then push AddrReg2 if);
     (if intel then push AddrReg1, AddrReg2, DataReg1, DataReg2; if);
     
     (if intel then
         stack[28] -> theRep;
      else
         (if MacOS then
             stack[16] -> theRep;
          else
             stack[12] -> theRep;
         if);
     if);
     theRep.HighBorder -> range;
     
     IOATop -> newTop;
     newTop[16+range*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     
     IOATop -> tmp;
     newTop -> IOATop;
     tmp    -> newRep;
     
     { The new Object is now allocated, but not assigned yet!}
     
     { Initialize the structual part of the repetition. }
     -4    -> newRep.ProtoType;
     1     -> newRep.Age;
     1     -> newRep.LowBorder;
     range -> newRep.HighBorder;
     
     (if intel then
         stack[28] -> theRep;
      else
         (if MacOS then
             stack[16] -> theRep;
          else  
             stack[12] -> theRep; 
         if);
     if);
     
     { Clear the body part of the repetition. }
     (if range <> 0 then
         1 -> range-;
         loop:
         theRep[16+range*4] -> value;
         value -> newRep[16+range*4];
         1 -> range-;
         (if FLAGS >= 0 then goto loop if)
     if);
     
     (if intel then
         stack[24] -> theCell;
         stack[20] -> offset;
      else
         (if MacOS then 
             stack[12] -> theCell;
             stack[8]  -> offset
          else
             stack[8] -> theCell;
             stack[4] -> offset
         if);
     if);
     theCell[offset*4][] -> theCell;
     newRep -> theCell[0];
     
     (if theCell outside IOA then  { The theCell resides in AOA. }
         push theCell;
         call ChkRA { No need to remove parameter. }
     if);
     
     (if intel then pop DataReg2, DataReg1, AddrReg2, AddrReg1; if);
     (if MacOS then pop AddrReg2 if);
     return 12;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     range -> ReqObjectSize+;
     (if intel then pop DataReg2, DataReg1, AddrReg2, AddrReg1; if);
     (if MacOS then pop AddrReg2 if);
     call doGC;              { Call the Garbage Collector.               }
     goto CopyRR             { Go back to the beginning of this routine. }
  #)
