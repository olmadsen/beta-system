------- CopyRefRep: System ----

CopyRR: public
  (#
     { Copy a Reference Repetition. }

     { stack on entry [return(0),...] }
     
     { input parameters }
     { Repetition: pReg1 }
     { Object:     pReg2 }
     { Offset:     pReg3 }
     
     stack: StackArea = SP;
     
     { Used variable.  }
     theRep:    Repetition        = AddrReg1;
     theItem:   Object            = AddrReg1;
     theCell:   DataArea          = AddrReg1;

     newTop:    DataArea          = AddrReg2;
     newRep:    Repetition        = AddrReg2;
     copyCell:  DataArea          = AddrReg2;
     
     BodySize:  Long              = DataReg1; { number of bytes to allocate. 
                                                Equal to range
                                              }
     
     tmp:       Long              = DataReg2;
     value:     Long              = DataReg2;
     offset:    Long              = DataReg2;
  do
     (if debug then 1 -> NumCopyRR+ if); 
     (if debug then call Ck if);
     
     {
       General stack layout:
       <DataReg2>          +0
       <DataReg1>          +4
       <AddrReg2>          +8
       <AddrReg1>          +12
       <return address>    +16 
     }
     
     (if do_unconditional_GC then
	 (if DoUGC<>0 then
	     { See general comment in AllocateItem.run }
	     push AddrReg1, AddrReg2, DataReg1, DataReg2 ;
	     pReg1 -> theRep;
	     theRep.HighBorder -> BodySize;
	     4 -> BodySize*;
	     ObjectAlign -> BodySize|;
	     goto GC;
	 if);
     if);
     
     again:
     
     push AddrReg1, AddrReg2, DataReg1, DataReg2 ;
     
     pReg1 -> theRep;
     theRep.HighBorder -> BodySize;
     
     (if BodySize > LARGE_REP_SIZE then
         push RegCall, RegThis;
         push BodySize; { Parameter to LVRAAlloc. }
         push RefRepPTValue; { Parameter to LVRAAlloc. }
         call LVRAAlloc;
         DataReg1 -> newRep; {Fetch LVRAAlloc result}
         {Restore theRep register, may have been destroyed by LVRAAlloc}
         pReg1 -> theRep;
         
         4 -> SP+; {Discard proto}
         pop BodySize; {Restore BodySize}
         pop RegThis, RegCall;
         
         4 -> BodySize*;
         ObjectAlign -> BodySize|;
	 (if newRep = 0 then 
	      goto GC; { FIXME: Should fall through to IOAAlloc }
	 if);

         goto copyBody
     if);
     
     IOATop -> newTop;
     4 -> BodySize*;
     ObjectAlign -> BodySize|;
     newRep[16+BodySize][] -> newTop;
     (if newTop above IOALimit then goto GC if);
     (if alloc_trace then 14 -> AllocPlace; call RunTraceAlloc; if);
     
     IOATop -> tmp;
     newTop -> IOATop;
     tmp    -> newRep;
     
     { The new Object is now allocated, but not assigned yet!}
     
     { Initialize the structual part of the repetition. }
     RefRepPTValue  -> newRep.ProtoType;
     (if IOAMinAge<>0 then
         IOAMinAge -> newRep.GCAttr;
     if);
     1                 -> newRep.LowBorder;
     theRep.HighBorder -> newRep.HighBorder;
     
     copyBody:
     (if BodySize <> 0 then
         { Copy the body part of the repetition. From last element to first }
         push newRep;
         4 -> BodySize-;
         newRep[16+BodySize][] -> copyCell;  { Cell to copy to }
         (if copyCell outside IOA then  { The theCell resides in AOA. }
             { Loop which calls ChkRA for each element }
             loop1:
             theRep[16+BodySize] -> value;
             value -> copyCell[0];
             (if call_chkra_unconditionally then
                 call ChkRA_ESI;
              else
                 push copyCell;
                 call ChkRA { No need to remove parameter. }
             if);
             4 -> copyCell-;
             4 -> BodySize-;
             (if FLAGS >= 0 then goto loop1 if);
             
             goto CopyDone;
         if);
         
         { Loop which does not call ChkRA }
         loop2:
         theRep[16+BodySize] -> value;
         value -> copyCell[0];
         4 -> copyCell-;
         4 -> BodySize-;
         (if FLAGS >= 0 then goto loop2 if);
         
         CopyDone:
         pop newRep; { pop unmodified newRep }
     if);
     
     { theRep no longer needed }
     
     AssignReference:
     pReg2 -> theItem; { destroys theRep }
     pReg3 -> offset;
     theItem[offset*4][] -> theCell; { destroys theItem }
     newRep -> theCell[0];
     
     (if call_chkra_unconditionally then
         call ChkRA_EBP;
      else
         (if theCell outside IOA then  { The theCell resides in AOA. }
             push theCell;
             call ChkRA { No need to remove parameter. }
         if);
     if);
     
     pop DataReg2, DataReg1, AddrReg2, AddrReg1 ;
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     4 -> BodySize/;
     BodySize -> ReqObjectSize+;
     pop DataReg2, DataReg1, AddrReg2, AddrReg1 ;
     { Stack: as on entry }
     push pReg1; { force update of pReg1 during GC }
     push pReg2; { force update of pReg2 during GC }
     { Stack:
       <pReg2> (Object)
       <pReg1> (Repetition)
       - data registers are tagged properly.
     }
     call doGC;         { Call the Garbage Collector.               }
     pop pReg2;
     pop pReg1;
     goto again       { Go back to the beginning of this routine. }
  #)
