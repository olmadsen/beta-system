---- CopySliceRefRep: System ----

CopySRR: public
  (#
     { Copy a slice of a Reference Repetition. }
     { stack on entry [return(0),offset(4),Item(8),ValRep(12),...] }
     { and registers DataReg1=low, DataReg2=high. }
     
     { Input variables. }
     low:     Long    = DataReg1; {pReg1 in Intel version}
     high:    Long    = DataReg2; {pReg2 in Intel version}
     
     { Used variables. }
     stack:  StackArea  = SP;    
     theRep: Repetition = AddrReg1;
     theObj: Object     = RegThis;
     newRep: Repetition = AddrReg2;
     newTop: DataArea   = AddrReg1;
     value:  Long       = DataReg1;
     offset: Long       = DataReg1;
     theItem: Item      = AddrReg1;
     theCell: DataArea  = AddrReg1
  do
     (if debug then 1 -> NumCopySRR+ if); 
     (if debug then call Ck if);
     
     {
       General stack layout:
       <Intel: AddrReg2>   Intel: +0
       <Intel: AddrReg1>   Intel: +4
       <Mac: AddrReg2>                  Mac: +0    
       <return address>    Intel: +8    Mac: +4    Default: +0
       <offset>            Intel: +12   Mac: +8    Default: +4
       <item>              Intel: +16   Mac: +12   Default: +8
       <valrep>            Intel: +20   Mac: +16   Default: +12
     }

     (if intel then pReg1 -> DataReg1; pReg2 -> DataReg2; if);
     
     (if MacOS then push AddrReg2 if);
     (if intel then push AddrReg1, AddrReg2 if);
     
     (if intel then
         stack[20] -> theRep;
      else 
         (if MacOs then
             stack[16] -> theRep;
          else
             stack[12] -> theRep;
         if);
     if);
     
     { Check that low and high are usable. }
     (if low  < theRep.LowBorder then 
         (if intel then 
             24 -> stack+ 
          else
             16-> stack+;
         if);
         (if MacOS then 4 -> stack+ if);
         push theObj; push -6; call BetaError 
     if);
     (if high > theRep.HighBorder then 
         (if intel then 
             24 -> stack+ 
          else
             16-> stack+;
         if);
         (if MacOS then 4 -> stack+ if);
         push theObj; push -7; call BetaError 
     if);
 
     { Calculate the range of the new repetition. }
     low  -> high-; 1 -> high+;
     (if high < 0 then 0 -> high if); { OBS: low>high confuses chk2 }
     { high is now converted to the range of the resulting repetition. }
     
     again:
     
     IOATop -> newRep;
     newRep[16+high*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     (if intel then
         stack[20] -> theRep;
      else
         (if MacOS then
             stack[16] -> theRep;
          else
             stack[12] -> theRep;
         if);
     if);
     
     { The new Object is now allocated, but not assigned yet!}
     
     { Initialize the structual part of the repetition. }
     -4    -> newRep.ProtoType;
     1     -> newRep.Age;
     1     -> newRep.LowBorder;
     high  -> newRep.HighBorder;
     
     { Copy the body part of the repetition. }
     1 -> low-;
     theRep[low*4][] -> theRep; { Start at low. }
     
     (if high <> 0 then
         1 -> high-;
         loop:
         theRep[16+high*4] -> value;
         value -> newRep[16+high*4];
         1 -> high-;
         (if FLAGS >= 0 then goto loop if)
     if);
     
     (if intel then
         stack[16] -> theItem;
         stack[12] -> offset;
      else         
         (if MacOS then
             stack[12] -> theItem;
             stack[8] -> offset;
          else
             stack[8] -> theItem;
             stack[4] -> offset;
         if);
     if);
     
     newRep -> theItem[offset*4];
     
     theItem[offset*4][] -> theCell;
     (if theCell outside IOA then  { The theCell resides in AOA. }
         push theCell;
         call ChkRA                { No need to remove parameter. }
     if);
     
     (if intel then pop AddrReg2, AddrReg1 if);
     (if MacOS then pop AddrReg2 if);
     (if debug then call Ck if);
     return 12;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     high -> ReqObjectSize+;
     (if intel then pop AddrReg2, AddrReg1 if);
     call doGC;              { Call the Garbage Collector.               }
     (if intel then push AddrReg1, AddrReg2 if);
     goto again              { Go back to the beginning of this routine. }
  #)
