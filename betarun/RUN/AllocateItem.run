------- AllocateItem: System ----
AlloI: public
  (#
     { Allocate an item. }
     
     { Input variable.  }
     protoType: ProtoType = RegArg;
     origin:    Object    = RegAdr;
     
     { variables }
     Size:      Word      = DataReg2;
     Tmp:       Long      = DataReg1;
     theObj:    Item      = RegObj;
     initTab:   CodeArea  = AddrReg1; { index in GC table in prototype }
{     genTab:    CodeArea  = RegArg; }
     Argument:  DataArea  = RegArg;
     Entry:     CodeArea  = AddrReg1;
     Index:     Word      = DataRegA; { saved and restored }
     SavedD0:   Long      = DataRegA; { needed to save and restore D0 as long }
     newTop:    DataArea  = AddrReg1;
     theItem:   Item      = AddrReg2
  do
     (if MacOS then push AddrReg2 if);
     0 -> Size!l;
     protoType.Size -> Size;
     IOATop -> theItem;
     theItem[Size*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part; prototype and age. }
     protoType    -> theItem.ProtoType;
     1            -> theItem.Age;
     
     { Initialize the body part of the item, according to the genTable. }
     protoType -> initTab;
     protoType.GCTabel -> initTab+;
     
     { initTab is now pointing to the static GCTable.                            }
     { This table has zero or more elements terminated with a zero word.         }
     { Each element looks like:                                                  }
     {   WORD  Offset in the generated object, where this static object begins.  }
     {   WORD  Signed distance to inclosing object.                              }
     {   LONG  Prototype of this static object.                                  }
     
     push SavedD0;
     2 -> Index; { relative index in object }
     loop:
     (if Index < Size then
         (if Index = initTab[0]!w then
             2 -> initTab+;
             { Insert the distance to the autonomous object. }
             initTab[0]!w -> Tmp;
             extend Tmp;
             Tmp!l -> theItem[4+Index*4];
             2 -> initTab+;
             { Insert the protoType from the initTab. }
             initTab[0]!l -> Tmp!l; Tmp!l -> theItem[Index*4]!l;
             4 -> initTab+;
             2 -> Index+;     
             goto loop
         if);
         0 -> theItem[Index*4]!l;
         1 -> Index+;
         goto loop
     if);
     
     protoType.OrigOff -> Index;
     origin -> theItem[Index*4];
     
     pop SavedD0;
     
     protoType.GenPart -> Entry;
     theItem -> Argument;
     (if MacOS then pop AddrReg2 if);     
     goto Entry;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.             }
     Size!l -> ReqObjectSize; { Set the requested Object Size.            }
     (if MacOS then pop AddrReg2 if);
     call DoGC;               { Call the Garbage Collector.               }
     goto AlloI               { Go back to the beginning of this routine. }
  #)

