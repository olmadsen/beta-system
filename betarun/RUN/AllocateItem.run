------- AllocateItem: System ----
AlloI: public
  (#
     { Allocate an item.
       Destroys: RegCall (output), DataReg1.
       May cause GC.
     }
     
     { Input variables. }
     protoType:     ProtoType = RegCall;
     { origin in pReg1 }
     
     { Output variable. }
     Argument:      DataArea  = RegCall;
     
     { Used variables. }
     Size:          Word      = DataReg2;
     DataReg2Long:  Long      = DataReg2; 
     Tmp:           Long      = DataReg1;
     initTab:       CodeArea  = AddrReg1; { index in GC table in prototype }
     Entry:         CodeArea  = AddrReg1;
     Index:         Word      = RegAdr; { saved and restored }
     RegAdrLong:  Long      = RegAdr; { saved and restored }
     newTop:        DataArea  = AddrReg1;
     theItem:       Item      = AddrReg2;
          
  do
     (if debug then 1 -> NumAlloI+ if); 
     (if debug then call Ck if);
     
     again:
     
     push AddrReg1, AddrReg2, DataReg2Long;
     
     protoType.Size -> Size;
     IOATop -> theItem;
     theItem[Size*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part; prototype and age. }
     protoType    -> theItem.ProtoType;
     (if IOAMinAge<>0 then
         IOAMinAge -> theItem.GCAttr;
     if);
     
     { Initialize the body part of the item, according to the genTable. }
          
     clear initTab;
     protoType.GCTabel -> initTab!w+;
     protoType -> initTab+;
     
     { initTab is now pointing to the static GCTable. 
       This table has zero or more elements terminated with a zero word. 
       Each element looks like: 
       
       WORD Offset in the generated object, where this static object begins 
       WORD Signed distance to enclosing object 
       LONG Prototype of this static object 
     }
     
     push RegAdrLong;
     
     2 -> Index; { relative index in object }
     loop:
     (if Index < Size then
         (if Index = initTab[0]!w then
             { Insert the distance to the autonomous object. }
             initTab[2]!w -> Tmp!w;
             extend Tmp;
             Tmp!l -> theItem[4+Index*4];
             { Insert the protoType from the initTab. }
             initTab[4]!l -> Tmp!l; 
             Tmp!l -> theItem[Index*4]!l;
             8 -> initTab+;
             2 -> Index+;     
             goto loop
         if);
         1 -> Index+;
         goto loop
     if);
     
     protoType.OrigOff -> Index;
     pReg1 -> Tmp!l;
     Tmp!l -> theItem[Index*4];
     
     pop RegAdrLong;
     
     protoType.GenPart -> Entry; { Must be before theItem -> Argument }
     theItem -> Argument;

     (if Entry=0 then goto CleanUp; if);
     
     clear AddrReg2; {Invalidate GC register}
     push Protect1Tag; {Protect DataReg2}
     call Entry;
     4 -> SP+; {Discard Protect1Tag tag}
     
     CleanUp:
     pop DataReg2Long, AddrReg2, AddrReg1;
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.  }
     Size!l -> ReqObjectSize; { Set the requested Object Size. }
     pop DataReg2Long, AddrReg2, AddrReg1;
     push pReg1; { update origin }
     call doGC;  { Call the Garbage Collector. }
     pop pReg1;
     goto again;
  #);



AlloH: public
  (#
     { Like AlloI, but does not set up an origin, and does not 
       call G-entry ("AllocateHeap")
     }
     
     { Input variables. }
     protoType:     ProtoType = RegCall;
     
     { Output variable. }
     Argument:      DataArea  = RegCall;
     
     { Used variables. }
     Size:          Word      = DataReg2;
     DataReg2Long:  Long      = DataReg2; 
     Tmp:           Long      = DataReg1;
     initTab:       CodeArea  = AddrReg1; { index in GC table in prototype }
     Entry:         CodeArea  = AddrReg1;
     Index:         Word      = RegAdr; { saved and restored }
     RegAdrLong:  Long      = RegAdr; 
     newTop:        DataArea  = AddrReg1;
     theItem:       Item      = AddrReg2;
     
  do
     (if debug then call Ck if);
     
     { 
       General stack layout:
       <DataReg2>: +0
       <AddrReg2>: +4
       <AddrReg1>: +8
       ... 
     }
     
     again:
     
     push AddrReg1, AddrReg2, DataReg2Long;
     
     protoType.Size -> Size;
     IOATop -> theItem;
     theItem[Size*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not initialized yet!}
     
     { Initialize the structual part; prototype and age. }
     protoType    -> theItem.ProtoType;
     (if IOAMinAge<>0 then
         IOAMinAge -> theItem.GCAttr;
     if);
     
     { Initialize the body part of the item, according to the genTable. }
          
     clear initTab; 
     protoType.GCTabel -> initTab!w+;
     protoType -> initTab+;
     
     { initTab is now pointing to the static GCTable. }
     { This table has zero or more elements terminated with a zero word. }
     { Each element looks like: }
     { WORD Offset in the generated object, where this static object begins }
     { WORD Signed distance to inclosing object }
     { LONG Prototype of this static object }
     
     push RegAdrLong;
     
     2 -> Index; { relative index in object }
     loop:
     (if Index < Size then
         (if Index = initTab[0]!w then
             2 -> initTab+;
             { Insert the distance to the autonomous object. }
             initTab[0]!w -> Tmp!w;
             extend Tmp;
             Tmp!l -> theItem[4+Index*4];
             2 -> initTab+;
             { Insert the protoType from the initTab. }
             initTab[0]!l -> Tmp!l; Tmp!l -> theItem[Index*4]!l;
             4 -> initTab+;
             2 -> Index+;     
             goto loop
         if);
         1 -> Index+;
         goto loop
     if);
     
     pop RegAdrLong;
     
     theItem -> Argument;
     pop DataReg2Long, AddrReg2, AddrReg1;
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.    }
     Size!l -> ReqObjectSize; { Set the requested Object Size.   }
     pop DataReg2Long, AddrReg2, AddrReg1;
     call doGC;    { Call the Garbage Collector.               }
     goto again;
  #)
