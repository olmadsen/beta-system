----- ExitObjects:system ------

ExO: public
  (#
     { Input variables. }
     theObj:  Item = RegObj;	   { the object we are leaving }
     exitObj: Item = RegArg;       { the object to exit to. }
     exitAdr: CodeArea = DataReg1; { Code address to jump to. }
                                   {pReg1 in Intel version}
     offset:  Long = DataRegA;     { Offset in theObj where to get to old sp-value }
                                   {DataReg1 in Intel version}
     
     { Used variables. }
     stackPointer: StackArea = SP;
     theComp: Component = AddrReg1;
     stackValue: DataArea = AddrReg1;
     tempSp: DataArea = AddrReg1;  { Necessary om mac: sp may not have strange
                                    values, even temporarily }
     addr:    CodeArea = AddrReg1;
     tmpOffset: Long = DataReg2;   { Used in Intel version }
  do
     (if debug then call Ck if);
     
     (if intel then 
         DataRegA -> DataReg2; { Save offset. About to be overwritten by exitAdr. }
         pReg1 -> DataReg1;
     if);
     
     again:
     
     (if theObj = exitObj then 
         { found the object to exit to }
         goto exitLabel
     if);
     
     pop stackValue;
     (if stackValue = exitObj then goto exitLabel if);
     
     ActiveComponent -> theComp;
     (if theObj = theComp.Item then
         { passing component. terminate it as in AttachComponent }

         (if theComp.CallerComp = 0 then 
             { attempt to leave basic component! }
             push -14;
             call BetaError
         if);
         theComp.CallerComp -> ActiveComponent; 
         theComp.CallerObj  -> theObj;         
         0 -> theComp.StackObj;
         0 -> theComp.CallerComp;
         0 -> theComp.CallerObj;
         
         { Pop the component block }
         pop ActiveCallBackFrame;
         pop lastCompBlock;
         4 -> SP+ { level not used ? }
     if);
     goto again;
     
     exitLabel:
     exitObj -> theObj;
     (if intel then
         exitObj[tmpOffset]!l -> tempSp;
      else
         exitObj[offset]!l -> tempSp;
     if);
     lastCompBlock -> tempSp+;
     tempSp -> stackPointer;
     exitAdr -> addr;
     goto addr; {AddrRegs ok; stack ok, too}
  #)
