------- Attach: System ----
Att: public
  (#
     { Attach a component. }
     
     { Used variable.  }
     theComp: Component = RegArg;
     compObj: Object    = RegArg;
     theObj:  Item      = RegObj;
     stackPointer: StackArea = SP;
     theProto: ProtoType = AddrReg2;
     theAddr:  CodeArea  = AddrReg1;
     theEntry: CodeArea = AddrReg1;
     theCell:  DataArea = AddrReg1;
     tmpComp: Component = AddrReg1
  do
     (if theComp.CallerLSC = 0 then goto FirstAttach if);
     pop theComp.CallerLSC; { Save the return address }
     
     ActiveComponent -> theComp.CallerComp;
     { theComp.CallerComp[] -> CheckReferenceAssignment. }
     theComp -> theCell; theCell[16][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA
     if);
     
     theObj -> theComp.CallerObj;
     { theComp.CallerObj -> CheckReferenceAssignment. }
     theComp -> theCell; theCell[12][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA
     if);
     
     { -1 -> ActiveComponent.StackObj. }
     { Tells that ActiveComponent is active.     }
     ActiveComponent -> tmpComp;
     -1 -> tmpComp.StackObj;
     
     { Push a new component block. }
     push 0;
     push lastCompBlock;
     push ActiveCallBackFrame;
     
     stackPointer -> lastCompBlock;
     0            -> ActiveCallBackFrame;  
     
     (if theComp.StackObj = 0 then goto IsTerminated if);
     
     theComp -> ActiveComponent;
     
     call UnpkCS;
     
     pop theObj;
     return;
     
     FirstAttach:
     
     pop theComp.CallerLSC; { Save the return address }
     ActiveComponent -> theComp.CallerComp;
     { theComp.CallerComp -> CheckReferenceAssignment. }
     theComp -> theCell; theCell[16][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA
     if);
     
     theObj -> theComp.CallerObj;
     { theComp.StackObj -> CheckReferenceAssignment. }
     theComp -> theCell; theCell[12][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA
     if);
     
     { -1 -> ActiveComponent.StackObj. }
     { Tells that ActiveComponent is active.     }
     ActiveComponent -> tmpComp;
     -1 -> tmpComp.StackObj;
     
     { Push a new component block. }
     push 0;
     push lastCompBlock;
     push ActiveCallBackFrame;
     
     stackPointer -> lastCompBlock;
     0            -> ActiveCallBackFrame;  
     
     theComp -> ActiveComponent;
     theComp[24][] -> compObj;
     compObj.ProtoType -> theProto;  
     TerminateComponent[] -> theAddr;
     push theAddr;
     
     theProto.EntryPoint -> theEntry;
     goto theEntry;
     
     TerminateComponent:
     ActiveComponent -> theComp;
     0 -> theComp.StackObj;
     theComp.CallerComp -> ActiveComponent; 0 -> theComp.CallerComp;
     theComp.CallerObj  -> theObj;          0 -> theComp.CallerObj;
     
     { Pop the Component Block. }
     pop ActiveCallBackFrame;
     pop lastCompBlock;
     pop theEntry;
     
     theComp.CallerLSC -> theEntry;
     goto theEntry;
     
     IsTerminated:
     push theObj;
     push -2;
     call BetaError
  #)

