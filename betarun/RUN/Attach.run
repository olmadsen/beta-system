------- Attach: System ----
Att: public
  (#
     { Attach a component. }
     
     { Input variables. }
     comp: Component = RegArg;
     theObj:  Item      = RegObj;
     
     { Used variables. }
     compObj: Object    = RegArg;
     stackPointer: StackArea = SP;
     theProto: ProtoType = AddrReg2;
     theAddr:  CodeArea  = AddrReg1;
     theEntry: CodeArea = AddrReg1;
     theCell:  DataArea = AddrReg1;
     tmpComp: Component = AddrReg1
  do
     (if debug then call Ck if);
     (if debug then 1 -> NumAtt+ if); 
     
     (if comp.CallerLSC = 0 then goto FirstAttach if);
     
     pop comp.CallerLSC; { Save the return address }
     
     (if comp.StackObj =  -1    then goto RecursiveAttach; if);
     (if comp = ActiveComponent then goto RecursiveAttach; if);
     
     ActiveComponent -> comp.CallerComp;
     { comp.CallerComp[] -> CheckReferenceAssignment. }
     comp -> theCell; theCell[16][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA; { No need to remove parameter }
     if);
     
     theObj -> comp.CallerObj;
     { comp.CallerObj -> CheckReferenceAssignment. }
     comp -> theCell; theCell[12][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA; { No need to remove parameter }
     if);
     
     { -1 -> ActiveComponent.StackObj. }
     { Tells that ActiveComponent is active.     }
     ActiveComponent -> tmpComp;
     -1 -> tmpComp.StackObj;
     
     (if comp.StackObj = 0 then goto IsTerminated if);
     
     { Push a new component block. }
     push 0;
     push lastCompBlock;
     push ActiveCallBackFrame;
     
     stackPointer -> lastCompBlock;
     0            -> ActiveCallBackFrame;  
     
     comp -> ActiveComponent;
     
     call UnpkCS;
     
     pop theObj; { Current object was pushed by Suspend before calling
                   PackCS
                 }
     (if intel then 
         0 -> AddrReg2; {Invalidate GC register; AddrReg1 invalidated by UnpkCS}
     if);
     (if debug then call Ck if);
     return;
     
     FirstAttach:
     
     pop comp.CallerLSC; { Save the return address }
     ActiveComponent -> comp.CallerComp;
     { comp.CallerComp -> CheckReferenceAssignment. }
     comp -> theCell; theCell[16][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA; { No need to remove parameter }
     if);
     
     theObj -> comp.CallerObj;
     { comp.StackObj -> CheckReferenceAssignment. }
     comp -> theCell; theCell[12][] -> theCell;
     (if theCell outside IOA then
         push theCell;
         call ChkRA; { No need to remove parameter }
     if);
     
     { -1 -> ActiveComponent.StackObj. }
     { Tells that ActiveComponent is active.     }
     ActiveComponent -> tmpComp;
     -1 -> tmpComp.StackObj;
     
     { Push a new component block. }
     push 0;
     push lastCompBlock;
     push ActiveCallBackFrame;
     
     stackPointer -> lastCompBlock;
     0            -> ActiveCallBackFrame;  
     
     comp -> ActiveComponent;
     comp[24][] -> compObj;
     compObj.ProtoType -> theProto;  
     TerminateComponent[] -> theAddr;
     push theAddr;
     
     theProto.TopMpart -> theEntry;
     goto theEntry; {AddrRegs ok; stack ok, too}
     
     TerminateComponent:
     ActiveComponent -> comp;
     0 -> comp.StackObj;
     comp.CallerComp -> ActiveComponent; 0 -> comp.CallerComp;
     comp.CallerObj  -> theObj;          0 -> comp.CallerObj;
     
     { Pop the Component Block. }
     pop ActiveCallBackFrame;
     pop lastCompBlock;
     pop theEntry;
     
     comp.CallerLSC -> theEntry;
     (if intel then
         0 -> AddrReg2; {Invalidate GC register}
     if);
     (if debug then call Ck if);
     goto theEntry;
     
     IsTerminated:
     4 -> SP+; { Pop comp, which compiler saves around call of Att }
     push theObj;
     push -2;
     call BetaError;
     
     RecursiveAttach:
     4 -> SP+; { Pop comp, which compiler saves around call of Att }
     push theObj;
     push -17; { RecursiveAttErr }
     call BetaError;
  #)

