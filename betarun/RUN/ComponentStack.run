----- ComponentStack: system ------
PackCS: public
  (#
     { Pack the top most component block into 'ActiveComponent.StackObj'. }
     
     stackPointer: StackArea = SP;
     theComp: Component = AddrReg1;
     Size: Long = DataReg1;
     StackSize: Long = DataReg2;
     theStackObj: StackObject = AddrReg2;
     Value: Long = DataReg2
  do
     (if MacOS then push AddrReg2 if);
     lastCompBlock -> Size;
     stackPointer -> Size-;
     4 -> Size/;
     1 -> Size-;
     { Size =  ((lastCompBlock - SP) DIV 4)-1; size of block in longs. }
     
     ActiveComponent -> theComp;
     theComp.StackObj -> theStackObj;
     
     (if theStackObj =  0 then goto allocStackObj if);
     (if theStackObj = -1 then goto allocStackObj if); 
     (if Size <= theStackObj.ObjectSize then goto fillStackObj if);
     
     { Allocate a new StackObject. }
     allocStackObj:
     5    -> StackSize;
     Size -> StackSize+;
     call AlloSO;
     ActiveComponent -> theComp;
     theStackObj -> theComp.StackObj;
     
     fillStackObj:
     Size -> theStackObj.StackSize;
     1 -> Size-;
     loop:
     stackPointer[4+Size*4]!l -> Value;
     Value -> theStackObj[16+Size*4];
     1 -> Size-;
     (if FLAGS >= 0 then goto loop if);
     (if MacOS then pop AddrReg2 if);
     return
  #);

UnpkCS: public
  (#
     { Unpack 'ActiveComponent.StackObj' on top of the stack.  }
     
     theComp:     Component   = AddrReg2;
     theStackObj: StackObject = AddrReg2;
     Size:        Long        = DataReg1;
     retAddr:     CodeArea    = AddrReg1
  do
     pop retAddr;
     
     (if MacOS then push AddrReg2 if);
     
     ActiveComponent -> theComp;
     theComp.StackObj -> theStackObj;
     theStackObj.StackSize -> Size;
     
     1 -> Size-;
     loop:
     push theStackObj[16+Size*4];
     1 -> Size-;
     (if FLAGS >= 0 then goto loop if);
     
     (if MacOS then pop AddrReg2 if);
     goto retAddr
  #)
