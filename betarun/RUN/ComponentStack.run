----- ComponentStack: system ------
PackCS: public
  (#
     { Pack the top most component block into 'ActiveComponent.StackObj'. }
     {Called from Suspend only}
     
     { Used variables. }
     stackPointer: StackArea = SP;
     theComp: Component = AddrReg1;
     theCell: Long = AddrReg1;
     Size: Long = DataReg1;
     StackSize: Long = DataReg2;
     theStackObj: StackObject = AddrReg2;
     Value: Long = DataReg2;
  do
     (if debug then call Ck if);
     
     lastCompBlock -> Size;
     stackPointer -> Size-;
     4 -> Size/;
     1 -> Size-;
     { Size =  ((lastCompBlock - SP) DIV 4)-1; size of block in longs. }
     
     ActiveComponent -> theComp;
     theComp.StackObj -> theStackObj;
     
     (if theStackObj =  0 then goto allocStackObj if);
     (if theStackObj = -1 then goto allocStackObj if); 
     (if Size <= theStackObj.ObjectSize then goto fillStackObj if);
     
     { Allocate a new StackObject. }
     allocStackObj:
     5    -> StackSize;
     Size -> StackSize+;
     call AlloSO; {No need to invalidate AddrReg1, AddrReg2}
     ActiveComponent -> theComp;
     theStackObj -> theComp.StackObj;
     { CheckReferenceAssignment. }
     theComp[8][] -> theCell; { invalidates theComp }
     (if theCell outside IOA then
         push theCell;
         call ChkRA; { No need to remove parameter }
         goto fillStackObj;
     if);
     
     
     fillStackObj:
     { clear AddrReg1 }
     (if hp then
         theCell -> theCell-;
      else
         0 -> theCell;
     if);         
     Size -> theStackObj.StackSize;
     1 -> Size-;
     loop:
     stackPointer[4+Size*4]!l -> Value;
     Value -> theStackObj[16+Size*4];
     1 -> Size-;
     (if FLAGS >= 0 then goto loop if);
     
     (if debug then call Ck if);
     return
  #);

UnpkCS: public
  (#
     { Unpack 'ActiveComponent.StackObj' on top of the stack.  }
     {Called from Attach only}
     
     { Used variables. }
     theComp:     Component   = AddrReg2;
     theStackObj: StackObject = AddrReg2;
     Size:        Long        = DataReg1;
     retAddr:     CodeArea    = AddrReg1;
  do
     pop retAddr;
     
     ActiveComponent -> theComp;
     theComp.StackObj -> theStackObj;
     theStackObj.StackSize -> Size;
     
     1 -> Size-;
     loop:
     push theStackObj[16+Size*4];
     1 -> Size-;
     (if FLAGS >= 0 then goto loop if);
     
     (if debug then call Ck if);
     goto retAddr
  #)
