----- ComponentStack: system ------
PackCS: public
  (#
     { Pack the top most component block into 'ActiveComponent.StackObj'. }
     { Called from Suspend only.
       FIXME: could relatively easy be inlined in Susp.
     }
     
     { Used variables. }
     stack: StackArea = SP;
     theComp: Component = AddrReg1;
     theCell: Long = AddrReg1;
     Size: Long = DataReg1;
     StackSize: Long = DataReg2;
     theStackObj: StackObject = AddrReg2;
     Value: Long = DataReg2;
  do
     (if debug then call Ck if);
     
     lastCompBlock -> Size;
     stack -> Size-;
     4 -> Size-; { ignore return address }
     { Size =  (lastCompBlock - SP) - 4; size of block in bytes. }
     
     { Allocate a new StackObject - stackobjects are no longer
       left in components, when they are attached.
     }
     Size -> StackSize;
     call AlloSO; { No need to clear AddrReg1, AddrReg2 }
     ActiveComponent -> theComp;
     theStackObj -> theComp.StackObj;
     { CheckReferenceAssignment. }
     theComp[8][] -> theCell; { invalidates theComp }
     (if call_chkra_unconditionally then
         call ChkRA_EBP;
      else
         (if theCell outside IOA then  { The theCell resides in AOA. }
             push theCell;
             call ChkRA { No need to remove parameter. }
         if);
     if);
     
     clear theCell; { clear AddrReg1 }
     4 -> Size/; { Size is now number of longs }
     Size -> theStackObj.StackSize;
     1 -> Size-;
     loop:
     stack[4+Size*4]!l -> Value;
     Value -> theStackObj[16+Size*4];
     1 -> Size-;
     (if FLAGS >= 0 then goto loop if);
     
     (if debug then call Ck if);
     return
  #)
