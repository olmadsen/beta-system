------- ExtendRefRep: System ----
ExtRR: public
  (#
     { Extend a value repetition. }
     { stack on entry [return(0),offset(4),Item(8),...] }
     
     { Input variable.  }
     addSize: Long    = DataReg1; { the size to extend the repetition with. }
     
     { Used variable.  }
     stack:  StackArea  = SP;
     newRep: Repetition = AddrReg2;
     newTop: DataArea   = AddrReg1;
     theRep: Repetition = AddrReg1;
     theCell: DataArea  = AddrReg1;
     
     oldRange: Long     = DataReg2;
     newRange: Long     = DataReg2;
     value:    Long     = DataReg1;
     offset:   Long     = DataReg2
  do
     {
       General stack layout:
       <Linux: DataReg2>   Linux: +0
       <Linux: AddrReg2>   Linux: +4
       <Linux: AddrReg1>   Linux: +8
       <Mac: AddrReg2>                  Mac: +0    
       <return address>    Linux: +12   Mac: +4    Default: +0
       <offset>            Linux: +16   Mac: +8    Default: +4
       <item>              Linux: +20   Mac: +12   Default: +8
     }
     
     (if linux then pReg1 -> DataReg1 if);
     
     again:
     
     (if MacOS then push AddrReg2 if);
     (if linux then push AddrReg1, AddrReg2, DataReg2 if);
     
     (if linux then
         stack[20] -> theRep;
         stack[16] -> offset;
      else
         (if MacOS then
             stack[12] -> theRep;
             stack[8] -> offset;
          else
             stack[8] -> theRep;
             stack[4] -> offset;
         if);
     if);
     theRep[offset*4] -> theRep;
     
     theRep.HighBorder -> newRange;
     addSize  -> newRange+;
     (if newRange < 0 then 0->newRange if);
     
     IOATop -> newRep;
     newRep[16+newRange*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Object is now allocated, but not assigned yet!}
     
     { Initialize the structual part of the repetition. }
     -4       -> newRep.ProtoType;
     1        -> newRep.Age;
     1        -> newRep.LowBorder;
     newRange -> newRep.HighBorder;
     
     { Reload theRep, was overwritten by newTop }
     (if linux then
         stack[20] -> theRep;
      else
         (if MacOS then
             stack[12] -> theRep;
          else
             stack[8] -> theRep;
         if);
     if);
     
     push newRange; { Assignment to offset destroys newRange }
     
     { Reload offset, was overwritten by newRange. }
     (if linux then
         stack[20{16+4}] -> offset;
      else
         (if MacOS then
             stack[12{8+4}] -> offset;
          else
             stack[8{4+4}] -> offset;
         if);
     if);
     
     theRep[offset*4]  -> theRep;
     
     pop newRange; { offset no longer needed, restore newRange }
     
     (if addSize > 0 then			
         theRep.HighBorder -> oldRange;
         goto copyBody;
     if);							
     {newRange -> oldRange}; { Assignment is void. Skip it! }
	  
     copyBody:
     { Copy the body part of theRep to newRep. }
     push oldRange; { Used to cycle through theRep }
     push addSize; { Assignment to value destroys addSize }
     (if oldRange > 0 then
         1 -> oldRange-;
         loop1:
         theRep[16+oldRange*4] -> value;
         value -> newRep[16+oldRange*4];
         1 -> oldRange-;
         (if FLAGS >= 0 then goto loop1 if)
     if);
     pop addSize; { Restore addSize }
     pop oldRange; { Restore original oldRange }
     
     { Calculate the address of the extension part into theRep. }
     newRep[16+oldRange*4][] -> theRep;
     
     { Clear the extension part of newRep, pointed out by theRep. }
     (if addSize > 0 then
         1 -> addSize-;
         loop2:
         0 -> theRep[addSize*4];
         1 -> addSize-;
         (if FLAGS >= 0 then goto loop2 if)
     if);
     
     (if linux then
         stack[20] -> theCell;
         stack[16]  -> offset;
      else
         (if MacOS then 
             stack[12] -> theCell;
             stack[8]  -> offset;
          else
             stack[8] -> theCell;
             stack[4] -> offset;
         if);
     if);
     theCell[offset*4][] -> theCell;
     newRep -> theCell[0];
     
     (if theCell outside IOA then  { The theCell resides in AOA. }
         push theCell;
         call ChkRA { No need to remove parameter. }
     if);
     
     (if linux then pop DataReg2, AddrReg2, AddrReg1 if);
     (if MacOS then pop AddrReg2 if);
     return 8;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     newRange -> ReqObjectSize+;
     (if linux then pop DataReg2, AddrReg2, AddrReg1 if);
     (if MacOS then pop AddrReg2 if);
     call DoGC;              { Call the Garbage Collector.               }
     goto again              { Go back to the beginning of this routine. }
  #)


