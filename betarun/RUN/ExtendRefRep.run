------- ExtendRefRep: System ----

ExtRR: public
  (#
     { Extend a reference repetition. }
     { stack on entry [return(0)...] }
     
     { Input variable. }
     { Object:     pReg1 }
     { Offset:     pReg2 }
     { Add:        pReg3 }
	
     { Used variables. }
     newRep:      Repetition       = AddrReg2;
     copyCell:    DataArea         = AddrReg2;
     
     newTop:      DataArea         = AddrReg1;
     theItem:     Object           = AddrReg1;
     theRep:      Repetition       = AddrReg1;
     theCell:     DataArea         = AddrReg1;
     
     addSize:     Long             = DataReg1;
     oldBodySize: Long             = DataReg1;
     
     newRange:    Long             = DataReg2;
     offset:      Long             = DataReg2;
     
     BodySize:    Long             = RegAdr; { Saved and restored. }
     value:       Long             = RegAdr  { Saved and restored. }
  do
     (if debug then 1 -> NumExtRR if); 
     (if debug then call Ck if);
     
     {
       General stack layout:
       
       <RegAdr>     
       <DataReg2>   
       <AddrReg2>   
       <AddrReg1>   
     }
     
     (if do_unconditional_GC then
	 (if DoUGC<>0 then
	     { See general comment in AllocateItem.run }
	     push AddrReg1, AddrReg2, DataReg2 ;
	     push RegAdr;
	     pReg1 -> theItem;
	     pReg2 -> offset;
	     pReg3 -> addSize;
	     theItem[offset*4] -> theRep; 
	     theRep.HighBorder -> newRange; 
	     addSize  -> newRange+;
	     (if newRange < 0 then 0 -> newRange if);
	     newRange -> BodySize; 
	     4 -> BodySize*;
	     ObjectAlign -> BodySize|;
	     goto GC;
	 if);
     if);

     again:
     
     push AddrReg1, AddrReg2, DataReg2 ;
     push RegAdr;
     
     pReg1 -> theItem;
     pReg2 -> offset;
     pReg3 -> addSize;
     theItem[offset*4] -> theRep; { invalidates theItem }
     
     theRep.HighBorder -> newRange; { invalidates offset }
     addSize  -> newRange+;
     
     (if newRange < 0 then 0 -> newRange if);
     
     { STACK:
       <General stack layout>
     }
     
     LVRAcheck:
     (if newRange > LARGE_REP_SIZE then
         
         { STACK:
           <General stack layout>
         }
         
         push RegCall, RegThis; 
         push theRep; {Save theRep}
         
         push newRange;  { Parameter to LVRAXAlloc. }
         push theRep.HighBorder; { Parameter to LVRAXAlloc. }
         push RefRepPTValue;  { Parameter to LVRAXAlloc. }
         call LVRAXAlloc; 
         DataReg1 -> newRep;
         8 -> SP+; {Discard prototype and oldrange parameter}
         pop newRange; {Restore newRange}
         pop theRep; {Restore theRep}
         pop  RegThis, RegCall;
         pReg3 -> addSize; { addsize needed by copybody }
         
         { STACK:
           <General stack layout>
         }
         
         { Calculate BodySize of new repetition }
         newRange -> BodySize; { invalidates RegAdr }
         4 -> BodySize*;
         ObjectAlign -> BodySize|;
	 (if newRep = 0 then 
	      goto GC; { FIXME: Should fall through to IOAAlloc }
	 if);
         goto copyBody 
     if);
     { end LVRAcheck }
     
     IOAAlloc: 
     
     { Calculate BodySize of new repetition }
     newRange -> BodySize; { invalidates RegAdr }
     
     4 -> BodySize*;
     ObjectAlign -> BodySize|;
     IOATop -> newRep;
     newRep[16+BodySize][] -> newTop;
     
     CheckGC:
     
     (if newTop above IOALimit then goto GC if);
     (if alloc_trace then 30 -> AllocPlace; call RunTraceAlloc; if);
     newTop -> IOATop;
          
     { The new Object is now allocated, but not assigned yet!}
          
     { recalculate theRep, it has been invalidated above }
     push newRange; { save newRange }
     { STACK:
       <newRange/DataReg2>
       <General stack layout>
     } 
     pReg1 -> theItem;
     pReg2 -> offset;
     theItem[offset*4] -> theRep;
     pop newRange; { restore Newrange }
     
     { STACK:
       <General stack layout>
     }
     
     { Initialize the structual part of the repetition. }
     RefRepPTValue   -> newRep.ProtoType;
     (if IOAMinAge<>0 then
         IOAMinAge -> newRep.GCAttr;
     if);
     1                -> newRep.LowBorder;
     newRange         -> newRep.HighBorder;
     
     copyBody: { Copy the body part of theRep to newRep. }
     
     { calculate bodysize of old repetition }
     (if addSize > 0 then			
         theRep.HighBorder -> oldBodySize; { invalidates addSize }
	 goto calcBody				
     if);							
     newRange -> oldBodySize; { in case of addsize <= 0 }
     calcBody:						
     (if oldBodySize > 0 then
         4->oldBodySize*;
         { ObjectAlign -> oldBodySize|; NO, NO, NO! May copy one element too much! }
         4 -> oldBodySize-;
         push newRep;
         newRep[16+oldBodySize][] -> copyCell;  { Cell to copy to }
         (if copyCell outside IOA then  { The copyCell resides in AOA. }
             { Loop which calls ChkRA for each element }
             loop1:
             theRep[16+oldBodySize] -> value;
             value -> copyCell[0];
             (if call_chkra_unconditionally then
                 call ChkRA_ESI;
              else
                 push copyCell;
                 call ChkRA { No need to remove parameter. }
             if);
             4 -> copyCell-;
             4 -> oldBodySize-;
             (if FLAGS >= 0 then goto loop1 if);
             
             goto CopyDone;
         if);
         
         { Loop which does not call ChkRA }
         loop2:
         theRep[16+oldBodySize] -> value;
         value -> copyCell[0];
         4 -> copyCell-;
         4 -> oldBodySize-;
         (if FLAGS >= 0 then goto loop2 if);
         
         CopyDone:
         { Copying of old elements DONE }
         pop newRep;
     if);
     
     AssignReference: 
     { calculate the cell to store new rep in }
     pReg1 -> theItem;
     pReg2 -> offset;
     theItem[offset*4][] -> theCell;
     newRep -> theCell[0];

     (if call_chkra_unconditionally then
         call ChkRA_EBP;
      else
         (if theCell outside IOA then  { The theCell resides in AOA. }
             push theCell;
             call ChkRA { No need to remove parameter. }
         if);
     if);
     
     pop RegAdr; { restore RegAdr }
     pop DataReg2, AddrReg2, AddrReg1 ;

     (if debug then call Ck; if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     4 -> BodySize/;
     BodySize -> ReqObjectSize+;
     pop RegAdr; { Restore RegAdr }
     pop DataReg2, AddrReg2, AddrReg1 ;
     { Stack: as on entry }
     push pReg1; { force update of pReg1 during GC }
     { Stack:
       <pReg1> (Object)
       - data registers are tagged properly.
     }
     call doGC;              { Call the Garbage Collector.               }
     pop pReg1;
     goto again              { Go back to the beginning of this routine. }
  #)

