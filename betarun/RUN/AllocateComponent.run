------- AllocateComponent: System ----
AlloC: public
  (#
     { Allocate an Component. }
     
     { Input variable.  }
     protoType: ProtoType  = RegArg;
     origin:    Object     = DataReg1; {pReg1 in Intel version}
     
     { Output variable. }
     Argument:  DataArea   = RegArg;
     
     { Used variable.   }
     Size:      Word       = DataReg2;
     SavedDR2:  Long       = DataReg2; { needed to save and restore DR2 as long }
     Tmp:       Word       = DataReg1; 
     initTab:   CodeArea   = AddrReg1; { index in GC table of prototype }
     genTab:    CodeArea   = RegArg;
     Entry:     CodeArea   = AddrReg1;
     Index:     Word       = DataReg3; { saved and restored }
     SavedDR3:  Long       = DataReg3; { needed to save and restore DR3 as long }
     newTop:    DataArea   = AddrReg1;
     theComp:   Component  = AddrReg2;
     
     stack:     StackArea = SP;
  do
     (if debug then 1 -> NumAlloC+ if); 
     (if debug then call Ck if);
     
     { 
       General stack layout:
       <Intel: DataReg2>   Intel: +0
       <Intel: AddrReg2>   Intel: +4
       <Intel: AddrReg1>   Intel: +8
       <origin>            Intel: +12   Mac: +0   Default: +0
       <Mac: AddrReg2>                  Mac: +4
       ... 
     }
     
     (if intel then pReg1 -> DataReg1 if);
     
     (if MacOS then push AddrReg2 if);
     push origin; { save (and force GC update of) origin }
     (if intel then push AddrReg1, AddrReg2, SavedDR2 if);
     
     again:
     
     0 -> Size!l;
     protoType.Size -> Size;
     IOATop -> theComp;
     theComp[24+Size*4][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     newTop -> IOATop;
     
     { The new Component is now allocated, but not initialized yet!}
     
     { Initialize the structual part; prototype, age etc. }
     -1 -> theComp.ProtoType;
     1  -> theComp.Age;
     0  -> theComp.StackObj;
     0  -> theComp.CallerObj;
     0  -> theComp.CallerComp;
     0  -> theComp.CallerLSC;
     
     protoType -> theComp[24]!l; { Initialize the item.protoType of the component. }
     -6        -> theComp[28]!l; { Initialize the item.age of the component. }
     
     { Initialize the body part of the item, according to the genTable. }
     
     initTab -> initTab-; {Stupid MC680x0 assembler!}
     protoType.GCTabel -> initTab!w+;
     protoType -> initTab+;
     
     { initTab is now pointing to the static GCTable. }
     { This table has zero or more elements terminated with a zero word. }
     { Each element looks like: }
     { WORD Offset in the generated object, where this static object begins }
     { WORD Signed distance to inclosing object }
     { LONG Prototype of this static object }
     
     push SavedDR3;
     
     2 -> Index; { index in object }
     loop:
     (if Index < Size then
         (if Index = initTab[0]!w then
             2 -> initTab+;
             { Insert the offset to the autonomous object. }
             initTab[0]!w -> Tmp;
             extend Tmp;
             Tmp!l -> theComp[28+Index*4];
             2 -> initTab+;
             { Insert the protoType from the initTab. }
             initTab[0]!l -> Tmp!l; Tmp!l -> theComp[24+Index*4]!l;
             4 -> initTab+;
             2 -> Index+;     
             goto loop
         if);
         0 -> theComp[24+Index*4]!l;
         1 -> Index+;
         goto loop
     if);
     
     protoType.OrigOff -> Index;
     (if intel then 
         stack[16{12+4}] -> origin;
      else
         stack[4{0+4}] -> origin;
     if);
     origin -> theComp[24+Index*4];
     
     pop SavedDR3;
     (if intel then ;
      else 
         pop origin { all but intel }
     if);
     
     protoType.GenPart -> Entry;
     (if Entry=0 then 
         theComp->Argument; 
         goto CleanUp; 
     if);
     
     theComp[24][] -> Argument; { let argument point to the item part }
     (if intel then
         0 -> AddrReg2; {Invalidate GC register}
         push -5; {Protect DataReg2}
     if);
     call Entry;
     (if intel then
         4 -> stack+; {Discard GC protection}
     if);
     Argument[-24][] -> Argument; { make argument point to the comp part }
     
     CleanUp:
     (if intel then
         pop SavedDR2, AddrReg2, AddrReg1;
         4 -> stack+; {Discard origin}
     if);
     (if MacOS then pop AddrReg2 if);
     (if debug then call Ck if);
     return;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.             }
     Size!l -> ReqObjectSize; { Set the requested Object Size.            }
     6      -> ReqObjectSize+;
     (if intel then pop SavedDR2, AddrReg2, AddrReg1 if);
     call doGC;    { Call the Garbage Collector.               }
     (if intel then push AddrReg1, AddrReg2, SavedDR2 if);
     goto again;
  #)
