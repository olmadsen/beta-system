------- CopyValRep: System ----

CopyVR: public
  (#
     { Copy a Value Repetition. }
     { stack on entry [return(0),Offset(4),Item(8),ValRep(12),....] }
     
     stack: StackArea = SP;
     
     { Used variable.  }
     theRep: Repetition  = AddrReg1;
     newTop: DataArea    = AddrReg2;
     newRep: Repetition  = AddrReg2;
     theCell: DataArea   = AddrReg1;
     proto: DataArea     = DataReg2;
     cycleCell: DataArea = AddrReg2;
     
     range:  Long        = DataReg1;
     BodySize: Long      = DataReg1; { number of bytes to allocate }
     tmp:    Long        = DataReg2;
     value:  Long        = DataReg2;
     offset: Long        = DataReg2;
     zero: DataArea      = DataRegB; { saved and restored, NOT used on intel }
  do
     {
       General stack layout:
       <Intel: DataReg2>   Intel: +0
       <Intel: DataReg1>   Intel: +4
       <Intel: AddrReg2>   Intel: +8
       <Intel: AddrReg1>   Intel: +12
       <Mac: AddrReg2>                  Mac: +0    
       <return address>    Intel: +16   Mac: +4    Default: +0
       <offset>            Intel: +20   Mac: +8    Default: +4
       <item>              Intel: +24   Mac: +12   Default: +8
       <valrep>            Intel: +28   Mac: +16   Default: +12
     }
     
     (if MacOS then push AddrReg2 if);
     (if intel then push AddrReg1, AddrReg2, DataReg1, DataReg2 if);
     
     (if intel then
         stack[28] -> theRep;
      else 
         (if MacOS then
             stack[16] -> theRep;
          else
             stack[12] -> theRep;
         if)
     if);
     theRep.HighBorder -> range;
     
     { stack: [return,offset,Item,ValRep...]          }
     { mac:   [AddrReg2,return,offset,Item,ValRep...] }
     
     (if range > 256 then
         (if intel then
             push RegArg, RegObj;
          else
             push RegArg, RegObj, DataRegA, DataRegB;
         if);
         
         { stack: [DataRegB,DataRegA,RegObj,RegArg,return,offset,Item,ValRep...]          }
         { mac:   [DataRegB,DataRegA,RegObj,RegArg,AddrReg2,return,offset,Item,ValRep...] }

         theRep.ProtoType -> proto;
         (if intel then
             0 -> cycleCell;
          else
             0 -> zero; { DataRegB, already saved on stack }
             zero -> cycleCell; { Needed since an address reg cannot be cleared on HP (?) }
         if);
         (if theRep outside IOA then { theRep is in LVRA }
             theRep.Age -> cycleCell { save pointer to cell refering theRep }
         if);
         
         push cycleCell;
         { stack: [cycleCell,DataRegB,DataRegA,RegObj,RegArg,return,offset,Item,ValRep...]          }
         { mac:   [cycleCell,DataRegB,DataRegA,RegObj,RegArg,AddrReg2,return,offset,Item,ValRep...] }

         push range; { Parameter to LVRAAlloc. }
         push proto; { Parameter to LVRAAlloc. }
         call LVRAAlloc;
         stack[8] -> cycleCell; {Restore cycleCell}
         (if cycleCell <> 0 then
             { restore theRep from pointer to cell. The reason for this is that 
               theRep may have been moved by an LVRACompaction 
             }
             cycleCell[0] -> theRep;
             goto endifCycleCell;
         if);
         {Restore theRep, may have been destroyed by LVRAAlloc}
         (if intel then
             stack[48{28+(3+2)*4}] -> theRep
          else
             (if MacOS then
                 stack[44{16+(3+4)*4}] -> theRep
              else
                 stack[40{12+(3+4)*4}] -> theRep
             if);
         if);
         endifCycleCell:
         
         DataRegA -> newRep; {Fetch LVRAAlloc result}
         4 -> SP+; {Discard proto}
         pop range; {Restore range}
         4 -> SP+; {Discard cycleCell}
         
         { stack: [DataRegB,DataRegA,RegObj,RegArg,return,offset,Item,ValRep...]          }
         { mac:   [DataRegB,DataRegA,RegObj,RegArg,AddrReg2,return,offset,Item,ValRep...] }

         (if newRep <> 0 then
             { Make the LVRA-cycle: theCell -> newRep.Age }
             (if intel then
                 stack[28{20+2*4}] -> tmp; 
                 4 -> tmp*;
                 stack[32{24+2*4}] -> tmp+;
              else
                 (if MacOS then
                     stack[24{8+4*4}] -> tmp; 
                     4 -> tmp*;
                     stack[28{12+4*4}] -> tmp+;
                  else
                     stack[20{4+4*4}] -> tmp; 
                     4 -> tmp*;
                     stack[24{8+4*4}] -> tmp+;
                 if);
             if);
             tmp -> newRep.Age
         if);
         (if intel then
             pop RegObj, RegArg;
          else
             pop DataRegB, DataRegA, RegObj, RegArg;
         if);
         
         { stack: [return,offset,Item,ValRep...]          }
         { mac:   [AddrReg2,return,offset,Item,ValRep...] }

         (if newRep <> 0 then
             { range -> BodySize; // no need; it is the same register }
             (if theRep.ProtoType = -5 then { long rep }
                 4 -> BodySize*;
                 goto copyBody
             if);
             (if theRep.ProtoType = -7 then { word rep }
                 2 -> BodySize*;
                 3 -> BodySize+;
                 4 -> BodySize/;
                 4 -> BodySize*;
                 goto copyBody
             if);
             (if theRep.ProtoType = -6 then { byte rep }
                 4 -> BodySize+;
                 4 -> BodySize/;
                 4 -> BodySize*;
                 goto copyBody
             if);
             (if theRep.ProtoType = -8 then { double rep }
                 2 -> BodySize*;
                 4 -> BodySize*;
                 goto copyBody
             if)
         if)
     if);
     
     { stack: [return,offset,Item,ValRep...]          }
     { mac:   [AddrReg2,return,offset,Item,ValRep...] }
     
     IOATop -> newTop;
     { range -> BodySize; // no need; it is the same register }
     (if theRep.ProtoType = -7 then { word rep }
         2 -> BodySize*;
         3 -> BodySize+;
         4 -> BodySize/;
         goto IOAalloc
     if);
     (if theRep.ProtoType = -6 then { byte rep }
         4 -> BodySize+;
         4 -> BodySize/;
         goto IOAalloc
     if);
     (if theRep.ProtoType = -8 then { double rep }
         2 -> BodySize*;
         goto IOAalloc
     if);
     
     
     IOAalloc:
     
     { stack: [return,offset,Item,ValRep...]          }
     { mac:   [AddrReg2,return,offset,Item,ValRep...] }

     4 -> BodySize*;
     newRep[16+BodySize][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     
     IOATop -> tmp;
     newTop -> IOATop;
     tmp    -> newRep;
     
     { The new Object is now allocated, but not assigned yet!}
     
     { Initialize the structual part of the repetition. }
     theRep.ProtoType  -> newRep.ProtoType;
     1                 -> newRep.Age;
     1                 -> newRep.LowBorder;
     theRep.HighBorder -> newRep.HighBorder;
     
     copyBody:
     
     { stack: [return,offset,Item,ValRep...]          }
     { mac:   [AddrReg2,return,offset,Item,ValRep...] }

     (if intel then
         stack[28] -> theRep
      else
         (if MacOS then
             stack[16] -> theRep
          else
             stack[12] -> theRep
         if);
     if);
     
     { Copy the body part of the repetition. }
     (if BodySize <> 0 then
         4 -> BodySize-;
         loop:
         theRep[16+BodySize] -> value;
         value -> newRep[16+BodySize];
         4 -> range-;
         (if FLAGS >= 0 then goto loop if)
     if);
     
     (if intel then
         stack[24] -> theCell;
         stack[20]  -> offset;
      else
         (if MacOS then 
             stack[12] -> theCell;
             stack[8]  -> offset;
          else
             stack[8] -> theCell;
             stack[4] -> offset;
         if);
     if);
     theCell[offset*4][] -> theCell;
     newRep -> theCell[0];
     
     (if theCell outside IOA then  { The theCell resides in AOA. }
         push theCell;
         call ChkRA { No need to remove parameter. }
     if);
     
     { stack: [return,offset,Item,ValRep...]          }
     { mac:   [AddrReg2,return,offset,Item,ValRep...] }

     (if intel then pop DataReg2, DataReg1, AddrReg2, AddrReg1 if);
     (if MacOS then pop AddrReg2 if);
     return 12;
     
     GC:
     { stack: [return,offset,Item,ValRep...]          }
     { mac:   [AddrReg2,return,offset,Item,ValRep...] }

     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     4 -> BodySize/;
     BodySize -> ReqObjectSize+;
     (if intel then pop DataReg2, DataReg1, AddrReg2, AddrReg1 if);
     (if MacOS then pop AddrReg2 if);
     call DoGC;         { Call the Garbage Collector.               }
     goto CopyVR         { Go back to the beginning of this routine. }
  #)
