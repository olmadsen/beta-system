------- CopyValRep: System ----

CopyVR: public
  (#
     { Copy a Value Repetition. }
     { stack on entry [return(0),Offset(4),Item(8),ValRep(12),....] }
     
     stack: StackArea = SP;
     
     { Used variable.  }
     theRep: Repetition = AddrReg1;
     newTop: DataArea   = AddrReg2;
     newRep: Repetition = AddrReg2;
     theCell: DataArea  = AddrReg1;
     
     range:  Long       = DataReg1;
     BodySize: Long     = DataReg1; { number of bytes to allocate }
     tmp:    Long       = DataReg2;
     value:  Long       = DataReg2;
     offset: Long       = DataReg2
  do
     stack[12] -> theRep;
     theRep.HighBorder -> range;
     
     (if MacOS then push AddrReg2 if);
     
     (if range > 200 then
         push RegArg, RegObj, DataRegA, DataRegB;
         push range; { Parameter to LVRA(Byte)Alloc. }
         (if theRep.ProtoType = -8 then { double rep }
             call LVRADoubleAlloc;
             goto cycle
         if);
         (if theRep.ProtoType = -7 then { word rep }
             call LVRAWordAlloc;
             goto cycle
         if);
         (if theRep.ProtoType = -6 then { byte rep }
             call LVRAByteAlloc;
             goto cycle
         if);
         (if theRep.ProtoType = -5 then { long rep }
             call LVRAAlloc;
             goto cycle
         if);
         cycle:
         DataRegA -> newRep;
         (if newRep <> 0 then
             { Make the LVRA-cycle: theCell -> newRep.Age }
             (if MacOS then
                 stack[28 { =  4 + 4 + 5*4 } ] -> DataRegA; 4 -> DataRegA*;
                 stack[32 { =  4 + 8 + 5*4 } ] -> DataRegA+
              else
                 stack[24 { =  4 + 5*4 } ] -> DataRegA; 4 -> DataRegA*;
                 stack[28 { =  8 + 5*4 } ] -> DataRegA+
             if);
             DataRegA -> newRep.Age
         if);  
         pop  range;
         pop  DataRegB, DataRegA, RegObj, RegArg;
         (if newRep <> 0 then goto clearBody if)
     if);
     
     IOATop -> newTop;
     { range -> BodySize; // no need; it is the same register }
     (if theRep.ProtoType = -7 then { word rep }
         2 -> BodySize*;
         3 -> BodySize+;
         4 -> BodySize/;
         goto IOAalloc
     if);
     (if theRep.ProtoType = -6 then { byte rep }
         4 -> BodySize+;
         4 -> BodySize/;
         goto IOAalloc
     if);
     (if theRep.ProtoType = -8 then { double rep }
         2 -> BodySize*;
         goto IOAalloc
     if);
     IOAalloc:
     4 -> BodySize*;
     newRep[16+BodySize][] -> newTop;
     (if newTop > IOALimit then goto GC if);
     
     IOATop -> tmp;
     newTop -> IOATop;
     tmp    -> newRep;
     
     { The new Object is now allocated, but not assigned yet!}
     
     { Initialize the structual part of the repetition. }
     theRep.ProtoType  -> newRep.ProtoType;
     1                 -> newRep.Age;
     1                 -> newRep.LowBorder;
     theRep.HighBorder -> newRep.HighBorder;
     
     clearBody:
     (if MacOS then
         { AddrReg2 in on the stack. }
         stack[16] -> theRep
      else
         stack[12] -> theRep
     if);
     
     { Copy the body part of the repetition. }
     (if BodySize <> 0 then
         4 -> BodySize-;
         loop:
         theRep[16+BodySize] -> value;
         value -> newRep[16+BodySize];
         4 -> range-;
         (if FLAGS >= 0 then goto loop if)
     if);
     
     (if MacOS then 
         stack[12] -> theCell;
         stack[8]  -> offset
      else
         stack[8] -> theCell;
         stack[4] -> offset
     if);
     theCell[offset*4][] -> theCell;
     newRep -> theCell[0];
     
     (if theCell outside IOA then  { The theCell resides in AOA. }
         push theCell;
         call ChkRA { No need to remove parameter. }
     if);
     
     (if MacOS then pop AddrReg2 if);
     return 12;
     
     GC:
     { The IOA heap is full, so we need a garbage collection.            }
     4 -> ReqObjectSize;     { Set the requested Object Size.            }
     4 -> BodySize/;
     BodySize -> ReqObjectSize+;
     (if MacOS then pop AddrReg2 if);
     call DoGC;         { Call the Garbage Collector.               }
     goto CopyVR         { Go back to the beginning of this routine. }
  #)

