------- AttachBasicComponent: System ----
StackStart: cvar;
beta_main: cproc;

_AttBC: public
  (#
     { Attach the basic component. }
     
     { Input variable. }
     theComp: Component = RegCall; { stack[4] on nti (called from beta_main) }
     
     { Used variables. }
     exitAddress: CodeArea = AddrReg1;
     futureObject: Item    = RegCall;
     theObj:       Item   = RegThis;
     stack: StackArea = SP;
     theProto:     ProtoType = AddrReg1;
     startAddress: CodeArea = AddrReg1
  do
     (if debug then
         (if linux then
             (code '	pushal' code);
             call NotifyRTDebug;
             (code '	popal' code);
         if)
     if);
     
     (if nti then
         stack[4] -> theComp;
     if);
     
     (if debug then call Ck if);
     
     stack -> StackStart;
    
     { Push the bottom component block.                      }
     { Terminates the list of component blocks on the stack. }
     push 0;  push 0;  push 0;
     
     0     -> ActiveCallBackFrame;
     stack -> lastCompBlock;
     
     theComp -> BasicItem;  { Save a reference to the betaenv object in BasicItem. } 
     24 -> BasicItem+;      { It is used as origin in MakeTextObj }
    
     TerminateBasicComponent[] -> exitAddress;
     push exitAddress;
     exitAddress -> theComp.CallerLSC;
     
     { Attach the component. }
     theComp -> ActiveComponent;
     
     theComp[24][] -> futureObject;
     
     futureObject.ProtoType -> theProto;
     clear theObj;
     theProto.TopMpart -> startAddress;
     goto startAddress; {AddrRegs ok; stack ok, too}
     
     TerminateBasicComponent:
     (if debug then call Ck if);
     push 0;
     call BetaExit
  #);

_AttBC: proc;

AttBC: public
  (#
     { Input variable. }
     theComp: Component = RegCall;
  do
     (if linux then
         goto _AttBC;
     if);
     (if nti then
         push theComp;
         (code '	push	OFFSET FLAT:_AttBC' code);
         { beta_main will install exception handler for nti, and then }
         { call _AttBC(theComp) }
         call beta_main;
         return;
     if);
  #)

