------- AttachBasicComponent: System ----

{
  nti call chain at startup:
  DllMain                         # ntiadditions.run (only when DLL)
  or WinMain                      # betaenv - compilergenerated
  -> main                         # betaenv - compilergenerated
  -> AttBC(theComp)               # ntiadditions.run
  -> beta_main(_AttBC, theComp)   # sighandler.c
  -> _AttBC(theComp)              # AttachBasicComponent.run
     (returns via TerminateBasicComponent)
}

_AttBC: public
  (#
     { Attach the basic component. }
     
     { Input variable. }
     theComp: Component = RegCall; { stack[4] on nti (called from beta_main) }
     
     { Used variables. }
     exitAddress: CodeArea = AddrReg1;
     futureObject: Item    = RegCall;
     theObj:       Item   = RegThis;
     stack: StackArea = SP;
     theProto:     ProtoType = AddrReg1;
     startAddress: CodeArea = AddrReg1
  do
     { stack:
       0: <return address>
       4: theComp (nti only)
     }
     
     (if debug then
         (if gas then
             PushAll;
             call NotifyRTDebug;
             PopAll;
         if)
     if);
     
     PushAll; { needed to allow correct return in case we are called from DllMain }
     
     { stack:
       0:  <PushAll>
       4:  <PushAll>
       8:  <PushAll>
       12: <PushAll>
       16: <PushAll>
       20: <PushAll>
       24: <PushAll>
       28: <PushAll>
       32: <return address>
       36: theComp (nti only)
     }
     
     (if nti then
         stack[36] -> theComp;
     if);
          
     (if debug then call Ck if);
     
     stack -> StackStart;
    
     { Push the bottom component block.                      }
     { Terminates the list of component blocks on the stack. }
     push 0;  push 0;  push 0;
     
     { stack:
       0:  0
       4:  0
       8:  0
       12: <PushAll>
       16: <PushAll>
       20: <PushAll>
       24: <PushAll>
       28: <PushAll>
       32: <PushAll>
       36: <PushAll>
       40: <PushAll>
       44: <return address>
       48: theComp (nti only)
     }
     
     0     -> ActiveCallBackFrame;
     stack -> lastCompBlock;
     
     theComp -> BasicItem;  { Save a reference to the betaenv object in BasicItem. } 
     24 -> BasicItem+;      { It is used as origin in MakeTextObj }
     
     TerminateBasicComponent[] -> exitAddress;
     push exitAddress;
     exitAddress -> theComp.CallerLSC;
     0 -> theComp.CallerComp;
     0 -> theComp.CallerObj;
     0 -> theComp.StackObj;
     
     { Attach the component. }
     theComp -> ActiveComponent;
     
     theComp[24][] -> futureObject;
     
     futureObject.ProtoType -> theProto;
     clear theObj;
     theProto.TopMpart -> startAddress;
     
     { stack:
       0:  <TerminateBasicComponent>
       4:  0
       8:  0
       12: 0
       16: <PushAll>
       20: <PushAll>
       24: <PushAll>
       28: <PushAll>
       32: <PushAll>
       36: <PushAll>
       40: <PushAll>
       44: <PushAll>
       48: <return address>
       52: theComp (nti only)
     }
     
     { Jump to Top Mpart of theComp. Will later return to TerminateBasicComponent }
     goto startAddress; {AddrRegs ok; stack ok, too}
  #);

TerminateBasicComponent: public
  (#
  do (if debug then call Ck if);
     
     { stack:
       0:  0
       4:  0
       8:  0
       12: <PushAll>
       16: <PushAll>
       20: <PushAll>
       24: <PushAll>
       28: <PushAll>
       32: <PushAll>
       36: <PushAll>
       40: <PushAll>
       44: <return address>
       48: theComp (nti only)
     }   
     12 -> SP+;
     PopAll;
     (if ReturnFromBasicComponent=1 then
         return;
     if);
  Exit:
     push 0;
     call BetaExit
  #)
