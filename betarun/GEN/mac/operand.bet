ORIGIN '../GENERATOR/asmfile'
[[
---- OutConstant:descriptor ----
  (# do '#' -> put; value -> putInt #)

---- ClearRegister:descriptor ----
(# do 'Oops! Called ClearRegister' -> screen.putLine #)

---- OutRegister:descriptor ----
  (# do
    (if size
    // 1 then OutLong[] -> putText
    // 2 then OutWord[] -> putText
    else OutByte[] -> putText
    if);
    (if usedAsIndex//TRUE then
       '.' -> put;
       (if size//1 then 'b' -> put //2 then 'w' -> put //4 then 'l' -> put if)
    if)
  #)

---- OutLabel:descriptor ----
  (# do
    (if subtype//ExternVar then
      '((' -> putText; name[] -> putText; ').L,a5)' -> putText
    else
      name[] -> putText
    if)
  #)

---- OutIndirect:descriptor ----
  (# do
    (if (offset<>0)//true then offset -> putInt if);
    '(' -> put; reg.out; ')' -> put
  #)

---- OutDispIndirect:descriptor ----
  (# tmp:@integer
  do
      (if (offset<>0)//true then
         offset -> putInt;
      if);
      '(' -> put;
      baseRegister.out; ',' -> put;
      dispRegister.usedAsIndex -> tmp;
      true -> dispRegister.usedAsIndex;
      dispRegister.out;
      tmp -> dispRegister.usedAsIndex;
      (if (scale<>1)//true then  '*' -> put; scale -> putInt if);
      ')' -> put
  #)
----]]

