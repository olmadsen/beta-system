ORIGIN '~beta/basiclib/betaenv';
BODY   './translate';
INCLUDE './asmfile';
INCLUDE '~beta/basiclib/formatio';
INCLUDE '~beta/containers/list';
----- Lib: Attributes ----
Control:
  (#
     AsmFile:@AssemblerFile;
     (* Compile debug version of betarun? *)
     Debug: @boolean;
     (* Compile uconditional GC version of betarun? *)
     unconditional_gc: @boolean;
     (* List of conditions to define to true *)
     conditionalNames: @List(# element::Text #);
  do
     blok:
       (#
          Usage:(# 
                do 'USAGE: ' -> putText; 1 -> Arguments -> putText;
                   ' [-debug] [-ugc] [-o <name> ] <filenames>' -> putLine 
                #);
          StartOfFilenames: @integer; 
          aText, OutFileName: ^text
       do
          (if (NoOfArguments <= 1) then Usage; leave blok if);
          2 -> StartOfFilenames;
          
          args: 
            (# 
            do StartOfFilenames -> Arguments -> aText[];    
               (if true 
                // ('-o' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1)//true then Usage; leave blok if);      
                   StartOfFilenames+1 -> Arguments -> OutFileName[]; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-debug' -> aText.equal) then 
                   true -> debug;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-ugc' -> aText.equal) then 
                   true -> unconditional_gc;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;

                // ('-define' -> aText.equal) then 
                   (if (NoOfArguments <= StartOfFilenames+1) then Usage; leave blok if);      
                   StartOfFilenames+1 -> Arguments -> aText[];
		   'Conditionally defining "' ->screen.putText;
		   aText[] -> screen.putText; '"'->screen.putLine;
		   aText.copy -> conditionalNames.append;
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;

               if);
            #);
          
          (if StartOfFilenames//NoOfArguments then else
              Usage; leave blok;
          if);
          
          (if OutFileName[]//NONE then
              AsmFile.Machine -> OutFileName[];
              '-run.s' -> OutFileName.Append;
          if);
          
          OutFileName[] -> AsmFile.Name;
          AsmFile.OpenWrite;
          AsmFile.Prolog; 
          
          StartOfFilenames -> Arguments ->
          (# filename:^text
          enter filename[]
          do <<SLOT Translate:descriptor>> #);
          AsmFile.Epilog;
          AsmFile.Close
       #)
  #);
