ORIGIN '~beta/basiclib/betaenv';
BODY   './translate';
INCLUDE './asmfile';
INCLUDE '~beta/basiclib/formatio';
----- Lib: Attributes ----
Control:
  (#
     AsmFile:@AssemblerFile;
     Debug: @boolean;
     unconditional_gc: @boolean;
  do
     blok:
       (#
          Usage:(# 
                do 'USAGE: ' -> putText; 1 -> Arguments -> putText;
                   ' [-debug] [-ugc] [-o <name> ] <filenames>' -> putLine 
                #);
          StartOfFilenames: @integer; 
          aText, OutFileName: ^text
       do
          (if (NoOfArguments <= 1) then Usage; leave blok if);
          2 -> StartOfFilenames;
          
          args: 
            (# 
            do StartOfFilenames -> Arguments -> aText[];    
               (if true 
                // ('-o' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1)//true then Usage; leave blok if);      
                   StartOfFilenames+1 -> Arguments -> OutFileName[]; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-debug' -> aText.equal) then 
                   true -> debug;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
                // ('-ugc' -> aText.equal) then 
                   true -> unconditional_gc;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
               if);
            #);
          
          (if StartOfFilenames//NoOfArguments then else
              Usage; leave blok;
          if);
          
          (if OutFileName[]//NONE then
              AsmFile.Machine -> OutFileName[];
              '-run.s' -> OutFileName.Append;
              (if debug then
                  'No output file name given - defaulting to %s\n' -> putFormat
                  (#
                  do OutFileName[] -> s;
                  #)
              if)
          if);
          
          OutFileName[] -> AsmFile.Name;
          AsmFile.OpenWrite;
          (if debug then
              'Opened for write: %s\n' -> putFormat
              (#
              do OutFileName[] -> s;
              #)
          if);
          AsmFile.Prolog; 
          (if debug then 'Prolog written to asm file\n' -> putline
          if);
          
          (StartOfFilenames -> Arguments, debug) ->
          (# filename:^text;
             debug:@boolean
          enter (filename[], debug)
          do <<SLOT Translate:descriptor>> #);
          AsmFile.Epilog;
          AsmFile.Close
       #)
  #);
