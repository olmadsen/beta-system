ORIGIN '~beta/basiclib/v1.6/betaenv';
BODY   './translate';
INCLUDE './asmfile';
----- Lib: Attributes ----
Control:
  (#
     AsmFile:@AssemblerFile;
     Debug: @boolean;
  do
     blok:
       (#
          Usage:(# 
                do 'USAGE: ' -> putText; 1 -> Arguments -> putText;
                   ' [-debug] [-o <name> ] <filenames>' -> putLine 
                #);
          StartOfFilenames: @integer; 
          aText, OutFileName: ^text
       do
          (if (NoOfArguments <= 1)//true then Usage; leave blok if);
          2 -> StartOfFilenames;
          
          args: 
            (# 
            do StartOfFilenames -> Arguments -> aText[];    
               (if true 
                // ('-o' -> aText.Equal) then
                   (if (NoOfArguments <= StartOfFilenames+1)//true then Usage; leave blok if);      
                   StartOfFilenames+1 -> Arguments -> OutFileName[]; 
                   StartOfFilenames+2 -> StartOfFilenames;
                   restart args;
                // ('-debug' -> aText.equal) then 
                   true -> debug;
                   StartOfFilenames+1 -> StartOfFilenames;
                   restart args;
               if);
            #);
          
          (if StartOfFilenames//NoOfArguments then else
              Usage; leave blok;
          if);
          
          (if OutFileName[]//NONE then
              AsmFile.Machine -> OutFileName[];
              '-run.s' -> OutFileName.Append;
          if);
          
          OutFileName[] -> AsmFile.Name;
          AsmFile.OpenWrite;
          AsmFile.Prolog; 
          
          StartOfFilenames -> Arguments ->
          (# filename:^text enter filename[] do <<SLOT Translate:descriptor>> #);
          
          AsmFile.Epilog;
          AsmFile.Close
       #)
  #);
