-- runtime: aGrammar : metagrammar --
grammar runtime:

option
  version  = 9
  comBegin = '{'
  comEnd   = '}'
  suffix   = '.run'

rule

<System>      ::*  <Att> ';' ;

<Att> ::|  <Attr> | <ConstDecl> ;
<Attr> ::=  <Name:NameDecl> ':' <Type:NameAppl> <ExtNameOpt> <DescriptorOpt> ;
<ConstDecl>   ::=  <Name:NameDecl> ':' 'const' '=' <value:Number> ;

<ExtNameOpt> ::? <ExtName>;

<ExtName>    ::= '=' <string>;

<DescriptorOpt>     ::? <Descriptor> ;

<Descriptor>   ::= '(#' <Declarations> 'do' <Imperatives> '#)' ;

<Declarations>   ::+ <DeclarationOpt> ';' ;

<DeclarationOpt> ::? <Declaration>;

<Declaration> ::=  <Name:NameDecl> ':' <Type:NameAppl> '=' <Register:NameAppl> ;


<Imperatives> ::+  <ImperativOpt> ';' ;
<ImperativOpt> ::? <Imperativ> ;

<Imperativ>   ::|  <LabelInstruction> | <Instruction> ;

<LabelInstruction> ::= <LabelDef> <Instruction> ;

<LabelDef>    ::=  <Name:NameDecl> ':'  ;

<Instruction> ::|  <Transaction> | <IfImp>  | <GotoImp> | <CallImp> | <LabelImp>
                |  <PushImp>     | <PopImp> | <CodeImp> | <Return>  | <ExtendImp> | <ClearImp> | <PushAll> | <PopAll> ;

<IfImp>     ::=  '(' 'if' <IfExp> 'then' <Imperatives> <ElsePartOpt> 'if' ')' ;

<ElsePartOpt> ::? <ElsePart> ;

<ElsePart>    ::=  'else' <Imperatives> ;

<GotoImp>     ::=  'goto' <Address> ;
<PushImp>     ::=  'push' <AddressList> ;
<PopImp>      ::=  'pop'  <AddressList> ;
<CallImp>     ::=  'call' <Address> ;
<LabelImp>    ::=  'label' <NameAppl> ;
<ExtendImp>   ::=  'extend' <Address> ;
<ClearImp>    ::=  'clear' <Address> ;

<CodeImp>     ::=  '(' 'code' <string>  'code' ')' ;

<PushAll>     ::=  'PushAll' ;
<PopAll>     ::=  'PopAll' ;

<Return>      ::=  'return' <DispOpt>;

<DispOpt>     ::? <Disp>;
<Disp>        ::= <Const>;

<IfExp>       ::| <SystemName>  | <Comp> ;

<Comp>        ::= <Op1:Address> <CompOp>  <Op2:Address> ;

<CompOp>     ::| <EqOp> | <LtOp> | <LeOp> | <GtOp> | <GeOp> | <NeOp>
               | <InOp> | <OutOp> ;

<EqOp>        ::= '=' ;
<LtOp>       ::= '<' ;
<LeOp>       ::= '<=' ;
<GtOp>       ::= '>' ;
<GeOp>       ::= '>=' ;
<NeOp>       ::= '<>' ;
<InOp>       ::= 'inside' ;
<OutOp>      ::= 'outside' ;

<Transaction> ::=  <Source> '->' <Destination> ;

<Source>      ::= <Address> <ReferenceOpt> ;
<Destination> ::= <Address> <OperationOpt> ;

<OperationOpt>::? <Operation> ;

<Operation>   ::| <Plus> | <Minus> | <Division> | <Mult> ;

<Plus>        ::= '+' ;
<Minus>       ::= '-' ;
<Division>    ::= '/' ;
<Mult>        ::= '*' ;


<AddressList>   ::+ <Address> ',' ;

<ReferenceOpt> ::? <Reference> ;

<Reference>   ::= '[]' ;

<Address>     ::|  <AddressNumber> | <Name> | <Remote> | <Indexed> ;

<AddressNumber> ::= <Number> ;

<Name>        ::= <NameAppl> <SizeOpt> ;

<SystemName>  ::= <NameAppl> ;

<Remote>      ::=  <Name:NameAppl> '.' <Extension:NameAppl> <SizeOpt> ;

<Indexed>     ::=  <NameAppl> '['<Indexor> ']' <SizeOpt> ;

<SizeOpt>     ::?  <Size> ;
<Size>        ::|  <ByteSize> | <WordSize> | <LongSize> ;
<ByteSize>    ::=  '!' 'b';
<WordSize>    ::=  '!' 'w';
<LongSize>    ::=  '!' 'l';

<Indexor>     ::|  <IndexNumber> | <IndexRegister> | <IndexNumAndReg> ;

<IndexNumber> ::= <Number> ;

<IndexRegister> ::= <NameAppl> <ScaleOpt> ;

<IndexNumAndReg> ::= <Number> '+' <NameAppl> <ScaleOpt> ;

<ScaleOpt>    ::?  <Scale> ;

<Scale>       ::=  '*' <Const>;

<Number>      ::=  <MinusOpt> <Const>;

<MinusOpt>    ::?  <MinusSign> ;

<MinusSign>   ::=  '-'


