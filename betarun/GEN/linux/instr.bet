ORIGIN '../GENERATOR/asmfile'
[[
---- INCLUDE './nop'
---- INCLUDE './sop'
---- INCLUDE './dop'
---- PrivatePart: descriptor ----
(##)

---- InstrLib:attributes ----
sep:(# do ',' -> put #);
tab:(# do ascii.ht -> put #);
isUsingMemory:
  (# result: @boolean; op:^operand
  enter op[]
  do
     (if op.type
      //LabelType//IndirectType//DispIndirectType
      //ExternVar then
         true -> result
      else
         false -> result
     if)
  exit result
  #);

---- ExtendInstr:descriptor ----
(# 
do 
   (* GNU as/gdb translates 'cwde' to 'orl $0xffff8000, %eax' *)
   4 -> op.size; 'orl\t$0XFFFF8000' -> putText; sep; op.out; 
#)

---- MoveInstr:descriptor ----
(# 
do 
   op2.clear;
   (if ((op1[] -> isUsingMemory) AND (op2[] -> isUsingMemory))//true then
       (* IS THIS SAFE??? Check putInstr comments for warnings!!! *)
       'push' -> putInstr; tab; op1.out; newline; tab; 
       'pop'  -> putInstr; tab; op2.out
    else
       'mov' -> putInstr; op1.out; sep; op2.out
   if)
#)

---- LeaInstr:descriptor ----
(# 
do op2.clear;
   'lea' -> putText; tab; op1.out; sep; op2.out
#)

---- AddInstr:descriptor ----
(# do 'add' -> putInstr; op1.out; sep; op2.out #)

---- SubtractInstr:descriptor ----
(# do 'sub' -> putInstr; op1.out; sep; op2.out #)

---- shiftRightInstr:descriptor ----
(# do 'shr' -> putInstr; op1.out; sep; op2.out #)

---- shiftLeftInstr:descriptor ----
(# do 'shl' -> putInstr; op1.out; sep; op2.out #)

---- DivideInstr:descriptor ----
(# do 'DivideInstr NYI(tm)' -> putText; (* Not used yet! *) #)

----MultiplyInstr:descriptor ----
(# do 'MultiplyInstr NYI(tm)' -> putText; (* Not used yet! *) #)

---- JumpInstr:descriptor ----
(# do 'jmp' -> putText; tab; op.out #)

---- BranchInstr:descriptor ----
(# do 'jmp' -> putText; tab; op.out #)

---- BranchEqualInstr:descriptor ----
(# do 'je' -> putText; tab; op.out #)

---- BranchNotEqualInstr:descriptor ----
(# do 'jne' -> putText; tab; op.out #)

---- BranchLessThanInstr:descriptor ----
(# do 'jl' -> putText; tab; op.out #)

---- BranchLessThanOrEqualInstr:descriptor ----
(# do 'jle' -> putText; tab; op.out #)

---- BranchGreaterThanInstr:descriptor ----
(# do 'jnle' -> putText; tab; op.out #)

---- BranchGreaterThanOrEqualInstr:descriptor ----
(# do 'jge' -> putText; tab; op.out #)

---- BranchCCInstr:descriptor ----
(# do 'jnc' -> putText; tab; op.out #)

---- BranchCSInstr:descriptor ----
(# do 'jc' -> putText; tab; op.out #)

---- CallInstr:descriptor ----
(# do 'call' -> putText; tab; op.out #)

---- PopInstr:descriptor ----
(# do op.clear; 'pop' -> putInstr; op.out #)

---- PushInstr:descriptor ----
(# do 'push' -> putInstr; op.out #)

---- ClearInstr:descriptor ----
(# do op.clear; 'mov' -> putInstr; '$0' -> putText; sep; op.out; #)

---- TestInstr:descriptor ----
(# do 'cmp' -> putInstr; '$0' -> putText; sep; op.out; #)

---- CmpInstr:descriptor ---- 
(# op2I: ^Indirect 
do (* the following is a size fix! Must be verified *)
   (*4->op1; 4->op2;*)
   (if op2.struc//Indirect## then 
       op2[]->op2I[]; 4->op2I.reg
   if);
   'cmp' -> putInstr; op2.out; sep; op1.out 
#)
---- CmpRangeInstr:descriptor ----
(# 
   failLabel, endLabel: ^Label;
do 
   (* 'boundl'-> putText; tab; op1.out; sep; op2.out; -- wrong: calls interrupt 5 *)
   
   (* Outrageously inefficient bound check coming up... *)
   newLocalLabel -> failLabel[];
   newLocalLabel -> endLabel[];
   'cmp' -> putInstr; op2.out; ',' -> putText; op1.out; newline; tab;
   'jb' -> putText; tab; failLabel.name[] -> putText; newline; tab;
   'cmp' -> putInstr; op2.out; '+4,' -> putText; op1.out; newline; tab;
   'jae\t' -> putText; failLabel.name[] -> putText; newline; tab;
   'clc' -> putText; newline; tab;
   'jmp' -> putText; tab; endLabel.name[] -> putText; newline;
   failLabel.name[] -> putText; ':' -> putText; tab; 'stc' -> putText; newline;
   endlabel.name[] -> putText; ':' -> putText;
#)

---- Return:descriptor ----
(# do 'ret' -> putText #)

---- ReturnDisp:descriptor ----
(# do 'ret' -> putText; tab; '$' -> put; disp->putInt  #)
 
---- OutCode:descriptor ----
(# do line[] -> putText; newLine #)

----]]
