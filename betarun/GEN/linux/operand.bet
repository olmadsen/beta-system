ORIGIN '../GENERATOR/asmfile'
[[
---- OutConstant:descriptor ----
(# do '$'->put; value -> putInt #)

---- OutRegister:descriptor ----
(# do
   (if size
    // 1 then  outByte[] -> putText 
    // 2 then  outWord[] -> putText 
    // 4 then  outLong[] -> putText
    else 'OutRegister: wrong size specified' -> putText
   if)
#)

---- ClearRegister:descriptor ----
(# s: @integer
do size -> s; 
   4 -> size; 
   'xorl\t' -> putText; out; ','->put; out;
   s -> size;
#)

---- OutLabel:descriptor ----
(# do name[] -> putText #)

---- OutIndirect:descriptor ----
(# do 
   (if offset <> 0 // true then offSet -> putInt if); 
   '(' -> put; reg.out; ')' -> put 
#)

---- OutDispIndirect:descriptor ----
(# tmpSize: @integer;
do 
   (if offset <> 0 // true then offset->putInt if);
   '('->put; 
   baseRegister.out; 
   ',' -> put;
   
   (* Quick hack - base and disp sizes must match. *)
   dispRegister.size -> tmpSize;
   4 -> dispRegister.Size;
   dispRegister.out;
   tmpSize -> dispRegister.Size;
   (if tmpSize<>4 // true then '/* QH - ok? */' -> putText if);
   
   (if scale <> 1 // true then ',' -> put; scale -> putInt if);
   ')'->put
#)
----]]
