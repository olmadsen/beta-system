ORIGIN '../classification';
INCLUDE 'tstenv'
--- program: descriptor ---
tstenv
(# tests::< (# do 'abcdefghijklmnopqrstuvwxyzABCDEFGHI'->value #);
   
   intClassificationSet: classificationSet
     (# element::< integerObject #);
   recPrint:
     (# class: ^intClassificationSet
     enter class[]
     do class.scanUnclassified(# do current->putint; ','->put #);
        class.subsets.scan(# do '{'->put; current[]->recPrint; '}'->put #);
     #);
   intClassification1: @intClassificationSet;
   intClassification2: ^intClassificationSet;
   intClassification3: @intClassificationSet;
   i: [14]^integerObject;
   
do (for int:14 repeat &integerObject[]->i[int][]; int->i[int] for);
   intClassification1.init;
   &intClassificationSet[]->intClassification2[]; intClassification2.init;
   intClassification3.init;
   
   ('a','1,2,3,4,5,6,')
     ->tst(# do
             (for int:6 repeat
                  int->putint; ','->put;
                  i[int][]->intClassification1.insert
             for)
          #);
   
    ('b','6')
     ->tst(# do
             intClassification1.size->putint
          #);
   
   ('c','6,5,4,3,2,1,')
     ->tst(# do
             intClassification1.scan(# do current->putint; ','->put #)
          #);
   
   
   ('d','9,10,11,12,13,14,')
     ->tst(# do
             (for int:6 repeat 
                  int+8->putint; ','->put;
                  i[int+8][]->intClassification2.insert
             for)
          #);
   
   ('e','6')
     ->tst(# do
             intClassification2.size->putint
          #);
   
   ('f','14,13,12,11,10,9,')
     ->tst(# do
             intClassification2.scan(# do current->putint; ','->put #)
          #);
   
   ('g','5,6,7,8,9,10,')
     ->tst(# do
             (for int:6 repeat 
                  int+4->putint; ','->put;
                  i[int+4][]->intClassification3.insert
             for)
          #);
   
   ('h','6')
     ->tst(# do
             intClassification3.size->putint
          #);
   
   ('i','10,9,8,7,6,5,')
     ->tst(# do
             intClassification3.scan(# do current->putint; ','->put #)
          #);
   
   ('j','8')
     ->tst(# do
             intClassification3.find(# predicate ::<
                                         (# do (current=8)->value #)
      	                            do current->putint #)
          #);
   
   ('k','6,5,3,2,1,')
     ->tst(# do
             i[4][]->intClassification1.delete;
             intClassification1.scan(# do current->putint; ','->put #)
          #);
   
   ('l','yes')
     ->tst(# do
             (if i[3][]->intClassification1.has//true
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('m','no')
     ->tst(# do
             (if i[3][]->intClassification3.has//true
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('n','8,7,6,5,14,13,12,11,10,9,')
     ->tst(# do
             intClassification2[]->intClassification3.insertSubset;
             intClassification3.scan(# do current->putint; ','->put #)
          #);
   
   ('o','8,7,6,5,{14,13,12,11,10,9,}')
     ->tst(# do
             intClassification3[]->recPrint
          #);
   
   ('p','3,2,1,8,7,6,5,14,13,12,11,10,9,')
     ->tst(# do
             intClassification3[]->intClassification1.insertSubset;
             intClassification1.scan(# do current->putint; ','->put #)
          #);
   
   ('q','3,2,1,{8,7,6,5,{14,13,12,11,10,9,}}')
     ->tst(# do
             intClassification1[]->recPrint
          #);
   
   ('r','3,2,1,')
     ->tst(# do
             intClassification1.scanUnclassified(# do current->putint; ','->put #)
          #);

   ('s','14,13,12,11,10,9,')
     ->tst(# do
             intClassification2.scanUnclassified(# do current->putint; ','->put #)
          #);
   
   ('t','8,7,6,5,')
     ->tst(# do
             intClassification3.scanUnclassified(# do current->putint; ','->put #)
          #);
   
   ('u','yes')
     ->tst(# do
             (if i[3][]->intClassification1.has//true 
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('v','no')
     ->tst(# do
             (if i[3][]->intClassification3.has//true
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('w','13')
     ->tst(# do
             intClassification1.size->putint
          #);
   
   ('x','10')
     ->tst(# do
             intClassification3.size->putint
          #);
   
   ('y','3,8,7,6,5,14,13,12,11,10,9,')
     ->tst(# do
             i[3][]->intClassification3.insert;
             intClassification3.scan(# do current->putint; ','->put #)
          #);
   
   ('z','13')
     ->tst(# do
             intClassification1.size->putint
          #);

   ('A','11')
     ->tst(# do
             intClassification3.size->putint
          #);
   
   ('B','yes')
     ->tst(# do
             (if i[3][]->intClassification1.has//true
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('C','yes')
     ->tst(# do
             (if i[3][]->intClassification3.has//true
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('D','2,1,8,7,6,5,14,13,12,11,10,9,')
     ->tst(# do
             i[3][]->intClassification1.delete;
             intClassification1.scan(# do current->putint; ','->put #)
          #);
   
   ('E','no')
     ->tst(# do
             (if i[3][]->intClassification3.has//true
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('F','1,8,7,6,5,14,13,12,11,10,9,')
     ->tst(# do
             i[2][]->intClassification1.delete;
             intClassification1.scan(# do current->putint; ','->put #)
          #);
      
   ('G','1,8,7,6,5,14,13,12,11,10,9,')
     ->tst(# do
             intClassification1.copy->intClassification2[];
             intClassification2.scan(# do current->putint; ','->put #)
          #);
   
   ('H','1,{8,7,6,5,{14,13,12,11,10,9,}}')
     ->tst(# do
             intClassification2[]->recPrint
          #);

   ('I','11')
     ->tst(# do
             intClassification2.size->putint
          #);
#)
