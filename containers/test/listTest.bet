ORIGIN '../list';
INCLUDE 'tstenv'
--- program: descriptor ---
tstenv
(# tests::< (# do 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJK'->value #);
   
   intList: list(# element::< integerObject #);
   intList1, intList2: @intList; intList3: ^intList;
   listPosition: ^intList3.theCellType;
   i: [10]^integerObject;
   
do (for int:10 repeat &integerObject[]->i[int][]; int->i[int] for);
   intList1.init;
   intList2.init;
   &intList[]->intList3[]; intList3.init;
   
   ('a','1,2,3,4,')
     ->tst(# do
             (for int:4 repeat
                  int->putint; ','->put;
                  i[int][]->intList1.prepend
             for)
          #);
   
   ('b','4,3,2,1,')
     ->tst(# do
             intList1.scan(# do current->putint; ','->put #)
          #);
   
   ('c','4,5,6,7,')
     ->tst(# do
             (for int:4 repeat 
                  int+3->putint; ','->put;
                  i[int+3][]->intList2.append
             for)
          #);
   
   ('d','4,5,6,7,')
     ->tst(# do
             intList2.scan(# do current->putint; ','->put #)
          #);
   
   ('e','7,8,9,10,')
     ->tst(# do
             (for int:4 repeat 
                  int+6->putint; ','->put;
                  i[int+6][]->intList3.prepend
             for)
          #);
   
   ('f','10,9,8,7,')
     ->tst(# do
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('g','4,3,2,1,')
     ->tst(# do
             intList1.copy->intList3[];
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('h','4,5,6,7,4,3,2,1,')
     ->tst(# do
             intList1[]->intList2.concatenate->intList3[];
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('i','7')
     ->tst(# do
             intList3.find(# predicate ::<
                               (# do (current=7)->value #)
	                  do current->putint
	                  #)
          #);
   
   ('j','yes')
     ->tst(# do
             (if i[7][]->intList3.has//true then 'yes'->puttext else 'no'->puttext if)
          #);
   
   ('k','no')
     ->tst(# do
             (if i[10][]->intList3.has//true then 'yes'->puttext else 'no'->puttext if)
          #);
   
   ('l','4,5,6,4,4,3,2,1,')
     ->tst(# do
             i[7][]->intList3.at->listPosition[];
             i[4][]->listPosition.elm[];
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('m','4,5,6,7,4,3,2,1,')
     ->tst(# do
             i[7][]->listPosition.elm[];
             intList3.scan(# do current->putint; ','->put #)
          #);               
   
   ('n','4,5,7,4,3,2,1,')
     ->tst(# do
             intList3.scan(# do (if current//6 then current[]->intList3.at->intList3.delete
                       else current->putint; ','->put
                      if) 
                #)
          #);
   
   ('o','1,3,4,7,5,4,')
     ->tst(# do
             intList3.scanReverse(# do (if current//2 then current[]->intList3.at->intList3.delete
                       else current->putint; ','->put
                      if) 
                #)
          #);
   
   ('p','7,4,3,1,')
     ->tst(# do
             listPosition[]->intList3.scanFrom(# do current->putint; ','->put #)
          #);
   
   ('q','7,5,4,')
     ->tst(# do
             listPosition[]->intList3.scanReverseFrom(# do current->putint; ','->put #)
          #);
   
   ('r','4,5,7,4,3,1,')
     ->tst(# do
             intList3.iterate(# do current.elm->putint; ','->put #)
          #);
   
   ('s','1,3,4,7,5,4,')
     ->tst(# do
             intList3.iterateReverse(# do current.elm->putint; ','->put #)
          #);
   
   ('t','7,4,3,1,')
     ->tst(# do
             listPosition[]->intList3.iterateFrom(# do current.elm->putint; ','->put #)
          #);
   
   ('u','7,5,4,')
     ->tst(# do
             listPosition[]->intList3.iterateReverseFrom(# do current.elm->putint; ','->put #)
          #);
   
   ('v','4,5,10,7,10,4,3,1,')
     ->tst(# do
             (i[10][],listPosition[])->intList3.insertBefore;
             (i[10][],listPosition[])->intList3.insertAfter;
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('w','4,5,7,10,4,3,1,')
     ->tst(# do
             listPosition[]->intList3.deleteBefore;
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('x','4,5,7,4,3,1,')
     ->tst(# do
             listPosition[]->intList3.deleteAfter;
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('y','4')
     ->tst(# do
             intList3.head->listPosition[];
             listPosition.elm->putint
          #);

   ('z','5,7,4,3,1,')
     ->tst(# do
             intList3.tail->intList3[]; 
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('A','5,7,4,3,1,1,')
     ->tst(# do
             i[1][]->intList3.append;
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('B','4,5,7,')
     ->tst(# do
             i[6][]->intList2.at->intList2.delete;
             intList2.scan(# do current->putint; ','->put #)
          #);
   
   ('C','7,5,4,')
     ->tst(# do
             intList2.scanReverse(# do current->putint; ','->put #)
          #);
   
   ('D','7')
     ->tst(# do
             intList2.last->listPosition[];
             listPosition.elm->putint
          #);
   
   ('E','4,5,')
     ->tst(# do
             intList2.preample->intList3[];
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('F','4,')
     ->tst(# do
             intList3.clear;
             i[4][]->intList3.append->listPosition[];
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('G','')
     ->tst(# do
             listPosition[]->intList3.delete;
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('H','5,')
     ->tst(# do
             i[5][]->intList3.append->listPosition[];
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('I','')
     ->tst(# do
             NONE->intList3.deleteBefore;
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('J','6,')
     ->tst(# do
             i[6][]->intList3.append->listPosition[];
             intList3.scan(# do current->putint; ','->put #)
          #);
   
   ('K','')
     ->tst(# do
             NONE->intList3.deleteAfter;
             intList3.scan(# do current->putint; ','->put #)
          #);
#)
