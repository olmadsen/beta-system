ORIGIN '../arrayContainer';
INCLUDE 'tstenv'
--- program: descriptor ---
tstenv
(# tests::< (# do 'abcdefghijklmnopqr'->value #);
   
   intArrayContainer: arrayContainer
     (# element::< integerObject;
        capacityInitial::< (# do 10->value; INNER #);
        less::< (# do
                   (if NONE
                    //right[] then true->value
                    //left[] then false->value
                    else (left<right)->value if)
                #)
     #);
   arrayCon1: @intArrayContainer
     (# capacityInitial::< (# do 6->value #);
        capacityIncrement::< (# do 6->value #)
     #);
   arrayCon2: ^intArrayContainer;
   i: [20]^integerObject; io: ^integerObject;
   
do (for int:20 repeat &integerObject[]->i[int][]; int->i[int] for);
   
   arrayCon1.init;
   &intArrayContainer[]->arrayCon2[]; arrayCon2.init;
                
   
   ('a','18,17,16,15,14,13,')
     -> tst(#
           do (for int:6 repeat
                   19-int->putint; ','->put;
                   (i[19-int][],int)->arrayCon1.put
              for)
           #);
   
   ('b','12')
     -> tst(# do arrayCon1.capacityExtend; arrayCon1.capacity->putint #);
   
   ('c','12,11,10,9,')
     -> tst(#
           do (for int:4 repeat
                   13-int->putint; ','->put;
                   (i[13-int][],int+6)->arrayCon1.put
              for)
           #);

   ('d','18,17,16,15,14,13,12,11,10,9,')
     -> tst(#
           do arrayCon1.scan(# do current->putint; ','->put #)
           #);

   ('e','12')
     -> tst(#
           do arrayCon1.capacity->putint
           #);

   ('f','10')
     -> tst(#
           do arrayCon1.size->putint
           #);
   
   
   ('g','1,2,3,4,5,6,7,8,9,10,')
     -> tst(#
           do (for int:10 repeat 
                   int->putint; ','->put;
                   (i[int][],int)->arrayCon2.put
              for)
           #);
   
   ('h','1,2,3,4,5,6,7,8,9,10,')
     -> tst(#
           do arrayCon2.scan(# do current->putint; ','->put #)
           #);
   
   ('i','18,17,16,15,14,13,12,11,10,9,')
     -> tst(#
           do arrayCon1.copy->arrayCon2[];
              arrayCon2.scan(# do current->putint; ','->put #)
           #);

   ('j','18,17,16,15,14,13,12,11,10,9,')
     -> tst(#
           do arrayCon2.clear; arrayCon1->arrayCon2;
              arrayCon2.scan(# do current->putint; ','->put #)
           #);

   ('k','12')
     -> tst(#
           do 7->arrayCon2.get->io[]; io->putint
           #);
   
   ('l','9')
     -> tst(#
           do 10->arrayCon2.get->io[]; io->putint
           #);
   
   ('m','9')
     -> tst(#
           do arrayCon2.find(# predicate ::<
                                 (# do (current=9)->value #)
      	                    do current->putint
      	                    #)
           #);
   
   ('n','no')
     -> tst(#
           do (if i[4][]->arrayCon2.has//true
                  then 'yes'->puttext
               else 'no'->puttext
              if)
           #);
   
   ('o','yes')
     -> tst(#
           do (if i[16][]->arrayCon2.has//true
                  then 'yes'->puttext
               else 'no'->puttext
              if)
           #);
   
   ('p','9,10,11,12,13,14,15,16,17,18,')
     -> tst(#
           do arrayCon1.copy->arrayCon2[]; 12->arrayCon2.bubbleSort;
              arrayCon2.scan(# do current->putint; ','->put #)
           #);
   
   ('q','9,10,11,12,13,14,15,16,17,18,')
     -> tst(#
           do arrayCon1.copy->arrayCon2[]; 12->arrayCon2.shellSort;
              arrayCon2.scan(# do current->putint; ','->put #)
           #);
   
   ('r','9,10,11,12,13,14,15,16,17,18,')
     -> tst(#
           do arrayCon1.copy->arrayCon2[]; 10->arrayCon2.quickSort;
              arrayCon2.scan(# do current->putint; ','->put #)
           #);
#)
