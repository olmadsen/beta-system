ORIGIN '../seqContainers';
INCLUDE 'tstenv'
--- program: descriptor ---
tstenv
(# tests::< (# do 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLM'->value #);
   
   intStack: @stack(# element ::< integerObject #);
   intQueue: @queue(# element ::< integerObject #);

   intPrioQueue: PrioQueue(# element ::< integerObject #);
   intPrioQ: @intPrioQueue;
   intPrioQref: ^intPrioQueue; 

   intDeque: @deque(# element ::< integerObject #); 
   i: [10]^integerObject; io: ^integerObject;
   
do (for int:10 repeat &integerObject[]->i[int][]; int->i[int] for);
   intStack.init;
   intQueue.init;
   intPrioQ.init; 
   intDeque.init;
   
   (************ STACK **********************)
   
   'intStack.push: '->puttext;
   ('a','1,2,3,4,')
     ->tst(# do
             (for int:4 repeat 
                  int->putint; ','->put;
                  i[int][]->intStack.push
             for)
          #);
   
   ('b','4,3,2,1,')
     ->tst(# do
             intStack.scan(# do current->putint; ','->put #)
          #);
   
   ('c','4')
     ->tst(# do
             intStack.top->io[]; io->putint
          #);
   
   ('d','4,3,2,1,')
     ->tst(# do
             (for int:4 repeat intStack.pop->io[]; io->putint; ','->put for)
          #);
   
   ('e','')
     ->tst(# do
             intStack.scan(# do current->putint; ','->put #)
          #);
   
   (************ QUEUE **********************)
   
   ('f','4,5,6,7,')
     ->tst(# do
             (for int:4 repeat 
                  int+3->putint; ','->put;
                  i[int+3][]->intQueue.insert
             for)
          #);
   
   ('g','4,5,6,7,')
     ->tst(# do
             intQueue.scan(# do current->putint; ','->put #)
          #);
   
   ('h','4')
     ->tst(# do
             intQueue.front->io[]; io->putint
          #);
   
   ('i','4,5,6,7,')
     ->tst(# do
             (for int:4 repeat intQueue.remove->io[]; io->putint; ','->put for)
          #);
   
   ('j','')
     ->tst(# do
             intQueue.scan(# do current->putint; ','->put #)
          #);
   
   (************ PRIOQUEUE **********************)

   ('k','(1:p:2),(2:p:1),(3:p:3),(4:p:2),(5:p:1),(6:p:3),(7:p:2),(8:p:1),(9:p:3),(10:p:2),')
     ->tst(# do
             (for int:10 repeat 
                  '('->put;int->putint;':p:'->puttext;(3-(int mod 3))->putint;')'->put; ','->put;
                  (i[int][],(3-(int mod 3)))->intPrioQ.insert
             for)
          #);
   
   ('l','3,6,9,2,5,8,1,4,7,10,')
     ->tst(# do
             intPrioQ.scan(# do current->putint; ','->put #)
          #);
   
   ('m','1: 2,5,8,')
     ->tst(# do
             '1: '->puttext;
             1->intPrioQ.scanPriority(# do current->putint; ','->put #)
          #);
   
   ('n','2: 1,4,7,10,')
     ->tst(# do
             '2: '->puttext;
             2->intPrioQ.scanPriority(# do current->putint; ','->put #)
          #);
   
   ('o','3: 3,6,9,')
     ->tst(# do
             '3: '->puttext;
             3->intPrioQ.scanPriority(# do current->putint; ','->put #)
          #);
   
   ('p','2')
     ->tst(# do
             1->intPrioQ.front->io[]; io->putint
          #);
   
   ('q','1')
     ->tst(# do
             2->intPrioQ.front->io[]; io->putint
          #);
   
   ('r','3')
     ->tst(# do
             3->intPrioQ.front->io[]; io->putint
          #);
   
   ('s','(1:p:2),(2:p:1),(3:p:3),')
     ->tst(# do
             (for int:3 repeat 
                  '('->put; int->putint;
                  int->intPrioQ.remove->io[];
                  ':p:'->puttext; io->putint;
                  ')'->put; ','->put
             for)
          #);
   
   ('t','6,9,5,8,4,7,10,')
    ->tst(# do
             intPrioQ.scan(# do current->putint; ','->put #)
          #);
 
   ('u','7')
     ->tst(# do
             intPrioQ.size->putint
          #);
   
   ('v','1: 5,8,')
     ->tst(# do
             '1: '->puttext;
             1->intPrioQ.scanPriority(# do current->putint; ','->put #)
          #);
   
   ('w','2: 4,7,10,')
     ->tst(# do
             '2: '->puttext;
             2->intPrioQ.scanPriority(# do current->putint; ','->put #)
          #);
   
   ('x','3: 6,9,')
     ->tst(# do
             '3: '->puttext;
             3->intPrioQ.scanPriority(# do current->putint; ','->put #)
          #);
   
   ('y','2: 4,7,10,')
     ->tst(# do
             '2: '->puttext;
             2->intPrioQ.scanPriority(# where::<(# do (current<5)->value #)
			             do current->putint; ','->put #)
          #);
   
   ('z','4,7,10,5,8,6,9,')
     ->tst(# do
             intPrioQ.copy->intPrioQref[];
             intPrioQref.scan(# do current->putint; ','->put #)
          #);
   
   ('A','1: 5,8,')
     ->tst(# do
             '1: '->puttext;
             1->intPrioQref.scanPriority(# do current->putint; ','->put #)
          #);
   
   ('B','2: 4,7,10,')
     ->tst(# do
             '2: '->puttext;
             2->intPrioQref.scanPriority(# do current->putint; ','->put #)
          #);
   
   ('C','3: 6,9,')
     ->tst(# do
             '3: '->puttext;
             3->intPrioQref.scanPriority(# do current->putint; ','->put #)
          #);
   
   
   ('D','1: 5,8, 2: 4,7,10, 3: 6,9,')
     ->tst(# e: ^intPrioQref.element 
          do '1: '->puttext;
             (for 2 repeat
                  1->intPrioQref.remove->e[]; e->putint; ','->put
             for);
   
             ' 2: '->puttext;
             (for 3 repeat
                  2->intPrioQref.remove->e[]; e->putint; ','->put
             for); 
   
             ' 3: '->puttext;
             (for 2 repeat
                  3->intPrioQref.remove->e[]; e->putint; ','->put
             for)
          #);

   (************ DEQUE **********************)

   ('E','1,2,3,4,')
     ->tst(# do
             (for int:4 repeat 
                  int->putint; ','->put;
                  i[int][]->intDeque.insertFront
             for)
          #);
   
   ('F','7,8,9,10,')
     ->tst(# do
             (for int:4 repeat 
                  int+6->putint; ','->put;
                  i[int+6][]->intDeque.insertBack
             for)
          #);
   
   ('G','4,3,2,1,7,8,9,10,')
     ->tst(# do
             intDeque.scan(# do current->putint; ','->put #)
          #);
   
   ('H','8')
     ->tst(# do
             intDeque.size->putint
          #);
   
   ('I','4')
     ->tst(# do
             intDeque.front->io[]; io->putint
          #);
   
   ('J','10')
     ->tst(# do
             intDeque.back->io[]; io->putint
          #);
   
   ('K','4,3,2,1,')
     ->tst(# do
             (for int:4 repeat intDeque.removeFront->io[]; io->putint; ','->put for)
          #);
   
   ('L','10,9,8,7,')
     ->tst(# do
             (for int:4 repeat intDeque.removeBack->io[]; io->putint; ','->put for)
          #);
   
   ('M','')
     ->tst(# do
             intDeque.scan(# do current->putint; ','->put #)
   #);

#)
