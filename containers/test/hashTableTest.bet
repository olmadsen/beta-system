ORIGIN '../hashTable';
INCLUDE 'tstenv'
--- program: descriptor ---
tstenv
(# tests::< (# do 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLM'->value #);
   
   intHashTable: hashTable
     (# element::< integerObject;
        hashFunction::< (# do e->value #);
     #);
   intEHashTable: extensibleHashTable
     (# element::< integerObject;
        hashFunction::< (# do e->value #);
     #);
   hashtable1: @intHashTable(# rangeInitial::< (# do 2->value #) #);
   hashtable2: @intHashTable(# rangeInitial::< (# do 4->value #) #);
   hashtable3: ^intHashTable;
   hashtable4: @intEHashTable(# rangeInitial::< (# do 2->value #) #);
   i: [10]^integerObject;
   
do (for int: 10 repeat &integerObject[]->i[int][]; int->i[int] for);
   
   hashtable1.init;
   hashtable2.init;
   &intHashTable[]->hashtable3[]; hashtable3.init;
   hashtable4.init;
   
   ('a','-1')
     ->tst(# do
             hashtable1.capacity->putInt
          #);
   
   ('b','2')
     ->tst(# do
             hashtable1.range->putInt
          #);
   
   ('c','1,2,3,4,')
     ->tst(# do
             (for int: 4 repeat
                  int->putint; ','->put;
                  i[int][]->hashtable1.insert
             for)
          #);
   
   ('d','3,1,4,2,')
     ->tst(# do
             hashtable1.scan(# do current->putint; ','->put #)
          #);
   
   ('e','4')
     ->tst(# do
             hashtable1.size->putInt
          #);
   
   ('f','-1')
     ->tst(# do
             hashtable2.capacity->putInt
          #);
   
   ('g','4')
     ->tst(# do
             hashtable2.range->putInt
          #);
   
   ('h','4,5,6,7,')
     ->tst(# do
             (for int: 4 repeat 
                  int+3->putint; ','->put;
                  i[int+3][]->hashtable2.insert
             for)
          #);
   
   ('i','5,6,7,4,')
     ->tst(# do
             hashtable2.scan(# do current->putint; ','->put #)
          #);
   
   ('j','4')
     ->tst(# do
             hashtable2.size->putInt
          #);
   
   ('k','-1')
     ->tst(# do
             hashtable3.capacity->putInt
          #);
   
   ('l','25')
     ->tst(# do
             hashtable3.range->putInt
          #);
   
   ('m','7,8,9,10,')
     ->tst(# do
             (for int: 4 repeat 
                  int+6->putint; ','->put;
                  i[int+6][]->hashtable3.insert
             for)
          #);
   
   ('n','7,8,9,10,')
     ->tst(# do
             hashtable3.scan(# do current->putint; ','->put #)
          #);
   
   ('o','4')
     ->tst(# do
             hashtable1.size->putInt
          #);
   
   ('p','1,3,2,4,')
     ->tst(# do
             hashtable1.copy->hashtable3[];
             hashtable3.scan(# do current->putint; ','->put #)
          #);
   
   ('q','1,2,4,')
     ->tst(# do
             i[3][]->hashtable3.delete;
             hashtable3.scan(# do current->putint; ','->put #)
          #);
   
   ('r','1,5,2,6,3,7,4,4,')
     ->tst(# do
             hashtable2.copy->hashtable3[];
             hashtable1[]->hashtable3.union->hashtable3[];
             hashtable3.scan(# do current->putint; ','->put #)
          #);
  
   ('s','5,6,7,')
     ->tst(# do
             hashtable2.copy->hashtable3[];
             hashtable1[]->hashtable3.diff->hashtable3[];
             hashtable3.scan(# do current->putint; ','->put #)
          #);
   
   ('t','4,')
     ->tst(# do
             hashtable2.copy->hashtable3[];
             hashtable1[]->hashtable3.sect->hashtable3[];
             hashtable3.scan(# do current->putint; ','->put #)
          #);
  
   ('u','1,5,2,6,3,7,4,')
     ->tst(# do
             hashtable2.copy->hashtable3[];
             hashtable1[]->hashtable3.symDiff->hashtable3[];
             hashtable3.scan(# do current->putint; ','->put #)
          #);
  
   ('v','4')
     ->tst(# do
             hashtable3.find
             (# predicate::< (# do (current=4)->value #)
             do current->putint
             #)
          #);
   
   ('w','1,5,2,6,3,7,4,')
     ->tst(# do
             hashtable3.scan(# do current->putint; ','->puttext#)
          #);
   
   ('x','4')
     ->tst(# do
             4->hashtable1.findIndexed
             (# predicate::< (# do (current=4)->value #)
             do current->putint
             #)
          #);
   
   ('y','yes')
     ->tst(# do
             (if i[4][]->hashtable3.has // true
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('z','no')
     ->tst(# do
             (if i[10][]->hashtable3.has // true
                 then 'yes'->puttext
              else 'no'->puttext
             if)
          #);
   
   ('A','-1')
     ->tst(# do
             hashtable4.capacity->putInt
          #);
   
   ('B','2')
     ->tst(# do
             hashtable4.range->putInt
          #);
   
   ('C','4,5,6,7,')
     ->tst(# do
             (for int: 4 repeat 
                  int+3->putint; ','->put;
                  i[int+3][]->hashtable4.insert
             for)
          #);
   
   ('D','7,5,6,4,')
     ->tst(# do
             hashtable4.scan(# do current->putint; ','->put #)
          #);
 
   ('E','4')
     ->tst(# do
             hashtable4.size->putInt
          #);
   
   ('F','Histogram: (2,2)
Maximum Collisions: 2
Minimum Collisions: 2
Average Collisions: 2
')
     ->tst(# do
             hashtable4.statistics(# do t[]->print #)
          #);
   
   ('G','1: 7 5 found
2: 6 4 found
')
     ->tst(# do
             (for int: hashTable4.range repeat 
                  int->putint; ': '->puttext;
                  i[int][]->hashTable4.hashFunction
                    ->hashtable4.scanIndexed(# do current->putint; ' '->put #);
                  'found'->putline;
             for)
          #);
  
   ('H','1: no
2: no
3: no
4: yes
5: yes
6: yes
7: yes
8: no
9: no
10: no
')
     ->tst(# do
             3->hashTable4.extend(# do hashTable4.rehash #);
             (for int: 10 repeat 
                  int->putint; ': '->puttext;
                  (if i[int][]->hashtable4.has // true
                      then 'yes'->putline
                   else 'no'->putline
                  if);
             for)
          #);
   
   ('I','1: 6 found
2: 7 found
3: found
4: 4 found
5: 5 found
')
     ->tst(# do
             (for int: hashTable4.range repeat 
                  int->putint; ': '->puttext;
                  i[int][]->hashTable4.hashFunction
                    ->hashtable4.scanIndexed(# end::< (# do 'found'->putline #)
                                            do current->putint; ' '->put
                                            #);
             for)
          #);
   
   ('J','5')
     ->tst(# do
             hashtable4.range->putInt
          #);
   
   ('K','6,7,4,5,')
     ->tst(# do
             hashtable4.scan(# do current->putint; ','->put #)
          #);
 
   ('L','4')
     ->tst(# do
             hashtable4.size->putInt
          #);   
   
   ('M','Histogram: (1,1,0,1,1)
Maximum Collisions: 1
Minimum Collisions: 0
Average Collisions: 1
')
     ->tst(# do
             hashtable4.statistics(# do t[]->print #)
          #);
#)
