ORIGIN 'collection';
BODY 'private/setsBody';
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- lib: attributes ---
multiSet: collection
  (* MultiSet is a data structure for collecting elements (dublicates
   * are allowed of the same element).  Defines no new operations.
   * MultiSet is the superpattern for Set, which ignores dublicates.
   * The available subpatterns are (indentation specifies
   * specialization): 
   *       multiSet
   *          set
   *             classification
   *)
  (# <<SLOT multiSetLib: attributes>>;
     clear::< (# <<SLOT multiSetClearDopart: dopart>> #);
     size::< (# <<SLOT multiSetSizeDopart: dopart>> #);
     insert::< 
       (* inserting in a multiSet is somewhat dependent on the
        * particular subpattern.  In some cases, insertion in only
        * done if the element is not already in the multiSet
        * (e.g. sets).  The local attribute, allowedToInsert, controls
        * whether the element is actually inserted in the multiSet.
        *)
       (# allowedToInsert: @boolean;
          multiSetInsertPrivate: @<<SLOT multiSetInsertPrivate: descriptor>>
       do true->allowedToInsert; 
          INNER; 
          (if allowedToInsert then multiSetInsertPrivate if)
       #);
     delete::< (# multiSetDeletePrivate: @<<SLOT multiSetDeletePrivate: descriptor>> do multiSetDeletePrivate; INNER #);
     theCellType::< (* Private *)
       (# pred, succ: (* Private *) ^theCellType;
          remove: (* Private *) (# <<SLOT multiSetTheCellTypeRemoveDopart: dopart>> #)
       do INNER #);
     implementationScanner::< (* Private *) 
       (# multiSetImplementationScannerPrivate: @<<SLOT multiSetImplementationScannerPrivateBody: descriptor>>;
          startOP::< (* Private *)
            (# theCell: ^theCellType
              <<SLOT multiSetImplementationScannerStartOPDopart: dopart>>
            #);
          nextOP::< (* Private *)
            (# theCell: ^theCellType
              <<SLOT multiSetImplementationScannerNextOPDopart: dopart>>
            #);
          moreOP::< (* Private *)
            (# <<SLOT multiSetImplementationScannerMoreOPDopart: dopart>> #)
       #);
     implementationCopier::< (* Private *)
       (# theCategory::< multiSet;
          doneInInner: @boolean;
          multiSetImplementationCopierPrivate: @<<SLOT multiSetImplementationCopierPrivate: descriptor>>
       do INNER;
          (if not doneInInner then multiSetImplementationCopierPrivate if)
       #);
     storage: (* Private *) @<<SLOT multiSetStorageBody: descriptor>>
  do INNER
  #) (* multiSet *);

(*------------------------------------------------------------*)
(*--- set: multiSet ------------------------------------------*)
(*------------------------------------------------------------*)

set: multiSet
  (* A set is a multiSet in which dublicates are removed.  Defines no
   * new operations
   *)
  (# <<SLOT setLib: attributes>>;
     insert::< 
       (* Inserts only the element if it is not in the set already *)
       (# <<SLOT setInsertDopart: dopart>> #);
     implementationCopier::< (* Private *)
       (# theCategory::< set do INNER #);
  do INNER
  #) (* set *);
