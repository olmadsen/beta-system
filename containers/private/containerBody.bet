ORIGIN '../container';
(*
 * COPYRIGHT
 *	 Copyright (C) Mjolner Informatics, 1992-94
 *	 All rights reserved.
 *)
-- containerHasBody: Descriptor --
   find
     (#
        predicate::<  (#  do (elm[],current[])->equal->value #);
        notFound::<  (#  do false->value #)
     do true->value
     #)
-- containerScanPrivate: Descriptor --
(#
   scanner: @implementationScanner;
   thePredicate: @where;
   theStart: @start;
   theEnd: @end
#)  

-- containerScanDopart: DoPart --
do
   containerScanPrivate.theStart;
   containerScanPrivate.scanner.start->current[];
   loop:
   (if containerScanPrivate.scanner.more then
       (if current[]->containerScanPrivate.thePredicate then INNER scan if);
       containerScanPrivate.scanner.next->current[];
       restart loop
   if);
   containerScanPrivate.theEnd  

-- containerFindPrivate: Descriptor --
(#
   scanner: @implementationScanner;
   thePredicate: @predicate;
   theStart: @start;
   theEnd: @end
#)  

-- containerFindDopart: DoPart --
do
   containerFindPrivate.theStart;
   containerFindPrivate.scanner.start->current[];
   loop:
   (if containerFindPrivate.scanner.more then
       (if current[]->containerFindPrivate.thePredicate then
           INNER find; leave loop
       if);
       containerFindPrivate.scanner.next->current[];
       restart loop
    else
       none ->current[]; notFound
   if);
   containerFindPrivate.theEnd  

-- containerCopyBody: Descriptor --
(# thePredicate: @predicate; copyType: ##container
do
   THIS(container)##->copyType##;
   &copyType[]->theCopy[];
   theCopy.init;
   (theCopy[],thePredicate[])->implementationCopier
#)  

-- containerImplementationCopierDopart: DoPart --
do
   (if theCopy[] = none then
         (# copyType: ##container
         do THIS(container)##->copyType##; &copyType[]->theCopy[]; theCopy.init
         #)
   if);
   INNER  

-- containerEnterDopart: DoPart --
do
   clear; (THIS(container)[],&elementPredicate[])->theOther.implementationCopier  

