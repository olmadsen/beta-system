ORIGIN '../recList';
(*
 * COPYRIGHT
 *	 Copyright (C) Mjolner Informatics, 1992-94
 *	 All rights reserved.
 *)
INCLUDE 'listBody'; 

--- recListCellElementDopart: dopart ---
do this(theCellType).elm[]->elm[]; INNER
--- recListCellSublistDopart: dopart ---
do this(theCellType).sublist[]->lst[]; INNER sublist
--- recListCellWhenPrivate: descriptor ---
(# do (if true
       //this(theCellType).sublist[]<>NONE then this(when).sublist
       //this(theCellType).elm[]<>NONE then this(when).elm
      if)
#)

--- recListImplementationScannerStartOPDopart: dopart ---
do currentCell[]->theCell[];
   (if theCell.sublist[] = NONE then
    else
       theCell[]->sublistPosition[];
       listImplementationScannerPrivate.nextCell[]->sublistNextPosition[];
       &theCell.sublist.implementationScanner[]->sublistScanner[];
       sublistScanner.start->currentElm[];
       (if not sublistScanner.more then
           subListPosition[]->currentCell[];
           subListNextPosition[]->listImplementationScannerPrivate.nextCell[];
           NONE->sublistScanner[];
           this(implementationScanner).next->currentElm[]
        else &listImplementationScannerPrivate.scanner[]->listImplementationScannerPrivate.theScanner[]
       if)
   if)
--- recListImplementationScannerNextOPDopart: dopart ---
do currentCell[]->theCell[];
   (if sublistScanner[] = NONE then
       (if theCell.sublist[] <>NONE then
           theCell[]->sublistPosition[];
           listImplementationScannerPrivate.nextCell[]->sublistNextPosition[];
           &theCell.sublist.implementationScanner[]->sublistScanner[];
           sublistScanner.start->currentElm[];
           (if not sublistScanner.more then
               subListPosition[]->currentCell[];
               subListNextPosition[]->listImplementationScannerPrivate.nextCell[];
               NONE->sublistScanner[];
               this(implementationScanner).next->currentElm[]
            else &listImplementationScannerPrivate.scanner[]->listImplementationScannerPrivate.theScanner[]
           if)
       if)
    else sublistScanner.next->currentElm[];
       (if not sublistScanner.more then
           subListPosition[]->currentCell[];
           subListNextPosition[]->listImplementationScannerPrivate.nextCell[];
           NONE->sublistScanner[];
           listImplementationScannerPrivate.directionScanner[]->listImplementationScannerPrivate.theScanner[];
           this(implementationScanner).next->currentElm[]
       if)
   if)

--- recListInsertSublistBeforeDopart: dopart ---
do (if lst.element##<=element##
       then &theCellType[]->newPosition[]; lst[]->newPosition.sublist[];
       (newPosition[],position[])->storage.insertBefore
    else IllegalSublist
   if);
   INNER insertSublistBefore

--- recListInsertSublistAfterDopart: dopart ---
do (if lst.element##<=element##
       then &theCellType[]->newPosition[]; lst[]->newPosition.sublist[];
       (newPosition[],position[])->storage.insertAfter
    else IllegalSublist
   if);
   INNER insertSublistAfter
