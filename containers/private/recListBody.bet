ORIGIN '../recList';
(*
 * COPYRIGHT
 *	 Copyright (C) Mjolner Informatics, 1992-94
 *	 All rights reserved.
 *)
INCLUDE 'listBody'; 

--- recListCellElementDopart: dopart ---
do this(theCellType).elm[]->elm[]; INNER
   
--- recListCellSublistDopart: dopart ---
do this(theCellType).sublist[]->lst[]; INNER sublist
   
--- recListCellWhenPrivate: descriptor ---
(# 
do (if true
    // this(theCellType).sublist[] <> NONE then this(when).sublist
    // this(theCellType).elm[] <> NONE then this(when).elm
   if)
#)
 
--- recListInsertSublistBeforeDopart: dopart ---
do (if lst.element## <= element##
       then &theCellType[]->newPosition[]; lst[]->newPosition.sublist[];
       
       (* insert in list *)
       (if position[]<>NONE then
           position.pred[]->newPosition.pred[];
           position[]->newPosition.succ[];
           (if position.pred[]<>NONE then
               newPosition[]->position.pred.succ[];
           if);
           newPosition[]->position.pred[];
        else
           newPosition[]->private.end.succ[];
           private.end[]->newPosition.pred[];
           newPosition[]->private.end[];
       if); 
       private.size+1->private.size;
       (* done *)
    else IllegalSublist
   if);
   INNER insertSublistBefore

--- recListInsertSublistAfterDopart: dopart ---
do 
   (if lst.element##<=element##
       then &theCellType[]->newPosition[]; 
       lst[]->newPosition.sublist[];
       
       (* insert in list *)
       (if position[]<>NONE then
           position.succ[]->newPosition.succ[];
           newPosition[]->position.succ[];
           position[]->newPosition.pred[];
           (if newPosition.succ[]<>NONE then
               newPosition[]->newPosition.succ.pred[];
           if);
        else
           newPosition[]->private.head.pred[];
           private.head[]->newPosition.succ[];
           newPosition[]->private.head[];
       if);
       private.size+1->private.size;
       (* done *)
    else IllegalSublist
   if);
   INNER insertSublistAfter
   
   
--- recListtheScannerdoPart:doPart ---
do
   (if theCell.sublist[]<>NONE then
       (s[],w[])->theCell.sublist.theScanner;
       true->s.first;     (* thescanner resets s at end *)
       false->notdone;
   if);
   
--- recListtheReverseScannerdopart:doPart ---
do
   (if aCell.sublist[]<>none then
       (s[],w[])->aCell.sublist.theReverseScanner;
       true->s.first;
       false->notdone;
   if);

--- recListtheFromScannerdopart:doPart ---
do
   (if aCell.sublist[]<>none then
       (s[],w[],none)->aCell.sublist.theFromScanner;
       true->s.first;
       false->notdone;
   if);

   
-- recListDoEnter:dopart --
do
   true->doneInInner;
   theOther.iterate
   (# 
   do (if current.elm[]<>none then
          current.copy->private.appendCell
       else
          (if current.sublist[]<>none then
              current.copy->private.appendCell
          if)
      if)
   #)

