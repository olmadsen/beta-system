ORIGIN '~beta/containers/v1.5/arrayContainer';
--- program:descriptor ---
(* This demo program illustrates the usage of the arrayContainer pattern.  The
 * first part of the demo illustrates the facilities for controlling the
 * capacity and the possibilities for dynamic extend of the arrayContainer.
 * The second part illustrates the sorting facilities.
 *
 * At the end of this file, a copy of the output of this demo is given 
 *)
(# intArrayContainer: arrayContainer
     (* intArrayContainer may contain references to integerObjects.  The
      * initial capacity will be 10, and the "less" operation compares the
      * numeric values of the integerObjects.
      *)
     (# element::< integerObject;
        capacityInitial::< (# do 10->value; INNER #);
        less::< (# do
                   (if NONE
                    //right[] then true->value
                    //left[] then false->value
                    else (left<right)->value if)
                #)
     #);
   
   arrayCon1: @intArrayContainer
     (* arrayCon1 will have an initial capacity of 6 integerObjects, and when
      * it will be expended, the capability will be extended with 6 elements
      * each time.
      *)
     (# capacityInitial::< (# do 6->value #);
        capacityIncrement::< (# do 6->value #)
     #);
   arrayCon2: @intArrayContainer;
   int: @integer; i: [20]^integerObject; io: ^integerObject;
   
do (* initializing the integerObjects *)
   (for int:20 repeat &integerObject[]->i[int][]; int->i[int] for);
   
   (* putting some integerObjects into arrayCon1 *)
   arrayCon1.init;
   'arrayCon1.put: '->puttext;
   (for int:6 repeat
        19-int->putint; ','->put;
        (i[19-int][],int)->arrayCon1.put
   for); newline;
   
   'arrayCon1.capacityExtend'->putline; arrayCon1.capacityExtend;
   
   'arrayCon1.put: '->puttext;
   (for int:4 repeat
        13-int->putint; ','->put;
        (i[13-int][],int+6)->arrayCon1.put
   for); newline;
   
   (* printing the elements in arrayCon1 *)
   'arrayCon1.elements: '->puttext;
   arrayCon1.scan(# do current->putint; ','->put #); newline;
   'arrayCon1.capacity: '->puttext; arrayCon1.capacity->putint; newline;
   'arrayCon1.size: '->puttext; arrayCon1.size->putint; newline;
   
   (* new intArrayContainer to arrayCon2, and putting some integerObjects into
    * arrayCon2
    *)
   arrayCon2.init;
    (for int:10 repeat 
         (i[int][],int)->arrayCon2.put
   for);
   (* printing the elements in arrayCon2 *)
   'arrayCon2.elements: '->puttext;
   arrayCon2.scan(# do current->putint; ','->put #); newline;
   
   (* copying arrayCon1 and printing this copy *)
   'arrayCon1->arrayCon2: '->puttext; arrayCon1->arrayCon2;
   arrayCon2.scan(# do current->putint; ','->put #); newline;

   (* getting some element from arrayCon2 *)
   '7->arrayCon2.get: '->puttext;
   7->arrayCon2.get->io[]; io->putint; newline;
   
   (* illustrating sorting using the different sorting algorithms *)
   'arrayCon1.bubbleSort: '->puttext;
   arrayCon1->arrayCon2; 12->arrayCon2.bubbleSort;
   arrayCon2.scan(# do current->putint; ','->put #); newline;
   
   'arrayCon1.shellSort: '->puttext;
   arrayCon1->arrayCon2; 12->arrayCon2.shellSort;
   arrayCon2.scan(# do current->putint; ','->put #); newline;
   
   'arrayCon1.quickSort: '->puttext;
   arrayCon1->arrayCon2; 10->arrayCon2.quickSort;
   arrayCon2.scan(# do current->putint; ','->put #); newline;
   
   (*********** OUTPUT ***************
    * arrayCon1.put: 18,17,16,15,14,13,
    * arrayCon1.capacityExtend
    * arrayCon1.put: 12,11,10,9,
    * arrayCon1.elements: 18,17,16,15,14,13,12,11,10,9,
    * arrayCon1.capacity: 12
    * arrayCon1.size: 10
    * arrayCon2.elements: 1,2,3,4,5,6,7,8,9,10,
    * arrayCon1->arrayCon2: 18,17,16,15,14,13,12,11,10,9,
    * 7->arrayCon2.get: 12
    * arrayCon1.bubbleSort: 9,10,11,12,13,14,15,16,17,18,
    * arrayCon1.shellSort: 9,10,11,12,13,14,15,16,17,18,
    * arrayCon1.quickSort: 9,10,11,12,13,14,15,16,17,18,
    **********************************)
#)
