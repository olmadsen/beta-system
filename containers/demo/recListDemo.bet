ORIGIN '~beta/containers/v1.5/recList';
--- program:descriptor ---
(* This demo program illustrates the usage of the recList pattern.  The first
 * part of the demo illustrates inserting and deleting elements from the lists,
 * and the last part illustrates the facilities for maintaining sublists.
 * 
 * At the end of this file, a copy of the output of this program is given
 *)
(# intList: recList(# element::< integerObject #);
   
   intList1, intList2: @intList; intList3: ^intList;
   int: @integer; i: [10]^integerObject; io: ^integerObject;
   listPosition: ^intList3.theCellType;
   
   recPrint:
     (* pattern for displaying a recursive intList *)
     (# lst: ^intList 
     enter lst[] 
     do lst.iterate
        (# do current.when
           (# sublist::< (# do '('->put; lst[]->recPrint; ')'->put #);
              elm::< (# do elm[]->io[];io->putint; ','->put #)
           #)
        #);
     #);
   
   recPrintReverse:
     (* pattern for displaying a recursive intList *)
     (# lst: ^intList;
     enter (lst[]) 
     do lst.iterateReverse
        (# do current.when
           (# sublist::< (# do '('->put; lst[]->recPrintReverse; ')'->put #);
              elm::< (# do elm[]->io[];io->putint; ','->put #)
           #)
        #);
     #);
   
   recPrintReverseFrom:
     (* pattern for displaying a recursive intList *)
     (# lst: ^intList; position: ^intList2.theCellType;
     enter (lst[], position[]) 
     do position[]->lst.iterateReverseFrom
        (# do current.when
           (# sublist::< (# do '('->put; lst[]->recPrintReverse; ')'->put #);
              elm::< (# do elm[]->io[];io->putint; ','->put #)
           #)
        #);
     #);
   
do (* initializing the integerObjects *)
   (for int:10 repeat &integerObject[]->i[int][]; int->i[int] for);
   
   (* initializing intList1 and putting some integerObjects into it *)
   intList1.init;
   (for int:4 repeat
      i[int][]->intList1.prepend
   for);
   'intList1.elements: '->puttext;
   intList1.scan(# do current->putint; ','->put #); newline;
   
   (* initializing intList3 and putting some integerObjects into it *)
   intList2.init;
   (for int:4 repeat 
      i[int+3][]->intList2.append
   for);
   'intList2.elements: '->puttext;
   intList2.scan(# do current->putint; ','->put #); newline;
   
   (* initializing intList3 and putting some integerObjects into it *)
   &intList[]->intList3[]; intList3.init;
   (for int:4 repeat 
      i[int+6][]->intList3.prepend
   for);
   'intList3.elements: '->puttext;
   intList3.scan(# do current->putint; ','->put #); newline;   
   
   (* illustrating inserting a sublist in intList3 before/after some list
    * position
    *)
   '(intList1[],NONE)->intList3.insertSublistBefore: '->puttext;
   (intList1[],NONE)->intList3.insertSublistBefore;
   intList3[]->recPrint; newline;
   '(intList1[],7)->intList3.insertSublistAfter: '->puttext;
   i[7][]->intList3.at->listPosition[];
   (intList1[],listPosition[])->intList3.insertSublistAfter;
   intList3[]->recPrint; newline;
   
   (* illustrating appending to a list *)
   '1->intList3.append: '->puttext;
   i[1][]->intList3.append;
   intList3[]->recPrint; newline;
   
   (* illustrates deleting an element from the list *)
   '6->intList2.at->intList2.delete: '->puttext;
   i[6][]->intList2.at->intList2.delete;
   intList2[]->recPrint; newline;
   
   (* illustrates the difference between scan and recPrint *)
   'intList2.scan: '->puttext;
   intList2.scan(# do current->putint; ','->put #); newline;
   
   (* illustrates the reverse scanning *)
   'intList2.scanReverse: '->puttext;
   intList2.scanReverse(# do current->putint; ','->put #); newline;
   '"5"->intList2.scanReverseFrom: '->puttext;
   i[5][]->intList2.at->listPosition[];
   listPosition[]->intList2.scanReversefrom(# do current->putint; ','->put #); newline;
   
   (* illustrating last and preample operations: *)
   'intList2.last: '->puttext;
   intList2.last->listPosition[];
   listPosition.when(# elm::<(# do elm[]->io[]; io->putint #) #); newline;
   'intList2.preample: '->puttext;
   intList2.preample->intList3[];
   intList3[]->recPrint; newline;
   
   (*********** OUTPUT ***************
    * intList1.elements: 4,3,2,1,
    * intList2.elements: 4,5,6,7,
    * intList3.elements: 10,9,8,7,
    * (intList1[],NONE)->intList3.insertSublistBefore: 10,9,8,7,(4,3,2,1,)
    * (intList1[],7)->intList3.insertSublistAfter: 10,9,8,7,(4,3,2,1,)(4,3,2,1,)
    * 1->intList3.append: 10,9,8,7,(4,3,2,1,)(4,3,2,1,)1,
    * 6->intList2.at->intList2.delete: 4,5,7,
    * intList2.scan: 4,5,7,
    * intList2.scanReverse: 7,5,4,
    * "5"->intList2.scanReverseFrom: 5,4,
    * intList2.last: 7
    * intList2.preample: 4,5,
    ******************************)
#)
