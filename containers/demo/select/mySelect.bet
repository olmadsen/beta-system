origin '~beta/basiclib/betaenv';
include '~beta/containers/sets';
include '~beta/containers/demo/select/person';
(* An example of using the set-container pattern.
 * Similar to listSelect except that a control
 * operation select with a condition iff is implemented. 
 * Select is similar to scan with the condition where
 * implemented for pattern set.
 * Select and iff are shown here for illuatrative purposes
 *)
---lib:attributes---
PersonSet: set
  (# element::< person;
     display:
       (#
       do scan(# do current.display #)
       #);
     select:
       (# iff:< booleanValue;
          current: ^element
       do scan
          (#
          do current[] -> this(Select).current[];
             (if iff then  
                 (* inner is only executed for
                  * elements satisfying iff
                  *)
                 INNER select 
             if)
       #)#);
  #);

---program:descriptor---
(# S1: @PersonSet; S2: @PersonSet
do S1.init;
   generate(# do current[] -> S1.insert #);
   
   S2.init;
   
   S1.select
   (# iff:: (#do (current.isMale) and (current.age < 35) -> value #)
   do
      current[] -> S2.insert
   #);
   

   S2[] -> S1.diff; (* The members of S2 are removed from S1 *)
   
   'S1:'->putline;
   S1.display;
   
   '\nS2:'->putline;
   S2.display;
#)
