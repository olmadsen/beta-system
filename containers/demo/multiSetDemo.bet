ORIGIN '~beta/containers/v1.5/sets';
--- program:descriptor ---
(* This demo program illustrates the usage of the multiSet pattern.  The first
 * part of the demo illustrates inserting and deleting elements from the
 * multiSets, and the last part illustrates the union, diff, sect and symDiff
 * operations.
 * 
 * At the end of this file, a copy of the output of this program is given
 *)
(# intMultiSet: multiSet
     (* a multiSet of integerObjects *)
     (# element::< integerObject #);
   
   intMultiSet1, intMultiSet2: @intMultiSet; intMultiSet3: ^intMultiSet;
   int: @integer; i: [10]^integerObject;
   
do (* initializing the integerObjects *)
   (for int:10 repeat &integerObject[]->i[int][]; int->i[int] for);
   
   (* initializing intMultiSet1 and putting some integerObjects into it *)
   intMultiSet1.init;
   (for int:4 repeat
        i[int][]->intMultiSet1.insert
   for);
   (* printing intMultiSet1 *)
   'intMultiSet1.elements: '->puttext;
   intMultiSet1.scan(# do current->putint; ','->put #); newline;
   
   (* initializing intMultiSet2 and putting some integerObjects into it *)
   intMultiSet2.init;
   (for int:4 repeat 
        i[int+3][]->intMultiSet2.insert
   for);
   (* printing intMultiSet2 *)
   'intMultiSet2.elements: '->puttext;
   intMultiSet2.scan(# do current->putint; ','->put #); newline;
   
   &intMultiSet[]->intMultiSet3[]; intMultiSet3.init;
   
   (* copying intMultiSet2 and printing the copy *)
   'intMultiSet2->intMultiSet3: '->puttext;
   intMultiSet2->intMultiSet3;
   intMultiSet3.scan(# do current->putint; ','->put #); newline;
   
   (* deleting some integerObjects from intMultiSet3 *)
   '"7"->intMultiSet3.delete: '->puttext;i[7][]->intMultiSet3.delete;
   intMultiSet3.scan(# do current->putint; ','->put #); newline;
   '"4"->intMultiSet3.delete: '->puttext;i[4][]->intMultiSet3.delete;
   intMultiSet3.scan(# do current->putint; ','->put #); newline;
   
   (* illustrating the union, diff, sect and symDiff operations *)
   'intMultiSet1[]->intMultiSet2.union: '->puttext;
   intMultiSet2->intMultiSet3;
   intMultiSet1[]->intMultiSet3.union->intMultiSet3[];
   intMultiSet3.scan(# do current->putint; ','->put #); newline;
   
   'intMultiSet1[]->intMultiSet2.diff: '->puttext;
   intMultiSet2->intMultiSet3;
   intMultiSet1[]->intMultiSet3.diff->intMultiSet3[];
   intMultiSet3.scan(# do current->putint; ','->put #); newline;
   
   'intMultiSet1[]->intMultiSet2.sect: '->puttext;
   intMultiSet2->intMultiSet3;
   intMultiSet1[]->intMultiSet3.sect->intMultiSet3[];
   intMultiSet3.scan(# do current->putint; ','->put #); newline;
   
   'intMultiSet1[]->intMultiSet2.symDiff: '->puttext;
   intMultiSet2->intMultiSet3;
   intMultiSet1[]->intMultiSet3.symDiff->intMultiSet3[];
   intMultiSet3.scan(# do current->putint; ','->put #); newline;
   
   (* finding some integerObject from intMultiSet3 *)
   'intMultiSet3.find(# predicate ::< (# "elm=4" #) #): '->puttext;
   intMultiSet3.find(# predicate::< (# do (current=4)->value #)
      	            do current->putint
      	            #); newline;
   
   (* testing for the presense of some elements in intMultiSet3 *)
   '4->intMultiSet3.has: '->puttext;
   (if i[4][]->intMultiSet3.has//true
       then 'yes'->putline
    else 'no'->putline
   if);
   '10->intMultiSet3.has: '->puttext;
   (if i[10][]->intMultiSet3.has//true
       then 'yes'->putline
    else 'no'->putline
   if);
   
   (*********** OUTPUT ***************
    * intMultiSet2.elements: 7,6,5,4,
    * intMultiSet2->intMultiSet3: 7,6,5,4,
    * "7"->intMultiSet3.delete: 6,5,4,
    * "4"->intMultiSet3.delete: 6,5,
    * intMultiSet1[]->intMultiSet2.union: 1,2,3,4,7,6,5,4,
    * intMultiSet1[]->intMultiSet2.diff: 7,6,5,
    * intMultiSet1[]->intMultiSet2.sect: 4,
    * intMultiSet1[]->intMultiSet2.symDiff: 1,2,3,7,6,5,4,
    * intMultiSet3.find(# predicate ::< (# "elm=4" #) #): 4
    * 4->intMultiSet3.has: yes
    * 10->intMultiSet3.has: no
    **********************************)
#)
