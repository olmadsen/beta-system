ORIGIN '~beta/containers/v1.5/dictionary'
--- program:descriptor ---
(* This demo program illustrates the usage of the dictionary pattern.
 * The first part of the demo illustrates inserting and deleting
 * elements from the dictionarys.
 *)
(# intDictionary: dictionary
     (* a dictionary containing integerObjects with text keys *)
     (# key::< text;
        element::< integerObject;
     #);
   
   dictionary1: @intDictionary;
   dictionary2: @intDictionary;
   dictionary3: ^intDictionary;
   int: @integer; i: [10]^integerObject; t: [10]^text; io: ^integerObject
   
do (* initializing the integerObjects *)
   (for int:10 repeat &integerObject[] -> i[int][]; int -> i[int] for);
   
   (* initializing dictionary1 and putting some integerObjects into it.  Also
    * illustrating the facilities for extend
    *)
   dictionary1.init;
   'dictionary1.associate: ' -> puttext;
   (for int:4 repeat
        int->putint; ','->puttext;
        &text[]->t[int][]; 'x'->t[int].puttext; int->t[int].putint;
        (t[int][],i[int][]) -> dictionary1.associate
   for); newline;
   'dictionary1.elements: ' -> putText;
   dictionary1.scan(# do current -> putint; ','->puttext#); newline;
   'dictionary1.size: '->putText;
   dictionary1.size->putInt; newline;
   newline;
   
   (* initializing dictionary2 and putting some integerObjects into it.  Also
    * illustrating the facilities for extend
    *)
   dictionary2.init;
   'dictionary2.associate: ' -> putline;
   (for int:4 repeat 
        &text[]->t[int+3][]; 'x'->t[int+3].puttext; int+3->t[int+3].putint;
        (t[int+3][],i[int+3][]) -> dictionary2.associate;
        '\t('->puttext;t[int+3][]->puttext;','->put;i[int+3]->putint;')'->putline;
   for); newline;
   'dictionary2.elements: ' -> putText;
   dictionary2.scan(# do current -> putint; ','->puttext#); newline;
   'dictionary2.size: '->putText; dictionary2.size->putInt; newline;
   newline;
   
   (* illustrating copying a dictionary *)
   'dictionary2.copy->dictionary3[]: ' -> putText;
   dictionary2.copy->dictionary3[];
   dictionary3.scan(# do current -> putint; ','->puttext#); newline;
   
   (* deleting integerObject 3 from dictionary1 *)
   t[3][]->puttext;'->dictionary1.disassociate: '->puttext;
   t[3][]->dictionary1.disassociate;
   dictionary1.scan(# do current -> putint; ','->puttext#); newline;
   newline;
   
   (* looking integerObject 2 from dictionary1 *)
   t[2][]->puttext;'->dictionary1.lookup: '->puttext;
   t[2][]->dictionary1.lookup -> io[]; io -> putint; newline;
   newline;
   
   (* illustrating inspecting for particular integerObjects in dictionary3 *)
   'dictionary3.find(# predicate ::< (# "elm=4" #) #): '->puttext;
   dictionary3.find
   (# predicate ::< (# do (current=4)->value #)
   do current -> putint
   #);
   newline;
   
   '4->dictionary3.lookup: '->puttext;
   (if (t[4][]->dictionary3.lookup)<>NONE then
       'yes'->putline
    else
       'no'->putline
   if);
   '10->dictionary3.lookup: '->puttext;
   (if (t[10][]->dictionary3.lookup)<>NONE then
       'yes'->putline
    else
       'no'->putline
   if);
   'dictionary1.scanAssociations: '->putline;
   dictionary1.scanAssociations
   (# do '\t('->puttext;k[]->puttext;','->put;e->putint;')'->putline #);
   'dictionary2.scanAssociations: '->putline;
   dictionary2.scanAssociations
   (# do '\t('->puttext;k[]->puttext;','->put;e->putint;')'->putline #);
   'dictionary3.scanAssociations: '->putline;
   dictionary3.scanAssociations
   (# where:: (# do (current < 5) or (t[6][]=k[]) -> value #)
   do '\t('->puttext;k[]->puttext;','->put;e->putint;')'->putline #);
#)
