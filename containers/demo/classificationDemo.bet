ORIGIN '~beta/containers/v1.5/classification';
--- program:descriptor ---
(* This demo program illustrates the use of the classification patterns.
 * Especially the use of classificationSubsets
 * 
 * At the end of this file, a copy of the output of this file is given
 *)
(# intClassificationSet: classificationSet
     (* intClassification is a classificationSet of integerObjects *)
     (# element::< integerObject #);
   recPrint:
     (# class: ^intClassificationSet
     enter class[]
     do class.scanUnclassified(# do current->putint; ','->put #);
        class.subsets.scan(# do '{'->put; current[]->recPrint; '}'->put #);
     #);
   intClass1,
   intClass2,
   intClass3: @intClassificationSet;
   int: @integer; i: [14]^integerObject;
   
do (* initializing the integerObjects *)
   (for int:14 repeat &integerObject[]->i[int][]; int->i[int] for);
   
   (* initializating intClass1 and inserting integerObjects into it *)
   intClass1.init;
   (for int:6 repeat 
        i[int+8][]->intClass1.insert
   for);
   'intClass1.size: '->puttext; intClass1.size->putint; newline;
   'intClass1.elements: '->putText;
   intClass1.scan(# do current->putint; ','->put #); newline;
   
   (* initializating intClass2 and inserting integerObjects into it *)
   intClass2.init;
   (for int:6 repeat
        i[int][]->intClass2.insert
   for);
   'intClass2.size: '->puttext; intClass2.size->putint; newline;
   'intClass2.elements: '->putText;
   intClass2.scan(# do current->putint; ','->put #); newline;
      
   (* initializating intClass3 and inserting integerObjects into it *)
   intClass3.init;
   (for int:6 repeat 
        i[int+4][]->intClass3.insert
   for);
   'intClass3.size: '->puttext; intClass3.size->putint; newline;
   'intClass3.elements: '->putText;
   intClass3.scan(# do current->putint; ','->put #); newline;
   
   '4->intClass2.delete: '->puttext;
   i[4][]->intClass2.delete;
   intClass2.scan(# do current->putint; ','->put #); newline;
   
   (* insert intClass3 as a subset of intClass2 *)
   'intClass2[]->intClass3.insertSubset: '->puttext;
   intClass2[]->intClass3.insertSubset;
   intClass3[]->recPrint; newline;
   '10->intClass3.has: '->puttext;
   (if i[10][]->intClass3.has//true
       then 'yes: '->puttext
    else 'no: '->puttext
   if); intClass3[]->recPrint; newline;
   (* insert intClass3 as a subset of intClass2 *)
   'intClass1[]->intClass2.insertSubset: '->puttext;
   intClass1[]->intClass2.insertSubset;
   intClass2[]->recPrint; newline;
   
   'intClass1.scanUnclassified: '->puttext;
   intClass1.scanUnclassified(# do current->putint; ','->put #); newline;
   'intClass2.scanUnclassified: '->puttext;
   intClass2.scanUnclassified(# do current->putint; ','->put #); newline;
   'intClass3.scanUnclassified: '->puttext;
   intClass3.scanUnclassified(# do current->putint; ','->put #); newline;
   
   (* illustrating the testing for presense of integerObject 3 in
    * intClass1 and intClass3
    *)
   '3->intClass1.has: '->puttext;
   (if i[3][]->intClass1.has//true 
       then 'yes: '->puttext
    else 'no: '->puttext
   if); intClass1[]->recPrint; newline;
   (* note that 3 is not in intClass1 *)
   '3->intClass3.has: '->puttext;
   (if i[3][]->intClass3.has//true
       then 'yes: '->puttext
    else 'no: '->puttext
   if); intClass3[]->recPrint; newline;

   'intClass1.size: '->puttext;
   intClass1.size->putint; newline;
   'intClass3.size: '->puttext;
   intClass3.size->putint; newline;
   
   (* inserting integerObject 3 in intClass1 *)
   '3->intClass1.insert: '->puttext;
   i[3][]->intClass1.insert;
   intClass1[]->recPrint; newline;
   
   'intClass1.size: '->puttext;
   intClass1.size->putint; newline;
   'intClass3.size: '->puttext;
   intClass3.size->putint; newline;
   
   (* note that 3 is now in both intClass1 and intClass3 *)
   '3->intClass1.has: '->puttext;
   (if i[3][]->intClass1.has//true
       then 'yes: '->puttext
    else 'no: '->puttext
   if); intClass1[]->recPrint; newline;
   '3->intClass3.has: '->puttext;
   (if i[3][]->intClass3.has//true
       then 'yes: '->puttext
    else 'no: '->puttext
   if); intClass3[]->recPrint; newline;
   
   (* deleting integerObject 3 from intClass2. Note that this
    * indirectly deletes integerObject 3 from intClass1
    *)
   '3->intClass3.delete: '->puttext;
   i[3][]->intClass3.delete;
   intClass3[]->recPrint; newline;
   
   (*********** OUTPUT ***************
    * intClass1.size: 6
    * intClass1.elements: 14,13,12,11,10,9,
    * intClass2.size: 6
    * intClass2.elements: 6,5,4,3,2,1,
    * intClass3.size: 6
    * intClass3.elements: 10,9,8,7,6,5,
    * 4->intClass2.delete: 6,5,3,2,1,
    * intClass2[]->intClass3.insertSubset: 10,9,8,7,{6,5,3,2,1,}
    * intClass1[]->intClass2.insertSubset: 6,5,3,2,1,{14,13,12,11,10,9,}
    * intClass1.scanUnclassified: 14,13,12,11,10,9,
    * intClass2.scanUnclassified: 6,5,3,2,1,
    * intClass3.scanUnclassified: 10,9,8,7,
    * 3->intClass1.has: no: 14,13,12,11,10,9,
    * 3->intClass3.has: yes: 10,9,8,7,{6,5,3,2,1,{14,13,12,11,10,9,}}
    * intClass1.size: 6
    * intClass3.size: 15
    * 3->intClass1.insert: 3,14,13,12,11,10,9,
    * intClass1.size: 7
    * intClass3.size: 15
    * 3->intClass1.has: yes: 3,14,13,12,11,10,9,
    * 3->intClass3.has: yes: 10,9,8,7,{6,5,2,1,{3,14,13,12,11,10,9,}}
    * 3->intClass3.delete: 10,9,8,7,{6,5,2,1,{14,13,12,11,10,9,}}
    **********************************)
#)
