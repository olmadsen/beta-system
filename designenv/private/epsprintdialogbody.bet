ORIGIN 'epsprintdialog';
INCLUDE '~beta/guienv/stddialogs'
        '~beta/guienv/utils/dialogfield';
-- ppageDialogprivate: Descriptor --
(#
   state: @boolean;
   name: @pushButton
     (#
	eventHandler::
	  (# 
	     onMouseUp::
	       (#
		  newFile: ^text
	       do
		  THIS(ppageDialog)[] -> &fileCreationDialog
		    (#
		    do
		       '*.eps' -> filter[];
		       name.label -> fileName[];
		       'Select EPS file' -> label[];
                    #) -> newFile[];
		  (if newFile[] <> NONE then newFile[] -> label if)
               #)
          #);
        open::<
	  (#
	  do
	     (240,20)->size;
	     (30,23)->position
	  #)
     #);
   scaleToFitButton: @checkBox
     (#
	eventHandler::
	  (# 
	     onStateChanged::
	       (#
	       do
		  (if state then false -> scaleButton.state if)
	       #)
	  #);
        open::< 
          (# 
          do
             'Shrink to fit A4-page'->label;
             false->state;
             (141,17)->size;
             (30,55)->position;
             
          #)
     #);
   scaleButton: @checkBox
     (#
	eventHandler::
	  (# 
	     onStateChanged::
	       (#
	       do
		  (if state then
		      false -> scaleToFitButton.state;
		      scaleText.enable
		   else
		      scaleText.disable
		  if)
	       #)
          #);
        open::< 
          (# 
          do
             'Scale %:'->label;
	     false->state; scaleText.disable;
	     (69,17)->size;
	     (30,85)->position
          #)
     #);
   scaleText: @numberField
     (# open::<  (#  do (146,30)->size; (124,80)->position;  #) #);
   okButton: @pushButton
     (#
	eventHandler::
	  (#
	     onMouseUp::
	       (# 
	       do 
		  (name.label,scaleButton.state,
		     scaleToFitButton.state,
		     scaleText.IntegerContents) -> onOK
               #)
          #);
        open::< (#  do 'OK'->label; (60,20)->size; (139,124)->position;  #)
     #);
   cancelButton: @pushButton
     (#
	eventHandler:: (# onMouseUp:: (# do onCancel #) #);
        open::<
          (#  do 'Cancel'->label; (60,20)->size; (210,124)->position;  #)
     #)
#)  

-- ppageDialogOpen: DoPart --
do
   (302,162)->size;
   private.name.open;
   (if fileName[] // NONE then
       'unamed.eps'->private.name.label
    else
       fileName[] -> private.name.label
   if);

   private.scaleText.open;
   scale -> private.scaleText.IntegerContents;

   private.scaleToFitButton.open;
   private.scaleButton.open;
   scaleToFit -> private.scaleToFitButton.state;
   scaled -> private.scaleButton.state;

   private.okButton.open;
   private.cancelButton.open;
   
   INNER  
