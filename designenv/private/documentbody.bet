ORIGIN '../designenv';
INCLUDE '../designenvutils'
-- NewDocument: Descriptor --
(# newPage: ^Page; 
do (* prevent the callback from New *)
   disable.MenuEventCallBack;
   disable.PostMenuEventCallBack;
   (menuID.fileMenu,menuID.fileMenu.New)->CallOAMenu;
   (* create a new document just by simulating New in File menu *)
   THIS(Document)[]->theDocument[];
   onInit->newPage[];
   (if newPage[]
    // none then &DesignPage[]->newPage[]; (* new document makes a new page automatically *) 
   if);
   DSStrGetCurPage->newPage.onInit;
   (* init page with id *)
   newPage[]->CurrentPage;
   (* allow onInit to create other pages *)
   (* allow the callback from New *)
   enable.MenuEventCallBack;
   enable.PostMenuEventCallBack;
   
#)  

-- DocumentPrivatePart: Descriptor --
(#
   currentPageID: @Integer;
   coarsening: @Integer;
   (* no os pages currently made by coarsening
    * but not yet made as beta object *)
   doingCoarse: @Boolean;
   InteractiveCreatedObject: ^Object;
   hackw,hackh: @Integer;
   
#)  

-- DocumentID: Descriptor --
(#  do DSStrGetDocID->i;  #)  

-- DocModified: Descriptor --
(#  do b->DSStrSetDiagModified;  #)  

-- SetPage: Descriptor --
(# 
do
   (if p.ID->DSStrSetCurPage
    // true then p.ID->privatepart.currentPageID; 
    else
       'SetPage failed; error ID: '->putline; 
   if);
   
#)  

-- GetPage: Descriptor --
(# 
do (* test whether current is the same page *)
   (if (DSStrGetCurPage->pid) <> privatepart.currentPageID
    // true then (* no find the new page *)
       pid->theObjectList.Find->p[]; pid->privatepart.currentPageID; 
   if);
   (if p[] // none then 'GetPage: page is none'->putline if);
   
#)  

-- GetPages: Descriptor --
(# count: @ShortRef; listRef: @IntegerRef; 
do (* get the pages from design/oa and make a list... *)
   (count[],listRef[])->DSStrGetPageList;
   (count,listRef)->GetIntList->theObjectList.NodeIDsToList->pagelist[];
   
#)
(*************************** GLOBAL **********************************)  

-- MaxGroupSize: Descriptor --
(#  do DSRdAttrGetMaxGroupSize->size;  #)  

-- PolyGetSide: Descriptor --
(# sidesRef: ^shortRef; orientRef: ^integerRef; 
do (sidesRef[],orientRef[])->DSRdAttrPolyDefaults; sidesRef->side; 
#)  

-- PolySetSide: Descriptor --
(# sidesRef: ^shortRef; orientRef: ^integerRef; 
do
   (sidesRef[],orientRef[])->DSRdAttrPolyDefaults;
   (if (side,orientRef)->DSWtAttrRegularPolyInfo
    // false then 'PolySetSide: could not set'->putline; 
   if);
   
#)  

-- PolyGetOrient: Descriptor --
(# sidesRef: ^shortRef; orientRef: ^integerRef; 
do (sidesRef[],orientRef[])->DSRdAttrPolyDefaults; orientRef->orient; 
#)  

-- PolySetOrient: Descriptor --
(# sidesRef: ^shortRef; orientRef: ^integerRef; 
do
   (sidesRef[],orientRef[])->DSRdAttrPolyDefaults;
   (if (sidesRef,orient)->DSWtAttrRegularPolyInfo
    // false then 'PolySetOrient: could not set'->putline; 
   if);
   
#)
(*********** CURSOR HANDLING ***********)  

-- SetCursor: Descriptor --
(#  do ID->DSUIChangeCursor->oldCurs;  #)  

-- RestoreCursor: Descriptor --
(#  do oldCurs->DSUIChangeCursor;  #)  

-- ResetCursor: Descriptor --
(#  do CrossHair->set;  #)  

-- SetStatusBar: Descriptor --
(#  do msg->DSUISetStatusBarMessage;  #)  

-- ResetStatusBar: Descriptor --
(#  do DSUIRestoreStatusBar;  #)  

