ORIGIN 'bifrostpagebody';
LIB_ITEM 'designenv';
INCLUDE '~beta/sysutils/time'
        '~beta/guienv/stddialogs'
	'printdialog';
BUILD default '$$/ipipe.o' 'ipipe.c' '$CC -D$$ -c $1 -o $0';
-- bifrostobjectbodymdbodyCVSINFO:descriptor--
(#
do
   '$Id: bifrostdesignX11body.bet,v 1.8 2001-06-18 11:43:01 datpete Exp $'
     -> msg.putLine
#)

--- hideTextOutline:descriptor --
(# #)

-- bifrostPageKeyDown:descriptor --
(#
(* enter (shiftKey,controlKey,altKey,shiftLockKey,keyNr,asciiCode) *)
do
   (if UI.veryVerbose then
       'onKeyDown: %d\n' -> putFormat(# do ch -> d #)
   if);
   (shiftKey, controlKey, metaKey, false (* shift lock *), ch, ch)
     -> theDocument.onKeyDown
#)

--LIB: attributes--
(* Process does not handle pipes too well, so we do it manually... *)
fputc: external
  (# c: @char;
     file: @integer;
     status: @integer;
  enter (c,file)
  exit status
  #);
create_pipe: external
  (#
     cmd: [1]@char;
     cmdpipe,readpipe: @integer;
     wfd: @integer;
     status: @integer
  enter(cmd,cmdpipe,readpipe,wfd)
  exit status
  #);
close_pipe: external
  (#
     buf: [1]@char;
     cmdpipe,readpipe: @integer;
     wfd: @integer;
  enter(cmdpipe,readpipe,wfd)
  exit buf
  #);
SIGPIPE: (# exit 13 #);
signal: external
  (# sig: @integer;
     handler: ##external;
  enter (sig, handler##)
  #);
EOF: 
  (# exit -1 #);
pipe: Stream
  (# command: ^text;
     file: @integer;
     readpipe,wfd: @integer;
     open: 
       (# 
	  error:< exception;
	  status: @integer
       enter command[]
       do
	  (if ((command,@@file,@@readpipe,@@wfd)->create_pipe) < 0 then
	      error
	  if)
       #);
     close: 
       (# 
	  output: @text;
       do
	  (file,readpipe,wfd)->close_pipe->output
       exit output[]
       #);
     putError:< exception;
     puttext::(# putc: @put; do txt.scanAll(# do ch->putc #)#);
     put:: (# do (if (ch,file)->fputc//EOF then putError if) #);
  #);

-- printPageExecute: DoPart --
do
   (if UI.menuVerbose then
       '%d: menubody-printPageExecute called!\n' -> putFormat(# do ID -> d #)
   if);
   try:
     (#
	pipe_broken: @boolean;
	handleSigPipe: external
	  (# sig: @integer;
	  enter sig
	  do CExternalEntry;
	     (* Be carefull not to cause GC here! *)
	     true->pipe_broken;
	  #);
	pd: @UI.theGUI.printDialog
	  (#
	     onOK::
	       (#
		  out: ^stream;
		  outf: @file
                    (#
	               AccessError::<
                         (#
                         do (NONE,msg[],'Print failed')
                               -> UI.theGUI.alertUser;
		            leave try 
                         #);
	               WriteError::<
                         (#
                         do (NONE,msg[],'Print failed')
                               -> UI.theGUI.alertUser;
		            leave try 
                         #);
	               ReadError::< 
                         (#
                         do (NONE,msg[],'Print failed')
                               -> UI.theGUI.alertUser;
		            leave try 
                         #);
	               EOSerror::< 
                         (#
                         do (NONE,msg[],'Print failed')
                               -> UI.theGUI.alertUser;
		            leave try 
                         #);
	               NoSuchFileError::<
                         (#
                         do (NONE,msg[],'Print failed')
                               -> UI.theGUI.alertUser;
		            leave try 
                         #);
	               FileExistsError::<
                         (#
                         do (NONE,msg[],'Print failed')
                               -> UI.theGUI.alertUser;
		            leave try 
                         #);
	               NoSpaceError::<
                         (#
                         do (NONE,msg[],'Print failed')
                               -> UI.theGUI.alertUser;
		            leave try 
                         #);
	               OtherError::<
                         (#
                         do (NONE,msg[],'Print failed')
                               -> UI.theGUI.alertUser;
		            leave try 
                         #);
	             #);
		  forget: @
		    (# 
		       output: ^text
		    do
		       (SIGPIPE, handleSigPipe##) -> signal;
		       outp.close -> output[];
		       'Command returned:\n\n' -> output.prepend;
		       (NONE,output[],'Print failed') -> UI.theGUI.alertUser;
		       leave try
                    #);
		  outp: @pipe (# putError::< (# do forget #) #);
		  pn,pgs: @integer
	       do
		  close;
		  toFile -> UI.designPlatformExtentions.toFile;
		  allPages -> UI.designPlatformExtentions.allPages;
		  cmd[] -> UI.designPlatformExtentions.cmd[];
		  fileName[] -> UI.designPlatformExtentions.fileName[];
		  (if allPages then
		      (if UI.verbose then 'Print all pages' -> putText if);
		      1 -> from;
		      pl.noOfElements -> to
		   else
		      (if UI.verbose then
			  'Print from page ' -> putText;
			  from -> putInt;
			  ' to ' -> putText;
			  to -> putInt
		      if)
		  if);
		  (if toFile then
		      (if UI.verbose then
			  ' to file: ' -> putText;
			  fileName[] -> putLine
                      if);
		      outf[] -> out[];
		      fileName[] -> outf.name;
		      outf.openWrite; 
		   else
		      (if UI.verbose then
			  ' using command: ' -> putText;
			  cmd[] -> putLine
                      if);
		      outp[] -> out[];
		      (SIGPIPE, handleSigPipe##) -> signal;
		      false -> pipe_broken;
		      cmd[] -> outp.open(# error::< (# do forget #) #)
		  if);
		  '%!PS-Adobe-3.0' -> out.putline;
		  '%%Creator: Mjolner Freja CASE tool 4.0' -> out.putLine;
		  '%%CreationDate: ' -> out.puttext;
		  systemtime -> formattime -> out.putLine;
		  '%%For: ' -> out.putText;
		  '$(USER)' -> expandEnvVar
		    (#
		       defaultValue:: (# do 'unkown'->envvarvalue[] #)
		    #) -> out.putLine;
		  '%%Title: Freja diagram' -> out.putLine;
		  (* '%%BoundingBox: (atend)' -> out.putLine; *)
		  '%%Pages: (atend)' -> out.putLine;
		  '%%EndComments' -> out.putLine;
		  '%%BeginProlog' -> out.putLine;
		  '%%BeginResource: procset Mjolner-Bifrost-StandardMacros 1.0 0'
		    -> out.putLine;
		  out.writeMacros;
		  '%%EndResource' -> out.putLine;
		  '%%EndProlog' -> out.putLine;
		  pl.scan
		    (# 
		       txt: @text; dp: ^theDocument.page
		    do
		       pn + 1 -> pn;
		       (if (from <= pn) and (pn <= to) then
			   pgs + 1 -> pgs;
			   '%%Page: (' -> out.putText;
			   current[] -> dp[];
			   dp.pageTitle -> txt;
			   txt[] -> out.putText;
			   ') ' -> out.putText;
			   pgs -> out.putInt;
			   out.newline;
			   '%%BeginPageSetup' -> out.putLine;
			   '/pgsave save def' -> out.putLine;
			   '%%EndPageSetup' -> out.putLine;
			   out[] -> (current.ID -> UI.getApage).print;
			   'pgsave restore' -> out.putLine;
			   'showpage'->out.putline
		       if)
		    #);
		  '%%Trailer' -> out.putLine;
		  '%%Pages: ' -> out.putText; pgs -> out.putInt; out.newline;
		  '%%EOF' -> out.putLine;
		  (if toFile then
		      outf.close
		   else
		      (if pipe_broken then 'Pipe broken' -> putLine if);
		      outp.close
		  if)
	       #);
	     onCancel::(# do close #)
	  #);
	pl: ^objectList
     do
	theDocument.getPages -> pl[];
	(if pl.noOfElements // 0 then
	    (NONE, 'No pages in current document', 'Print Dialog')
	      -> UI.theGUI.alertUser
	 else
	    (UI.designPlatformExtentions.toFile,
	       UI.designPlatformExtentions.allPages,
	       UI.designPlatformExtentions.cmd[],
	       UI.designPlatformExtentions.fileName[],
	       1,pl.noOfElements) -> pd.open;
	    'Print' -> pd.title;
	    pd.showModal;
	if)
     #);
   INNER execute

-- bifrostPageLoadDrawMaybe: descriptor --
(# 
do
#)

-- repositionTextAdjust: descriptor --
(#
do
   tc.y - 2 -> tc.y
#)

-- designPlatformExtentions:descriptor --
(#
   toFile,allPages: @Boolean;
   cmd,filename: ^text
do
   (if UI.verbose then
       'designPlatformExtentions start' -> putLine
   if);
   false -> toFile;
   true -> allPages;
   '/usr/local/bin/lpr' -> cmd[];
   '/tmp/freja-print.ps' -> filename[];
   (if UI.verbose then
       'designPlatformExtentions end' -> putLine
   if);
#)
