ORIGIN 'printdialog';
INCLUDE '~beta/guienv/utils/dialogfield';
-- printDialogprivate: Descriptor --
(#
   OKButton: @pushButton
     (#
	eventHandler::
	  (#
	     onMouseUp::
	       (#
	       do
		  FileRB.state -> toFile;
		  PrintAllPages.state -> allPages;
		  CommandText.contents -> cmd[];
		  FileText.contents -> filename[];
		  PrintFrom.IntegerContents -> from;
		  PrintTo.IntegerContents -> to;
		  onOK
	       #)
	  #);
        open::<
          (# 
          do
             true->bindBottom;
             false->bindTop;
             true->bindRight;
             false->bindLeft;
             'OK'->label;
             (60,20)->size;
             (309,186)->position
          #)
     #);
   cancelButton: @pushButton
     (#
	eventHandler::
	  (#
	     onMouseDown::(# do onCancel #)
	  #);
        open::<
          (# 
          do
             true->bindBottom;
             false->bindTop;
             true->bindRight;
             false->bindLeft;
             'Cancel'->label;
             (60,20)->size;
             (389,186)->position
          #)
     #);
   CommandRB: @radioButton
     (#
	eventHandler::
	  (#
	     onStateChanged::
	       (#
	       do (if state = TRUE then
		      FALSE -> fileRB.state;
		      commandText.enable
		   else
		      commandText.disable
		  if)
	       #);
	     onLabelChanged:: (# do fitToContents #)
	  #);
        open::<
          (# 
          do
             'Command:'->label; false->state; (69,17)->size; (20,32)->position;
          #)
     #);
   FileRB: @radioButton
     (#
	eventHandler::
	  (#
	     onStateChanged::
	       (#
	       do (if state = TRUE then
		      FALSE -> commandRB.state;
		      fileText.enable
		   else
		      fileText.disable
		  if)
	       #);
	     onLabelChanged:: (# do fitToContents #)
	  #);
        open::<
          (# 
          do 'File:'->label; false->state; (51,17)->size; (20,70)->position; 
          #)
     #);
   CommandText: @editText
     (#
        open::<
          (#  do true->bindRight; (349,30)->size; (100,25)->position;  #)
     #);
   PrintAllPages: @radioButton
     (#
	eventHandler::
	  (#
	     onStateChanged::
	       (#
	       do
		  (if state = TRUE then
		      FALSE -> PrintRangeRB.state
		  if)
	       #);
	     onLabelChanged:: (# do fitToContents #)
	  #);
        open::<
          (# 
          do
             'Print all pages'->label;
             false->state;
             (111,17)->size;
             (21,148)->position;
             
          #)
     #);
   PrintRangeRB: @radioButton
     (#
	eventHandler::
	  (#
	     onStateChanged::
	       (#
	       do
		  (if state = TRUE then
		      FALSE -> PrintAllPages.state;
		      printFrom.enable;
		      printTo.enable
		   else
		      printFrom.disable;
		      printTo.disable
		  if)
	       #);
	     onLabelChanged:: (# do fitToContents #)
	  #);
        open::<
          (# 
          do
             'Print from page:'->label;
             false->state;
             (117,17)->size;
             (151,148)->position;
             
          #)
     #);
   printFrom: @numberField
     (# open::< (#  do (57,30)->size; (273,141)->position;  #) #);
   PrintToLabel: @staticText
     (#
        open::<
          (#  do 'to page:'->label; (52,17)->size; (335,148)->position;  #)
     #);
   PrintTo: @numberField
     (# open::< (#  do (57,30)->size; (392,141)->position;  #) #);
   FileText: @editText
     (#
        open::<
          (#  do true->bindRight; (349,30)->size; (100,63)->position;  #)
     #)
#)  

-- printDialogOpen: DoPart --
do
   (463,220)->size;
   private.OKButton.open;
   private.cancelButton.open;
   private.CommandText.open;
   private.CommandRB.open;
   private.FileText.open;
   private.FileRB.open;
   private.PrintAllPages.open;
   private.printFrom.open;
   private.printTo.open;
   private.PrintRangeRB.open;
   private.printToLabel.open;
   (if toFile then
       true -> private.FileRB.state
    else
       true -> private.CommandRB.state
   if);
   (if allPages then
       true -> private.PrintAllPages.state
    else
       true -> private.PrintRangeRB.state
   if);
   cmd[] -> private.CommandText.contents;
   filename[] -> private.FileText.contents;
   from -> private.PrintFrom.integerContents;
   to -> private.PrintTo.integerContents;
   INNER
