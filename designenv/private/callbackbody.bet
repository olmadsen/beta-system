ORIGIN '../designenv';
(************************ CALLBACKS ****************************)
INCLUDE 'pagebody'
        'documentbody';
-- EnableCallBacks: Descriptor --
(# 
do
   enable.AbortCallBack;
   enable.AttachCallBack;
   enable.CoarsenCallBack;
   enable.ConnGrammarCallBack;
   enable.CreateObjectCallBack;
   enable.DelConnGramCallBack;
   enable.DeleteConnCallBack;
   enable.DoubleClickCallBack;
   enable.DuplicateCallBack;
   enable.MakeRegionGrammarCallBack;
   enable.MenuEventCallBack;
   enable.MergeGrammarCallBack;
   enable.PrePageChangeCallBack;
   enable.PageNameNumberCallBack;
   enable.PostPageStructChangeCallBack;
   enable.PostLoadPageCallBack;
   enable.PostMenuEventCallBack;
   enable.PostObjectSelectCallBack;
   enable.PostObjectSizeCallBack;
   enable.PreDeleteNodeCallBack;
   enable.PreDeleteRegionCallBack;
   enable.PreLoadPageCallBack;
   enable.ProcessDragCallBack;
   enable.RefineCallBack;
   enable.UnmakeRegionGrammarCallBack;
   enable.ConnCreatedCallBack;
   enable.NodeCreatedCallBack;
   enable.PostPageChangedCallBack;
   enable.PageCreatedCallBack;
   enable.CreatingConnCallBack;
   enable.ReattachGrammarCallBack;
   enable.MouseDownCallBack;
   enable.PaletteClickCallBack;
   
#)  

-- CallBackAbort: Descriptor --
(# thePage: ^theDocument.Page; 
do
   statusbar.reset;
   (if theDocument[] <> none
    // true then
       theDocument.CurrentPage->thePage[];
       (if thePage[] <> none
        // true then thePage.DesignObject##->thePage.DesignObjectDesc##
       if)
   if)
#)  

-- CallBackAttach: Descriptor --
(#  do  #)  

-- CallBackCoarsen: Descriptor --
(#
   thePage: ^theDocument.Page;
   theNode: ^thePage.Node;
   nodeList,connList: ^ObjectList;
   crossConn: ^thePage.CrossConnector;
   crossList: ^thePage.CrossConnectorList;
   
do
   theDocument.CurrentPage->thePage[];
   (if thePage[] <> none
    // true then
       (if callBackVerbose
        // true then
             (# o: ^thePage.DesignObject; 
             do
                'CoarseNode: '->puttext;
                coarseNodeID->putint;
                newline;
                thePage.currentobject->o[];
                'CurrentObject: '->puttext;
                o.id->putint;
                newline;
                
             #);
           
       if);
       theDocument.PrivatePart.coarsening+1->theDocument.PrivatePart.coarsening;
       true->theDocument.PrivatePart.doingCoarse;
       coarseNodeID->theDocument.theObjectList.Find->theNode[];
       ninlist->theDocument.theObjectList.NodeIDsToList->nodeList[];
       cinlist->theDocument.theObjectList.NodeIDsToList->connList[];
       (if cxnlist.range <> 0
        // true then &thePage.CrossConnectorList[]->crossList[]; 
       if);
       (for i: cxnlist.range repeat
         &thePage.CrossConnector[]->crossConn[];
         cxnList[i]->theDocument.theObjectList.Find->crossConn.conn[];
         cxnFlag[i]->crossConn.end1;
         nxnList[i]->theDocument.theObjectList.Find->crossConn.exNode[];
         nInList[i]->theDocument.theObjectList.Find->crossConn.inNode[];
         crossConn[]->crossList.insert;
         
       for);
       (theNode[],nodeList[],connList[],crossList[])
         ->thePage.PageCallBack.makeCoarse;
       
    else
       'CallBackCoarsen: no page'->putline; 
   if);
   
#)  

-- CallBackRefine: Descriptor --
(#  do  #)  

-- CallBackMenuEvent: Descriptor --
(# m: ^BasicMenu; i: ^m.BasicItem; 
do
   theMenu->MenuBar.findMenu->m[];
   (if m[] = none
    // false then
       theItem->m.ItemList.findItem->i[];
       (if i[] = none
        // false then
           (if i.PreCall // true then i.hit;  if); i.proceed->ok; 
        else
           'Unknown menu item: '->puttext; theItem->putint; newline
       if);
       
    else
       'Unknown menu: '->puttext; theMenu->putint; newline
   if);
   
#)  

-- CallBackPostMenuEvent: Descriptor --
(# m: ^BasicMenu; i: ^m.BasicItem; 
do
   theMenu->MenuBar.findMenu->m[];
   (if m[] <> none
    // true then
       theItem->m.ItemList.findItem->i[];
       (if i[] <> none
        // true then (if i.PreCall // false then i.hit;  if); 
        else
           'Unknown menu item'->puttext; theItem->putint; newline; 
       if);
       
    else
       'Unkown menu'->puttext; theMenu->putint; newline; 
   if);
   
#)
(* Not available in this release
 ---- CallBackPreLoadPage: descriptor ----
 (#
 do 'CallBackPreLoadPage' -> putline;
 #)
 
 ---- CallBackPostLoadPage: descriptor ----
 (# 
 do 'CallBackPostLoadPage' -> putline;
 #)
 *)  

-- CallBackPageChanged: Descriptor --
(# thePage: ^theDocument.Page; theObjectDesc: ##Object; 
do
   (if callbackVerbose // true then 'CallBackPageChanged'->putline if);
   (if theDocument[]
    // none then
       (if callBackVerbose
        // true then 'Page Changed: No document!!'->putline
       if)
    else
       (if theDocument.privatePart.InteractiveCreatedObject[] <> none
        // true then
        (* creating an object interactive on a specified page
         * select that page again *)
           (if theDocument.CurrentPage.p[] <> none
            // true then
               enable.PostPageChangedCallBack;
               theDocument.CurrentPage.p.ID->DSStrSetCurPage;
               disable.PostPageChangedCallBack;
               
            else
               'InteractiveCreate: no page'->putline; 
           if);
           
        else
           (if theDocument.CurrentPage.p[] <> none
            // true then
               (if callbackVerbose
                // true then 'Deactivating old page'->putline
               if);
               theDocument.CurrentPage.p.Deactivate;
               (* save the ObjectDescriptor for create menu *)
               theDocument.CurrentPage.p.DesignObjectDesc##->theObjectDesc##;
               
            else
               'No old page'->putline; 
           if);
           (* get the new current page *)
           pageID->theDocument.theObjectList.Find->thePage[];
           (if thePage[] <> none
            // true then
               (if callBackVerbose
                // true then
                   'Activate new page: '->puttext; thePage.ID->putint; newline; 
               if);
               thePage.Activate;
               theObjectDesc##->thePage.DesignObjectDesc##;
               
            else
               (if callbackVerbose // true then 'No new page'->putline if);
               (if theDocument.PrivatePart.coarsening > 0
                // true then
                   (if callBackVerbose
                    // true then
                       'Making a coarse page id: '->puttext;
                       pageID->putint;
                       newline;
                       
                   if);
                   &theDocument.DesignPage[]->thePage[];
                   pageID->thePage.onInit;
                   thePage.onReadDiagram;
                   (* read in the nodes and connectors *)
                   theDocument.PrivatePart.coarsening-1
                     ->theDocument.PrivatePart.coarsening;
                   
               if);
               
           if);
           
       if)
   if)
#)  

-- CallBackCreateObject: Descriptor --
(# thePage: ^theDocument.Page; 
do (* The created object is currentobject *)
   theDocument.CurrentPage->thePage[];
   (if thePage[] <> none
    // true then DSStrGetCurObject->thePage.PageCallBack.MakeDescObject; 
   if);
   
#)  

-- CallBackDuplicate: Descriptor --
(# thePage: ^theDocument.Page; 
do
   (if callBackVerbose
    // true then
       'CallbackDuplicate'->putline;
       'Nodes: '->puttext;
       (for i: nodes.range repeat nodes[i]->putint; ' '->put for);
       newline;
       
   if);
   theDocument.CurrentPage->thePage[];
   (if thePage[] <> none
    // true then
       nodes->thePage.PageCallBack.MakeObjectsFromList;
       conns->thePage.PageCallBack.MakeObjectsFromList;
       
   if);
   
#)  

-- CallBackConnGrammar: Descriptor --
(# thePage: ^theDocument.Page; 
do
   (if callBackVerbose // true then 'CallBackConnGrammar'->putline if);
   (if theDocument.PrivatePart.coarsening
    // 1 then
       theDocument.CurrentPage->thePage[];
       (if thePage[] <> none
        // true then
           'ConnGrammar: here we like to make a new connector'->putline;
           (* DSStrGetCurObject -> thePage.PageCallBack.MakeDescObject; *)
           
       if);
       
   if);
   
#)  

-- CallBackPostObjectSelect: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   theDocument.currentPage->thePage[];
   objID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then 'Can''t find selected object'->putline; 
    else
   (*theObject.onSelect;*)
       
   if);
   
#)  

-- CallBackMouseDown: Descriptor --
(#
   o: @Integer;
   thePage: ^theDocument.Page;
   theObject: ^thePage.DesignObject;
   
do
   (if callbackVerbose // true then 'Mousedown'->putline if);
   INNER MouseDown;
   (mouseStateP.x,mouseStateP.y,28)->DSUtilPointInObject->o;
   (if callbackVerbose // true then o->putInt; newLine if);
   (if not locked then
       (if callbackVerbose // true then 'not Locked'->putline if);
       (if o
        // 0 then
           (if callbackVerbose // true then 'No object'->putline if); 
        else
           theDocument.currentPage->thePage[];
           o->theDocument.theObjectList.Find->theObject[];
           (if theObject[]
            // none then 'Can''t find selected object'->putline; 
            else
               theObject.onSelect; o->oldObjectId; 
           if);
           
       if);
       false->ok;
       false->oldLocked
    else
   (* allow double-click only if not locked on the first click *)
       (if (o = oldObjectID) and (not oldLocked) then
           (if o
            // 0 then
               (if callbackVerbose // true then 'No object'->putline if); 
            else
               theDocument.currentPage->thePage[];
               o->theDocument.theObjectList.Find->theObject[];
               (if theObject[]
                // none then 'Can''t find selected object'->putline; 
                else
                   theObject.onSelect; true->oldLocked; 0->oldObjectId; 
               if);
               
           if);
           false->ok;
           
        else
           ascii.bel->screen.put
       if);
       
   if);
   
#)  

-- CallBackPostObjectSize: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   theDocument.currentPage->thePage[];
   objID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then 'Can''t find selected object'->putline; 
    else
       theObject.Size; 
   if);
   
#)  

-- CallBackDoubleClick: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do (* objID is id of the object *)
   ;
   (if callbackVerbose // true then 'DoubleClick'->putline if);
   INNER DoubleClick;
   (if true (*not locked*) then
       (if callbackVerbose // true then 'not Locked'->putline if);
       theDocument.currentPage->thePage[];
       objID->theDocument.theObjectList.Find->theObject[];
       (if theObject[]
        // none then
           'Can''t find double click object '->puttext; objID->putint; newline; 
        else
           theObject.onDoubleClick->ok; 
       if)
   if);
   
#)  

-- CallBackDrag: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do (* objID is id of the object *)
   ;
   theDocument.currentPage->thePage[];
   objID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then 'Can''t find drag object'->putline; 
    else
       theObject.onDrag; 
   if);
   
#)  

-- CallBackMakeRgnGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do (* sobjID is id of the object becoming a region *)
   ;
   (if callBackVerbose // true then 'CallbackMakeRgnGrammar'->putline if);
   theDocument.currentPage->thePage[];
   sobjID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then
       'MakeRegion: Can''t find object to become a region'->putline; 
    else
       (tobjID->theDocument.theObjectList.Find,
        cList->theDocument.theObjectList.NodeIDsToList)->theObject.onInitRegion;
       
   if);
   
#)  

-- CallBackUnmakeRegionGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   (if callBackVerbose
    // true then
       'CallbackDelRegionGrammar: ID '->puttext; rgnID->putint; newline; 
   if);
   theDocument.currentPage->thePage[];
   rgnID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then
       'UnMakeRegion: Can''t find region to become a object'->putline; 
    else
       false->theObject.isRegion; 
   if);
   
#)  

-- CallBackDelNodeGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   (if callBackVerbose
    // true then 'CallbackDelNodeGrammar'->puttext; nodeID->putint; newline; 
   if);
   theDocument.currentPage->thePage[];
   nodeID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then 'Can''t find node to delete'->putline; 
    else
       theObject.onRemove; 
   if);
   
#)  

-- CallBackDelConnGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   (if callBackVerbose
    // true then 'CallbackDelConnGrammar'->puttext; connID->putint; newline; 
   if);
   theDocument.currentPage->thePage[];
   connID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then 'Can''t find connector to delete'->putline; 
    else
       theObject.onRemove; 
   if);
   
#)  

-- CallBackDelRegionGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   (if callBackVerbose
    // true then 'CallbackDelRegionGrammar'->puttext; rgnID->putint; newline; 
   if);
   theDocument.currentPage->thePage[];
   (if thePage[] <> none then
       rgnID->theDocument.theObjectList.Find->theObject[];
       (if theObject[]
        // none then 'Can''t find region to delete'->putline; 
        else
           theObject.onRemove; 
       if)
   if);
   
#)  

-- CallbackPaletteClick: Descriptor --
(#
   anIDObject: ^IDObject;
   thePalette: ^theDocument.Palette;
   theObject: ^thePalette.DesignObject;
   parent: @Integer;
   
do
   objID->parent;
   (if theDocument[]
    // none then 'PaletteClick: No Document'->screen.putline; 
    else
       loop:
         (# 
         do
            parent->DSStrGetParent->theDocument.theObjectList.Find
              ->anIDObject[];
            (if anIDObject[]
             // none then 'PaletteClick: No ID object'->putline; 
             else
                (if anIDObject.struc <= theDocument.Page##
                 // true then (* it is a page => a palette *)
                    anIDObject[]->thePalette[];
                    objID->theDocument.theObjectList.Find->theObject[];
                    (if theObject[]
                     // none then
                        'PaletteClick: No palette object'->screen.putline; 
                     else
                        theObject[]->thePalette.onSelect; 
                    if);
                    
                 else
                    anIDObject.ID->parent; restart loop; 
                if);
                
            if);
            
         #);
       
   if)
#)  

