ORIGIN 'designobjectbody';
(************************ CALLBACKS ****************************)
INCLUDE 'pagebody';
-- EnableCallBacks: Descriptor --
(# 
do
   enable.AbortCallBack;
   enable.AttachCallBack;
   enable.CoarsenCallBack;
   enable.ConnGrammarCallBack;
   enable.CreateObjectCallBack;
   enable.DelConnGramCallBack;
   enable.DeleteConnCallBack;
   enable.DoubleClickCallBack;
   enable.DuplicateCallBack;
   enable.MakeRegionGrammarCallBack;
   enable.MenuEventCallBack;
   enable.MergeGrammarCallBack;
   enable.PrePageChangeCallBack;
   enable.PageNameNumberCallBack;
   enable.PostPageStructChangeCallBack;
   enable.PostLoadPageCallBack;
   enable.PostMenuEventCallBack;
   enable.PostObjectSelectCallBack;
   enable.PostObjectSizeCallBack;
   enable.PreDeleteNodeCallBack;
   enable.PreDeleteRegionCallBack;
   enable.PreLoadPageCallBack;
   enable.ProcessDragCallBack;
   enable.RefineCallBack;
   enable.UnmakeRegionGrammarCallBack;
   enable.ConnCreatedCallBack;
   enable.NodeCreatedCallBack;
   enable.PostPageChangedCallBack;
   enable.PageCreatedCallBack;
   enable.CreatingConnCallBack;
   enable.ReattachGrammarCallBack;
   enable.MouseDownCallBack;
   enable.PaletteClickCallBack;
   enable.ConnEndsGrammarCallBack;
   enable.KbdEventCallBack;
   enable.TitlebarClickedCallback;
   
#)  

-- CallBackAbort: Descriptor --
(# thePage: ^theDocument.Page; 
do
   statusbar.reset;
   (if theDocument[] <> none
    // true then
       theDocument.CurrentPage->thePage[];
       (if thePage[] <> none
        // true then thePage.DesignObject##->thePage.DesignObjectDesc##
       if)
   if);
   abortCommand;
   INNER Abort
#)  

-- CallBackAttach: Descriptor --
(#  do  #)  

-- CallBackCoarsen: Descriptor --
(#
   thePage: ^theDocument.Page;
   theNode: ^thePage.Node;
   nodeList,connList: ^ObjectList;
   crossConn: ^thePage.CrossConnector;
   crossList: ^thePage.CrossConnectorList;
   
do
   theDocument.CurrentPage->thePage[];
   (if thePage[] <> none
    // true then
       (if callBackVerbose
        // true then
             (# o: ^thePage.DesignObject; 
             do
                'CoarseNode: '->puttext;
                coarseNodeID->putint;
                newline;
                thePage.currentobject->o[];
                'CurrentObject: '->puttext;
                o.id->putint;
                newline;
                
             #);
           
       if);
       theDocument.PrivatePart.coarsening+1->theDocument.PrivatePart.coarsening;
       true->theDocument.PrivatePart.doingCoarse;
       coarseNodeID->theDocument.theObjectList.Find->theNode[];
       ninlist->theDocument.theObjectList.NodeIDsToList->nodeList[];
       cinlist->theDocument.theObjectList.NodeIDsToList->connList[];
       (if cxnlist.range <> 0
        // true then &thePage.CrossConnectorList[]->crossList[]; 
       if);
       (for i: cxnlist.range repeat
         &thePage.CrossConnector[]->crossConn[];
         cxnList[i]->theDocument.theObjectList.Find->crossConn.conn[];
         cxnFlag[i]->crossConn.end1;
         nxnList[i]->theDocument.theObjectList.Find->crossConn.exNode[];
         nInList[i]->theDocument.theObjectList.Find->crossConn.inNode[];
         crossConn[]->crossList.insert;
         
       for);
       (theNode[],nodeList[],connList[],crossList[])
         ->thePage.PageCallBack.makeCoarse;
       
    else
       'CallBackCoarsen: no page'->putline; 
   if);
   
#)  

-- CallBackRefine: Descriptor --
(#  do  #)  

-- CallBackMenuEvent: Descriptor --
(# m: ^BasicMenu; i: ^m.BasicItem; 
do
   theMenu->MenuBar.findMenu->m[];
   (if m[] <> none then
       theItem->m.ItemList.findItem->i[];
       (if i[] <> none then
           (if i.PreCall then INNER MenuEvent; i.hit;  if); i.proceed->ok; 
        else
           'Unknown menu item: '->puttext; theItem->putint; newline
       if);
       
    else
       'Unknown menu: '->puttext; theMenu->putint; newline
   if);
   
#)  

-- CallBackPostMenuEvent: Descriptor --
(# m: ^BasicMenu; i: ^m.BasicItem; 
do
   theMenu->MenuBar.findMenu->m[];
   (if m[] <> none
    // true then
       theItem->m.ItemList.findItem->i[];
       (if i[] <> none
        // true then
           (if i.PreCall // false then i.hit;  if); INNER PostMenuEvent; 
        else
           'Unknown menu item'->puttext; theItem->putint; newline; 
       if);
       
    else
       'Unkown menu'->puttext; theMenu->putint; newline; 
   if);
   
#)
(* Not available in this release
 ---- CallBackPreLoadPage: descriptor ----
 (#
 do 'CallBackPreLoadPage' -> putline;
 #)
 
 ---- CallBackPostLoadPage: descriptor ----
 (# 
 do 'CallBackPostLoadPage' -> putline;
 #)
 *)  

-- CallBackPageChanged: Descriptor --
(# thePage: ^theDocument.Page; theObjectDesc: ##Object; 
do
   (if callbackVerbose // true then 'CallBackPageChanged'->putline if);
   (if theDocument[]
    // none then
       (if callBackVerbose
        // true then 'Page Changed: No document!!'->putline
       if)
    else
       (if theDocument.privatePart.InteractiveCreatedObject[] <> none
        // true then
        (* creating an object interactive on a specified page
         * select that page again *)
           (if theDocument.CurrentPage.p[] <> none
            // true then
               enable.PostPageChangedCallBack;
               theDocument.CurrentPage.p.ID->UI.DSStrSetCurPage;
               disable.PostPageChangedCallBack;
               
            else
               'InteractiveCreate: no page'->putline; 
           if);
           
        else
           (if theDocument.CurrentPage.p[] <> none
            // true then
               (if callbackVerbose
                // true then 'Deactivating old page'->putline
               if);
               theDocument.CurrentPage.p.Deactivate;
               theDocument.CurrentPage.p.DesignObjectDesc##->theObjectDesc##;
               
            else
               'No old page'->putline; 
           if);
           (* get the new current page *)
           pageID->theDocument.theObjectList.Find->thePage[];
           (if thePage[] <> none
            // true then
               (if callBackVerbose
                // true then
                   'Activate new page: '->puttext; thePage.ID->putint; newline; 
               if);
               thePage.Activate;
               theObjectDesc##->thePage.DesignObjectDesc##;
               
            else
               (if callbackVerbose // true then 'No new page'->putline if);
               (if theDocument.PrivatePart.coarsening > 0
                // true then
                   (if callBackVerbose
                    // true then
                       'Making a coarse page id: '->puttext;
                       pageID->putint;
                       newline;
                       
                   if);
                   &theDocument.DesignPage[]->thePage[];
                   pageID->thePage.onInit;
                   thePage.onReadDiagram;
                   theDocument.PrivatePart.coarsening-1
                     ->theDocument.PrivatePart.coarsening;
                   
               if);
               
           if);
           
       if)
   if)
#)  

-- CallBackCreateObject: Descriptor --
(# thePage: ^theDocument.Page; 
do (* The created object is currentobject *)
   theDocument.CurrentPage->thePage[];
   (if thePage[] <> none then
       true->theDocument.Modified;
       UI.DSStrGetCurObject->thePage.PageCallBack.MakeDescObject;
       
   if);
   
#)  

-- CallBackDuplicate: Descriptor --
(# thePage: ^theDocument.Page; 
do
   (if callBackVerbose
    // true then
       'CallbackDuplicate'->putline;
       'Nodes: '->puttext;
       (for i: nodes.range repeat nodes[i]->putint; ' '->put for);
       newline;
       
   if);
   theDocument.CurrentPage->thePage[];
   (if thePage[] <> none
    // true then
       nodes->thePage.PageCallBack.MakeObjectsFromList;
       conns->thePage.PageCallBack.MakeObjectsFromList;
       
   if);
   
#)  

-- CallBackKbdEvent: DoPart --
do
   (if theDocument[] <> none then
       (1,keyNrP)->getIntList->L;
       (1,asciiCodeP)->getIntList->A;
       (KbdStateP.shiftKey,kbdStateP.controlKey,kbdStateP.altKey,
        kbdStateP.shiftLockKey,L[1],A[1])->theDocument.onKeyDown->ok;
       INNER KbdEvent
    else
       true->ok
   if)  

-- CallBackConnEndsGrammar: DoPart --
do
   theDocument.currentPage->thePage[];
   conn->theDocument.theObjectList.find->theObject[];
   (if theObject[] <> none then
       (if theObject## <= thePage.connector## then
           NodeID->theDocument.theObjectList.find->theNode[];
           (if theNode[] <> none then
               true->theDocument.Modified;
               theObject[]->theConn[];
               (theNode[],WhichEnd)->theConn.onReattach->ok
            else
               'Cannot find new connector end '->puttext;
               nodeID->putint;
               newline
           if)
       if)
    else
       'Cannot find reattached connector '->puttext; conn->putint; newline
   if)  

-- CallBackConnGrammar: Descriptor --
(# thePage: ^theDocument.Page; 
do
   (if callBackVerbose // true then 'CallBackConnGrammar'->putline if);
   (if theDocument.PrivatePart.coarsening = 1 then
       theDocument.CurrentPage->thePage[];
       (if thePage[] <> none then
           true->theDocument.Modified;
           'ConnGrammar: here we like to make a new connector'->putline;
           (* DSStrGetCurObject -> thePage.PageCallBack.MakeDescObject; *)
           
       if);
       
   if);
   
#)  

-- CallBackPostObjectSelect: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   true->checkRgns;
   theDocument.currentPage->thePage[];
   (if callbackVerbose
    // true then 'PostObjectSelect: '->puttext; objID->putInt; newLine
   if);
   objID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then 'Can''t find selected object'->putline; 
    else
       theObject.onSelect
   if);
   
#)  

-- CallBackMouseDown: Descriptor --
(# thePage: ^theDocument.Page; 
do
   theDocument.currentpage->thePage[];
   (if thePage[] <> none then
       (MouseStateP.x,MouseStateP.y,MouseStateP.leftButton,
        MouseStateP.middleButton,MouseStateP.rightButton,kbdStateP.shiftKey,
        kbdStateP.controlKey,kbdStateP.altKey,kbdStateP.shiftLockKey)
         ->thePage.onMouseDown->ok
    else
       'MouseDown: No currentPage??!'->putline
   if);
   
#)  

-- CallBackPostObjectSize: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   theDocument.currentPage->thePage[];
   objID->theDocument.theObjectList.Find->theObject[];
   (if theObject[] = none then
       'Can''t find selected object'->putline; 
    else
       true->theDocument.Modified; theObject.onSize; 
   if);
   
#)  

-- CallBackDoubleClick: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do (* objID is id of the object *)
   ;
   (if callbackVerbose // true then 'DoubleClick'->putline if);
   INNER DoubleClick;
   theDocument.currentPage->thePage[];
   objID->theDocument.theObjectList.Find->theObject[];
   (if theObject[]
    // none then
       'Can''t find double click object '->puttext; objID->putint; newline; 
    else
       theObject.onDoubleClick->ok; 
   if);
   
#)  

-- CallBackDrag: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do (* objID is id of the object *)
   theDocument.currentPage->thePage[];
   objID->theDocument.theObjectList.Find->theObject[];
   (if theObject[] = none then
       'Can''t find drag object'->putline; 
    else
       true->theDocument.Modified; theObject.onDrag
   if)
#)  

-- CallBackMakeRgnGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do (* sobjID is id of the object becoming a region *)
   (if callBackVerbose then 'CallbackMakeRgnGrammar'->putline if);
   theDocument.currentPage->thePage[];
   sobjID->theDocument.theObjectList.Find->theObject[];
   (if theObject[] = none then
       'MakeRegion: Can''t find object to become a region'->putline; 
    else
       true->theDocument.Modified;
       (tobjID->theDocument.theObjectList.Find,
        cList->theDocument.theObjectList.NodeIDsToList)->theObject.onInitRegion;
       
   if);
   INNER MakeRegionGrammar;
   
#)  

-- CallBackUnmakeRegionGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   (if callBackVerbose then
       'CallbackDelRegionGrammar: ID '->puttext; rgnID->putint; newline; 
   if);
   theDocument.currentPage->thePage[];
   rgnID->theDocument.theObjectList.Find->theObject[];
   (if theObject[] = none then
       'UnMakeRegion: Can''t find region to become a object'->putline; 
    else
       true->theDocument.Modified; false->theObject.isRegion; 
   if);
   
#)  

-- CallBackDelNodeGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   (if callBackVerbose then
       'CallbackDelNodeGrammar'->puttext; nodeID->putint; newline; 
   if);
   theDocument.currentPage->thePage[];
   nodeID->theDocument.theObjectList.Find->theObject[];
   (if theObject[] = none then
       'Can''t find node to delete'->putline; 
    else
       true->theDocument.Modified; theObject.onRemove; 
   if);
   
#)  

-- CallBackDelConnGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   (if callBackVerbose then
       'CallbackDelConnGrammar'->puttext; connID->putint; newline; 
   if);
   theDocument.currentPage->thePage[];
   connID->theDocument.theObjectList.Find->theObject[];
   (if theObject[] = none then
       'Can''t find connector to delete'->putline; 
    else
       true->theDocument.Modified; theObject.onRemove; 
   if);
   
#)  

-- CallBackDelRegionGrammar: Descriptor --
(# thePage: ^theDocument.Page; theObject: ^thePage.DesignObject; 
do
   (if callBackVerbose then
       'CallbackDelRegionGrammar'->puttext; rgnID->putint; newline; 
   if);
   theDocument.currentPage->thePage[];
   (if thePage[] <> none then
       rgnID->theDocument.theObjectList.Find->theObject[];
       (if theObject[] = none then
           'Can''t find region to delete'->putline; 
        else
           true->theDocument.Modified; theObject.onRemove; 
       if)
   if);
   
#)  

-- CallbackPaletteClick: Descriptor --
(#
   anIDObject: ^IDObject;
   thePalette: ^theDocument.Palette;
   theObject: ^thePalette.DesignObject;
   parent: @Integer;
   
do
   objID->parent;
   (if theDocument[]
    // none then 'PaletteClick: No Document'->screen.putline; 
    else
       loop:
         (# 
         do
            parent->UI.DSStrGetParent->theDocument.theObjectList.Find
              ->anIDObject[];
            (if anIDObject[]
             // none then 'PaletteClick: No ID object'->putline; 
             else
                (if anIDObject## <= theDocument.Page##
                 // true then (* it is a page => a palette *)
                    anIDObject[]->thePalette[];
                    objID->theDocument.theObjectList.Find->theObject[];
                    (if theObject[]
                     // none then
                        'PaletteClick: No palette object'->screen.putline; 
                     else
                        theObject[]->thePalette.onSelect; 
                    if);
                    
                 else
                    anIDObject.ID->parent; restart loop; 
                if);
                
            if);
            
         #);
       
   if)
#)  

