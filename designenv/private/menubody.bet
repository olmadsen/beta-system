ORIGIN '../designenv';
(*************************** INIT  ******************************)
INCLUDE 'documentbody'
-- InitMenus: DescriptorForm --
(#
   Delete:
     (# i: @integer;
     enter i
     do
        (if i->DSMenuDeleteMenu
         // false then
            'InitMenus: Cannot delete menu'->puttext; i->putint; newline;
        if);
     #);
do (*
    10000 -> Delete; (* remove the ML menu *) DSMenuShowMenuBar;
#)
(************************* GENERAL  **********************************)  

-- MenuSeperator: DescriptorForm --
(# do (sepID,'-',ID,0)->DSMenuInsertItem; #)  

-- MenuEnable: DescriptorForm --
(#
do
   (if (id,MenuEnableFlag)->DSMenuSetAttr
    // false then 'Menu: can''t enable menu'->putline
   if);
#)  

-- MenuDisable: DescriptorForm --
(#
do
   (if (id,MenuDiableFlag)->DSMenuSetAttr
    // false then 'Menu: can''t disable menu'->putline
   if);
#)
(************************* USER MENU  **********************************)  

-- UserMenuInsert: DescriptorForm --
(#
do (* Insert the user menu into Design/OA *)
   (if ID >= 10000 (* below 10000 is Design/OA menus *)
    // true then
       (if (ID,BeforeMenuID)->DSMenuInsertMenu
        // false then 'DesignMenuInit: can''t create new menu'->putline
       if);
       (if (ID,name)->DSMenuRename
        // false then 'MenuInsert can''t rename menu'->putline
       if);
    else
       (if verbose
        // true then
           'Design/OA menu already inserted, cannot overwrite'->putline;
       if);
   if);
   (if ItemList.size
    // 0 then (* no items: remove dummy item *)
       (if ID+1->DSMenuDeleteItem
        // false then 'UserMenuDeleteItem: can''t delete item 1'->putline
       if);
       (if ID+2->DSMenuDeleteItem
        // false then 'UserMenuDeleteItem: can''t delete item 2'->putline
       if);
   if);
   (if ItemList.size
    // 1 then (* one item: remove dummy item two *)
       (if ID+2->DSMenuDeleteItem
        // false then 'UserMenuDeleteItem: can''t delete item 2'->putline
       if);
   if);
   (* Insert the items of this user menu into Design/OA menus *)
   ItemList.scan
     (# do Current.Insert #);
#)  

-- UserMenuDelete: DescriptorForm --
(#
do
   (if ID->DSMenuDeleteMenu
    // false then 'UserMenuDelete: can''t delete menu'->putline
   if);
   ItemList.scan
     (# do false->Current.att.shown #);
#)  

-- UserMenuInsertBefore: DescriptorForm --
(#
do
   (if (ID,BeforeMenuID)->DSMenuMoveMenu
    // false then 'UserMenuMove can''t move menu'->putline;
   if);
#)
(***************************** ITEM **********************************)  

-- BasicMenuItemAtt: DescriptorForm --
(# shown,disabled: @Boolean; #)  

-- UserMenuInsertItem: DescriptorForm --
(# (* Insert items into the menu *)
do
   (if itemNumber-ID
    // 1 // 2 then
       (if (itemNumber,name,0)->DSMenuItemRename
        // false then
           'DesignItemInit: can''t rename item: '->puttext;
           itemNumber->putint;
           newline;
       if);
    else
       (if
       (itemNumber,name,ID,0)->DSMenuInsertItem (* 0: the item is appended *)
        // false then
           'DesignItemInit: can''t create new item '->puttext;
           itemNumber->putint;
           newline;
       if);
   if);
   true->att.shown;
   (if att.disabled // true then disable if);
#)  

-- UserMenuDeleteItem: DescriptorForm --
(#
do
   (if itemNumber->DSMenuDeleteItem
    // false then
       'UserMenuDeleteItem: can''t delete item'->puttext;
       itemNumber->putint;
       newline;
   if);
   false->att.shown;
#)  

-- UserMenuItemRename: DescriptorForm --
(#
do
   (if (itemNumber,name,0)->DSMenuItemRename
    // false then
       'UserMenuItemRename: can''t rename item: '->puttext;
       itemNumber->putint;
       newline;
   if);
#)  

-- MenuItemEnable: DescriptorForm --
(#
do
   (if att.shown
    // true then
       (if (itemNumber,MenuEnableFlag)->DSMenuItemSetAttr
        // false then 'Menu: can''t enable item'->putline
       if);
   if);
   false->att.disabled;
#)  

-- MenuItemDisable: DescriptorForm --
(#
do
   (if att.shown
    // true then
       (if (itemNumber,MenuDiableFlag)->DSMenuItemSetAttr
        // false then 'Menu: can''t disable item'->putline;
       if);
   if);
   true->att.disabled;
#)  

-- MenuItemCheck: DescriptorForm --
(#
do (id,itemNumber,checked)->DSMenuCheckItem;
#)
(*********************** DESIGN MENU ********************************)  

-- DesignMenuInsert: DescriptorForm --
(#
do (* Insert the items of this user menu into Design/OA menus *)
   ItemList.scan (# do Current.Insert #);
#)  

-- DesignMenuDelete: DescriptorForm --
(# do ItemList.scan (# do false->Current.att.shown #); #)  

-- DesignMenuInsertItem: DescriptorForm --
(# (* Insert items into the menu *)
do
   (if (ID,itemNumber,name)->DSMenuAppendItem (* 0: the item is appended *)
    // false then
       'DesignItemInit: can''t append new item '->puttext;
       itemNumber->putint;
       newline;
   if);
   true->att.shown;
   (if att.disabled // true then disable if);
#)  

-- DesignMenuDeleteItem: DescriptorForm --
(#
do
   (if itemNumber->DSMenuDeleteItem
    // false then
       'DesignMenuDeleteItem: can''t delete item'->puttext;
       itemNumber->putint;
       newline;
   if);
   false->att.shown;
#)  

-- DesignMenuItemRename: DescriptorForm --
(#
do
   (if (itemNumber,name,0)->DSMenuItemRename
    // false then
       'DesignMenuItemRename: can''t rename item: '->puttext;
       itemNumber->putint;
       newline;
   if);
#)
(***************************** MENUBAR **********************************)  

-- MenuBarInsert: DescriptorForm --
(#
(* add this menu to the design menus
 * this constitutes a problem, since a new menu MUST be created
 * with a resource!!
 *)
do
   e.Insert;
   (* insert the menu and items in the Design/OA menus *)
   DSMenuShowMenuBar;
   (* update the menubar *)
   (if verbose
    // true then 'MenubarInsert: '->puttext; e.ID->putint; newline
   if);
#)  

-- MenuBarDelete: DescriptorForm --
(#
do
   e.Delete;
   (* remove the menu and items in the Design/OA menus *)
   DSMenuShowMenuBar;
   (* update the menubar *)
#)
(***************************** MENU UTIL *****************************)  

-- CheckDesignOAMenus: DescriptorForm --
(# (* remove not installed DesignOA menus *)
   CheckMenu:
     (# theID: @Integer; theMenu: ^BasicMenu;
     enter theID
     do
        theID->MenuBar.findMenu->theMenu[];
        (if theMenu[]
         // none then
            (if theID->DSMenuDeleteMenu
             // false then 'CheckDesignOAMenus: can''t delete menu'->putline
            if);
        if);
     #);
do
   menuID.FileMenu->CheckMenu;
   menuID.EditMenu->CheckMenu;
   menuID.CreateMenu->CheckMenu;
   menuID.MakeUpMenu->CheckMenu;
   menuID.PageMenu->CheckMenu;
   menuID.GroupMenu->CheckMenu;
   menuID.TextMenu->CheckMenu;
   menuID.AlignMenu->CheckMenu;
   DSMenuShowMenuBar;
   (* update the menubar *)
#)
(************************ FILE MENU ***************************)  

-- NewItem: DescriptorForm --
(# newPage: ^theDocument.Page;
do
   (if proceed
    // true then
       (if verbose // true then 'NewItem hitted'->putline if);
       &DesignDocument[]->theDocument[];
       (* make a new document *)
       theDocument.onInit->newPage[];
       (if newPage[]
        // none then
           &theDocument.DesignPage[]->newPage[];
           (* new document makes a new page automatically *)
       if);
       DSStrGetCurPage->newPage.onInit;
       (* init page with id *)
       (if verbose
        // true then
           'NewItem: Page inserted: '->puttext; newPage.ID->putint; newline;
       if);
   if);
#)  

-- OpenItem: DescriptorForm --
(#
   count: @ShortRef;
   list: @IntegerRef;
   pages,nodes,conns,regions: [0] @Integer;
   thePage: ^theDocument.Page;
   theObject: ^thePage.DesignObject;
do
   (if proceed
    // true then
       (if callbackVerbose // true then 'CallBackOpenItem'->putline if);
       (* we are opening an ordinary Design document *)
       &DesignDocument[]->theDocument[];
       (* make a new document *)
       theDocument.onInit;
       (count[],list[])->DSStrGetPageList;
       (count,list)->GetIntList->pages;
       (* Get all pages *)
       (for i: pages.range repeat
         &theDocument.DesignPage[]->thePage[];
         pages[i]->thePage.onInit;
         thePage.onReadDiagram;
       for);
   if);
#)  

-- CloseItem: DescriptorForm --
(# pages: ^ObjectList;
do
   theDocument.GetPages->pages[];
   (if pages[]
    // none then
    else
       pages.scan
         (# dummy: ^theDocument.Page
         do
            current[]->dummy[];
            (if dummy[]
             // none then 'CloseItem: Page is node'->putline;
             else
                dummy.CleanUp
            if);
         #);
   if);
   theDocument.close;
   (* call the close virtual of document *)
#)  

-- LoadPageItem: DescriptorForm --
(#
do 'CallBackLoadPageItem'->putline;
#)
(***************************** EDIT MENU ******************************)  

-- PasteItem: DescriptorForm --
(* this really belongs to the Paste callback
 * but is not available *)
  (#
     thePage: ^Page;
     theObject: ^thePage.DesignObject;
     o: @Integer;
     group: [0] @Integer;
     count: @ShortRef;
     list: @IntegerRef;
     isGroup: @Boolean;
  do
     (if verbose // true then 'PasteItem'->putline if);
     CurrentPage->thePage[];
     true->isGRoup;
     (if (count[],list[])->DSStrGetCurGroup
      // false then
         false->isGroup;
         (if verbose // true then 'Group Error'->putline if);
     if);
     (count,list)->GetIntList->group;
     (if isGroup
      // true then
         (if verbose
          // true then
             'Group: '->puttext;
             (for i: group.range repeat group[i]->putint; ' '->puttext; for);
             newline;
         if);
         (* group -> thePage.PageCallBack.MakeObjectsFromList; *)
      else
         (if verbose // true then 'No Group'->putline if);
         DSStrGetCurObject->o;
         (if verbose
          // true then 'CurrentObject: '->puttext; o->putint; newline;
         if);
         (* o -> thePage.PageCallBack.MakeDesignObject -> theObject; *)
     if);
  #)
  (*************************** PAGE MENU ******************************)  

-- NewPageItem: DescriptorForm --
(# thePage: ^DesignPage;
do
   (if verbose // true then 'NewPageItem hitted'->putline; if);
   &DesignPage[]->thePage[];
   DSStrGetCurPage->thePage.onInit;
   (*  id of the new page *)
   (if verbose
    // true then
       'NewPageItem: Page inserted: '->puttext; thePage.ID->putint; newline;
   if);
#)
(*************************** GROUP MENU ******************************)  

-- CoarsenItemHit: DescriptorForm --
(# (* called after the operation has been done *)
do false->theDocument.privatepart.doingCoarse;
#)
(************* INIT DEFAULT MENUS *****************)
(* id numbers are given to all menus and items of the Design/OA menu *)  

-- InitFileMenu: DescriptorForm --
(#
do
   menuID.FileMenu->id;
   menuID.FileMenu.new->NewItem.init;
   menuID.FileMenu.open->OpenItem.init;
   menuID.FileMenu.close->CloseItem.init;
   menuID.FileMenu.save->SaveItem.init;
   menuID.FileMenu.saveAs->SaveAsItem.init;
   menuID.FileMenu.revert->RevertItem.init;
   menuID.FileMenu.info->GetInfoItem.init;
   menuID.FileMenu.prSet->PageSetupItem.init;
   menuID.FileMenu.prPage->PrintPageItem.init;
   menuID.FileMenu.prPrev->PrintPreviewItem.init;
   menuID.FileMenu.prefs->PreferencesItem.init;
   menuID.FileMenu.loadText->LoadTextItem.init;
   menuID.FileMenu.saveText->SaveTextItem.init;
   menuID.FileMenu.loadField->LoadPageItem.init;
   menuID.FileMenu.saveField->SavePageItem.init;
   menuID.FileMenu.quit->QuitItem.init;
   (if verbose // true then 'InitFileMenu'->putline if);
#)  

-- InitEditMenu: DescriptorForm --
(#
do
   menuID.EditMenu->id;
   menuID.EditMenu.undo->UndoItem.init;
   menuID.EditMenu.cut->CutItem.init;
   menuID.EditMenu.copy->CopyItem.init;
   menuID.EditMenu.paste->PasteItem.init;
   menuID.EditMenu.clear->ClearItem.init;
   menuID.EditMenu.select->SelectItem.init;
   menuID.EditMenu.show->ShowItem.init;
   menuID.EditMenu.status->StatusItem.init;
   (if verbose // true then 'InitEditMenu'->putline if);
#)  

-- InitCreateMenu: DescriptorForm --
(#
do
   menuID.CreateMenu->id;
   menuID.CreateMenu.conn->ConnItem.init;
   menuID.CreateMenu.box->RectItem.init;
   menuID.CreateMenu.round->RndRectItem.init;
   menuID.CreateMenu.ellipse->EllipseItem.init;
   menuID.CreateMenu.poly->PolygonItem.init;
   menuID.CreateMenu.polyReg->RegularPolygonItem.init;
   menuID.CreateMenu.wedge->WedgeItem.init;
   menuID.CreateMenu.picture->LineItem.init;
   menuID.CreateMenu.text->LabelItem.init;
   menuID.CreateMenu.region->RegionItem.init;
   menuID.CreateMenu.unmakeregion->UnmakeRegionItem.init;
   menuID.CreateMenu.setAttr->SetAttrItem.init;
   menuID.CreateMenu.setCAttr->SetCAttrItem.init;
   menuID.CreateMenu.setWedge->SetWedgeItem.init;
   menuID.CreateMenu.setPoly->SetPolyItem.init;
   menuID.CreateMenu.autoPalette->AutoPaletteItem.init;
   (if verbose // true then 'InitCreateMenu'->putline if);
#)  

-- InitMakeUpMenu: DescriptorForm --
(#
do
   menuID.MakeupMenu->id;
   menuID.MakeupMenu.select->SelectItem.init;
   menuID.MakeupMenu.adjust->AdjustItem.init;
   menuID.MakeupMenu.changeShape->ChangeShapeItem.init;
   menuID.MakeupMenu.drag->DragItem.init;
   menuID.MakeupMenu.displace->DisplaceItem.init;
   menuID.MakeupMenu.moveto->MoveToItem.init;
   menuID.MakeupMenu.hideSubStructure->HideSubStructureItem.init;
   menuID.MakeupMenu.showSubStructure->ShowSubStructureItem.init;
   menuID.MakeupMenu.forward->ForwardItem.init;
   menuID.MakeupMenu.backward->BackwardItem.init;
   menuID.MakeupMenu.toFront->ToFrontItem.init;
   menuID.MakeupMenu.mergeAttr->MergeAttrItem.init;
   menuID.MakeupMenu.merge->MergeItem.init;
#)  

-- InitPageMenu: DescriptorForm --
(#
do
   menuID.PageMenu->id;
   menuId.PageMenu.show->ShowItem.init;
   menuId.PageMenu.blowUp->BlowupItem.init;
   menuId.PageMenu.reduce->ReduceItem.init;
   menuId.PageMenu.new->NewItem.init;
   menuId.PageMenu.delete->DeleteItem.init;
   menuId.PageMenu.closepage->ClosePageItem.init;
   menuId.PageMenu.changeFatts->ChangeFattsItem.init;
   menuId.PageMenu.child->ChildItem.init;
   menuId.PageMenu.parent->ParentItem.init;
   menuId.PageMenu.strTree->StrTreeItem.init;
   menuId.PageMenu.default->DefaultItem.init;
   menuId.PageMenu.cleanUp->CleanUpItem.init;
#)  

-- InitGroupMenu: DescriptorForm --
(#
do
   menuID.groupMenu->id;
   menuID.groupMenu.enable->EnableItem.init;
   menuID.groupMenu.form->FormItem.init;
   menuID.groupMenu.sum->SumItem.init;
   menuID.groupMenu.copyDr->CopyDrItem.init;
   menuID.groupMenu.spread->SpreadItem.init;
   menuID.groupMenu.coarsen->CoarsenItem.init;
   menuID.groupMenu.refine->RefineItem.init;
   menuID.groupMenu.attach->AttachItem.init;
   menuID.groupMenu.detach->DetachItem.init;
   menuID.groupMenu.morph->MorphItem.init;
   menuID.groupMenu.domain->DomainItem.init;
   menuID.groupMenu.deskform->DeskFormItem.init;
   menuID.groupMenu.shift->ShiftItem.init;
#)  

-- InitTextMenu: DescriptorForm --
(#
do
   menuID.TextMenu->id;
   menuID.TextMenu.enable->EnableItem.init;
   menuID.TextMenu.fitBox->FitBoxItem.init;
   menuID.TextMenu.textAttr->TextAttrItem.init;
   menuID.TextMenu.selBracket->SelBracketItem.init;
   menuID.TextMenu.selString->SelStringItem.init;
   menuID.TextMenu.scrollString->ScrollStringItem.Init;
   menuID.TextMenu.scrNext->SrcNextItem.init;
   menuID.TextMenu.srcZero->SrcZeroItem.init;
#)  

-- InitAlignMenu: DescriptorForm --
(#
do
   menuID.AlignMenu->id;
   menuID.AlignMenu.hor->HorItem.init;
   menuID.AlignMenu.ver->VerItem.init;
   menuID.AlignMenu.leftLeft->LeftLeftItem.init;
   menuID.AlignMenu.leftRight->LeftRightItem.init;
   menuID.AlignMenu.rightLeft->RightLeftItem.init;
   menuID.AlignMenu.rightRight->RightRightItem.init;
   menuID.AlignMenu.topTop->TopTopItem.init;
   menuID.AlignMenu.topBot->TopBotItem.init;
   menuID.AlignMenu.botTop->BotTopItem.init;
   menuID.AlignMenu.botBot->BotBotItem.init;
   menuID.AlignMenu.cent->CentItem.init;
   menuID.AlignMenu.betw->BetwItem.init;
   menuID.AlignMenu.proj->ProjItem.init;
   menuID.AlignMenu.radial->RadialItem.init;
   menuID.AlignMenu.pickNode->PickNodeItem.init;
#)  

