ORIGIN 'designobjectbody';
(*************************** INIT  ******************************)
INCLUDE 'documentbody';
(*************************** INIT  ******************************)
-- InitMenus: Descriptor --
(#
   Delete:
     (# i: @integer; 
     enter i
     do
        (if i->UI.DSMenuDeleteMenu
         // false then
            'InitMenus: Cannot delete menu'->puttext; i->putint; newline; 
        if);
        
     #);
   
do (*
    10000 -> Delete; (* remove the ML menu *) (UI.DSMenuShowMenuBar); 
#)
(************************* GENERAL  **********************************)  

-- MenuSeperator: Descriptor --
(#  do (sepID,'-',ID,0)->UI.DSMenuInsertItem;  #)  

-- MenuEnable: Descriptor --
(# 
do
   (if (id,UI.MenuEnableFlag)->UI.DSMenuSetAttr
    // false then 'Menu: can''t enable menu'->putline
   if);
   false->att.disabled;
   
#)  

-- MenuDisable: Descriptor --
(# 
do
   (if (id,UI.MenuDiableFlag)->UI.DSMenuSetAttr
    // false then 'Menu: can''t disable menu'->putline
   if);
   true->att.disabled;
   
#)
(************************* USER MENU  **********************************)  

-- UserMenuIsDisabled: DoPart --
do att.disabled->value  

-- UserMenuInsert: Descriptor --
(# 
do (* Insert the user menu into Design/OA *)
   (if ID >= 10000 (* below 10000 is Design/OA menus *)
    // true then
       (if (ID,BeforeMenuID)->UI.DSMenuInsertMenu
        // false then 'DesignMenuInit: can''t create new menu'->putline
       if);
       (if (ID,name)->UI.DSMenuRename
        // false then 'MenuInsert can''t rename menu'->putline
       if);
       
    else
       (if verbose
        // true then
           'Design/OA menu already inserted, cannot overwrite'->putline; 
       if);
       
   if);
   (if ItemList.size
    // 0 then (* no items: remove dummy item *)
       (if ID+1->UI.DSMenuDeleteItem
        // false then 'UserMenuDeleteItem: can''t delete item 1'->putline
       if);
       (if ID+2->UI.DSMenuDeleteItem
        // false then 'UserMenuDeleteItem: can''t delete item 2'->putline
       if);
       
   if);
   (if ItemList.size
    // 1 then (* one item: remove dummy item two *)
       (if ID+2->UI.DSMenuDeleteItem
        // false then 'UserMenuDeleteItem: can''t delete item 2'->putline
       if);
       
   if);
   ItemList.scan
     (#  do Current.Insert #);
   
#)  

-- UserMenuDelete: Descriptor --
(# 
do
   (if ID->UI.DSMenuDeleteMenu
    // false then 'UserMenuDelete: can''t delete menu'->putline
   if);
   ItemList.scan
     (#  do false->Current.att.shown #);
   
#)  

-- UserMenuInsertBefore: Descriptor --
(# 
do
   (if (ID,BeforeMenuID)->UI.DSMenuMoveMenu
    // false then 'UserMenuMove can''t move menu'->putline; 
   if);
   
#)
(***************************** ITEM **********************************)  

-- BasicMenuItemAtt: Descriptor --
(# shown,disabled: @Boolean;  #)  

-- BasicMenuAtt: Descriptor --
(# disabled: @boolean #)  

-- UserMenuItemIsDisabled: DoPart --
do (att.disabled or THIS(UserMenu).att.disabled)->value  

-- UserMenuInsertItem: Descriptor --
(# (* Insert items into the menu *) 
do
   (if itemNumber-ID
    // 1 // 2 then
       (if (itemNumber,name,0)->UI.DSMenuItemRename
        // false then
           'DesignItemInit: can''t rename item: '->puttext;
           itemNumber->putint;
           newline;
           
       if);
       
    else
       (if
       (itemNumber,name,ID,0)
         ->UI.DSMenuInsertItem (* 0: the item is appended *)
        // false then
           'DesignItemInit: can''t create new item '->puttext;
           itemNumber->putint;
           newline;
           
       if);
       
   if);
   true->att.shown;
   (if att.disabled // true then disable if);
   
#)  

-- UserMenuDeleteItem: Descriptor --
(# 
do
   (if itemNumber->UI.DSMenuDeleteItem
    // false then
       'UserMenuDeleteItem: can''t delete item'->puttext;
       itemNumber->putint;
       newline;
       
   if);
   false->att.shown;
   
#)  

-- UserMenuItemRename: Descriptor --
(# 
do
   (if (itemNumber,name,0)->UI.DSMenuItemRename
    // false then
       'UserMenuItemRename: can''t rename item: '->puttext;
       itemNumber->putint;
       newline;
       
   if);
   
#)  

-- BasicItemExecute: DoPart --
do INNER execute; commandID->UI.DSUIMenuExecute->ok  

-- MenuItemEnable: Descriptor --
(# 
do
   (if not doNotEnable then
       (if att.shown
        // true then
           (if (itemNumber,UI.MenuEnableFlag)->UI.DSMenuItemSetAttr
            // false then 'Menu: can''t enable item'->putline
           if);
           
       if);
       false->att.disabled
   if);
   
#)  

-- MenuItemDisable: Descriptor --
(# 
do
   (if att.shown
    // true then
       (if (itemNumber,UI.MenuDiableFlag)->UI.DSMenuItemSetAttr
        // false then 'Menu: can''t disable item'->putline; 
       if);
       
   if);
   true->att.disabled;
   
#)  

-- MenuItemCheck: Descriptor --
(# 
do (id,itemNumber,checked)->UI.DSMenuCheckItem; 
#)
(*********************** DESIGN MENU ********************************)  

-- DesignMenuInsert: Descriptor --
(# 
do (* Insert the items of this user menu into Design/OA menus *)
   ItemList.scan (#  do Current.Insert #); 
#)  

-- DesignMenuDelete: Descriptor --
(#  do ItemList.scan (#  do false->Current.att.shown #);  #)  

-- DesignMenuInsertItem: Descriptor --
(# (* Insert items into the menu *) 
do
   (if (ID,itemNumber,name)->UI.DSMenuAppendItem (* 0: the item is appended *)
    // false then
       'DesignItemInit: can''t append new item '->puttext;
       itemNumber->putint;
       newline;
       
   if);
   true->att.shown;
   (if att.disabled // true then disable if);
   
#)  

-- DesignMenuDeleteItem: Descriptor --
(# 
do
   (if itemNumber->UI.DSMenuDeleteItem
    // false then
       'DesignMenuDeleteItem: can''t delete item'->puttext;
       itemNumber->putint;
       newline;
       
   if);
   false->att.shown;
   
#)  

-- DesignMenuItemRename: Descriptor --
(# 
do
   (if (itemNumber,name,0)->UI.DSMenuItemRename
    // false then
       'DesignMenuItemRename: can''t rename item: '->puttext;
       itemNumber->putint;
       newline;
       
   if);
   
#)
(***************************** MENUBAR **********************************)  

-- MenuBarInsert: Descriptor --
(#
(* add this menu to the design menus
 * this constitutes a problem, since a new menu MUST be created
 * with a resource!!
 *) 
do
   e.Insert;
   (* insert the menu and items in the Design/OA menus *)
   (UI.DSMenuShowMenuBar);
   (* update the menubar *)
   (if verbose
    // true then 'MenubarInsert: '->puttext; e.ID->putint; newline
   if);
   
#)  

-- MenuBarDelete: Descriptor --
(# 
do
   e.Delete;
   (* remove the menu and items in the Design/OA menus *)
   (UI.DSMenuShowMenuBar);
   (* update the menubar *)
   
#)
(***************************** MENU UTIL *****************************)  

-- CheckDesignOAMenus: Descriptor --
(# (* remove not installed DesignOA menus *)
   CheckMenu:
     (# theID: @Integer; theMenu: ^BasicMenu; 
     enter theID
     do
        theID->MenuBar.findMenu->theMenu[];
        (if theMenu[]
         // none then
            (if theID->UI.DSMenuDeleteMenu
             // false then 'CheckDesignOAMenus: can''t delete menu'->putline
            if);
            
        if);
        
     #);
   
do
   UI.menuID.FileMenu->CheckMenu;
   UI.menuID.EditMenu->CheckMenu;
   UI.menuID.CreateMenu->CheckMenu;
   UI.menuID.MakeUpMenu->CheckMenu;
   UI.menuID.PageMenu->CheckMenu;
   UI.menuID.GroupMenu->CheckMenu;
   UI.menuID.TextMenu->CheckMenu;
   UI.menuID.AlignMenu->CheckMenu;
   (UI.DSMenuShowMenuBar);
   (* update the menubar *)
   
#)
(************************ FILE MENU ***************************)  

-- NewItem: Descriptor --
(# newPage: ^theDocument.Page; 
do
   (if proceed
    // true then
       (if verbose // true then 'NewItem hitted'->putline if);
       &DesignDocument[]->theDocument[];
       (* make a new document *)
       theDocument.onInit->newPage[];
       (if newPage[]
        // none then
           &theDocument.DesignPage[]->newPage[];
           (* new document makes a new page automatically *)
           
       if);
       UI.DSStrGetCurPage->newPage.onInit;
       (* init page with id *)
       (if verbose
        // true then
           'NewItem: Page inserted: '->puttext; newPage.ID->putint; newline; 
       if);
       
   if);
   
#)  

-- OpenItem: Descriptor --
(#
   count: @ShortRef;
   list: @IntegerRef;
   pages,nodes,conns,regions: [0] @Integer;
   thePage: ^theDocument.Page;
   theObject: ^thePage.DesignObject;
   
do
   (if proceed
    // true then
       (if callbackVerbose // true then 'CallBackOpenItem'->putline if);
       (* we are opening an ordinary Design document *)
       &DesignDocument[]->theDocument[];
       (* make a new document *)
       theDocument.onInit;
       (count[],list[])->UI.DSStrGetPageList;
       (count,list)->GetIntList->pages;
       (* Get all pages *)
       (for i: pages.range repeat
         &theDocument.DesignPage[]->thePage[];
         pages[i]->thePage.onInit;
         thePage.onReadDiagram;
         
       for);
       
   if);
   
#)  

-- CloseItem: Descriptor --
(# pages: ^ObjectList; 
do
   theDocument.GetPages->pages[];
   (if pages[]
    // none then 
    else
       pages.scan
         (# dummy: ^theDocument.Page
         do
            current[]->dummy[];
            (if dummy[]
             // none then 'CloseItem: Page is node'->putline; 
             else
                dummy.CleanUp
            if);
            
         #);
       
   if);
   theDocument.close;
   (* call the close virtual of document *)
   
#)  

-- LoadPageItem: Descriptor --
(# 
do 'CallBackLoadPageItem'->putline; 
#)
(***************************** EDIT MENU ******************************)  

-- PasteItem: Descriptor --
(* this really belongs to the Paste callback
 * but is not available *)
  (#
     thePage: ^Page;
     theObject: ^thePage.DesignObject;
     o: @Integer;
     group: [0] @Integer;
     count: @ShortRef;
     list: @IntegerRef;
     isGroup: @Boolean;
     
  do
     (if verbose // true then 'PasteItem'->putline if);
     CurrentPage->thePage[];
     true->isGRoup;
     (if (count[],list[])->UI.DSStrGetCurGroup
      // false then
         false->isGroup;
         (if verbose // true then 'Group Error'->putline if);
         
     if);
     (count,list)->GetIntList->group;
     (if isGroup
      // true then
         (if verbose
          // true then
             'Group: '->puttext;
             (for i: group.range repeat group[i]->putint; ' '->puttext;  for);
             newline;
             
         if);
         (* group -> thePage.PageCallBack.MakeObjectsFromList; *)
         
      else
         (if verbose // true then 'No Group'->putline if);
         UI.DSStrGetCurObject->o;
         (if verbose
          // true then 'CurrentObject: '->puttext; o->putint; newline; 
         if);
         
     if);
     
  #)
  (*************************** PAGE MENU ******************************)  

-- blowUpExecute: DoPart --
do UI.menuID.pagemenu.blowUp->commandID  

-- reduceExecute: DoPart --
do UI.menuID.pagemenu.reduce->commandID  

-- NewPageItem: Descriptor --
(# thePage: ^DesignPage; 
do
   (if verbose // true then 'NewPageItem hitted'->putline;  if);
   &DesignPage[]->thePage[];
   UI.DSStrGetCurPage->thePage.onInit;
   (*  id of the new page *)
   (if verbose
    // true then
       'NewPageItem: Page inserted: '->puttext; thePage.ID->putint; newline; 
   if);
   
#)
(*************************** GROUP MENU ******************************)  

-- CoarsenItemHit: Descriptor --
(# (* called after the operation has been done *) 
do false->theDocument.privatepart.doingCoarse; 
#)
(************* INIT DEFAULT MENUS *****************)
(* id numbers are given to all menus and items of the Design/OA menu *)  

-- InitFileMenu: Descriptor --
(# 
do
   UI.menuID.FileMenu->id;
   UI.menuID.FileMenu.new->NewItem.init;
   UI.menuID.FileMenu.open->OpenItem.init;
   UI.menuID.FileMenu.close->CloseItem.init;
   UI.menuID.FileMenu.save->SaveItem.init;
   UI.menuID.FileMenu.saveAs->SaveAsItem.init;
   UI.menuID.FileMenu.revert->RevertItem.init;
   UI.menuID.FileMenu.info->GetInfoItem.init;
   UI.menuID.FileMenu.prSet->PageSetupItem.init;
   UI.menuID.FileMenu.prPage->PrintPageItem.init;
   UI.menuID.FileMenu.prPrev->PrintPreviewItem.init;
   UI.menuID.FileMenu.prefs->PreferencesItem.init;
   UI.menuID.FileMenu.loadText->LoadTextItem.init;
   UI.menuID.FileMenu.saveText->SaveTextItem.init;
   UI.menuID.FileMenu.loadField->LoadPageItem.init;
   UI.menuID.FileMenu.saveField->SavePageItem.init;
   UI.menuID.FileMenu.quit->QuitItem.init;
   (if verbose // true then 'InitFileMenu'->putline if);
   
#)  

-- InitEditMenu: Descriptor --
(# 
do
   UI.menuID.EditMenu->id;
   UI.menuID.EditMenu.undo->UndoItem.init;
   UI.menuID.EditMenu.cut->CutItem.init;
   UI.menuID.EditMenu.copy->CopyItem.init;
   UI.menuID.EditMenu.paste->PasteItem.init;
   UI.menuID.EditMenu.clear->ClearItem.init;
   UI.menuID.EditMenu.select->SelectItem.init;
   UI.menuID.EditMenu.show->ShowItem.init;
   UI.menuID.EditMenu.status->StatusItem.init;
   (if verbose // true then 'InitEditMenu'->putline if);
   
#)  

-- InitCreateMenu: Descriptor --
(# 
do
   UI.menuID.CreateMenu->id;
   UI.menuID.CreateMenu.conn->ConnItem.init;
   UI.menuID.CreateMenu.box->RectItem.init;
   UI.menuID.CreateMenu.round->RndRectItem.init;
   UI.menuID.CreateMenu.ellipse->EllipseItem.init;
   UI.menuID.CreateMenu.poly->PolygonItem.init;
   UI.menuID.CreateMenu.polyReg->RegularPolygonItem.init;
   UI.menuID.CreateMenu.wedge->WedgeItem.init;
   UI.menuID.CreateMenu.line->LineItem.init;
   UI.menuID.CreateMenu.text->LabelItem.init;
   UI.menuID.CreateMenu.region->RegionItem.init;
   UI.menuID.CreateMenu.unmakeregion->UnmakeRegionItem.init;
   UI.menuID.CreateMenu.setAttr->SetAttrItem.init;
   UI.menuID.CreateMenu.setCAttr->SetCAttrItem.init;
   UI.menuID.CreateMenu.setWedge->SetWedgeItem.init;
   UI.menuID.CreateMenu.setPoly->SetPolyItem.init;
   UI.menuID.CreateMenu.autoPalette->AutoPaletteItem.init;
   (if verbose // true then 'InitCreateMenu'->putline if);
   
#)  

-- InitMakeUpMenu: Descriptor --
(# 
do
   UI.menuID.MakeupMenu->id;
   UI.menuID.MakeupMenu.select->SelectItem.init;
   UI.menuID.MakeupMenu.adjust->AdjustItem.init;
   UI.menuID.MakeupMenu.changeShape->ChangeShapeItem.init;
   UI.menuID.MakeupMenu.drag->DragItem.init;
   UI.menuID.MakeupMenu.displace->DisplaceItem.init;
   UI.menuID.MakeupMenu.moveto->MoveToItem.init;
   UI.menuID.MakeupMenu.hideSubStructure->HideSubStructureItem.init;
   UI.menuID.MakeupMenu.showSubStructure->ShowSubStructureItem.init;
   UI.menuID.MakeupMenu.forward->ForwardItem.init;
   UI.menuID.MakeupMenu.backward->BackwardItem.init;
   UI.menuID.MakeupMenu.toFront->ToFrontItem.init;
   UI.menuID.MakeupMenu.mergeAttr->MergeAttrItem.init;
   UI.menuID.MakeupMenu.merge->MergeItem.init;
   
#)  

-- InitPageMenu: Descriptor --
(# 
do
   UI.menuID.PageMenu->id;
   UI.menuId.PageMenu.show->ShowItem.init;
   UI.menuId.PageMenu.blowUp->BlowupItem.init;
   UI.menuId.PageMenu.reduce->ReduceItem.init;
   UI.menuId.PageMenu.new->NewItem.init;
   UI.menuId.PageMenu.delete->DeleteItem.init;
   UI.menuId.PageMenu.closepage->ClosePageItem.init;
   UI.menuId.PageMenu.changeFatts->ChangeFattsItem.init;
   UI.menuId.PageMenu.child->ChildItem.init;
   UI.menuId.PageMenu.parent->ParentItem.init;
   UI.menuId.PageMenu.strTree->StrTreeItem.init;
   UI.menuId.PageMenu.default->DefaultItem.init;
   UI.menuId.PageMenu.cleanUp->CleanUpItem.init;
   
#)  

-- InitGroupMenu: Descriptor --
(# 
do
   UI.menuID.groupMenu->id;
   UI.menuID.groupMenu.enable->EnableItem.init;
   UI.menuID.groupMenu.form->FormItem.init;
   UI.menuID.groupMenu.sum->SumItem.init;
   UI.menuID.groupMenu.copyDr->CopyDrItem.init;
   UI.menuID.groupMenu.spread->SpreadItem.init;
   UI.menuID.groupMenu.coarsen->CoarsenItem.init;
   UI.menuID.groupMenu.refine->RefineItem.init;
   UI.menuID.groupMenu.attach->AttachItem.init;
   UI.menuID.groupMenu.detach->DetachItem.init;
   UI.menuID.groupMenu.morph->MorphItem.init;
   UI.menuID.groupMenu.domain->DomainItem.init;
   UI.menuID.groupMenu.deskform->DeskFormItem.init;
   UI.menuID.groupMenu.shift->ShiftItem.init;
   
#)  

-- InitTextMenu: Descriptor --
(# 
do
   UI.menuID.TextMenu->id;
   UI.menuID.TextMenu.enable->EnableItem.init;
   UI.menuID.TextMenu.fitBox->FitBoxItem.init;
   UI.menuID.TextMenu.textAttr->TextAttrItem.init;
   UI.menuID.TextMenu.selBracket->SelBracketItem.init;
   UI.menuID.TextMenu.selString->SelStringItem.init;
   UI.menuID.TextMenu.scrollString->ScrollStringItem.Init;
   UI.menuID.TextMenu.scrNext->SrcNextItem.init;
   UI.menuID.TextMenu.srcZero->SrcZeroItem.init;
   
#)  

-- InitAlignMenu: Descriptor --
(# 
do
   UI.menuID.AlignMenu->id;
   UI.menuID.AlignMenu.hor->HorItem.init;
   UI.menuID.AlignMenu.ver->VerItem.init;
   UI.menuID.AlignMenu.leftLeft->LeftLeftItem.init;
   UI.menuID.AlignMenu.leftRight->LeftRightItem.init;
   UI.menuID.AlignMenu.rightLeft->RightLeftItem.init;
   UI.menuID.AlignMenu.rightRight->RightRightItem.init;
   UI.menuID.AlignMenu.topTop->TopTopItem.init;
   UI.menuID.AlignMenu.topBot->TopBotItem.init;
   UI.menuID.AlignMenu.botTop->BotTopItem.init;
   UI.menuID.AlignMenu.botBot->BotBotItem.init;
   UI.menuID.AlignMenu.cent->CentItem.init;
   UI.menuID.AlignMenu.betw->BetwItem.init;
   UI.menuID.AlignMenu.proj->ProjItem.init;
   UI.menuID.AlignMenu.radial->RadialItem.init;
   UI.menuID.AlignMenu.pickNode->PickNodeItem.init;
   
#)  

-- undoExecute: DoPart --
do UI.menuID.EditMenu.undo->commandID  

-- cutExecute: DoPart --
do UI.menuID.EditMenu.cut->commandID  

-- copyExecute: DoPart --
do UI.menuID.EditMenu.copy->commandID  

-- pasteExecute: DoPart --
do UI.menuID.EditMenu.paste->commandID  

-- clearExecute: DoPart --
do UI.menuID.EditMenu.clear->commandID  

-- selectExecute: DoPart --
do UI.menuID.EditMenu.select->commandID  

-- showExecute: DoPart --
do UI.menuID.EditMenu.show->commandID  

-- statusExecute: DoPart --
do UI.menuID.EditMenu.status->commandID  

-- closePageExecute: DoPart --
do UI.menuID.pagemenu.closepage->commandID  

-- cleanUpExecute: DoPart --
do UI.menuID.pagemenu.cleanup->commandID  

-- changeFattsExecute: DoPart --
do UI.menuID.pagemenu.changeFatts->commandID  

-- strTreeExecute: DoPart --
do UI.menuID.pagemenu.strTree->commandID  

-- textAttrExecute: DoPart --
do UI.menuID.textmenu.textAttr->commandID  

-- pageSetupExecute: DoPart --
do UI.menuID.filemenu.prSet->commandID  

-- printPageExecute: DoPart --
do UI.menuID.filemenu.prPage->commandID  

-- preferencesExecute: DoPart --
do UI.menuID.filemenu.prefs->commandID  

-- loadTextExecute: DoPart --
do UI.menuID.filemenu.loadText->commandID  

-- saveTextExecute: DoPart --
do UI.menuID.filemenu.saveText->commandID  

