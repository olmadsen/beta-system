ORIGIN '~beta/bifrost/Bifrost';
-- bifrostAttributes: attributes --
alignRects:
  (#
     impl: [25]@rectangle;
     top: @integer;
     left: @integer;
     range: @integer;
     insert:<
       (#
	  elm: @rectangle
       enter elm
       do
	  (if range = impl.range then
	      impl.range -> impl.extend
          if);
	  range + 1 -> range;
	  (if top = 0 then
	      range -> top
	   else
	      (if impl[top].y < elm.y then range -> top if)
          if);
	  (if left = 0 then
	      range -> left
	   else
	      (if impl[left].x > elm.x then range -> left if)
          if);
	  elm -> impl[range]
       #);
     inx: @integer;
     scan:
       (# 
       do
	  (for current:impl.range repeat
	       current -> inx;
	       INNER
          for)
       #);
     align:
       (#
	  to: @rectangle
       enter to
       do
	  scan(# do INNER align #)
       #);
     alignLeft: align(# do to.x -> impl[inx].x #);
     alignRight: align
       (#
       do
	  to.x+to.width-impl[inx].width -> impl[inx].x
       #);
     alignTop: align(# do to.y -> impl[inx].y #);
     alignBottom: align
       (#
       do
	  to.y-to.height+impl[inx].height -> impl[inx].y
       #);
     alignVertical: align
       (#
       do
	  to.x+(to.width div 2)-(impl[inx].width div 2) -> impl[inx].x
       #);
     alignHorizontal: align
       (#
       do
	  to.y-(to.height div 2)+(impl[inx].height div 2) -> impl[inx].y
       #);
     spacing:
       (#
	  vdist,hdist,vlast,hlast: @integer;
	  vertical,horizontal: @boolean;
	  to: @rectangle
       enter (to,vdist,hdist,vertical,horizontal)
       do
	  to.x+to.width + hdist -> hlast;
	  to.y-(to.height + vdist) -> vlast;
	  scan
	    (#
	    do
	       (if horizontal then
		   hlast -> impl[inx].x;
		   impl[inx].x+impl[inx].width + hdist -> hlast
               if);
	       (if vertical then
		   vlast -> impl[inx].y;
		   impl[inx].y-(impl[inx].height + vdist) -> vlast
               if)
	    #)
       #)
  #)
