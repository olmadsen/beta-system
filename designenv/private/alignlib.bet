ORIGIN '~beta/bifrost/Bifrost';
-- bifrostAttributes: Attributes --
alignRects:
  (#
     impl: [25] @rectangle;
     top: @integer;
     left: @integer;
     range: @integer;
     insert:<
       (# elm: @rectangle
       enter elm
       do
          (if range = impl.range then impl.range->impl.extend if);
          range+1->range;
          (if top = 0 then
              range->top
           else
              (if impl[top].y < elm.y then range->top if)
          if);
          (if left = 0 then
              range->left
           else
              (if impl[left].x > elm.x then range->left if)
          if);
          elm->impl[range]
       #);
     inx: @integer;
     (* inx is used both in scan and inside the align patterns, *)
     (* this is the reason of it being placed on this level*)
     scan:
       (# 
       do (for current: impl.range repeat current->inx; INNER for)
       #);
     align:
       (# to: @rectangle;
          selectAndHilit: @boolean;
       enter to 
       do 
          false->selectAndHilit;
          scan (#  do 
                  (if ((impl[inx].x=(to.x)) AND 
                      (impl[inx].y=(to.y))) then 
                      true->selectAndHilit; 
                  if); 
                  INNER
          align #) #);
     alignLeft: align (#  do (if NOT(selectAndHilit) then
                                 to.x+4->impl[inx].x if);
                      #);
     alignRight: align (# 
                       do  
                          (if NOT(selectAndHilit) 
                              then
                              (to.x+(to.width-4)-impl[inx].width)->impl[inx].x 
                          if);
                       #);
     alignTop: align (#  do
                        (if NOT(selectAndHilit) then
                            (to.y-4)->impl[inx].y 
                        if);    
                        #);
     alignBottom: align
       (#  do 
           (if NOT(selectAndHilit) then
               to.y-(to.height-4)+impl[inx].height->impl[inx].y
           if);    
           #);
     alignVertical: align
       (#  do 
          (to.x+(to.width div 2)-(impl[inx].width div 2))->impl[inx].x 
       #);
     alignHorizontal: align
       (# 
       do 
          to.y-(to.height div 2)+(impl[inx].height div 2)->impl[inx].y
       #);
     spacing:
       (#
          vdist,hdist,vlast,hlast: @integer;
          vertical,horizontal: @boolean;
          to: @rectangle
       enter (to,vdist,hdist,vertical,horizontal)
       do
          to.x+to.width+hdist->hlast;
          to.y-(to.height+vdist)->vlast;
          scan
            (# 
            do
               (if horizontal then
                   hlast->impl[inx].x; impl[inx].x+impl[inx].width+hdist->hlast
               if);
               (if vertical then
                   vlast->impl[inx].y;
                   impl[inx].y-(impl[inx].height+vdist)->vlast
               if)
            #)
       #)
  #)  

