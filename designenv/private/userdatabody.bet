ORIGIN 'designobjectbody';
-- userdatabodyCVSINFO:descriptor --
(#
do
   '$Id: userdatabody.bet,v 1.6 1998-03-11 13:12:50 beta Exp $'
     -> msg.putLine
#)

-- WriteUserData: Descriptor --
(#  do INNER WriteUserData #)

-- ReadUserData: Descriptor --
(#  do INNER ReadUserData #)

-- WriteReferenceUserData: Descriptor --
(#
do
   (if dataElement[] <> none then
       false->isNone;
       (if (ID <> 0) and (attributeId <> 0) then
           (if dataType## <= IDObject## then
               dataElement[]->theIDObject[];
               theIDObject.ID->IDdata;
               ID
                 ->UI.lookUpUserDataObject
		 (#  do (attributeId,IDdata)->theObject.writeInt #);
               (if userDataVerbose then
                   'Write IDObject data for id: '->puttext;
                   id->putint;
                   ' with attributeId: '->puttext;
                   attributeId->putint;
                   ' succeed:'->puttext;
                   IDData->putint;
                   newline
               if)
           if);
           (if dataType## <= Text## then
               ID
                 ->UI.lookUpUserDataObject
		 (#  do (attributeId,dataElement[])->theObject.writeText #);
               (if userDataVerbose then
                   'Write Text data for id: '->puttext;
                   id->putint;
                   ' with attributeId: '->puttext;
                   attributeId->putint;
                   ' succeed:'->puttext;
                   dataElement[]->puttext;
                   newline
               if)
           if);
           INNER WriteUserData
       if)
    else
       true->isNone
   if);

#)

-- ReadReferenceUserData: Descriptor --
(# b: @boolean
do
   isNone->b;
   (if not b then
       (if (ID <> 0) and (attributeID <> 0) then
           (if dataType## <= IDObject## then
               ID ->UI.lookUpUserDataObject
		 (#  do attributeId->theObject.readInt->(ok,IDdata) #);
	       (if ok then
		   (if userDataVerbose then
		       'Read IDObject data for id: '->puttext;
		       id->putint;
		       ' with attributeId: '->puttext;
		       attributeId->putint;
		       ' succeed: '->puttext;
		       IDdata->putint;
		       newline
		   if);
		   IDdata->theObjectList.find->dataElement[]
		else
                   (if userDataVerbose then
                       'Read IDObject data for id: '->puttext;
                       id->putint;
                       ' with attributeId: '->puttext;
                       attributeId->putint;
                       ' fail'->putline
                   if)
               if)
           if);
           (if dataType## <= Text## then
               ID ->UI.lookUpUserDataObject
		 (#  do attributeId->theObject.readText->(ok,theText[]) #);
	       (if ok then
		   (if theText[] <> none then
		       theText->textData;
		       (if userDataVerbose then
			   'Read text data for id: '->puttext;
			   id->putint;
			   ' with attributeId: '->puttext;
			   attributeId->putint;
			   ' succeed: '->puttext;
			   theText[]->puttext;
			   newline
		       if);
		       theText[]->dataElement[]
		    else
		       (if userDataVerbose then
			   'Read text data for id: '->puttext;
			   id->putint;
			   ' with attributeId: '->puttext;
			   attributeId->putint;
			   ' failed'->puttext;
			   newline
		       if)
		   if)
                else
                   (if userDataVerbose then
                       'Read Text data: get User data TypeSize fail!'->putline
                   if)
               if)
           if);
           INNER ReadUserData
       if)
   if);

#)

-- getIDReferenceUserData: Descriptor --
(# ok: @Boolean; b: @boolean
do
   isNone->b;
   (if not b then
       (if (ID <> 0) and (attributeID <> 0) then
           (if dataType## <= IDObject## then
               ID ->UI.lookUpUserDataObject
		 (#  do attributeId->theObject.readInt->(ok,theID) #);
	       (if ok then
		   (if userDataVerbose then
		       'getID for id: '->puttext;
		       id->putint;
		       ' with attributeId: '->puttext;
		       attributeId->putint;
		       ' succeed: '->puttext;
		       theID->putint;
		       newline
		   if)
		else
                   (if userDataVerbose then
                       'getID for id: '->puttext;
                       id->putint;
                       ' with attributeId: '->puttext;
                       attributeId->putint;
                       ' fail'->putline
                   if)
               if)
           if);
           INNER getID
       if)
   if)
#)

-- InitReferenceUserData: Descriptor --
(#
do
   &Type[]->DataType[];
   isNone.init;
   (if dataType## <= IDObject## then
       IDUserDataAttributes+1->IDUserDataAttributes->attributeId;
       (if userDataVerbose then
           'Initialising IDObject reference user data for id: '->puttext;
           id->putint;
           ' ;attributeID: '->puttext;
           attributeID->putint;
           newline
       if)
    else
       INNER Init;
       (if not attrIDalreadyInit then
           UserDataAttributes+1->UserDataAttributes->attributeId;
           (if userDataVerbose then
               'Initialising (other) reference user data for id: '->puttext;
               id->putint;
               ' ;attributeID: '->puttext;
               attributeID->putint;
               newline
           if)
       if)
   if);
   (if dataElement[] <> none then
       (if userDataVerbose then
           'Init: dataElement was not none. Writing user data!'->putline
       if);
       dataElement[]->WriteUserData
   if)
#)

-- WriteIntegerUserData: Descriptor --
(#
do
   (if (ID <> 0) and (attributeID <> 0) then
       ID
         ->UI.lookUpUserDataObject
	 (#  do (attributeId,integerData)->theObject.writeInt #);
       (if userDataVerbose then
           'Write integer data: '->puttext;
           integerData->putint;
           ' succeed'->putline
       if)
   if)
#)

-- ReadIntegerUserData: Descriptor --
(#
do
   (if ID <> 0 then
       ID ->UI.lookUpUserDataObject
	 (#  do attributeId->theObject.readInt->(ok,integerData) #);
       (if ok then
           (if userDataVerbose then 'Read integer data succeed'->putline if);
        else
           (if userDataVerbose then 'Read integer data fail'->putline if)
       if)
   if)
#)

-- InitIntegerUserData: Descriptor --
(#
do
   INNER Init;
   (if not specialData then
       UserDataAttributes+1->UserDataAttributes->attributeId;
       (if userDataVerbose then
           'Initialising integer user data for id='->puttext;
           id->putint;
           ' ;attributeID: '->puttext;
           attributeID->putint;
           newline
       if)
   if);
   (if IntegerData <> 0 then IntegerData->WriteUserData if)
#)

-- WriteBooleanUserData: Descriptor --
(#
do
   (if (ID <> 0) and (attributeID <> 0) then
       (if BooleanData then 1->integerData else 0->integerData if);
       ID
         ->UI.lookUpUserDataObject
	 (#  do (attributeId,integerData)->theObject.writeInt #);
       (if userDataVerbose then
           'Write boolean data: '->puttext;
           integerData->putint;
           ' succeed'->putline
       if)
   if)
#)

-- ReadBooleanUserData: Descriptor --
(#
do
   (if ID <> 0 then
       ID ->UI.lookUpUserDataObject
	 (#  do attributeId->theObject.readInt->(ok,integerData) #);
       (if integerData = 1 then
           true->BooleanData
        else
           (if integerData = 0 then false->BooleanData if)
       if);
       (if ok then
           (if userDataVerbose then 'Read Boolean data succeed'->putline if);
        else
           (if userDataVerbose then 'Read Boolean data fail'->putline if)
       if)
   if)
#)

-- InitBooleanUserData: Descriptor --
(#
do
   INNER Init;
   (if not specialData then
       UserDataAttributes+1->UserDataAttributes->attributeId;
       (if userDataVerbose then
           'Initialising Boolean user data for id='->puttext;
           id->putint;
           ' ;attributeID: '->puttext;
           attributeID->putint;
           newline
       if)
   if);
   (if BooleanData <> false then BooleanData->WriteUserData if)
#)

-- UserDataInit: Descriptor --
(#
do
   (if userDataVerbose then
       'Initialising user data for id='->puttext; id->putint; newline
   if);
   ID
     ->UI.lookUpUserDataObject
     (#  do (20000,THIS(userDataInit).Type)->theObject.writeInt #)
#)

-- updateUserDataIDs: Descriptor --
(# oldIDUserdataAttributes: @integer; ok: @Boolean
do
   theObjectList.scan
     (#
     do
        IDUserDataStart->rangeStart;
        IDUserDataEnd->rangeEnd;
        current.ID ->UI.lookUpUserDataObject
	  (#
	  do
	     IDUserDataEnd-1->theObject.readInt
	       ->(ok,oldIDUserDataAttributes)
	  #);
        (IDUserDataStart,oldIDUserDataAttributes,current.ID)->update;
        current[]->myCurrent[];
        INNER updateUserDataIDs
     #)
#)

-- designenvDoUpdate: Descriptor --
(#
do
   theID->UI.lookUpUserDataObject
     (# do rangeEnd->theObject.readInt->(ok,count) #);
   (if ok then
       count-RangeStart->count;
       (if userDataVerbose then
	   'FindRange ok: '->puttext; count->putint; newline
       if);
       (if count = 0 then
	   (if oldCount <> 0 then
	       oldCount-RangeStart->count;
	       (if userDataVerbose then
		   'Trying oldCount: '->puttext; count->putint; newline
	       if)
	   if)
       if);
       (for i: count repeat
	    theID->UI.lookUpUserDataObject
	      (# do UDStart+i->theObject.readInt->(ok,oldID) #);
	    (if userDataVerbose then
		'On current.ID: '->puttext;
		theID->putint;
		' for attributeID: '->puttext;
		UDStart+i->putint;

	    if);
	    (if ok then
		(if oldID <> 0 then
		    oldID->UserDataIDMap.getID->newID;
		    (if userDataVerbose then
			' updating oldID='->puttext;
			oldID->putint;
			' to newID='->puttext;
			newID->putint;
			newline
		    if);
		    theID ->UI.lookUpUserDataObject
		      (# do (UDStart+i,newID)->theObject.writeInt #)
		 else
		    (if userDataVerbose then 'oldID was 0!'->putline if)
		if)
	     else
		(if userDataVerbose then 'Read old ID fail!'->putline if)
	    if)
       for)
    else
       (if userDataVerbose then
           'ID: '->puttext;
           theID->putint;
           ' User data FindRange fail: '->puttext;
           rangeStart->putint;
           ' - '->puttext;
           rangeEnd->putint;
           newline
       if)
   if)
#)

-- UserDataIDMapPutID: DoPart --
do
   (if userDataVerbose then
       'Mapping UserdataID='->puttext;
       UserDataID->putint;
       ' to ID='->puttext;
       ID->putint;
       newline
   if);
   (if UserDataID <> 0 then
       (if UserDataID > private.impl.range then
           UserDataID+private.impl.range->private.impl.extend
       if);
       ID->private.impl[UserDataID]
    else
       (if ID > private.impl.range then
           ID+private.impl.range->private.impl.extend
       if);
       ID->private.impl[ID] (*newly created object: ID=UserDataID.*)
   if)

-- UserDataIDMapGetID: DoPart --
do private.impl[oldID]->newID

-- UserDataIDMap: Descriptor --
(# impl: [1000] @Integer #)

-- getNodeType: Descriptor --
(#
do
   ID->UI.lookUpUserDataObject (#  do 20000->theObject.readInt->(ok,Type) #);
   (if ok then
       (if userDataVerbose then 'Read node type data succeed'->putline if)
    else
       (if userDataVerbose then 'Read node type data fail'->putline if)
   if)
#)

-- readTextUserData: DoPart --
do
   ID->UI.lookUpUserDataObject
     (# ok: @boolean do type->theObject.readText->(ok,t[]) #)

-- writeTextUserData: DoPart --
do ID->UI.lookUpUserDataObject (#  do (type,t[])->theObject.writeText #)

-- DocumentReadIntegerUserData: DoPart --
do ID->UI.lookUpUserDataObject
     (# ok: @boolean do type->theObject.readInt->(ok,i) #)

-- DocumentWriteIntegerUserData: DoPart --
do ID->UI.lookUpUserDataObject (#  do (type,i)->theObject.writeInt #)

-- deleteUserDataType: DoPart --
do ID->UI.lookUpUserDataObject (#  do type->theObject.deleteType #)
