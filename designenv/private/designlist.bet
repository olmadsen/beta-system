ORIGIN '~beta/basiclib/v1.6/betaenv';
-- LIB: Attributes --
IDObject: (* superpattern for all designobjects that has an ID *)
  (# ID: @Integer;  do INNER ;  #);
ObjectList: (* repetition implementation of the list of designobjects *)
  (#
     element:< IDObject;
     impl: [32] ^Element;
     (* start with the size of 32; and extend if necessary *)
     NoOfElements: @Integer;
     insert:<
       (# e: ^Element
       enter e[]
       do
          (if e[]
           // none then 'insert: element is NONE'->screen.putline; 
           else
              (if e.ID
               // 0 then 'insert: ID is 0'->screen.putline
               else
                  (if e.ID > impl.range
                   // true then
                      (e.ID-impl.range)+20->impl.extend; (* extend the size *) 
                  if);
                  (if impl[e.ID][]
                   // none then NoOfElements+1->NoOfElements; 
                   else
                  (* 'insert: overwriting existing element'->screen.putline;*)
                      
                  if);
                  e[]->impl[e.ID][];
                  
              if)
          if);
          INNER ;
          
       #);
     delete:<
       (# e: ^Element; 
       enter e[]
       do
          (if e[]
           // none then 'delete: empty element'->screen.putline; 
           else
              (if e.ID <= impl.range
               // true then
                  (if impl[e.ID][]
                   // none then
                      'delete: element not found ID: '->screen.puttext;
                      e.ID->screen.putint;
                      screen.newline;
                      
                   else
                      none ->impl[e.ID][];
                      NoOfElements-1->NoOfElements;
                      INNER ;
                      
                  if);
                  
               else
                  'delete: element out of range ID: '->screen.puttext;
                  e.ID->screen.putint;
                  screen.newline;
                  
              if);
              
          if);
          
       #);
     find:<
       (# ID: @Integer; e: ^Element; 
       enter ID
       do
          (if ID
           // 0 then 'find: ID is 0'->screen.putline
           else
              (if ID <= impl.range
               // true then impl[ID][]->e[]; INNER ; 
              if)
          if)
       exit e[]
       #);
     makeNull:
       (# 
       do (for i: impl.range repeat none ->impl[i][] for); 0->NoOfElements
       #);
     scan:
       (# current: ^Element; 
       do
          (for i: impl.range repeat
            (if impl[i][] <> none
             // true then impl[i][]->current[]; INNER
            if)
          for)
       #);
     NodeIDsToList:
       (# nodeIDs: [0] @Integer; theList: ^ObjectList; e: ^Element; 
       enter nodeIDs
       do
          (if nodeIDs.range
           // 0 then none ->theList[]
           else
              &ObjectList[]->theList[];
              (for i: nodeIDs.range repeat
                nodeIDs[i]->find->theList.insert; 
              for);
              
          if);
          
       exit theList[]
       #);
     ListToNodeIDs:
       (# 
       exit &
         (# nl: [NoOfElements] @Integer; i: @Integer; 
         do 1->i; scan (#  do current.ID->nl[i]; i+1->i;  #); 
         exit nl
         #)
       #);
     DumpList:
       (# 
       do
          'Dumping ObjectList'->screen.putline;
          scan
            (# 
            do 'ID '->screen.puttext; current.ID->screen.putint; screen.newline
            #);
          
       #);
     
  #);
;
  

