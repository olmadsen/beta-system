ORIGIN '~beta/basiclib/directory';
INCLUDE 'path';
---lib:attributes---
Environment:
  (# workingDirectory: ^directory; 
     count: @integer;
     thePath: @Path;
     history: [3] ^text; 
     histStart,histTop: @integer;
     nonEmptyHist: @boolean;
     
     init:<
       (# wd: ^text
       enter wd[]
       do &Directory[] -> workingDirectory[];
          wd[] -> workingDirectory.name;
          thePath.init;
          1 -> histStart; 
          0 -> histTop;
          INNER init;
       #);
     prompt:
       (# P: @text
       do 'beta(' -> P;
          count -> P.putint;
          ') ' -> P.puttext;
          P[] -> puttext;
       #);
     changeDirectory:
       (# dir: ^text;
          newDir: ^diskEntry
       enter dir[]
       do L:
            (#
            do find:
                 (#
                 do workingDirectory.scanEntries
                    (# N: ^text
                    do found.path -> N[];
                       (if dir[] -> N.equal then
                           found[] -> newDir[];
                           leave find
                    if)#);
                    'No such directory' -> putline;
                    leave L
                 #);
               (if newDir.isDirectory then
                   &Directory[] -> workingDirectory[];
                   newDir.path -> workingDirectory.name;
                else
                   dir[] -> puttext;
                   ' is not a directory' -> putline;
               if)
            #)	
       #);
     addToHistory:
       (# T: ^text
       enter T[]
       do (if (histTop+1 -> histTop) > history.range  then
              1 -> histTop
          if);
          (if (histTop = histStart) and not nonEmptyHist then 
              histStart+1 -> histStart 
          if);
          true -> nonEmptyHist;
          T[] -> history[histTop][];
          (* histTop->putint; ':' -> put;   history[histTop][]->putline;*)
       #);
     printHistory:
       (# n,i: @integer
       do histStart -> i;          
          Loop:
            (#
            do history[i][] -> putline;
               i+1 -> i;
               n+1 -> n;
               (if i > history.range then 1 -> i if);
               (if n < history.range then restart loop if)
            #)
       #)
     
  #); 
---directoryLib:attributes---
print:
  (#
  do this(directory).name -> putline;
  #);
list:
  (# 
  do this(directory).scanEntries
     (#
     do found.path -> puttext; ' '->put;
     #);
     newline
  #)
