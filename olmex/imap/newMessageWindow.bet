ORIGIN '~beta/guienv/fields';
INCLUDE '~beta/guienv/stddialogs';
INCLUDE '~beta/basiclib/file';
INCLUDE '~beta/basiclib/textUtils';
INCLUDE 'sendMail';
(* This demo shows how to build a simple texteditor. *)
--- guienvLib:attributes---
newMessageWindow: window
  (# 
     send:<
       (# server,to,subject,msg,m: ^text
       enter(server[],to[],subject[],msg[]) 
       do inner
       #);
     
     menubarType::
       (# fileMenu: @menu
            (# textFile: @file;
               openItem: @menuItem
                 (# eventHandler::
                      (# onSelect::
                           (# theText: @StyledText; path: ^text;
                           do this(newMessageWindow)[] -> fileSelectionDialog -> path[];
                              (if path[]<>NONE then
                                  path[] -> textFile.name;
                                  textFile.openRead;
                                  textFile.scan
                                  (# while:: (# do true->value #);
                                  do ch -> theText.put
                                  #);
                                  theText[]
                                    ->theTextEditor.contents.contents;
                                  textFile.close
                              if)
                      #)#);
                    open:: (# do 'Open' -> name #)
                 #);
               saveItem: @menuItem
                 (# eventHandler::
                      (# onSelect::
                           (# theText: @Text; tempName: ^text;
                           do textFile.name -> tempName[];
                              (if tempName.length>0 then
                                  textFile.openWrite;
                                  theTextEditor.contents.contents
                                    ->textFile.puttext;
                                  textFile.close
                              if)
                      #)#);
                    open:: 
                      (# do 'Save' -> name #)
                 #);
               quitItem: @menuItem
                 (# eventHandler:: 
                      (# onSelect:: (# do terminate #) #);
                    open:: 
                      (# do 'Quit' -> name #)
                 #);
               open::
                 (#
                 do 'File' -> name;
                    openItem.open; openItem[] -> append;
                    saveItem.open; saveItem[] -> append;
                    quitItem.open; quitItem[] -> append
            #)#);
          open:: 
            (# do fileMenu.open; fileMenu[] -> append #)
       #); (* menubarType *)
     eventhandler::
       (# onAboutToClose:: (# do terminate #) #);
     cToLabel: @staticText
       (# open:: 
            (#  do (10,10)->position; (95,25)->size; 'To: '->label #)
       #);
     cTo: @editText
       (# open::  (#  do (70,10)->position; (200,25)->size #);
          toName: @text;          
       #);
     cSubjectLabel: @staticText
       (# open:: 
            (#  do (10,45)->position; (95,25)->size; 'Subject: '->label #)
       #);
     cSubject: @editText
       (# open::  (#  do (70,45)->position; (200,25)->size #);
          subject: @text;
       #);
     cCancel: @pushButton
       (# open:: 
            (#  
            do (10+115+150,45)->position; (80,25)->size; 'Cancel'->label 
            #);
          eventhandler:: 
            (# onMouseUp::  (#  do THIS(newMessageWindow).close #) #)
       #);
     cSend: @pushButton
       (# open:: 
            (# 
            do (10+115+150,10)->position; (50,25)->size; 'Send'->label
            #);
          eventhandler:: 
            (#
               onMouseUp:: 
                 (# to,subject,msg: ^text
                 do
                    cTo.contents -> cTo.toName.setText;
                    cSubject.contents -> cSubject.subject.setText;
                    'To: ' -> puttext;
                    cTo.toName[] -> to[] -> putline;
                    'Subject: ' -> puttext;
                    cSubject.subject[] -> subject[] -> putline;
                    'Message: ' -> putline;
                    theTextEditor.contents.contents -> msg[] -> putline;
                    
                    ('smtp.mail.dk', to[], subject[],msg[]) -> send;
                    (*('daimi.au.dk', to[], subject[],msg[]) -> send;*)
                    THIS(newMessageWindow).close
                 #)
            #)
       #);
     
     thetextEditor: @textEditor
       (# open:: 
            (# w,h: @integer
            do 'New message' -> title;
               this(newMessageWindow).size ->(w,h);
               (w-30,h-30) -> size;
               (10,100) -> position;
               true -> bindBottom -> bindRight
            #)
       #);
     open::
       (#
       do 'New message editor' -> title;
          (500,600) -> size;
          cToLabel.open;
          cTo.open;
          cSubjectLabel.open;
          cSubject.open;
          cSend.open;
          cCancel.open;
          (*cSend[]->defaultButton;*)

          thetextEditor.open
       #)
  #)


