origin '~beta/basiclib/betaenv';
include '~beta/containers/list'
---lib:attributes---
mailFolder:
  (# name: ^text;
     isFolder: @boolean;
     subs: @list
       (# element::
            (# M: ^mailFolder #);
          add:
            (# N: ^text; isFolder: @boolean;
               E: ^element
            enter(N[],isFolder)
            do L:
                 (#
                 do scan
                    (#
                    do (if current.M.name[] -> N.equal then
                           current[] -> E[];
                           leave L
                    if)#);
                    &element[] -> E[];
                    &mailFolder[] -> E.M[];
                    E.M.init;
                    N[] -> E.M.name[];
                    isFolder -> E.M.isFolder;
                    E[] -> append
                 #)
            exit E[]
            #)
       #);
     addSub:
       (# N: ^text;
          isFolder: @boolean;
          pos: @integer;
          E: ^subs.element;
       enter(N[],isFolder)
       do L:
            N.scanAll
            (# 
            do pos+1->pos;
               (if ch = dirChar then
                   leave L
               if)
            #);
          (if pos < N.length then
              (* add subfolder *)
              ((1,pos-1) -> N.sub,true) -> subs.add -> E[]; 
              ((pos+1,N.length) -> N.sub,false) -> E.M.addSub
           else
              (* add leaf *)
              ((1,pos) -> N.sub,isFolder) -> subs.add -> E[]; 
          if)
       #);
     scan:
       (# isF: @boolean;
          current: ^text;
       do name[] -> current[];
          isFolder -> isF;
          inner;
          subs.scan(# 
                   do &current.M.scan(#
                                     do current[] -> this(scan).current[];
                                        isF -> this(scan).isF;
                                        inner scan
                                     #)
                   #)
       #);
     doScan:
       (#
       do
       #);
     dirChar: @char;
     init:< (# do '/' -> dirChar #)
  #)
     
