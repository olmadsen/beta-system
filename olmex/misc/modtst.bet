ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/v1.3/file'
(*	Modtime test
 *	File: 	~olm/beta/expm/modtst
 *	
 *	Input:	a file consisting of a list of file names.
 *		Example: ~olm/beta/system/EXP/l1
 *	Function:	
 *		The files are scanned and inserted into a list
 *		together with their current modtime.
 *		The user is the asked to touch some of the files,
 *		This could be done by suspending this program or
 *		from another window. 
 *		When the program is resumed, a nonblank char should be typed.
 *		If 's' is printed, the next phase is silent,
 *		otherwise output is printed.
 *		The program will then scan all the files to find
 *		those that have been modified. In non-silent mode,
 *		the names of those files will be printed.
 *	Output:	The user should 'watch the real time between the
 * 		printing of:	Checking files 
 *		and:		Done
 *		This gives an impression of the time it takes to test
 *		which files that have been modified.
 *		The time from typing the input file name until 
 *		the input file has been processed gives an impression of
 *		I/O time and text processing.
 *)
---program:descriptor---
(# 
   fList: (# f: ^file; suc: ^fList; mTime: @integer #);
   head: @fList;

   insert: 
     (# FN: @text; f: ^file; R: ^fList
     enter FN
     do &file[]->f[]; FN[]->f.name;
        (if f.entry.exists//true then (* else skip the file *)
            &fList[]->R[]; f[]->R.f[]; head.suc[]->R.suc[]; R[]->head.suc[];
	    f.entry.modTime->R.mTime
        if)
     #);

   scan:
     (# thisF: ^fList
     do head[]->thisF[];
        L: (if (thisF.suc[]->thisF[])// NONE then else INNER; restart L if)
     #);
   ch: @char; noOfFiles: @integer
do L:
   (# input: @file(# eosError::<(#do true->continue #)#)
   do 'Input file name: '->putText; 
       keyBoard.getAtom->input.name; input.openRead;
      L: cycle
        (# FN: ^text 
        do &text[]->FN[]; noOfFiles+1->noOfFiles;
           input.getAtom->FN[];
           FN->insert; 
	   (if input.eos//true then leave L if)
        #);
   #);
   'The list has:' ->putText; (noOfFiles)->putInt; ' files'->putline;

   'Print list of files and their modtime?[yn]'->puttext; getNonBlank->ch;
   (if ch//'y' then 
       scan(#do thisF.F.name->putText; (thisF.mTime)->putInt; newline #);
   if);
   newline;
   'Suspend this program. Touch some of the files.'->putline;
   'You will have to wait some time before touch has an effect!!!'->putLine;
   'Resume this program and type a non blank char.'->puttext;
   ' ''s'' is silent mode'->putLine;
    getNonBlank->ch;
   'Checking files: '->puttext;
   scan(#do (if thisF.F.entry.modtime//thisF.mTime then else
                (if ch//'s' then else 
                    'Modified:'->putText; thisF.F.name->putline if)if)#);
   'Done!'->putline
#)

