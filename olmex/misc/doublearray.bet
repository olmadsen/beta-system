ORIGIN '~beta/basiclib/v1.4/betaenv';
---INCLUDE 'array'
--program:descriptor--
(# vers1:
     (* version 1 of double array *)
     (# B: array
          (# element::Belm; low::(#do 13->value #); high::(#do 17->value #)
          #);
        
        A: @B;
        Belm: array
          (# element::text; low::(#do 12->value #); high::(#do 15->value#);
             indexOutOfRange::(#do msg[]->putline; true->continue#)          
          #);
        
        T: @text;        
     do 'Version 1'->putLine;
        (A.low,A.high)->forTo
        (# inx1: @integer; elm: ^Belm
        do inx->inx1;
           (* (T,inx1,inx2)->A.put *)
           &Belm[]->elm[];
           (elm.low,elm.high)->forTo
           (#
           do T.clear;; inx1->T.putint; ':'->T.put;
              inx->T.putint; 
              ':Hello\n'->T.append;  (inx,T.copy)->elm.put;
           #);
           (inx1,elm[])->A.put
        #);
        
        (A.low,A.high)->forTo
        (# elm: ^Belm
        do inx->A.get->elm[];
           (elm.low,elm.high)->forTo
           (#
           do inx->elm.get->puttext
           #);
           newline;
        #);        
     #);
   
   vers2:
     (* version 2 of double array *)
     (# A: @array
          (# low::(#do 13->value #); high::(#do 17->value #);
             element::array
               (# element::text; low::(#do 12->value#); high::(#do 15->value#);
                  indexOutOfRange::(#do msg[]->putline; true->continue#)
               #);
             get2:
               (# i,j: @integer; e: ^object
               enter(i,j)
               do
               exit e[]
               #)
          #);
        T: @text
     do 'Version 2'->putLine;
        (A.low,A.high)->forTo
        (# inx1: @integer; elm: ^A.element
        do inx->inx1;
           (* (T,inx1,inx2)->A.put *)
           &A.element[]->elm[];
           (elm.low,elm.high)->forTo
           (#
           do T.clear;; inx1->T.putint; ':'->T.put;
              inx->T.putint; 
              ':World\n'->T.append;  (inx,T.copy)->elm.put;
           #);
           (inx1,elm[])->A.put
        #);

        (A.low,A.high)->forTo
        (# elm: ^A.element
        do inx->A.get->elm[];
           (elm.low,elm.high)->forTo
           (#
           do inx->elm.get->puttext
           #);
           newline;
        #);
        (*4->(13->A.get).get->putline  *)
     #)
do vers1;
   vers2;
#)
