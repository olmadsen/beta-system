ORIGIN 'taskmng';
INCLUDE '~beta/Xt/awenv';
INCLUDE 'taskmng';
---GetOp:descriptor---
awenv
(# window: @box
     (# commandButton: @command
          (# callback::< 
	      (# getTask: ^TaskForm do &TaskForm[]->getTask[]; getTask.init#)
	  #);
        taskList: @command(# callBack::<(#do theTaskBrowser.init #)#);
	quit: @command(# callback::< (#do (normal,'')->stop #)#);
	init::<
	  (#
	  do commandButton.init;
	     'New task'->commandButton.label;
	     TaskList.init; 'Task browser'->TaskList.label;
	     quit.init;
	  #);
     #);
  TaskForm: toplevelShell
  (# window: @form
      (# lastField: ^field;
         field: 
          (# lab: @ label; ed: @ asciiText;
             init: 
             (# labelValue,defaultValue: @ text
             enter (labelValue,defaultValue)
             do lab.init; labelValue->lab.label; 0->lab.borderWidth;
                ed.init; defaultValue->ed.string; ed.edit->ed.editType;

                (* place ed and lab correctly in the form: *) 
                lab->ed.fromHoriz;
                (if (lastField[]=NONE)//false then
                  lastField.lab->lab.fromVert;
                  lastField.lab->ed.fromVert;
                if);
                this(field)[]->lastField[];
             #);

             textValue: (# t: ^text do ed.string->t[] exit t #);
             integerValue: 
		(# t: ^text do ed.string->t[]; 0->t.setPos exit t.getInt #);
          #);
          taskName,startTime,endTime: @ field;
	  desc:@command(# callBack::<(#do &descEditor[]->de[]; de.init #)#);
          ok: @command 
            (# callback::< 
                (# p:  ^task; T:  ^text;
                do &task[]->p[];
                   taskName.textValue->p.name;
                   startTime.integerValue->p.start;
		   endTime.integervalue->p.end;
		   (if de[]//NONE then else 
		       de.aPane.Txt.string->T[]; T->p.description if);
                       'You have entered the task '->screen.putText;
                   (p[],screen[])->printObject;
		   p[]->NewTask;
                   removeAgain;
             #)#);
          cancel: @command (# callback::< (# do removeAgain #) #);
          init::< 
            (# s: @text
            do (if theTask[]//none then
		   ('taskName','unknown')->taskname.init; 
                   ('startTime','0')->startTime.init; 
                   ('endTime','0')->endTime.init; 
	        else
		   ('taskName',theTask.name)->taskname.init; 
                   theTask.start->s.putint; ('startTime',s)->startTime.init; 
                   s.clear;theTask.end->s.putint; ('endTime',s)->endTime.init; 
	       if);
	       desc.init;
               ok.init;
               cancel.init;
	       10->desc.horizDistance;
               (* place desc,ok and cancel correctly *)
               desc->ok.fromHoriz;
               ok->cancel.fromHoriz; 
	       lastfield.lab->desc.fromVert;
               lastField.lab->ok.fromVert;
               lastField.lab->cancel.fromVert;
             #)
       #) (* window *);
      theTask: ^Task;
      de: ^descEditor ;
      removeAgain: 
        (#
        do this(TaskForm).destroy; 
	   (if de[]//none then else de.destroy if)
        #);
      init::<
	(# do window.init; XtGrabExclusive->popup #);
    #);
  DescEditor: topLevelShell
   (# aPane: @paned
        (# clear: @ command(# callback::< (# do ''->txt.string #)#);
           txt: @ asciiText
             (# init::<
                  (#
                  do 200->preferredPaneSize;
	             edit->editType;
		     ScrollWhenNeeded->scrollVertical;
		     ScrollWhenNeeded->scrollHorizontal;
        	     false->autoFill;
	             ''->string
	      #)#);
           init::< 
             (# 
             do clear.init; 
	        'Click here to clear the text widget'->clear.label;
                txt.init;
             #);
        #);
      init::<(#do aPane.init; popUp #)
   #);
   theTaskBrowser: @ TopLevelShell
     (# taskList: @ listWidget
	  (# callBack::<
	      (# TF: ^TaskForm; i,n: @integer
	      do &TaskForm[]->TF[];
		 current.listindex+1->n;
		 L: scanTasks
		   (#
		   do (if i+1->i//n then 
			  thisTask[]->TF.theTask[]; TF.init; leave L if)
	      #)#);
             strings::<
	      (# init::<(#do scanTasks(#do thisTask.name->addtext#)
	      #)#)
           #);
	init::<(#do taskList.init; popUp #)
     #);
	     
  aJob: ^ workProc;
  myWorkProc:  workProc
    (# job ::<
	(#
	do L:
	   cycle
	    (#
	    do (if scheduler //true then
		    SUSPEND 
		else
		    3000->theTimer.start;
		    leave L
     if)#)#)#);

  theTimer: @ timer
     (# timeOut::< (#do 'time gone'->putLine; &myWorkProc[]->aJob[]; ajob.start #)#);


do 500->theTimer.start;
   window.init;
#)

