ORIGIN '~beta/basiclib/betaenv'
---lib: attributes---
Node:
  (# isNull:< BooleanValue;
     scan:< (# do inner #);
     add:<
       (# e: @char; S: ^Interior
       enter e 
       do e -> (Interior -> S[]).init;
          inner
       exit S[]
       #)
  exit this(Node)[]
  #);
Interior: Node
  (#
     elm: @char;
     next: ^Node;
     isNull:: FalseValue;
     init:
       (# e: @char
       enter e
       do e -> elm;
          Null -> next[]
       #);
     add::
       (#
       do S[] -> next[]
       #);
     scan::
       (#
       do elm -> put;
          next.scan
       #)
  #);
Null: Node
  (# isNull:: TrueValue;
  #);

List:
  (# first,last: ^Node;
     init:
       (#
       do Null -> first[] -> last[]
       #);
     add:
       (# e: @char
       enter e
       do e -> last.add -> last[];
          (if first.isNull then last[] -> first[] if)
       #);
     scan:
       (#
       do first.scan; newline
       #)
  #)
---program: descriptor---
(# L: @List
do L.init;
   'a'-> L.add;
   L.scan;
   'b'-> L.add;
   L.scan;
   'c'-> L.add;
   L.scan 
#)


