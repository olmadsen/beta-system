ORIGIN '~beta/basiclib/betaenv'
---lib:attributes---
Unit: 
  (# isAtom:< BooleanValue;
     PP:< (# log: @text do INNER exit log[] #);
  do INNER
  #);

Atom: Unit
  (# isAtom:: TrueValue;
     a: ^text; 
     PP::< (#do a[] -> log.putText #)
  enter a[] 
  exit this(Atom)[]
  #);

Conss: Unit
  (# car: ^Unit; cdr: ^Conss;
     scan:
       (# current: ^Unit; L: ^Conss
       do (if car[] <> none then car[] -> current[]; INNER if);
          cdr[] -> L[];
          loop:
            (if L[] <> NONE then 
                (if (L.car[] -> current[]) <> NONE then 
                    INNER
                if);
                L.cdr[] -> L[];
                restart Loop
            if);
       #);
     PP::< 
       (# L: ^Conss; first: @boolean
       do '(' -> log.put;
          (if true then
              true -> first;
              scan(#
                  do (if not first then ' ' -> log.put if);
                     false -> first;
                     current.PP -> log.puttext; 
                  #)
           else
              (if car[] <> NONE then car.PP->log.puttext if);
              cdr[] -> L[];
              Loop:
                (if L[] <> NONE then 
                    (if L.car[] <> NONE then 
                        ' ' -> log.put; L.car.PP->log.puttext 
                    if);
                    L.cdr[] -> L[];
                    restart Loop
                if);
          if);
          ')' -> log.put
       #)
  enter(car[],cdr[])
  exit this(Conss)[]
  #);


Append:
  (* [L1[],L2[],...,Ln[]] -> Append ->  R[] *)
  (# L1,L2,R: ^Conss
  enter(L1[],L2[])
  do (if L1[] = NONE then 
         L2[] -> R[]
      else 
         (L1.car[], (L1.cdr[],L2[]) -> &Append) -> &Conss -> R[] 
     if)
  exit R[]
  #);




