ORIGIN '~beta/basiclib/betaenv'
---lib:attributes---
Unit: 
  (# isAtom:< BooleanValue;
     PP:< (#do INNER #);
  do INNER
  #);

Atom: Unit
  (# isAtom:: TrueValue;
     a: ^text; 
     PP::< (#do a[] -> screen.putText #)
  enter a[] 
  exit this(Atom)[]
  #);

Conss: Unit
  (# car: ^Unit; cdr: ^Conss;
     PP::< 
       (# L: ^Conss
       do '(' -> screen.put;
          (if car[] <> NONE then car.PP if);
          cdr[] -> L[];
          Loop:
            (if L[] <> NONE then 
                (if L.car[] <> NONE then ' ' -> put; L.car.PP if);
                L.cdr[] -> L[];
                restart Loop
            if);
          ')' -> screen.put
       #)
  enter(car[],cdr[])
  exit this(Conss)[]
  #);


Append:
  (* [L1[],L2[],...,Ln[]] -> Append ->  R[] *)
  (# L1,L2,R: ^Conss
  enter(L1[],L2[])
  do (if L1[] = NONE then 
         L2[] -> R[]
      else 
         (L1.car[], (L1.cdr[],L2[]) -> &Append) -> &Conss -> R[] 
     if)
  exit R[]
  #);




