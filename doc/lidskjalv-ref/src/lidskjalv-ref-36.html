<H1> Chapter 10:   The guienvactions Library</H1>

<P> As described in chapter 1, the event handling facilities in guienv includes facilities for attaching actions before and after the predefined actions of an interfaceObject.  These actions are created as instances of the action pattern.  However, in order to be able to access the informations related to the specific event, specialized action patterns are defined for each event type.  These actions are defined in this library (and those described in chapter 12 and 13).</P>


<A NAME=HEADING37-0></A>
<H1> Using the guienvactions Library</H1>
<P> Remember that in order to utilize this extension to Lidskjalv, the fragment guienvactions must be included as follows:</P>
<P> </P>
<PRE CLASS=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/guienvactions'
--- program: descriptor ---
guienv(# beforeKeyDown: @keyDownAction
           (# do 'Hello' -&gt; putText #);
         ...
      do ...
         beforeKeyDown[] -&gt; prependAction;
         ...
      #)
</PRE>


<A NAME=HEADING38-0></A>
<H1> Examples of Use of the guienvactions Fragment</H1>

<A NAME=HEADING39-0></A>
<H2> Using keyboard actions</H2>
<P> This demo program illustrates the action facilities by attaching actions to be executed before and after onKeyDown events in a edittext control.</P>
<P> </P>


<H4 class=betacaption>keyboardactions.bet</H4>

<PRE class=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/guienvactions';
(* This demo shows how to prepend/append actions to keyDown events in
 * an editText control.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# menubarType::
          (# testMenu: @menu
               (# quitItem: @menuItem
                    (# open::
                         (# do 'Quit' -&gt; name #);
                       eventHandler::
                         (# onSelect::
                              (# do terminate #)
                         #)
                    #);
                  open::
                    (# do quitItem.open;  quitItem[] -&gt; append #)
               #);
             open::
               (# do testMenu.open; testMenu[] -&gt; append #)
          #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        editText1: @editText
          (# beforeKeyDown: @keyDownAction
               (# do 'My ' -&gt; puttext #);
             afterKeyDown1: @keyDownAction
               (# do 'is ' -&gt; puttext #);
             afterKeyDown2: @keyDownAction
               (# do 'EditText1' -&gt; putline #);
             open::
               (# 
               do (40,40) -&gt; position;
                  (100,30) -&gt; size;
                  beforeKeyDown[] -&gt; prependAction;
                  afterKeyDown1[] -&gt; appendAction;
                  afterKeyDown2[] -&gt; appendAction
               #);
             eventHandler::
               (# onKeyDown::
                    (# do 'name ' -&gt; puttext #)
               #)
          #);
        open::
          (# 
          do (400,400) -&gt; size;
             editText1.open;
             contents -&gt; target
          #)
     #)
do theWindow.open
#)
</PRE>


<BLOCKQUOTE>
<IMG ALIGN=TOP SRC=images/keyboardactions.gif>
</BLOCKQUOTE>










