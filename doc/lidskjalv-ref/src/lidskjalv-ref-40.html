<H1> Chapter 11:   The controlactions Library</H1>

<P> The controlactions library defines the actions related to the events, related to the interfaceObjects, described in the control library.</P>


<A NAME=HEADING41-0></A>
<H1> Using the controlactions Library</H1>
<P> Remember that in order to utilize this extension to Lidskjalv, the fragment controlactions must be included as follows:</P>
<P> </P>
<PRE CLASS=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controlactions'
--- program: descriptor ---
guienv(# lca: @labelChangedAction;
         ...
      do ...
         lca[] -&gt; appendAction;
         ...
      #)
</PRE>


<A NAME=HEADING42-0></A>
<H1> Examples of Use of the controlactions Fragment</H1>
<P> This example illustrates how to use the labelChangedAction to adjust the size of a pushButton to the length of its label.</P>
<P> </P>

<H4 class=betacaption>buttonactions.bet</H4>
<PRE class=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/controlsactions';
(* This demo shows how to use the labelChangedAction to adjust the
 * size of a pushButton to the length of its label.  The action is
 * created by remote access to show that actions can be added without
 * specializing the button.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# theLabelChanged: @aButton.labelChangedAction
          (# theTextStyle: ^textStyle; widthOfLabel: @integer;
             lw,lh: @integer
          do aButton.size -&gt; (lw,lh);
             aButton.style -&gt; theTextStyle[];
             aButton.label -&gt; theTextStyle.widthOfText -&gt; widthOfLabel;
             (if (lw &lt; widthOfLabel + 6) then
                 widthOfLabel + 6 -&gt; lw
             if);
             (if (lh &lt; theTextStyle.lineHeight + 2) then
                 theTextStyle.lineHeight + 2 -&gt; lh
             if);
             (lw,lh) -&gt; aButton.size
          #);
        aButton: @pushButton;
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        open::
          (# 
          do (40,40) -&gt; position;
             (300,300) -&gt; size;
             aButton.open;
             (10,10) -&gt; aButton.position; 
             (50,16) -&gt; aButton.size;
             'Button1' -&gt; aButton.label;
             theLabelChanged[] -&gt; aButton.appendAction;
             'A Long Button Label' -&gt; aButton.label
          #)
     #)
do theWindow.open
#)
</PRE>


<BLOCKQUOTE>
<IMG ALIGN=TOP SRC=images/controlsactions.gif>
</BLOCKQUOTE>









