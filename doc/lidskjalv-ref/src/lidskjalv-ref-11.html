<H1> Examples of Use of the guienv Fragment</H1>

<P> The demo programs in this manual can be found in the reference demo subdirectory in the guienv directory. The location of the directory in installation-dependent <BR></P>
<P> ~beta/guienv/demo/ReferenceDemos</P>
<P> The demo directory contains many more demo programs than is included in this manual.  Please inspect the demo directory for other illustrative demo programs.  Appendix A contains a short overview of the demos in the demo directory.</P>

<P> This demo program is nearly the simplest possible Lidskjalv program.  It opens one window and prints activate (resp. deactivate) each time the window is made active (resp. inactive).</P>
<P> </P>

<H4 class=betacaption>simplewindow.bet</H4>
<PRE class=beta>
ORIGIN '~beta/guienv/guienv';
(* This demo shows how to create a very simple window and it
 * illustrates the activate/deactivate event.
 *)
--- program: descriptor ---
guienv
(# simpleWindow: @window
     (# eventHandler::
          (# onAboutToClose:: (# do terminate #);
             onActivate::
               (# do 'activate' -&gt; putline #);
             onDeactivate::
               (# do 'deactivate' -&gt; putline #)
          #);
        open:: 
          (# do (400,400) -&gt; size #)
     #)
do simpleWindow.open
#)
</PRE>


<P><blockquote> <IMG ALIGN=center SRC=images/simplewindow.gif></blockquote></P>

<P> This demo program illustrates the use of standard menubar where the file menu only has one menuItem.  It also illustrates how to further bind noMouseUp, onMouseDown and onKeyDown event patterns.</P>

<P> 

<H4 class=betacaption>windowWithStandardMenubar.bet</H4>


<PRE class=beta>
ORIGIN '~beta/guienv/guienv';
(* This demo shows how to create a simple window with a
 * standardMenubar where the file menu only has one menuitem. It also
 * illustrates how to further bind the onMouseDown, onMouseUp,
 * onKeyDown event patterns.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# menubarType:: standardMenubar
          (# fileMenu::
               (# quitItem: @menuitem
                    (# open::
                         (# do 'Quit' -&gt; name #);
                      eventHandler::
                         (# onSelect::
                              (# do terminate #)
                         #)
                    #);
                  open::
                    (# 
                    do 'File' -&gt; name;
                       quitItem.open; quitItem[] -&gt; append
                    #)
               #)
          #);
        eventHandler::
          (# onAboutToClose:: (# do terminate #);
             onMouseDown::
               (# 
               do 'MouseDown: ' -&gt; puttext;
                  buttonState -&gt; putint;
                  (if doubleClick then
                      ' doubleclick' -&gt; puttext
                  if);
                  newLine
               #);
             onMouseUp::
               (# 
               do 'onMouseUp ' -&gt; puttext;
                  buttonState -&gt; putint;
                  (if doubleClick then
                      ' doubleclick' -&gt; puttext
                  if);
                  newLine
               #);
             onKeyDown::
               (# 
               do 'onKeyDown: ' -&gt; puttext;
                  ch -&gt; put;
                  newLine
               #)
          #)
     #)
do theWindow.open
#)
</PRE>


<P> 

<blockquote><IMG ALIGN=center SRC=images/windowWithStandardMenuBar.gif>
</bloackquote>






<!-- TOC -->




