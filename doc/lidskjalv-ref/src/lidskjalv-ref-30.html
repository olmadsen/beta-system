<H1>The graphics Library</H1>

<P> The graphics library is defining a simple drawing system (without any event handling facilities).  Graphics is intended for lightweight drawings in canvasses, dialogs, etc., where user interaction with the drawings are not important.</P>
<P> Graphics defines a pen for controlling the color, size, etc. of the lines and points to be drawn.  Furthermore, graphics defines seleval drawing operations, such as moveTo, drawTo, drawSpot, drawLine, etc.</P>


<A NAME=HEADING31-0></A>
<H2> Using the graphics Library</H2>
<P> Remember that in order to utilize this extension to Lidskjalv, the fragment graphics must be included as follows:</P>
<P> </P>
<PRE CLASS=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/graphics'
--- program: descriptor ---
guienv(# pm: @pixmap;
         ...
      do ...
         ... -&gt; pm.read;
         ...
      #)
</PRE>


<A NAME=HEADING32-0></A>
<H2> Examples of Use of the graphics Fragment</H2>
<P> This example illustrates reading a pixmap from a file and display it on the screen.</P>
<P> </P>

<H4 class=betacaption>drawbitmap.bet</H4>
<PRE class=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/graphics';
(* This demo is an example showing how read a bitmap from a file, and
 * using the graphics pattern to draw the bitmap on the screen.
 *)
--- program: descriptor ---
guienv
(# pm: @pixmap;
   theWindow: @window
     (# eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        obj: @windowItem
          (# open::
               (# 
               do (50,50) -&gt; position;
                  (pm.width,pm.height) -&gt; size
               #);
             eventHandler::
               (# onRefresh::
                    (# 
                    do graphics
                       (# 
                       do (pm[], (0, 0), (0, 0), pm.width,pm.height)
                            -&gt; drawRaster
                       #)
                    #);
                  onMouseDown::
                    (# do drag #)
               #)
          #);
        open::
          (# 
          do (400,400) -&gt; size;
             obj.open
          #)
     #)
do 'picture' -&gt; pm.read;
   (0xffff,0xffff,0xffff)-&gt;pm.transparentcolor;
   theWindow.open
#)
</PRE>


<BLOCKQUOTE>
<IMG ALIGN=TOP SRC=images/drawbitmap.gif>
</BLOCKQUOTE>









