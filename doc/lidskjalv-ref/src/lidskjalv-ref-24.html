<H1>The scrolllists<A NAME=MARKER-2-155></A> Library</H1>

<P> The scrolllists library contains two subpatterns of windowItem<A NAME=MARKER-2-156></A> which implements scrolling lists:</P>
<P> <center><IMG ALIGN=TOP SRC=images/scrolllisthierarchy.gif></center></P>

<P> ScrollList<A NAME=MARKER-2-157></A> is a general superpattern realizing the basic functionality of displaying and scrolling in a list of equally sized elements (including facilities for managing single and multiple selections of the elements in the scrollList).</P>
<P> ScrollList contains attributes for inserting and deleting elements as well as for managing the selections (inserting, deleting, scanning and testing).</P>
<P> ScrollList is an abstract pattern that cannot be instantiated.</P>

<P> TextScrollList<A NAME=MARKER-2-158></A> is a minor augmentation of scrollList to make support for scrolling lists of text elements.  Adds three additional attributes: setText<A NAME=MARKER-2-159></A>, getText<A NAME=MARKER-2-160></A> and style<A NAME=MARKER-2-161></A>, which allows for accessing and changing the text and the text style of the individual text elements in the scrollList.</P>


<A NAME=HEADING25-0></A>
<H2> Using the scrolllists Library</H2>
<P> Remember that in order to utilize this extension to Lidskjalv, the fragment scrolllists must be included as follows:</P>
<P> </P>
<PRE CLASS=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/scrolllists'
--- program: descriptor ---
guienv(# tsl: @textScrollList;
         ...
      do ...
         tsl.selection.first -&gt; ...;
         ...
      #)
</PRE>


<A NAME=HEADING26-0></A>
<H2> Examples of Use of the scrolllists Fragment</H2>
<P> This example illustrates how to use the textscrolllist
windowItem.</P>

<H4 class=betacaption>textscrolllist.bet</H4>
<PRE class=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/scrolllists';
(* This demo gives a simple example of how to use the textScrollList
 * windowitem.
 *)
--- program: descriptor ---
guienv
(# theWindow: @window
     (# menubarType::
          (# fileMenu: @menu
               (# quitItem: @menuItem
                    (# eventHandler::
                         (# onSelect::
                              (# do terminate #);
                         #);
                       open::
                         (# do 'Quit' -&gt; name #)
                    #);
                  open::
                    (# 
                    do 'File' -&gt; name;
                       quitItem.open; quitItem[] -&gt; append
                    #)
               #);
             open::
               (# 
               do fileMenu.open; fileMenu[] -&gt; append
               #)
          #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        theTextScrollList: @textScrollList
          (# eventHandler::
               (# onMouseDown::
                    (# 
                    do (if doubleClick then
                           selection.first -&gt; gettext -&gt; putline
                       if)
                    #)
               #);
             open::
               (# v: @point;
                  noOfItems: @integer
               do (5,5) -&gt; position;
                  theWindow.size -&gt; v;
                  (10,10) -&gt; v.subtract;
                  v -&gt; size;
                  true -&gt; bindBottom;
                  true -&gt; bindRight;
                  5 -&gt; append;
                  theTextScrollList.numberOfItems -&gt; noOfItems;
                  (noOfItems-4,'Abraham Lincoln 1') -&gt; settext;
                  (noOfItems-3,'Abraham Lincoln 2') -&gt; settext;
                  (noOfItems-2,'Abraham Lincoln 3') -&gt; settext;
                  (noOfItems-1,'Abraham Lincoln 4') -&gt; settext;
                  (noOfItems,'Abraham Lincoln 5') -&gt; settext
               #)
          #);
        open::
          (# 
          do (400,400) -&gt; size;
             theTextScrollList.open;
             theTextScrollList[] -&gt; target
          #)
     #)
do theWindow.open
#)
</PRE>

<P> </P>

<BLOCKQUOTE>
<IMG ALIGN=TOP SRC=images/textscrolllist.gif>
</BLOCKQUOTE>









