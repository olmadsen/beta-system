<H1> Appendix A</H1>
<P> This appendix contains the source code for the BETA program used as example in the tutorial. The source consists of the files record.bet and recordlib.bet.</P>
<P> </P>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/betaenv';
(* record.bet
 * 
 * COPYRIGHT
 *			 Copyright Mj&oslash;lner Informatics, 1989 - 1999
 *			 All rights reserved.
 *)
INCLUDE 'recordlib'
--- PROGRAM:descriptor ----
(#	Birger, PeterA, Bible, LarsP, ClausN, ElmerS, KimJ:  ^Record;
	Greg: @Register;
	Ereg: @Register
	(# element::&lt; Employee; 
	   Display::&lt; (#do '/Employee '-&gt;putText; INNER #);
	#);
do 	(0 ,'Bimmer Moller-Pedersen','Unknown',- 200,'Piccolo')
		-&gt; &amp;NewEmployee-&gt;Birger[];
	(1,'Peter Andersen','male','missing M.D.')
		-&gt; &amp;NewStudent-&gt;peterA[];
	(2,'Lars Bak','male',1000000,'Garbage collector') 
		-&gt; &amp;NewEmployee-&gt;LarsP[];
	(3,'Claus Norgaard','male',1000010,'Senior Coder')
		-&gt; &amp;NewEmployee-&gt;ClausN[];
	(4,'Elmer Sandvad','male',1000050,'Senior Supporter')
		-&gt; &amp;NewEmployee-&gt;ElmerS[];
	(5,'Kim Jensen M|ller','male',999990,'Painter')
		-&gt; &amp;NewEmployee-&gt;KimJ[];
	(9,'Kristensen et al.','Object Oriented Programming in the BETA language') 
		-&gt; &amp;NewBook-&gt;Bible[];
	Birger.Display; PeterA.Display; Bible.Display;
	'==========================='-&gt;putText; newline;
	Greg.init; Ereg.init;
	Birger[]-&gt;Greg.insert; Bible[]-&gt;Greg.insert; PeterA[]-&gt;Greg.insert;
	ClausN[]-&gt;Ereg.insert; LarsP[]-&gt;Ereg.insert; ElmerS[]-&gt;Ereg.insert;
	KimJ[]-&gt;Ereg.insert;
	Greg.display; Ereg.display;
	(if LarsP[]-&gt;Ereg.has then
	    'LarsP in employee register'-&gt;puttext
	 else
            'LarsP not in employee register'-&gt;puttext 
	if);
	newline;
	(if LarsP[]-&gt;Greg.has then
	    'LarsP in general register'-&gt;puttext
	 else
            'LarsP not in general register'-&gt;puttext 
	if);
	newline;
#)
</PRE>
<P> </P>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/betaenv';
(* recordlib.bet
 *
 * COPYRIGHT
 *  Copyright Mj&oslash;lner Informatics, 1989 - 1994
 *  All rights reserved.
 *) 
INCLUDE '~beta/containers/hashTable'
--- lib:attributes ---
Record:
  (# key: @Integer;
     Display:&lt; (* declaration of a virtual (procedure) pattern  *)
       (#
       do newline;
          '-------------------'-&gt;putText; newline;
          'Record:      Key  = '-&gt;putText; Key-&gt;putInt; newline;
          INNER
       #);
  #);
Person: Record
  (# name,sex: @Text;
     Display::&lt;  (* a further binding of Display from Record *)
       (#
       do 'Person:      Name = '-&gt;putText; name[]-&gt;putText; newline;
          '             Sex  = '-&gt;putText; sex[]-&gt;putText; newline;
          INNER
       #);
  #);
Employee: Person
  (# salary: @Integer; position: @Text;
     Display::&lt;
       (#
       do 'Employee:    Salary = '-&gt;putText;   salary  -&gt;putInt; newline;
          '             Position = '-&gt;putText; Position[]-&gt;putText; newline;
          INNER
       #);
  #);
Student: Person
  (# status: @Text;
     Display::&lt;
       (#
       do 'Student:     Status = '-&gt;putText; Status[]-&gt;putText; newline;
          INNER
       #)
  #);
Book: Record
  (# author, title: ^Text;
     Display ::&lt;
       (#
       do 'Book:        Author = '-&gt;putText; Author[]-&gt;putText; newline;
          '             Title  = '-&gt;putText; Title[]-&gt;putText; newline;
          INNER
       #)
  #);
NewRecord:
  (# resultType:&lt; Record;
     new: ^resultType;
     key: @Integer;
  enter key
  do &amp;resultType[]-&gt;new[]; 
     key-&gt;new.key;
     INNER;
  exit new[]
  #);
NewPerson: NewRecord
  (# resultType::&lt; Person;
     N,S: @Text
  enter (N,S)
  do N-&gt;new.Name; S-&gt;new.Sex; 
     INNER; 
  #);
NewEmployee: NewPerson
  (# resultType::&lt; Employee;
     S: @Integer; P: @Text
  enter (S,P)
  do S-&gt;new.Salary; P-&gt;new.Position;
     INNER; 
  #);
NewStudent: NewPerson
  (# resultType::&lt; Student;
     S: @Text
  enter S
  do S-&gt;new.Status; 
     INNER; 
  #);
NewBook: NewRecord
  (# resultType::&lt; Book;
     A,T: @Text
  enter (A,T)
  do A-&gt;new.Author; T-&gt;new.Title;
     INNER;
  #);

Register: HashTable
  (# element::&lt; Record; 
     (* Virtual class specifying the (generic) element 
      * type of the hashtable. *)
     
     hashfunction::&lt; (# do e.key-&gt;value #);
     (* Specialization of the hashfunction to use on 
      * elements of the hashtable. *)
     
     Display:&lt;
       (* Display all elements of the table. *)
       (#
       do newline; '############ Register Display '-&gt;putText;
          INNER;
          newline;
          scan (# do current.display #);
          '############ End Register Display #######'-&gt;putText; newline
       #);
     
     Has:
       (* Check if an element is present in the table. *)
       (# e: ^element;
          found: @Boolean;
       enter e[]
       do 
          e[]-&gt;hashfunction-&gt;findIndexed
          (# predicate::&lt; (# do current.key = e.key-&gt;value #);
             notFound::&lt; (# do false-&gt;found #);
          do true-&gt;found;
          #);
       exit found
       #);
  #)
</PRE>



