<H1> Regexp Demos</H1>
<A NAME=HEADING24-1></A>
<H1> Search for regexp</H1>
<P> The following example is a simple program that enters a regexp from the keyboard, followed by another test string, trying to find the regexp in the test string. If found, the registers are printed on the screen, and the corresponding matched substrings:</P>

<A NAME=searchDemo.bet><H4 CLASS=betacaption>searchDemo.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/regexp';
--- program: descriptor ---
(#
do loop:
     (# regex: ^text;
     do 'Search for: ' -&gt; puttext; getLine -&gt; regex[];
        (if regex.length = 0 then leave loop if);
        loop:
          (# string: ^text;
          do 'Search for: '-&gt;puttext; regex[]-&gt;puttext;
             ' in: ' -&gt; puttext; getline -&gt; string[];
             (if string.length = 0 then leave loop if);
             string.reset;
             regex[] -&gt; string.regexp_search
             (# noMatch::&lt; (# do '\tNo match' -&gt; putline #)
             do (for i: regexp_numberOfRegisters repeat
                     (if (i-1-&gt;regs.start)&gt;=0 then
                         '\tregister '-&gt;puttext; i-1 -&gt; putint; ': '-&gt;puttext;
                         i-1-&gt;regs.start-&gt;putint;
                         ' to '-&gt;puttext;
                         i-1-&gt;regs.end-&gt;putint;
                         ': '-&gt;puttext;
                         ((i-1-&gt;regs.start)+1, i-1-&gt;regs.end)-&gt;string.sub-&gt;putline
                     if)
                for);
             #);
             restart loop
          #);
        restart loop
     #)
#)
</PRE>
<P> Output from the demo might be:</P>
<PRE CLASS=beta>
Search for: 1\(234\)5
Search for: 1\(234\)5 in: dfjdfhdf1235jfdkj12345ghdf
register 0: 17 to 22: 12345
register 1: 18 to 21: 234
Search for: 1\(234\)5 in: 12345
register 0: 0 to 5: 12345
register 1: 1 to 4: 234
Search for: 1\(234\)5 in: 123
No match
Search for: 1\(234\)5 in:

Search for: \(22\).*\1
Search for: \(22\).*\1 in: xxx22yyy2zzz
No match
Search for: \(22\).*\1 in: xxx22yyy22zzz
register 0: 3 to 10: 22yyy22
register 1: 3 to 5: 22
</PRE>
<A NAME=HEADING24-57></A>
<H1> Replace with a regexp</H1>
<P> The following example is nearly identical to the previous, except that it allows for the specification of a replacement string:</P>

<A NAME=replaceDemo.bet><H4 CLASS=betacaption>replaceDemo.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/regexp';
--- program: descriptor ---
(#
do loop:
     (# regex: ^text;
     do 'Replace: ' -&gt; puttext; getLine -&gt; regex[];
        (if regex.length = 0 then leave loop if);
        loop:
          (# string: ^text;
          do 'Replace: '-&gt;puttext; regex[]-&gt;puttext;
             ' in: ' -&gt; puttext; getline -&gt; string[];
             (if string.length = 0 then leave loop if);
             loop:
               (# t, replacement: ^text
               do string.copy -&gt; t[]; t.reset;
                  'Replace: '-&gt;puttext; regex[]-&gt;puttext;
                  ' in: ' -&gt; puttext; string[]-&gt;puttext;
                  ' with: '-&gt;puttext; getline -&gt; replacement[];
                  (if replacement.length = 0 then leave loop if);
                  (regex[], replacement[]) -&gt; t.regexp_replace
                  (# noMatch::&lt; (# do '\tNo match'-&gt;screen.putline #) #);
                  'Replace: '-&gt;puttext; regex[]-&gt;puttext;
                  ' in: ' -&gt; puttext; string[]-&gt;puttext;
                  ' with: '-&gt;puttext; replacement[]-&gt;puttext;
                  ' gives: '-&gt;puttext; t[] -&gt; putline;
                  restart loop
               #);
             restart loop
          #);
        restart loop
     #)
#)
</PRE>
<P> Output from the demo might be:</P>
<PRE CLASS=beta>
Replace: [Ww]ord
Replace: [Ww]ord in: This Word is in capital
Replace: [Ww]ord in: This Word is in capital with: sentence
Replace: [Ww]ord in: This Word is in capital with: sentence gives: This sentence is in capital
</PRE>
<P> </P>





