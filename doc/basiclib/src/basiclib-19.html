<H1> Using the random Fragment</H1>
<P> A program using the random fragment will have the following structure:</P>
<PRE CLASS=beta>
INCLUDE '~beta/basiclib/random
--- program: descriptor ---
(# ...
   i: @integer;
do ...
   (123, 456) -&gt; setsd;
   ...
   (1,100) -&gt; inguin -&gt; i;
   ...
#)
</PRE>
<A NAME=HEADING19-13></A>
<H1> Illustrating Integer Random Generator</H1>

<A NAME=tstign.bet><H4 CLASS=betacaption>tstign.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/random';
--- program: descriptor ---
(# testIgn:
     (# iarray: [mxint]@integer;
        itmp: @integer;
        lo, hi, i, up: @integer
     do (for i:nrep repeat
             (1,mxint)-&gt;ignuin-&gt;itmp;
	     itmp-&gt;screen.putint(# do 7-&gt;width #);
             (if (i mod 10) = 0 then newline if);
             iarray[itmp]+1-&gt;iarray[itmp];
        for);
        newline;
        '         Counts of Integers Generated:'-&gt;putline;
        (* Print 10 to a line using 7 characters for each field.         *)
        mxint-&gt;up; 1-&gt;lo;
        l: (if lo&lt;=up then
               (lo+9,up)-&gt;min-&gt;hi;
               lo-&gt;i;
    	       ll: (if i&lt;=hi then
                       iarray[i]-&gt;screen.putint(# do 7-&gt;width #);
                       i+1-&gt;i;
                       restart ll
                   if);
               screen.newline;
               lo+10-&gt;lo;
               restart l;
           if)
     #);
   
   mxint,nrep: @integer;

do ' Tests uniform random integer generator.'-&gt;putline; newline;
   
   ' Enter two seeds to initialize rn generator: '-&gt;puttext;
   (getint,getint)-&gt;setall;
      
   ' Enter maximum uniform integer: '-&gt;puttext;
   getint-&gt;mxint;

   ' Enter number of randoms to generate: '-&gt;puttext;
   getint-&gt;nrep;
   
   testIgn;
#)
</PRE>
<P> </P>





