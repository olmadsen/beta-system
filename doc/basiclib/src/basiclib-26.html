<H1> Using the file Fragment</H1>
<P> A program using the file fragment will have the following structure:</P>
<PRE CLASS=beta>
INCLUDE '~beta/basiclib/file
--- program: descriptor ---
(# ...
   f: @file;
do ...
   'Hello world' -&gt; f.putline;
...
#)
</PRE>
<A NAME=HEADING26-11></A>
<H1> Using diskEntry</H1>
<P> An example showing the use of the path attribute of DiskEntry: The path specified on the command line is decomposed, and the various parts of it are printed: </P>

<A NAME=decompose.bet><H4 CLASS=betacaption>decompose.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/file';

--- program: descriptor ---
(* An example showing the use of the 'path' attribute of DiskEntry: The path
 * specified on the command line is decomposed, and the various parts of it are
 * printed.
 *)

(# e: @diskentry;
do (if noOfArguments &lt;&gt; 2 then
       'Usage: ' -&gt; puttext; 1-&gt;arguments-&gt;puttext; ' path' -&gt; putline;
       stop;
   if);
   2 -&gt; arguments -&gt; e.path;
   'The path '''-&gt;puttext; 
   e.path -&gt; puttext;
   ''' is composed like this:'-&gt;putline;
   'Head of path:      '-&gt;puttext; e.path.head -&gt; putline;
   'Name:              '-&gt;puttext; e.path.name -&gt; putline;
   'Prefix of name:    '-&gt;puttext; e.path.name.prefix -&gt; putline;
   'Suffix of name:    '-&gt;puttext; e.path.name.suffix -&gt; putline;
   'Extension of name: '-&gt;puttext; e.path.name.extension -&gt; putline;
   (if e.exists then '(and the entry exists on disk)' -&gt; putline;
    else '(there is no such entry on disk)' -&gt; putline;
   if);
#)
</PRE>
<A NAME=HEADING26-43></A>
<H1> Using File</H1>
<P> Program showing the use of exceptions in files: if the input file specified on the command line cannot be opened, the exception noSuchFileError is raised. In this example, it is further bound specifying that the program should continue after the exception is raised. Then a new file name is prompted for. Instead of this approach, of course, the attributes exists of diskEntry could have been used.</P>
<P> If the output file could not be opened, the exception noSpaceError is raised, and this exception is further bound to print a message:</P>

<A NAME=fileerror.bet><H4 CLASS=betacaption>fileerror.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/file';
--- program: descriptor ---

(* Program showing the use of exceptions in files: if the input file
 * specified on the command line cannot be opened, the exception
 * NoSuchFileError is raised. In this example, it is further bound
 * specifying that the program should continue after the exception is raised.
 * Then a new file name is prompted for.
 * Instead of this approach, of course, the attributes 'exists' of DiskEntry
 * could have been used.
 * If the output file could not be opened, the exception NoSpaceError is raised,
 * and this exception is further bound to print a message.
 *)
 
(# outFile: @file
     (# NoSpaceError ::&lt; 
          (# do 'It is time to delete garbage!' -&gt; msg.putLine #); 
     #);     
   inFile: @ file 
     (# NoSuchFileError ::&lt; (# do true -&gt; continue; false -&gt; OK #); 
     #);
   OK: @ boolean; 
   
do (if noOfArguments &lt;&gt; 2 then
       'Usage: ' -&gt; puttext; 1-&gt;arguments-&gt;puttext; ' out-file' -&gt; putline;
       stop;
   if);
   2 -&gt; arguments -&gt; outFile.name;
   outFile.OpenWrite;
   
   'nofile.bet' -&gt; inFile.name;
   true -&gt; OK;
   openFile: 
     (# 
     do inFile.OpenRead;
        (if not OK then
            'File does not exist: ' -&gt;  screen.puttext;
            infile.name -&gt; putline;
            'Type input file name: ' -&gt;  screen.putText;
            inFile.name.read;
            true -&gt; OK;
            restart openFile
        if);
     #);
   
   readFile: 
     (# 
     do (if not inFile.eos then 
            inFile.getatom -&gt; outFile.putText;
            outFile.newLine;
            restart readFile
            else leave readFile
        if)
     #);
   inFile.close;
   outFile.close;
   'Tokens from '''-&gt;puttext; infile.name -&gt; puttext;
   ''' copied to '''-&gt;puttext; outfile.name -&gt; puttext;
   ''''-&gt;putline;
#)
</PRE>
<P> </P>





