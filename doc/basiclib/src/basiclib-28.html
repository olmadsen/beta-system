<H1> Using the directory Fragment</H1>
<P> A program using the directory fragment will have the following structure:</P>
<PRE CLASS=beta>
INCLUDE '~beta/basiclib/directory'
--- program: descriptor ---
(# ...
   dir: @directory;
do ...
   '/user/local/lib/beta/basiclib/' -&gt; dir.name;
   dir.scanEntries(# do found.name -&gt; putline #);
...
#)
</PRE>
<A NAME=HEADING28-12></A>
<H1> Listing a directory</H1>
<P> Program showing a simple use of directory: The directory with the path given as argument is scanned, and the names of all the entries are printed with an indication of what type of entry it is. This is done using the select pattern of scanentries. This is a more efficient strategy than using found.entry.isFile etc., possibly correcting for the case that 'd' is not current working directory.</P>
<P> If the path given is not a directory, an exception will be raised:</P>

<A NAME=listDir.bet><H4 CLASS=betacaption>listDir.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/directory';

--- program: descriptor ---

(* Program showing a simple use of directory: The directory with the path 
 * given as argument is scanned, and the names of all the entries are printed
 * with an indication of what type of entry it is.
 * This is done using the 'select' pattern of 'scanentries'. This is a more
 * efficient startegy than using 'found.entry.isFile' etc., possibly 
 * correcting for the case that 'd' is not current working directory.
 * If the path given is not a directory, an exception will be raised.
 *)

(# arg: ^text; 
   d: @directory; 
   nl: @boolean; 
   full: @boolean;
   usage:
     (# do 'Usage: ' -&gt; puttext; 
        1-&gt;arguments-&gt;puttext; 
        ' [-f] path' -&gt; putline;
        stop;
     #);
do (* Parse command line *)
   (if noOfArguments
    // 1 then '.' -&gt; d.name
    // 2 then
       2 -&gt; arguments -&gt; arg[];
       (if '-f' -&gt; arg.equal then
           true -&gt; full;
           '.' -&gt; d.name;
        else
           arg[] -&gt; d.name;
       if)
    // 3 then
       2 -&gt; arguments -&gt; arg[];
       (if '-f' -&gt; arg.equal then
           true -&gt; full;
        else usage;
       if);
       3 -&gt; arguments -&gt; d.name;
    else
       usage;
   if);
   
   (* Scan directory *)
   newline;
   'The content of '''-&gt; puttext;
   d.name -&gt; puttext;
   ''' is: ' -&gt; putline;
   d.scanEntries
   (# 
   do select
      (# whenFile::&lt;
           (# do 'File:      ' -&gt; puttext; #);
         whenDir::&lt;
           (# do 'Directory: ' -&gt; puttext; #);
         whenOther::&lt;
           (# do '(Unknown): ' -&gt; puttext; #);
      #);
      (if full then foundFullPath -&gt; putline; 
       else found.path -&gt; putline; 
      if);
   #);
   newline;
#)
</PRE>
<P> </P>





