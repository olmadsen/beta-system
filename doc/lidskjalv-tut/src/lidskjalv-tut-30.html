<H1> Scroll Lists</H1>
<P> Scroll lists are used to display an interface object in which the user is able to select elements from a list of elements (e.g. file names).</P>
<P> A scrollList maintains the list of elements and the user is allowed to scroll in the list or to select elements in the list by clicking on them.  ScrollList has operations for inserting, deleting and scanning the elements in the list.  Furthermore, scrollList maintains a list of the currently selected elements in the list.</P>
<P> TextScrollList is a subpattern of scrollList for maintaining a list of text strings.  TextScrollList defines additional operations for manipulating the text strings and for manipulation the text style of the elements in the list.</P>
<P> The following is an example of a textScrollList for selecting in a list of items:</P>
<P> </P>
<H4 class=betacaption> scrollinglist.bet</H4>
<PRE CLASS=beta>
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/scrolllists'
--- program: descriptor ---
guienv
(# scrollListWindow: @window
     (# scrollList: @textScrollList
          (# open::
               (# tmpText: @text;
                  windowSize: @point
               do (5,20)-&gt;position;
                  20-&gt;append;
                  (for inx: 20 repeat
                       'Item: '-&gt;tmpText;
                       inx-&gt;tmpText.putint;
                       (inx,tmpText[])-&gt;setText
                  for);
                  this(scrollListWindow).size-&gt;windowSize;
                  position-&gt;windowSize.subtract;
                  (0,15)-&gt;windowSize.subtract;
                  windowSize-&gt;size;
                  true-&gt;bindRight-&gt;bindBottom
               #);
             eventhandler::
               (# onSelect::
                    (# do item-&gt;gettext-&gt;putline #)
               #)
          #);
        eventhandler::
          (# onAboutToClose:: (# do terminate #) #);
        open::
          (#
          do 'scrollingList'-&gt;title;
             scrollList.open 
          #)
     #)
do scrollListWindow.open
#)
</PRE>
<H4 class=betacaption> screendumps (Windows 95)</H4>

<BLOCKQUOTE>
<IMG ALIGN=TOP SRC=scrollinglist.gif>
<IMG ALIGN=TOP SRC=scrollinglist-console.gif>
</BLOCKQUOTE>





