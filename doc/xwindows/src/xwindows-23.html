<H1> Examples of using Menus</H1>
<A NAME=HEADING23-1></A>
<H1> A SimpleMenu example</H1>
<P> The following program demonstrates how the SimpleMenu<A NAME=MARKER-2-237></A> and the Sme<A NAME=MARKER-2-238></A> widgets can be used. The menu is activated from a MenuButton<A NAME=MARKER-2-239></A> widget.</P>
<A NAME=HEADING23-3></A>
<A NAME=menu.bet><H4 CLASS=betacaption>menu.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/Xt/awenv'
--- PROGRAM: descriptor --- 
AwEnv
(# command: @MenuButton
     (# theMenu: @SimpleMenu
          (# item1,item2,item3,item4: @SmeBSB
               (# callback::&lt;
                    (# do label -&gt; putText; ' selected'-&gt;putLine #);
               #);
             
             quit: @Item(# callback::&lt; (# do stop #)#);
             
             init::&lt;
               (# bl: ^Blank;
               do quit.init; 
                  item1.init; 
                  'item1' -&gt; item1.label;
                  item2.init;
                  'item2' -&gt; item2.label;
                  &amp;blank[] -&gt; bl[]; bl.init;
                  20 -&gt; bl.height;
                  item3.init;
                  'item3' -&gt; item3.label;
                  item4.init;
                  'item4' -&gt; item4.label;
               #);
          #);
        
        init::&lt;
          (#
          do 'Click here for a pulldown menu' -&gt; label;
             theMenu.init; 
             theMenu[] -&gt; setMenu;
          #);
     #);
do command.init;
#)
</PRE>
<P> When the program is run, the following window is popped up:</P>
<P> </P>
<BLOCKQUOTE><IMG ALIGN=TOP SRC=images/awenv/menu.gif></BLOCKQUOTE>
<P> </P>
<P> when the users clicks with the left mouse-button, the following menu is popped up:</P>
<P> </P>
<BLOCKQUOTE><IMG ALIGN=TOP SRC=images/awenv/menu-popdown.gif></BLOCKQUOTE>
<P> </P>
<A NAME=HEADING23-51></A>
<H1> Using cascading menus</H1>
<P> The following program is an extension of the previous, where a cascade menu item has been added.</P>
<A NAME=HEADING23-53></A>
<A NAME=cascademenu.bet><H4 CLASS=betacaption>cascademenu.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/Xt/awenv' 
--- PROGRAM: descriptor ---
AwEnv
(# main: @Box
     (# theMenuButton: @MenuButton
          (# init::&lt; 
               (# 
               do firstMenu.init; 
                  firstMenu[] -&gt; setmenu;
                  'Click here for a pulldown menu' -&gt; label;
               #)
          #);
        Menu: SimpleMenu
          (# item1,item2,item3,item4: @SmeBSB
               (# callback::&lt; 
                    (# 
                    do name -&gt; putText; ' selected' -&gt; putLine
                    #)
               #);
             quit: @SmeBSB(# callback::&lt; (# do stop #)#);
             init::&lt;
               (# bl: ^blank;
               do quit.init; 
                  ('item1', THIS(Menu)) -&gt; item1.init; 
                  ('item2', THIS(Menu)) -&gt; item2.init;
                  &amp;blank[] -&gt; bl[]; bl.init;
                  20 -&gt; bl.height;
                  ('item3', THIS(Menu)) -&gt; item3.init;
                  ('item4', THIS(Menu)) -&gt; item4.init;
                  INNER;
               #);
          #);
        firstMenu: @Menu
          (# oneMore: @SmeCascade
               (# theMenu: @Menu;
                  init::&lt; 
                    (# do theMenu.init; theMenu -&gt; subMenu #)
               #);
             init::&lt; (# do oneMore.init #)
          #);    
        init::&lt; 
          (# do theMenuButton.init; #);
     #);
do main.init;
#)
</PRE>
<P> When the cascading menu entry is selected, the window looks like this:</P>
<P> </P>
<BLOCKQUOTE><IMG ALIGN=TOP SRC=images/awenv/cascadingmenu.gif></BLOCKQUOTE>
<P> </P>





