<H1> Examples using Xt widgets</H1>
<P> The following program illustrates the basic structure of an XtEnv application. It does nothing but popping up a window which is 50 pixels high and 150 pixels wide.</P>
<A NAME=HEADING12-2></A>
<A NAME=generic.bet><H4 CLASS=betacaption>generic.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/Xt/xtenv' 
--- program : descriptor --- 
XtEnv
(# widget: @Core 
do widget.init; 
   50  -&gt; widget.height; 
   150 -&gt; widget.width 
#)
</PRE>
<P> In a specialization of XtEnv, a Core<A NAME=MARKER-2-119></A> widget called widget is initialized. This will be a child of toplevel. Values are assigned to some of widget's attributes, whereafter the control returns to XtEnv. In the prefix-part toplevel is realized and XtEnv goes into the basic event loop<A NAME=MARKER-2-120></A>. The realization of toplevel<A NAME=MARKER-2-121></A> results in the popping up of the following window:</P>
<P> </P>
<BLOCKQUOTE><IMG ALIGN=TOP SRC=images/xtenv/generic.gif></BLOCKQUOTE>
<P> </P>
<P> The decoration<A NAME=MARKER-2-122></A> of the window - a titlebar<A NAME=MARKER-2-123></A> with the title and a closebox<A NAME=MARKER-2-124></A> - depends on the window manager<A NAME=MARKER-2-125></A> running. The above dump of the window have been made on a Macintosh using the MacX<A NAME=MARKER-2-126></A> X Window System. MacX contains its own window manager. Other window managers may make other decorations of the window like another type of titlebar or putting a resize-box on.</P>
<P> The name used in the titlebar is the name of the application.</P>
<P> Not very many interesting programs can be written using 'pure' XtEnv. The following program illustrates how the low-level Xlib<A NAME=MARKER-2-127></A> calls can be used together with XtEnv, and it also illustrates how the low-level eventhandler<A NAME=MARKER-2-128></A> facilities can be used. The demo program is thus for the advanced programmer. The demo program is an interactive drawing program. Every time the user presses the mouse button, a line is drawn.</P>
<A NAME=HEADING12-20></A>
<A NAME=draw.bet><H4 CLASS=betacaption>draw.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/Xt/xtenv'
--- PROGRAM: descriptor --- 
XtEnv
(# widget: @Core
     (# px,py: @integer (* Used to hold the previous position *);
        gc: @integer 
          (* The &quot;Graphics Context&quot; used in the drawings *);
        eventHandler::&lt;
          (* Furtherbind to draw a line to the button press 
           * position each time the mouse is pressed.
           *)
          (# bp: @ButtonPress
               (* Called each time the mouse is pressed *)
               (# 
               do (display, window, gc, px, py, 
                   event.x, event.y) -&gt; XDrawLine;
                  (event.x,event.y) -&gt; (px,py);
               #);
             init::&lt; (# do bp.enable #); 
          #);
        init::&lt; (# do screenResource-&gt;XDefaultGCOfScreen-&gt;gc #);
     #);
do widget.init; 
   200 -&gt; widget.height; 
   100 -&gt; widget.width 
#)
</PRE>
<P> The bp object is called every time the user presses the mouse button. The Xlib functions XDrawLine<A NAME=MARKER-2-129></A> and XDefaultGCOfScreen<A NAME=MARKER-2-130></A> are used in the drawing of the line. The program may be used to make drawings like this:</P>
<P> </P>
<BLOCKQUOTE><IMG ALIGN=TOP SRC=images/xtenv/draw.gif></BLOCKQUOTE>
<P> </P>

<P> The above program does not handle expose events<A NAME=MARKER-2-131></A>. If another window is put on top of the above window and afterward moved, the contents of the 'draw' window is erased. The following demo-program is another version of the above program that handles expose-events. It is only necessary to worry about expose-events when drawing by means of low-level Xlib calls.</P>
<A NAME=HEADING12-55></A>
<A NAME=drawWithRefresh.bet><H4 CLASS=betacaption>drawWithRefresh.bet</H4></A>
<PRE CLASS=beta>
ORIGIN '~beta/Xt/xtenv';
INCLUDE '~beta/containers/list';
--- PROGRAM: descriptor --- 
XtEnv
(# widget: @Core
     (# px,py: @integer; (* Previous position *)
        gc: @integer; (* Graphics Context *)
        
        Line: 
          (* Definition of a simple &quot;graphical object&quot;,                         
           * remembering its own state and with the ability 
           * to redraw itself.
           *)
          (# x1,y1,x2,y2: @integer; (* End positions *)
             
             draw: 
               (# do (display,window,gc,x1,y1,x2,y2)-&gt;XDrawLine #);
             init: 
               (# enter ((x1,y1),(x2,y2))
               do THIS(line)[] -&gt; lineList.append
               #);
          #);
        lineList: @List
          (* List of graphical objects to redraw in response to 
           * expose events
           *)
          (# element::&lt; line;
             redraw: scan(# do current.draw #)
          #);
        
        eventHandler::&lt;
          (* Furtherbind to create a new line object each time             
           * the mouse is pressed, and to redraw the entire list 
           * of lines in response to each expose event.
           *)
          (# bp: @ButtonPress
               (# l: ^Line 
               do &amp;Line[] -&gt; l[];
                  ((px,py),(event.x,event.y)) -&gt; l.init;
                  l.draw;
                  (event.x,event.y) -&gt; (px,py);
               #);
             ex: @Exposure(# do lineList.redraw #);
             init::&lt; (# do bp.enable; ex.enable #); 
          #);
        
        init::&lt; 
          (# 
          do 200 -&gt; height; 
             100 -&gt; width;
             screenResource -&gt; XDefaultGCOfScreen -&gt; gc; 
             lineList.init 
          #);
     #);
do widget.init; 
#)
</PRE>





