<H1> <A NAME="LINK-yggdrasil-_Toc364700504"><A NAME=MARKER-9-77></A>Using the Context-Free Level</A></H1>
<P> Consider the references:</P>
<PRE CLASS=beta>
P: ^ProcDcl; B: ^Block; N: ^nameDecl
</PRE>
<P> P.getBody refers to the block of P, and after executing the assignment P.getBody -&gt; B[], B will refer to this block. P.getName-&gt;N[]; N.GetText will return the name of the procedure as a text.</P>

<P> Consider a tool for investigating the contents of a block, where part of the investigation is to count the number of imperatives in the block. In addition the number of different types of imperatives will be counted.</P>
<P> This tool may be implemented by adding the operation Investigate to the pattern Block. Investigate makes use of the virtual operation Count which is added to the pattern Imp. Count is further specialized in the sub-patterns of Imp.</P>
<PRE CLASS=beta>
Small: Treelevel
  (# ImpCount, AssignmentCount,
     ProcCallCount, ICount: @Integer;
     Block: Cons
       (# Investigate:
            (# do
               0 &AElig; ImpCount &AElig; AssignmentCount
                 &AElig; ProcCallCount &AElig; IfCount;
               ImpPart.Scan(# do Current.Count #);
               ... (* Use ImpCount, ProcCallCount, ... *)
            #);
       #);
     ...
     Imp: Cons
       (# Count:&lt; (# do ImpCount.Add1; inner #) #);
     IfImp: Imp
       (# Count::&lt;
            (# do
               IfCount.Add1;
               ThenPart.Scan(#do Current.Count #);
               ElsePart.Scan(#do Current.Count #);
            #);
       #);
     AssignmentImp: Imp
       (# Count::&lt; (# do AssignmentCount.Add1 #) #);
     ProcCall: Imp
       (# Count::&lt; (# do ProcCallCount.Add1 #) #);
     ...
  #);
</PRE>
<P> B can now be investigated by B.Investigate.</P>
<P> In spite of the limited usefulness of the above example it gives a flavour of how semantic attributes may be added to the generated patterns. Tools like a semantic analyzer, a code generator, a program interpreter, a browser, presentation tools, program analyzers, transformation tools benefit from the possibility to add semantic attributes.</P>

<P> The next example will demonstrate how the syntax directed editor of the Mj&oslash;lner BETA System can be extended to provide the user of the editor with transformations.</P>
<P> The editor is an ordinary syntax directed editor which presents an AST in a window by means of a pretty-printer, it allows the user to navigate in the AST and to edit it. The pattern describing the editor has the outline:</P>
<PRE CLASS=beta>
Sde:
  (# Grammar:&lt; TreeLevel;
     G: @Grammar;
     Root, CurrentSelection: ^G.AST;
     ... (* a lot of other stuff *)
  #)
</PRE>
<P> Grammar describes which grammar is actually used. An editor for Pascal may be constructed by binding the context-free level generated for Pascal to Grammar. The reference Root denotes the program fragment being edited by the user. CurrentSelection denotes the sub-AST which is the current focus of the user.</P>
<P> To extend the editor with transformations the pattern SdeWithTransformations is declared as a sub-pattern to Sde. SdeWithTransformations declares the pattern Transformation, which has three virtual operations Init, EnablingCondition and Perform and a static reference Name.</P>
<P> SdeWithTransformations keeps a list containing an instance of each sub-pattern of Transformation. This list is created by means of initialization operations not shown here.</P>
<P> When the user selects a new node in the tree EnablingCondition will be tested for all transformations. The Names of those that are enabled will be presented to the user in a menu, and if the user selects one of the items in this menu, Perform for the corresponding transformation will be called.</P>
<PRE CLASS=beta>
SdeWithTransformations: Sde
  (# Transformation:
       (# Name: (* Presented in the menu *) @Text;
          Init:&lt; (* Called when an instance is created *)
            (# do ... inner; ... #);
          ...
          EnablingCondition:&lt;
            (* Virtual operation to test if transformation
             * is applicable for the current selection of
             * the editor.
             *)
            (# Enabled: @Boolean
            do inner 
            exit Enabled
            #);
          Perform:&lt; 
            (* Operation to be performed if the user 
             * selects this transformation
             *)
            (# do inner #);
       #);
     ...
  #);
</PRE>
<P> Assume a grammar for Pascal has been written, structured as Small, and including the rule</P>
<PRE CLASS=beta>
&lt;WhileImp&gt;::= while &lt;Condition:Exp&gt; do &lt;DoPart:ImpLst&gt;
</PRE>
<P> A syntax directed editor for Pascal with a transformation that will allow the user to transform an IfImp into a WhileImp could be created by the pattern:</P>
<PRE CLASS=beta>
PascalEditor: SdeWithTransformations
  (# Grammar::&lt; PascalGrammar;
     IfToWhileTransformation: Transformation
       (# Init::&lt; (# do 'IfImp to WhileImp' -&gt; Name #)
          EnablingCondition::&lt;
            (# do (CurrentSelection## = G.IfImp##)
                    -&gt; Enabled
            #);
          Perform::&lt;
            (# theIfImp: ^G.IfImp;
               theWhileImp: ^G.WhileImp;
               frag: ^fragmentForm;
            do CurrentSelection[] -&gt; theIfImp[];
               (whileImp, frag[]) -&gt; newAst
                 -&gt; theWhileImp[];
               theIfImp.getCondition
                 -&gt; theWhileImp.putCondition;
               theWhileImp -&gt; ReplaceCurrentSelection;
            #);
       #);
     ... (* More Pascal-transformations *)
  #);
</PRE>
<P> The pattern ReplaceCurrentSelection used by Perform is an attribute of pattern Sde.</P>
<P> The IfToWhileTransformation is a simple tree-match transformation. In the same way more advanced context-sensitive transformations could be added to the Pascal-editor. A transformation that extends a &lt;Procedure-Identifier&gt; with a template for the list of actual parameters is an example of this. This list could be generated with the correct number of parameters, and the parameters could be specialized such that a &lt;Variable&gt; nonterminal is inserted if the formal parameter is a &lt;Var-Parameter&gt;, an &lt;Exp&gt; nonterminal if it is a &lt;Value-Parameter&gt;, etc.</P>

<P> The context-free level provides the programmer with much more structure and security than the tree level alone. Consider the declaration P: ^ProcDcl. The body part of P may be denoted by P.getBody. If only the tree level is used, the corresponding declaration and denotation will be P: ^AST and P.getson2. This is less readable and it is solely the responsibility of the programmer that P actually denotes an AST for &lt;ProcDcl&gt; otherwise P.getson2 will not return an AST for &lt;Block&gt;. </P>





