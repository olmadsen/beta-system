
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
                      "http://www.w3.org/TR/REC-html40/loose.dtd"
>
<HTML>
<HEAD>
<META http-equiv="Content-Type" CONTENT="text/html; CHARSET=ISO-8859-1">
<TITLE> Set Example</TITLE>
<LINK REL=stylesheet HREF="../style/miadoc.css" TYPE="text/css">

</HEAD>
<BODY>
<A NAME=HEADING21></A>
<A HREF="containers-22.html"><IMG ALIGN="BOTTOM" ALT="Next" BORDER=0 SRC="../images/next.gif">
</A> <A HREF="containers-20.html"><IMG ALIGN="BOTTOM" ALT="Previous" BORDER=0 SRC="../images/prev.gif">
</A> <IMG ALIGN="BOTTOM" ALT="Up" BORDER=0 SRC="../images/upg.gif">
 <A HREF="../index.html"><IMG ALIGN="BOTTOM" ALT="Top" BORDER=0 SRC="../images/top.gif">
</A> <A HREF="index.html#_toc"><IMG ALIGN="BOTTOM" ALT="Contents" BORDER=0 SRC="../images/content.gif">
</A> <A HREF="inx.html"><IMG ALIGN="BOTTOM" ALT="Index" BORDER=0 SRC="../images/index.gif">
</A><P>
 <P>
<A NAME=HEADING21-0></A>
<H1> Set Example</H1>

 <P>
<PRE class=beta>
ORIGIN '~beta/containers/sets';
--- program:descriptor ---
(* This demo program illustrates the usage of the set pattern.  The first part
 * of the demo illustrates inserting elements from the sets, and
 * the last part illustrates the union and symDiff operations.
 * 
 * At the end of this file, a copy of the output of this program is given
 *)
(# intSet: set
     (* intSet is a set containing integerObjects *)
     (# element::&lt; integerObject #);
   
   intSet1, intSet2, intSet3: @intSet;
   i: [10]^integerObject;
   
do (* initializing the integerObjects *)
   (for int:10 repeat &amp;integerObject[]-&gt;i[int][]; int-&gt;i[int] for);
   
   (* initializating intSet1 and inserting integerObjects into it *)
   intSet1.init;
   'intSet1.capacity: '-&gt;puttext; intSet1.capacity-&gt;putInt; newline;
   (for int:4 repeat '*'-&gt;put;
      i[int][]-&gt;intSet1.insert
   for);
   (* printing the size of intSet1 *) '+'-&gt;put;
   'intSet1.size: '-&gt;puttext; intSet1.size-&gt;putInt; newline;
   (* printing intSet1 *)
   'intSet1.elements: '-&gt;puttext;
   intSet1.scan(# do current-&gt;putint; ','-&gt;put #); newline;

   (* initializating intSet2 and inserting integerObjects into it *)
   intSet2.init;
   (for int:4 repeat 
      i[int+3][]-&gt;intSet2.insert
   for);
   (* printing intSet2 *)
   'intSet2.elements: '-&gt;puttext;
   intSet2.scan(# do current-&gt;putint; ','-&gt;put #); newline;
   
   (* illustrating the use of the union, diff, sect and symDiff operations *)
   'intSet2-&gt;intSet3; intSet1-&gt;intSet3.union: '-&gt;puttext;
   intSet2-&gt;intSet3; intSet1-&gt;intSet3.union;
   intSet3.scan(# do current-&gt;putint; ','-&gt;put #); newline;

   'intSet2-&gt;intSet3; intSet1-&gt;intSet3.symDiff: '-&gt;puttext;
   intSet2-&gt;intSet3; intSet1-&gt;intSet3.symDiff;
   intSet3.scan(# do current-&gt;putint; ','-&gt;put #); newline;

   (* illustrating finding some integerObject in insSets *)
   'intSet3.find(# predicate::&lt; (# "elm=7" #) #): '-&gt;puttext;
   intSet3.find(# predicate::&lt; (# do (current=7)-&gt;value #)
      	       do current-&gt;putint
      	       #); newline;
   '7-&gt;intSet3.has: '-&gt;puttext;
   (if i[7][]-&gt;intSet3.has
    //true then 'yes'-&gt;putline
    else 'no'-&gt;putline
   if);
   '10-&gt;intSet3.has: '-&gt;puttext;
   (if i[10][]-&gt;intSet3.has
    //true then 'yes'-&gt;putline
    else 'no'-&gt;putline
   if);
   
   (*********** OUTPUT ***************
    * intSet1.capacity: -1
    * intSet1.size: 4
    * intSet1.elements: 4,3,2,1,
    * intSet2.elements: 7,6,5,4,
    * intSet2-&gt;intSet3; intSet1-&gt;intSet3.union: 1,2,3,7,6,5,4,
    * intSet2-&gt;intSet3; intSet1-&gt;intSet3.symDiff: 1,2,3,7,6,5,
    * intSet3.find(# predicate::&lt; (# "elm=7" #) #): 7
    * 7-&gt;intSet3.has: yes
    * 10-&gt;intSet3.has: no
    **********************************)
#)
</PRE>
<!-- TOC -->

