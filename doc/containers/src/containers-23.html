<H1> List Example</H1>

<PRE class=beta>
ORIGIN '~beta/containers/list';  
--- program:descriptor ---
(* This demo program illustrates the usage of the list pattern.  The first
 * part of the demo illustrates inserting and deleting elements from the lists.
 * 
 * At the end of this file, a copy of the output of this program is given
 *)
(# intList: list(# element::&lt; integerObject #);
   
   intList1, intList2: @intList; intList3: ^intList;
   i: [10]^integerObject;
   listPosition: ^intList3.theCellType;
   
do (* initializing the integerObjects *)
   (for int:10 repeat &amp;integerObject[]-&gt;i[int][]; int-&gt;i[int] for);
   
   (* initializing intList1 and putting some integerObjects into it *)
   intList1.init;
   'intList1.prepend: '-&gt;puttext;
   (for int:4 repeat
      i[int][]-&gt;intList1.prepend
   for);
   intList1.scan(# do current-&gt;putint; ','-&gt;put #); newline;
   
   (* initializing intList3 and putting some integerObjects into it *)
   intList2.init;
   'intList2.append: '-&gt;puttext;
   (for int:4 repeat 
      i[int+3][]-&gt;intList2.append
   for);
   intList2.scan(# do current-&gt;putint; ','-&gt;put #); newline;
   
   (* illustrates the reverse scanning *)
   'intList2.scanReverse: '-&gt;puttext;
   intList2.scanReverse(# do current-&gt;putint; ','-&gt;put #); newline;
   
   (* initializing intList3 *)
   &amp;intList[]-&gt;intList3[]; intList3.init;
   
   (* illustrating concatenation of lists *)
   'intList1[]-&gt;intList2.concatenate-&gt;intList3[]: '-&gt;puttext;
   intList1[]-&gt;intList2.concatenate-&gt;intList3[];
   intList3.scan(# do current-&gt;putint; ','-&gt;put #); newline;
   
   (* illustration getting the position of some integerObject in the list, and
    * replacing the element at that position
    *)
   '"10"-&gt;("2"-&gt;intList3.at).elm[]: '-&gt;puttext;
   i[2][]-&gt;intList3.at-&gt;listPosition[];
   i[10][]-&gt;listPosition.elm[];
   intList3.scan(# do current-&gt;putint; ','-&gt;put #); newline;
   
   (* illustrates deleting an element from the list *)
   '6-&gt;intList2.at-&gt;intList2.delete: '-&gt;puttext;
   i[6][]-&gt;intList2.at-&gt;intList2.delete;
   intList2.scan(# do current-&gt;putint; ','-&gt;put #); newline;

    i[7][]-&gt;intList3.at-&gt;listPosition[];
  (* illustrating inserting integerObjects before some list position *)
   '(8,7)-&gt;intList3.insertBefore: '-&gt;puttext; 
   (i[8][],listPosition[])-&gt;intList3.insertBefore;
   intList3.scan(# do current-&gt;putint; ','-&gt;put #); newline;
                           
   (* illustrating deleting integerObjects after some list position *)
   '7-&gt;intList3.deleteAfter: '-&gt;puttext; 
   listPosition[]-&gt;intList3.deleteAfter;
   intList3.scan(# do current-&gt;putint; ','-&gt;put #); newline;
   
   (*********** OUTPUT ***************
    * intList1.prepend: 4,3,2,1,
    * intList2.append: 4,5,6,7,
    * intList2.scanReverse: 7,6,5,4,
    * intList1[]-&gt;intList2.concatenate-&gt;intList3[]: 4,5,6,7,4,3,2,1,
    * "10"-&gt;("2"-&gt;intList3.at).elm[]: 4,5,6,7,4,3,10,1,
    * 6-&gt;intList2.at-&gt;intList2.delete: 4,5,7,
    * (8,7)-&gt;intList3.insertBefore: 4,5,6,8,7,4,3,10,1,
    * 7-&gt;intList3.deleteAfter: 4,5,6,8,7,3,10,1,
    ******************************)
#)
</PRE>

<!-- TOC -->

