<H2> 9.1 Multidimensional Repetitions<A NAME=MARKER-2-76></A></H2>
<P> It is not possible to make multidimensional repetitions using the current version of the Mj&oslash;lner System. However, multidimensional repetitions are easily constructed, e.g. a repetition with dimension NxM can be declared like:</P>
<PRE CLASS=beta>
mul_table: [N*M] @Integer;
</PRE>
<P> which is intended to realize a two-dimensional array of the form:</P>
<P> <IMG ALIGN=TOP SRC=matrix.gif></P>
<P> The following example shows how the multiplication table constructed in section 4 previously can be stored in a two-dimensional repetition:</P>
<A NAME=HEADING19-7></A>
<H4 CLASS=betacaption><A NAME="fig:Program 9.2: MultiplicationTable.bet"> Program 9.2: MultiplicationTable.bet</A></H4>
<PRE CLASS=beta>
ORIGIN '~beta/basiclib/betaenv'
---- program: descriptor ----
(#
  (* Multiplication Table 3
   *
   *  Objective: Store values in a repetition
   *)
  N,M: @Integer;
do
   '\n\t** Multiplication Table ** \n\n'-&gt; puttext;
   'Enter dimensions (NxM): '-&gt;puttext;
   getint -&gt; N;
   getint -&gt; M;
   (# mul_table: [N*M] @Integer;
   do (* build table *)
      (for i: N repeat
           (for j: M repeat
                i*j -&gt; mul_table[(i-1)*M + j];
           for);
      for);

      (* print table *)
      newline; 
        '    '-&gt;puttext;
      (for i: M repeat
        i -&gt; screen.putint(# format::(# do 4-&gt; width #)#);
      for);
      newline;
      (for i: N repeat
           i -&gt; screen.putint(# format::(# do 4-&gt; width #)#);
           (for j: M repeat
                mul_table[(i-1)*M + j]
                 -&gt; screen.putint(# format::(# do 4-&gt; width #)#);
           for);
           newline;
      for)
   #);
#)
</PRE>
<P> </P>












