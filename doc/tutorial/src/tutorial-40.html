<H1>The <INX>Fragment System</INX></H1>
<P> Every BETA program uses the Fragment System. A fragment can be viewed as a piece of a BETA program ist of one or more fragments.</P>
<P> The basic BETA environment, called betaenv, supplies basic BETA patterns, such as integer, char, boolean, and text. In order to use these basic patterns, the program must specify that the betaenv environment is to be used. The following example illustrates how:</P>
<A NAME=HEADING40-4></A>
<BETA SRC="$BETALIB/demo/tutorial/HelloWorld.bet" LIST="HelloWorld.bet">

<P> The example consists of two parts, the specification of ORIGIN and
the descriptor
<FOOTNOTE>descriptor is an alias for ObjectDescriptor,
i.e. <BR>--program:ObjectDescriptor-- is also legal</FOOTNOTE> 
called program.</P>

<P> The specification of ORIGIN tells that the program uses the fragment file ~beta/basiclib/betaenv. The descriptor program tells that following the line</P>

<BETA>
---program: descriptor---
</BETA>
<P> comes a BETA descriptor, i.e. (# ... #), that will be named program. The name is used to identify the descriptor for the purpose of binding it to an unbound hole in the betaenv environment. A simple betaenv environment could have the following outline:</P>

<BETA>
(* The basic BETA environment betaenv *)
(# ...
   put: (# c: @char; enter c do ... #);
   puttext: (# t: ^text; enter t[] do ... #);
   putline: (# t: ^text; enter t[] do t[]->puttext; newline #);
   newline: (# do ... #);
   text: (# ... #);
   ...
   <<SLOT LIB: Attributes>>
   ...
do (* initialize for execution *)
   <<SLOT program: descriptor>>
   (* terminate execution *)
#)
</BETA>

<P> The betaenv environment consists of a single descriptor with two holes&Ntilde;slots. One named program of type descriptor and one named LIB of type Attributes. </P>
<P> The program slot is empty and can be filled (or bound) by a BETA program by defining an descriptor like:</P>
<BETA>
---program: descriptor---
(# ... #)
</BETA>
<P> as illustrated above. Every BETA program must have exactly one such construct in order to fill the empty slot in betaenv. </P>

<P> Filling a slot can be compared to a textual replacement
<FOOTNOTE>Textual replacement is not exactly correct due to the scope
rules. Please see the BETA book chapter 17 for a description of these
rules</FOOTNOTE>.  
The Hello World example program above, thus replaces the program slot in betaenv, resulting in the following expanded BETA program:</P>
<A NAME=HEADING40-21></A>
<BETA LIST="HelloWorld with filled program slot">
(* The basic BETA environment betaenv *)
(# ...
   put: (# c: @char; enter c do ... #);
   puttext: (# t: ^text; enter t[] do ... #);
   putline: (# t: ^text; enter t[] do t[]->puttext; newline #);
   newline: (# do ... #);
   text: (# ... #);
   ...
do (* initialize for execution *)
   (#
   do 'Hello World!'->putline;
   #)
   (* terminate execution *)
#)
</BETA>
<P> The LIB slot can be used to define libraries that may used in other BETA programs. If we want to add an operation called putBoxed to the basic environment, we can fill the LIB slot:</P>

<A NAME=HEADING40-25></A>
<BETA LIST="putBoxed.bet">
ORIGIN '~beta/basiclib/betaenv'
---LIB: Attributes---
  putBoxed: 
     (* print the text with a box surrounding: 
      * 'text'->putBoxed results in '[text]'
      *)
    (# t: ^Text;
    enter t[]
    do '['->put; t[]-> puttext; ']'->put;
    #);
</BETA>
<P> The HelloWorld program can then use this library by including it:</P>

<A NAME=HEADING40-29></A>
<INX HIDDEN>INCLUDE</INX>
<BETA SRC="$BETALIB/demo/tutorial/HelloWorld.bet" LIST="HelloWorld.bet">

<P> Resulting in the following output:</P>
<BETA>
[Hello World!]
</BETA>
<P> The HelloWorld example program using the putBoxed library, results in the following expanded BETA program:</P>
<A NAME=HEADING40-34></A>
<BETA LIST="HelloWorld with filled program and LIB slot">
(* The basic BETA environment betaenv *)
(# ...
   put: (# c: @char; enter c do ... #);
   puttext: (# t: ^text; enter t[] do ... #);
   putline: (# t: ^text; enter t[] do t[]->puttext; newline #);
   newline: (# do ... #);
   text: (# ... #);
   putBoxed: 
     (* print the text with a box surrounding: 
      * 'text'->putBoxed results in '[text]'
      *)
    (# t: ^Text;
    enter t[]
    do '['->put; t[]-> puttext; ']'->put;
    #);
   ...
do (* initialize for execution *)
   (#
   do 'Hello World!'->putline;
   #)
   (* terminate execution *)
#)
</BETA>
<P> The program can be made even more simpler by having ORIGIN in the putBoxed.bet file:</P>
<BETA>
ORIGIN 'putBoxed'
---program: descriptor---
(#
do 'Hello World!'->putBoxed;
#)
</BETA>
<P> This works because the putBoxed.bet has ORIGIN in the betaenv environment, so the HelloWorld program will also have access to the betaenv environment.</P>
<P> The ---LIB: Attributes--- may be multiply specified in the same file or in different files. The way to make libraries in BETA is thus to define the pattern declarations in a fragment called LIB. The file containing the ---LIB: Attributes--- fragment can then be included in your program and the declarations can be used.</P>













