<H1> Using the unixDirectory Fragment</H1>
<P> A program using the unixDirectory fragment will have the following structure:</P>
<PRE CLASS=beta>
INCLUDE '~beta/unixlib/unixDirectory'
--- program: descriptor ---
(# ...
   ud: @unixDirectory;
do ...
   'unixDirectory.bet' -&gt; ud.findEntry(# ... do ... #);
#)
</PRE>
<A NAME=HEADING6-10></A>
<H1> Listing Unix Directory Example</H1>
<P> Program showing a simple use of UNIX directory: The directory with the path given as argument is scanned, and the names of all the entries are printed with an indication of what type of entry it is. This is done using the select pattern of <A NAME=MARKER-scanEntries>scanEntries</A>. This is a more efficient strategy than using found.entry.isFile etc., possibly correcting for the case that 'd' is not current <A NAME=MARKER-working>working</A> directory.</P>
<P> If the path given is not a directory, an exception will be raised: </P>

<H4 CLASS=betacaption>listUnix.bet</H4>
<PRE CLASS=beta>
ORIGIN '~beta/unixlib/unixdirectory';
-- PROGRAM: descriptor --

(* Program showing a simple use of UNIX directory: The directory with the path 
 * given as argument is scanned, and the names of all the entries are printed
 * with an indication of what type of entry it is.
 * This is done using the 'select' pattern of 'scanentries'. This is a more
 * efficient startegy than using 'found.entry.isFile' etc., possibly 
 * correcting for the case that 'd' is not current working directory.
 * If the path given is not a directory, an exception will be raised.
 *)

(# arg: ^text; 
   d: @UnixDirectory; 
   nl: @boolean; 
   full: @boolean;
   usage:
     (# do 'Usage: ' -&gt; puttext; 
        1-&gt;arguments-&gt;puttext; 
        ' [-f] path' -&gt; putline;
        stop;
     #);
do (* Parse command line *)
   (if noOfArguments
    // 1 then '.' -&gt; d.name
    // 2 then
       2 -&gt; arguments -&gt; arg[];
       (if '-f' -&gt; arg.equal
        // true then
           true -&gt; full;
           '.' -&gt; d.name;
        // false then
           arg[] -&gt; d.name;
       if)
    // 3 then
       2 -&gt; arguments -&gt; arg[];
       (if '-f' -&gt; arg.equal
        // true then
           true -&gt; full;
        // false then usage;
       if);
       3 -&gt; arguments -&gt; d.name;
    else
       usage;
   if);
   
   (* Scan directory *)
   newline;
   'The content of '''-&gt; puttext;
   d.name -&gt; puttext;
   ''' is: ' -&gt; putline;
   d.scanEntries
   (# 
   do false -&gt; found.followlinks;
      select
      (# whenFile::&lt;
           (# do 'File:          ' -&gt; puttext; #);
         whenDir::&lt;
           (# do 'Directory:     ' -&gt; puttext; #);
         whenSocket::&lt;  
           (# do 'Socket:        ' -&gt; puttext; #);
         whenSymboliclink::&lt; 
           (# do 'Link:          ' -&gt; puttext; #);
         whenCharSpecial::&lt;  
           (# do 'Char special:  ' -&gt; puttext; #);
         whenBlockSpecial::&lt; 
           (# do 'Block special: ' -&gt; puttext; #);
         whenOther::&lt;
           (# do '(Unknown):     ' -&gt; puttext; #);
      #);
      (if full
       // true then foundFullPath -&gt; putline; 
       // false then found.path -&gt; putline; 
      if);
   #);
   newline;
#)
</PRE>





