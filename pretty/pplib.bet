ORIGIN 'astviewer';
INCLUDE '~beta/betaast/v5.1/semanticerrortext'
        '~beta/basiclib/v1.5/textUtils';
(* COPYRIGHT
 *      Copyright (C) Mjolner Informatics, 1986-94
 *       All rights reserved.
 *)
-- Lib: Attributes --
prettyprintFragment:
  (#
     mps: ^astInterface;
     f: ^mps.fragment;
     outStream: ^stream;
     (* if none then if streamName is empty then on screen else on file *)
     streamName: ^text;
     node: (# aNode: ^mps.ast do INNER exit aNode[] #);
     width:< (# value: @integer do 80->value; INNER exit value #);
     abstractPresentation:< booleanValue;
     showSemanticErrors:< booleanValue;
     includeComments:< trueObject;
     onlyProperties:< booleanValue;
     test:< booleanValue;
     
  enter (mps[],f[],outStream[],streamName[])
  do
  (*enter 
   * (mps[],f[],node,outStream[],streamName[],width,depth,abstractPresentation,showSemanticErrors,
   * includeComments,parsingInformation,specialLexemTreatment,longLexemsWithQuote,
   * specialCommentTreatment,ppSpec[],onlyProperties,test)
   *)
     (mps[],f[],node,outStream[],streamName[],width,0,abstractPresentation,
      showSemanticErrors,includeComments,false,false,false,false,'',
      onlyProperties,test)->ppFragment;
     
  #);
ppFragment:
  (#
     astView: ^mps.astviewer;
     outStream: ^stream;
     theComments: (* in the fragment syntax *) ^text;
     outputStream: astView.output
       (#
          put::<  (#  do ch->outStream.put #);
          putText::<  (#  do s[]->outStream.putText #);
          putInt::<  (#  do n->outStream.putInt #);
          newLine::<  (#  do outStream.newLine #);
          printError::< 
            (# 
            do
               (if showSemanticErrors then
                   newLine;
                   '***** '->putText;
                   theAst.semanticError->semanticErrorText->putText;
                   newLine;
                   
               if)
            #);
          printSpecialComment::< 
            (# skipLeadingBlanks: @boolean; 
            do
               0->noOfLines;
               astView.pp.comBegin.lgth->lengthOfLastLine;
               astView.pp.comBegin[]->putText;
               false->skipLeadingBlanks;
               com.scanAll
                 (# 
                 do
                    (if ch
                     // ascii.newLine then
                        noOfLines+1->noOfLines;
                        1->lengthOfLastLine;
                        lineNo+1->lineNo;
                        lineNo->newLine;
                        (for margin+1 repeat ' '->put for);
                        true->skipLeadingBlanks;
                        
                     else
                        (if skipLeadingBlanks
                         // true then
                            (if (ch->ascii.isSpace)
                             // false then
                                ch->put;
                                lengthOfLastLine+1->lengthOfLastLine;
                                false->skipLeadingBlanks
                            if)
                         else
                        (* skipping tabs *)
                            (if (ch->ascii.isSpace)
                             // true then
                                (if ch
                                 // ascii.sp then
                                    ch->put;
                                    lengthOfLastLine+1->lengthOfLastLine;
                                    
                                 else
                                    'eating tabs in comment: '->screen.putText;
                                    ch->screen.putInt;
                                    screen.newLine;
                                    ' '->put;
                                    
                                if)
                             else
                                ch->put; lengthOfLastLine+1->lengthOfLastLine; 
                            if);
                            
                        if);
                        
                    if);
                    
                 #);
               lengthOfLastLine+astView.pp.comEnd.lgth->lengthOfLastLine;
               astView.pp.comEnd[]->putText;
               
            #);
          measureSpecialComment::< 
            (# 
            do
               (if (com.lgth > 40)
                // true then
                   100
                     ->length
                     (* only used to provoke a break
                      * the length 
                      *)
               if);
               
            #);
          scanWords:
            (# t: ^text; word: ^text; 
            enter t[]
            do (*'scanWords begin on:'->putL;
                t[]->putL;*)
               t.reset;
               loop:
                 (# 
                 do
                    t.getAtom->word[];
                    INNER scanWords;
                    (if t.eos // false then restart loop if);
                    
                 #);
               (*'scanWords end'->putL;*)
               
            #);
          printSpecialLexem::< 
            (# t,outt: ^text; firstWord,isString: @boolean; 
            do
               false->isString;
               (if lt.kind // mps.kinds.string then true->isString if);
               0->noOfLines;
               lt.getText->t[];
               &text[]->outt[];
               outt.reset;
               true->firstWord;
               (if LongLexemsWithQuote and IsString
                // true then 1->lengthOfLastLine
                else
                   0->lengthOfLastLine
               if);
               t[]
                 ->scanWords
                   (# 
                   do
                      (if
                      (outt.length+word.length+margin > astView.pp.width-5
                       (*scroll bar*) )
                       // false then
                          (if firstWord
                           // false then
                              ' '->outt.put;
                              lengthOfLastLine+1->lengthOfLastLine;
                              
                           else
                              (if LongLexemsWithQuote and IsString
                               // true then astView.pp.stringChar->outt.put; 
                              if)
                          if);
                          lengthOfLastLine+word.length->lengthOfLastLine;
                          word[]->outt.putText;
                          false->firstWord;
                          
                       else
                      (* break line and empty outt *)
                          outt[]->putText;
                          &text[]->outt[];
                          outt.reset;
                          lineNo+1->lineNo;
                          lineNo->newLine;
                          noOfLines+1->noOfLines;
                          (for margin repeat
                            ' '->put;
                            (*(if {* REMOVE later!! *} astView.pp.ppDebug //true then '~'->screen.put; if);*)
                            
                          for);
                          word[]->outt.putText;
                          word.length->lengthOfLastLine;
                          
                      if);
                      
                   #);
               (if (outt.length > 0)
                // true then
                   (if LongLexemsWithQuote and IsString
                    // true then
                       astView.pp.stringChar->outt.put;
                       lengthOfLastLine+1->lengthOfLastLine;
                       
                   if);
                   outt[]->putText;
                   (*(if {* REMOVE later!! *} astView.pp.ppDebug //true then outt[]->screen.putText;if);*)
                   
               if);
               
            #);
          measureSpecialLexem::< 
            (# t: ^text; 
            do
               lt.getText->t[];
               (if (t.lgth > 40)
                // true then
                   100
                     ->length
                     (* only used to provoke a break
                      * the length 
                      *)
               if);
               
            #);
          
       #);
     includeList: @
       (#
          rep: [10] ^text;
          top: @integer;
          append:
            (# name: ^text; 
            enter name[]
            do
               top+1->top;
               (if top // rep.range then top div 2->rep.extend if);
               name[]->rep[top][]
            #);
          exists:
            (# name: ^text; found: @boolean
            enter name[]
            do
               loop:
               (for i: top repeat
                 (if rep[i][]->name.equal then true->found; leave loop;  if)
               for);
               
            exit found
            #)
       #);
     getNextComment: @|
     (*
      * A comment c for the fragment group is organized as follows:
      * 
      * c = c1 c2 ... cn, where the positions of the ci's are:
      * c1 son1 c1 son2 c2 .... cn sonn cn+1
      * each ci can be further divided into comments that must be 
      * prettyprinted separately.
      * nextComment scans all subcomments one of the time.
      * 
      * A call of nextComment returns the next subcomment in the 
      * sequence of comments belonging to the fragment group. 
      * 
      * n is the number of comments in a sub-sequence, 
      * if n is -1 the comment is empty and c[] is none
      * if n is 0 there is only one comment between the two sons or it is the last subcomment
      * if n is 1 there are more than one subcomment and c[] contains the current one
      * if n is -2 we have read the whole comment
      * 
      * the comment looks like this:
      * xxx 1 xxx 2 xxx 2 xxx 2 xxx 21 xxx 1 xxx 1 xxx 2 xxx 2 xxx 2 
      * 
      * This is a copy of getNextComment in mps.astlevel
      *)
       (# subcomment: ^text; n: @integer; inx: @Integer; ch: @Char; 
       do
          (if theComments[] <> none
           // true then
              theComments.reset;
              mainloop:
              (if inx
               // theComments.lgth then (* end of comment *)
                  - 1->n;
                  none ->subcomment[];
                  SUSPEND;
                  0->inx;
                  restart mainloop;
                  
               else
              (* read next comment *)
                  &Text[]->subcomment[];
                  loop:
                  (if ((inx+1->inx)->theComments.inxget->ch)
                   // mps.CommentSeparator1 then
                      (if subcomment.lgth
                       // 0 then (* no comments *) - 1->n; 
                       else
                      (* one comment, or the last comment in a sub sequence *)
                          1->n; 
                      if);
                      
                   // mps.CommentSeparator2 then
                      (if true
                       // inx = theComments.lgth then
                          0->n; 
                       // (inx+1->theComments.inxget) = mps.CommentSeparator1
                       then
                          inx+1->inx; 0->n; 
                       else
                          1->n
                      if);
                      
                   else
                      ch->subcomment.put; restart loop; 
                  if);
                  SUSPEND;
                  restart mainloop;
                  
              if);
              
           else
              - 1->n; none ->subcomment[]; 
          if);
          
       exit (subcomment[],n,inx)
       #);
     Checkcomment:
       (#
          currentSubcomment: ^text;
          n,n2,inx: @integer;
          before:< object;
          after:< object;
          doComment:
            (# 
            do
               (if currentSubcomment[] <> none then
                   (if currentSubcomment.lgth <> 0 then
                       before;
                       astView.pp.comBegin[]->astView.out.putText;
                       currentSubcomment[]->astView.out.putText;
                       astView.pp.comEnd[]->astView.out.putText;
                       after;
                       
                   if)
               if)
            #)
       do
          getNextComment->(currentSubcomment[],n,inx);
          (if n
           // 0 then
              doComment; 
           // 1 then
              doComment;
              loop:
              (if 1
               // 1 then
                  getNextComment->(currentSubcomment[],n2,inx);
                  doComment;
                  (if n2 // 1 then restart loop if);
                  
              if);
              
          if);
          
       exit inx
       #);
     getFragmentSyntaxComments:
       (# g: ^mps.fragmentGroup; theCommentss: ^text
       enter g[]
       do
          ''->theComments[];
          theComments.reset;
          g.prop.scanProp
            (#
               doProp::< 
                 (# 
                 do
                    (if 'Comment'->prop.equalNCS then
                        scanParameters
                          (#
                             doString::< 
                               (# 
                               do
                                  s.scanAll
                                    (# 
                                    do
                                       (if ch
                                        // mps.CommentSeparator3 then
                                           mps.CommentSeparator1
                                             ->theComments.put;
                                           
                                        else
                                           ch->theComments.put; 
                                       if);
                                       
                                    #);
                                  
                               #);
                             
                          #);
                        
                    if)
                 #)
            #)
       exit theComments[]
       #);
     prettyPrintPropertyList:
       (# prop: ^propertyList; firstTime,semicolonWritten: @boolean; 
       enter prop[]
       do
          true->firstTime;
          prop.scanProp
            (#
               firstname,firststring,commentAtStart,notCommentUnlessTest:
                 @boolean;
               help: @text;
               doProp::< 
                 (# 
                 do
                    (if true
                     // 'donecheck'->prop.equal // 'abstracted'->prop.equal
                     // 'UNIQUE_GROUP_ID'->prop.equalNCS
                     // 'SLOTS'->prop.equalNCS // 'frejamark'->prop.equal then
                     (* Ignore these properties *)
                        checkComment
                          (# after::<  (#  do astView.out.newLine #)
                          #);
                        
                     else
                        (if firstTime then
                            false->firstTime
                         else
                            ';'->astView.out.put;
                            astView.out.Newline;
                            true->semicolonWritten;
                            
                        if);
                        checkComment
                          (# after::<  (#  do astView.out.newLine #)
                          #);
                        (if (not ('Comment'->prop.equalNCS)) then
                            prop->help;
                            help.makeUC;
                            help[]->astView.out.putText;
                            true->firstname->firststring;
                            true->notCommentUnlessTest;
                            
                         else
                            test->notCommentUnlessTest; 
                        if);
                        (if notCommentUnlessTest then
                            scanParameters
                              (#
                                 doName::< 
                                   (# 
                                   do
                                      (if firstname then
                                          (if true
                                           // 'mdbody'->prop.equalNCS
                                           // 'objfile'->prop.equalNCS
                                           // 'libfile'->prop.equalNCS
                                           // 'linktopt'->prop.equalNCS
                                           // 'betarun'->prop.equalNCS
                                           // 'make'->prop.equalNCS
                                           // 'resource'->prop.equalNCS
                                           // 'build'->prop.equalNCS then
                                              checkComment
                                                (#
                                                   before::< 
                                                     (# 
                                                     do ' '->astView.out.put
                                                     #);
                                                   after::< 
                                                     (# 
                                                     do
                                                        astView.out.newLine;
                                                        true->commentAtStart
                                                     #)
                                                #);
                                              
                                           else
                                              checkComment
                                                (#
                                                   before::< 
                                                     (# 
                                                     do astView.out.newLine
                                                     #);
                                                   after::< 
                                                     (# 
                                                     do astView.out.newLine
                                                     #)
                                                #);
                                              
                                          if);
                                          false->firstname;
                                          (if commentAtStart then
                                              '       '->astView.out.putText; 
                                           else
                                              ' '->astView.out.put; 
                                          if);
                                          n[]->astView.out.putText;
                                          
                                       else
                                          astView.out.newLine;
                                          (if true
                                           // 'mdbody'->prop.equalNCS
                                           // 'objfile'->prop.equalNCS
                                           // 'libfile'->prop.equalNCS
                                           // 'linktopt'->prop.equalNCS
                                           // 'betarun'->prop.equalNCS
                                           // 'make'->prop.equalNCS
                                           // 'resource'->prop.equalNCS
                                           // 'build'->prop.equalNCS then
                                              checkComment
                                                (#
                                                   before::< 
                                                     (# 
                                                     do
                                                        '       '
                                                          ->astView.out.putText
                                                     #);
                                                   after::< 
                                                     (# 
                                                     do astView.out.newLine
                                                     #)
                                                #);
                                              '       '->astView.out.putText;
                                              
                                           else
                                              checkComment
                                                (#
                                                   before::<  (#  do  #);
                                                   after::< 
                                                     (# 
                                                     do astView.out.newLine
                                                     #)
                                                #);
                                              
                                          if);
                                          n[]->astView.out.putText;
                                          
                                      if)
                                   #);
                                 doString::< 
                                   (# 
                                   do
                                      (if ('Comment'->prop.equalNCS) and test
                                       then
                                          astView.pp.comBegin[]
                                            ->astView.out.putText;
                                          ' Comment in property list'
                                            ->astView.out.putText;
                                          astView.out.newLine;
                                          '* '->astView.out.putText;
                                          s.scanAll
                                            (# 
                                            do
                                               (if ch
                                                // mps.CommentSeparator1 then
                                                   'S'->astView.out.put;
                                                   astView.out.newLine;
                                                   '* '->astView.out.putText;
                                                   
                                                // mps.CommentSeparator2 then
                                                   's'->astView.out.put; 
                                                // mps.CommentSeparator3 then
                                                   'p'->astView.out.put; 
                                                else
                                                   ch->astView.out.put; 
                                               if);
                                               
                                            #);
                                          astView.out.newLine;
                                          astView.pp.comEnd[]
                                            ->astView.out.putText;
                                          
                                       else
                                          (if true
                                           // 'mdbody'->prop.equalNCS
                                           // 'objfile'->prop.equalNCS
                                           // 'libfile'->prop.equalNCS
                                           // 'linktopt'->prop.equalNCS
                                           // 'betarun'->prop.equalNCS
                                           // 'make'->prop.equalNCS
                                           // 'resource'->prop.equalNCS
                                           // 'build'->prop.equalNCS then
                                              checkComment
                                                (#
                                                   before::< 
                                                     (# 
                                                     do ' '->astView.out.put
                                                     #);
                                                   after::< 
                                                     (# 
                                                     do astView.out.newLine
                                                     #)
                                                #);
                                              ' '''->astView.out.putText;
                                              s[]->astView.out.putText;
                                              ''''->astView.out.put;
                                              
                                           else
                                              (if firststring then
                                                  checkComment
                                                    (#
                                                       before::< 
                                                         (# 
                                                         do
                                                            ' '->astView.out.put
                                                         #);
                                                       after::< 
                                                         (# 
                                                         do astView.out.newLine
                                                         #)
                                                    #);
                                                  false->firststring;
                                                  ' '''->astView.out.putText;
                                                  
                                               else
                                                  astView.out.newLine;
                                                  (if true
                                                   //
                                                   ('include'->prop.equalNCS)
                                                   then
                                                      checkComment
                                                        (#
                                                           before::< 
                                                             (# 
                                                             do
                                                                '        '
                                                                  ->
                                                                    astView.out.
                                                                      putText
                                                             #);
                                                           after::< 
                                                             (# 
                                                             do
                                                                astView.out.
                                                                  newLine
                                                             #)
                                                        #);
                                                      '        '''
                                                        ->astView.out.putText;
                                                      
                                                   // ('body'->prop.equalNCS)
                                                   then
                                                      checkComment
                                                        (#
                                                           before::< 
                                                             (# 
                                                             do
                                                                '     '
                                                                  ->
                                                                    astView.out.
                                                                      putText
                                                             #);
                                                           after::< 
                                                             (# 
                                                             do
                                                                astView.out.
                                                                  newLine
                                                             #)
                                                        #);
                                                      '     '''
                                                        ->astView.out.putText;
                                                      
                                                  if);
                                                  
                                              if);
                                              s[]->astView.out.putText;
                                              ''''->astView.out.put;
                                              (if 'include'->prop.equalNCS then
                                                  s[]->includeList.append; 
                                              if);
                                              
                                          if);
                                          
                                      if);
                                      
                                   #);
                                 
                              #)
                        if);
                        
                    if);
                    
                 #)
            #);
          loop:
          (if true then
              (if checkComment
              (* only necessary until new property syntax is fully used
               * e.g. 
               * INCLUDE a;b;c 
               * instead of 
               * INCLUDE a; 
               * INCLUDE b; 
               * INCLUDE c;
               *)
                (#
                   before::<  (#  do  #);
                   after::<  (#  do astView.out.newLine #)
                #) < theComments.lgth then
                  restart loop
              if)
          if);
          (if not semicolonWritten then astView.out.newLine if)
       #);
     prettyPrintForm:
       (# ff: ^mps.fragmentForm; node: ^mps.ast
       enter (ff[],node[])
       do (* ff.root.dump;*)
          (if node[]
           // none then (ff.root[],depth)->astView.pp.present; 
           else
              (node[],depth)->astView.pp.present; 
          if);
          
       #);
     prettyPrintGroup:
       (#
          g: ^mps.fragmentGroup;
          t: ^text;
          no,noOfFrags: @integer;
          grammarName: ^text;
          firstTime: @boolean;
          
       enter g[]
       do
          true->firstTime;
          (if g.fragmentlist[] <> none then
              g.fragmentList.scan
                (# 
                do
                   no+1->no;
                   (if current.type
                    // mps.linkType then
                       (if current.name[]->includeList.exists then
                       (* already prettyprinted *)
                           
                        else
                           (if firstTime then
                               false->firstTime
                            else
                               ';'->astView.out.put; astView.out.Newline
                           if);
                           'INCLUDE '''->astView.out.putText;
                           current.name[]->astView.out.putText;
                           ''''->astView.out.putText;
                           
                       if);
                       
                    // mps.formType then
                         (# ff: ^mps.fragmentForm
                         do
                            (if not onlyProperties then
                                (if firstTime
                                 // true then false->firstTime
                                 else
                                    astView.out.Newline
                                if);
                                current.open->ff[];
                                astView.pp.EOSchar[]->astView.out.putText;
                                astView.pp.EOSchar[]->astView.out.putText;
                                ' '->astView.out.put;
                                ff.name->astView.out.putText;
                                ': '->astView.out.putText;
                                ff.category->ff.Grammar.symbolToName->t[];
                                (if true
                                 // ('DescriptorForm'->t.equal)
                                 // ('ObjectDescriptor'->t.equal) then
                                    'Descriptor'->astView.out.putText; 
                                 // ('AttributesForm'->t.equal)
                                 // ('AttributeDecl'->t.equal) then
                                    'Attributes'->astView.out.putText; 
                                 else
                                    t[]->astView.out.putText; 
                                if);
                                ff.grammar.grammarIdentification->grammarName[];
                                (if ('beta'->grammarName.equal)
                                 // false then
                                    ': '->astView.out.putText;
                                    grammarName[]->astView.out.putText;
                                    
                                if);
                                ' '->astView.out.put;
                                astView.pp.EOSchar[]->astView.out.putText;
                                astView.pp.EOSchar[]->astView.out.putText;
                                astView.out.newLine;
                                (ff[],none )->prettyPrintForm;
                                
                            if);
                            
                         #)
                   if)
                #);
              astView.out.newLine;
              
          if)
       #);
     mps: ^astInterface;
     f: ^mps.fragment;
     node: ^mps.ast;
     streamName: ^text;
     width,depth: @integer;
     abstractPresentation,showSemanticErrors,includeComments,parsingInformation,
       specialLexemTreatment,specialCommentTreatment,longLexemsWithQuote:
       @boolean;
     ppSpec: ^text;
     onlyProperties,test: @boolean;
     outputFile,ppSpecFile: @file;
     fg,fg2: ^mps.fragmentGroup;
     ff: ^mps.fragmentForm;
     dashPos: @integer;
     help,help2,theGrammarName,ppSpecName: ^text;
     e: @diskEntry;
     suffix: @text;
     onFile: @boolean;
     
  enter
  (mps[],f[],node[],outStream[],streamName[],width,depth,abstractPresentation,
   showSemanticErrors,includeComments,parsingInformation,specialLexemTreatment,
   longLexemsWithQuote,specialCommentTreatment,ppSpec[],onlyProperties,test)
  do
     &mps.astViewer[]->astView[];
     (if test
      // true then
         'ppgroup: '->putLine;
         '<'->put;
         f.name->putText;
         '>'->put;
         newLine;
         '<'->put;
         streamName[]->putText;
         '>'->put;
         newLine;
         width->putInt;
         newLine;
         depth->putInt;
         newLine;
         'abstractPresentation: '->putText;
         abstractPresentation->putBoolean;
         newLine;
         'showSemanticErrors: '->putText;
         showSemanticErrors->putBoolean;
         newLine;
         'includeComments: '->putText;
         includeComments->putBoolean;
         newLine;
         'parsingInformation: '->putText;
         parsingInformation->putBoolean;
         newLine;
         'specialLexemTreatment: '->putText;
         specialLexemTreatment->putBoolean;
         newLine;
         'longLexemsWithQuote: '->putText;
         longLexemsWithQuote->putBoolean;
         newLine;
         'specialCommentTreatment: '->putText;
         specialCommentTreatment->putBoolean;
         newLine;
         '<'->put;
         ppSpec[]->putText;
         '>'->put;
         newLine;
         
     if);
     (if outStream[]
      // none then
         (if streamName.empty then
             screen[]->outStream[]; 
          else
             true->onFile;
             streamName[]->outputfile.name;
             outputFile.openWrite;
             outPutFile[]->outStream[];
             
         if);
         
     if);
     (if f.type
      // mps.formType then
         f[]->ff[]
      // mps.groupType then
         f[]->fg[];
         (if fg.fragmentList[] <> none then
             fg.fragmentList.scan
               (# 
               do
                  (if current.type
                   // mps.formType then current.f[]->ff[]
                  if)
               #);
             
         if)
     if);
     (if ff[] <> none then
         (if width // 0 then 80->width if);
         (if depth // 0 then - 1->depth; (* full depth *)  if);
         (if ppSpec.empty
          // true then
             ff.grammar.grammarAst.father->fg2[];
             (fg2.fullName).copy->ppspec[];
             (ppspec.length-4,ppspec.length)->ppspec.delete;
             ppSpec.copy->help[];
             'NoTags'->help.putText;
             '-pretty'->help2[];
             mps.ppFileExtension->help2.append;
             help2[]->(help.copy).Append->ppSpecfile.name;
             (if ppSpecfile.entry.exists
              // true then help.copy->ppSpec[]
              else
                 ppSpec.copy->help[];
                 'Alt'->help.putText;
                 help2[]->(help.copy).Append->ppSpecfile.name;
                 (if ppSpecfile.entry.exists
                  // true then help.copy->ppSpec[]
                  else
                 (* default pp spec *)
                     
                 if);
                 
             if);
             
         if);
         ff.grammar.grammarIdentification
           ->astView.pp.init
             (# ppSpecification::<  (#  do ppSpec[]->ppSpecName[] #)
             #);
         false->astView.pp.editorMode;
         width->astView.pp.width;
         abstractPresentation->astView.pp.abstractPresentation;
         includeComments->astView.pp.includeComments;
         parsingInformation->astView.pp.parsingInformation;
         specialLexemTreatment->astView.pp.specialLexemTreatment;
         specialCommentTreatment->astView.pp.specialCommentTreatment;
         false->astView.pp.ppTrace;
         false->astView.pp.ppDebug;
         
     if);
     &outputStream[]->astView.out[];
     astView.out.init;
     (if f.type
      // mps.formType then
         (ff[],node[])->prettyPrintForm
      // mps.groupType then
         fg[]->getFragmentSyntaxComments->theComments[];
         fg.prop[]->prettyPrintPropertyList;
         fg[]->prettyPrintGroup;
         
     if);
     astView.out.finish;
     (if onFile then outputFile.close;  if);
     
  #)  

