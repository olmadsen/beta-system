origin '../astviewer';
INCLUDE 'unparser'
(* COPYRIGHT
 *      Copyright (C) Mjolner Informatics, 1986-94
 *       All rights reserved.
 *)
--updatePositions: DescriptorForm--
(* Per Fack Sorensen, DAIMI, 9. okt. 1987 *)
(* PFS: 2/12: converted to new syntax *)
  (# dX,dY,lineRelToFather: @integer; theNode,N: @SAST.node;
  enter (theNode,dx,dy)
  do (* update line numbers in SAST *)
     theNode->N;
     loop :
     (if true
      // true then
         (if N.brother
          // SAST.NULL then
          (* update only LR of fathers and both UL and LR of brothers *)
             N.father->N; (if (N = SAST.NULL) // true then leave loop if);
          else
             N.brother->N; N.ULdotx+dX->N.ULdotx;
         if);
         N.LRdotx+dX->N.LRdotx;
         restart loop
     if);
     (* update char numbers of last line of updated AST in SAST *)
     theNode.LRdotx->lineRelToFather;
     (* last line relative to father *)
     theNode->N;
     loop :
     (if N.brother
      // SAST.NULL then
         (if N.father
          // SAST.NULL then leave loop
          else
             N.father->N;
             (* check father ends on the same line *)
             (if (N.LRdotx-N.ULdotx)
              // lineRelToFather then
                 N.LRdoty+dY->N.LRdoty;
                 N.LRdotx->lineRelToFather;
                 restart loop;
              else
                 leave loop
             if);
         if);
      else
         N.brother->N;
         (* check brothers FIRST line is on same line as reference node *)
         (if N.ULdotx
          // lineRelToFather then N.ULdoty+dY->N.ULdoty;
          else
             leave loop;
         if);
         (* check brothers LAST line is on same line as reference node *)
         (if N.LRdotx
          // lineRelToFather then N.LRdoty+dY->N.LRdoty;
          else
             leave loop;
         if);
         (* do for all brothers *)
         restart loop;
     if);
  #)  

