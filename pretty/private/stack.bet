origin '../astviewer';
(* COPYRIGHT
 *      Copyright (C) Mjolner Informatics, 1986-94
 *       All rights reserved.
 *)
--stack: DescriptorForm--
(* Per Fack S|rensen, DAIMI, 24. aug. 1987 *)
(* PFS: 2/12: converted to new syntax *)
  (# (* This is a stack two additional operations:                         *)
  (* bottom:		read the bottom-element of the stack.             *)
  (* popBottom:	read and remove the bottom-element of the stack   *)
  (*   array: [________XXXXXXXXXXXXXXXXXXX_______]  <-> not Empty       *)
  (*                   ^                 ^                              *)
  (*                bottomP             topP                            *)
  (*                                                                    *)
  (*   array: [________X_________________________]  <-> not Empty       *)
  (*                   ^                                                *)
  (*            bottomP topP                                            *)
  (*                                                                    *)
  (*   array: [__________________________________]  <-> Empty           *)
  (*                   ^^                                               *)
  (*                topPbottomP                                         *)
     Initialize: @(# do 1->topP; 2->bottomP; true->Empty; #);
     Empty: @boolean;
     Push: @
       (# enter array[(topP mod stackSize)+1->topP] do false->Empty; #);
     Pop: @
       (# i: @integer;
       do
       (* Commented out, due to performance problems!
        (if Empty//true then
        (failureTrace,'PP: Stack: pop: empty') -> &stop
        if);
        *)
          array[topP]->i;
          (bottomP = topP)->Empty;
          (if topP // 1 then stackSize->topP; else topP-1->topP; if);
       exit i
       #);
     Top: @
       (#
       do
       (* Commented out, due to performance problems!
        (if Empty//true then
        (failureTrace,'PP: Stack: top: empty') -> &stop
        if);
        *)
       exit (array[topP])
       #);
     Bottom: @
       (#
       do
       (* Commented out, due to performance problems!
        (if Empty//true then
        (failureTrace,'PP: Stack: bottom: empty') -> &stop
        if);
        *)
       exit (array[bottomP])
       #);
     PopBottom: @
       (# i: @integer;
       do
       (* Commented out, due to performance problems!
        (if Empty//true then
        (failureTrace,'PP: Stack: popbot.: empty') -> &stop
        if);
        *)
          array[bottomP]->i;
          (bottomP = topP)->Empty;
          (bottomP mod stackSize)+1->bottomP;
       exit i
       #);
     (* private *)
     array: [stackSize] @integer;
     topP,bottomP: @integer;
     stackSize: (# exit 300 #);
  #)  

