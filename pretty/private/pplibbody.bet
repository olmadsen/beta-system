ORIGIN '../pplib';
LIB_ITEM 'prettyprinterpplib';
(* COPYRIGHT
 *      Copyright (C) Mjolner Informatics, 1986-2000
 *       All rights reserved.
 *)
(* COPYRIGHT
 *      Copyright (C) Mjolner Informatics, 1986-2000
 *       All rights reserved.
 *)
-- prettyprintFragment: DoPart --
do
(*enter 
 * (mps[],f[],node,outStream[],streamName[],width,depth,abstractPresentation,showSemanticErrors,
 * includeComments,parsingInformation,specialLexemTreatment,longLexemsWithQuote,
 * specialCommentTreatment,ppSpec[],onlyProperties,test)
 *)
   (mps[],f[],node,outStream[],streamName[],width,0,abstractPresentation,
    showSemanticErrors,includeComments,false,false,false,false,'',
    onlyProperties,false,test)->ppFragment->value;
     

-- ppFragment: DoPart --
do
   thisOperation:
     (#
        theComments: (* in the fragment syntax *) ^text;
        getNextComment: @|
        (*
         * A comment c for the fragment group is organized as follows:
         * 
         * c = c1 c2 ... cn, where the positions of the ci's are:
         * c1 son1 c1 son2 c2 .... cn sonn cn+1
         * each ci can be further divided into comments that must be 
         * prettyprinted separately.
         * nextComment scans all subcomments one of the time.
         * 
         * A call of nextComment returns the next subcomment in the 
         * sequence of comments belonging to the fragment group. 
         * 
         * n is the number of comments in a sub-sequence, 
         * if n is -1 the comment is empty and c[] is none
         * if n is 0 there is only one comment between the two sons or it is the last subcomment
         * if n is 1 there are more than one subcomment and c[] contains the current one
         * if n is -2 we have read the whole comment
         * 
         * the comment looks like this:
         * xxx 1 xxx 2 xxx 2 xxx 2 xxx 21 xxx 1 xxx 1 xxx 2 xxx 2 xxx 2 
         * 
         * This is a copy of getNextComment in mps.astlevel
         *)
          (# subcomment: ^text; n: @integer; inx: @Integer; ch: @Char; 
          do
             (if theComments[] <> none then
                 theComments.reset;
                 mainloop:
                 (if inx = theComments.lgth then (* end of comment *)
                     - 1->n;
                     none ->subcomment[];
                     SUSPEND;
                     0->inx;
                     restart mainloop;
                     
                  else
                 (* read next comment *)
                     &Text[]->subcomment[];
                     loop:
                     (if ((inx+1->inx)->theComments.inxget->ch)
                      // mps.CommentSeparator1 then
                         (if subcomment.lgth = 0 then (* no comments *)
                             - 1->n; 
                          else
                         (* one comment, or the last comment in a sub sequence *)
                             1->n; 
                         if);
                         
                      // mps.CommentSeparator2 then
                         (if true
                          // inx = theComments.lgth then
                             0->n; 
                          //
                          (inx+1->theComments.inxget)
                          =
                          mps.CommentSeparator1 then
                             inx+1->inx; 0->n; 
                          else
                             1->n
                         if);
                         
                      else
                         ch->subcomment.put; restart loop; 
                     if);
                     SUSPEND;
                     restart mainloop;
                     
                 if);
                 
              else
                 - 1->n; none ->subcomment[]; 
             if);
             
          exit (subcomment[],n,inx)
          #);
        Checkcomment:
          (#
             currentSubcomment: ^text;
             n,n2,inx: @integer;
             before:< object;
             after:< object;
             doComment:
               (# 
               do
                  (if currentSubcomment[] <> none then
                      (if currentSubcomment.lgth <> 0 then
                          before;
                          astView.pp.comBegin[]->astView.out.putText;
                          currentSubcomment[]->astView.out.putText;
                          astView.pp.comEnd[]->astView.out.putText;
                          after;
                          
                      if)
                  if)
               #)
          do
             getNextComment->(currentSubcomment[],n,inx);
             (if n
              // 0 then
                 doComment; 
              // 1 then
                 doComment;
                 loop:
                 (if 1 = 1 then
                     getNextComment->(currentSubcomment[],n2,inx);
                     doComment;
                     (if n2 = 1 then restart loop if);
                     
                 if);
                 
             if);
             
          exit inx
          #);
        getFragmentSyntaxComments:
          (# g: ^mps.fragmentGroup; theCommentss: ^text
          enter g[]
          do
             ''->theComments[];
             theComments.reset;
             g.prop.scanProp
               (#
                  doProp::< 
                    (# 
                    do
                       (if 'Comment'->prop.equalNCS then
                           scanParameters
                             (#
                                doString::< 
                                  (# 
                                  do
                                     s.scanAll
                                       (# 
                                       do
                                          (if ch = mps.CommentSeparator3 then
                                              mps.CommentSeparator1
                                                ->theComments.put;
                                              
                                           else
                                              ch->theComments.put; 
                                          if);
                                          
                                       #);
                                     
                                  #);
                                
                             #);
                           
                       if)
                    #)
               #)
          exit theComments[]
          #);
        prettyPrintPropertyList:
          (#
             prop: ^propertyList;
             firstTime,didWriteSomething,semicolonWritten: @boolean;
             
          enter fg[]
          do
             fg.prop[]->prop[];
             true->firstTime;
             prop.scanProp
               (#
                  firstname,firststring,commentAtStart,notCommentUnlessTest:
                    @boolean;
                  propUC: @text;
                  doProp::< 
                    (# 
                    do
                       prop->propUC;
                       propUC.makeUC;
                       (if true
                        // 'DONECHECK'->propUC.equal
                        // 'ABSTRACTED'->propUC.equal
                        // 'UNIQUE_GROUP_ID'->propUC.equal
                        // 'SLOTS'->propUC.equal // 'FREJAMARK'->propUC.equal
                        then (* Ignore these properties *)
                           checkComment
                             (# after::<  (#  do astView.out.newLine #)
                             #);
                           
                        //
                        'INCLUDE'
                          ->propUC.equal (* INCLUDE is handled after ORIGIN *)
                        then
                           
                        else
                           (if (1->prop.inxGet) = '_' then
                               checkComment
                                 (#
                                    after::<  (#  do astView.out.newLine #)
                                 #)
                            else
                               (if firstTime then
                                   false->firstTime
                                else
                                   ';'->astView.out.put;
                                   astView.out.Newline;
                                   true->semicolonWritten;
                                   
                               if);
                               checkComment
                                 (#
                                    after::< 
                                      (# 
                                      do
                                         astView.out.newLine;
                                         true->didWriteSomething
                                      #)
                                 #);
                               (if not ('COMMENT'->propUC.equal) then
                                   (if true
                                    // 'ORIGIN'->propUC.equal
                                    // 'BODY'->propUC.equal
                                    // 'MDBODY'->propUC.equal
                                    // 'OBJFILE'->propUC.equal
                                    // 'LIBFILE'->propUC.equal
                                    // 'LINKOPT'->propUC.equal
                                    // 'BETARUN'->propUC.equal
                                    // 'MAKE'->propUC.equal
                                    // 'RESOURCE'->propUC.equal
                                    // 'BUILD'->propUC.equal
                                    // 'LIB_DEF'->propUC.equal
                                    // 'LIB_ITEM'->propUC.equal then
                                       propUC[]->astView.out.putText
                                    else
                                       prop[]->astView.out.putText
                                   if);
                                   true->firstname->firststring;
                                   true->notCommentUnlessTest;
                                   true->didWriteSomething
                                else
                                   test->notCommentUnlessTest; 
                               if);
                               (if notCommentUnlessTest then
                                   scanParameters
                                     (#
                                        doName::< 
                                          (# 
                                          do
                                             (if firstname then
                                                 (if true
                                                  // 'MDBODY'->propUC.equal
                                                  // 'OBJFILE'->propUC.equal
                                                  // 'LIBFILE'->propUC.equal
                                                  // 'LINKOPT'->propUC.equal
                                                  // 'BETARUN'->propUC.equal
                                                  // 'MAKE'->propUC.equal
                                                  // 'RESOURCE'->propUC.equal
                                                  // 'BUILD'->propUC.equal then
                                                     checkComment
                                                       (#
                                                          before::< 
                                                            (# 
                                                            do
                                                               ' '
                                                                 ->
                                                                   astView.out.
                                                                     put
                                                            #);
                                                          after::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine;
                                                               true
                                                                 ->
                                                                   commentAtStart
                                                            #)
                                                       #);
                                                     
                                                  else
                                                     checkComment
                                                       (#
                                                          before::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine
                                                            #);
                                                          after::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine
                                                            #)
                                                       #);
                                                     
                                                 if);
                                                 false->firstname;
                                                 (if commentAtStart then
                                                     '       '
                                                       ->astView.out.putText;
                                                     
                                                  else
                                                     ' '->astView.out.put; 
                                                 if);
                                                 n[]->astView.out.putText;
                                                 
                                              else
                                                 astView.out.newLine;
                                                 (if true
                                                  // 'MDBODY'->propUC.equal
                                                  // 'OBJFILE'->propUC.equal
                                                  // 'LIBFILE'->propUC.equal
                                                  // 'LINKOPT'->propUC.equal
                                                  // 'BETARUN'->propUC.equal
                                                  // 'MAKE'->propUC.equal
                                                  // 'RESOURCE'->propUC.equal
                                                  // 'BUILD'->propUC.equal then
                                                     checkComment
                                                       (#
                                                          before::< 
                                                            (# 
                                                            do
                                                               '       '
                                                                 ->
                                                                   astView.out.
                                                                     putText
                                                            #);
                                                          after::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine
                                                            #)
                                                       #);
                                                     '       '
                                                       ->astView.out.putText;
                                                     
                                                  else
                                                     checkComment
                                                       (#
                                                          before::< 
                                                            (#  do  #);
                                                          after::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine
                                                            #)
                                                       #);
                                                     
                                                 if);
                                                 n[]->astView.out.putText;
                                                 
                                             if)
                                          #);
                                        doString::< 
                                          (# 
                                          do
                                             (if ('COMMENT'->propUC.equal) and
                                             test then
                                                 astView.pp.comBegin[]
                                                   ->astView.out.putText;
                                                 ' Comment in property list'
                                                   ->astView.out.putText;
                                                 astView.out.newLine;
                                                 '* '->astView.out.putText;
                                                 s.scanAll
                                                   (# 
                                                   do
                                                      (if ch
                                                       // mps.CommentSeparator1
                                                       then
                                                          'S'->astView.out.put;
                                                          astView.out.newLine;
                                                          '* '
                                                            ->
                                                              astView.out.
                                                                putText;
                                                          
                                                       // mps.CommentSeparator2
                                                       then
                                                          's'->astView.out.put; 
                                                       // mps.CommentSeparator3
                                                       then
                                                          'p'->astView.out.put; 
                                                       else
                                                          ch->astView.out.put; 
                                                      if);
                                                      
                                                   #);
                                                 astView.out.newLine;
                                                 astView.pp.comEnd[]
                                                   ->astView.out.putText;
                                                 
                                              else
                                                 (if true
                                                  // ('ORIGIN'->propUC.equal)
                                                  then
                                                     checkComment
                                                       (#
                                                          before::< 
                                                            (# 
                                                            do
                                                               ' '
                                                                 ->
                                                                   astView.out.
                                                                     put
                                                            #);
                                                          after::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine
                                                            #)
                                                       #);
                                                     ' \''->astView.out.putText;
                                                     s[]->astView.out.putText;
                                                     '\''->astView.out.put;
                                                     true->firstString;
                                                     fg.scanIncludes
                                                       (# 
                                                       do
                                                          (if firststring then
                                                              checkComment
                                                                (#
                                                                   before::< 
                                                                     (# 
                                                                     do
                                                                        ' '
                                                                          ->
                                                                            astView
                                                                            .out
                                                                            .put
                                                                     #);
                                                                   after::< 
                                                                     (# 
                                                                     do
                                                                        astView.
                                                                          out.
                                                                          newLine
                                                                     #)
                                                                #);
                                                              false
                                                                ->firststring;
                                                              ';'
                                                                ->
                                                                  astView.out.
                                                                    put;
                                                              astView.out.
                                                                newLine;
                                                              true
                                                                ->
                                                                  semicolonWritten;
                                                              'INCLUDE '
                                                                ->
                                                                  astView.out.
                                                                    putText;
                                                              '\''
                                                                ->
                                                                  astView.out.
                                                                    put;
                                                              current.linkname[]
                                                                ->
                                                                  astView.out.
                                                                    putText;
                                                              '\''
                                                                ->
                                                                  astView.out.
                                                                    put;
                                                              
                                                           else
                                                              astView.out.
                                                                newLine;
                                                              checkComment
                                                                (#
                                                                   before::< 
                                                                     (# 
                                                                     do
                                                                        '        '
                                                                          ->
                                                                            astView
                                                                            .out
                                                                            .
                                                                              putText
                                                                     #);
                                                                   after::< 
                                                                     (# 
                                                                     do
                                                                        astView.
                                                                          out.
                                                                          newLine
                                                                     #)
                                                                #);
                                                              '        \''
                                                                ->
                                                                  astView.out.
                                                                    putText;
                                                              current.linkname[]
                                                                ->
                                                                  astView.out.
                                                                    putText;
                                                              '\''
                                                                ->
                                                                  astView.out.
                                                                    put;
                                                              
                                                          if)
                                                       #)
                                                  // 'MDBODY'->propUC.equal
                                                  // 'OBJFILE'->propUC.equal
                                                  // 'LIBFILE'->propUC.equal
                                                  // 'LINKOPT'->propUC.equal
                                                  // 'BETARUN'->propUC.equal
                                                  // 'MAKE'->propUC.equal
                                                  // 'RESOURCE'->propUC.equal
                                                  // 'BUILD'->propUC.equal then
                                                     checkComment
                                                       (#
                                                          before::< 
                                                            (# 
                                                            do
                                                               ' '
                                                                 ->
                                                                   astView.out.
                                                                     put
                                                            #);
                                                          after::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine
                                                            #)
                                                       #);
                                                     ' \''->astView.out.putText;
                                                     s[]->astView.out.putText;
                                                     '\''->astView.out.put;
                                                     
                                                  // 'LIB_DEF'->propUC.equal
                                                  then
                                                     checkComment
                                                       (#
                                                          before::< 
                                                            (# 
                                                            do
                                                               ' '
                                                                 ->
                                                                   astView.out.
                                                                     put
                                                            #);
                                                          after::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine
                                                            #)
                                                       #);
                                                     ' \''->astView.out.putText;
                                                     s[]->astView.out.putText;
                                                     '\''->astView.out.put
                                                  // 'LIB_ITEM'->propUC.equal
                                                  then
                                                     checkComment
                                                       (#
                                                          before::< 
                                                            (# 
                                                            do
                                                               ' '
                                                                 ->
                                                                   astView.out.
                                                                     put
                                                            #);
                                                          after::< 
                                                            (# 
                                                            do
                                                               astView.out.
                                                                 newLine
                                                            #)
                                                       #);
                                                     ' \''->astView.out.putText;
                                                     s[]->astView.out.putText;
                                                     '\''->astView.out.put
                                                  else
                                                     (if firststring then
                                                         checkComment
                                                           (#
                                                              before::< 
                                                                (# 
                                                                do
                                                                   ' '
                                                                     ->
                                                                       astView.
                                                                         out.put
                                                                #);
                                                              after::< 
                                                                (# 
                                                                do
                                                                   astView.out.
                                                                     newLine
                                                                #)
                                                           #);
                                                         false->firststring;
                                                         ' \''
                                                           ->
                                                             astView.out.
                                                               putText;
                                                         
                                                      else
                                                         astView.out.newLine;
                                                         (if true
                                                          //
                                                          ('BODY'->propUC.equal)
                                                          then
                                                             checkComment
                                                               (#
                                                                  before::< 
                                                                    (# 
                                                                    do
                                                                       '     '
                                                                         ->
                                                                           astView
                                                                           .out.
                                                                             putText
                                                                    #);
                                                                  after::< 
                                                                    (# 
                                                                    do
                                                                       astView.
                                                                         out.
                                                                         newLine
                                                                    #)
                                                               #);
                                                             '     \''
                                                               ->
                                                                 astView.out.
                                                                   putText;
                                                             
                                                         if);
                                                         
                                                     if);
                                                     s[]->astView.out.putText;
                                                     '\''->astView.out.put;
                                                     
                                                 if)
                                             if);
                                             
                                          #);
                                        doConst:: 
                                          (# 
                                          do
                                             checkComment
                                               (#
                                                  before::< 
                                                    (# 
                                                    do ' '->astView.out.put
                                                    #);
                                                  after::< 
                                                    (# 
                                                    do ' '->astView.out.put
                                                    #)
                                               #);
                                             ' '->astView.out.put;
                                             (c,0)->astView.out.putInt
                                          #)
                                     #)
                               if)
                           if);
                           
                       if);
                       
                    #)
               #);
             loop:
             (if true then
                 (if checkComment
                 (* only necessary until new property syntax is fully used
                  * e.g. 
                  * INCLUDE a;b;c 
                  * instead of 
                  * INCLUDE a; 
                  * INCLUDE b; 
                  * INCLUDE c;
                  *)
                   (#
                      before::<  (#  do  #);
                      after::<  (#  do astView.out.newLine #)
                   #) < theComments.lgth then
                     restart loop
                 if)
             if);
             (if didWriteSomething and not semicolonWritten then
                 astView.out.newLine
             if)
          #);
        withFragmentSyntax: booleanValue
          (# fg: ^mps.fragmentGroup
          enter fg[]
          do
             true->value;
             scanning: fg.prop.scanProp
               (#
                  doProp::< 
                    (# 
                    do
                       (if '_WITHOUT_FRAGMENT_SYNTAX'->prop.equalNCS then
                           false->value; leave scanning
                       if)
                    #)
               #)
          #);
        prettyPrintFragmentSyntax:
          (# ff: ^mps.fragmentForm; t: ^text; grammarName: ^text
          enter ff[]
          do
             astView.pp.EOSchar[]->astView.out.putText;
             astView.pp.EOSchar[]->astView.out.putText;
             ' '->astView.out.put;
             ff.name->astView.out.putText;
             ': '->astView.out.putText;
             ff.category->ff.Grammar.symbolToName->t[];
             (if true
              // ('DescriptorForm'->t.equal) // ('ObjectDescriptor'->t.equal)
              then
                 'Descriptor'->astView.out.putText; 
              // ('AttributesForm'->t.equal) // ('AttributeDecl'->t.equal) then
                 'Attributes'->astView.out.putText; 
              else
                 t[]->astView.out.putText; 
             if);
             ff.grammar.grammarIdentification->grammarName[];
             (if not ('beta'->grammarName.equal) then
                 ': '->astView.out.putText; grammarName[]->astView.out.putText
             if);
             ' '->astView.out.put;
             astView.pp.EOSchar[]->astView.out.putText;
             astView.pp.EOSchar[]->astView.out.putText;
             astView.out.newLine
          #);
        prettyPrintGroup:
          (#
             g: ^mps.fragmentGroup;
             no,noOfFrags: @integer;
             firstTime: @boolean
          enter g[]
          do
             true->firstTime;
             (if g.fragmentlist[] <> none then
                 g.fragmentList.scan
                   (# 
                   do
                      no+1->no;
                      (if current.type = mps.formType then
                            (# ff: ^mps.fragmentForm
                            do
                               (if not onlyProperties then
                                   (if firstTime then
                                       false->firstTime
                                    else
                                       astView.out.Newline
                                   if);
                                   current.open->ff[];
                                   (if fg[]->withFragmentSyntax then
                                       ff[]->prettyprintFragmentSyntax
                                   if);
                                   (ff[],none )->prettyPrintForm
                               if)
                            #)
                      if)
                   #);
                 astView.out.newLine
             if)
          #);
        prettyPrintForm:
          (# ff: ^mps.fragmentForm; node: ^mps.ast
          enter (ff[],node[])
          do (* ff.root.dump;*)
             (if astDump then
                 (if node[] = none then
                     (depth,outStream[])->ff.root.dump; 
                  else
                     (depth,outStream[])->node.dump; 
                 if)
              else
                 (if node[] = none then
                     (ff.root[],depth)->astView.pp.present; 
                  else
                     (node[],depth)->astView.pp.present; 
                 if)
             if);
             
          #);
        outputFile: @file
          (#
             otherError:: 
               (# mess: ^text
               do
                  'Prettyprint file error, there is probably a sharing violation on: \n'
                    ->mess[];
                  name->mess.append;
                  '\nThe file is not updated'->mess.append;
                  mess[]->screen.putLine;
                  false->value;
                  leave thisOperation
               #)
          #);
        ppSpecFile: @file;
        fg,fg2: ^mps.fragmentGroup;
        ff: ^mps.fragmentForm;
        dashPos: @integer;
        help,help2,theGrammarName,ppSpecName: ^text;
        e: @diskEntry;
        suffix: @text;
        onFile: @boolean
     do
        true->value;
        &mps.astViewer[]->astView[];
        (if test then
            'ppgroup: '->putLine;
            '<'->put;
            f.name->putText;
            '>'->put;
            newLine;
            '<'->put;
            streamName[]->putText;
            '>'->put;
            newLine;
            width->putInt;
            newLine;
            depth->putInt;
            newLine;
            'abstractPresentation: '->putText;
            abstractPresentation->putBoolean;
            newLine;
            'showSemanticErrors: '->putText;
            showSemanticErrors->putBoolean;
            newLine;
            'includeComments: '->putText;
            includeComments->putBoolean;
            newLine;
            'parsingInformation: '->putText;
            parsingInformation->putBoolean;
            newLine;
            'specialLexemTreatment: '->putText;
            specialLexemTreatment->putBoolean;
            newLine;
            'longLexemsWithQuote: '->putText;
            longLexemsWithQuote->putBoolean;
            newLine;
            'specialCommentTreatment: '->putText;
            specialCommentTreatment->putBoolean;
            newLine;
            '<'->put;
            ppSpec[]->putText;
            '>'->put;
            newLine;
            
        if);
        (if outStream[] = none then
            (if streamName.empty then
                screen[]->outStream[]; 
             else
                true->onFile;
                streamName[]->outputfile.name;
                (if not outputFile.entry.writeable then
                    'No write access to \''->putText;
                    outputFile.name->putText;
                    '\''->putLine;
                    leave thisOperation
                if);
                outputFile.openWrite;
                outPutFile[]->outStream[]
            if)
        if);
        (if f.type
         // mps.formType then
            f[]->ff[]
         // mps.groupType then
            f[]->fg[];
            (if fg.fragmentList[] <> none then
                fg.fragmentList.scan
                  (# 
                  do
                     (if current.type = mps.formType then
                         current.f[]->ff[]
                     if)
                  #);
                
            if)
        if);
        (if ff[] <> none then
            (if width = 0 then 80->width if);
            (if depth = 0 then - 1->depth; (* full depth *)  if);
            (if ppSpec.empty then
                ff.grammar.grammarAst.father->fg2[];
                (fg2.fullName).copy->ppspec[];
                (ppspec.length-4,ppspec.length)->ppspec.delete;
                ppSpec.copy->help[];
                'NoTags'->help.putText;
                '-pretty'->help2[];
                mps.ppFileExtension->help2.append;
                help2[]->(help.copy).Append->ppSpecfile.name;
                (if ppSpecfile.entry.exists then
                    help.copy->ppSpec[]
                 else
                    ppSpec.copy->help[];
                    'Alt'->help.putText;
                    help2[]->(help.copy).Append->ppSpecfile.name;
                    (if ppSpecfile.entry.exists then
                        help.copy->ppSpec[]
                     else
                    (* default pp spec *)
                        
                    if);
                    
                if);
                
            if);
            ff.grammar.grammarIdentification
              ->astView.pp.init
                (# ppSpecification::<  (#  do ppSpec[]->ppSpecName[] #)
                #);
            false->astView.pp.editorMode;
            width->astView.pp.width;
            abstractPresentation->astView.pp.abstractPresentation;
            includeComments->astView.pp.includeComments;
            parsingInformation->astView.pp.parsingInformation;
            specialLexemTreatment->astView.pp.specialLexemTreatment;
            specialCommentTreatment->astView.pp.specialCommentTreatment;
            false->astView.pp.ppTrace;
            false->astView.pp.ppDebug;
            
        if);
        &outputStream[]->astView.out[];
        astView.out.init;
        (if f.type
         // mps.formType then
            (ff[],node[])->prettyPrintForm
         // mps.groupType then
            fg[]->getFragmentSyntaxComments->theComments[];
            (if fg[]->withFragmentSyntax then
                fg[]->prettyPrintPropertyList
            if);
            fg[]->prettyPrintGroup
        if);
        astView.out.finish;
        (if onFile then outputFile.close;  if)
     #)  

