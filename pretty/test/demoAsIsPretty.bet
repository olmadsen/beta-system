ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/fields'
        '~beta/guienv/fields'
        '~beta/guienv/utils/textfieldadds'
        '~beta/guienv/utils/codeTextField'
        '~beta/mps/astlevel'
        '~beta/pretty/asIsPretty'
        '~beta/mps/grammarinit'
        '~beta/betaast/betacfl'
        '~beta/mps/findgrammar'
        '~beta/guienv/stddialogs'
        '~beta/basiclib/textutils';
-- windowlib: Attributes --
codeTextEditor: textEditor
  (#
     contentsType::< codeTextField
       (# open::<  (#  do init; INNER #);  #);
     open::<  (#  do 'Open codeTextEditor'->screen.putline; INNER #)
  #)  

-- program: Descriptor --
guienv
  (#
     W: @window
       (#
          menubarType:: 
            (#
               fileMenu: @menu
                 (#
                    openItem: @menuItem
                      (#
                         eventHandler:: 
                           (#
                              onSelect:: 
                                (#  do W[]->fileSelectionDialog->showFile #)
                           #);
                         open::  (#  do 'Open'->name #)
                      #);
                    saveItem: @menuItem
                      (#
                         eventHandler:: 
                           (# onSelect::  (#  do 'Save'->putline #) #);
                         open::  (#  do 'Save'->name #)
                      #);
                    quitItem: @menuItem
                      (#
                         eventHandler:: 
                           (# onSelect::  (#  do terminate #) #);
                         open::  (#  do 'Quit'->name #)
                      #);
                    open:: 
                      (# 
                      do
                         'File'->name;
                         openItem.open;
                         openItem[]->append;
                         saveItem.open;
                         saveItem[]->append;
                         quitItem.open;
                         quitItem[]->append
                      #)
                 #);
               open::  (#  do fileMenu.open; fileMenu[]->append #)
            #);
          (* menubarType *)
          eventhandler::  (# onAboutToClose::  (#  do terminate #) #);
          G: @codeTextEditor (# open::  (#  do W.size->size #) #);
          open::< 
            (# node: ^AST.cons
            do
               (400,400)->size;
               G.open;
               AST.astLevelInit;
               ('~beta/grammars/beta/beta','beta',screen[])
                 ->betagram.betaGrammarInit;
               betaGram.init;
               
            #);
          AST: @astInterface;
          betagram: @AST.BETA;
          FG: ^AST.fragmentGroup;
          viewer: @AST.asIsViewer
            (#
               output::< 
                 (#
                    put::< 
                      (# T: @text
                      do
                         (if false then
                             'output: '->screen.puttext;
                             '"'->screen.put;
                             ch->screen.put;
                             '"'->screen.put;
                             ch->screen.putint;
                             screen.newline
                         if);
                         ch->T;
                         T[]->G.contents.insert
                      #)
                 #)
            #);
          showFile:
            (# path: ^text; node: ^AST.cons; S: @styledText
            enter path[]
            do
               (if path[] <> none then
                   (if true
                    // '.bet'->path.endsWith // '.ast'->path.endsWith then
                       (path.length-3,path.length)->path.delete
                    // '.astL'->path.endsWith then
                       (path.length-4,path.length)->path.delete
                   if);
                   (path[],screen[])->AST.top.open->FG[];
                   FG.fragmentList.scan
                     (# F: ^AST.fragmentForm; 
                     do
                        (if current.type = AST.formType then
                            current.open->F[]; f.root[]->node[]; 
                        if)
                     #);
                   'Path; '->screen.puttext;
                   path[]->screen.putline;
                   path[]->viewer.init;
                   ''->S;
                   S[]->G.contents.contents;
                   (node[],500)->viewer.present;
                   
               if)
            #)
       #)
  do W.open
  #)  

