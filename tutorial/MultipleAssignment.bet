ORIGIN '~beta/basiclib/v1.6/betaenv'
---- program: descriptor ----
(# 
   (*   Multiple assignment and function calls  *)
   
   i,j,k: @integer;
   NINES: (# exit 99999  #);    (* Constant  *)
   
   complex: (# Re,Im: @integer;
            enter (Re,Im)
            exit  (Re,Im)
            #);
   add: (# A,B: @integer;
        enter (A,B)
        exit A+B
        #);
   
do
   'Examples using multiple assignment and function calls'->putline; newline;
   
   'Outputting a constant: NINES= '->puttext;
   NINES->putint;
   newline; newline;
   
   '(1,2,3)->(i,j,k); '->putline; newline;
   
   (1,2,3)->(i,j,k);
   ' I= '->puttext;  i->putint;
   ', J= '->puttext;  j->putint;
   ', K= '->puttext;  k->putint; newline; 
   
   newline; '(i,j)->(j,i): '->puttext;
   
   (i,j)->(j,i);
   ' I= '->puttext;  i->putint;
   ', J= '->puttext;  j->putint; newline;
   
   'Note that (x,y)->(y,x) doesn''t imply swap semantics: ' ->
   putline; newline;
   
   '  (*** More examples ***)'->putline; newline;
   
   (111,999)->complex->(i,j);
   (111,999)->add->k ;
   
   ' I= '->puttext;  i->putint;
   ', J= '->puttext;  j->putint;
   ', K= '->puttext;  k->putint; newline;
#)  
