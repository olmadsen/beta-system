ORIGIN '~beta/basiclib/v1.6/betaenv'
---- program: descriptor ----
(# 
   (* Multiplication Table 3
    *
    *  Objective: Store values in a repetition
    *)
   N,M: @Integer;
do
   '\n\t** Multiplication Table ** \n\n'->puttext;
   'Enter dimensions (N M): '->puttext;
   getint->N;
   getint->M;
   (# mul_table: [N*M] @Integer;
   do (* build table *)
      (for i: N repeat
           (for j: M repeat
                i*j->mul_table[j+(i-1)*M];
           for);
      for);
      (* print table *)
      newline; 
      '    '->puttext;
      (for i: M repeat
           i->screen.putint(# format:: (# do 4->width #)#);
      for);
      newline;
      (for i: N repeat
           i->screen.putint(# format:: (# do 4->width #)#);
           (for j: M repeat
                mul_table[j+(i-1)*M]
                  ->screen.putint(# format:: (# do 4->width #)#);
           for);
           newline;
      for)
   #);
#)
