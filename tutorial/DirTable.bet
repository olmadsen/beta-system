ORIGIN '~beta/basiclib/v1.4/directory';
INCLUDE '~beta/containers/v1.4/hashTable';
---program: descriptor---
(# dirTable: @hashTable
     (# element::< Text;
        hashFunction:: 
          (# 
          do e.scanAll(# do value*100 + ch->value #);
          #);
     #);
   d: @directory;
do (if noOfArguments <> 2 then
       'Usage: '->puttext; 1->arguments->puttext; ' path'->putline;
       stop;
   if);
   (* set name of directory *)
   2->arguments->d.name;
   (* print name of directory *)
   newline;
   (* initialize table *)
   dirTable.init;
   (* scan the entries and append to list *)
   d.scanEntries
   (# (* found refers to the current entry *)
   do found.path->dirTable.insert;
   #);
   (* dirTablenow contains all the names of the entries in the
    * directory
    *)
   dirTable.scan
   (# (* current refers to the current text element *)
   do current[]->putline; (* print the text *)
   #);

   (* print hashtable statistics on screen*)
   '\nStatistics: '->screen.putline;
   dirTable.statistics(# do screen[]->print #);
#)
