ORIGIN  '~beta/uienv/v1.1.8/uienv';
INCLUDE '~beta/uienv/v1.1.8/fields'
'~beta/uienv/v1.1.8/stddialogs'
'~beta/basiclib/v1.4/file'
-- program: descriptor --
uienv (* inherit from guienv *)
(# theWindow: @window (* make a window *)
     (# menubarType:: (* extend the menubar *)
          (# fileMenu: @menu (* make a file menu *)
               (# textFile: @file; (* the file we open and save *)
                  openItem: @menuitem (* make an open item *)
                    (# eventHandler:: (* extend the virtual that is called *)
                         (# onSelect:: (* this menu item is selected *)
                              (# theText: @StyledText;
                              do theWindow[]->fileSelectionDialog 
                                   -> textFile.name;
                                 textFile.openRead;
                                 textFile.scan
                                 (# while:: (# do true->value #);
                                 do ch->theText.put
                                 #);
                                 theText[]->theTextEditor.contents.contents;
                                 textFile.close;
                         #)#);
                       open:: (# do 'Open'->name #);
                    #);
                  saveItem: @menuitem (* make a save item *)
                    (# eventHandler:: (* extend the virtual that is called *)
                         (# onSelect:: (* this menu itme is selected *)
                              (# theText: @Text;
                              do textFile.openWrite;
                                 theTextEditor.contents.contents->textFile.puttext;
                                 textFile.close;
                         #)#);
                       open:: (# do 'Save'->name #);
                    #);
                  quitItem: @menuitem (* make a quit item *)
                    (# eventHandler:: (# onSelect:: (# do Terminate #) #);
                       open:: (# do 'Quit'->name #);
                    #);
                  open:: (* extend the open virtual of filemenu to open the items *)
                    (# 
                    do 'File'->name;
                       openItem.open; openItem[]->append;
                       saveItem.open; saveItem[]->append;
                       quitItem.open; quitItem[]->append;
               #)#);
             open:: (* extend the open virtual of the menubar to open the filemenu *) 
               (# do fileMenu.open; fileMenu[]->append #);
          #);
        thetextEditor: @textEditor (* our text editor *)
          (# open:: (* extend the open virtual to set the size and the placement *)
               (# 
               do theWindow.size->Size;
                  True->bindBottom; True->bindRight 
               #);
          #);
        open:: (* extend the window open virtual to open the textEditor *)
          (# 
          do thetextEditor.open;
          #);
     #);
do theWindow.open; (* open the window when the application start up *)
#)
