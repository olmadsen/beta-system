ORIGIN '~beta/basiclib/v1.6/systemenv'
--- program: descriptor ---
systemEnv (* inherits from systemEnv *)
(# 
   (* everyNthSecond calls INNER every Nth second. *)
   everyNthSecond: System (* Inherit from System: can run concurrently *)
     (# N:< IntegerValue;
        now: @Integer;
     do cycle 
          (# 
          do INNER everyNthSecond;
             N -> sleep; (* sleep for N seconds *)
             now+N -> now; (* accumulate time *)
          #);
     #);
   every: @| everyNthSecond (* a co-routine that inherits from everyNthSecond *)
     (# N::< (# do 1 -> value #);  (* sleep for 1 second *)
     do 'every: ' -> putText; now -> screen.putInt; newline;
     #);
   fourth: @| everyNthSecond (* a co-routine that inherits from everyNthSecond *)
     (# N::< (# do 4 -> value #); (* sleep for 4 seconds *)
     do 'fourth: ' -> putText; now -> screen.putInt; newline;
     #);
   eigth: @| everyNthSecond (* a co-routine that inherits from everyNthSecond *)
     (# N::< (# do 8 -> value #); (* sleep for 8 seconds *)
     do 'eigth: ' -> putText; now -> screen.putInt; newline;
     #);
do
   (* execute concurrently: *)
   conc (# do every[] -> start; fourth[] -> start; eigth[] -> start; #);
   (* terminates when all systems stops *)
#)
