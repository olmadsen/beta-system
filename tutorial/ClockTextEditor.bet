ORIGIN '~beta/guienv/v1.6/guienvsystemenv';
INCLUDE '~beta/guienv/v1.6/fields'
        '~beta/guienv/v1.6/stddialogs'
        '~beta/basiclib/v1.6/file'
        '~beta/sysutils/v1.6/time';
-- program: Descriptor --
systemEnv
(#
   setWindowEnv::<  (#  do myguienv[]->theWindowEnv[] #);
   updateClock: @|System
     (# 
     do
        cycle
        (# theText: @StyledText; 
        do 1->sleep;
           systemtime->formattime-> myguienv.theWindow.clock.label;
        #);
     #);
   myguienv: @guienv (* inherit from guienv *)
     (# theWindow: @window (* make a window *)
          (# menubarType:: (* extend the menubar *) 
               (# fileMenu: @menu (* make a file menu *)
                    (# textFile: @file;
                       openItem: @menuitem (* make an open item *)
                         (# eventHandler::
                            (* extend the virtual that is called *) 
                              (# onSelect:: 
                                 (* this menu item is selected *) 
                                   (# theText: @StyledText; 
                                   do theWindow[]->
                                        fileSelectionDialog->
                                        textFile.name;
                                      textFile.openRead;
                                      textFile.scan
                                        (# while::(#do true->value#);
                                        do ch->theText.put
                                        #);
                                      theText[]->
                                      theTextEditor.contents.
                                                    contents;
                                      textFile.close;
                              #)#);
                            open::  (#  do 'Open'->name #);
                         #);
                       saveItem: @menuitem (* make a save item *)
                         (# eventHandler::
                            (* extend the virtual that is called *) 
                              (# onSelect:: 
                                 (* this menu item is selected *) 
                                   (# theText: @Text; 
                                   do
                                      textFile.openWrite;
                                      theTextEditor.contents.
                                                    contents->
                                        textFile.puttext;
                                      textFile.close;
                                   #)
                              #);
                            open:: (# do 'Save'->name #);
                         #);
                       quitItem: @menuitem (* make a quit item *)
                         (# eventHandler:: 
                              (# onSelect:: (#  do Terminate #) #);
                            open:: (#  do 'Quit'->name #);
                         #);
                       open:: 
                       (* extend the open virtual of filemenu 
                        * to open the items *)
                         (# 
                         do 'File'->name;
                            openItem.open;
                            openItem[]->append;
                            saveItem.open;
                            saveItem[]->append;
                            quitItem.open;
                            quitItem[]->append;
                         #)
                    #);
                  open::
                  (* extend the open virtual of the menubar
                   * to open the filemenu *)
                   (# do fileMenu.open; fileMenu[]->append #);
               #);
             thetextEditor: @textEditor (* our text editor *)
               (#
                  open::
                  (* extend the open virtual to set the size 
                   * and the placement *)
                  (# w,h: @integer; 
                    do theWindow.size->(w,h);
                       (w,h-20)->Size;
                       True->bindBottom;
                       True->bindRight
                    #);
               #);
             clock: @staticText
               (# open:: 
                    (# w,h: @integer; 
                    do systemtime->formattime->label;
                       theWindow.size->(w,h);
                       (5,h-16)->position;
                       (300,15)->size;
                       True->BindBottom;
                       False->BindTop;
                    #);
               #);
             open::
             (* extend the window open virtual
              * to open the textEditor *) 
               (#  do thetextEditor.open; clock.open;  #);
          #);
     do theWindow.open; 
        (* open the window when the application start up *) 
     #)
do updateClock[]->fork; 
#)  
