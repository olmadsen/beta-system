ORIGIN '~beta/basiclib/v1.4/directory';
INCLUDE '~beta/containers/v1.4/list';
---program: descriptor---
(# dirList: @List
     (# element:: Text;
        equal:: 
          (# (* since out element type is text simply test whether the
              * two text strings are equal
              *)
          do left[]->right.equal->value;
          #);
     #);
   d: @directory;
do (if noOfArguments <> 2//true then
       'Usage: '->puttext; 1->arguments->puttext; ' path'->putline;
       stop;
   if);
   (* set name of directory *)
   2->arguments->d.name;
   (* print name of directory *)
   newline;
   (* initialize list *)
   dirList.init;
   (* scan the entries and append to list *)
   d.scanEntries
   (# (* found refers to the current entry *)
   do found.path->dirList.append;
   #);
   (* dirList now contains all the names of the entries in the
    * directory
    *)
   dirList.scan
   (# (* current referns to the current text element *)
   do current[]->putline; (* print the text *)
   #);
#)
