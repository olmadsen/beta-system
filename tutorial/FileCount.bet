ORIGIN '~beta/basiclib/v1.4/file'
---- program: descriptor ----

(# (* ----------------------------------------------
    *   count.bet: Simple file handling program
    *              -Counting lines/characters-
    * ----------------------------------------------*)
   
   inFile: @file;
   Ch: @char;
   nc: @integer;
   answer: ^text;
   lines, chars: @Boolean;
do
   2->Arguments->inFile.name ;
   inFile.openRead;  (* OPENING *)
   'Count what in '''->Puttext;   inFile.name->PutText;
   ''' (lines/chars)? '->PutText;
   GetLine->answer[]; (* read from keybord ­ what the user types *)
   (if true
    //('lines'->answer.equal) then true->lines;
    //('chars'->answer.equal) then true->chars;
    else 
       'Unknown input'->PutLine;
       Stop; (* end execution *)
   if);
   Loop: 
     (if inFile.eos//false then
         inFile.Get->Ch; 
         (if true
          //lines then (if Ch//ascii.newline then nc + 1->nc if);
          //chars then nc + 1->nc;
         if);
         restart Loop
     if);
   NewLine;
   nc->PutInt; 
   (if true
    //lines then ' lines '->PutText;
    //chars then ' characters '->PutText;
   if);
   'in file '''->Puttext;
   inFile.name->PutText;
   '''\n\n'->PutText;
   inFile.close;
#)
