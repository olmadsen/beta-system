ORIGIN '~beta/basiclib/v1.5/betaenv';
---program: descriptor---
(# (* Hoare QuickSort program illustrating how to use 
    * repetitions, simple pattern declarations,
    * block structure and recursion.
    *)
   numberRepetition: (# r: [1] @Integer #);
   qsort: 
     (# nr: ^numberRepetition;
        left, right, last: @Integer;
        swap: 
          (# i,j: @Integer;
             temp: @Integer;
          enter (i,j)
          do nr.r[i]->temp;
             nr.r[j]->nr.r[i];
             temp->nr.r[j];
          #);
     enter (nr[], left, right)
     do L: (if left >= right then (* stop if rep. contains *) 
               leave L;           (* fewer than two elements *)
            else
               (* move partition element to nr.r[1] *)
               (left, (left+right)/2) -> swap;
               left->last;
               (* partition *)
               (for i: right-left repeat
                    (if nr.r[i+left] < nr.r[left] then
                        last+1->last;
                        (last,i+left) -> swap;
                    if);
               for);
               (left,last) -> swap; (* restore partition element *)
               (nr[],left,last) -> qsort;
               (nr[],last+1,right) -> qsort;
           if);
     #);
   numbers: @numberRepetition;
   t: ^Text;
   i: @Integer;
do
   (* initialize a repetition with numbers typed
    * by the user
    *)
   'Type some numbers: '->puttext;
   getline->t[]; (* read all what the user types until newline *)
   1->i;
   t.reset;
   L: (if not t.eos then
          (* parse the text;
           * assuming that the user only types numbers
           *)
          (if i>numbers.r.range then 
              (* remember to extend the repetition *)
              numbers.r.range->numbers.r.extend;
          if);
          t.getint->numbers.r[i];
          i+1->i;
          restart L;
      if);
   
   (* sort the repetition *)
   (numbers[],1,i-1) -> qsort;
   
   'Sorted numbers: '->puttext;
   (for j: i-1 repeat
        numbers.r[j]->putint; ' '->put;
   for);
   newline;
#)
