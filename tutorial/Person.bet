ORIGIN '~beta/basiclib/v1.4/external';
INCLUDE '~beta/sysutils/v1.4.2/cstring';
OBJFILE nti     '$/cperson.obj'
        mac     '$/cperson.obj'
        default '$/cperson.o';
MAKE    nti     'person_nti.make'
        mac     'person_mac.make'
        default 'person_unix.make';
--program: descriptor-- 
(# 
   getPerson: external
     (# ID: @integer;
        ptr: @integer;
     enter ID
     exit ptr
     #);
   putPerson: external
     (# ID: @integer;
        firstname, surname: [1]@char;
        sex: @char;
        result: @boolean;
     enter (ID,firstname,surname,sex)
     exit result
     #);
   Person: ExternalRecord
     (# ID: @long(# pos::(# do 0-> value #)#);
        firstname: @long(# pos::(# do 4-> value #)#);
        surname: @long(# pos::(# do 8-> value #)#); (* pointers to text *)
        sex: @byte(# pos::(# do 12-> value #)#);
     #);
   putCString:
     (# cstr: @cString;
     enter cstr
     do cstr.get -> puttext;
     #);
   aPerson: @Person;
do 
   (* store a person in C-database *)
   (if not ((117,'Roger','Smith','m')->putPerson) then
       'Failed to store person'->putline; stop;
   if);
   (* get person from C-database *)
   117 -> getPerson -> aPerson.ptr;
   (if aPerson.ptr = 0 then
       'Failed to retrieve person' -> putline; stop;
   if);
   'Person: ' -> puttext;
   aPerson.ID -> putint; 
   ' ' -> put;
   'Name: ''' -> puttext;
   aPerson.firstname -> putCString;
   ' ' -> put;
   aPerson.surname -> putCString; 
   ''' ' -> puttext;
   'Sex: ' -> puttext;
   aPerson.sex-> put;
   newline;
#)
