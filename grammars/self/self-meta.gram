
---- self: AGrammar: metagrammar ----
Grammar self :

Option 

  version = 1 
  comBegin = '(*'
  comEnd = '*)'
  stringChar = ''' '
  suffix = '.self'

Rule
  <SelfExp> ::= <exx> ;

  <exx> ::=  <expression> ;

  <expression> ::| <keywordMessage> | <term> ;

  <keywordMessage> ::| <explicitDirectKeywordMessage>
		     | <implicitDirectKeywordMessage>
                     | <resendKeywordMessage> ;

  <explicitDirectKeywordMessage> ::= '!!' <keywordReceiver:term> <keywordSend> ;
  <implicitDirectKeywordMessage> ::= '!!' <keywordSend> ;

  <keywordSend> ::= '::' <smallKeyword:nameAppl> ':' <keywordArguments> '!' ;

  <keywordArguments> ::| <singleArgument> | <multipleArguments> ;

  <singleArgument> ::= <expression> ;

  <multipleArguments> ::= <term> '::'<capKeyword:nameAppl> ':' <keywordArguments> ;

  <resendKeywordMessage> ::= <resend> '.' <keywordSend> ;

  <term> ::| <binaryMessage> | <factor> ;

  <factor> ::| <unaryMessage> | <constant> ;

  <constant> ::| <selfx> | <number> | <text> | <object>;
  <selfx> ::= 'selfx';
  <number> ::= <const>;
  <text> ::= <string>;
  <unaryMessage> ::| <explicitDirectUnaryMessage> 
		   | <implicitDirectUnaryMessage>
		   | <resendUnaryMessage>
	           ;
  <explicitDirectUnaryMessage> ::= <unaryReceiver:factor> <unarySend>;
  <implicitDirectUnaryMessage> ::= <unarySend>;

  <resendUnaryMessage> ::= <resend> '.' <unarySend>;
  <resend> ::= 'resend';
  <unarySend> ::= <nameAppl>;
  <binaryMessage> ::| <explicitDirectBinaryMessage> 
		    | <implicitDirectBinaryMessage>
		    | <resendBinaryMessage>
		    ;
  <explicitDirectBinaryMessage> ::= <binaryReceiver:term> <binarySend>;
  <implicitDirectBinaryMessage> ::= <binarySend>;

  <binarySend> ::= <operator> <factor>;
  <resendBinaryMessage> ::= <resend> '.' <binarySend>;
  <object> ::| <unannotatedObject>;
  <unannotatedObject> ::| <regularObject> | <block>;
  <regularObject> ::= '(' <body> ')';
  <block> ::= '[' <body> ']';
  <body> ::= <slotsOpt> <codeOpt>;
  <slotsOpt> ::? <slots>;
  <slots> ::= '|' <slotList> '|';
  <slotList> ::* <slot> '.';
  <slot> ::|
      <argSlot> | <dataSlot> | <unarySlot> | <binarySlot> | <keyWordSlot>;
  <argSlot> ::= ':' <nameDecl>;
  <dataSlot> ::| <slotName>;
  <slotname> ::| <localSlotName> | <parentName>;
  <localSlotName> ::= <nameDecl>;
  <parentName> ::= <nameDecl> '*';
  <unarySlot> ::= <slotName> '=' <regularObject>;
  <binarySlot> ::= <operator> '=' <regularObject>;
  <keyWordSlot> ::| <keyWordSlotA> | <keyWordSlotB>;
  <keyWordSlotA> ::= <smallKeyword> <capKeywords> '=' <regularObject>;
  <keyWordSlotB> ::=
      <smallKeyword> <nameDecl> <capKeywordsB> '=' <regularObject>;
  <smallKeyWord> ::= <nameDecl> ':';
  <capKeyWords> ::* <capKeyWord> ;
  <capKeyWord> ::= <nameDecl> ':';
  <capKeyWordsB> ::* <capKeyWordB> ;
  <capKeyWordB> ::= <nameDecl> ':' <nameDecl>;
  <codeOpt> ::? <code>;
  <code> ::= <expressions>;
  <expressions> ::+ <Rexpression> '.';
  <Rexpression> ::= <returnOpt> <expression>;
  <returnOpt> ::? <return>;
  <return> ::= '^';
  <operator> ::| <plus> | <mult>;
  <plus> ::= '+';
  <mult> ::= '*'  

