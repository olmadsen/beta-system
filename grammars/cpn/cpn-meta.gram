(* Textual Interchange Format for Design/CPN ver. 3.1 
 * Manually generated from DTD/cpn.dtd.
 * CVS: $Revision: 1.17 $
 *)

specialLexems;
dashNames;

--- cpn: aGrammar: metagrammar ---
Grammar cpn:

Option
  version = 1
  dashKeywords = true
  caseSensitive = true
  comBegin = '<!--'
  comEnd = '-->'
  stringChar = '"'
  suffix = '.int'
  EOSchar = '%'
  bobsOptions='5,32,34,59'
  TYPE = SGML
  CDATA = ( '<text>' ) (* Not yet used *)

  (* the predefined nonterminal nameAppl is unused *)
  nameAppl=unused

Rule
 
(* NOTICE: all non-terminals with names xxx_gen are generators for
 * rules for the & SGML syntax, which means that each rightside appears
 * exactly once, but in any order. This cannot be expressed in SNBF
 * without permuting over all possible combinations of right-sides.
 * Instead a generator production is made, which can generate a number
 * of each right-side. The tool using the CFL generated from this
 * grammar must check that each rightside occurs exactly once
 *)


(************************* ENTITIES ****************************)

(* Possible shapes for objects: rbox is a rounded box *)
<shape>     ::| <box> 
              | <ellipse>
              | <poly>
              | <line> 
              | <label> 
              | <wedge>
              | <rbox>
              | <seg_conn>;

(* Legal attributes for objects *)
<atts>      ::* <atts_gen>;
<atts_gen>  ::| <shape> 
              | <posattr> 
              | <fillattr> 
              | <lineattr> 
              | <textattr>
              | <flags>
              | <layering>;

(* Line types: *)
<lintyps>   ::| <Solid>
              | <Dashed>
              | <DotDash>
              | <GroupInd>
              | <FlashInd>
              | <None>
              | <Null>
              | <Dotted>;

<Solid>     ::= 'Solid';
<Dashed>    ::= 'Dashed';
<DotDash>   ::= 'DotDash';
<GroupInd>  ::= 'GroupInd';
<FlashInd>  ::= 'FlashInd';
<None>      ::= 'None';
<Null>      ::= 'Null';
<Dotted>    ::= 'Dotted';

(* Colours. These colours corresponds to the standard colours of HTML:
   Name and RGB value:
     Black  = #000000    Green  = #008000
     Silver = #C0C0C0    Lime   = #00FF00
     Gray   = #808080    Olive  = #808000
     White  = #FFFFFF    Yellow = #FFFF00
     Maroon = #800000    Navy   = #000080
     Red    = #FF0000    Blue   = #0000FF
     Purple = #800080    Teal   = #008080
     Fuchsia= #FF00FF    Aqua   = #00FFFF
*)
<cols>      ::| <Black>
              | <Maroon>
              | <Gray> 
              | <Olive>
              | <Purple> 
              | <Silver> 
              | <Red> 
              | <Teal> 
              | <Navy> 
              | <Fucia> 
              | <Blue> 
              | <Aqua> 
              | <Green> 
              | <Lime> 
              | <Yellow> 
              | <White>;

<Black>     ::= 'Black'; 
<Maroon>    ::= 'Maroon';       
<Gray>      ::= 'Gray'; 
<Olive>     ::= 'Olive'; 
<Purple>    ::= 'Purple';
<Silver>    ::= 'Silver';
<Red>       ::= 'Red';
<Teal>      ::= 'Teal';
<Navy>      ::= 'Navy';
<Fucia>     ::= 'Fucia';
<Blue>      ::= 'Blue';
<Aqua>      ::= 'Aqua'; 
<Green>     ::= 'Green';
<Lime>      ::= 'Lime';
<Yellow>    ::= 'Yellow';
<White>     ::= 'White';

(* The possible orientations of an arc:
     BOTHDIR = Bidirectional arc: O<->[]
     NODIR = Arc without arrows: O-[]
     PtoT = Arc from Place to Transition: 0->[]
     TtoP = Arc from Transition to Place: []->O
*)
<arcors>    ::| <BOTHDIR>
              | <NODIR>
              | <PtoT>
              | <TtoP>;

(* Possible types of fusion sets:
     Global = Global fusion set.
     Page = Page Fusion set.
     Instance = Instance Fusion Set 
*)
<fustyps>   ::| <Global>
              | <Page>
              | <Instance>;

(* Possible orientations of an auxiliary connector: 
     BOTHDIR = Bidirectional connector.
     NODIR = Nondirectional connector.
     1to2 = Connector from object1 to object2
     2to1 = Connector from object2 to object1 
*)
<orients>   ::| <BOTHDIR>
              | <NODIR>
              | <one_to_two>
              | <two_to_one>;

(* Possible fill patterns for objects *)
<pats>      ::| <None> 
              | <Solid> 
              | <AlmostSolid> 
              | <InverseHeavyDotted> 
              | <Chessboard> 
              | <InverseOrderedDotted> 
              | <NarrowVertical> 
              | <FilledDiagonals>
              | <WideVertical> 
              | <Speckled> 
              | <RandomDotted> 
              | <NarrowGrid> 
              | <Bricks> 
              | <SparselyDotted> 
              | <ShortDiagonals> 
              | <Lightbubbles> 
              | <PieTop> 
              | <Buttercups> 
              | <Waffles> 
              | <LightDotted> 
              | <MediumDotted> 
              | <LightGray> 
              | <OrderedDotted> 
              | <NarrowHorizontal> 
              | <NarrowDiagonal> 
              | <WideHorizontal> 
              | <WideDiagonal>
              | <DottedBoxes> 
              | <WideGrid> 
              | <DiagonalBricks> 
              | <Holiday> 
              | <Carrots> 
              | <DiagonalScales> 
              | <RoundScales> 
              | <Mountains> 
              | <HeavyBubbles> 
              | <Diamonds>;

<None>                 ::= 'None'; 
<Solid>                ::= 'Solid'; 
<AlmostSolid>          ::= 'AlmostSolid'; 
<InverseHeavyDotted>   ::= 'InverseHeavyDotted'; 
<Chessboard>           ::= 'Chessboard'; 
<InverseOrderedDotted> ::= 'InverseOrderedDotted'; 
<NarrowVertical>       ::= 'NarrowVertical'; 
<FilledDiagonals>      ::= 'FilledDiagonals';
<WideVertical>         ::= 'WideVertical'; 
<Speckled>             ::= 'Speckled'; 
<RandomDotted>         ::= 'RandomDotted'; 
<NarrowGrid>           ::= 'NarrowGrid'; 
<Bricks>               ::= 'Bricks'; 
<SparselyDotted>       ::= 'SparselyDotted'; 
<ShortDiagonals>       ::= 'ShortDiagonals'; 
<Lightbubbles>         ::= 'Lightbubbles'; 
<PieTop>               ::= 'PieTop'; 
<Buttercups>           ::= 'Buttercups'; 
<Waffles>              ::= 'Waffles'; 
<LightDotted>          ::= 'LightDotted'; 
<MediumDotted>         ::= 'MediumDotted'; 
<LightGray>            ::= 'LightGray'; 
<OrderedDotted>        ::= 'OrderedDotted'; 
<NarrowHorizontal>     ::= 'NarrowHorizontal'; 
<NarrowDiagonal>       ::= 'NarrowDiagonal'; 
<WideHorizontal>       ::= 'WideHorizontal'; 
<WideDiagonal>         ::= 'WideDiagonal';
<DottedBoxes>          ::= 'DottedBoxes'; 
<WideGrid>             ::= 'WideGrid'; 
<DiagonalBricks>       ::= 'DiagonalBricks'; 
<Holiday>              ::= 'Holiday'; 
<Carrots>              ::= 'Carrots'; 
<DiagonalScales>       ::= 'DiagonalScales'; 
<RoundScales>          ::= 'RoundScales'; 
<Mountains>            ::= 'Mountains'; 
<HeavyBubbles>         ::= 'HeavyBubbles'; 
<Diamonds>             ::= 'Diamonds';

(* Possible font names. *)
<fonts>     ::| <Chicago> 
              | <Courier> 
              | <Geneva> 
              | <Helvetica> 
              | <Monaco>
              | <Times> 
              | <Symbol>;

<Chicago>   ::= 'Chicago'; 
<Courier>   ::= 'Courier'; 
<Geneva>    ::= 'Geneva'; 
<Helvetica> ::= 'Helvetica'; 
<Monaco>    ::= 'Monaco';
<Times>     ::= 'Times'; 
<Symbol>    ::= 'Symbol';

(* The justification of text (left aligned, centered or right aligned. *)
<justs>     ::| <Left> 
              | <Centered> 
              | <Right>;

<Left>      ::= 'Left'; 
<Centered>  ::= 'Centered'; 
<Right>     ::= 'Right';

(* Possible porttypes: 
     In      = Input Port.
     Out     = Output Port.
     InOut   = Input/Output Port
     General = General Port
*)
<prttyps>   ::| <In> 
              | <Out>
              | <InOut> 
              | <General>;
<In>        ::= 'In'; 
<Out>       ::= 'Out';
<InOut>     ::= 'InOut'; 
<General>   ::= 'General';

(* Possible layering of object:
     Copy
     IOR = Inclusive OR
     XOR = Exclusive OR
     INV = Inverse the objects covered
*)
<laytyps>   ::| <Copy> 
              | <IOR> 
              | <XOR> 
              | <INV>;

<Copy>      ::= 'Copy'; 
<IOR>       ::= 'IOR'; 
<XOR>       ::= 'XOR'; 
<INV>       ::= 'INV';

(* Possible attributes of objects: 
     posattr = Position attributes
     fillattr = Fill Attributes
     lineattr = Line Attributes
     textattr = Text Attributes
     flags = Various flags (see definition of flags)
     layering = Layering logic of object
*)
<objatts>   ::| <posattr> 
              | <fillattr> 
              | <lineattr>
              | <textattr> 
              | <flags> 
              | <layering>;

<posattr>   ::= 'posattr'; 
<fillattr>  ::= 'fillattr'; 
<lineattr>  ::= 'lineattr';
<textattr>  ::= 'textattr'; 
<flags>     ::= 'flags'; 
<layering>  ::= 'layering';

(* Objects legal within a page:
   place = Place
     trans = Transition
     tempdecl = Temporary Declaration Node
     globdecl = Global Declaration Node
     locdecl = Local Declaration Node
     aux = Auxiliary Object
     aux-conn = Auxiliary Connector 
*)
<pageobs>   ::| <place> 
              | <trans> 
              | <tempdecl> 
              | <globdecl> 
              | <locdecl>
              | <arc> 
              | <aux> 
              | <aux_conn>;

<place>     ::= 'place'; 
<trans>     ::= 'trans'; 
<tempdecl>  ::= 'tempdecl'; 
<globdecl>  ::= 'globdecl'; 
<locdecl>   ::= 'locdecl';
<arc>       ::= 'arc'; 
<aux>       ::= 'aux'; 
<aux_conn>  ::= 'aux-conn';

(* Blocks legal within a place block:
   name = Name block.
     type = Type declaration block.
     initmark = Initial marking block.
     aux-endp = Auxiliary endpoint block
     aux-std = Auxiliary region block
     fus-key = Fusion key region block
     portkreg = Port key region block
     mark-key = Marking key region block 
*)
<placeob>   ::| <name> 
              | <type> 
              | <initmark> 
              | <aux_endp> 
              | <aux_std> 
              | <fus_key> 
              | <portkreg> 
              | <mark_key>;

<name>      ::= 'name';
<type>      ::= 'type'; 
<initmark>  ::= 'initmark'; 
<aux_endp>  ::= 'aux-endp'; 
<aux_std>   ::= 'aux-std'; 
<fus_key>   ::= 'fus-key'; 
<portkreg>  ::= 'portkreg'; 
<mark_key>  ::= 'mark-key';

(************************* ELEMENTS ****************************)


(* Elements recognised on outermost level of a cpn_document:
     Information about generator tool.
     One or more ordinary pages.
     Zero or one hierarchy page 
*)
<cpn>             ::= <cpn_stagOpt> <cpn_content> <cpn_etagOpt>;
<cpn_stagOpt>	  ::? <cpn_stag>;
<cpn_stag>	  ::= '<cpn>';
<cpn_content>	  ::* <cpn_content_gen>;
<cpn_content_gen> ::| <pages>
                    | <hierpage>
                    | <generator>;
<cpn_etagOpt>	  ::? <cpn_etag>;
<cpn_etag>	  ::= '</cpn>';


(* CDATA: Text blocks are recognised to contain Character Data *)
<text>            ::= '<text>' <string> '</text>';

(* Elements and attributes recognised within a page *)
<page>            ::= '<page' <att_idOpt> '>' <page_content> '</page>';
<page_content>    ::* <page_content_gen>;
<page_content_gen>::| <text>
                    | <page_contents>;
<page_contents>   ::* <page_content2>;
<page_content2>	  ::| <pageobs>
                    | <shape> 
		    | <pageattr> 
		    | <fillattr> 
		    | <lineattr> 
		    | <posattr> 
		    | <winattr> 
		    | <modeattr> 
		    | <mult>;
<pages>           ::* <page>;

<att_idOpt>	  ::? <att_id>;
<att_id>	  ::= 'id' '=' <id:const>;

(* Elements and attributes recognised within a place *)
<place>             ::= '<place' <att_idOpt> '>' <place_content> '</place>';
<place_content>	    ::* <place_content_gen>;
<place_content_gen> ::| <text>
                      | <place_contents>;
<place_contents>    ::* <place_content2>;
<place_content2>    ::| <placeob> | <shape> | <objatts> | <port>;

(* Elements and attributes recognised within a name block *)
<name>            ::= '<name' <att_idOpt> '>' <name_content> '</name>';
<name_content>	  ::* <name_content_gen>;
<name_content_gen>::| <text>
                    | <name_contents>;
<name_contents>   ::* <name_content2>;
<name_content2>	  ::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a type block *)
<type>	          ::= '<type' <att_idOpt> '>' <type_content> '</type>';
<type_content>	  ::* <type_content_gen>;
<type_content_gen>::| <text>
                    | <type_contents>;
<type_contents>   ::* <type_content2>;
<type_content2>	  ::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within an initial marking block *)
<initmark>             ::= '<initmark' <att_idOpt> '>' <initmark_content> '</initmark>';
<initmark_content>     ::* <initmark_content_gen>;
<initmark_content_gen> ::| <text>
                         | <initmark_contents>;
<initmark_contents>    ::* <initmark_content2>;
<initmark_content2>    ::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a transition block *)
<trans>                ::= '<trans' <att_idOpt> '>' <trans_content> '</trans>';
<trans_content>	       ::* <trans_content_gen>;
<trans_content_gen>    ::| <text>
                         | <trans_contents>;
<trans_contents>       ::* <trans_content2>;
<trans_content2>       ::| <transob> | <shape> | <objatts> | <modeattr> | <subst>;

(* Elements and attributes recognised within a condition block *)
<cond>                 ::= '<cond' <att_idOpt> '>' <cond_content> '</cond>';
<cond_content>	       ::* <cond_content_gen>;
<cond_content_gen>     ::| <text>
                         | <cond_contents>;
<cond_contents>	       ::* <cond_content2>;
<cond_content2>	       ::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a code key region block *)
<code_key>             ::= '<code-key' <att_idOpt> '>' <code_key_content> '</code-key>';
<code_key_content>     ::* <code_key_content_gen>;
<code_key_content_gen> ::| <text>
                         | <code_key_contents>;
<code_key_contents>    ::* <code_key_content2>;
<code_key_content2>    ::| <aux_std> | <code> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a code region block *)
<code>		       ::= '<code' <att_idOpt> '>' <code_content> '</code>';
<code_content>	       ::* <code_content_gen>;
<code_content_gen>     ::| <text>
		         | <code_contents>;
<code_contents>        ::* <code_content2>;
<code_content2>	       ::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a log key region block *)
<log_key>	       ::= '<log-key' <att_idOpt> '>' <log_key_content> '</log-key>';
<log_key_content>      ::* <log_key_content_gen>;
<log_key_content_gen>  ::| <text>
                         | <log_key_contents>;
<log_key_contents>     ::* <log_key_content2>;
<log_key_content2>     ::| <aux_std> | <log> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a log region block *)
<log>		  ::= '<log' <att_idOpt> '>' <log_content> '</log>';
<log_content>	  ::* <log_content_gen>;
<log_content_gen> ::| <text>
                    | <log_contents>;
<log_contents>    ::* <log_content2>;
<log_content2>	  ::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a time block *)
<time>         ::= '<time' <att_idOpt> '>' <time_content> '</time>';
<time_content> ::* <time_content_gen>;
<time_content_gen> ::| <text>
                     | <time_contents>;
<time_contents>::* <time_content2>;
<time_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a hierarchy key region block *)
<hier_key>         ::= '<hier-key' <att_idOpt> '>' <hier_key_content> '</hier-key>';
<hier_key_content> ::* <hier_key_content_gen>;
<hier_key_content_gen> ::| <text>
                         | <hier_key_contents>;
<hier_key_contents>::* <hier_key_content2>;
<hier_key_content2>::| <aux_std> | <hierarch> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a hierarchy region block *)
<hierarch>              ::= '<hierarch' <att_idOpt> '>' <hierarch_content> '</hierarch>';
<hierarch_content> ::* <hierarch_content_gen>;
<hierarch_content_gen> ::| <text>
                         | <hierarch_contents>;
<hierarch_contents>::* <hierarch_content2>;
<hierarch_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a fusion key region block *)
<fus_key>               ::= '<fus-key' <att_idOpt> '>' <fus_key_content> '</fus_key>';
<fus_key_content> ::* <fus_key_content_gen>;
<fus_key_content_gen> ::| <text>
                        | <fus_key_contents>;
<fus_key_contents>::* <fus_key_content2>;
<fus_key_content2>::| <aux_std> | <fusion> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a fusion region block *)
<fusion>                ::= '<fusion' <att_idOpt> '>' <fusion_content> '</fusion>';
<fusion_content> ::* <fusion_content_gen>;
<fusion_content_gen> ::| <text>
                       | <fusion_contents>;
<fusion_contents>::* <fusion_content2>;
<fusion_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;


(* Elements and attributes recognised within an arc block *)
<arc>           ::= '<arc' <arc_att_gen> '>' <arc_content> '</arc>';
<arc_att_gen>   ::* <arc_att>;
<arc_att>       ::| <att_id> | <att_orientation>;
<arc_content>   ::* <arc_content_gen>;
<arc_content_gen> ::| <text>
                    | <arc_contents>;
<arc_contents>::* <arc_content2>;
<arc_content2>::| <annot>
                | <aux_std>     
                | <shape> 
                | <lineattr> 
                | <placeend> 
                | <transend> 
                | <textattr> 
                | <flags>
                | <connattr>
                | <fillattr>
                | <layering>;

<att_orientation>       ::= 'orientation' '=' <arcors>;

(* Elements and attributes recognised within an arc annotation block *)
<annot>         ::= '<annot' <att_idOpt> '>' <annot_content> '</annot>';
<annot_content> ::* <annot_content_gen>;
<annot_content_gen> ::| <text>
                      | <annot_contents>;
<annot_contents>::* <annot_content2>;
<annot_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within an local declaration block *)
<locdecl>               ::= '<locdecl' <att_idOpt> '>' <locdecl_content> '</locdecl>';
<locdecl_content> ::* <locdecl_content_gen>;
<locdecl_content_gen> ::| <text>
                        | <locdecl_contents>;
<locdecl_contents>::* <locdecl_content2>;
<locdecl_content2>::| <aux_endp> | <aux_std> | <shape> | <objatts>;

(* Elements and attributes recognised within a temporary declaration
block *)
<tempdecl>              ::= '<tempdecl' <att_idOpt> '>' <tempdecl_content> '</tempdecl>';
<tempdecl_content> ::* <tempdecl_content_gen>;
<tempdecl_content_gen> ::| <text>
                         | <tempdecl_contents>;
<tempdecl_contents>::* <tempdecl_content2>;
<tempdecl_content2>::| <aux_endp> | <aux_std> | <shape> | <objatts>;

(* Elements and attributes recognised within a global declaration block *)
<globdecl>              ::= '<globdecl' <att_idOpt> '>' <globdecl_content> '</globdecl>';
<globdecl_content> ::* <globdecl_content_gen>;
<globdecl_content_gen> ::| <text>
                         | <globdecl_contents>;
<globdecl_contents>::* <globdecl_content2>;
<globdecl_content2>::| <aux_endp> | <aux_std> | <shape> | <objatts>;

(* Elements and attributes recognised within an auxiliary object block *)
<aux>           ::= '<aux' <att_idOpt> '>' <aux_content> '</aux>';
<aux_content> ::* <aux_content_gen>;
<aux_content_gen> ::| <text>
                    | <aux_contents>;
<aux_contents>::* <aux_content2>;
<aux_content2>::| <aux_endp> | <aux_std> | <shape> | <objatts>;

(* Elements and attributes recognised within an auxiliary connector block *)
<aux_conn>         ::= '<aux-conn' <aux_conn_att_gen> '>' <aux_conn_content> '</aux-conn>';
<aux_conn_att_gen> ::* <aux_conn_att>;
<aux_conn_att>     ::| <att_id> | <att_orient>;
<aux_conn_content> ::* <aux_conn_content_gen>;
<aux_conn_content_gen> ::| <text>
                         | <aux_conn_contents>;
<aux_conn_contents>::* <aux_conn_content2>;
<aux_conn_content2>::| <aux_std> 
                     | <shape> 
                     | <lineattr>
                     | <node1> 
                     | <node2> 
                     | <textattr> 
                     | <flags> 
                     | <connattr> 
                     | <fillattr> 
                     | <layering>;

<att_orient>    ::= 'orient' '=' <orients> ;

(* Elements and attributes recognised within an auxiliary standard region block *)
<aux_std>               ::= '<aux-std' <att_idOpt> '>' <aux_std_content> '</aux-std>';
<aux_std_content> ::* <aux_std_content_gen>;
<aux_std_content_gen> ::| <text>
                        | <aux_std_contents>;
<aux_std_contents>::* <aux_std_content2>;
<aux_std_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within an auxiliary endpoint region block *)
<aux_endp>              ::= '<aux-endp' <att_idOpt> '>' <aux_endp_content> '</aux-endp>';
<aux_endp_content> ::* <aux_endp_content_gen>;
<aux_endp_content_gen> ::| <text>
                         | <aux_endp_contents>;
<aux_endp_contents>::* <aux_endp_content2>;
<aux_endp_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a port key region block *)
<portkreg>              ::= '<portkreg' <att_idOpt> '>' <portkreg_content> '</portkreg>';
<portkreg_content> ::* <portkreg_content_gen>;
<portkreg_content_gen> ::| <text>
                         | <portkreg_contents>;
<portkreg_contents>::* <portkreg_content2>;
<portkreg_content2>::| <aux_std> | <portreg> | <shape> ;

(* Elements and attributes recognised within a port region block *)
<portreg>               ::= '<portreg' <att_idOpt> '>' <portreg_content> '</portreg>';
<portreg_content> ::* <portreg_content_gen>;
<portreg_content_gen> ::| <text>
                        | <portreg_contents>;
<portreg_contents>::* <portreg_content2>;
<portreg_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a marking key region block *)
<mark_key>              ::= '<mark-key' <att_idOpt> '>' <mark_key_content> '</mark-key>';
<mark_key_content> ::* <mark_key_content_gen>;
<mark_key_content_gen> ::| <text>
                         | <mark_key_contents>;
<mark_key_contents>::* <mark_key_content2>;
<mark_key_content2>::| <aux_std> | <mark> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a marking region block *)
<mark>          ::= '<mark' <att_idOpt> '>' <mark_content> '</mark>';
<mark_content> ::* <mark_content_gen>;
<mark_content_gen> ::| <text>
                     | <mark_contents>;
<mark_contents>::* <mark_content2>;
<mark_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within an auxiliary enabling region  block *)
<aux_enab>              ::= '<aux-enab' <att_idOpt> '>' <aux_enab_content> '</aux-enab>';
<aux_enab_content> ::* <aux_enab_content_gen>;
<aux_enab_content_gen> ::| <text>
                         | <aux_enab_contents>;
<aux_enab_contents>::* <aux_enab_content2>;
<aux_enab_content2>::| <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a hierarchy page block. *)
<hierpage>              ::= '<hierpage' <att_idOpt> '>' <hierpage_content> '</hierpage>';
<hierpage_content> ::* <hierpage_content_gen>;
<hierpage_content_gen> ::| <text>
                         | <hierpage_contents>;
<hierpage_contents>::* <hierpage_content2>;
<hierpage_content2>::| <aux> 
                     | <aux_conn> 
                     | <pagenode>
                     | <substcon>
                     | <shape> 
                     | <pageattr>
                     | <fillattr> 
                     | <lineattr> 
                     | <posattr> 
                     | <winattr>;

(* Elements and attributes recognised within an hierarchy page node block. *)
<pagenode>              ::= '<pagenode' <pagenode_att_gen> '>' <pagenode_content> '</pagenode>';
<pagenode_att_gen> ::* <pagenode_att>;
<pagenode_att>     ::| <att_id> | <att_pageref>;
<pagenode_content> ::* <pagenode_content_gen>;
<pagenode_content_gen> ::| <text>
                         | <pagenode_contents>;
<pagenode_contents>::* <pagenode_content2>;
<pagenode_content2>::| <aux_std> | <pgmodkey> | <shape> | <objatts>;

<att_pageref> ::= 'pageref' '=' <idref>;

<idref>       ::= <const>;

(* Elements and attributes recognised within a page mode key region block *)
<pgmodkey>              ::= '<pgmodkey' <att_idOpt> '>' <pgmodkey_content> '</pgmodkey>';
<pgmodkey_content> ::* <pgmodkey_content_gen>;
<pgmodkey_content_gen> ::| <text>
                         | <pgmodkey_contents>;
<pgmodkey_contents>::* <pgmodkey_content2>;
<pgmodkey_content2>::| <pagemode> | <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a page mode region block *)
<pagemode>              ::= '<pagemode' <att_idOpt> '>' <pagemode_content> '</pagemode>';
<pagemode_content> ::* <pagemode_content_gen>;
<pagemode_content_gen> ::| <text>
                         | <pagemode_contents>;
<pagemode_contents>::* <pagemode_content2>;
<pagemode_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

(* Elements and attributes recognised within a substitution connector block *)
<substcon>              ::= '<substcon' <substcon_att_gen> '>' <substcon_content> '</substcon>';
<substcon_att_gen> ::* <substcon_att>;
<substcon_att>     ::| <att_id> | <att_orient>;
<substcon_content> ::* <substcon_content_gen>;
<substcon_content_gen> ::| <text>
                         | <substcon_contents>;
<substcon_contents>::* <substcon_content2>;
<substcon_content2>::| <aux_std> 
                     | <substtag>
                     | <shape> 
                     | <lineattr> 
                     | <sup_page> 
                     | <sub_page> 
                     | <textattr> 
                     | <flags> 
                     | <connattr> 
                     | <fillattr> 
                     | <layering>;

(* Elements and attributes recognised within a substitution tag block *)
<substtag>              ::= '<substtag' <substtag_att_gen> '>' <substtag_content> '</substtag>';
<substtag_att_gen> ::* <substtag_att>;
<substtag_att>     ::| <att_id> | <att_transref>;
<substtag_content> ::* <substtag_content_gen>;
<substtag_content_gen> ::| <text>
                      | <substtag_contents>;
<substtag_contents>::* <substtag_content2>;
<substtag_content2>::| <aux_std> | <shape> | <objatts> | <regattr>;

<att_transref> ::= 'transref' '=' <idref:const>;

(* Name and version of tool which generated file *)
<generator>  ::= '<generator' <generator_att_gen> '>' <generator_etagOpt>;
<generator_att_gen> ::* <generator_att>;
<generator_att> ::| <att_tool> | <att_version>;
<generator_etagOpt> ::? <generator_etag>;
<generator_etag>    ::= '</generator>';

<att_tool>          ::= 'tool' '=' <string>;
<att_version>       ::= 'version' '=' <string>;

(* SBNF types used below *)

<coordinate>         ::= <minusOpt> <const>;
<dimension>          ::= <coordinate>;
<number>             ::= <const>;
<minusOpt>           ::? <minus>;
<minus>              ::= '-';
<att_xy_gen>         ::* <att_xy>;
<att_xy>             ::| <att_x> | <att_y>;
<att_x>              ::= 'x' '=' <coordinate>;
<att_y>              ::= 'y' '=' <coordinate>;
<att_hw_gen>         ::* <att_hw>;
<att_hw>             ::| <att_h> | <att_w>;
<att_h>              ::= 'h' '=' <dimension>;
<att_w>              ::= 'w' '=' <dimension>;


(* Position attributes: *)
(* x : x coordinate in mm
   y:  y coordinate in mm

The coordinate system looks like this:
      ^
      | y
      |
      |
      |      x
______________\
      |       /
      |
      |
      |
*)

<posattr>            ::= '<posattr' <att_xy_gen> '>' <posattr_etagOpt>;
<posattr_etagOpt>    ::? <posattr_etag>;
<posattr_etag>       ::= '</posattr>';

(* Definition of various shapes.

The attributes:
     h = height (in mm)
     w = width (in mm)

Decimal seperator is '.'.

     curv = curvature (0.0-360.0)
     stangle = start angle (0.0-360.0)
     endangle = end angle (0.0-360.0)
     points = sequence of points "(x1,y1)(x2,y2)...(xn,yn)"
*)

<points>         ::* <point>;
<point>          ::= '(' <coordinate> ',' <coordinate> ')' ;
<att_pointsOpt>  ::? <att_points>;
<att_points>     ::= 'points' '=' '"' <points> '"' ;

<box>            ::= '<box' <att_hw_gen> '>' <box_etagOpt>;
<box_etagOpt>    ::? <box_etag>;
<box_etag>       ::= '</box>';

<ellipse>        ::= '<ellipse' <att_hw_gen> '>' <ellipse_etagOpt>;
<ellipse_etagOpt>::? <ellipse_etag>;
<ellipse_etag>   ::= '</ellipse>';

<poly>           ::= '<poly' <att_points> '>' <poly_etagOpt>;
<poly_etagOpt>   ::? <poly_etag>;
<poly_etag>      ::= '</poly>';

<line>           ::= '<line' <att_points> '>' <line_etagOpt>;
<line_etagOpt>   ::? <line_etag>;
<line_etag>      ::= '</line>';

<label>          ::= '<label>' <label_etagOpt>;
<label_etagOpt>  ::? <label_etag>;
<label_etag>     ::= '</label>';

<wedge>          ::= '<wedge' <att_hwse_gen> '>' <wedge_etagOpt>;
<wedge_etagOpt>  ::? <wedge_etag>;
<wedge_etag>     ::= '</wedge>';
<att_hwse_gen>   ::* <att_hwse>;
<att_hwse>       ::| <att_hw> | <att_stangle> | <att_endangle>;
<att_stangle>    ::= 'stangle' '=' <number>;
<att_endangle>   ::= 'endangle' '=' <number>;

<rbox>           ::= '<rbox' <att_hw_gen> '>' <rbox_etagOpt>;
<rbox_etagOpt>   ::? <rbox_etag>;
<rbox_etag>      ::= '</rbox>';
<att_hwc_gen>    ::* <att_hwc>;
<att_hwc>        ::| <att_hw> | <att_curv>;
<att_curv>       ::= 'curv' '=' <number>;

<seg_conn>        ::= '<seg-conn' <att_cp_gen> '>' <seg_conn_etagOpt>;
<seg_conn_etagOpt>::? <seg_conn_etag>;
<seg_conn_etag>   ::= '</seg-conn>';
<att_cp_gen>      ::* <att_cp>;
<att_cp>          ::| <att_curv> | <att_points>;

(* Different attributes related to objects: *)
<fillattr>        ::= '<fillattr' <att_pc_gen> '>' <fillattr_etagOpt>;
<fillattr_etagOpt>::? <fillattr_etag>;
<fillattr_etag>   ::= '</fillattr>';
<att_pc_gen>      ::* <att_pc>;
<att_pc>          ::| <att_pattern> | <att_colour>;
<att_pattern>     ::= 'pattern' '=' <pats>;
<att_colour>      ::= 'colour' '=' <cols>;

(* Line attributes (type, thichness and colour). Thickness is measured in points *)
<lineattr>        ::= '<lineattr' <att_ttc_gen> '>' <lineattr_etagOpt>;
<lineattr_etagOpt>::? <lineattr_etag>;
<lineattr_etag>   ::= '</lineattr>';
<att_ttc_gen>     ::* <att_ttc>;
<att_ttc>         ::| <att_type> | <att_thick> | <att_colour>;
<att_type>        ::= 'type' '=' <lintyps>;
<att_thick>       ::= 'thick' '=' <number>;

<boolean>         ::| <true> | <false>;
<true>            ::= 'TRUE';
<false>           ::= 'FALSE';

(* Text attributes. *)
<textattr>        ::= '<textattr' <att_textattr_gen> '>' <textattr_etagOpt>;
<textattr_etagOpt>::? <textattr_etag>;
<textattr_etag>   ::= '</textattr>';
<att_textattr_gen>::* <att_textattr>;
<att_textattr>    ::| <att_font> 
                    | <att_just> 
                    | <att_colour> 
                    | <att_bold> 
                    | <att_italic> 
                    | <att_underlin> 
                    | <att_outline> 
                    | <att_shadow> 
                    | <att_condense> 
                    | <att_extend> 
                    | <att_scbar> 
                    | <att_size>;    

<att_font>        ::= 'font' '=' <fonts>;   
<att_just>        ::= 'just' '=' <justs>;  
<att_colour>      ::= 'colour' '=' <cols>;    
<att_bold>        ::= 'bold' '=' <boolean>;
<att_italic>      ::= 'italic' '=' <boolean>;
<att_underlin>    ::= 'underlin' '=' <boolean>;
<att_outline>     ::= 'outline' '=' <boolean>;
<att_shadow>      ::= 'shadow' '=' <boolean>;
<att_condense>    ::= 'condense' '=' <boolean>;
<att_extend>      ::= 'extend' '=' <boolean>;
<att_scbar>       ::= 'scbar' '=' <boolean>;
<att_size>        ::= 'size' '=' <number>;   

(* Object flags *)
(* All flags attributes might assume values TRUE or FALSE.
     mask - object pickable or not.
     omit - object showable or not
     nobound - object boundary visible or not
     visible - visible or not 
*)

<flags>          ::= '<flags' <att_flags_gen> '>' <flags_etagOpt>;
<flags_etagOpt>  ::? <flags_etag>;
<flags_etag>     ::= '</flags>';
<att_flags_gen>  ::* <att_flags>;
<att_flags>      ::| <att_mask> | <att_omit> | <att_nobound> | <att_nosizing> | <att_visible>;

<att_mask>       ::= 'mask' '='  <boolean>;
<att_omit>       ::= 'omit' '='  <boolean>;
<att_nobound>    ::= 'nobound' '='  <boolean>;
<att_nosizing>   ::= 'nosizing' '='  <boolean>;
<att_visible>    ::= 'visible' '='  <boolean>;

(* Place end of an arc.
     idref is a reference to the id of the place to connect to.
     endpoint is a reference to the object that the arc physically
     connects to.
*)
<placeend>         ::= '<placeend' <att_placeend_gen> '>' <placeend_etagOpt>;
<placeend_etagOpt> ::? <placeend_etag>;
<placeend_etag>    ::= '</placeend>';
<att_placeend_gen> ::* <att_placeend>;
<att_placeend>     ::| <att_idref> | <att_endpoint>;
<att_idref>        ::= 'idref' '=' <idref>;
<att_endpoint>     ::= 'endpoint' '=' <idref>;

(* Transition end of an arc. Same reasoning as for the placeend *)
<transend>         ::= '<transend' <att_placeend_gen_gen> '>' <transend_etagOpt>;
<transend_etagOpt> ::? <transend_etag>;
<transend_etag>    ::= '</transend>';

(* First object connected by a connector *)
<node1>          ::= '<node1' <att_idOpt> '>' <node1_etagOpt>;
<node1_etagOpt>  ::? <node1_etag>;
<node1_etag>     ::= '</node1>';

(* Second object connected by a connector *)
<node2>          ::= '<node2' <att_idOpt> '>' <node2_etagOpt>;
<node2_etagOpt>  ::? <node2_etag>;
<node2_etag>     ::= '</node2>';

(* Attributes for a page *)
(* name - name of page.
     number - the number of the page (this is NOT the id of the page)
     visbor - borders visible (TRUE/FALSE)
*)
<pageattr>        ::= '<pageattr' <att_pageattr_gen> '>' <pageattr_etagOpt>;
<pageattr_etagOpt>::? <pageattr_etag>;
<pageattr_etag>   ::= '</pageattr>';
<att_pageattr_gen>::* <att_pageattr>;
<att_pageattr>    ::| <att_name> | <att_number> | <att_visbor>;
<att_name>        ::= 'name' '=' <boolean>;
<att_number>      ::= 'number' '=' <number>;
<att_visbor>      ::= 'visbor' '=' <boolean>;

(* Region attributes: *)
(* colorap - Colour as parent
     sizewp - resize with parent
     hoffset - horisontal offset (in mm)
     voffset - vertical offset (in mm)
*)
<regattr>	  ::= '<regattr' <att_regattr_gen> '>' <regattr_etagOpt>;
<regattr_etagOpt> ::? <regattr_etag>;
<regattr_etag>	  ::= '</regattr>';
<att_regattr_gen> ::* <att_regattr>;
<att_regattr>	  ::| <att_colorap> | <att_sizewp> | <att_hoffset> | <att_voffset>;
<att_colorap>     ::= 'colorap' '=' <boolean>;
<att_sizewp>	  ::= 'sizewp' '=' <boolean>;
<att_hoffset>	  ::= 'hoffset' '=' <dimension>;
<att_voffset>	  ::= 'voffset' '=' <dimension>;

(* Connector attributes *)
(* hdwidth - Arrow head width (in points)
     hdheight - Arrow head height (in points)
     txtwidth - Text width (in points)
     txtheight - Text height (in points)
*)
<conattr>	  ::= '<conattr' <att_conattr_gen> '>' <conattr_etagOpt>;
<conattr_etagOpt> ::? <conattr_etag>;
<conattr_etag>	  ::= '</conattr>';
<att_conattr_gen> ::* <att_conattr>;
<att_conattr>     ::| <att_hdwidth> | <att_hdheight> | <att_txtwidth> | <att_txtheight>;
<att_hdwidth>     ::= 'hdwidth' '=' <dimension>;
<att_hdheight>    ::= 'hdheight' '=' <dimension>;
<att_txtwidth>    ::= 'txtwidth' '=' <dimension>;
<att_txtheight>   ::= 'txtheight' '=' <dimension>;

(* Window attributes *)
(* open - open on load (TRUE/FALSE)
     width - Width (in pixels).
     height - Height (in pixels) *)
<winattr>	  ::= '<winattr' <att_winattr_gen> '>' <winattr_etagOpt>;
<winattr_etagOpt> ::? <winattr_etag>;
<winattr_etag>	  ::= '</winattr>';
<att_winattr_gen> ::* <att_winattr>;
<att_winattr>     ::| <att_open> | <att_width> | <att_height> | <att_xpos> | <att_ypos>;
<att_open>	  ::= 'open' '=' <boolean>;
<att_width>	  ::= 'width' '=' <number>;
<att_height>	  ::= 'height' '=' <number>;
<att_xpos>	  ::= 'xpos' '=' <number>;
<att_ypos>	  ::= 'ypos' '=' <number>;

(* Mode attributes *)
(* Determines some simulation code options. *)
<modeattr>	  ::= '<modeattr' <att_modeattr_gen> '>' <modeattr_etagOpt>;
<modeattr_etagOpt>::? <modeattr_etag>;
<modeattr_etag>	  ::= '</modeattr>';
<att_modeattr_gen>::* <att_modeattr>;
<att_modeattr>	  ::| <att_siminc> | <att_observe> | <att_propocc> | <att_interact> | <att_execcode>;
<att_siminc>	  ::= 'siminc' '=' <string>;
<att_observe>	  ::= 'observe' '=' <string>;
<att_propocc>	  ::= 'propocc' '=' <string>;
<att_interact>	  ::= 'interact' '=' <string>;
<att_execcode>	  ::= 'execcode' '=' <string>;

(* For substitution transitions *)
(* subpage - The ID of the corresponding subpage
     portsock - List of port/socket assingments on the form:
       "(port1ID, socket1ID)(port2ID, socket2ID)...(portNID,
       socketID)"
*)
<subst>	           ::= '<subst' <att_subst_gen> '>' <subst_etagOpt>;
<subst_etagOpt>    ::? <subst_etag>;
<subst_etag>	   ::= '</subst>';
<att_subst_gen>	   ::* <att_subst>;
<att_subst>	   ::| <att_subpage> | <att_portsock>;
<att_subpage>	   ::= 'subpage' '=' <idref>;
<att_portsock>	   ::= 'portsock' '=' <portsocks>;
<portsocks>	   ::* <portsock>;
<portsock>	   ::= '(' <port:string> ',' <socket:string> ')';

(* Port type *)
<port>	           ::= '<port' <att_porttypeOpt> '>' <port_etagOpt>;
<port_etagOpt>	   ::? <port_etag>;
<port_etag>	   ::= '</port>';
<att_porttypeOpt>  ::? <att_porttype>;
<att_porttype>	   ::= 'type' '=' <prttyps>;

(* Superpage end of substitution connectors *)
<sup_page>	   ::= '<sup-page' <att_sub_pageOpt> '>' <sup_page_etagOpt>;
<sup_page_etagOpt> ::? <sup_page_etag>;
<sup_page_etag>	   ::= '</sup-page>';
<att_sub_pageOpt>  ::? <att_sub_page>;
<att_sub_page>	   ::= 'idref' '=' <idref>;

(* Subpage end of substitution connectors *)
<sub_page>	   ::= '<sub-page' <att_sub_pageOpt> '>' <sub_page_etagOpt>;
<sub_page_etagOpt> ::? <sub_page_etag>;
<sub_page_etag>	   ::= '</sub-page>';
<att_sub_pageOpt>  ::? <att_sub_page>;
<att_sub_page>	   ::= 'idref' '=' <idref>;

(* Multiplicity of page *)
(* insts - number of instances *)
<mult>	          ::= '<mult' <att_instOpt> '>' <mult_etagOpt>;
<mult_etagOpt>    ::? <mult_etag>;
<mult_etag>	  ::= '</mult>';
<att_instOpt>	  ::? <att_insts>;
<att_insts>	  ::= 'insts' '=' <number>;

<layering>	  ::= '<layering' <att_laytypeOpt> '>' <layering_etagOpt>;
<layering_etagOpt>::? <layering_etag>;
<layering_etag>	  ::= '</layering>';
<att_laytypeOpt>  ::? <att_laytype>;
<att_laytype>     ::= 'type' '=' <laytyps>;

Attribute

