EOLasComEnd; 
--- idl: aGrammar: metagrammar ---
Grammar idl:

Option
  version = 4
  comBegin = '//'
  comEnd = '*/'
  stringChar = '"'
  suffix = '.idl'
  EOSchar = '%'

  nameDecl = unused


Rule
 
<specification> ::=     <definitions> ;
                       
<definitions> ::+ <definition>  ; (* ; separator originally *)

<definition>  ::|     <def_type_dcl> 
                |     <const_dcl> 
                |     <except_dcl> 
                |     <interface> 
                |     <module> 
		|     <definition_error>
                ;
<def_type_dcl> 	::=	<type_dcl>  ';'
		;
<module>        ::=     'module' <identifier> '{' <definitions> '}'
		;
<interface>     ::|    <interface_dcl>
                |      <forward_dcl>
		;
<interface_dcl> ::=     <interface_header> '{' <interface_body> '}'
		;
<forward_dcl>   ::=     'interface' <identifier> ';' (* ';' added *)
		;
<interface_header>      ::=     '[' <interface_attributes> ']'
				'interface' <identifier>
                                <inheritance_specOpt>
			;
<inheritance_specOpt> ::? <inheritance_spec> 
			;
<interface_body>        ::=     <exports>
			;
<exports> ::+ <export> 
	  ;
<export>        ::|     <export_type_dcl> 
                |       <export_const_dcl> 
                |       <export_except_dcl> 
                |       <export_attr_dcl> 
                |       <export_op_dcl> 
		|	<cpp_quote>
		|	<export_error>
		;
(* cpp_quote is a COM hack - probably not correct *)
<cpp_quote>	::= 'cpp_quote' '(' <string> ')'
		;
<export_type_dcl>	::= <type_dcl> ';'
			;
<export_const_dcl> 	::= <const_dcl> ';'
			;	
<export_except_dcl> 	::= <except_dcl> ';'
		;
<export_attr_dcl> 	::= <attr_dcl> ';'
			;
<export_op_dcl> 	::= <op_dcl> ';'
			;
<inheritance_spec>      ::=     ':' <scoped_names> 
			;
<scoped_names>	::* <scoped_name> ',' 
		;
<scoped_name>   ::| <scoped_name1> | <scoped_name2> | <scoped_name3> 
		;
<scoped_name1> ::=     	<identifier>
		;
<scoped_name2> ::=      '::' <identifier>
		;
<scoped_name3>  ::= 	<scoped_name> '::' <identifier>
		;
<const_dcl>     ::=     'const' <const_type> <identifier> '='<const_exp>
		;
<const_type>    ::|     <const_integer_type>
                |       <const_char_type>
                |       <const_boolean_type>
                |       <const_floating_pt_type>
                |       <const_string_type>
                |       <const_scoped_name>
		;
<const_integer_type>	::= <integer_type>
			;
<const_char_type>	::= <char_type>
			;
<const_boolean_type>	::= <boolean_type>
			;
<const_floating_pt_type>	::= <floating_pt_type>
			;
<const_string_type>	::= <string_type>
		;
<const_scoped_name>	::= <scoped_name>
		;
<const_exp>     ::=     <or_expr>
		;
<or_expr>       ::+     <xor_expr> '|' ;

<xor_expr>      ::+     <and_expr> '^' ;

<and_expr>      ::+     <shift_expr>  '&'  ;

<shift_expr>    ::+     <add_exprA> 
		;
<add_exprA> 	::| <shift> | <add_expr> ;

<shift> 	::| <shiftRight> | <shiftLeft> ;

<shiftRight>	::= <shift_expr> '>>' ;
<shiftLeft> 	::= <shift_expr> '<' '<' ;

<add_expr>      ::| <mult_expr> | <add> | <sub> ;

<add>		::= <add_expr> '+' <mult_expr> ;

<sub>		::= <add_expr> '-' <mult_expr> ;

<mult_expr>     ::| <unary_expr> | <mult> | <idiv> | <imod> ;

<mult>		::= <mult_expr> '*' <unary_expr> 
		;
<idiv>		::= <mult_expr> '/' <unary_expr> 
		;
<imod> 		::= <mult_expr> '%' <unary_expr> 
		;
<unary_expr>    ::|    <unary> 
                |       <primary_expr>
		;
<unary>		::= <unary_operator> <primary_expr>
		;
<unary_operator>        ::|     <unaryMinus> | <unaryPlus> | <negation>
			;
<unaryMinus>	::= '-'
		;
<unaryPlus>	::= '+'
		;
<negation>	::= '~'
		;
<primary_expr>  ::|     <scoped_nameA>
                |       <literal>
                |       <bracketed_expr>
		|	<simple_function>
		;
<scoped_nameA>	::= <scoped_name>
		;
<bracketed_expr>	::= '(' <const_exp> ')'
			;
<literal>       ::|     <number_literal>
                |       <string_literal>
                |       <boolean_literal>
		;
<boolean_literal>       ::| <true> | <false>
			;
<true> 	::=   'TRUE'
	;
<false>	::=  'FALSE'
	;
<positive_int_const>    ::|     <const_exp>
		;
<type_dcl>      ::|     <typedef> 
                |       <struct_type>
                |       <union_type>
                |       <enum_type>
		|	<type_dcl_error>
		;
<typedef> 	::= 'typedef' <attribute_opt> <type_declarator>
		;
(* <attribute_opt> added by OLM *)
<type_declarator>       ::=     <type_spec> <declarators>
		;
<type_spec>     ::|     <simple_type_spec>
                |       <constr_type_spec>
		|	<void_type_spec>
		;
<simple_type_spec>      ::|     <base_type_spec>
                	|       <template_type_spec>
                	|       <scoped_name>
			;
<base_type_spec>        ::|     <floating_pt_type>
                |       <integer_type>
                |       <char_type>
                |       <boolean_type>
                |       <octet_type>
                |       <any_type>
		;
<template_type_spec>    ::|     <sequence_type>
                	|       <string_type>
			;
<constr_type_spec>      ::|     <constr_struct_type>
                	|       <constr_union_type>
                	|       <constr_enum_type>
			;
<constr_struct_type>	::= <struct_type>
			;
<constr_union_type>	::= <union_type>
			;
<constr_enum_type>	::= <enum_type>
			;	
<declarators>   ::*     <declarator> ','  (* originally a + *)
		;
<declarator>    ::|     <simple_declarator>
                |       <complex_declarator>
		| 	<pointer_declarator>
		|	<pointer_to_pointer_declarator>
		|	<pointer_to_array_declarator>
		;
<simple_declarator>     ::=     <identifier>
			;
<complex_declarator>    ::|     <array_declarator>
		;
<floating_pt_type>      ::| <float> | <double>
			;
<float>	::=     'float'
	;
<double>	::=   'double'
		;
<integer_type>  ::|     <signed_int>
                |       <unsigned_int>
		;
<signed_int>    ::|    <signed_long_int>
                |       <signed_short_int>
		;
<signed_long_int>       ::=     'long'
			;
<signed_short_int>      ::=     'short'
			;
<unsigned_int>  ::|     <unsigned_long_int>
                |       <unsigned_short_int>
		;
<unsigned_long_int>    	::=     'unsigned' 'long'
			;
<unsigned_short_int>    ::=     'unsigned' 'short'
			;
<char_type>     ::=     'char'
		;
<boolean_type>  ::=     'boolean'
		;
<octet_type>    ::=     'octet'
		;
<any_type>      ::=     'any'
		;
<struct_type>   ::=     'struct' <identifier>

                                 '{' <member_list> '}'
		;
<member_list>   ::=     <members>
		;
<member>        ::=     <attribute_opt> <type_spec> <declarators> ';'
		;
<union_type>    ::=     'union' <union_spec_opt> '{' <switch_body> '}'
		;
<switch_type_spec>      ::|     <switch_integer_type>
                	|       <switch_char_type>
                	|       <switch_boolean_type>
                	|       <switch_enum_type>
                	|       <switch_scoped_name>
			;
<switch_integer_type>	::= <integer_type>
			;
<switch_char_type>	::= <char_type>
		;
<switch_boolean_type>	::= <boolean_type>
			;
<switch_enum_type>	::= <enum_type>
		;
<switch_scoped_name>	::= <scoped_name>
		;
<switch_body>   ::+     <case>
		;
<case>  ::=     <case_labels> <element_spec_opt> ';'
	;
<case_labels>	::+ <case_label>
		;
<case_label>    ::| <case_const_label> | <default> | <case_att> | <default2>
		;
<case_const_label> 	::= 'case' <const_exp> ':'
			;
<default>	::=       'default' ':'
		;
<element_spec>  ::=     <type_spec> <declarator>
		;
<enum_type>     ::=     'enum' <identifier> '{' <enumerators> <sep_opt> '}' 
		;
<enumerators> 	::+ <enumerator> ','
		;
<enumerator>    ::=     <identifier> <value_opt>
		;
<sequence_type> ::|     <sequence1> | <sequence2> 
		;
<sequence1>	::= 'sequence' '<' <simple_type_spec>
                                ',' <positive_int_const> '>'
		;
<sequence2>	::= 'sequence' '<' <simple_type_spec> '>'
		;
<string_type>   ::|     <string1> | <string2>
		;
<string1>	::= 'string' '<' <positive_int_const> '>'
		;
<string2>	::= 'string'
		;
<array_declarator>      ::=     <identifier> <fixed_array_sizes>
			;
<fixed_array_sizes> 	::+ <fixed_array_size> 
			;
<fixed_array_size>      ::=     '[' <positive_int_const_opt> ']'
			;
<attr_dcl>      ::=     <readonlyOpt> 'attribute'
                                <param_type_spec>
                                <simple_declarators>
		;
<simple_declarators>	::+ <simple_declarator> ','
			;
<readOnlyOpt>	::? <readOnly>
		;
<readOnly>	::= 'readonly'
		;
<except_dcl>   	::= 'exception' <identifier> '{' <members> '}'
		;

<members> 	::* <member>
		;
<op_dcl>        ::= <op_attributeOpt> <op_type_spec> <std_call_opt>
                                <identifier>
                                <parameter_dcls>
                                <raises_exprOpt> <context_exprOpt>
		;
<op_attributeOpt> 	::? <op_attribute> 
			;
<op_attribute>  ::|  	<op_attribute_param_attribute>
		|	<oneway>
		;
<op_attribute_param_attribute>	::=	<param_attribute>
				;
<oneway>	::=      'oneway'
		;
<raises_exprOpt>	::? <raises_expr>  
			;
<context_exprOpt> 	::? <context_expr> 
			;
<op_type_spec>  ::|     <param_type_spec>
                |       <void>
		|	<param_type_spec_ref>
		|	<void_ref>
		;
<void> 	::= 'void'
	;
<parameter_dcls>        ::=     '(' <param_dcls> ')'
			;
<param_dcls> 	::* <param_dcl> ',' 
		;
<param_dcl>     ::= 	<attributes: param_attribute_opt> 
			<const1: const_opt> 
			<type: param_type_spec> 
			<const2: const_opt> 
			<declarator: declarator_opt>
			(* used to be <simple_declarator> *)
		;
<param_attribute_opt>	::?	<param_attribute>
			;
<declarator_opt>	::?	<declarator>
			;
<param_attribute>       ::| <in> | <out> | <in_out> | <param_attribute_list>
	;
<in>	::=     'in'
	;
<out>	::=     'out'
	;
<in_out>	::= 'inout'
	;
<raises_expr>   ::=     'raises' '(' <scoped_names> ')'
		;

<context_expr>  	::=     'context' '(' <string_literals> ')' 
			;
<string_literals>	::+ <string_literal> ',' ;

<param_type_spec>	::|     <param_base_type_spec> 
			|	<param_string_type>
                        |	<param_scoped_name>
			|	<param_void>
			;
<param_base_type_spec> 	::=	<base_type_spec> 
			;
<param_string_type>	::=	<string_type>
			;
<param_scoped_name>	::=	<scoped_name>
			;
<identifier>	::= <nameAppl>
		;
<number_literal> 	::= <const>
			;
<string_literal>	::= <string>
			
			;

(****************************additional COM stuff ****************************)
<interface_attributes> ::+ <interface_attribute> ',' ;
<interface_attribute> ::| <uuid>
			| <iversion>
			| <endpoint>
			| <local>
			| <pointer_default>
			| <object>
			;
<uuid>	::= 	'uuid' '(' <uuid_rep> ')'
	;
<uuid_rep>	::*  <keys> '-' 
		;
<keys>	::+ <key>
	;
<key>	::| <constKey> | <nameKey>
	;
<constKey>	::= <const>
		;
<nameKey>	::= <nameAppl>
		;
<iversion>	::= 	'version'
		;
<endpoint>	::=	'endpoint' '(' ')'
		;
<local>	::=	'local'
	;
<pointer_default>	::=	'pointer_default' '(' <ptr_attr> ')'
			;
<ptr_attr>	::|	<ref> | <ptr> | <iid_is> | <unique>
		;
<ref>	::=	'ref'
	;
<ptr>	::= 'ptr'
	;
<iid_is>	::= 'iid_is' '(' <identifier> ')' (* can be more than identifier *)
		;
<unique> ::= 'unique'
	 ;
<object>	::= 'object'
		;
<attribute_opt>	::? <attributeX> 
		;
<attributeX>	::= '[' <attributes> ']'
		;
<attributes> 	::+ <attributeY> ','
		;
<attributeY>	::| 	<att_unique>
		|	<att_ref>
		|	<att_in>
		|	<att_out>
		|	<att_in_out>
		|	<att_local>
		|	<call_as>
		|	<att_iid_is>
		|	<size_is>
		|	<length_is>
		|	<v1_enum>
		|	<transmit_as>
		|	<att_string>
		|	<switch_type>
		|	<switch_is>
		|	<att_case>
		|	<async>
		;
		
<att_unique>	::= <unique>
		;
<att_ref>	::=	<ref>
		;
<att_in>	::=	<in>
		;
<att_out>	::=	<out>
		;
<att_in_out>	::=	<in_out>
		;
<att_local>	::=	<local>
	;
<call_as>	::=	'call_as' '(' <identifier> ')'
		;
<param_attribute_list> 	::= <attributeX>
			;
<param_void>	::= <void>
			;
<pointer_declarator>	::= '*' <identifier>
			;
<pointer_to_pointer_declarator>	::= '*' '*' <identifier>
			;
<std_call_opt>	::?	<std_call>
		;
<std_call>	::=	'__stdcall'
		;
<att_iid_is>	::=	<iid_is>
		;
<size_is>	::=	'size_is' '(' <const_exp> ')'
		;
<length_is>	::=	'length_is' '(' <declarator> ')'
		;
<v1_enum>	::=	'v1_enum'
		;
<transmit_as>	::=	'transmit_as' '(' <identifier> ')'
		;
<att_string>	::=	'string'
		;
<switch_type>	::=	'switch_type' '(' <identifier> ')'
		;
<switch_is>	::=	'switch_is' '(' '(' <id1:identifier> ')' <id2:identifier> ')'
		;
<att_case>	::=	'case' '(' <identifier> ')'
	;
<async>	::=	'async'
	;
<void_ref>	::=	'void' '*'
		;
<value_opt>	::?	<value>
		;
<value>	::=	'=' <const_exp>
	;
<sep_opt>	::? 	<sep>
		;
<sep>	::= ','
	;
<const_opt> 	::? <const_key>
		;
<const_key>	::= 'const'
		;
<positive_int_const_opt> ::? <positive_int_const>
			 ;
<simple_function>	::= <identifier> '(' <const_exp> ')'
			;
<union_spec_opt> 	::?	<union_spec>
			;
<union_spec>	::=	<identifier> 'switch' '(' <switch_type_spec> ')'
		;
<case_att>	::=	<attributeX>
		;
<default2>	::=	'[' 'default' ']' 
		;
<element_spec_opt> 	::?	<element_spec>
			;
<void_type_spec>	::=	'void'
			;
<param_type_spec_ref>	::=	<param_type_spec> '*'	
			;
<pointer_to_array_declarator>	::=	'*' <array_declarator>
				;
<definition_error>	::= error
			;
<export_error>	::=	error
		;
<type_dcl_error>	::= error
