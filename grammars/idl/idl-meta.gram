EOLasComEnd; 
--- idl: aGrammar: metagrammar ---
Grammar idl:

Option
  version = 7
  comBegin = '//'
  comEnd = '*/'
  stringChar = '"'
  suffix = '.idl'
  EOSchar = '%'

  nameDecl = unused


Rule
 
<specification> ::=     <definitions> ;
                       
<definitions> ::+ <definition> (* ; separator originally *)
	      ;
<definition>  ::|     <def_type_dcl> 
                |     <const_dcl> 
                |     <except_dcl> 
                |     <interface> 
                |     <dispinterface> 
                |     <library> 
                |     <coclass> 
                |     <module> 
		|     <import>
		|     <importlib>
		|     <def_cpp_quote>
		|     <definition_error>
                ;
<def_type_dcl> 	::=	<type_dcl>  ';'
		;
<def_cpp_quote> ::=     <cpp_quote>
		;
<module>        ::=     'module' <identifier> '{' <definitions> '}' (*<semicolon_Opt>*)
		;
<comma_opt>  ::? 	<comma>
		;
<comma>	::=	','
		;
<semicolon_opt>  ::? 	<semicolon>
		;
<semicolon>	::=	';'
		;
<minus_opt>  ::? 	<minus>
		;
<minus>	::=	'-'
		;
<import>   	::=     'import' <string> ';'
		;
<importlib>   	::=     'importlib' '(' <string> ')' ';'
		;
<library>	::=     <library_header> '{' <library_body> '}' ';'
			;
<library_header>::=     '[' <attributes> <comma_opt> ']'
			'library' <identifier>
			;
<library_body>  ::=     <definitions>
		;
<coclass>	::=     <coclass_header> '{' <coclass_body>  '}' <semicolon_opt>
		;
<coclass_header>::=     '[' <attributes> <comma_opt> ']'
			'coclass' <identifier>
			;
<coclass_body>  ::+	<coclass_interface_dcl>
		;
<coclass_interface_dcl> ::| <coclass_interface>
			|   <coclass_dispinterface>
			;
<coclass_interface> 	::= <attributes_spec_opt> 'interface' <identifier> ';'
			;	
<coclass_dispinterface> ::= <attributes_spec_opt> 'dispinterface' <identifier> ';'
			;
<attributes_spec_opt> 	::? <attributes_spec>
			;
<attributes_spec> 	::= '[' <attributes> ']'
			;
<attributes>	 	::+ <iattribute> ',' (* <attribute> reserved BOBS word *)
			;
<iattribute>  ::| <aggregatable>
		| <appobject>
		| <async>
		| <bindable>
		| <broadcast>
		| <call_as>
		| <callback>
		| <coclass_att>
		| <control>
		| <custom>
		| <default>
		| <defaultbind>
		| <defaultcollelem>
		| <defaultvalue>
		| <defaultvtbl>
		| <dispinterface_att>
		| <displaybind>
		| <dllname>
		| <dual>
		| <endpoint>
		| <entry>
		| <first_is>
		| <handle>
		| <helpcontext>
		| <helpfile>
		| <helpstring>
		| <helpstringdll>
		| <hidden>
		| <id>
		| <idempotent>
		| <ignore>
		| <iid_is>
		| <immediatebind>
		| <in>
		| <last_is>
		| <lcid>
		| <length_is>
		| <library_att>
		| <licensed>
		| <local>
		| <max_is>
		| <maybe>
		| <message>
		| <ms_union>
		| <nonbrowsable>
		| <noncreatable>
		| <nonextensible>
		| <object>
		| <odl>
		| <oleautomation>
		| <optional>
		| <out>
		| <pipe>
		| <pointer_default>
		| <propget>
		| <propput>
		| <propputref>
		| <ptr>
		| <public>
		| <readonly>
		| <ref>
		| <replaceable>
		| <requestedit>
		| <restricted>
		| <retval>
		| <size_is>
		| <source>
		| <istring> (* <string> is reserved BOBS word *)
		| <switch>
		| <switch_is>
		| <switch_type>
		| <transmit_as> 
		| <uidefault>
		| <ull_asynch>
		| <unique>
		| <usesgetlasterror>
		| <uuid>
		| <v1_enum>
		| <vararg>
		| <iversion> (* <version> reserved BOBS word *)
		| <wire_marshal>
		;



<aggregatable>	::= 'aggregatable'
		;
<appobject>	::= 'appobject'
		;
<async>		::= 'async'
		;
<bindable>	::= 'bindable'
		;
<broadcast>	::= 'broadcast'
		;
<call_as>	::= 'call_as' '(' <identifier> ')'
		;
<callback>	::= 'callback'
		;
<coclass_att>	::= 'coclass'
		;
<control>	::= 'control'
		;
<custom>	::= 'custom'
		;
<default>	::= 'default'
		;
<defaultbind>	::= 'defaultbind'
		;
<defaultcollelem>::= 'defaultcollelem'
		;
<defaultvalue>	::= 'defaultvalue'
		;
<defaultvtbl>	::= 'defaultvtbl'
		;
<dispinterface_att>::= 'dispinterface'
		;
<displaybind>	::= 'displaybind'
		;
<dllname>	::= 'dllname'
		;
<dual>		::= 'dual'
		;
<endpoint>	::= 'endpoint' '(' ')'
		;
<entry>		::= 'entry'
		;
<first_is>	::= 'first_is'
		;
<handle>	::= 'handle'
		;
<helpcontext>	::= 'helpcontext' '(' <const> ')' 
		;
<helpfile>	::= 'helpfile'
		;
<helpstring>	::= 'helpstring' '(' <string> ')' 
		;
<helpstringdll>	::= 'helpstringdll'
		;
<hidden>	::= 'hidden'
		;
<id>		::= 'id' '(' <minus_opt> <id_value> ')'
		;
<id_value>	::| <id_const> | <id_identifier>
		;
<id_const>	::= <const>
		;
<id_identifier>	::= <identifier>
		;
<idempotent>	::= 'idempotent'
		;
<iid_is>	::= 'iid_is' '(' <identifier> ')' (* can be more than identifier *)
		;
<ignore>	::= 'ignore'
		;
<immediatebind>	::= 'immediatebind'
		;
<in>		::= 'in'
		;
<last_is>	::= 'last_is'
		;
<lcid>		::= 'lcid' '(' <const> ')'
		;
<length_is>	::= 'length_is' '(' <declarator> ')'
		;
<library_att>	::= 'library'
		;
<licensed>	::= 'licensed'
		;
<local>		::= 'local'
		;
<max_is>	::= 'max_is'
		;
<maybe>		::= 'maybe'
		;
<message>	::= 'message'
		;
<ms_union>	::= 'ms_union'
		;
<nonbrowsable>	::= 'nonbrowsable'
		;
<noncreatable>	::= 'noncreatable'
		;
<nonextensible>	::= 'nonextensible'
		;
<object>	::= 'object'
		;
<odl>		::= 'odl'
		;
<oleautomation>	::= 'oleautomation'
		;
<optional>	::= 'optional'
		;
<out>		::= 'out'
		;
<pipe>		::= 'pipe'
		;
<pointer_default>::= 'pointer_default' '(' <ptr_attr> ')'
		;
<ptr_attr>	::| <ptr_ref> | <ptr_ptr> | <ptr_iid_is> | <ptr_unique>
		;
<ptr_ref>	::= <ref>
		;
<ptr_ptr>	::= <ptr>
		;
<ptr_iid_is>	::= <iid_is>
		;
<ptr_unique>	::= <unique>
		;
<propget>	::= 'propget'
		;
<propput>	::= 'propput'
		;
<propputref>	::= 'propputref'
		;
<ptr>		::= 'ptr'
		;
<public>	::= 'public'
		;
<readonly>	::= 'readonly'
		;
<ref>		::= 'ref'
		;
<replaceable>	::= 'replaceable'
		;
<requestedit>	::= 'requestedit'
		;
<restricted>	::= 'restricted'
		;
<retval>	::= 'retval'
		;
<size_is>	::= 'size_is' '(' <const_exp> ')'
		;
<source>	::= 'source'
		;
<istring>	::= 'string'
		;
<switch>	::= 'switch'
		;
<switch_type>	::= 'switch_type' '(' <identifier> ')'
		;
<switch_is>	::= 'switch_is' '(' '(' <id1:identifier> ')' <id2:identifier> ')'
		;
<transmit_as>	::= 'transmit_as' '(' <identifier> ')'
		;
<uidefault>	::= 'uidefault'
		;
<ull_asynch>	::= 'ull_asynch'
		;
<unique>	::= 'unique'
		;
<usesgetlasterror>::= 'usesgetlasterror'
		;
<uuid>		::= 'uuid' '(' <uuid_rep> ')'
		;
<uuid_rep>	::* <keys> '-' 
		;
<keys>		::+ <key>
		;
<key>		::| <constKey> | <nameKey>
		;
<constKey>	::= <const>
		;
<nameKey>	::= <nameAppl>
		;
<v1_enum>	::= 'v1_enum'
		;
<vararg>	::= 'vararg'
		;
<iversion>	::= 'version' '(' <major_version> <minor_version_opt> ')'
		;
<major_version> ::= <const>
		;
<minor_version_opt> ::? <minor_version>
		    ;
<minor_version> ::= '.' <const>
		;
<wire_marshal>	::= 'wire_marshal'
		;

<interface>	      ::|    <interface_dcl>
		      |      <forward_dcl>
		      ;
<dispinterface>	      ::|    <dispinterface_dcl>
		      |      <dispforward_dcl>
		      ;
<interface_dcl> ::=     <interface_header> '{' <interface_body> '}'
			<semicolon_opt> (* datpete: added semicolon_opt *)
		;
<dispinterface_dcl> ::= <dispinterface_header> '{' <interface_body> '}'
			<semicolon_opt> (* datpete: added semicolon_opt *)
			;
<forward_dcl>		::=     'interface' <identifier> ';' (* ';' added *)
			;
<dispforward_dcl>	::=     'dispinterface' <identifier> ';' (* ';' added *)
			;
<interface_header>      ::=     '[' <attributes> <comma_opt> ']'
				'interface' <identifier>
                                <inheritance_specOpt>
			;
<dispinterface_header>  ::=     '[' <attributes> <comma_opt> ']'
				'dispinterface' <identifier>
			;
<inheritance_specOpt> ::? <inheritance_spec> 
			;
<interface_body>        ::=     <exports>
			;
<exports> ::+ <export> 
	  ;
<export>        ::|     <export_type_dcl> 
                |       <export_const_dcl> 
                |       <export_except_dcl> 
                |       <export_attr_dcl> 
                |       <export_op_dcl> 
		|	<cpp_quote>
		|	<properties> (* Only for dispinterfaces *)
		|	<methods>    (* Only for dispinterfaces *)
		|	<export_import>
		|	<export_error>
		;
(* cpp_quote is a COM hack - probably not correct *)
<cpp_quote>	::= 'cpp_quote' '(' <string> ')'
		;
<properties>	::= 'properties' ':'(* Not yet complete *)
		;
<methods>	::= 'methods' ':' (* Not yet complete *)
		;
<export_import>	::= <import>
		;
<export_type_dcl>	::= <type_dcl> ';'
			;
<export_const_dcl> 	::= <const_dcl> ';'
			;	
<export_except_dcl> 	::= <except_dcl> ';'
		;
<export_attr_dcl> 	::= <attr_dcl> ';'
			;
<export_op_dcl> 	::= <op_dcl> ';'
			;
<inheritance_spec>      ::=     ':' <scoped_names> 
			;
<scoped_names>	::* <scoped_name> ',' 
		;
<scoped_name>   ::| <scoped_name1> | <scoped_name2> | <scoped_name3> 
		;
<scoped_name1> ::=     	<identifier>
		;
<scoped_name2> ::=      '::' <identifier>
		;
<scoped_name3>  ::= 	<scoped_name> '::' <identifier>
		;
<const_dcl>     ::=     'const' <const_type> <identifier> '='<const_exp>
		;
<const_type>    ::|     <const_integer_type>
                |       <const_char_type>
                |       <const_boolean_type>
                |       <const_floating_pt_type>
                |       <const_string_type>
                |       <const_scoped_name>
		;
<const_integer_type>	::= <integer_type>
			;
<const_char_type>	::= <char_type>
			;
<const_boolean_type>	::= <boolean_type>
			;
<const_floating_pt_type>	::= <floating_pt_type>
			;
<const_string_type>	::= <string_type>
		;
<const_scoped_name>	::= <scoped_name>
		;
<const_exp>     ::=     <or_expr>
		;
<or_expr>       ::+     <xor_expr> '|' ;

<xor_expr>      ::+     <and_expr> '^' ;

<and_expr>      ::+     <shift_expr>  '&'  ;

<shift_expr>    ::+     <add_exprA> 
		;
<add_exprA> 	::| <shift> | <add_expr> ;

<shift> 	::| <shiftRight> | <shiftLeft> ;

<shiftRight>	::= <shift_expr> '>>' ;
<shiftLeft> 	::= <shift_expr> '<' '<' ;

<add_expr>      ::| <mult_expr> | <add> | <sub> ;

<add>		::= <add_expr> '+' <mult_expr> ;

<sub>		::= <add_expr> '-' <mult_expr> ;

<mult_expr>     ::| <unary_expr> | <mult> | <idiv> | <imod> ;

<mult>		::= <mult_expr> '*' <unary_expr> 
		;
<idiv>		::= <mult_expr> '/' <unary_expr> 
		;
<imod> 		::= <mult_expr> '%' <unary_expr> 
		;
<unary_expr>    ::|    <unary> 
                |       <primary_expr>
		;
<unary>		::= <unary_operator> <primary_expr>
		;
<unary_operator>::| <unaryMinus> 
                  | <unaryPlus> 
                  | <negation>
		  | <pointer_dereference> (* datpete *)
		  | <logical_not> (* datpete *)
		  | <address_of> (* datpete *)
			;
<unaryMinus>	::= '-'
		;
<unaryPlus>	::= '+'
		;
<negation>	::= '~'
		;
<pointer_dereference>	::= '*'
			;
<logical_not>	::= '!'
		;
<address_of>	::= '&'
		;
<primary_expr>  ::|     <scoped_nameA>
                |       <literal>
                |       <bracketed_expr>
		|	<simple_function>
		;
<scoped_nameA>	::= <scoped_name>
		;
<bracketed_expr>	::= '(' <const_exp> ')'
			;
<literal>       ::|     <number_literal>
                |       <string_literal>
                |       <boolean_literal>
		;
<boolean_literal>       ::| <true> | <false>
			;
<true> 	::=   'TRUE'
	;
<false>	::=  'FALSE'
	;
<positive_int_const>    ::|     <const_exp>
		;
<type_dcl>      ::|     <typedef> 
                |       <struct_type>
                |       <union_type>
                |       <enum_type>
		|	<type_dcl_error>
		;
<typedef> 	::= 'typedef' <attributes_spec_opt> <type_declarator>
		;
<type_declarator>       ::=     <type_spec> <declarators>
		;
<type_spec>     ::|     <simple_type_spec>
                |       <constr_type_spec>
		|	<void_type_spec>
		;
<simple_type_spec>      ::|     <base_type_spec>
                	|       <template_type_spec>
                	|       <scoped_name>
			;
<base_type_spec>        ::|     <floating_pt_type>
                |       <integer_type>
                |       <char_type>
                |       <boolean_type>
                |       <octet_type>
                |       <any_type>
		;
<template_type_spec>    ::|     <sequence_type>
                	|       <string_type>
			;
<constr_type_spec>      ::|     <constr_struct_type>
                	|       <constr_union_type>
                	|       <constr_enum_type>
			;
<constr_struct_type>	::= <struct_type>
			;
<constr_union_type>	::= <union_type>
			;
<constr_enum_type>	::= <enum_type>
			;	
<declarators>   ::*     <declarator> ','  (* originally a + *)
		;
<declarator>    ::|     <simple_declarator>
                |       <complex_declarator>
		| 	<pointer_declarator>
		|	<pointer_to_pointer_declarator>
		|	<pointer_to_array_declarator>
		;
<simple_declarator>     ::=     <identifier>
			;
<complex_declarator>    ::|     <array_declarator>
		;
<floating_pt_type>      ::| <float> | <double>
			;
<float>	::=     'float'
	;
<double>	::=   'double'
		;
<integer_type>  ::|     <int>
		|	<signed_int>
                |       <unsigned_int>
		;
<signed_int>    ::|    <signed_long_int>
                |       <signed_short_int>
		;
<int>			::=     'int'
			;
<signed_long_int>       ::=     'long'
			;
<signed_short_int>      ::=     'short'
			;
<unsigned_int>  ::|     <unsigned_long_int>
                |       <unsigned_short_int>
		;
<unsigned_long_int>    	::=     'unsigned' 'long'
			;
<unsigned_short_int>    ::=     'unsigned' 'short'
			;
<char_type>     ::|     <unsigned_char> | <signed_char> | <char>
		;
<unsigned_char> ::=     'unsigned' 'char'
		;
<signed_char>   ::=     'signed' 'char'
		;
<char>          ::=     'char'
		;
<boolean_type>  ::=     'boolean'
		;
<octet_type>    ::=     'octet'
		;
<any_type>      ::=     'any'
		;
<struct_type>   ::=     'struct' <identifierOpt>

                                 '{' <member_list> '}' (*<semicolon_opt>*)
		;
<identifierOpt> ::?	<identifier>
		;
<member_list>   ::=     <members>
		;
<member>        ::=     <attributes_spec_opt> <type_spec> <declarators> ';'
		;
<union_type>    ::=     'union' <union_spec_opt> '{' <switch_body> '}' (*<semicolon_opt>*)
		;
<switch_type_spec>      ::|     <switch_integer_type>
                	|       <switch_char_type>
                	|       <switch_boolean_type>
                	|       <switch_enum_type>
                	|       <switch_scoped_name>
			;
<switch_integer_type>	::= <integer_type>
			;
<switch_char_type>	::= <char_type>
		;
<switch_boolean_type>	::= <boolean_type>
			;
<switch_enum_type>	::= <enum_type>
		;
<switch_scoped_name>	::= <scoped_name>
		;
<switch_body>   ::+     <case>
		;
<case>  ::=     <case_labels> <element_spec_opt> ';'
	;
<case_labels>	::+ <case_label>
		;
<case_label>    ::| <case_const_label> | <case_default> | <case_attributes> | <default2>
		;
<case_default>  ::= <default>
		;
<case_const_label> 	::= 'case' <const_exp> ':'
			;
<element_spec>  ::=     <type_spec> <declarator>
		;
<enum_type>     ::=     'enum' <identifier> '{' <enumerators> <sep_opt> '}' (*<semicolon_opt> *)
		;
<enumerators> 	::+ <enumerator> ','
		;
<enumerator>    ::=     <attributes_spec_opt> <identifier> <value_opt>
		;
<sequence_type> ::|     <sequence1> | <sequence2> 
		;
<sequence1>	::= 'sequence' '<' <simple_type_spec>
                                ',' <positive_int_const> '>'
		;
<sequence2>	::= 'sequence' '<' <simple_type_spec> '>'
		;
<string_type>   ::|     <string1> | <string2>
		;
<string1>	::= 'string' '<' <positive_int_const> '>'
		;
<string2>	::= 'string'
		;
<array_declarator>      ::=     <identifier> <fixed_array_sizes>
			;
<fixed_array_sizes> 	::+ <fixed_array_size> 
			;
<fixed_array_size>      ::=     '[' <positive_int_const_opt> ']'
			;
<attr_dcl>      ::=     <readonlyOpt> 'attribute'
                                <param_type_spec>
                                <simple_declarators>
		;
<simple_declarators>	::+ <simple_declarator> ','
			;
<readOnlyOpt>	::? <readOnly>
		;
<except_dcl>   	::= 'exception' <identifier> '{' <members> '}' (*<semicolon_opt>*)
		;

<members> 	::* <member>
		;
<op_dcl>        ::= <attributes_spec_opt> <op_type_spec> <std_call_opt>
                                <identifier>
                                <parameter_dcls>
                                <raises_exprOpt> <context_exprOpt>
		;
<raises_exprOpt>	::? <raises_expr>  
			;
<context_exprOpt> 	::? <context_expr> 
			;
<op_type_spec>  ::|     <param_type_spec>
                |       <void>
		|	<param_type_spec_ref>
		|	<void_ref>
		;
<void> 	::= 'void'
	;
<parameter_dcls>        ::=     '(' <param_dcls> ')'
			;
<param_dcls> 	::* <param_dcl> ',' 
		;
<param_dcl>     ::= 	<attributes_spec_opt> 
			<const1: const_opt> 
			<type: param_type_spec> 
			<const2: const_opt> 
			<declarator: declarator_opt>
			(* used to be <simple_declarator> *)
		;
<declarator_opt>	::?	<declarator>
			;
<raises_expr>   ::=     'raises' '(' <scoped_names> ')'
		;

<context_expr>  	::=     'context' '(' <string_literals> ')' 
			;
<string_literals>	::+ <string_literal> ',' ;

<param_type_spec>	::|     <param_base_type_spec> 
			|	<param_string_type>
                        |	<param_scoped_name>
			|	<param_void>
			;
<param_base_type_spec> 	::=	<base_type_spec> 
			;
<param_string_type>	::=	<string_type>
			;
<param_scoped_name>	::=	<scoped_name>
			;
<identifier>	::= <nameAppl>
		;
<number_literal> 	::= <const>
			;
<string_literal>	::= <string>
			;

(****************************additional COM stuff ****************************)

<param_void>	::= <void>
			;
<pointer_declarator>	::= '*' <identifier>
			;
<pointer_to_pointer_declarator>	::= '*' '*' <identifier>
			;
<std_call_opt>	::?	<std_call>
		;
<std_call>	::|	<_stdcall> | <__stdcall>
		;
<__stdcall>	::=	'__stdcall'
		;
<_stdcall>	::=	'_stdcall'
		;
<void_ref>	::=	'void' '*'
		;
<value_opt>	::?	<value>
		;
<value>	::=	'=' <const_exp>
	;
<sep_opt>	::? 	<sep>
		;
<sep>	::= ','
	;
<const_opt> 	::? <const_key>
		;
<const_key>	::= 'const'
		;
<positive_int_const_opt> ::? <positive_int_const>
			 ;
<simple_function>	::= <identifier> '(' <const_exp> ')'
			;
<union_spec_opt> 	::?	<union_spec>
			;
<union_spec>	::=	<identifier> 'switch' '(' <switch_type_spec> ')'
		;
<case_attributes> ::=	<attributes>
		;
<default2>	::=	'[' 'default' ']' 
		;
<element_spec_opt> 	::?	<element_spec>
			;
<void_type_spec>	::=	'void'
			;
<param_type_spec_ref>	::=	<param_type_spec> '*'	
			;
<pointer_to_array_declarator>	::=	'*' <array_declarator>
				;
<definition_error>	::= error
			;
<export_error>	::=	error
		;
<type_dcl_error>	::= error
