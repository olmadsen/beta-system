EOLasComEnd; 
--- idl: aGrammar: metagrammar ---
Grammar idl:

Option
  version = 7
  comBegin = '//'
  comEnd = '*/'
  stringChar = '"'
  suffix = '.idl'
  EOSchar = '%'

  nameDecl = unused


Rule
 
<specification> ::=     <definitions> ;
                       
<definitions> ::+ <definition> (* ; separator originally *)
	      ;
<definition>  ::|     <def_type_dcl> 
                |     <const_dcl> 
                |     <except_dcl> 
                |     <interface> 
                |     <dispinterface> 
                |     <library> 
                |     <coclass> 
                |     <module> 
		|     <import>
		|     <importlib>
		|     <def_cpp_quote>
		|     <definition_error>
                ;
<def_type_dcl> 	::=	<type_dcl>  ';'
		;
<def_cpp_quote> ::=     <cpp_quote>
		;
<module>        ::=     'module' <identifier> '{' <definitions> '}' (*<semicolon_opt>*)
		;
<comma_opt>  ::? 	<comma>
		;
<comma>	::=	','
		;
<semicolon_opt>  ::? 	<semicolon>
		;
<semicolon>	::=	';'
		;
<minus_opt>  ::? 	<minus>
		;
<minus>	::=	'-'
		;
<import>   	::=     'import' <string> ';'
		;
<importlib>   	::=     'importlib' '(' <string> ')' ';'
		;
<library>	::=     <library_header> '{' <library_body> '}' <semicolon_opt>
			;
<library_header>::=     '[' <attributes> <comma_opt> ']'
			'library' <identifier>
			;
<library_body>  ::=     <definitions>
		;
<coclass>	::=     <coclass_header> '{' <coclass_body>  '}' <semicolon_opt>
		;
<coclass_header>::=     '[' <attributes> <comma_opt> ']'
			'coclass' <identifier>
			;
<coclass_body>  ::+	<coclass_interface_dcl>
		;
<coclass_interface_dcl> ::| <coclass_interface>
			|   <coclass_dispinterface>
			;
<coclass_interface> 	::= <attributes_specs> 'interface' <identifier> ';'
			;	
<coclass_dispinterface> ::= <attributes_specs> 'dispinterface' <identifier> ';'
			;

(* MIDL attributes *)

<attributes_specs> 	::* <attributes_spec>
			;
<attributes_spec> 	::= '[' <attributes> ']'
			;
<attributes>	 	::+ <attribute_spec> ',' (* <attribute> reserved BOBS word *)
			;
<attribute_spec>	::| <readonly> 
			|   <default>
			|   <uuid>
			|   <iversion> (* 'version' reserved BOBS word *)
			|   <attribute_general>
			;
<attribute_general>	::= <NameAppl> <attribute_value_opt>
			;
<attribute_value_opt>	::? <attribute_value>
			;
<attribute_value>	::= '(' <att_val_spec> ')'
			;
<att_val_spec>		::| (*<att_val_identifier> 
			  | *)<att_val_const_exp>
			  | <att_val_string>
			;
<att_val_const_exp>	::= <minus_opt> <const_exp>
			;
<att_val_string>	::= <string>
			;
<istring>		::= <NameAppl> (* MUST be 'string *)
			;
<default>		::= 'default'
			;
<uuid>			::= 'uuid' '(' <uuid_rep> ')'
			;
<uuid_rep>		::* <keys> '-' 
			;
<keys>			::+ <key>
			;
<key>			::| <constKey> | <nameKey>
			;
<constKey>		::= <const>
			;
<nameKey>		::= <nameAppl>
			;
<iversion>		::= 'version' '(' <major_version> <minor_version_opt> ')'
			;
<major_version>		::= <const>
			;
<minor_version_opt>	::? <minor_version>
			;
<minor_version>		::= '.' <const>
			;	

(* End of attributes *)

<interface>	      ::|    <interface_dcl>
		      |      <forward_dcl>
		      ;
<dispinterface>	      ::|    <dispinterface_dcl>
		      |      <dispforward_dcl>
		      ;
<interface_dcl> ::=     <interface_header> '{' <interface_body> '}'
			<semicolon_opt> (* datpete: added semicolon_opt *)
		;
<dispinterface_dcl> ::= <dispinterface_header> '{' <interface_body> '}'
			<semicolon_opt> (* datpete: added semicolon_opt *)
			;
<forward_dcl>		::=     'interface' <identifier> ';' (* ';' added *)
			;
<dispforward_dcl>	::=     'dispinterface' <identifier> ';' (* ';' added *)
			;
<interface_header>      ::=     '[' <attributes> <comma_opt> ']'
				'interface' <identifier>
                                <inheritance_spec_opt>
			;
<dispinterface_header>  ::=     '[' <attributes> <comma_opt> ']'
				'dispinterface' <identifier>
			;
<inheritance_spec_opt> ::? <inheritance_spec> 
			;
<interface_body>        ::=     <exports_opt>
			;
<exports_opt>		::?	<exports>
			;
<exports> ::+ <export> 
	  ;
<export>        ::|     <export_type_dcl> 
                |       <export_const_dcl> 
                |       <export_except_dcl> 
                |       <export_attr_dcl> 
                |       <export_op_dcl> 
		|	<cpp_quote>
		|	<properties> (* Only for dispinterfaces *)
		|	<methods>    (* Only for dispinterfaces *)
		|	<export_import>
		|	<export_error>
		;
(* cpp_quote is a COM hack - probably not correct *)
<cpp_quote>	::= 'cpp_quote' '(' <string> ')'
		;
<properties>	::= 'properties' ':'(* Not yet complete *)
		;
<methods>	::= 'methods' ':' (* Not yet complete *)
		;
<export_import>	::= <import>
		;
<export_type_dcl>	::= <type_dcl> ';'
			;
<export_const_dcl> 	::= <const_dcl> ';'
			;	
<export_except_dcl> 	::= <except_dcl> ';'
		;
<export_attr_dcl> 	::= <attr_dcl> ';'
			;
<export_op_dcl> 	::= <op_dcl> ';'
			;
<inheritance_spec>      ::=     ':' <scoped_names> 
			;
<scoped_names>	::* <scoped_name> ',' 
		;
<scoped_name>   ::| <scoped_name1> | <scoped_name2> | <scoped_name3> 
		;
<scoped_name1> ::=     	<identifier>
		;
<scoped_name2> ::=      '::' <identifier>
		;
<scoped_name3>  ::= 	<scoped_name> '::' <identifier>
		;
<const_dcl>     ::=     'const' <const_type> <identifier> '='<const_exp>
		;
<const_type>    ::|     <const_integer_type>
                |       <const_char_type>
                |       <const_boolean_type>
                |       <const_floating_pt_type>
                |       <const_string_type>
                |       <const_scoped_name>
		;
<const_integer_type>	::= <integer_type>
			;
<const_char_type>	::= <char_type>
			;
<const_boolean_type>	::= <boolean_type>
			;
<const_floating_pt_type>	::= <floating_pt_type>
			;
<const_string_type>	::= <string_type>
			;
<const_scoped_name>	::= <scoped_name>
			;
<const_exps>		::+ <const_exp> ','
			;
<const_exp>        ::|	<paren_const_exp>
		     |	<const_expression>
		   ;
<paren_const_exp>  ::=	'(' <const_exp> ')'
		   ;
<const_expression> ::=  <or_expr>
		   ;
<or_expr>       ::+     <xor_expr> '|' ;

<xor_expr>      ::+     <and_expr> '^' ;

<and_expr>      ::+     <shift_expr>  '&'  ;

<shift_expr>    ::+     <add_exprA> 
		;
<add_exprA> 	::| <shift> | <add_expr> ;

<shift> 	::| <shiftRight> | <shiftLeft> ;

<shiftRight>	::= <shift_expr> '>>' ;
<shiftLeft> 	::= <shift_expr> '<' '<' ;

<add_expr>      ::| <mult_expr> | <add> | <sub> ;

<add>		::= <add_expr> '+' <mult_expr> ;

<sub>		::= <add_expr> '-' <mult_expr> ;

<mult_expr>     ::| <unary_expr> | <mult> | <idiv> | <imod> ;

<mult>		::= <mult_expr> '*' <unary_expr> 
		;
<idiv>		::= <mult_expr> '/' <unary_expr> 
		;
<imod> 		::= <mult_expr> '%' <unary_expr> 
		;
<unary_expr>    ::|    <unary> 
                |       <primary_expr>
		;
<unary>		::= <unary_operator> <primary_expr>
		;
<unary_operator>::| <unaryMinus> 
                  | <unaryPlus> 
                  | <negation>
		  | <pointer_dereference> (* datpete *)
		  | <logical_not> (* datpete *)
		  | <address_of> (* datpete *)
			;
<unaryMinus>	::= '-'
		;
<unaryPlus>	::= '+'
		;
<negation>	::= '~'
		;
<pointer_dereference>	::= '*'
			;
<logical_not>	::= '!'
		;
<address_of>	::= '&'
		;
<primary_expr>  ::|     <scoped_nameA>
                |       <literal>
                |       <bracketed_expr>
		|	<simple_function>
		;
<scoped_nameA>	::= <scoped_name>
		;
<bracketed_expr>	::= '(' <const_exp> ')'
			;
<literal>       ::|     <number_literal>
                |       <string_literal>
                |       <boolean_literal>
		;
<boolean_literal>       ::| <true> | <false>
			;
<true> 	::=   'TRUE'
	;
<false>	::=  'FALSE'
	;
<positive_int_const>    ::|     <const_exp>
		;
<type_dcl>      ::|     <typedef> 
                |       <struct_type>
                |       <union_type>
                |       <enum_type>
		|	<type_dcl_error>
		;
<typedef> 	::=     'typedef' <type_declarator>
		;
<type_declarator_opt> ::? <type_declarator>
		      ;
<type_declarator>::= <attributes_specs> <const_opt> <type_spec> <declarators>
		;
<type_spec>     ::|     <simple_type_spec>
                |       <constr_type_spec>
		|	<void_type_spec>
		;
<simple_type_spec>      ::|     <base_type_spec>
                	|       <template_type_spec>
                	|       <scoped_name>
			;
<base_type_spec>        ::|     <floating_pt_type>
                |       <integer_type>
                |       <char_type>
                |       <boolean_type>
                |       <octet_type>
                |       <any_type>
		;
<template_type_spec>    ::|     <sequence_type>
                	|       <string_type>
			;
<constr_type_spec>      ::|     <constr_struct_type>
                	|       <constr_union_type>
                	|       <constr_enum_type>
			;
<constr_struct_type>	::= <struct_type>
			;
<constr_union_type>	::= <union_type>
			;
<constr_enum_type>	::= <enum_type>
			;	
<declarators>   ::*     <declarator> ','  (* originally a + *)
		;
<declarator>    ::|     <simple_declarator>
                |       <complex_declarator>
		| 	<pointer_declarator>
		|	<pointer_to_pointer_declarator>
		|	<pointer_to_array_declarator>
		;
<simple_declarator>     ::=     <identifier>
			;
<complex_declarator>    ::|     <array_declarator>
		;
<floating_pt_type>      ::| <float> | <double>
			;
<float>	::=     'float'
	;
<double>	::=   'double'
		;
<integer_type>   ::|    <int>
		 |	<long>
		 |	<long_int>
		 |	<signed>
		 |	<unsigned>
		 |	<signed_int>
		 |	<unsigned_int>
		 |	<signed_long>
		 |	<unsigned_long>
		 |	<signed_long_int>
		 |	<unsigned_long_int>
		 |	<short>
		 |	<short_int>
		 |	<signed_short>
		 |	<unsigned_short>
		 |	<signed_short_int>
		 |	<unsigned_short_int>
		 |	<small>
		 |	<small_int>
		 |	<signed_small>
		 |	<unsigned_small>
		 |	<signed_small_int>
		 |	<unsigned_small_int>
		 |	<int64>
		 |	<signed_int64>
		 |	<unsigned_int64>
		 |	<hyper>
		 ;
<int>			::=     'int'
			;
<long>			::=     'long'
			;
<long_int>		::=     'long' 'int'
			;
<short>			::=     'short'
			;
<short_int>		::=     'short' 'int'
			;
<small>			::=     'small'
			;
<small_int>		::=     'small' 'int'
			;
<hyper>			::=     'hyper'
			;
<signed>		::=     'signed' 
			;
<unsigned>		::=     'unsigned' 
			;
<signed_int>		::=     'signed' 'int'
			;
<unsigned_int>		::=     'unsigned' 'int'
			;
<signed_long_int>       ::=     'signed' 'long' 'int'
			;
<unsigned_long_int>     ::=     'unsigned' 'long' 'int'
			;
<signed_long>    	::=     'signed' 'long'
			;
<unsigned_long>    	::=     'unsigned' 'long'
			;
<signed_short_int>      ::=     'signed' 'short' 'int'
			;
<unsigned_short_int>    ::=     'unsigned' 'short' 'int'
			;
<signed_short>		::=     'signed' 'short'
			;
<unsigned_short>	::=     'unsigned' 'short'
			;
<signed_small_int>      ::=     'signed' 'small' 'int'
			;
<unsigned_small_int>    ::=     'unsigned' 'small' 'int'
			;
<signed_small>		::=     'signed' 'small'
			;
<unsigned_small>	::=     'unsigned' 'small'
			;
<int64>			::=     '__int64'
			;
<signed_int64>		::=     'signed' '__int64'
			;
<unsigned_int64>	::=     'unsigned' '__int64'
			;
<char_type>     ::|     <unsigned_char> | <signed_char> | <char>
		;
<unsigned_char> ::=     'unsigned' 'char'
		;
<signed_char>   ::=     'signed' 'char'
		;
<char>          ::=     'char'
		;
<boolean_type>  ::=     'boolean'
		;
<octet_type>    ::=     'octet'
		;
<any_type>      ::=     'any'
		;
<struct_type>     ::=   'struct' <tag:identifier_opt> <struct_body_opt>
		  ;
<struct_body_opt> ::?   <struct_body>
		  ;
<struct_body>     ::=   '{' <members> <semicolon_opt> '}'
		  ;
<identifier_opt>  ::?	<identifier>
		  ;
<members> 	  ::*   <member>
		  ;
<member>          ::=   <type_declarator> ';'
		  ;
<union_type>            ::=     'union' <union_spec_opt> <union_body_opt> 
		        ;
<union_body_opt>	::? <union_body>
			;
<union_body>		::= '{' <union_cases> '}'
			;
<switch_type_spec>      ::|     <switch_integer_type>
                	|       <switch_char_type>
                	|       <switch_boolean_type>
                	|       <switch_enum_type>
                	|       <switch_scoped_name>
			;
<switch_integer_type>	::= <integer_type>
			;
<switch_char_type>	::= <char_type>
		        ;
<switch_boolean_type>	::= <boolean_type>
			;
<switch_enum_type>	::= <enum_type>
		        ;
<switch_scoped_name>	::= <scoped_name>
		        ;
<union_cases>      ::+     <case>
		   ;
<case>             ::=     <case_labels> ';'
	           ;
<case_labels>	   ::+ <case_label> 
		   ;
<case_label>       ::| <bracketed_case_const_label> 
		   |   <bracketed_case_default> 
		   |   <case_const_label> 
		   |   <case_default> 
		   |   <type_declarator>
		   ;
<case_default>     ::= 'default' ':' <type_declarator>
		   ;
<case_const_label> ::= 'case' <const_exp> ':' <type_declarator>
		   ;
<bracketed_case_default>     ::= '[' 'default' ']' <type_declarator_opt>
			     ;
<bracketed_case_const_label> ::= '[' 'case' '(' <const_exps> ')' ']' <type_declarator_opt>
			     ;
<enum_type>     ::=     'enum' <identifier> <enum_body_opt>
		;
<enum_body_opt> ::?     <enum_body>
		;
<enum_body>	::=     '{' <enumerators> <comma_opt> '}' 
		;
<enumerators> 	::+ <enumerator> ','
		;
<enumerator>    ::=     <attributes_specs> <identifier> <value_opt>
		;
<sequence_type> ::|     <sequence1> | <sequence2> 
		;
<sequence1>	::= 'sequence' '<' <simple_type_spec>
                                ',' <positive_int_const> '>'
		;
<sequence2>	::= 'sequence' '<' <simple_type_spec> '>'
		;
<string_type>   ::|     <string1> | <string2>
		;
<string1>	::= <istring> '<' <positive_int_const> '>'
		;
<string2>	::= <istring>
		;
<array_declarator>      ::=     <identifier> <fixed_array_sizes>
			;
<fixed_array_sizes> 	::+ <fixed_array_size> 
			;
<fixed_array_size>      ::=     '[' <positive_int_const_opt> ']'
			;
<attr_dcl>      ::=     <readonly_opt> 'attribute'
                                <param_type_spec>
                                <simple_declarators>
		;
<simple_declarators>	::+ <simple_declarator> ','
			;
<readOnly_opt>	::? <readOnly>
		;
<readOnly>	::= 'readonly' (* <NameAppl> - Must be readOnly *)
		;
<except_dcl>   	::= 'exception' <identifier> '{' <members> '}' 
		;
<op_dcl>        ::= <attributes_specs> <op_type_spec> <std_call_opt>
                                <identifier>
                                <parameter_dcls>
                                <raises_expr_opt> <context_expr_opt>
		;
<raises_expr_opt>	::? <raises_expr>  
			;
<context_expr_opt> 	::? <context_expr> 
			;
<op_type_spec>  ::|     <param_type_spec>
                |       <void>
		|	<param_type_spec_ref>
		|	<void_ref>
		;
<void> 	::= 'void'
	;
<parameter_dcls>        ::=     '(' <param_dcls> ')'
			;
<param_dcls> 	::* <param_dcl> ',' 
		;
<param_dcl>     ::= 	<attributes_specs> 
			<const1: const_opt> 
			<type: param_type_spec> 
			<const2: const_opt> 
			<declarator: declarator_opt>
			(* used to be <simple_declarator> *)
		;
<declarator_opt>	::?	<declarator>
			;
<raises_expr>   ::=     'raises' '(' <scoped_names> ')'
		;

<context_expr>  	::=     'context' '(' <string_literals> ')' 
			;
<string_literals>	::+ <string_literal> ',' ;

<param_type_spec>	::|     <param_base_type_spec> 
			|	<param_string_type>
                        |	<param_scoped_name>
			|	<param_void>
			;
<param_base_type_spec> 	::=	<base_type_spec> 
			;
<param_string_type>	::=	<string_type>
			;
<param_scoped_name>	::=	<scoped_name>
			;
<identifier>	::= <nameAppl>
		;
<number_literal> 	::= <const>
			;
<string_literal>	::= <string>
			;

(****************************additional COM stuff ****************************)

<param_void>	::= <void>
			;
<pointer_declarator>	::= '*' <identifier_opt>
			;
<pointer_to_pointer_declarator>	::= '*' '*' <identifier_opt>
			;
<std_call_opt>	::?	<std_call>
		;
<std_call>	::|	<_stdcall> | <__stdcall>
		;
<__stdcall>	::=	'__stdcall'
		;
<_stdcall>	::=	'_stdcall'
		;
<void_ref>	::=	'void' '*'
		;
<value_opt>	::?	<value>
		;
<value>	::=	'=' <const_exp>
	;
<const_opt> 	::? <const_key>
		;
<const_key>	::= 'const'
		;
<positive_int_const_opt> ::? <positive_int_const>
			 ;
<simple_function>	::= <identifier> '(' <const_exp> ')'
			;
<union_spec_opt> 	::? <union_spec>
			;
<union_spec>		::= <tag:identifier> <union_switch_opt>
			;
<union_switch_opt>	::? <union_switch>
			;
<union_switch>		::= 'switch' '(' <switch_type_spec>
			    <identifier> ')' <name:identifier_opt>
			;
<void_type_spec>	::=	'void'
			;
<param_type_spec_ref>	::=	<param_type_spec> '*'	
			;
<pointer_to_array_declarator>	::=	'*' <array_declarator>
				;
<definition_error>	::= error
			;
<export_error>	::=	error
		;
<type_dcl_error>	::= error
