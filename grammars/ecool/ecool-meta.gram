contractioncategories 
   Attributes
-- ecool: aGrammar: metagrammar ---

Grammar ecool:

Option
  version      = 2
  comBegin     = '(*'
  comEnd       = '*)'
  splitOnFiles = 1
  stringChar   = ''' '
  suffix       = '.ecool'
  string       = unused

Rule
  <EcoolSpec> ::= <DeclList> ;
  <DeclList>  ::+ <DeclOpt> ';' ;
  <DeclOpt>   ::? <ClassDecl> ;

  <ClassTemplate>  ::= 'Class' <ClassNameOpt> '(#' <Sections> '#)' ;

  <Sections>  ::= <PublicSection> <ProtectedSectionOpt> <PrivateSectionOpt> ;
  <ProtectedSectionOpt> ::? <ProtectedSection> ;
  <PrivateSectionOpt>   ::? <PrivateSection> ;
  <PublicSection>    ::= <PublicKwdOpt> <Attributes> ;
  <ProtectedSection> ::= 'Protected' <Attributes> ;
  <PrivateSection>   ::= 'Private' <Attributes> ;
  <PublicKwdOpt>     ::? <PublicKwd> ;
  <PublicKwd>        ::= 'Public' ;

  <Attributes>       ::+ <AttributeDeclOpt> ';' ;
  <AttributeDeclOpt> ::? <AttributeDecl> ;
  <AttributeDecl>    ::| <SimpleDecl>
                       | <ArrayDecl> 
                       | <ProcDecl> 
                       | <VariableClassDecl>
                       | <VariableProcDecl> 
                       | <PartDecl> 
                       | <ClassDecl> ;

  <SimpleDecl> ::= <Names> ':' <referenceSpecification> ;
  <ArrayDecl>  ::= <Names> ':' 'Array' '[' <index> ']' 
                                'Of' <referenceSpecification> ;

  <referenceSpecification> ::| <StaticItem>
                             | <DynamicItem> ;
  <StaticItem>  ::=  <SimpleType> ;
  <SimpleType>  ::| <Integer> | <Text> | <Boolean> | <Char> | <Real> ;
  <Integer>     ::= 'Integer' ;
  <Text>        ::= 'Text' ;
  <Boolean>     ::= 'Boolean' ;
  <Char>        ::= 'Char' ;
  <Real>        ::= 'Real' ;
  <DynamicItem> ::= 'Ref' <DynamicType> ;
  <DynamicType> ::| <SimpleType>
                  | <NameApl> ;

  <ProcDecl>    ::| <StandardDecl>
                  | <VirtualDecl>
                  | <BindDecl> ;
  <StandardDecl>::= <Names> ':' 'Proc' <ProcParams> ;
  <VirtualDecl> ::= <Names> ':' 'Virtual' <ProcParams> ;
  <BindDecl>    ::= <Names> ':' 'Bind' <ProcNameOpt> ;

  <ProcParams>  ::= <InArgOpt> <OutArgOpt> ;
  <InArgOpt>    ::? <InArg> ;
  <InArg>       ::= '(' <Parameters> ')' ;
  <OutArgOpt>   ::? <OutArg> ;
  <OutArg>      ::= 'Out' '('  <Parameters> ')' ;
  <Parameters>  ::+ <SimpleDecl> ';' ;

  <ClassDecl>   ::| <StandardClassDecl>
                  | <VirtualClassDecl>
                  | <BindClassDecl> ;
  <StandardClassDecl> ::= <Names> ':' <ClassTemplate> ;
  <VirtualClassDecl>  ::= <Names> ':' 'Virtual' 'Class' <ClassName> ;
  <BindClassDecl>     ::= <Names> ':' 'Bind' 'Class' <ClassNameOpt> ;

  <VariableClassDecl> ::= <Names> ':' 'Ref' 'Class' <ClassName> ;
  <VariableProcDecl>  ::= <Names> ':' 'Ref' 'Proc' <ProcName> ;
  <PartDecl>          ::= <Names> ':' 'Part' <ClassName> ;

  <ClassNameOpt> ::? <ClassName> ;
  <ClassName>    ::= <NameApl> ;
  <ProcNameOpt>  ::? <ProcName> ;
  <ProcName>     ::= <NameApl> ;
  <Names>        ::+ <NameDcl> ', ';
  <NameDcl>      ::= <NameDecl> ;
  <NameApl>      ::= <NameAppl> ;
  <Index>        ::= <IntegerConst> ;
  <IntegerConst> ::= <Const>
  
