contractioncategories 
	classSpecification
	parameterSpecification
	nameBindingSpecification
	attributeSpecification
	attributeGroupSpecification
	behaviourSpecification
	actionSpecification
	notificationSpecification
	conditionDefinition
	descriptionString
	definedString;

longLexemsWithQuote;
dashNames;
EOLasComEnd;
caseSensitive;

--- gdmo: aGrammar: metagrammar ---

Grammar gdmo:

Option 
  dashKeywords = true
  caseSensitive = true
  bobsoptions = '4,5,32,34,59'
  version = 1
  comBegin = '--'
  comEnd = '--'
  stringChar = '"'
  suffix = '.gdmo'
  EOSchar = '%'
  
  nameDecl = unused

Rule

<GDMOdefinitions> ::* <GDMOdefinition> ;
<GDMOdefinition>  ::| <classDefinition>
		    | <packageDefinition>
		    | <parameterDefinition>
		    | <nameBindingDefinition>
		    | <attributeDefinition>
		    | <attributeGroupDefinition>
		    | <behaviourDefinition>
		    | <actionDefinition>
		    | <notificationDefinition> ;

<classDefinition> ::= <className> 'MANAGED' 'OBJECT' 'CLASS' <classSpecification> <registeredAs> ';' ;
<classSpecification> ::= <derivedFromOpt>
			 <characterizedByOpt>
			 <conditionalPackagesOpt> ;

<className> ::= <gdmoIdentifier> ;
<derivedFromOpt> ::? <derivedFrom> ;
<derivedFrom>    ::= 'DERIVED' 'FROM' <classLabelList> ';' ;
<classLabelList> ::+ <classLabel> ',' ;
<classLabel> ::= <label> ;

<characterizedByOpt> ::? <characterizedBy> ;
<characterizedBy> ::= 'CHARACTERIZED' 'BY' <packageLabelList> ';' ;
<packageLabelList> ::+ <packageLabel> ;

<conditionalPackagesOpt> ::? <conditionalPackages> ;
<conditionalPackages> ::= 'CONDITIONAL' 'PACKAGES' <conditionalPackageList> ';' ;
<conditionalPackageList> ::+ <conditionalPackage> ',' ;
<conditionalPackage> ::= <packageLabel> 'PRESENT' 'IF' <conditionDefinition> ;
<conditionDefinition> ::= <string> ;

<registeredAsOpt> ::? <registeredAs> ;
<registeredAs> ::= 'REGISTERED' 'AS' <objectId> ;




<packageLabel> ::| <packageDefinitionName> | <packageDefn> ;
<packageDefinitionName> ::= <gdmoIdentifier> ;
<packageDefn> ::= <packageDefinition> ;
<packageDefinition> ::= <packageDefinitionName> 'PACKAGE' <behaviourOpt> <attributesOpt> <attributeGroupsOpt> <actionsOpt> <notificationsOpt> <registeredAsOpt> ';' ;

<behaviourOpt> ::? <behaviour> ;
<behaviour> ::= 'BEHAVIOUR' <behaviourDefinitionList> ';' ;
<behaviourDefinitionList> ::+ <behaviourLabel> ',' ;

<attributesOpt> ::? <attributes> ;
<attributes> ::= 'ATTRIBUTES' <attributeList> ';' ;
<attributeList> ::+  <attributeSpec> ',' ;
<attributeSpec> ::= <attributeName> <propertyList> <parameterList> ;
<attributeName> ::= <label> ;
<parameterList> ::* <parameter> ;
<parameter> ::= <label> ;
<propertyList> ::= <replaceWithDefaultOpt>
		   <defaultValueOpt>
		   <initialValueOpt>
		   <permittedValuesOpt>
		   <requiredValuesOpt>
		   <getReplaceOpt>
		   <addRemoveOpt> ;
<replaceWithDefaultOpt> ::? <replaceWithDefault> ;
<replaceWithDefault> ::= 'REPLACE-WITH-DEFAULT' ;

<defaultValueOpt> ::? <defaultValue> ;
<defaultValue> ::= 'DEFAULT' 'VALUE' <valueSpecifier> ;
<valueSpecifier> ::| <valueReference> | <derivationRule> ;
<valueReference> ::= <label> ;
<derivationRule> ::= 'DERIVATION' 'RULE' <behaviourDefinitionLabel> ;
<behaviourDefinitionLabel> ::= <gdmoIdentifier> ;

<initialValueOpt> ::? <initialValue> ;
<initialValue> ::= 'INITIAL' 'VALUE' <valueSpecifier> ;

<permittedValuesOpt> ::? <permittedValues> ;
<permittedValues> ::= 'PERMITTED' 'VALUES' <typeReference> ;

<requiredValuesOpt> ::? <requiredValues> ;
<requiredValues> ::= 'REQUIRED' 'VALUES' <typeReference> ;

<getReplaceOpt> ::? <getReplace> ;
<getReplace> ::| <GetKwd> | <ReplaceKwd> | <GetReplaceKwd> ;
<GetKwd> ::= 'GET' ; 
<ReplaceKwd> ::= 'REPLACE' ; 
<GetReplaceKwd> ::= 'GET-REPLACE' ;


<addRemoveOpt> ::? <addRemove> ;
<addRemove> ::| <AddKwd> | <RemoveKwd> | <AddRemoveKwd> ;
<AddKwd> ::= 'ADD' ; 
<RemoveKwd> ::= 'REMOVE' ; 
<AddRemoveKwd> ::= 'ADD-REMOVE' ;
<parameterLabelList> ::* <parameterLabel> ;
<parameterLabel> ::= <gdmoIdentifier> ;

<attributeGroupsOpt> ::? <attributeGroups> ;
<attributeGroups> ::= 'ATTRIBUTE' 'GROUP' <attributeGroupSpecList> ';' ;
<attributeGroupSpecList> ::+ <attributeGroupSpec> ',' ;
<attributeGroupSpec> ::= <groupLabel> <attributeLabelList> ;
<attributeLabelList> ::* <attributeLabel> ;
<attributeLabel> ::= <gdmoIdentifier> ;

<actionsOpt> ::? <actions> ;
<actions> ::= 'ACTIONS' <actionSpecList> ';' ;
<actionSpecList> ::+ <actionSpec> ',' ;
<actionSpec> ::= <groupLabel> <parameterLabelList> ;

<notificationsOpt> ::? <notifications> ;
<notifications> ::= 'NOTIFICATIONS' <notificationSpecList> ';' ;
<notificationSpecList> ::+ <notificationSpec> ',' ;
<notificationSpec> ::= <notificationLabel> <parameterLabelList> ;





<parameterDefinition> ::= <parameterLabel> 'PARAMETER' <parameterSpecification> <registeredAsOpt> ';' ;
<parameterSpecification> ::=	<context>
				<syntaxOrAttributeChoice>
				<behaviourOpt> ;
<context> ::= 'CONTEXT' <contextType> ';' ;
<contextType> ::| <contextKeyword>
                | <ActionInfoKwd> | <ActionReplyKwd>
                | <EventInfoKwd>  | <EventReplyKwd>
                | <SpecificErrorKwd> ;
<ActionInfoKwd> ::= 'ACTION-INFO' ;
<ActionReplyKwd>::= 'ACTION-REPLY' ;
<EventInfoKwd>  ::= 'EVENT-INFO' ;
<EventReplyKwd> ::= 'EVENT-REPLY';
<SpecificErrorKwd> ::= 'SPECIFIC-ERROR' ;
<contextKeyword> ::= <typeReference> ;
<typeReference> ::= <gdmoIdentifier>'.'<gdmoIdentifier> ;

<syntaxOrAttributeChoice> ::| <syntaxChoice> | <attributeChoice> ;
<syntaxChoice> ::= 'WITH' 'SYNTAX' <typeReference> ';' ;
<attributeChoice> ::= 'ATTRIBUTE' <attributeLabel> ';' ;







<nameBindingDefinition> ::= <nameBindingLabel> 'NAME' 'BINDING' <nameBindingSpecification> <registeredAs> ';' ;
<nameBindingSpecification> ::=	<subordinateObjectClass>
				<namedBy>
				<withAttribute>
				<behaviourOpt>
				<createOpt>
				<deleteOpt> ;

<subordinateObjectClass> ::= 'SUBORDINATE' 'OBJECT' 'CLASS'
				 <classLabel> <andSubClassesOpt> ';' ;
<namedBy> ::= 'NAMED' 'BY' 'SUPERIOR' 'OBJECT' 'CLASS'
				 <classLabel> <andSubClassesOpt> ';' ;
<withAttribute> ::= 'WITH' 'ATTRIBUTE' <attributeLabel> ';' ;

<andSubClassesOpt> ::? <andSubClasses> ;
<andSubClasses> ::= 'AND' 'SUBCLASSES' ;
<createOpt> ::? <create> ;
<create> ::= 'CREATE' <createSpecification> ';' ;
<createSpecification> ::= <createModifierList> <parameterLabelList> ;
<createModifierList> ::* <createModifier> ',' ;
<createModifier> ::| <WithReferenceObjectKwd>
                   | <WithAutomaticInstanceNamingKwd> ;
<WithReferenceObjectKwd> ::= 'WITH-REFERENCE-OBJECT' ;
<WithAutomaticInstanceNamingKwd> ::= 'WITH-AUTOMATIC-INSTANCE-NAMING' ;
<deleteOpt> ::? <delete> ;
<delete> ::= 'DELETE' <deleteSpecification> ';' ;
<deleteSpecification> ::= <deleteModifier> <parameterLabelList> ;
<deleteModifier> ::| <OnlyIfNoContainedObjectsKwd>
                   | <DeletesContainedObjectsKwd> ;
<OnlyIfNoContainedObjectsKwd> ::= 'ONLY-IF-NO-CONTAINED-OBJECTS' ;
<DeletesContainedObjectsKwd> ::= 'DELETES-CONTAINED-OBJECTS' ;





<attributeDefinition> ::= <attributeLabel> 'ATTRIBUTE' <attributeSpecification> <registeredAsOpt> ';' ;
<attributeSpecification> ::=	<derivedOrWithSyntaxChoice>
				<matchesForOpt>
				<behaviourOpt>
				<parametersOpt> ;

<derivedOrWithSyntaxChoice> ::| <derivedChoice>
			      | <withAttributeSyntaxChoice> ;
<derivedChoice> ::= 'DERIVED' 'FROM' <attributeLabel> ';' ;
<withAttributeSyntaxChoice> ::= 'WITH' 'ATTRIBUTE' 'SYNTAX' <typeReference> ';' ;

<matchesForOpt> ::? <matchesFor> ;
<matchesFor> ::= 'MATCHES' 'FOR' <qualifierList> ';' ;
<qualifierList> ::+ <qualifier> ',' ;
<qualifier> ::| <EqualityKwd> | <OrderingKwd> | <SubstringsKwd>
              | <SetComparisonKwd> | <SetIntersectionKwd> ;
<EqualityKwd> ::= 'EQUALITY' ;
<OrderingKwd> ::= 'ORDERING' ;
<SubstringsKwd> ::= 'SUBSTRINGS' ;
<SetComparisonKwd> ::= 'SET-COMPARISON' ;
<SetIntersectionKwd> ::= 'SET-INTERSECTION' ;

<parametersOpt> ::? <parameters> ;
<parameters> ::= 'PARAMETERS' <parameterListCommaSeparated> ';' ;
<parameterListCommaSeparated> ::+ <parameterLabel> ',' ;





<attributeGroupDefinition> ::= <attributeLabel> 'ATTRIBUTE' 'GROUP' <attributeGroupSpecification> <registeredAs> ';' ;
<attributeGroupSpecification> ::=	<groupElementsOpt>
					<fixedOpt>
					<descriptionOpt> ;
<groupElementsOpt> ::? <groupElements> ;
<groupElements> ::= 'GROUP' 'ELEMENTS' <attributeLabelList> ';' ;

<fixedOpt> ::? <fixed> ;
<fixed> ::= 'FIXED' ';' ;

<descriptionOpt> ::? <description> ;
<description> ::= 'DESCRIPTION' <descriptionString> ';' ;
<descriptionString> ::= <string> ;





<behaviourLabel> ::| <behaviourDefinitionName> | <behaviourDefn>;
<behaviourDefn> ::= <behaviourDefinition>;
<behaviourDefinition> ::= <behaviourDefinitionName> 'BEHAVIOUR' 'DEFINED' 'AS' <behaviourSpecification> ;
<behaviourSpecification> ::= <definedString> ';' ;
<definedString> ::= <string> ;
			





<actionDefinition> ::= <actionName> 'ACTION' <actionSpecification> <registeredAs> ';' ;
<actionSpecification> ::=	<behaviourOpt>
				<modeConfirmedOpt>
				<parametersOpt>
				<withSyntaxOpt> ;

<modeConfirmedOpt> ::? <modeConfirmed> ;
<modeConfirmed> ::= 'MODE' 'CONFIRMED' ';' ;

<withSyntaxOpt> ::? <withSyntax> ;
<withSyntax> ::= 'WITH' <syntaxSpec> ;

<syntaxSpec> ::| <replySyntax> | <informationSyntax> ;
<replySyntax> ::= 'REPLY' 'SYNTAX' <typeReference> ';' ;
<informationSyntax> ::= 'INFORMATION' 'SYNTAX' <typeReference> ';'
		        <replySyntaxOpt> ;
<replySyntaxOpt> ::? <withReplySyntax> ;
<withReplySyntax> ::= 'WITH' <replySyntax> ;




<notificationDefinition> ::= <notificationName> 'NOTIFICATION' <notificationSpecification> <registeredAs> ';' ;
<notificationSpecification> ::=	<behaviourOpt>
				<parametersOpt>
				<withPartOpt> ;

<withpartOpt> ::? <withpart> ;
<withPart> ::= 'WITH' <informationOrReply> ;
<informationOrReply> ::| <replySyntaxCase> | <informationSyntaxAndAttributeIds> ;
<replySyntaxCase> ::= <replySyntax> ;
<informationSyntaxAndAttributeIds> ::= 
			'INFORMATION' 'SYNTAX' <typeReference> 
			<andAttributeIdsOpt> ';' <replySyntaxOpt> ;
<andAttributeIdsOpt> ::? <andAttributeIds> ;
<andAttributeIds> ::= 'AND' 'ATTRIBUTE' 'IDS' <fieldAttributeList> ;
<fieldAttributeList> ::+ <fieldAttribute> ',' ;
<fieldAttribute> ::= <fieldName> <attributeLabel> ;

<groupLabel> ::= <gdmoIdentifier> ;
<notificationLabel> ::= <label> ;
<nameBindingLabel> ::= <gdmoIdentifier> ;
<behaviourDefinitionName> ::= <gdmoIdentifier> ;
<actionName> ::= <gdmoIdentifier> ;
<notificationName> ::= <gdmoIdentifier> ;
<fieldName> ::| <objectId> | <gdmoId> ; 
<gdmoId> ::= <gdmoIdentifier> ;
<objectId> ::= '{' <arcs> <objectIdNumberOpt> '}' ;
<arcs> ::+ <arc> ;
<arc> ::= <arcId> <arcParamOpt> ;
<arcParamOpt> ::? <arcParam> ;
<arcParam> ::= '(' <arcParamNo> ')' ;
<arcId> ::= <gdmoIdentifier> ;
<arcParamNo> ::= <const> ;
<objectIdNumberOpt> ::? <objectIdNumber> ;
<objectIdNumber> ::= <const> ;



<label> ::= <documentIdentificationOpt> <labelString> ;
<documentIdentificationOpt> ::? <documentIdentification> ;
<documentIdentification> ::=  <documentIdentifier> ':' ;
<documentIdentifier> ::= <standardName> ;
<standardName> ::= <string> ;
<labelString> ::| <labelIdentifier> | <typeReference> ;
<labelIdentifier> ::= <gdmoIdentifier> ;

<gdmoIdentifier> ::= <nameAppl> 
