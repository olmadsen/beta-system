contractioncategories 
       ModuleBody ;

dashNames;
EOLasComEnd;
caseSensitive;

--- asn1: aGrammar: metagrammar ---

Grammar asn1:

Option 
  dashKeywords = 'true'
  caseSensitive = 'true'
  bobsoptions = '4,5,32,34,59'
  version = 1
  comBegin = '--'
  comEnd = '--'
  stringChar = '"'
  suffix = '.asn1'
  EOSchar = '%'

  nameAppl = unused  

Rule

<ModuleDefinition> ::=
        <ModuleIdentifier> <DEFINITIONSkwd> <TagDefaultOpt> '::='
           <BEGINkwd>
              <ModuleBodyOpt>
           <ENDkwd>
        ;
<TagDefaultOpt> ::?
        <TagDefault>
        ;
<TagDefault> ::|
        <TagExplicit> |
        <TagImplicit>
        ;
<TagExplicit> ::=
        <EXPLICITkwd> <TAGSkwd>
        ;
<TagImplicit> ::=
        <IMPLICITkwd> <TAGSkwd>
        ;
<ModuleIdentifier> ::=
        <identifier> <AssignedIdentifierOpt>
        ;
<AssignedIdentifierOpt> ::?
        <ObjectIdentifierValue>
        ;
<ObjectIdentifierValue> ::=
        '{' <ObjIdComponentList> '}'
        ;
<ObjIdComponentList> ::+
        <ObjIdComponent>
        ;
<ObjIdComponent> ::|
        <ReferenceFour> |
        <ObjIdNumber> |
        <NameAndNumberForm>
        ;
<ObjIdNumber> ::=
	<number>
	;
<ModuleBodyOpt> ::?
        <ModuleBody>
        ;
<ModuleBody> ::=
        <ExportsOpt> <ImportsOpt> <AssignmentList>
        ;
<ExportsOpt> ::?
        <Exports>
        ;
<Exports> ::=
        <EXPORTSkwd> <SymbolsExportedOpt> ';'
        ;
<SymbolsExportedOpt> ::?
        <SymbolsExported>
        ;
<SymbolsExported> ::=
        <SymbolList>
        ;
<ImportsOpt> ::?
        <Imports>
        ;
<Imports> ::=
        <IMPORTSkwd> <SymbolsImportedOpt> ';'
        ;
<SymbolsImportedOpt> ::?
        <SymbolsImported>
        ;
<SymbolsImported> ::=
        <SymbolsFromModuleList>
        ;
<SymbolsFromModuleList> ::+
        <SymbolsFromModule>
        ;
<SymbolsFromModule> ::=
        <SymbolList> <FROMkwd> <ModuleIdentifier>
        ;
<SymbolList> ::+ <identifier> ','
        ;
<AssignmentList> ::+
        <Assignment>
        ;
<Assignment> ::=
        <identifier> <TypeOpt> '::=' <TypeOrValue>
        ;
<TypeOrValue> ::|
	<TypeOne> |
	<Value>
        ;
<TypeOne> ::= <Type> ;
(*------TYPES----------------------------------------------------------------*)
<Type> ::|
        <ReferenceSix> |
	<BuiltinType> |
        <Subtype>
        ;
<TypeOpt> ::?
	<Type>
	;
<BuiltinType> ::|
        <BooleanType> |
        <IntegerType> |
        <BitStringType> |
        <OctetStringType> |
        <NullType> |
        <SequenceType> |
        <SetType> |
        <ChoiceType> |
        <SelectionType> |
        <TaggedType> |
        <AnyType> |
        <ObjectIdentifierType> |
        <EnumeratedType> |
        <RealType>
        ;
<NamedType> ::=
        <identifier> <Type>
        ;
<BooleanType> ::= <BOOLEANkwd>
        ;
<IntegerType> ::=
        <INTEGERkwd> <NamedNumbersOpt>
        ;
<NamedNumbersOpt> ::=
        <NamedNumbers>
        ;
<NamedNumbers> ::=
        '{' <NamedNumberList> '}'
        ;
<NamedNumberList> ::+
        <NamedNumber> ','
        ;
<NamedNumber> ::=
        <identifier> '(' <SignedNumberOrReference> ')'
        ;
<SignedNumberOrReference> ::|
        <SignedNumber> |
        <ReferenceOne>
        ;
<SignedNumber> ::= <minusOpt> <number>
        ;
<EnumeratedType> ::=
        <ENUMERATEDkwd> '{' <Enumeration> '}'
        ;
<Enumeration> ::+
        <NamedNumber> ','
        ;
<RealType> ::=
        <REALkwd>
        ;
<BitStringType> ::=
        <BITSTRINGkwd> <NamedBits>
        ;
<NamedBits> ::=
        '{' <NamedBitList> '}'
        ;
<NamedBitList> ::+
        <NamedBit> ','
        ;
<NamedBit> ::=
        <identifier> '(' <NamedBitValue> ')'
        ;
<NamedBitValue> ::|
        <BitNumber> |
        <ReferenceTwo>
        ;
<BitNumber> ::=
	<number>
	;
<OctetStringType> ::=
        <OCTETSTRINGkwd>
        ;
<NullType> ::=
        <NULLkwd>
        ;
<SequenceType> ::=
        <SEQUENCEkwd> <SequenceTypeSpec>
        ;
<SequenceTypeSpec> ::|
	<ElementTypes> |
	<OfType>
        ;
<ElementTypes> ::=
        '{' <ElementTypeListOpt> '}'
        ;
<ElementTypeListOpt> ::?
        <ElementTypeList>
        ;
<ElementTypeList> ::+
        <ElementType> ','
        ;
<ElementType> ::|
        <NamedType> |
        <NamedTypeOptional> |
        <NamedTypeDefault> |
        <ComponentType>
        ;
<NamedTypeOptional> ::=
        <NamedType> <OPTIONALkwd>
        ;
<NamedTypeDefault> ::=
        <NamedType> <DEFAULTkwd> <Value> 
        ;
<ComponentType> ::=
        <COMPONENTSkwd> <OFkwd> <Type>
        ;
<OfType> ::=
        <OFkwd> <Type>
        ;
<SetType> ::=
        <SETkwd> <SetTypeSpecification>
        ;
<SetTypeSpecification> ::|
	<SetTypeSpec> |
	<SetOfTypeSpec> |
	<SetConstraintTypeSpec>
	;
<SetTypeSpec> ::=
        '{' <ElementTypeList> '}'
	;
<SetOfTypeSpec> ::=
	<OfType>
	;
<SetConstraintTypeSpec> ::=
        <SizeConstraint> <OFkwd> <Type>
	;
<ChoiceType> ::=
        <CHOICEkwd> '{' <AlternativeTypeList> '}'
        ;
<AlternativeTypeList> ::+
        <NamedType> ','
        ;
<SelectionType> ::=
        <identifier> '<' <Type>
        ;
<TaggedType> ::=
        <Tag> <TagType>
        ;
<Tag> ::=
        '[' <ClassOpt> <ClassNumber> ']'
        ;
<TagType> ::|
        <OrdinaryType> |
        <ImplicitType> |
        <ExplicitType>
        ;
<OrdinaryType> ::=
	<Type>
	;
<ImplicitType> ::=
        <IMPLICITkwd> <Type>
        ;
<ExplicitType> ::=
        <EXPLICITkwd> <Type>
        ;
<ClassNumber> ::|
        <NumberOne> |
        <ReferenceThree>
        ;
<ClassOpt> ::?
        <Class>
        ;
<Class> ::|
        <UNIVERSALkwd> |
        <APPLICATIONkwd> |
        <PRIVATEkwd>
        ;
<AnyType> ::=
        <ANYkwd> <DefinedByOpt>
        ;
<DefinedByOpt> ::?
        <DefinedBy>
        ;
<DefinedBy> ::=
        <DEFINEDkwd> <BYkwd> <identifier>
        ;
<ObjectIdentifierType> ::=
        <OBJECTkwd> <IDENTIFIERkwd>
        ;
(*------SUBTYPES-------------------------------------------------------------*)
<Subtype> ::|
        <ParentType> |
        <SequenceConstraintType>
        ;
<ParentType> ::=
        <Reference> <SubtypeSpec>
        ;
<SequenceConstraintType> ::=
        <SEQUENCEkwd> <SizeConstraint> <OFkwd> <Type>
        ;
<SubtypeSpec> ::=
        '(' <SubtypeValueSetListOpt> ')'
        ;
<SubtypeValueSetListOpt> ::?
        <SubtypeValueSetList>
        ;
<SubtypeValueSetList> ::+
	<SubtypeValueSet> '|'
	;
<SubtypeValueSet> ::|
        <SingleValue> |
        <ContainedSubtype> |
        <ValueRange> |
        <PermittedAlphabet> |
        <SizeConstraint> |
        <InnerTypeConstraints>
        ;
<SingleValue> ::=
        <Value>
        ;
<ContainedSubtype> ::=
        <INCLUDESkwd> <Type>
        ;
<ValueRange> ::=
        <LowerEndpoint> '..' <UpperEndpoint>
        ;
<LowerEndPoint> ::=
        <LowerEndValue> <lessOpt>
        ;
<UpperEndPoint> ::=
        <lessOpt> <UpperEndValue>
        ;
<LowerEndValue> ::|
        <LowerValue> |
        <MINkwd>
        ;
<UpperEndValue> ::|
        <UpperValue> |
        <MAXkwd>
        ;
<LowerValue> ::=
	<Value>
	;
<UpperValue> ::=
	<Value>
	;
<PermittedAlphabet> ::=
        <FROMkwd> <SubtypeSpec>
        ;
<SizeConstraint> ::=
        <SIZEkwd> <SubtypeSpec>
        ;
<InnerTypeConstraints> ::=
        <InnerTypeConstraint>
        ;
<InnerTypeConstraint> ::|
        <SingleTypeConstraint> |
        <MultipleTypeConstraint>
        ;
<SingleTypeConstraint> ::=
        <WITHkwd> <COMPONENTkwd> <SubtypeSpec>
        ;
<MultipleTypeConstraint> ::=
	<WITHkwd> <COMPONENTSkwd> <MultipleTypeConstraints>
	;
<MultipleTypeConstraints> ::=
        '{' <PartialSpecificationOpt> <TypeConstraints> '}'
        ;
<TypeConstraints> ::+
        <TypeConstraint> ','
        ;
<TypeConstraint> ::|
	<NamedConstraint> |
	<Constraint>
	;
<NamedConstraint> ::=
        <identifier> <Constraint>
        ;
<Constraint> ::=
        <ValueConstraintOpt> <PresenceConstraintOpt>
        ;
<ValueConstraintOpt> ::?
        <ValueConstraint>
        ;
<ValueConstraint> ::=
        <SubtypeSpec>
        ;
<PresenceConstraintOpt> ::?
        <PresenceConstraint>
        ;
<PresenceConstraint> ::|
        <PRESENTkwd> |
        <ABSENTkwd> |
        <OPTIONALkwd>
        ;
(*------VALUES---------------------------------------------------------------*)
<Value> ::|
        <Reference> |
        <IdentifierValue> |
        <BuiltinValue>
        ;
<IdentifierValue> ::=
	<Identifier> ':' <Value>
	(* ':' inserted by JLK to conform to new ASN.1 standard *)
	;
<BuiltinValue> ::|
        <StringBvalue> |
        <StringHvalue> |
        <CharacterStringValue> |
        <BooleanValue> |
        <IntegerValue> |
        <RealValue> |
        <NullValue> |
        <AnyValue> |
        <ListValue>
	;
<StringBvalue> ::=
	<bstring>
	;
<StringHvalue> ::=
	<hstring>
	;
<CharacterStringValue> ::=
        <cstring>
        ;
<BooleanValue> ::|
        <TRUEkwd> |
        <FALSEkwd>
        ;
<IntegerValue> ::=
	<SignedNumber>
	;
<RealValue> ::=
        <SpecialRealValue>
        ;
<SpecialRealValue> ::|
        <PLUSINFINITYkwd> |
        <MINUSINFINITYkwd>
        ;
<NullValue> ::=
        <NULLkwd>
        ;
<AnyValue> ::=
        <Type> ':' <Value>
        (* ':' inserted by JLK to conform to new ASN.1 standard *)
        ;
<ListValue> ::=
	'{' <ListValuesOpt> '}'
	;
<ListValuesOpt> ::?
	<ListValues>
	;
<ListValues> ::=
	<ListValueElement> ','
	;
<ListValueElement> ::|
        <ReferenceSeven> |
        <IdentifierValueOne> |
        <number> |
        <NameAndNumberFormOne>
        ;
<IdentifierValueOne> ::=
	<IdentifierValue>
	;
<NameAndNumberFormOne> ::=
        <NameAndNumberForm>
	;
<NameAndNumberForm> ::=
        <identifier> '(' <NumberForm> ')'
        ;
<NumberForm> ::|
        <FormNumber> |
        <ReferenceFive>
        ;
<FormNumber> ::=
	<number>
	;
<Reference> ::|
        <Internalreference> |
        <Externalreference>
	;
<Externalreference> ::=
        <identifier> '.' <identifier>
        ;
<Internalreference> ::=
	<identifier>
	;
(*------LEXICAL PRODUCTIONS--------------------------------------------------*)
<identifier> ::=
        <nameDecl>
        ;
<number> ::=
        <const>
        ;
<bstring> ::=
        <string>'''B'''
        ;
<hstring> ::=
        <string>'''H'''
        ;
<cstring> ::=
        <string>
        ;
(*------HELP PRODUCTIONS-----------------------------------------------------*)
<ReferenceOne> ::=
	<Reference>
	;
<ReferenceTwo> ::=
	<Reference>
	;
<ReferenceThree> ::=
	<Reference>
	;
<ReferenceFour> ::=
	<Reference>
	;
<ReferenceFive> ::=
	<Reference>
	;
<ReferenceSix> ::=
	<Reference>
	;
<ReferenceSeven> ::=
	<Reference>
	;
<NumberOne> ::=
	<number>
	;
(*------KEYWORDS etc.--------------------------------------------------------*)
<PartialSpecificationOpt> ::? <PartialSpecification>;
<lessOpt> ::? <less> ;
<minusOpt> ::? <minus> ;

<PartialSpecification> ::= '...,' ;
<less> ::= '*' ;
        (* changed '<' to '*' by jlk to avoid SHIFT/REDUCE conflict *)
<minus> ::= '-' ;
<BOOLEANkwd>       ::= 'BOOLEAN' ;
<INTEGERkwd>       ::= 'INTEGER' ;
<BITSTRINGkwd>     ::= 'BIT' 'STRING' ;
<OCTETSTRINGkwd>   ::= 'OCTET' 'STRING' ;
<NULLkwd>          ::= 'NULL' ;
<SEQUENCEkwd>      ::= 'SEQUENCE' ;
<OFkwd>            ::= 'OF' ;
<SETkwd>           ::= 'SET' ;
<IMPLICITkwd>      ::= 'IMPLICIT' ;
<CHOICEkwd>        ::= 'CHOICE' ;
<ANYkwd>           ::= 'ANY' ;
<OBJECTkwd>        ::= 'OBJECT' ;
<IDENTIFIERkwd>    ::= 'IDENTIFIER' ;
<OPTIONALkwd>      ::= 'OPTIONAL' ;
<DEFAULTkwd>       ::= 'DEFAULT' ;
<COMPONENTSkwd>    ::= 'COMPONENTS' ;
<UNIVERSALkwd>     ::= 'UNIVERSAL' ;
<APPLICATIONkwd>   ::= 'APPLICATION' ;
<PRIVATEkwd>       ::= 'PRIVATE' ;
<TRUEkwd>          ::= 'TRUE' ;
<FALSEkwd>         ::= 'FALSE' ;
<BEGINkwd>         ::= 'BEGIN' ;
<ENDkwd>           ::= 'END' ;
<DEFINITIONSkwd>   ::= 'DEFINITIONS' ;
<EXPLICITkwd>      ::= 'EXPLICIT' ;
<ENUMERATEDkwd>    ::= 'ENUMERATED' ;
<EXPORTSkwd>       ::= 'EXPORTS' ;
<IMPORTSkwd>       ::= 'IMPORTS' ;
<REALkwd>          ::= 'REAL' ;
<INCLUDESkwd>      ::= 'INCLUDES' ;
<MINkwd>           ::= 'MIN' ;
<MAXkwd>           ::= 'MAX' ;
<SIZEkwd>          ::= 'SIZE' ;
<FROMkwd>          ::= 'FROM' ;
<WITHkwd>          ::= 'WITH' ;
<COMPONENTkwd>     ::= 'COMPONENT' ;
<PRESENTkwd>       ::= 'PRESENT' ;
<ABSENTkwd>        ::= 'ABSENT' ;
<DEFINEDkwd>       ::= 'DEFINED' ;
<BYkwd>            ::= 'BY' ;
<PLUSINFINITYkwd>  ::= 'PLUS-INFINITY' ;
<MINUSINFINITYkwd> ::= 'MINUS-INFINITY' ;
<TAGSkwd>          ::= 'TAGS'
