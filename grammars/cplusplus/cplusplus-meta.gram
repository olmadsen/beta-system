--- cplusplus : aGrammar : metagrammar ---

Grammar cplusplus:

Option
  version=1
  comBegin='/*'
  comEnd='*/'
  stringChar='"'
  suffix='.c'
  string=unused

Rule

<cppSpec> ::= <DefList>;

<DefList> ::* <ClassDef>;

<ClassDef> ::= <Definition> ';';

<Definition> ::| <ClassDecl>
	       | <TypeDef>
	       | <BugInNewAst>
	       ;

<ClassDecl> ::= 'class' <NameDcl> <BaseClassOpt> <ClassTemplate>;

<ClassTemplate> ::= '{' 'public' ':' <MemberList> '}';

<BaseClassOpt> ::? <BaseClassDecl>;

<BaseClassDecl> ::= ':' 'public' <NameApl>;


<TypeDef> ::= 'typedef' <NameApl> <NameDcl>;


<MemberList> ::* <MemberSpec>;

<MemberSpec> ::= <Members> ';';

<Members> ::= <Type> <DeclList>;

<DeclList> ::+ <Decl> ',';

<Decl> ::| <SimpleDecl>
         | <ArrayDecl>
         | <FuncDecl>
	 ;

<SimpleDecl> ::| <ValueDecl>
	       | <PointerDecl>
	       ;


<ValueDecl> ::= <NameDcl>;


<PointerDecl> ::= '*' <NameDcl>;


<ArrayDecl> ::= <SimpleDecl> '[' <IntegerConst> ']';


<FuncDecl> ::= <SimpleDecl> '(' <OptParams> ')' ;


<OptParams> ::? <ParamList>;

<ParamList> ::+ <Parameter> ',';

<Parameter> ::= <Type> <ParDecl>;

<ParDecl> ::| <NormParDecl>	
	    | <OutParDecl>
	    ;

<NormParDecl> ::= <SimpleDecl>;

<OutParDecl> ::= '*' <SimpleDecl>;


<Type> ::| <Integer>
	 | <Boolean>
	 | <Text>
	 | <Char>
	 | <Real>
	 | <Void>
	 | <NameApl>
	 ;


<Integer> ::= 'int';

<Boolean> ::= 'bool';

<Text> ::= 'text';

<Char> ::= 'char';

<Real> ::= 'real';
  	       
<Void> ::= 'void'; 


<NameDcl> ::= <NameDecl>;

<NameApl> ::= <NameAppl>;

<IntegerConst> ::= <Const>;


<BugInNewAst> ::= <Void>
