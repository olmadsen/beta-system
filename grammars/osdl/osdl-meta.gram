contractioncategories 
	system 
	textualSystemDefinition 
	systemTextualDefinition 	systemTextualDefinitions
	processTextualDefinition	processTextualDefinitions
	procedureTextualDefinition	procedureTextualDefinitions
	serviceTextualDefinition	serviceTextualDefinitions
	blockTextualDefinition  	blockTextualDefinitions
	typeDefinition			typeDefinitions
	gateDefinition			gateDefinitions
	referencedDefinition		referencedDefinitions
	dataDefinition state
	transition action actions terminator task 
	decision answerBranch elseBranch  
	expression select alternative synonym viewDefinition importDefinition 
	timerDefinition variableDefinition structureDefinition 
	generatorDefinition formalConstraintParameter typeDefinition 
	procedureDefinition remoteProcedureSpecification 
	procedureTimerDefinition signalDefinition 
	signalListDefinition signalRouteDefinition serviceTypeDefinition 
	serviceDefinition 
	processTypeDefinition processDefinition processTypeStart 
	processSignature blockDefinition blockTypeDefinition channelDefinition

-- osdl: aGrammar : metagrammar --

Grammar osdl:

Option 
  version = 1
  comBegin = '/*'
  comEnd = '*/'
  stringChar = ''''
  suffix = '.osdl'

     (* the predefined nonterminal nameDecl is unused *)
  nameDecl = unused 

  splitOnFiles = 3 (* the generated cfl will be split on 3 files *) 

Rule

(* OSDL grammar originated from TFL:  *)

<sdlSpecification> ::= <units> <systemOpt> ;
<systemOpt> ::? <system> ;
<system> ::= 
   <unitReferenceOpt> <textualSystemDefinition> <referencedDefinitions> ;
<textualSystemDefinition> ::| <systemDefinition>
                            | <systemInstance> ;
<systemDefinition> ::= 
   'SYSTEM' <systemName> <endd> <systemTextualDefinitions> <enddsystem> ;
<systemName> ::= <ident> ;
<enddsystem> ::= 'ENDSYSTEM' <nameComment> ;
<nameComment> ::= <nameOpt> <endd> ;
<nameOpt> ::? <name> ;
<CTD>  ::| <dataDefinition>
         | <select>
         | <procedureDefinition>
         | <procedureReference> ;
<systemCTD>    ::= <CTD> ;
<processCTD>   ::= <CTD> ;
<procedureCTD> ::= <CTD> ;
<blockCTD>     ::= <CTD> ;
<serviceCTD>   ::= <CTD> ;

<formalConstraint> ::? <formalConstraints> ;

<CWTD> ::| <viewDefinition>
         | <importDefinition>
         | <timerDefinition>
         | <variableDefinition> ;
<processCWTD> ::= <CWTD> ;
<blockCWTD>   ::= <CWTD> ;
<serviceCWTD>   ::= <CWTD> ;

<CCTD> ::| <signalDefinition>
         | <signalListDefinition>
	  | <serviceTypeDefinition>
	  | <serviceTypeReference> ;
 <systemCCTD>  ::= <CCTD> ;
 <processCCTD> ::= <CCTD> ;
 <blockCCTD>   ::= <CCTD> ;

 <CPTD> ::| <processTypeDefinition>
	  | <processTypeReference>;

 <procedureTimerDefinition> ::= <timerDefinition> ;
 <systemCPTD> ::= <CPTD> ;
 <blockCPTD>  ::= <CPTD> ;

 <CSTD> ::| <connection>
	  | <signalRouteDefinition> ;
 <processCSTD> ::= <CSTD> ;
 <blockCSTD>   ::= <CSTD> ;

<systemTextualDefinitions> ::| <emptySystemTextualDefinition> 
			     | <listSystemTextualDefinition> ;
<emptySystemTextualDefinition> ::= <EmptyHackOpt> ;
<listSystemTextualDefinition> ::= 
	<systemTextualDefinitions> <systemTextualDefinition> ;

<systemTextualDefinition>  ::| <systemCTD>
			      | <systemCCTD>
			      | <systemCPTD>
			      | <blockDefinition>
			      | <blockTypeDefinition>
			      | <blockReference>
			      | <blockTypeReference>
			      | <blockInstance>
			      | <channelDefinition> ;

<processTextualDefinitions> ::| <emptyProcessTextualDefinition>
			      | <listProcessTextualDefinition> ;
<emptyProcessTextualDefinition> ::= <EmptyHackOpt>;
<listProcessTextualDefinition> ::= 
	<processTextualDefinitions> <processTextualDefinition> ;

<processTextualDefinition>  ::| <processCTD>
			       | <processCCTD>
			       | <processCSTD>
			       | <processCWTD>
			       | <remoteProcedureSpecification>
			       | <serviceReference>
			       | <serviceInstance> ;

<procedureTextualDefinitions> ::| <emptyProcedureTextualDefinition>
			        | <listProcedureTextualDefinition> ;
<emptyProcedureTextualDefinition> ::= <EmptyHackOpt>;
<listProcedureTextualDefinition> ::= 
	<procedureTextualDefinitions> <procedureTextualDefinition> ;

<procedureTextualDefinition>  ::| <procedureCTD>
				 | <procedureTimerDefinition> ;

<serviceTextualDefinitions> ::| <emptyServiceTextualDefinition>
			      | <listServiceTextualDefinition> ;
<emptyServiceTextualDefinition> ::= <EmptyHackOpt> ;
<listServiceTextualDefinition> ::=
	 <serviceTextualDefinitions>  <serviceTextualDefinition>;

<serviceTextualDefinition>  ::| <serviceCTD> | <serviceCWTD>;

<blockTextualDefinitions> ::| <emptyBlockTextualDefinition> 
			| <listBlockTextualDefinition> ;
<emptyBlockTextualDefinition> ::= <EmptyHackOpt> ;
<listBlockTextualDefinition>::= 
	<blockTextualDefinitions> <blockTextualDefinition> ;

<blockTextualDefinition>  ::| <blockCTD>
			     | <blockCCTD>
			     | <blockCSTD>
			     | <blockCPTD>
			     | <blockCWTD>
			     | <processReference> 
			     | <processInstance>
			     | <processDefinition> ;

<blockDefinition> ::=
    <virtualityOpt> <blockqual> <endd> <blockTextualDefinitions> <enddblock> ;
<enddblock> ::= 'ENDBLOCK' <identComment> ;
<identComment> ::= <identOpt> <endd> ;
<channelDefinition> ::= 
   'CHANNEL' <channelName> <nodelayOpt> <channelDirection> <enddchannel> ;
<channelDirection> ::| <unidirectionalChannel>
		     | <bidirectionalChannel> ;
<unidirectionalChannel> ::= <channelPath> ;
<bidirectionalChannel> ::= <ch1:channelPath> <ch2:channelPath> ;
<channelPath> ::= 'FROM' <fromId:identOrEnv> <fromGate:gate> 
	   'TO' <toId:identOrEnv> <toGate:gate> 'WITH' <signalList> <endd> ;
<identOrEnv> ::| <ident> | <env> ;
<env> ::= 'ENV' ;
<enddchannel> ::= 'ENDCHANNEL' <nameComment> ;
<channelName> ::= <name> ;
<nodelayOpt> ::? <nodelay> ;
<nodelay> ::= 'NODELAY' ;
<signalRouteDefinition> ::= 'SIGNALROUTE' <signalRouteName> <signalRouteDirection> ;
<signalRouteDirection> ::| <unidirectionalSignalRoute>
			  | <bidirectionalSignalRoute> ;
<unidirectionalSignalRoute> ::= <signalRoutePath> ;
<bidirectionalSignalRoute> ::= <sr1:signalRoutePath> <sr2:signalRoutePath> ;


<signalRoutePath> ::= <channelPath>;
 (*<signalRoutePath> ::= 'FROM' <identOrEnv> <gate> 'TO' <identOrEnv> <gate> 'WITH' <signalList> <endd> ;*)

 <signalRouteName> ::= <name> ;
 <connection> ::= 'CONNECT' <connectList:identList> 'AND' <identList> <endd> ;
 <signalDefinition> ::= 'SIGNAL' <signalNameSortLists> <endd> ;
 <signalNameSortLists> ::+ <signalNameSortList> ',' ;
 <signalNameSortList> ::= <signalName> <formalConstraint> <specializationOpt> <parSortListOpt> ;
 <parSortListOpt> ::? <parSortList> ;
 <parSortList> ::= '(' <sortList> ')' ;
 <signalName> ::= <name> ;
 <dataDefinition> ::| <synonymDefinition>
		    | <partialTypeDefinition>
		    | <syntypeDefinition>
		    | <generatorDefinition> ;
 <partialTypeDefinition> ::= 'NEWTYPE' <sortName> <formalConstraint> <extendedPropertiesOpt> <properties> <constants> <enddPartialTypeDefinition> ;
 <extendedPropertiesOpt> ::? <extendedProperties> ;
 <properties> ::= <literalList> <operatorList> <axiomList> <defaultt> ;
 (* <literalListOpt> ::? <literalList> ; *)
 (* <operatorListOpt> ::? <operatorList> ; *)
 (* <axiomListOpt> ::? <axiomList> ; *)
 <defaulttOpt> ::? <defaultt> ;
 <enddPartialTypeDefinition> ::= 'ENDNEWTYPE' <nameComment> ;
 <sortName> ::= <name> ;
 <literalList> ::= 'LITERALS' <literalSignatures> <enddOpt> ;
 <enddOpt> ::? <endd> ;
 <literalSignatures> ::+ <literalSignature> ',' ;
 <literalSignature> ::| <literalName> | <literalNameclass> ;
 <literalNameclass> ::= <nameclass> ;
 <literalName> ::| <nameLiteral> | <stringLiteral> ;
 <nameLiteral> ::= <name> ;
 <stringLiteral> ::= <charstring> ;
 <nameclass> ::= 'NAMECLASS' <regularexpr> ;
 <regularexpr> ::| <partialregexp> | <infixregexp> ;
 <rngregexp> ::= <str1:charstring> ':' <str2:charstring> ;
 <partialregexp> ::= <regularElement> <repOpt> ;
 <repOpt> ::? <rep> ;
 <regularElement> ::| <charstring> | <rngregexp> | <parenthesisregexp> ;
 <rep> ::| <namerep> | <plusoprep> | <multoprep> ;
 <namerep> ::= <name> ;
 <plusoprep> ::= <plusop> ;
 <multoprep> ::= <multop> ;
 <parenthesisregexp> ::= '(' <regularexpr> ')' ;
 <infixregexp> ::= <partialregexp> <oropOpt> <regularexpr> ;
 <oropOpt> ::? <orop> ;
 <operatorList> ::= 'OPERATORS' <operatorSignatures> ;
 <operatorSignatures> ::+ <operatorSignatureOneWithEndd> ;
 <operatorSignatureOneWithEndd> ::= <operatorSignatureOne> <endd>;
 (*<operatorSignatures> ::+ <operatorSignatureOne> <endd>*)
 <operatorSignatureOne> ::| <operatorSignature> | <ordering> | <noequality> ;
 <operatorSignature> ::= <operatorName> <exclOpt> ':' <argumentList> '->' <result> <endd>;
 <exclOpt> ::? <excl> ;
 <argumentList> ::= <sortList> ;
 <result> ::= <sortIdent> ;
 <operatorName> ::| <name> | <quotedName> ;
 <quotedName> ::= <quoted> ;
 <noequality> ::= 'NOEQUALITY' ;
 <axiomList> ::= 'AXIOMS' <axioms> <enddOpt> ;
 <axioms> ::| <axiom> | <axiomscont> ;
 <axiomscont> ::= <axioms> <axiomstail>;
 <axiomstail> ::= <endd> <axiom>;
 (*<axioms> ::+ <axiom> <endd> ;*)
 <axiom> ::| <unquantaxiom> | <quantaxiom> | <condaxiom> ;
 <unquantaxiom> ::| <equation> | <booleanaxiom> ;
 <equation> ::= <t1:term> '==' <t2:term> ;
 <quantaxiom> ::= 'FOR' 'ALL' <nameList> 'IN' <sortIdent> '(' <axiomList> ')' ;
 <condaxiom> ::= <restrictions> '==>' <unquantaxiom> ;
 <restrictions> ::+ <unquantaxiom> ',' ;
 <booleanaxiom> ::= <term> ;
 <term> ::| <operandZeroTerm> | <implyterm> ;
 <implyterm> ::= <term> '=>' <operandZeroTerm> ;
 <operandZeroTerm> ::| <operandOneTerm> | <orterm> | <xorterm> ;
 <orterm> ::= <operandZeroTerm> 'OR' <operandOneTerm> ;
 <xorterm> ::= <operandZeroTerm> 'XOR' <operandOneTerm> ;
 <operandOneTerm> ::| <operandTwoTerm> | <andterm> ;
 <andterm> ::= <operandOneTerm> 'AND' <operandTwoTerm> ;
 <operandTwoTerm> ::| <operandThreeTerm> | <eqlterm> | <neqterm> | <gtrterm> | <geqterm>
		| <lssterm> | <leqterm> | <insideterm> ;
 <eqlterm> ::= <operandTwoTerm> '=' <operandThreeTerm> ;
 <neqterm> ::= <operandTwoTerm> '/=' <operandThreeTerm> ;
 <gtrterm> ::= <operandTwoTerm> '>' <operandThreeTerm> ;
 <geqterm> ::= <operandTwoTerm> '>=' <operandThreeTerm> ;
 <lssterm> ::= <operandTwoTerm> '<' <operandThreeTerm> ;
 <leqterm> ::= <operandTwoTerm> '<=' <operandThreeTerm> ;
 <insideterm> ::= <operandTwoTerm> 'IN' <operandThreeTerm> ;
 <operandThreeTerm> ::| <operandFourTerm> | <plusterm> | <minusterm> | <concatterm> ;
 <plusterm> ::= <operandThreeTerm> '+' <operandFourTerm> ;
 <minusterm> ::= <operandThreeTerm> '-' <operandFourTerm> ;
 <concatterm> ::= <operandThreeTerm> '//' <operandFourTerm> ;
 <operandFourTerm> ::| <operandFiveTerm> | <multterm> | <divterm> | <modterm> | <remterm> ;
 <multterm> ::= <operandFourTerm> '*' <operandFiveTerm> ;
 <divterm> ::= <operandFourTerm> '/' <operandFiveTerm> ;
 <modterm> ::= <operandFourTerm> 'MOD' <operandFiveTerm> ;
 <remterm> ::= <operandFourTerm> 'REM' <operandFiveTerm> ;
 <operandFiveTerm> ::| <primaryterm> | <uminusterm> | <notterm> ;
 <uminusterm> ::= '-' <primaryterm> ;
 <notterm> ::= 'NOT' <primaryterm> ;
 <primaryterm> ::| <identterm> | <parterm> | <stringIdentTerm> | <constTerm>
	       | <operatorterm> | <ifterm> | <errorterm> ;
 <identterm> ::= <ident> ;
 <parterm> ::= '(' <term> ')' ;
 <stringIdentTerm> ::= <stringIdent> ;
 <constTerm> ::= <const> ;
 <operatorterm> ::= <operatorIdent> <exclOpt> '(' <termList> ')' ;
 <operatorIdent> ::| <identOperator> | <quotedOperator> ;
 <identOperator> ::= <ident> ;
 <quotedOperator> ::= <qualifier> <quoted> ;
 <stringIdent> ::= <qualifier> <charstring> ;
 <excl> ::= '!' ;
 <termList> ::+ <term> ',' ;
 <ifterm> ::= 'IF' <ifPart:term> 'THEN' <thenPart:term> 'ELSE' <elsePart:term> 'FI' ;
 <errorterm> ::= 'ERROR' '!' ;
 <rangeConditions> ::+ <range> ',' ;
 <range> ::= <relopOpt> <expression> <rangeToOpt> ;
 <relopOpt> ::? <relop> ;
 <rangeToOpt> ::? <rangeTo>;
 <rangeTo> ::= ':' <expression>;
 <relop> ::| <eqrelop> | <nerelop> | <ltrelop> | <gtrelop> | <lerelop>
	   | <gerelop> ; 
 <eqrelop> ::= <eqop> ;
 <nerelop> ::= <neop> ;
 <ltrelop> ::= <ltop> ;
 <gtrelop> ::= <gtop> ;
 <lerelop> ::= <leop> ;
 <gerelop> ::= <geop> ;
<processDefinition> ::= <ProcessQual> <endd> 
	<formalParametersOpt> <validInputSignalSet> 
	<processTextualDefinitions> <processBody> <enddprocess> ;
<processQual> ::| <simpleProcessQual> | <virtualProcessQual>
	     | <finalizedProcessQual> | <redefinedProcessQual> ;
<simpleProcessQual> ::=  'PROCESS' <ident>  <numberOfInstancesOpt>  ;
<virtualProcessQual> ::=  'VIRTUAL' 'PROCESS' <ident> <numberOfInstancesOpt>  ;
<finalizedProcessQual> ::= 
	 'FINALIZED' 'PROCESS' <ident> <numberOfInstancesOpt>  ;
<redefinedProcessQual> ::= 
	'REDEFINED' 'PROCESS' <ident> <numberOfInstancesOpt>  ;

 <processBody> ::= 'START' <endd> <transition> <stateBodyList> ;
 <validInputSignalSet> ::= 'SIGNALSET' <signalListOpt> <endd> ;
 <signalListOpt> ::? <signalList> ;
 <enddprocess> ::= 'ENDPROCESS' <identComment> ;
 <numberOfInstances> ::= '(' <e1:expressionOpt> ',' <e2:expressionOpt> ')' ;
 <expressionOpt> ::? <expression> ;
 <formalParameters> ::= 'FPAR' <formalParametersOne> <endd> ;
 <formalParametersOpt> ::? <formalparameters> ;
 <formalParametersOne> ::+ <formalParameter> ',' ;
 <formalParameter> ::= <variableNameList> <sortIdent> ;
 <variableNameList> ::= <nameList> ;
<procedureDefinition> ::= <ProcDefQual> <enddOpt> <formalConstraint> 
	 <virtualityConstraint> <specializationOpt> 
	 <procedureFormalParametersOpt> <returnsOpt> 
	 <procedureTextualDefinitions> <procedureBody> <enddprocedure> ;
<ProcDefQual> ::| <simpleProcDefQual> | <simpleExportedProcDefQual> 
                | <virtualProcDefQual> | <virtualExportedProcDefQual>
                | <redefinedProcDefQual> | <redefinedExportedProcDefQual> 
                | <finalizedProcDefQual> | <finalizedExportedProcDefQual> ;
<simpleProcDefQual> ::= 'PROCEDURE' <procedureIdent> ;
<simpleExportedProcDefQual>  ::= 'EXPORTED' 'PROCEDURE' <procedureIdent> ;
<virtualProcDefQual> ::= 'VIRTUAL' 'PROCEDURE' <procedureIdent>;
<virtualExportedProcDefQual> ::= 'VIRTUAL' 'EXPORTED' 'PROCEDURE' <procedureIdent>;
<redefinedProcDefQual> ::= 'REDEFINED' 'PROCEDURE' <procedureIdent>;
<redefinedExportedProcDefQual> ::= 'REDEFINED' 'EXPORTED' 'PROCEDURE' <procedureIdent>;
<finalizedProcDefQual> ::='FINALIZED' 'PROCEDURE' <procedureIdent>;
<finalizedExportedProcDefQual> ::= 'FINALIZED' 'EXPORTED' 'PROCEDURE' <procedureIdent>;
<specializationOpt> ::? <specialization> ;
<procedureFormalParametersOpt> ::? <procedureFormalParameters> ;
<returnsOpt> ::? <returns> ;
<procedureBody> ::= <processTypeBody> ;
<enddprocedure> ::= 'ENDPROCEDURE' <identComment> ;
<procedureIdent> ::= <ident> ;
<procedureFormalParameters> ::= 'FPAR' <procedureFormalParametersOne> <endd> ;
<procedureFormalParametersOne> ::+ <procedureFormalParameter> ',' ;
<procedureFormalParameter> ::= <inoutalt> <variableNameList> <sortIdent> ;
<inoutalt> ::| <innOpt> | <inout> ;
<innOpt> ::? <inn> ;
<inn> ::= 'IN' ;
<inout> ::= 'IN' '/' 'OUT' ;
<remoteProcedureSpecification> ::= 
   'REMOTE' 'PROCEDURE' <procedureIdent> <rend:endd> 
   <procedureFormalParametersOpt> <returnsOpt> <endd> ;
<variableDefinition> ::= 'DCL' <visibilityOpt> <variableDefinitionOne> <endd>;
<visibilityOpt> ::? <visibility> ;
<visibility> ::| <revealed> | <exported> 
               | <revealedExported> | <exportedRevealed> ;
<revealed> ::= 'REVEALED' ;

<exported> ::= 'EXPORTED' ;
<revealedExported> ::= 'REVEALED' 'EXPORTED' ;
<exportedRevealed> ::= 'EXPORTED' 'REVEALED' ;
 <variableDefinitionOne> ::+ <varDef> ',' ;
 <varDef> ::= <variableNameList> <sortIdent> <varInitOpt> ;
 <varInitOpt> ::? <varInit> ;
 <varInit> ::= ':=' <groundExpression> ;
 <viewDefinition> ::= 'VIEWED' <viewDefinitionOne> <endd> ;
 <viewDefinitionOne> ::+ <viewDef> ',' ;
 <viewDef> ::= <identList> <sortIdent> ;
 <importDefinition> ::= 'IMPORTED' <importDefinitionOne> <endd> ;
 <importDefinitionOne> ::+ <importDef> ',' ;
 <importDef> ::= <importName> <sortIdent> ;
 <importName> ::= <name> ;
 <timerDefinition> ::= 'TIMER' <timerDefinitionOne> <endd> ;
 <timerDefinitionOne> ::+ <timerDef> ',' ;
 <timerDef> ::= <timerName> <formalConstraint> <specializationOpt> <parSortListOpt> ;
 <timerName> ::= <name> ;
 <stateBodyList> ::* <state>  ;
 <state> ::= 'STATE' <stateList> <endd> <inputSaveContinuList> <enddstateOpt> ;
 <enddstateOpt> ::? <enddstate> ;
 <inputSaveContinuList> ::* <inputSaveContinu>  ;
 <inputSaveContinu> ::| <inputPart> | <savePart> | <continuousSignal>
		      | <priorityInput> | <spontaneousTransition> ;
 <enddstate> ::= 'ENDSTATE' <nameCommentOpt> ;
 <nameCommentOpt> ::? <nameComment> ;
 <stateList> ::| <stateListIncludedOpt> | <stateListExcluded> ;
 <stateListExcluded> ::= '*' <stateParanListOpt> ;
 <stateParanListOpt> ::? <stateParanList> ;
 <stateParanList> ::= '(' <stateParanListIncluded> ')' ;
 <stateParanListIncluded> ::= <stateListIncludedOpt> ;
 <stateListIncludedOpt> ::= <stateListIncluded> ;
 <stateListIncluded> ::+ <stateName> ',' ;
 <stateName> ::= <name> ;
 <inputPart> ::= <virtualityOpt> 'INPUT' <inputAlt> <endd> <enablingConditionOpt> <transition> ;
 <inputAlt> ::| <asteriskInputList> | <inputListAlt> ;
 <asteriskInputList> ::= '*' ;
 <inputListAlt> ::= <inputList> ;
 <inputList> ::+ <stimulus> ',' ;
 <stimulus> ::= <signalIdent> <inputParameterParanListOpt> ;
 <inputParameterParanListOpt> ::? <inputParameterParanList> ;
 <inputParameterParanList> ::= '(' <inputParameterList> ')' ;
 <inputParameterList> ::+ <inputParameter> ',' ;
 <inputParameter> ::? <variable> ;
 <enablingCondition> ::= 'PROVIDED' <expression> <endd> ;
 <enablingConditionOpt> ::? <enablingCondition> ;
 <savePart> ::= <virtualityOpt> 'SAVE' <saveList> <endd> ;
 <saveList> ::| <asteriskSaveList> | <signalListAlt> ;
 <signalListAlt> ::= <signalList> ;
 (* <saveListAlt> ::= <saveList> ; *)
 <asteriskSaveList> ::= '*' ;
 <continuousSignal> ::= <virtualityOpt> 'PROVIDED' <continuousContent> <endd> <transition> ;
 <continuousContent> ::= <expression> <endd> <contPriorityOpt> ;
 <contPriority> ::= 'PRIORITY' <ident> <endd> ;
 <contPriorityOpt> ::? <contPriority> ;
 <spontaneousTransition> ::= <virtualityOpt> 'INPUT' 'NONE' <enablingConditionOpt> <transition> ;
 <actionstmt> ::| <action> | <labeledActionstmt> ;
 <labeledActionstmt> ::= <labelName> ':' <action> ;
 <terminatorstmt> ::| <terminator> | <labeledTerminatorstmt> ;
 <labeledTerminatorstmt> ::= <labelName> ':' <terminator> ;
 <action> ::| <task> | <output> | <createRequest> | <set> | <reset> | <export> | <decision> | <alternative> | <procedureCall> | <remoteProcedureCall> | <priorityOutput> ;
 <terminator> ::| <nextstate> | <join> | <stop> | <return> | <valueReturn> ;
 <nextstate> ::= 'NEXTSTATE' <stateNameAlt> <endd> ;
 <stateNameAlt> ::| <implicitstate> | <stateName> ;
 <implicitstate> ::= '-' ;
 <join> ::= 'JOIN' <labelName> <endd> ;
 <stop> ::= 'STOP' <endd> ;
 <return> ::= 'RETURN' <endd> ;
 <labelName> ::= <name> ;
 <task> ::= 'TASK' <taskContent> <endd> ;
 <taskContent> ::+ <statementElem> ',' ;
 <statementElem> ::| <assignmentStatement> | <informalText> ;
 <assignmentStatement> ::= <variable> ':=' <expression> ;
 <informalText> ::= <charstring> ;
 <variable> ::| <identvar> | <indexedvar> | <fieldvar> ;
 <identvar> ::= <ident> ;
 <indexedvar> ::= <variable> '(' <expressionList> ')' ;
 <fieldvar> ::= <variable> '!' <fieldName> ;
 <set> ::= 'SET' <setList> <endd> ;
 <setList> ::+ <setStatement> ',' ;
 <setStatement> ::= '(' <expression> ',' <timerIdent> <expressionParanListOpt> ')' ;
 <expressionParanListOpt> ::? <expressionParanList> ;
 <expressionParanList> ::=  '(' <expressionList> ')' ;
 <timerIdent> ::= <ident> ;
 <reset> ::= 'RESET' '(' <resetList> ')' <endd> ;
 <resetList> ::+ <resetElem> ',' ;
 <resetElem> ::= <timerIdent>  <expressionParanListOpt> ;
 <createRequest> ::= 'CREATE' <ident> <actualParameterParanListOpt> <endd> ;
 <actualParameterParanListOpt> ::? <actualParameterParanList> ;
 <actualParameterParanList> ::= '(' <actualParameterList> ')' ;
 <procedureCall> ::= 'CALL' <procedureIdent> <actualParameterParanListOpt> <endd> ;
 <remoteProcedureCall> ::= 'CALL' <procedureIdent> <actualParameterParanListOpt> ',' <pidExpression> <endd> ;
 <output> ::= 'OUTPUT' <outputList> <toPidOpt> <viaOpt> <endd> ;
 <outputList> ::+ <outputSignal> ',' ;
 <outputSignal> ::= <signalIdent> <actualParameterParanListOpt> ;
 <via> ::= 'VIA' <allOpt> <identList> ;
 <viaOpt> ::? <via> ;
 <all> ::= 'ALL' ;
 <allOpt> ::? <all> ;
 <toPidOpt> ::? <toPid> ;
 <toPid> ::= 'TO' <pidExpression> ;
 <decision> ::= 'DECISION' <question> <endd> <answerBranchList> <elseBranchOpt> 'ENDDECISION' <endd> ;
 <question> ::| <expression> | <anyAnswer> ;
 <anyAnswer> ::= 'ANY' ;
 <answerBranchList> ::* <answerBranch>  ;
 <answerBranch> ::= '(' <rangeConditions> ')' ':' <transition> ;
 <elseBranch> ::= 'ELSE' ':' <transition> ;
 <elseBranchOpt> ::? <elseBranch> ;
 <export> ::= 'EXPORT' '(' <exportList> ')' <endd> ;
 <exportList> ::+ <variableIdent> ',' ;
 <actualParameterList> ::+ <actualParameter> ',' ;
 <actualParameter> ::? <expression> ;
 <expression> ::| <operandZero> | <imply> ;
 <imply> ::= <expression> '=>' <operandZero> ;
 <operandZero> ::| <operandOne> | <orr> | <xor> ;
 <orr> ::= <operandZero> 'OR' <operandOne> ;
 <xor> ::= <operandZero> 'XOR' <operandOne> ;
 <operandOne> ::| <operandTwo> | <andd> ;
 <andd> ::= <operandOne> 'AND' <operandTwo> ;
 <operandTwo> ::| <operandThree> | <eql> | <neq> | <gtr> | <geq> | <lss> | <leq> | <inside> ;
 <eql> ::= <operandTwo> '=' <operandThree> ;
 <neq> ::= <operandTwo> '/=' <operandThree> ;
 <gtr> ::= <operandTwo> '>' <operandThree> ;
 <geq> ::= <operandTwo> '>=' <operandThree> ;
 <lss> ::= <operandTwo> '<' <operandThree> ;
 <leq> ::= <operandTwo> '<=' <operandThree> ;
 <inside> ::= <operandTwo> 'IN' <operandThree> ;
 <operandThree> ::| <operandFour> | <plus> | <minus> | <concat> ;
 <plus> ::= <operandThree> '+' <operandFour> ;
 <minus> ::= <operandThree> '-' <operandFour> ;
 <concat> ::= <operandThree> '//' <operandFour> ;
 <operandFour> ::| <operandFive> | <mult> | <divide> | <modulus> | <rem> ;
 <mult> ::= <operandFour> '*' <operandFive> ;
 <divide> ::= <operandFour> '/' <operandFive> ;
 <modulus> ::= <operandFour> 'MOD' <operandFive> ;
 <rem> ::= <operandFour> 'REM' <operandFive> ;
 <operandFive> ::| <primary> | <uminus> | <nott> ;
 <uminus> ::= '-' <primary> ;
 <nott> ::= 'NOT' <primary> ;
 <primary> ::| <identpri> | <parexpr> | <stringIdent> | <operatorapp> | <field> 	    | <primaryConst>
	     | <struct> | <iff> | <view> | <import> | <active> | <now> | <self>
	     | <parent> | <offspring> | <sender> | <valueCall> | <any> ;
 <identpri> ::= <ident> ;
 <primaryConst> ::= <const> ;
 <parexpr> ::= '(' <expression> ')' ;
 <operatorapp> ::= <exprOrQuoted> '(' <expressionList> ')' ;
 <exprOrQuoted> ::| <primaryExp> | <quotedOperatorExp> ;
 <primaryExp> ::= <primary> ;
 <quotedOperatorExp> ::= <quotedOperator> ;
 <field> ::= <primary> '!' <fieldName> ;
 <struct> ::= <qualifier> '(.' <expressionList> '.)' ;
 <iff> ::= 'IF' <expression> 'THEN' <expression> 'ELSE' <expression> 'FI' ;
 <view> ::= 'VIEW' '(' <variableIdent> <commaPidExpressionOpt> ')' ;
 <commaPidExpressionOpt> ::? <commaPidExpression> ;
 <commaPidExpression> ::= ',' <pidExpression>  ;
 <import> ::= 'IMPORT' '(' <variableIdent> <commaPidExpressionOpt> ')' ;
 <active> ::= 'ACTIVE' '(' <timerIdent> <expressionParanList> ')' ;
 <now> ::= 'NOW' ;
 <self> ::= 'SELF' ;
 <parent> ::= 'PARENT' ;
 <offspring> ::= 'OFFSPRING' ;
 <sender> ::= 'SENDER' ;
 <any> ::= 'ANY' '(' <sortIdent> ')' ;
 <pidExpression> ::= <expression> ;
 <groundExpression> ::= <expression> ;
 <quoted> ::= '"' <quotop> '"' ;
 <quotop> ::| <orop> | <xorop> | <andop> | <eqop> | <neop> | <gtop> | <geop> | <ltop>
	  | <leop> | <inop> | <plusop> | <minop> | <concop> | <multop> | <divop>
	  | <modop> | <remop> | <notop> ;
 <orop> ::= 'OR' ;
 <xorop> ::= 'XOR' ;
 <andop> ::= 'AND' ;
 <eqop> ::= '=' ;
 <neop> ::= '/=' ;
 <gtop> ::= '>' ;
 <geop> ::= '>=' ;
 <ltop> ::= '<' ;
 <leop> ::= '<=' ;
 <inop> ::= 'IN' ;
 <plusop> ::= '+' ;
 <minop> ::= '-' ;
 <concop> ::= '//' ;
 <multop> ::= '*' ;
 <divop> ::= '/' ;
 <modop> ::= 'MOD' ;
 <remop> ::= 'REM' ;
 <notop> ::= 'NOT' ;
<ident> ::| <nameIdent> | <qualifiedName>;
<nameIdent> ::= <name>;
<qualifiedName> ::= <qualifier> <name> ;
<qualifier> ::+ <qualifierelem> '/' ;
<qualifierelem> ::| <unitqual> | <systemqual> | <blockqual> | <servicequal>
		 | <processQualifier> 
		 | <procedurequal>
		 | <sortqual> ;
 <unitqual> ::= 'UNIT' <ident> ;
 <systemqual> ::= 'SYSTEM' <ident> ;
 <blockqual> ::= 'BLOCK' <ident> ;
 <servicequal> ::= 'SERVICE' <ident> ;
 <processQualifier> ::= 'PROCESS' <ident> ;
 <procedurequal> ::= 'PROCEDURE' <ident> ;
 <sortqual> ::= 'TYPE' <ident> ;
 <systemtypequal> ::= 'SYSTEM' 'TYPE' <ident> ;
<blocktypequal> ::= 'BLOCK' 'TYPE' <ident> ;
<serviceTypeQual> ::| <simpleServiceTypeQual> | <virtualServiceTypeQual>
	     | <redefinedServiceTypeQual> | <finalizedServiceTypeQual> ;
<simpleServiceTypeQual> ::= 'SERVICE' 'TYPE' <ident> ;
<virtualServiceTypeQual> ::= 'VIRTUAL' 'SERVICE' 'TYPE' <ident> ;
<redefinedServiceTypeQual> ::= 'REDEFINED' 'SERVICE' 'TYPE' <ident> ;
<finalizedServiceTypeQual> ::= 'FINALIZED' 'SERVICE' 'TYPE' <ident> ;

<processTypeQual> ::| <simpleProcessTypeQual> | <virtualProcessTypeQual>
	     | <finalizedProcessTypeQual> | <redefinedProcessTypeQual> ;
<simpleProcessTypeQual> ::=  'PROCESS' 'TYPE' <ident> ;
<virtualProcessTypeQual> ::=  'VIRTUAL' 'PROCESS' 'TYPE' <ident> ;
<finalizedProcessTypeQual> ::=  'FINALIZED' 'PROCESS' 'TYPE' <ident> ;
<redefinedProcessTypeQual> ::=  'REDEFINED' 'PROCESS' 'TYPE' <ident> ;

<procedureTypeQual> ::| <simpleProcedureTypeQual> | <virtualProcedureTypeQual>
	     | <finalizedProcedureTypeQual> | <redefinedProcedureTypeQual> ;
<simpleProcedureTypeQual> ::=  'PROCEDURE' <ident> ;
<virtualProcedureTypeQual> ::=  'VIRTUAL' 'PROCEDURE' <ident> ;
<finalizedProcedureTypeQual> ::=  'FINALIZED' 'PROCEDURE' <ident> ;
<redefinedProcedureTypeQual> ::=  'REDEFINED' 'PROCEDURE' <ident> ;

<textualSystemTypeDefinition> ::= 
	 'SYSTEM' <systemName> <endd> <formalConstraint> <specializationOpt> 
	 <systemTextualDefinitions> <enddsystem> ;

<blockTypeDefinition> ::= 
	 <virtualityOpt> <blocktypequal> <formalConstraint> 
	 <virtualityConstraint> <specializationOpt> <endd> <gateDefinitions> 
	 <blockTextualDefinitions> <enddblock> ;

<processTypeDefinition> ::= 
	 <processTypeQual> <formalConstraint> 
	 <virtualityConstraint> <specializationOpt> <endd> 
	 <formalParametersOpt> <validInputSignalSetOpt> <gateDefinitions> 
	 <processTextualDefinitions> <processTypeBody> <enddprocess> ;

 <validInputSignalSetOpt> ::? <validInputSignalSet> ;

 <serviceTypeDefinition> ::= 
	 <serviceTypeQual> <formalConstraint> 
	 <virtualityConstraint> <specializationOpt> <endd> 
	 <validInputSignalSetOpt> <gateDefinitions> 
	 <serviceTextualDefinitions> <serviceTypeBody> <enddservice> ;

<blockReference> ::= <virtualityOpt> 'BLOCK' <ident> 'REFERENCED' <endd> ;
<processReference> ::=  <processQual> 'REFERENCED' <endd> ;
(*<processReferenceQual> ::| <simpleProcessRefQual> | <virtualProcessRefQual>
	         | <finalizedProcessRefQual> | <redefinedProcessRefQual> ;
<simpleProcessRefQual> ::= 'PROCESS' <ident> ;
<virtualProcessRefQual> ::= 'VIRTUAL' 'PROCESS' <ident> ;
<finalizedProcessRefQual> ::= 'FINALIZED' 'PROCESS' <ident> ;
<redefinedProcessRefQual> ::= 'REDEFINED' 'PROCESS' <ident> ;*)

 <serviceReference> ::= 'SERVICE' <ident> 'REFERENCED' <endd> ;
 <procedureReference> ::= <procedureTypeQual> 'REFERENCED' <endd> ;
 <systemTypeReference> ::= <systemtypequal> 'REFERENCED' <endd> ;
 <blockTypeReference> ::= <virtualityOpt> <blocktypequal> 'REFERENCED' <endd> ;
 <processTypeReference> ::= <processTypeQual> 'REFERENCED' <endd> ;
 <serviceTypeReference> ::= <serviceTypeQual> 'REFERENCED' <endd> ;
 <processTypeBody> ::= <processTypeStartOpt> <stateBodyList> ;
 <processTypeStart> ::| 
	<simpleStart> | <virtualStart> | <finalizedStart> | <redefinedStart>;
<simpleStart> ::= 'START'  <endd> <transition> ;
<virtualStart> ::= 'VIRTUAL' 'START' <endd> <transition>  ;
<finalizedStart> ::= 'FINALIZED' 'START' <endd> <transition>  ;
<redefinedStart> ::= 'REDEFINED' 'START' <endd> <transition>  ;

<processTypeStartOpt> ::? <processTypeStart> ;
<serviceTypeBody> ::= <processTypeBody> ;
<select> ::| <startSelect> | <enddSelect> ;
<startSelect> ::= 'SELECT' 'IF' '(' <groundExpression> ')' <endd> ;
<enddSelect> ::= 'ENDSELECT' <endd> ;
<alternative> ::= 'ALTERNATIVE' <groundExpression> <endd> <answerBranchList> <elseBranchOpt> 'ENDALTERNATIVE' <endd> ;
<signalListDefinition> ::= 'SIGNALLIST' <signalListName> '=' <signalList> <endd> ;
<signalList> ::+ <signalItem> ',' ;
<signalItem> ::| <signalIdent> | <signalListReference> ;
<signalListReference> ::= '(' <signalListIdent> ')' ;
<signalIdent> ::= <ident> ;
<signalListIdent> ::= <ident> ;
<signalListName> ::= <name> ;
<syntypeDefinition> ::= 'SYNTYPE' <syntypeName> '=' <sortIdent> <defaulttOpt> <constantsOpt> 'ENDSYNTYPE' <nameComment> ;
<constants> ::= 'CONSTANTS' <rangeConditions> ;
<constantsOpt> ::? <constants> ;
<syntypeName> ::= <name> ;
<synonymDefinition> ::= 'SYNONYM' <synonymName> <sortIdentOpt> '=' <exprOrExternal> <endd> ;
<exprOrExternal> ::| <groundexpression> | <externaldef>;
<externalDef> ::= 'EXTERNAL';
<synonymName> ::= <name> ;
<generatorDefinition> ::= 'GENERATOR' <generatorName> '(' <generatorParameterList> ')' <generatorInstantiationsOpt> <propertiesOpt> <enddGeneratorDefinition> ;
<generatorInstantiationsOpt> ::? <generatorInstantiations> ;
<propertiesOpt> ::? <properties> ;
<enddGeneratorDefinition> ::= 'ENDGENERATOR' <nameComment> ;
<generatorName> ::= <name> ;
<structureDefinition> ::= 'STRUCT' <fieldList> <enddOpt> <addingOpt> ; 

<fieldList> ::| <fields> | <fieldscont> ;
<fieldscont> ::= <fieldList> <fieldstail>;
<fieldstail> ::= <endd> <fields>;

(*<fieldList> ::+ <fields> <endd> ;*)
<fields> ::= <fieldNames> <fieldSort> ;
<fieldNames> ::+ <fieldName> ',' ;
<fieldSort> ::= <sortIdent> ;
<fieldName> ::= <name> ;
<sortIdent> ::= <ident> ;
<sortIdentOpt> ::? <sortIdent> ;
<adding> ::= 'ADDING' ;
<addingOpt> ::? <adding> ;
<extendedProperties> ::| <structureDefinition> | <specialization>
                      | <generatorInstantiationsAlt> ;
<generatorInstantiationsAlt> ::= <generatorInstantiations> ;
<defaultt> ::= 'DEFAULT' <expression> <enddOpt> ;
<ordering> ::= 'ORDERING' ;
<generatorInstantiations> ::+ <generatorInstantiation> ;
<generatorInstantiation> ::= <generatorIdent> '(' <generatorActualList> ')' <enddOpt> <addingOpt> ;
<generatorActualList> ::+ <generatorActual> ',' ;
<generatorActual> ::| <term> | <nameclass> | (* <quoted> | *) <exclop> ;
<generatorIdent> ::= <ident> ;
<exclop> ::= <operatorIdent> '!' ;

<specialization> ::| <specializationX> | <specializationY> ;
<specializationX> ::=
	'INHERITS' <typeExpression> <formalConstraints> <addingOpt> ;
<specializationY> ::=
	'INHERITS' <typeExpression> <addingOpt> ;

<typeExpression> ::= <supertype> <actualConstraintsOpt> ;
<actualConstraintsOpt> ::? <bracketedActualConstraints> ;
<bracketedActualConstraints> ::= '<' <actualConstraints> '>' ;
<supertype> ::= <ident> ;
<identOpt> ::? <ident> ;
<actualConstraints> ::+ <identOpt> ',' ;

<formalConstraints> ::= <addingOpt> '<' <formalConstraintParameters> '>' ;

<formalConstraintParameters> ::+ <formalConstraintParameterOpt> ';' ;
(*<formalConstraintParameters> ::+ <formalConstraintParameterOpt> <endd> ;*)
<formalConstraintParameterOpt> ::? <formalConstraintParameter> ;
<formalConstraintParameter> ::| <sortConstraintParameter>
                              | <processConstraintParameter>
                              | <procedureConstraintParameter>
                              | <signalConstraintParameter>
                              | <variableConstraintParameter>
                              | <timerConstraintParameter>
                              | <synonymConstraintParameter> ;
<processConstraintParameter> ::= 'PROCESS' <nameList> <processConstraint> ;
<processConstraint> ::| <processAtleastConstraint> | <processSignature> ;
<processAtleastConstraint> ::= <atleastConstraint> ;
(* <processSignatureOpt> ::? <processSignature> ; *)
<processSignature> ::| <validInputSignalSetSignature>
                    | <formalParametersSignature>
                    | <formalProcessSignature> ;
<formalProcessSignature> ::= <formalParametersSignature> <validInputSignalSetSignature> ;
<formalParametersSignature> ::= 'FPAR' <formalParameterlistSignature> <endd> ;
<formalParameterlistSignature> ::+ <formalParameterConstraint> ',' ;
<formalParameterConstraint> ::= <identOpt> ;
<validInputSignalSetSignature> ::= 'SIGNALSET' <signalListOpt> <endd> ;
<procedureConstraintParameter> ::= 'PROCEDURE' <nameList> <procedureConstraintOpt> ;
<procedureConstraint> ::| <procedureAtleastConstraint> | <procedureSignature> ;
<procedureAtleastConstraint> ::= <atleastConstraint> ;
<procedureConstraintOpt> ::? <procedureConstraint> ;
<procedureSignature> ::= 'FPAR' <procedureParameterConstraint> ;
<procedureParameterConstraint> ::+ <variableParameterConstraint> ',' ;
<variableParameterConstraint> ::= <inoutalt> <sortIdent> ;
<signalConstraintParameter> ::= 'SIGNAL' <name> <signalConstraint> ;
<signalConstraint> ::| <signalAtleastConstraint> | <signalSignature> ;
<signalAtleastConstraint> ::= <atleastConstraint> ;
<signalSignature> ::? <parSortList> ;
<variableConstraintParameter> ::= 'DCL' <variableNameList> <variableConstraint> ;
<variableConstraint> ::= <sortIdent> ;
<timerConstraintParameter> ::= 'TIMER' <nameList> ':' <timerConstraintOpt> ;
<timerConstraintOpt> ::? <timerConstraint> ;
<timerConstraint> ::| <timerAtleastConstraint> | <parSortListAlt> ;
<parSortListAlt> ::= <parSortList> ;
<timerAtleastConstraint> ::= <atleastConstraint> ;
<synonymConstraintParameter> ::= 'SYNONYM' <nameList> <synonymConstraint> ;
<synonymConstraint> ::= <sortIdent> ;
<sortConstraintParameter> ::= 'NEWTYPE' <name> <sortConstraint> 'ENDNEWTYPE' <nameOpt> ;
<sortConstraint> ::| <sortAtleastConstraint> | <operatorsOpt> ;
<sortAtleastConstraint> ::= <atleastConstraint> ;
<operatorsOpt> ::= <operators> ;
<operators> ::| <operatorList> | <literalList> | <operatorsList> ;
<operatorsList> ::= <literalList> <operatorList> ;
<virtualityOpt> ::? <virtuality> ;
<virtuality> ::| <redefined> | <virtual> | <finalized> ;
<virtual> ::= 'VIRTUAL' ;
<finalized> ::= 'FINALIZED' ;
<redefined> ::= 'REDEFINED' ;
<virtualityConstraint> ::? <atleastConstraint> ;

<serviceDefinition> ::= 
	'SERVICE' <ident> <endd> <validInputSignalSetOpt> 
	<serviceTextualDefinitions> <serviceBody> <enddservice> ;

<serviceBody> ::= <processBody> ;
<enddservice> ::= 'ENDSERVICE' <identComment> ;
<priorityInput> ::= 'PRIORITYX' <inputList> <endd> <transition> ;
<priorityOutput> ::= 'PRIORITY' <outputList> <endd> ;
<unitReference> ::= 'USE' <unitItems> <endd> ;
<unitItems> ::+ <unitItem> ',' ;
<unitItem> ::= <unitName> <componentSelections> ;
<componentSelections> ::* <componentSelection>  ;
<componentSelection> ::= '!' <keyword> <ident> <asNameOpt> ;
<asNameOpt> ::? <asName> ;
<asName> ::= 'AS' <name> ;
<keyword> ::| <keyProcess> | <keyProcedure> | <keySystem> | <keyBlock> | <keyService> 
          | <keySynonym> | <keyNewtype> | <keySignallist> | <keySignal>
          | <keyTimer> | <keyGenerator> ;
<keyProcess> ::= 'PROCESS' ;
<keyProcedure> ::= 'PROCEDURE' ;
<keySystem> ::= 'SYSTEM' ;
<keyBlock> ::= 'BLOCK' ;
<keyService> ::= 'SERVICE' ;
<keySynonym> ::= 'SYNONYM' ;
<keyNewtype> ::= 'NEWTYPE' ;
<keySignallist> ::= 'SIGNALLIST' ;
<keySignal> ::= 'SIGNAL' ;
<keyTimer> ::= 'TIMER' ;
<keyGenerator> ::= 'GENERATOR' ;
<units> ::| <emptyUnits> | <listUnits> ;
<emptyUnits> ::=  <emptyHackOpt> ;
<listUnits> ::= <units> <unitDefinition> ;
<unitDefinition> ::= <textualUnitDefinition> <referencedDefinitions> ;
<textualUnitDefinition> ::= <unitReferenceOpt> 'UNIT' <unitName> <endd> <typeDefinitions> <enddunit> ;
<unitReferenceOpt> ::? <unitReference> ;
<unitName> ::= <name> ;
<enddunit> ::= 'ENDUNIT' <nameComment> ;
<typeDefinitions> ::| <emptyTypeDefinition> | <listTypeDefinition> ;
<emptyTypeDefinition> ::= <EmptyHackOpt> ;
<listTypeDefinition> ::= <typeDefinitions>  <typeDefinition>;

<typeDefinition> ::| <signalDefinitionType>
                   | <signalListDefinitionType>
                   | <dataDefinitionType>
                   | <selectType>
                   | <blockTypeDefinitionType>
                   | <blockTypeReferenceType>
                   | <processTypeDefinitionType>
                   | <processTypeReferenceType>
                   | <procedureReferenceType>
                   | <procedureDefinitionType>
                   | <timerDefinitionType>
                   | <serviceTypeDefinitionType>
                   | <serviceTypeReferenceType>
                   | <textualSystemTypeDefinitionType>
                   | <systemTypeReferenceType> ;

<signalDefinitionType>            ::= <signalDefinition> ;
<signalListDefinitionType>        ::= <signalListDefinition> ;
<dataDefinitionType>              ::= <dataDefinition> ;
<selectType>                      ::= <select> ;
<blockTypeDefinitionType>         ::= <blockTypeDefinition> ;
<blockTypeReferenceType>          ::= <blockTypeReference> ;
<processTypeDefinitionType>       ::= <processTypeDefinition> ;
<processTypeReferenceType>        ::= <processTypeReference> ;
<procedureReferenceType>          ::= <procedureReference> ;
<procedureDefinitionType>         ::= <procedureDefinition> ;
<timerDefinitionType>             ::= <timerDefinition> ;
<serviceTypeDefinitionType>       ::= <serviceTypeDefinition> ;
<serviceTypeReferenceType>        ::= <serviceTypeReference> ;
<textualSystemTypeDefinitionType> ::= <textualSystemTypeDefinition> ;
<systemTypeReferenceType>         ::= <systemTypeReference> ;	

<referencedDefinitions> ::| 
	<emptyReferencedDefinition> | <listReferencedDefinition> ;
<emptyReferencedDefinition> ::=  <EmptyHackOpt> ;
<listReferencedDefinition> ::= <referencedDefinitions> <referencedDefinition> ;
<referencedDefinition> ::| <blockDefinitionReference>
                         | <processDefinitionReference>
                         | <procedureDefinitionReference>
                         | <serviceDefinitionReference>
                         | <blockTypeDefinitionReference>
                         | <processTypeDefinitionReference>
                         | <serviceTypeDefinitionReference> ;

<blockDefinitionReference>       ::= <blockDefinition> ;      
<processDefinitionReference>     ::= <processDefinition> ;    
<procedureDefinitionReference>   ::= <procedureDefinition> ;  
<serviceDefinitionReference>     ::= <serviceDefinition> ;    
<blockTypeDefinitionReference>   ::= <blockTypeDefinition> ;  
<processTypeDefinitionReference> ::= <processTypeDefinition> ;
<serviceTypeDefinitionReference> ::= <serviceTypeDefinition> ;

<gate> ::? <gateIdent> ;
<gateIdent> ::= '!' <ident> ;
<gateDefinitions> ::* <gateDefinition> ;
<gateDefinition> ::| <simpleGateDefinition> 
	   | <singleConstrainedGateDefinition> 
	   | <doubleConstrainedGateDefinition> ;
<simpleGateDefinition>  ::= 'GATE' <name> <endd> ;
<singleConstrainedGateDefinition> ::= 'GATE' <name> <gateConstraint> <endd> ;
<doubleConstrainedGateDefinition> ::= 
	'GATE' <name> <gateConstraint> ';' <gateConstraint> <endd> ;
<gateConstraint> ::= <gateEndpointConstraint> 'WITH' <signalList> ;
<gateEndpointConstraint> ::| 
	<inConstraint> | <outConstraint> | <inEnv> | <outEnv> ;
<inConstraint> ::= 'IN' 'FROM' <constraint> ;
<outConstraint> ::= 'OUT' 'TO' <constraint> ;
<constraint> ::| <identConstraint> | <atleastConstraint> ;
<identConstraint> ::= <ident> ;
<atleastConstraint> ::= 'ATLEAST' <ident> ;
<inEnv> ::= 'IN' <envOpt> ;
<outEnv> ::= 'OUT' <envOpt> ;
<envOpt> ::? <env> ;
<systemInstance> ::= 'SYSTEM' <ident> ':' <typeExpression> <endd> ;
<blockInstance> ::= <virtualityOpt> 'BLOCK' <ident> <numberOfBlockInstancesOpt> ':' <typeExpression> <endd> ;
<processInstance> ::=  <processQual> ':' <typeExpression> <endd> ;
<numberOfInstancesOpt> ::? <numberOfInstances> ;
<serviceInstance> ::= 'SERVICE' <ident> ':' <typeExpression> <endd> ;
<numberOfBlockInstances> ::= <expression> ;
<numberOfBlockInstancesOpt> ::?  <numberOfBlockInstances> ;
<valueReturn> ::= 'RETURN' <expression> <endd> ;
<returns> ::= 'RETURNS' <ident> ;
<valueCall> ::= <procedureCall> ;
<nameList> ::+ <name> ',' ;
<identList> ::+ <ident> ',' ;
<sortList> ::+ <sortIdent> ',' ;
<expressionList> ::+ <expression> ',' ;
<variableIdent> ::= <ident> ;
<endd> ::| <simpleEnd> | <comment> ;
<simpleEnd>  ::= ';' ;
<comment> ::= 'COMMENT' <charstring> ';' ;
<transition> ::| <actionsAlt> | <terminatorstmt> | <transitionPair> ;
<actionsAlt> ::= <actions> ;
<transitionPair> ::= <actions> <terminatorstmt> ;
<actions> ::+ <actionstmt>;
<generatorParameterList> ::| <generatorTypeParameterList>
                           | <generatorLiteralParameterList>
                           | <generatorOperatorParameterList>
                           | <generatorConstantParameterList> ;
<generatorTypeParameterList> ::= 'TYPE' <nameList> <genparlst> ;
<generatorLiteralParameterList> ::= 'LITERAL' <nameList> <genparlst> ;
<generatorOperatorParameterList> ::= 'OPERATOR' <nameList> <genparlst> ;
<generatorConstantParameterList> ::= 'CONSTANT' <nameList> <genparlst> ;
<genparlst> ::? <commaGenparlst> ;
<commaGenparlst> ::= ',' <generatorParameterList>;
<charstring> ::= <string> ;
<name> ::= <nameappl>;

<emptyHackOpt> ::? <emptyHack> ;
<emptyHack> ::= 'EmptyHack' 
