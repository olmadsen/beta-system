EOLasComEnd;
--- corba: aGrammar: metagrammar ---
Grammar corba:

Option
  version = 1
  comBegin = '/*'
  comEnd = '*/'
  stringChar = '"'
  suffix = '.corba'
  EOSchar = '%'

  nameAppl = unused

Rule

<CORBAdefinitions>
      ::= <definitions>
        ;

<definitions>
      ::+ <definition> ';'
        ;

<definition>
      ::| <typeDefinition>
        | <constDefinition>
        | <exceptionDefinition>
        | <interfaceDefinition>
        | <moduleDefinition>
        ;
<typeDefinition>
      ::= <typeDcl>
        ;
<constDefinition>
      ::= <constDcl>
        ;
<exceptionDefinition>
      ::= <exception>
        ;
<interfaceDefinition>
      ::= <interfaceDef>
        ;
<moduleDefinition>
      ::= <module>
        ;

<module> 
      ::= <MODUREkwd> <IDENTIFIER> '{' <definitions> '}'
        ;

<interfaceDef>
      ::| <interface>
        | <forward>
        ;

<interface>
      ::= <interfaceHeader> '{' <exports> '}'
        ;

<interfaceDecl>
      ::= <INTERFACEkwd> <IDENTIFIER>
        ;

<interfaceHeader>
      ::= <interfaceDecl> <inheritanceSpecopt>
        ;

<inheritanceSpecopt>
      ::? <inheritanceSpec>
	;
<inheritanceSpec>
      ::= ':'  <atLeastOneScopedName>
        ;

<exports>
      ::+ <export> ';'
        ;

<export>
      ::| <typeDcl>
        | <constDcl>
        | <exception>
        | <attributeSpec>
        | <operation>
        ;


<atLeastOneScopedName>
      ::+ <scopedName> ','
        ;

<scopedName>
      ::| <IDENTIFIER>
        | <globalScopeName>
        | <namedScopeName>
        ;
<globalScopeName>
      ::= <SCOPEDelimitorkwd> <IDENTIFIER>
        ;
<namedScopeName>
      ::= <scopedName> <SCOPEDelimitorkwd> <IDENTIFIER>
        ;
<forward>
      ::= <interfaceDecl>
        ;

<constDcl>
      ::= <CONSTkwd> <constType> <IDENTIFIER> '=' <expression>
        ;

<constType>
      ::| <constIntegerType>
        | <constCharType>
        | <constOctetType>
        | <constBooleanType>
        | <constFloatingPtType>
        | <constStringTypeSpec>
        | <constScopedName>
        ;
<constIntegerType>
      ::= <integerType>
        ;
<constCharType>
      ::= <charType>
        ;
<constOctetType>
      ::= <octetType>
        ;
<constBooleanType>
      ::= <booleanType>
        ;
<constFloatingPtType>
      ::= <floatingPtType>
        ;
<constStringTypeSpec>
      ::= <stringTypeSpec>
        ;
<constScopedName>
      ::= <scopedName>
        ;

<expression>
      ::= <constExpr> ;

<constExpr>
      ::= <orExpr> ;
 
<orExpr>
      ::| <xorExpr>
	| <orXorExpr>
	;
<orXorExpr>
      ::= <orExpr> '|' <xorExpr>
        ;

<xorExpr>
      ::| <andExpr>
	| <xorXorExpr>
	;
<xorXorExpr>
      ::= <xorExpr> '^' <andExpr>
        ;

<andExpr>
      ::| <shiftExpr>
	| <andAndExpr>
	;
<andAndExpr>
      ::= <andExpr> '&' <shiftExpr>
        ;

<shiftExpr>
      ::| <addExpr> 
	| <shiftLeftExpr> 
	| <shiftRightExpr> 
	;
<shiftLeftExpr>
      ::= <shiftExpr> <LEFTShiftkwd> <addExpr>
	;
<shiftRightExpr>
      ::= <shiftExpr> <RIGHTShiftkwd> <addExpr>
        ;

<addExpr>
      ::| <multExpr>
	| <addPlusExpr> 
	| <addMinusExpr>
	;
<addPlusExpr>
      ::= <addExpr> '+' <multExpr>
	;
<addMinusExpr>
      ::= <addExpr> '-' <multExpr>
	;


<multExpr>
      ::| <unaryExpr>
	| <multMultExpr>
	| <multDivExpr>
	| <multModExpr>
	;
<multMultExpr>
      ::= <multExpr> '*' <unaryExpr>
	;
<multDivExpr>
      ::= <multExpr> '/' <unaryExpr>
	;
<multModExpr>
      ::= <multExpr> '%' <unaryExpr>
        ;

<unaryExpr>
      ::| <primaryExpr>
	| <unaryPlusExpr>
	| <unaryMinusExpr>
	| <unaryNotExpr>
	;
<unaryPlusExpr>
      ::= '+' <primaryExpr>
        ;
<unaryMinusExpr>
      ::= '-' <primaryExpr>
        ;
<unaryNotExpr>
      ::= '~' <primaryExpr>
        ;

<primaryExpr>
      ::| <scopedNameExpr>
        | <literal>
        | <paramExpr>
        ;
<scopedNameExpr>
      ::= <scopedName>
        ;
<paramExpr>
      ::= '(' <constExpr> ')'
        ;

<literal>
      ::| <INTEGERLiteral>
        | <STRINGLiteral>
(*      | <CHARACTERLiteral>  *)
(*      | <FLOATINGPtLiteral> *) 
        | <TRUE>
        | <FALSE>
        ;

<positiveIntExpr>
      ::= <constExpr>
        ;

<typeDcl>
      ::| <typedefDcl>
        | <structTypeDcl>
        | <unionTypeDcl>
        | <enumTypeDcl>
        ;
<structTypeDcl>
      ::= <structType>
        ;
<unionTypeDcl>
      ::= <unionType>
        ;
<enumTypeDcl>
      ::= <enumType>
        ;

<typedefDcl>
      ::= <TYPEDEFkwd> <typeDeclarator>
        ;

<typeDeclarator>
      ::= <typeSpec> <atLeastOneDeclarator>
        ;

<typeSpec>
      ::| <simpleTypeSpec>
        | <constructedTypeSpec>
        ;

<simpleTypeSpec>
      ::| <baseTypeSpec>
        | <templateTypeSpec>
        | <scopedNameSpec>
        ;
<scopedNameSpec>
      ::= <scopedName>
	;

<baseTypeSpec>
      ::| <integerTypeSpec>
        | <floatingPtType>
        | <charTypeSpec>
        | <booleanTypeSpec>
        | <octetTypeSpec>
        | <anyType>
        ;
<integerTypeSpec>
      ::= <integerType>
        ;
<charTypeSpec>
      ::= <charType>
        ;
<booleanTypeSpec>
      ::= <booleanType>
        ;
<octetTypeSpec>
      ::= <octetType>
        ;

<templateTypeSpec>
      ::| <sequenceTypeSpec>
        | <stringTypeSpec>
        ;

<constructedTypeSpec>
      ::| <structType>
        | <unionType>
        | <enumTypeSpec>
        ;
<enumTypeSpec>
      ::= <enumType>
        ;

<atLeastOneDeclarator>
      ::+ <declarator> ','
        ;

<declarator>
      ::| <simpleDeclarator>
        | <complexDeclarator>
        ;

<simpleDeclarator>
      ::= <IDENTIFIER>
        ;

<complexDeclarator>
      ::= <arrayDeclarator>
        ;

<integerType>
      ::| <signedInt>
        | <unsignedInt>
        ;

<signedInt>
      ::| <LONGkwd>
        | <SHORTkwd>
        ;

<unsignedInt>
      ::= <UNSIGNEDkwd> <signedInt>
        ;

<floatingPtType>
      ::| <DOUBLEkwd>
        | <FLOATkwd>
        ;

<charType>
      ::= <CHARkwd>;

<octetType>
      ::= <OCTETkwd>
	;

<booleanType>
      ::= <BOOLEANkwd>
	;

<anyType>
      ::= <ANYkwd>
	;

<structType>
      ::= <STRUCTkwd> <IDENTIFIER> '{' <atLeastOneMember> '}'
        ;

<atLeastOneMember>
      ::+ <member>
        ;

<member>  
      ::= <typeSpec> <atLeastOneDeclarator> ';'
        ;

<unionType>
      ::= <UNIONkwd> <IDENTIFIER> <SWITCHkwd> '(' <switchTypeSpec> ')'
          '{' <atLeastOneCaseBranch> '}'
        ;

<switchTypeSpec>
      ::| <integerType>
        | <charType>
        | <octetType>
        | <booleanType>
        | <enumType>
        | <scopedName>
        ;

<atLeastOneCaseBranch>
      ::+ <caseBranch>
        ;

<caseBranch>
      ::= <atLeastOneCaseLabel> <elementSpec> ';'
        ;

<atLeastOneCaseLabel>
      ::+ <caseLabel>
        ;

<caseLabel>
      ::| <caseDefaultLabel>
	| <caseConstLabel>
        ;
<caseDefaultLabel>
      ::= <DEFAULTkwd> ':'
        ;
<caseConstLabel>
      ::= <CASEkwd> <constExpr> ':'
        ;

<elementSpec> 
      ::= <typeSpec> <declarator>
        ;

<enumType> 
      ::= <ENUMkwd> <IDENTIFIER> '{' <atLeastOneEnumerator> '}'
        ;

<atLeastOneEnumerator>
      ::+ <enumerator> ','
        ;

<enumerator>
      ::= <IDENTIFIER>
        ;

<sequenceTypeSpec>
      ::= <seqHead> <sequenceMaxLengthopt> '>'
        ;
<sequenceMaxLengthopt>
      ::? <sequenceMaxLength>
	;
<sequenceMaxLength>
      ::= ',' <positiveIntExpr>
	;
<seqHead>
      ::= <SEQUENCEkwd> '<' <simpleTypeSpec>
        ;

<stringTypeSpec>
      ::= <stringHead> <stringMaxLengthopt>
        ;
<stringMaxLengthopt>
      ::? <stringMaxLength>
	;
<stringMaxLength>
      ::= '<' <positiveIntExpr> '>'
	;
<stringHead>
      ::= <STRINGkwd>
        ;

<arrayDeclarator> 
      ::= <IDENTIFIER> <atLeastOneArrayDim>
        ;

<atLeastOneArrayDim> 
      ::+ <arrayDim>
        ;

<arrayDim> 
      ::= '[' <positiveIntExpr> ']'
        ;

<attributeSpec>
      ::= <readonlyOpt> <ATTRIBUTEkwd> <simpleTypeSpec> <atLeastOneDeclarator>
        ;

<readonlyOpt>
      ::? <READONLYkwd>
        ;

<exception>
      ::= <EXCEPTIONkwd> <IDENTIFIER> '{' <members> '}'
        ;

<members>
      ::* <member>
        ;

<operation>
      ::= <opAttributeopt> <opTypeSpec> <IDENTIFIER> <parameterList>
	  <raisesOpt> <contextOpt>
        ;
<opAttributeopt>
      ::? <opAttribute>
	;
<opAttribute>
      ::| <ONEWAYkwd>
        | <IDEMPOTENTkwd>
        ;

<opTypeSpec>
      ::| <opSimpleTypeSpec>
        | <VOIDkwd>
        ;
<opSimpleTypeSpec>
      ::= <simpleTypeSpec>
	;

<parameterList>
      ::= '(' <parametersOpt> ')'
        ;

<parametersOpt>
      ::? <parameters>
	;
<parameters>
      ::+ <parameter> ','
        ;

<parameter>
      ::= <direction> <simpleTypeSpec> <declarator>
        ;

<direction>
      ::| <INkwd>
        | <OUTkwd>
        | <INOUTkwd>
        ;

<raisesOpt>
      ::? <raisesSpec>
	;
<raisesSpec>
      ::= <RAISESkwd> '(' <atLeastOneScopedName> ')'
        ;

<contextOpt>
      ::? <contextSpec>
        ;
<contextSpec>
      ::= <CONTEXTkwd> '(' <atLeastOneStringLiteral> ')'
        ;

<atLeastOneStringLiteral>
      ::+ <STRINGLiteral> ','
        ;

<IDENTIFIER>		::= <nameDecl> ;
<INTEGERLiteral>	::= <const> ;
<STRINGLiteral>		::= <string> ;
(* <CHARACTERLiteral>	::= <string> ; *)
(* <FLOATINGPtLiteral>	::= <const> ;  *)

(* The tokens are here *)

<TRUE>			::= 'TRUE' ;
<FALSE>			::= 'FALSE' ;

<CONSTkwd>	::= 'CONST' ;
<MODUREkwd>	::= 'MODULE' ;
<INTERFACEkwd>	::= 'INTERFACE' ;
<TYPEDEFkwd>	::= 'TYPEDEF' ;
<LONGkwd>	::= 'LONG' ;
<SHORTkwd>	::= 'SHORT' ;
<UNSIGNEDkwd>	::= 'UNSIGNED' ;
<DOUBLEkwd>	::= 'DOUBLE' ;
<FLOATkwd>	::= 'FLOAT' ;
<CHARkwd>	::= 'CHAR' ;
<OCTETkwd>	::= 'OCTET' ;
<BOOLEANkwd>	::= 'BOOLEAN' ;
<ANYkwd>	::= 'ANY' ;
<STRUCTkwd>	::= 'STRUCT' ;
<UNIONkwd>	::= 'UNION' ;
<SWITCHkwd>	::= 'SWITCH' ;
<ENUMkwd>	::= 'ENUM' ;
<SEQUENCEkwd>	::= 'SEQUENCE' ;
<STRINGkwd>	::= 'STRING' ;
<EXCEPTIONkwd>	::= 'EXCEPTION' ;
<CASEkwd>	::= 'CASE' ;
<DEFAULTkwd>	::= 'DEFAULT' ;
<READONLYkwd>	::= 'READONLY' ;
<ATTRIBUTEkwd>	::= 'ATTRIBUTE' ;
<ONEWAYkwd>	::= 'ONEWAY' ;
<IDEMPOTENTkwd>	::= 'IDEMPOTENT' ;
<VOIDkwd>	::= 'VOID' ;
<INkwd>		::= 'IN' ;
<OUTkwd>	::= 'OUT' ;
<INOUTkwd>	::= 'INOUT' ;
<RAISESkwd>	::= 'RAISES' ;
<CONTEXTkwd>	::= 'CONTEXT' ;

<SCOPEDelimitorkwd>	::= '::' ;
<LEFTShiftkwd>		::= '<<' ;
<RIGHTShiftkwd>		::= '>>'

