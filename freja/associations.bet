ORIGIN '~beta/basiclib/v1.5/betaenv';
INCLUDE '~beta/containers/v1.5/list';
-- lib: Attributes --
ManyToManyAssociation: List
  (#
     element::< 
       (# left: ^leftType; right: ^rightType; attributes: ^attributesType
       #);
     leftType:< Object;
     rightType:< Object;
     attributesType:< Object;
     scanLeft: iterate
       (#
          where::<  (#  do (current.elm.right[] = right[])->value #);
          right: ^rightType;
          left: ^leftType;
          attributes: ^attributesType
       enter right[]
       do
          current.elm.left[]->left[];
          current.elm.attributes[]->attributes[];
          INNER
       #);
     scanRight: iterate
       (#
          where::<  (#  do (current.elm.left[] = left[])->value #);
          left: ^leftType;
          right: ^rightType;
          attributes: ^attributesType
       enter left[]
       do
          current.elm.right[]->right[];
          current.elm.attributes[]->attributes[];
          INNER
       #);
     insert:
       (#
          left: ^leftType;
          right: ^rightType;
          attributes: ^AttributesType;
          theElm: ^element
       enter (left[],right[],attributes[])
       do
          &element[]->theElm[];
          left[]->theElm.left[];
          right[]->theElm.right[];
          attributes[]->theElm.attributes[];
          theElm[]->append
       #)
  #);
OneToManyAssociation: List
  (#
     one: ^oneType;
     oneType:< Object;
     element::<  (# manyElm: ^manyElmType; attributes: ^attributesType #);
     manyElmType:< Object;
     attributesType:< Object;
     scanMany: scan
       (# manyElm: ^manyElmType; attributes: ^attributesType
       do
          current.manyElm[]->manyElm[];
          current.attributes[]->attributes[];
          INNER
       #);
     insert:
       (#
          manyElm: ^manyElmType;
          attributes: ^attributesType;
          theElm: ^element
       enter (manyElm[],attributes[])
       do
          &element[]->theElm[];
          manyElm[]->theElm.manyElm[];
          attributes[]->theElm.attributes[];
          theElm[]->append
       #)
  #);
OneToOneAssociation:
  (#
     left: ^LeftType;
     right: ^rightType;
     attributes: ^attributesType;
     leftType:< Object;
     rightType:< Object;
     attributesType:< Object
  #);
AssociationOne:
  (#
     element:< Object;
     elm: ^element;
     associate: (#  enter elm[] #);
     deassociate: (#  do none ->elm[] #);
     get: (#  exit elm[] #)
  #);
AssociationMany: List
  (#
     element::< Object;
     elm: ^element;
     associate: (#  enter append do INNER associate #);
     deassociate:
       (#
          elm: ^element;
          pos: ^theCellType;
          notFound:< Notification
            (# 
            do
               'Deassociate: Element not in association'->msg.putline;
               INNER notFound
            #);
          emptyAssociation:< Notification
            (# 
            do
               'Deassociate: No elements in association'->msg.putline;
               INNER emptyAssociation
            exit continue
            #)
       enter elm[]
       do
          elm[]->at->pos[];
          (if pos[] <> none then
              pos[]
                ->delete
                  (# empty:: (#  do emptyAssociation->continue #) #)
           else
              notFound
          if);
          INNER deassociate
       #)
  #);
  

