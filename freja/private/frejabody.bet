ORIGIN '../freja';
INCLUDE '~beta/process/v1.4/osinterface'
        '~beta/gpp/v1.5.toby/prettyprintersetup'
        '~beta/containers/v1.4/seqContainers'
        '~beta/gpp/v1.5.toby/private/diagramattributes';
-- FrejaDoPart: DescriptorForm --
(# do 'Mjolner BETA System - Frejatoby v1.5(0)'->putline #)  

-- HandleControllerMessage: DescriptorForm --
(#
   theDoc: ^theGraphicalInterface.theDesignInterface.OADDocument;
   thePage: ^theDoc.Page;
   theOADPage: ^theDoc.OADPage;
   theObject: ^theOADPage.DesignObject;
   theNode: ^theOADPage.PatternDiagramNode;
   command,trailer,editstatus,edittype: ^Text;
   OK: @Boolean;
   editorID: @Integer;
   graf: ^theGraphicalInterface.DesignInterface;
   anElement: ^theGraphicalInterface.sifEditorList.element;
   HandleSifCommand:
     (#
        t,s,command: ^Text;
        index,position: @Integer;
        status: @Integer;
        doSendStatus: @boolean;
        ff: ^theGraphicalInterface.fragmentForm;
        anElement: ^theGraphicalInterface.sifEditorList.element;
        anAst: ^theGraphicalInterface.ast;
     enter t[]
     do
        true->doSendStatus;
        1->t.setpos;
        t.getint->editorID;
        2->t.setpos;
        t.getatom->command[];
        (if theGraphicalInterface.switch[12]
         // true then 'Sif-Command is: '->puttext; command[]->putline;
        if);
        (if true
         // ('openGroup'->command.equal) then
            t.getAtom->s[];
            (* open a new group named 's' *)
            s->theGraphicalInterface.theDesignInterface.openFragmentGroup;
         // ('openForm'->command.equal) then
            t.getAtom->s[];
            (s,editorID)
              ->theGraphicalInterface.theDesignInterface.openFragmentForm->ff[];
            (if ff[]
             // none then
                'openFragmentForm: '->putText;
                s[]->putText;
                ' not found'->putline;
             else
                &theGraphicalInterface.sifEditorList.element[]->anElement[];
                editorId->anElement.editorId;
                ff[]->anElement.ff[];
                anElement[]->theGraphicalInterface.sifEditorList.append;
            if);
            (if theGraphicalInterface.theDesignInterface.theDocument[]
             // none then
                'Freja ------ NO DOCUMENT after openForm'->screen.putline;
             else
                theGraphicalInterface.theDesignInterface.theDocument[]
                  ->theDoc[];
                theDoc.CurrentPage->thePage[];
                (if thePage.struc <= theDoc.OADPage##
                 // true then
                    thePage[]->theOADPage[];
                    t.getint->index;
                    (if index
                     // 0 then
                     else
                        (editorID,index,1)->theOADPage.onFocusChange->status;
                        (if status // false then if);
                        (*index -> theOADPage.DetailAndSelect;*)
                    if)
                if)
            if);
         // ('focusChanged'->command.equal) then
         (* ess: (if theOADPage.insertedObject[]//NONE then *)
            (editorID,t.getint,t.getint)->theOADPage.onFocusChange->status;
            (*else
             t.getint -> theOADPage.insertedObject.astIndex;
             (theOADPage.insertedObject.astIndex,
             theOADPage.insertedObject.ID)
             -> theGraphicalInterface.theDesignInterface.
             IndexIDList.insert;
             theOADPage.insertedObject[] -> theOADPage.CurrentObject;
             NONE -> theOADPage.insertedObject[];
             true -> status;
             if);
             * *)
            (if status
             // true then theOADPage.CurrentObject->theOADPage.GetStatus;
            if);
         // ('newFragment'->command.equal) then
         (* newFragment
          * # <editorId> newFragment
          *)
            editorId->theGraphicalInterface.sifEditorList.findFragmentForm
              ->ff[];
            (if ff[]
             // none then
                'Fragment form: '->putText;
                ff.name->putText;
                ' not found!!!!'->putLine;
             else
                ff[]->theOADPage.newFragment->status;
            if);
         // ('astReplaced'->command.equal) then
         (* astReplaced:
          * # <editorId> astReplaced <oldIndex> <newIndex>
          *)
            editorId->theGraphicalInterface.sifEditorList.findFragmentForm
              ->ff[];
            (if ff[]
             // none then
                'Fragment form: '->putText;
                ff.name->putText;
                ' not found!!!!'->putLine;
             else
                (ff[],t.getint,t.getint)->theOADPage.astReplaced->status;
            if);
         // ('listElementInserted'->command.equal) then
         (* listElementInserted:
          * # <editorId> listElementInserted <fatherIndex> <position> <synCatNo> <synCatName>
          *)
            editorId->theGraphicalInterface.sifEditorList.findFragmentForm
              ->ff[];
            (if ff[]
             // none then
                'Fragment form: '->putText;
                ff.name->putText;
                ' not found!!!!'->putLine;
             else
                t.getint->index;
                t.getint->position;
                (* ESS: *)
                (ff[],index,position)->theOADPage.listElementInserted;
                (*(t.getint - 1,t.getint,
                 t.getatom -> *)
                (*(position- 1,t.getint,
                 t.getatom -> theOADPage.nonTerminalCatName[])
                 -> theNode.theDiagram.InsertNonterminalNode 
                 -> theOADPage.insertedObject[];
                 * *)
                true->status;
            if);
         // ('listElementsDeleted'->command.equal) then
         (* listElementsDeleted:
          * # <editorId> listElementsDeleted <fatherIndex> <position> <length> <oldElements>
          *  <oldElements> ::= { <elementIndex> } i    (i = <length>)
          *)
            editorId->theGraphicalInterface.sifEditorList.findFragmentForm
              ->ff[];
            (if ff[]
             // none then
                'Fragment form: '->putText;
                ff.name->putText;
                ' not found!!!!'->putLine;
             else
                (ff[],t[])->theOADPage.listElementsDeleted;
            if);
         // ('listElementsReplaced'->command.equal) then
         (* listElementsReplaced:
          * # <editorId> listElementsReplaced <fatherIndex> <position> <length> <oldElement> <newLength> 
          * <oldElement> ::= { <elementIndex> } i    (i = <length>)
          *)
            editorId->theGraphicalInterface.sifEditorList.findFragmentForm
              ->ff[];
            (if ff[]
             // none then
                'Fragment form: '->putText;
                ff.name->putText;
                ' not found!!!!'->putLine;
             else
                (ff[],t[])->theOADPage.listElementsReplaced;
            if);
         // ('presentAnchor'->command.equal) then
            t.getInt->theOADPage.presentAnchor;
         // 'quit'->command.equal then
            theController.com.closeCommunication; false->doSendStatus; stop;
         // 'closeComm'->command.equal then
            theController.com.closeCommunication; false->doSendStatus;
         else
            'Freja ------ Command not handled: '->puttext; command[]->putline;
        if);
        (if doSendStatus
         // true then
            '!'->s[];
            (* send OK message to SIF *)
            (if theNode[]
             // none then EditorID->s.putint;
             else
                theNode.SifEditorInstanceNo->s.putint
            if);
            ' '->s.put;
            command[]->s.puttext;
            s[]->theController.sendSifStatus;
        if);
     #)
do
   (if theGraphicalInterface.switch[12]
    // true then
       'input to handelcontrollermessage: '->screen.putText;
       t[]->screen.putLine;
   if);
   theGraphicalInterface.theDesignInterface[]->graf[];
   graf.theDocument[]->theDoc[];
   (if (1->t.inxGet)
    // '!' then
       graf.statusbar.reset;
       t[]->parseStatusMessage->(command[],OK,editorID,trailer[]);
       (if ok
        // true then
           (if true
            // 'openGroup'->command.equal then
               (if theGraphicalInterface.switch[12]
                // true then
                   'Freja ------ [[Status OpenGroup; editorID is: '->puttext;
                   editorID->putint;
                   ']]'->putline;
               if);
            // 'openForm'->command.equal then
               theDoc.CurrentPage->thePage[];
               (if thePage[]
                // theDoc.OADPalette[] then
                   'Freja ------ [[ERROR: HandleContrMes: CurrentPage is the palette]]'
                     ->putline;
                else
                   thePage[]->theOADPage[];
                   theOADPage.CurrentObject->theObject[];
                   (if theObject[] <> none
                    // true then
                       (if theObject.struc <= theOADPage.PatternDiagramNode##
                        // true then
                           theObject[]->theNode[];
                           editorID->theNode.SifEditorInstanceNo;
                           &theGraphicalInterface.sifEditorList.element[]
                             ->anElement[];
                           editorId->anElement.editorId;
                           (* should test whether it is the right fragmentform *)
                           theGraphicalInterface.sifEditorList.
                             askedForFragmentForm[]->anElement.ff[];
                           anElement[]
                             ->theGraphicalInterface.sifEditorList.append;
                       if);
                       (if withDexter
                        // true then editorID->theOADPage.GetDexterAnchors;
                       if)
                   if)
               if);
            // 'save'->command.equal then
            // 'undo'->command.equal then
            // 'cut'->command.equal then
            // 'copy'->command.equal then
            // 'paste'->command.equal then
            // 'before'->command.equal then
            // 'after'->command.equal then
            // 'changeFocus'->command.equal then
               theDoc.CurrentPage->thePage[];
               (if thePage[]
                // theDoc.OADPalette[] then
                   'HandleContrMes: CurrentPage is the palette'->putline;
                else
                   thePage[]->theOADPage[];
                   trailer.reset;
                   trailer.getatom->editType[];
                   trailer.getint->theOADPage.canUndo;
                   trailer.getint->theOADPage.canPaste;
                   trailer.getint->theOADPage.canListInsert;
                   (if theGraphicalInterface.switch[31]
                    // false then
                       (if ('expanded'->edittype.equal) or
                       ('lexem'->edittype.equal)
                        // true then
                           (if theDoc.GppProp.usingExpandMenu
                            // true then
                               (if theDoc.theExpandMenu[]
                                // none then
                                else
                                   theDoc.theExpandMenu.delete;
                                   none ->theDoc.theExpandMenu[];
                               if);
                            else
                               theDoc.OADPalette.clear;
                           if);
                        else
                           (if not ('lexem'->editType.equal) then
                               (if theDoc.GppProp.usingExpandMenu
                                // true then
                                   (if theDoc.theExpandMenu[]
                                    // none then
                                    else
                                       theDoc.theExpandMenu.delete;
                                       none ->theDoc.theExpandMenu[];
                                   if);
                                   &theDoc.ExpandMenu[]->theDoc.theExpandMenu[];
                                   (graf.UserMenu8,'Expand',
                                    (trailer.pos+1,trailer.length)->trailer.sub)
                                     ->theDoc.theExpandMenu.init;
                                else
                                   (trailer.pos+1,trailer.length)->trailer.sub
                                     ->theOADPage.MakeAPalette;
                                   'unexpNormal'->edittype.equal
                                     ->theDoc.OADPalette.normal;
                               if)
                           if);
                       if);
                   if);
               if)
            // 'getStatus'->command.equal then
                 (# index,length: @Integer
                 do
                    theDoc.CurrentPage->thePage[];
                    (if thePage[]
                     // theDoc.OADPalette[] then
                        'HandleContrMes: CurrentPage is the palette'->putline;
                     else
                        (if thePage.struc
                         // theDoc.OADPage## then
                            thePage[]->theOADPage[];
                            trailer.reset;
                            trailer.getint->index;
                            trailer.getint->length;
                            trailer.getatom->edittype[];
                            trailer.getint->theOADPage.canUndo;
                            trailer.getint->theOADPage.canPaste;
                            trailer.getint->theOADPage.canListInsert;
                            (if theGraphicalInterface.switch[31]
                             // false then
                                (if 'expanded'->edittype.equal
                                 // true then
                                    (if theDoc.GppProp.usingExpandMenu
                                     // true then
                                        (if theDoc.theExpandMenu[]
                                         // none then
                                         else
                                            theDoc.theExpandMenu.delete;
                                            none ->theDoc.theExpandMenu[];
                                        if);
                                     else
                                        theDoc.OADPalette.clear;
                                    if);
                                 else
                                    (if not ('lexem'->editType.equal) then
                                        (if theDoc.GppProp.usingExpandMenu
                                         // true then
                                            (if theDoc.theExpandMenu[]
                                             // none then
                                             else
                                                theDoc.theExpandMenu.delete;
                                                none ->theDoc.theExpandMenu[];
                                            if);
                                            &theDoc.ExpandMenu[]
                                              ->theDoc.theExpandMenu[];
                                            (graf.UserMenu8,'Expand',
                                             (trailer.pos+1,trailer.length)
                                               ->trailer.sub)
                                              ->theDoc.theExpandMenu.init;
                                         else
                                            (trailer.pos+1,trailer.length)
                                              ->trailer.sub
                                              ->theOADPage.MakeAPalette;
                                            'unexpNormal'->edittype.equal
                                              ->theDoc.OADPalette.normal;
                                        if)
                                    if);
                                if)
                            if)
                         else
                            'HandleContrllMessage: GetStatus: thePage is not an OADPage'
                              ->screen.putline;
                        if)
                    if)
                 #);
            // 'expandNormal'->command.equal then
               (if theGraphicalInterface.switch[31]
                // true then
                   (if theDoc.GppProp.usingExpandMenu
                    // true then
                       (if theDoc.theExpandMenu[]
                        // none then
                        else
                           theDoc.theExpandMenu.delete;
                           none ->theDoc.theExpandMenu[];
                       if);
                    else
                       theDoc.OADPalette.clear;
                   if);
               if);
               theDoc.CurrentPage->thePage[];
               (if thePage[] = theDoc.OADPalette[] then
                   'HandleContrMes: CurrentPage is the palette'->putline
                else
                   (if thePage## <= theDoc.OADPage## then
                       thePage[]->theOADPage[]; theOADPage.concludeExpansion
                    else
                       'HandleContrMes: CurrentPage is an ObjectPage'->putline
                   if)
               if)
            // 'expandOptional'->command.equal then
               (if theGraphicalInterface.switch[31]
                // true then
                   (if theDoc.GppProp.usingExpandMenu
                    // true then
                       (if theDoc.theExpandMenu[]
                        // none then
                        else
                           theDoc.theExpandMenu.delete;
                           none ->theDoc.theExpandMenu[];
                       if);
                    else
                       theDoc.OADPalette.clear;
                   if);
               if)
            // 'getAnchors'->command.equal then
               (if theGraphicalInterface.switch[12]
                // true then
                   'Freja ------ [[Got anchors: '->screen.puttext;
                   trailer[]->screen.puttext;
                   ']]'->screen.putline;
               if);
               theDoc.CurrentPage->thePage[];
               (if thePage[]
                // theDoc.OADPalette[] then
                   'HandleContrMes: CurrentPage is the palette'->putline;
                else
                   (if thePage.struc
                    // theDoc.OADPage## then
                       thePage[]->theOADPage[];
                       trailer->theOADPage.MakeAnchors;
                    else
                       'HandleContrllMessage: GetAnchors: thePage is not an OADPage'
                         ->screen.putline;
                   if)
               if)
            // 'startLink'->command.equal // 'endLink'->command.equal then
               theDoc.CurrentPage->thePage[];
               (if thePage[]
                // theDoc.OADPalette[] then
                   'HandleContrMes: CurrentPage is the palette'->putline;
                else
                   thePage[]->theOADPage[];
                   theOADPage.CurrentObject->theObject[];
                   (if theObject[] <> none
                    // true then
                       (if theObject.struc <= theOADPage.PatternDiagramNode##
                        // true then
                           theObject[]->theNode[]; theNode.MakeAnchor;
                       if)
                   if)
               if);
           if);
        else
           'Freja: BAD Status from Sif: "'->puttext;
           t[]->puttext;
           '"'->put;
           newline;
       if);
    else
       (if (1->t.inxGet)
        // '#' then
           (if theGraphicalInterface.switch[12]
            // true then
               'Freja ------ Command from SIF: '->puttext; t[]->putline;
           if);
           (if theDoc[]
            // none then t[]->HandleSifCommand;
            else
               theDoc.CurrentPage->thePage[];
               (if thePage[]
                // none then
                   'HandleCommand: No current page'->putline;
                   '!0 0'->theController.sendSifStatus;
                else
                   (if thePage[]
                    // theDoc.OADPalette[] then
                       'HandleContrMes: CurrentPage is the palette'->putline;
                       '!0 0'->theController.sendSifStatus;
                    else
                       (if thePage.struc
                        // theDoc.OADPage## then
                           thePage[]->theOADPage[];
                           theOADPage.CurrentFocus[]->theObject[];
                           (if theObject[]
                            // none then
                               'HandleCommand: No object to handle the command'
                                 ->putline;
                               '!0 0'->theController.sendSifStatus;
                            else
                               (if theObject.struc <=
                               theOADPage.PatternDiagramNode##
                                // true then
                                   theObject[]->theNode[];
                                   (if theNode.theDiagram[]
                                    // none then
                                       'HandleCommand: Current object has no diagram'
                                         ->putline;
                                       '!0 0'->theController.sendSifStatus;
                                    else
                                       t[]->HandleSifCommand;
                                   if)
                               if)
                           if)
                        else
                           'HandleContrllMessage: SifCommand: thePage is not an OADPage'
                             ->screen.putline;
                       if)
                   if)
               if)
           if)
        else
           'Something from SIF: '->t.prepend;
           t[]->putline;
           '!0 0'->theController.sendSifStatus;
       if)
   if);
   graf.statusbar.reset;
#)  

-- ParseStatusMessage: DescriptorForm --
(#
do
   (if (1->status.inxGet)
    // '!' then
       (if (2->status.inxGet)
        // '0' then (* global command *)
           2->status.setpos;
           (* skip '!0' *)
           status.Getatom->command[];
           status.getint->OK;
           (if status.pos < status.length
            // true then
               status.getint->editorID;
               (if status.pos < status.length
                // true then
                   loop:
                     (#
                     do (* skip blanks *)
                        (if status.pos->status.inxget
                         // ' ' then status.get; restart loop;
                        if)
                     #);
                   (status.pos,status.length)->status.sub->trailer[];
               if)
           if);
        else
           1->status.setpos;
           (* skip '!' *)
           (* editorID is next *)
           status.getint->editorID;
           (* ess 021394 status.pos-1 -> status.setpos;*)
           status.Getatom->command[];
           status.getint->OK;
           (if status.pos < status.length
            // true then
               status.pos+1->status.setpos;
               (* move the cursor! getInt should have done that!! *)
               loop:
                 (#
                 do (* skip blanks *)
                    (if status.pos->status.inxget
                     // ' ' then status.get; restart loop;
                    if)
                 #);
               (status.pos,status.length)->status.sub->trailer[];
           if)
       if);
    else
       false->ok;
   if);
   (if theGraphicalInterface.switch[12]
    // true then
       'Freja ------ Sif status from command: "'->screen.puttext;
       (if command[] <> none // true then command[]->screen.puttext; if);
       '"'->screen.put;
       (if ok
        // true then ' ok. '->screen.puttext
        else
           ' not ok. '->screen.puttext
       if);
       'editorID is: '->screen.puttext;
       editorID->screen.putint;
       (if trailer[] <> none
        // true then '. Rest is: '->puttext; trailer[]->screen.puttext;
       if);
       screen.newLine;
   if);
#)  

-- findFragmentForm: DescriptorForm --
(#
do
   search: scan
     (#
     do
        (if current.editorId
         // editorId then current.ff[]->ff[]; leave search;
        if)
     #)
#)  

-- updateFragmentForm: DescriptorForm --
(#
do
   search: scan
     (#
     do
        (if current.ff.name
         // oldff.name then ff[]->current.ff[]; leave search;
        if)
     #)
#)  

-- findEditorId: DescriptorForm --
(#
do
   search: scan
     (#
     do
        (if current.ff.name
         // ff.name then current.editorId->editorId; leave search;
        if)
     #)
#)  

-- OpenObjectInSif: DescriptorForm --
(# theNode: ^PatternDiagramNode; aForm: ^FragmentForm; t: ^Text;
do
   '#0openForm '->t[];
   (if theObject[] // none then CurrentObject->theObject[] if);
   (if theObject[]
    // none then 'No selection, please select a node'->DSUIUserAckMessage;
    else
       (if theObject.struc <= PatternDiagramNode##
        // true then
           theObject[]->theNode[];
           theNode.GetFragment->aForm[];
           (if aForm[] <> none
            // true then
               aForm[]
                 ->theGraphicalInterface.sifEditorList.askedForFragmentForm[];
               aForm.fullname->t.append;
               ' '->t.put;
               theNode.ASTIndex->t.putint;
               (if theGraphicalInterface.switch[12]
                // true then t[]->putline
               if);
               t[]->theController.sendSifCommand;
            else
               'Cannot find FragmentForm for selected node'->DSUIUserAckMessage;
               (if theGraphicalInterface.switch[12]
                // true then t[]->putline
               if);
           if)
       if)
   if);
#)  

-- GetDexterAnchors: DescriptorForm --
(# t: ^Text;
do
   (if theController.com.sifIsRunning and false
    // true then
       'Getting Anchors...'->statusbar.set;
       '# '->t[];
       editorID->t.putint;
       ' getAnchors'->t.append;
       t[]->theController.sendSifCommand;
       (if theGraphicalInterface.switch[12]
        // true then t[]->screen.putline
       if);
   if)
#)  

-- ChangedFocus: DescriptorForm --
(# t: ^Text; ok: @boolean; editorid: @Integer;
do
   (if theGraphicalInterface.switch[12]
    // true then
       '----------------------------------------------------------------------'
         ->screen.putLine;
       'Show Sif Selection: '->putText;
       ShowSIFSelection->putInt;
       newline;
       'newObject.SifEditorInstanceNo: '->putText;
       newObject.SifEditorInstanceNo->putInt;
       newline;
   if);
   (if ShowSIFSelection
    // true then
       (if newObject.SifEditorInstanceNo <> 0
        // true then
           '# '->t[];
           newObject.SifEditorInstanceNo->t.putint;
           ' changeFocus '->t.append;
           newObject.astIndex->t.putint;
           ' 1'->t.append;
           t[]->theController.sendSifCommand;
       if)
   if);
#)  

-- LexemExpand: DescriptorForm --
(# (*n: @Text;
    prompt: [1]@char;
    answer: [64]@Char;*) t: ^Text;
do
(*'Enter the Name for the declaration' -> prompt;
 (if (prompt,@@answer[1]) -> DSUIGetString//true then
 @@answer[1] -> cStringToCharRep -> n;
 n -> theObject.theText.set;
 n.copy -> newName[];*) (* send new name to sif *)
   (if theObject.SifEditorInstanceNo <> 0
    // true then
       '# '->t[];
       theObject.SifEditorInstanceNo->t.putint;
       ' changeFocus '->t.append;
       lexemIndex->t.putint;
       ' 1'->t.append;
       t[]->theController.sendSifCommand;
       theName->DoExpandLexem;
   if);
#)  

-- prefixExpand: DescriptorForm --
(# t: ^Text;
do
   (if CurrentFocus[] <> none
    // true then
       (if CurrentFocus.theDiagram[] <> none
        // true then
           (if CurrentFocus.SifEditorInstanceNo <> 0
            // true then
               '# '->t[];
               CurrentFocus.SifEditorInstanceNo->t.putint;
               (if normal
                // true then ' expandNormal '->t.append;
                else
                   ' expandOptional '->t.append;
               if);
               'Go call sif to expand the prefix'->screen.putline;
           if)
        else
           'DoExpand: theDiagram is NONE'->putline;
       if)
    else
       'DoExpand: CurrentFocus is NONE'->putline;
   if)
#)  

-- copy: DescriptorForm --
(#
do
   'copy: '->puttext;
   (if theObject[] <> none // true then theObject.astIndex->putint if);
   newline;
#)  

-- onFocusChange: DescriptorForm --
(#
(* Selects the diagram node that corresponds to the focus in sif (anAst).
 * If the diagram node doesn't exist, the path up to the rooth in the AST is
 * traversed until an AST node that has a corresponding diagram node is found.
 * This diagram node is then detailed and the same is done for the AST nodes
 * that have been met on the way up.
 * The stack S is used for this.
 *)
   theObject: ^PatternDiagramNode;
   ff: ^fragmentForm;
   anAst: ^ast;
   S: @stack (# element::< integerObject #);
   io: ^integerObject;
   hasDetailed: @boolean;
do
   false->status;
   S.init;
   (if switch[10]
    // true then
       'Freja ----- onFocusChange: '->putText;
       astIndex->putInt;
       ' '->put;
       length->putInt;
       ' editorId: '->putText;
       editorId->putInt;
       newline;
   if);
   editorId->sifEditorList.findFragmentForm->ff[];
   (if ff[]
    // none then 'Fragment form not found!'->putLine;
    else
       thisOp:
       astIndex
         ->ff.indexToNode
           (#
              indexOutOfRange::< 
                (# do 'indexOutOfRange'->putLine; true->continue; #);
              noSuchSymbol::< 
                (# do 'noSuchSymbol'->putLine; true->continue; #);
              grammarGenRefArrayError::< 
                (#
                do 'grammarGenRefArrayError'->putLine; true->continue;
                #)
           #)->anAst[];
       (if anAst[]
        // none then 'Node not found'->putLine
        else
           search:
             (#
             do
                (if switch[10] // true then 'searching...'->putLine; if);
                (if (anAst[] <> none )
                 // true then
                    (if switch[11]
                     // true then
                        'Freja ----- anAst.Index: '->putText;
                        anAst.Index->putInt;
                        newLine;
                    if);
                    anAst.index->IndexIDList.IndextoObject->theObject[];
                 else
                    none ->theObject[];
                if);
                (if (theObject[] <> none )
                 // true then
                    theObject[]->CurrentObject (# do true->autoPan #);
                    theObject[]->CurrentFocus[];
                    (if switch[10]
                     // true then
                        'theObject.astIndex: '->putText;
                        theObject.astIndex->putInt;
                        newLine;
                        (if true
                         // theObject.struc <= PatternDiagramNode## then
                         else
                            'It is not a PatternDiagramNode!!'->putLine;
                            (if true
                             // theObject.struc = GenericNode## then
                                'It is a GenericNode!!'->putLine;
                             // theObject.struc = Node## then
                                'It is a Node!!'->putLine;
                             // theObject.struc = DesignObject## then
                                'It is a DesignObject!!'->putLine;
                             // theObject.struc = IdObject## then
                                'It is a IdObject!!'->putLine;
                            if);
                            (* not visible here!!
                             * (if true 
                             // theObject.struc <= AbstractNode## then
                             'It is an AbstractNode!!' ->putLine;
                             (if true 
                             // theObject.struc <= FragmentNode## then
                             'It is an FragmentNode!!' ->putLine;
                             // theObject.struc = NonterminalNode## then
                             'It is an NonterminalNode!!' ->putLine;
                             // theObject.struc <= DiagramNode## then
                             'It is a DiagramNode!!' ->putLine;
                             if);
                             if);
                             * *)
                        if);
                    if);
                    true->status;
                    (if S.empty
                     // true then
                        (if switch[11]
                         // true then 'Freja ----- stack is empty'->putLine;
                        if);
                     else
                        openDownUntilNode:
                        (if S.empty
                         // false then
                            S.pop->io[];
                            (if switch[11]
                             // true then
                                'Freja ----- pop '->putText;
                                io.value->putInt;
                                newline;
                            if);
                            (* showNode if not shown *)
                            io.value->IndexIDList.IndextoObject->theObject[];
                            (if theObject[]
                             // none then
                                'detailing... '->statusbar.set;
                                false->currentFocus.IndicateSelection;
                                currentFocus.detail;
                                statusbar.reset;
                                true->hasDetailed;
                                io.value->IndexIDList.IndextoObject
                                  ->theObject[];
                                (if theObject[] <> none
                                 // true then
                                    (if switch[11]
                                     // true then
                                        'Freja ----- node found'->putLine;
                                    if);
                                    theObject[]->CurrentObject;
                                    theObject[]->CurrentFocus[];
                                    true->status;
                                 else
                                    'node not found in detailed diagram!!'
                                      ->putLine;
                                if);
                                restart openDownUntilNode
                            if)
                        if)
                    if)
                 else
                    (if (anAst[] <> none )
                     // true then
                        (if switch[11]
                         // true then
                            'Freja ----- anAst.symbol '->putText;
                            anAst.symbol->putint;
                            ' anAst.index '->putText;
                            anAst.index->putint;
                            ' '->put;
                            anAst.symbol->betaGram.symbolToName->putLine;
                        if);
                        (if anAst.kind
                         // kinds.interior then
                            (if anAst.symbol
                             // betaGram.SimpleDecl // betaGram.PatternDecl
                             // betaGram.VirtualDecl // betaGram.BindingDecl
                             //
                             betaGram.RepetitionDecl
                             (*// betaGram.VariablePatternDecl*) then
                                (if switch[11]
                                 // true then
                                    'Freja ----- push '->putText;
                                    anAst.index->putInt;
                                    newline;
                                if);
                                &integerObject[]->io[];
                                anAst.index->io.value;
                                io[]->S.push;
                            if)
                         // kinds.unexpanded then
                            (if
                            (anAst[]->qua (# as:: unexpanded #)).
                            nonterminalSymbol
                             // gram.AttributeDeclOpt then
                                (if switch[11]
                                 // true then
                                    'Freja ----- push nonterminal'->putText;
                                    anAst.index->putInt;
                                    newline;
                                if);
                                &integerObject[]->io[];
                                anAst.index->io.value;
                                io[]->S.push;
                            if)
                        if);
                        anAst.father->anAst[];
                        restart search;
                     else
                    (* astIndex not on the path up to the root *)
                        'astIndex not on the path up to the root'->putLine;
                        false->status;
                    if);
                if)
             #);
       if)
   if);
   (if status
    // false then astIndex->putint; ': no such object in freja'->putline;
   if);
   (* if focusChanged has detail,
    * refresh page due to delay in update *)
   (if hasDetailed then Redraw if)
#)  

-- frejaAstReplaced: DescriptorForm --
(#
do
   (if switch[10]
    // true then
       'Freja: onAstReplaced: '->puttext;
       oldindex->putint;
       ' to: '->puttext;
       newIndex->putint;
       newline;
       'nothing is done in freja'->putLine;
   if);
#)  

-- presentAnchor: DescriptorForm --
(# theObject: ^PatternDiagramNode;
do
   '[[Freja: presentAnchor '->screen.puttext;
   theIndex->screen.putint;
   theIndex->IndexIDList.IndextoObject->theObject[];
   (if theObject[] <> none
    // true then theObject[]->CurrentObject;
    else
       'no such index shown in Freja'->screen.puttext;
   if);
   ']]'->screen.putline;
#)  

-- GetStatusFromSif: DescriptorForm --
(# t: ^Text; theNode: ^PatternDiagramNode;
do
   (if theObject.struc <= PatternDiagramNode##
    // true then
       theObject[]->theNode[];
       (if theNode.SifEditorInstanceNo
        // 0 then
           (if theGraphicalInterface.switch[12]
            // true then 'Freja ------ No Sif Editor Instance open'->putline;
           if);
        else
           '# '->t[];
           theNode.SifEditorInstanceNo->t.putint;
           ' getStatus '->t.append;
           (if theGraphicalInterface.switch[12]
            // true then t[]->putline
           if);
           t[]->theController.sendSifCommand
       if);
    else
       'Freja ------ ERROR: GetStatus: theObject is not a DiagramNode'->putline;
   if)
#)  

-- DoExpand: DescriptorForm --
(# t: ^Text;
do
   (if CurrentFocus[] <> none
    // true then
       (if CurrentFocus.theDiagram <> none
        // true then
           (if CurrentFocus.SifEditorInstanceNo <> 0
            // true then
               '# '->t[];
               CurrentFocus.SifEditorInstanceNo->t.putint;
               (if normal
                // true then ' expandNormal '->t.append;
                else
                   ' expandOptional '->t.append;
               if);
               synCatNo->t.putint;
               t[]->theController.sendSifCommand;
               (if theGraphicalInterface.switch[31]
                // true then
                   (if gppProp.UsingExpandMenu
                    // true then
                       (if theExpandMenu[] <> none
                        // true then
                           theExpandMenu.delete; none ->theExpandMenu[];
                       if);
                    else
                       OADPalette.reset;
                   if);
               if)
           if)
        else
           'DoExpand: theDiagram is NONE'->putline;
       if)
    else
       'DoExpand: CurrentFocus is NONE'->putline;
   if)
#)  

-- DoExpandLexem: DescriptorForm --
(# t: ^Text;
do
   (if CurrentFocus[] <> none
    // true then
       (if CurrentFocus.theDiagram[] <> none
        // true then
           (if CurrentFocus.SifEditorInstanceNo <> 0
            // true then
               '# '->t[];
               CurrentFocus.SifEditorInstanceNo->t.putint;
               ' expandLexem '->t.append;
           if);
           name[]->t.append;
           t[]->theController.sendSifCommand;
           (if theGraphicalInterface.switch[31]
            // true then
               (if gppProp.UsingExpandMenu
                // true then
                   (if theExpandMenu[] <> none
                    // true then
                       theExpandMenu.delete; none ->theExpandMenu[];
                   if);
                else
                   OADPalette.reset;
               if);
           if)
       if)
    else
       'DoExpandLexem: CurrentFocus is NONE'->putline;
   if)
#)  

-- MakeAPalette: DescriptorForm --
(# tt: ^Text;
do
   (if OADPalette.isopen
    // false then (100,100,200,400)->OADPalette.PositionNew;
    else
       OADPalette.clear;
   if);
   nonTerminalCatName->OADPalette.title;
   (if t[]
    // none then
    else
       t.reset;
       t.getatom->tt[];
       (if tt[]
        // none then
        else
           (if ('Enter'->tt.equal)
            // true then OADPalette.close;
            else
               t.reset;
               loop:
               (if 1
                // 1 then
                   (t.getint,t.getatom)->OADPalette.add;
                   (if t.pos+1 < t.length // true then restart loop if)
               if);
               THIS(OADPage)[]->CurrentPage;
               (* select the drawing page again
                * in order to make the palette work right *)
           if)
       if)
   if);
#)  

-- ControllerInit: DescriptorForm --
(# portText,host,t: ^Text; help: @text; port: @integer;
do
   theModel.theGraphicalInterface.switch[12]->com.debugCOM;
   'Starting Sif, please stand by...'
     ->theModel.theGraphicalInterface.theDesignInterface.statusbar.set;
   false->com.debugCOM;
   '$(FrejaSifPort)'
     ->expandEnvVar
       (# defaultValue::<  (# do '5000'->envVarValue[] #) #)->portText[];
   portText->help;
   ' '->help.put;
   help.reset;
   help.getInt->port;
   '$(FrejaHost)'
     ->expandEnvVar
       (#
          defaultValue::< 
            (# os: @osinterface; do 'localhost'->envVarValue[] #)
       #)->host[];
   'Freja: Establishing communication through port : <'->screen.putText;
   port->screen.putInt;
   '> on host: <'->screen.putText;
   host[]->screen.putText;
   '>'->screen.put;
   screen.newLine;
   '$(SifFromFreja)'
     ->expandEnvVar
       (#
          defaultValue::< 
            (# betalib: ^text;
            do
               '$(BETALIB)'
                 ->expandEnvVar
                   (# defaultValue::<  (# do '~beta/'->envVarValue[] #);
                   #)->betalib[];
               'bin/sifhm4.4'->betalib.append;
               betalib[]->envVarValue[]
            #)
       #)->theModel.theGraphicalInterface.ExpandToFullPath->t[];
   t[]->com.application.init;
   '-wp'->com.application.argument.append;
   portText[]->com.application.argument.Append;
   host[]->com.application.argument.Append;
   (if theModel.withDexter
    // true then '-dex'->com.application.argument.Append;
   if);
   com.application.activate;
   port->com.initInSocket;
   theModel.theGraphicalInterface.theDesignInterface.statusbar.reset;
#)  

-- ControllerClose: DescriptorForm --
(#
do
   (if com.sifIsRunning
    // true then
       (if startedFromProgram
        // false then
           'Stopping Sif'->screen.putline;
           com.stopXinputFromSocket;
           '#0quit'->sendSifCommand;
           com.closeCommunication;
        else
           'Closing communication with Sif'->screen.putline;
           com.stopXinputFromSocket;
           '#0closeComm'->sendSifCommand;
           com.closeCommunication;
       if);
   if);
#)  

-- ControllerSendSifCommand: DescriptorForm --
(#
do
   (if com.sifIsRunning // false then init (* make connection *) if);
   (if com.sifIsRunning
    // false then
       'Cannot make connecton to SIF'
         ->theModel.theGraphicalInterface.theDesignInterface.DSUIUserAckMessage;
    else
       'Sending...'
         ->theModel.theGraphicalInterface.theDesignInterface.statusbar.set;
       t[]->com.putline;
   if);
#)  

-- activatedFromSif: DescriptorForm --
(#
do
   true->theController.startedFromProgram;
   'Freja: I was started from Sif.'->screen.putline;
   'Initiating communication throgh port: '->screen.puttext;
   port[]->screen.puttext;
   ' on host: '->screen.puttext;
   host[]->screen.putline;
   port.reset;
   (port.getint,host[])->theController.com.initOutSocket;
#)  

-- changeASTFocus: DescriptorForm --
(# t: ^Text
do
   (if SifEditorInstanceNo <> 0 then
       '# '->t[];
       SifEditorInstanceNo->t.putint;
       ' changeFocus '->t.append;
       ASTIndex->t.putint;
       ' 1'->t.append;
       t[]->theController.sendSifCommand;
   if);
   INNER changeASTFocus
#)  

-- concludeExpansion: DescriptorForm --
(#
   anAST: ^AST;
   theListDiagram: ^ListDiagram;
   theSimpleNode: ^theListDiagram.SimpleNode;
   t: ^Text;
   theNodeText: @Text
do
   (if private.constructingStaticItem then
       (if currentFocus[] <> none then
           (if currentFocus.theDiagram <> none then
               currentFocus.theDiagram->theListDiagram[];
               (if currentFocus## <= theListDiagram.SimpleNode## then
                   currentFocus[]->theSimpleNode[];
                   (theSimpleNode.theDeclaration).getSon2->anAST[];
                   (if currentFocus.SifEditorInstanceNo <> 0 then
                       false->private.constructingStaticItem;
                       (currentFocus.SifEditorInstanceNo,anAST.index)
                         ->changeASTFocus;
                       '# '->t[];
                       currentFocus.SifEditorInstanceNo->t.putint;
                       ' expandNormal '->t.append;
                       betaGram.StaticItem->t.putint;
                       t[]->theController.sendSifCommand;
                       currentFocus.theText.get->theNodeText;
                       ':Part <Type>'->theNodeText.append;
                       theNodeText->currentFocus.theText.set;
                   if)
               if)
           if)
       if)
   if);
   (if private.constructingDynamicItem then
       (if currentFocus[] <> none then
           (if currentFocus.theDiagram <> none then
               currentFocus.theDiagram->theListDiagram[];
               (if currentFocus## <= theListDiagram.SimpleNode## then
                   currentFocus[]->theSimpleNode[];
                   (theSimpleNode.theDeclaration).getSon2->anAST[];
                   (if currentFocus.SifEditorInstanceNo <> 0 then
                       false->private.constructingDynamicItem;
                       (currentFocus.SifEditorInstanceNo,anAST.index)
                         ->changeASTFocus;
                       '# '->t[];
                       currentFocus.SifEditorInstanceNo->t.putint;
                       ' expandNormal '->t.append;
                       betaGram.DynamicItem->t.putint;
                       t[]->theController.sendSifCommand;
                       currentFocus.theText.get->theNodeText;
                       ':Ref <Type>'->theNodeText.append;
                       theNodeText->currentFocus.theText.set;
                   if)
               if)
           if)
       if)
   if)
#)  

-- FrejaOADPagePrivate: DescriptorForm --
(# constructingStaticItem: @Boolean; constructingDynamicItem: @Boolean #)  

