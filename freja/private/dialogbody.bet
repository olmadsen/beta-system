ORIGIN '../frejaenv';
INCLUDE '~beta/guienv/utils/private/promptsbody'
        '~beta/guienv/stddialogs'
        '~beta/guienv/preliminary/tabControl/tabControl';
-- frejaDesignInterfacePrivate: Descriptor --
(# UI: ^guienv #)  

-- FrejaDesignInterfaceOnInit: Descriptor --
(# 
do
   INNER onInit;
   thegui->private.UI[];
   (UserMenu3,'File')->theFileMenu.init;
   3 (* menuID.editMenu *) ->theFileMenu.insertBefore;
   DocFileMenu[]->menuBar.remove
#)  

-- frejaPromptForBoolean: DoPart --
do
     (# maxLength: @integer; line,t: ^text
     do
        msgText.reset;
        getMaxLineLength:
          (# 
          do
             msgText.getLine->line[];
             (if line[] <> none then
                 (if line.length > 0 then
                     (maxLength,line.length)->max->maxLength;
                     restart getmaxLineLength
                 if)
             if)
          #);
        msgText.reset;
        msgText.getLine;
        msgText.getLine->line[];
        (if line.length > 0 then
            msgText.reset;
            msgText.getLine;
            &Text[]->t[];
            (for i: maxLength-msgText.getPos+1 repeat ascii.sp->t.put for);
            (t[],msgText.getPos)->msgText.insert
        if);
        (* The above is a hack that ensures proper resize of prompt window
         when using multi-line message texts *)
        (none ,titleText[],msgText[])
          ->theDesignInterface.private.UI.promptForBoolean
            (#
               popup::< 
                 (# 
                 do private.theMessage.theEventHandler.onLabelChanged
                 #);
               ok::<  (#  do yes->THIS(promptForBoolean).ok #);
               notok::<  (#  do no->THIS(promptForBoolean).ok #)
            #)
     #)  

-- FrejaFileCreateDialog: DoPart --
do
   theDesignInterface.private.UI.FileCreationDialog->t[];
   (if t[] <> none then t->filename if)  

-- frejaDesignInterfaceAlertUser: DoPart --
do (none ,message[],'Alert')->theDesignInterface.private.UI.alertUser  

-- FrejaFileSelectDialog: DoPart --
do
   theDesignInterface.private.UI.FileSelectionDialog->t[];
   (if t[] <> none then t->filename if)  

-- FrejaPromptForText: DoPart --
do
   '                        '->message.append;
   (none ,'Freja',message[],defaultText[])
     ->
       theModel.theGraphicalInterface.theDesignInterface.private.UI.
         promptForText
       (#
          ok::<  (#  do usertext[]->confirm #);
          cancel::<  (#  do THIS(FrejaPromptForText).cancel #);
          popup::< 
            (# 
            do
               (if defaultText[] <> none then
                   private.theText.all->private.theText.selection
               if)
            #)
       #)  

-- layoutPreferencesDialog: Descriptor --
private.UI.window
  (#
     resizeable::  (#  do false->value #);
     alreadyOpen: @boolean;
     theCanvas: @canvas
       (#
          open::< 
            (# 
            do
               true->border.visible;
               theDesignInterface.private.UI.borderStyles.etchedIn
                 ->border.style;
               ShowLabel.open;
               SpecializationsLabel.open;
               diagPosLabel.open;
               showAttrLabel.open;
               Attributes.open;
               References.open;
               Specializations.open;
               Associations.open;
               NameAndType.open;
               Name.open;
               Type.open;
               NameAndKind.open;
               EdgedConnectors.open;
               SubPatternsAreRegions.open;
               AllManual.open;
               AllAutomatic.open;
               AutomaticForSubPatterns.open;
               AutomaticWhenDetailing.open;
               AutomaticForNewPatterns.open;
               true->Attributes.state;
               true->References.state;
               true->Specializations.state;
               true->Associations.state;
               true->subPatternsAreRegions.state;
               true->Name.state;
               true->AllAutomatic.state;
               EdgedConnectors.disable;
               SubPatternsAreRegions.disable;
               AllManual.disable;
               AllAutomatic.disable;
               AutomaticForSubPatterns.disable;
               AutomaticWhenDetailing.disable;
               AutomaticForNewPatterns.disable;
               (6,32)->position;
               (494,326)->size
            #);
          diagPosLabel: @staticText
            (#
               open::< 
                 (# 
                 do
                    (191,157)->position;
                    (132,16)->size;
                    'Diagram positioning:'->label
                 #)
            #);
          SpecializationsLabel: @staticText
            (#
               open::< 
                 (# 
                 do
                    (191,14)->position;
                    (105,16)->size;
                    'Specializations:'->label
                 #)
            #);
          EdgedConnectors: @checkBox
            (#
               reset:
                 (#  do gppProp.edgedSpecializationConnectors->state #);
               open::< 
                 (# 
                 do
                    (191,43)->position;
                    (132,20)->size;
                    'Edged Connectors'->label
                 #)
            #);
          SubPatternsAreRegions: @checkBox
            (#
               reset: (#  do gppProp.subRegionsOfSuper->state #);
               open::< 
                 (# 
                 do
                    (191,67)->position;
                    (280,20)->size;
                    'Sub-patterns are regions of super-patterns'->label
                 #)
            #);
          ShowLabel: @staticText
            (#
               open::< 
                 (#  do (12,14)->position; (60,16)->size; 'Show:'->label #)
            #);
          Attributes: @checkBox
            (#
               reset: (#  do gppProp.showAttributes->state #);
               open::< 
                 (# 
                 do (12,43)->position; (100,20)->size; 'Attributes'->label
                 #);
               eventhandler::< 
                 (#
                    onMouseUp::< 
                      (#  do not state->localProp.showAttributes #)
                 #)
            #);
          References: @checkBox
            (#
               reset: (#  do gppProp.references->state #);
               open::< 
                 (# 
                 do (12,67)->position; (100,20)->size; 'Aggregations'->label
                 #);
               eventhandler::< 
                 (#
                    onMouseUp::<  (#  do not state->localProp.references #)
                 #)
            #);
          Specializations: @checkBox
            (#
               reset: (#  do gppProp.specializations->state #);
               open::< 
                 (# 
                 do (12,93)->position; (116,20)->size; 'Specializations'->label
                 #);
               eventhandler::< 
                 (#
                    onMouseUp::< 
                      (#  do not state->localProp.specializations #)
                 #)
            #);
          Associations: @checkBox
            (#
               reset: (#  do gppProp.associations->state #);
               open::< 
                 (# 
                 do (12,119)->position; (100,20)->size; 'Associations'->label
                 #);
               eventhandler::< 
                 (#
                    onMouseUp::< 
                      (#  do not state->localProp.associations #)
                 #)
            #);
          AllManual: @radioButton
            (#
               open::< 
                 (# 
                 do (191,180)->position; (91,20)->size; 'All Manual'->label
                 #)
            #);
          AllAutomatic: @radioButton
            (#
               open::< 
                 (# 
                 do (191,206)->position; (100,20)->size; 'All Automatic'->label
                 #)
            #);
          AutomaticForSubPatterns: @radioButton
            (#
               open::< 
                 (# 
                 do
                    (191,232)->position;
                    (185,20)->size;
                    'Automatic for sub-patterns'->label
                 #)
            #);
          AutomaticForNewPatterns: @radioButton
            (#
               open::< 
                 (# 
                 do
                    (191,284)->position;
                    (179,20)->size;
                    'Automatic for new patterns'->label
                 #)
            #);
          AutomaticWhenDetailing: @radioButton
            (#
               open::< 
                 (# 
                 do
                    (191,258)->position;
                    (164,20)->size;
                    'Automatic when detailing'->label
                 #)
            #);
          showAttrLabel: @staticText
            (#
               open::< 
                 (# 
                 do
                    (11,157)->position;
                    (138,16)->size;
                    'Show Attributes with:'->label
                 #)
            #);
          NameAndType: @radioButton
            (#
               reset:
                 (# 
                 do
                    (if gppProp.SimpleDeclDisplay = gppProp.AsNameAndType then
                        true->state
                     else
                        false->state
                    if)
                 #);
               open::< 
                 (# 
                 do (11,180)->position; (100,20)->size; 'Name and type'->label
                 #);
               eventhandler::< 
                 (#
                    onMouseUp::< 
                      (# 
                      do
                         (if not state then
                             false->Name.state;
                             false->Type.state;
                             false->NameAndKind.state;
                             gppProp.AsNameAndType->localProp.SimpleDeclDisplay
                          else
                             gppProp.AsName->localProp.SimpleDeclDisplay;
                             true->Name.state;
                             false->NameAndType.state
                         if)
                      #)
                 #)
            #);
          Name: @radioButton
            (#
               reset:
                 (# 
                 do
                    (if gppProp.SimpleDeclDisplay = gppProp.AsName then
                        true->state
                     else
                        false->state
                    if)
                 #);
               open::< 
                 (#  do (11,206)->position; (54,20)->size; 'Name'->label #);
               eventhandler::< 
                 (#
                    onMouseUp::< 
                      (# 
                      do
                         (if not state then
                             false->NameAndType.state;
                             false->Type.state;
                             false->NameAndKind.state;
                             gppProp.AsName->localProp.SimpleDeclDisplay;
                             
                          else
                             gppProp.AsType->localProp.SimpleDeclDisplay;
                             true->Type.state;
                             false->name.state
                         if)
                      #)
                 #)
            #);
          Type: @radioButton
            (#
               reset:
                 (# 
                 do
                    (if gppProp.SimpleDeclDisplay = gppProp.AsType then
                        true->state
                     else
                        false->state
                    if)
                 #);
               open::< 
                 (#  do (11,232)->position; (52,20)->size; 'Type'->label #);
               eventhandler::< 
                 (#
                    onMouseUp::< 
                      (# 
                      do
                         (if not state then
                             false->NameAndType.state;
                             false->Name.state;
                             false->NameAndKind.state;
                             gppProp.AsType->localProp.SimpleDeclDisplay;
                             
                          else
                             gppProp.AsNameAndKind->localProp.SimpleDeclDisplay;
                             true->NameAndKind.state;
                             false->type.state
                         if)
                      #)
                 #)
            #);
          NameAndKind: @radioButton
            (#
               reset:
                 (# 
                 do
                    (if gppProp.SimpleDeclDisplay = gppProp.AsNameAndKind then
                        true->state
                     else
                        false->state
                    if)
                 #);
               open::< 
                 (# 
                 do (11,258)->position; (100,20)->size; 'Name and kind'->label
                 #);
               eventhandler::< 
                 (#
                    onMouseUp::< 
                      (# 
                      do
                         (if not state then
                             false->NameAndType.state;
                             false->Name.state;
                             false->Type.state;
                             gppProp.AsNameAndKind->localProp.SimpleDeclDisplay;
                             
                          else
                             gppProp.AsNameAndType->localProp.SimpleDeclDisplay;
                             true->NameAndType.state;
                             false->NameAndKind.state
                         if)
                      #)
                 #)
            #)
       #);
     ResetButton: @pushButton
       (#
          open::< 
            (#  do (393,373)->position; (93,20)->size; 'Reset'->label #);
          eventhandler::<  (# onMouseUp::<  (#  do reset #) #)
       #);
     CancelButton: @pushButton
       (#
          open::< 
            (#  do (287,373)->position; (93,20)->size; 'Cancel'->label #);
          eventhandler::< 
            (# onMouseUp::<  (#  do reset; layoutPreferencesDialog.hide #)
            #)
       #);
     OKbutton: @pushButton
       (#
          open::< 
            (#  do (181,373)->position; (93,20)->size; 'OK'->label #);
          eventhandler::< 
            (#
               onMouseUp::< 
                 (#
                    connectors: ^ObjectList;
                    theConn: ^theWorkPage.deletableConnector;
                    n1,n2: ^theWorkPage.DesignObject;
                    theWedgeNode: ^theWorkPage.WedgeNode;
                    thePatternDiagramNode,theOtherPatternDiagramNode:
                      ^theWorkPage.PatternDiagramNode;
                    theListDiagram,theOtherListDiagram:
                      ^theWorkPage.Listdiagram;
                    t: @text;
                    help: ^text;
                    SimpleDeclDisplayChanged,showAttributesChanged: @boolean
                 do
                    layoutPreferencesDialog.hide;
                    (if localProp.changed then
                        false->repair;
                        (if theWorkPage[] <> none then
                            (if localProp.showAttributes.isChanged or
                            localProp.references.isChanged or
                            localProp.specializations.isChanged or
                            localProp.associations.isChanged then
                                theWorkPage.getConnectors->connectors[];
                                (if connectors[] <> none then
                                    connectors.scan
                                      (#
                                         theAssociationConn:
                                           ^theWorkPage.AssociationConnector;
                                         theListDiagram:
                                           ^theWorkPage.ListDiagram
                                      do
                                         (if current## <=
                                         theWorkPage.deletableConnector## then
                                             current[]->theConn[];
                                             (if theConn## <=
                                             theWorkPage.PrefixConnector## then
                                                 (if
                                                 localProp.specializations.
                                                   isChanged then
                                                     (if
                                                     localProp.specializations
                                                      then
                                                         true
                                                           ->theConn.selectable;
                                                         true
                                                           ->
                                                             theConn.
                                                               borderVisible
                                                      else
                                                         false
                                                           ->theConn.selectable;
                                                         false
                                                           ->
                                                             theConn.
                                                               borderVisible
                                                     if)
                                                 if)
                                              else
                                                 (if theConn## =
                                                 theWorkPage.
                                                   AssociationConnector## then
                                                     (if
                                                     localProp.associations.
                                                       isChanged then
                                                         localProp.associations
                                                           ->theConn.visible
                                                     if);
                                                     (if
                                                     localProp.showAttributes.
                                                       isChanged then
                                                         theConn[]
                                                           ->
                                                             theAssociationConn[];
                                                         localProp.
                                                           showAttributes
                                                           ->
                                                             theAssociationConn.
                                                               attributesVisible
                                                     if)
                                                  else
                                                     (if theConn## =
                                                     theWorkPage.
                                                       AggregationConnector##
                                                      then
                                                         (if
                                                         localProp.references.
                                                           isChanged then
                                                             localProp.
                                                               references
                                                               ->theConn.visible
                                                         if);
                                                         (if
                                                         localProp.
                                                           showAttributes.
                                                           isChanged then
                                                             theConn[]
                                                               ->
                                                                 theAssociationConn[];
                                                             (
                                                             (
                                                             theAssociationConn.
                                                               rightRole
                                                               ->
                                                                 theAssociationConn
                                                                 .rr[]).
                                                             thePatternDiagramNode
                                                             ).theDiagram
                                                               ->
                                                                 theListDiagram[];
                                                             (if
                                                             (
                                                             theAssociationConn.
                                                               rr.
                                                               thePatternDiagramNode
                                                             )._struc <=
                                                             theListDiagram.
                                                               PatternNode##
                                                              then
                                                                 localProp.
                                                                   showAttributes
                                                                   ->
                                                                     theAssociationConn
                                                                     .visible;
                                                                 localProp.
                                                                   showAttributes
                                                                   ->
                                                                     theAssociationConn
                                                                     .
                                                                       attributesVisible
                                                              else
                                                                 localProp.
                                                                   showAttributes
                                                                   ->
                                                                     theAssociationConn
                                                                     .
                                                                       attributesVisible
                                                             if)
                                                         if)
                                                     if)
                                                 if)
                                             if)
                                         if)
                                      #)
                                if);
                                
                            if);
                            (if
                            localProp.showAttributes.isChanged
                            (* have to update globally to make
                             adjustsizes work *) then
                                localProp.showAttributes.update;
                                true->showAttributesChanged
                            if);
                            theWorkPage.patternDiagrams.theList.Scan
                              (#
                                 thisListDiagram: ^theWorkPage.ListDiagram;
                                 sad: ^thisListDiagram.SimpleAttributeDecl
                              do
                                 current.e[]->thisListDiagram[];
                                 thisListDiagram.localNodes.scan
                                   (# 
                                   do
                                      (if
                                      localProp.SimpleDeclDisplay.isChanged
                                      (* have to update globally to make
                                       redisplay work *) then
                                          (if current## =
                                          thisListDiagram.SimpleAttributeDecl##
                                           then
                                              current[]->sad[];
                                              localProp.SimpleDeclDisplay
                                                ->sad.changeDisplay
                                          if)
                                      if);
                                      (if showAttributesChanged then
                                          (if localProp.showAttributes then
                                              current.invisibleText->help[];
                                              help->current.theText.set;
                                              true->current.selectable
                                           else
                                              (if not
                                              (current.theText.length = 0) then
                                                  current.theText.get->t;
                                                  t[]->current.invisibleText
                                              if);
                                              current.theText.clear;
                                              false->current.selectable
                                          if)
                                      if)
                                   #);
                                 thisListDiagram.localNodes.adjustSizes
                              #)
                        if);
                        localProp.updateGppProp;
                        true->repair
                    if)
                 #)
            #)
       #);
     dumpPropsbutton: @pushButton
       (#
          open::< 
            (# 
            do (75,373)->position; (93,20)->size; 'dump props'->label
            #);
          eventhandler::< 
            (#
               onMouseUp::< 
                 (# 
                 do
                    localProp.dump;
                    newline;
                    '-----------gppProp----------'->putline;
                    'SimpleDeclDisplay: '->puttext;
                    gppProp.SimpleDeclDisplay->putint;
                    newline;
                    'showAttributes: '->puttext;
                    gppProp.showAttributes->putboolean;
                    newline;
                    'references: '->puttext;
                    gppProp.references->putBoolean;
                    newline;
                    'specializations: '->puttext;
                    gppProp.specializations->putBoolean;
                    newline;
                    'associations: '->puttext;
                    gppProp.associations->putBoolean;
                    newline;
                    'edgedSpecializationConnectors: '->puttext;
                    gppProp.edgedSpecializationConnectors->putBoolean;
                    newline;
                    'subRegionsOfSuper: '->puttext;
                    gppProp.subRegionsOfSuper->putBoolean;
                    newline;
                    newline
                 #)
            #)
       #);
     canvasLabel: @staticText
       (#
          open::< 
            (# 
            do (6,6)->position; (121,16)->size; 'Layout Preferences'->label
            #)
       #);
     eventhandler::< 
       (# onAboutToClose::<  (#  do false->okToClose #) #);
     open::< 
       (# 
       do
          canvasLabel.open;
          theCanvas.open;
          reset;
          (if switch[1] then dumpPropsButton.open if);
          OKbutton.open;
          CancelButton.open;
          ResetButton.open;
          (507,406)->size;
          'Layout Preferences'->title;
          INNER
       #);
     reset:
       (# 
       do
          &props[]->localProp[];
          theCanvas.NameAndType.reset;
          theCanvas.Name.reset;
          theCanvas.Type.reset;
          theCanvas.NameAndKind.reset;
          theCanvas.attributes.reset;
          theCanvas.specializations.reset;
          theCanvas.references.reset;
          theCanvas.associations.reset;
          
       #);
     props:
       (#
          SimpleDeclDisplay: @gppProp.IntegerDefault
            (#
               isChanged:
                 (# v: @boolean
                 do SimpleDeclDisplay <> gppProp.SimpleDeclDisplay->v
                 exit v
                 #);
               update:
                 (#  do SimpleDeclDisplay->gppProp.SimpleDeclDisplay #)
            do gppProp.SimpleDeclDisplay->value
            #);
          showAttributes: @gppProp.BooleanDefault
            (#
               isChanged:
                 (# v: @boolean
                 do showAttributes <> gppProp.showAttributes->v
                 exit v
                 #);
               update: (#  do showAttributes->gppProp.showAttributes #)
            do gppProp.showAttributes->value
            #);
          references: @gppProp.BooleanDefault
            (#
               isChanged:
                 (# v: @boolean
                 do references <> gppProp.references->v
                 exit v
                 #);
               update: (#  do references->gppProp.references #)
            do gppProp.references->value
            #);
          specializations: @gppProp.BooleanDefault
            (#
               isChanged:
                 (# v: @boolean
                 do specializations <> gppProp.specializations->v
                 exit v
                 #);
               update: (#  do specializations->gppProp.specializations #)
            do gppProp.specializations->value
            #);
          associations: @gppProp.BooleanDefault
            (#
               isChanged:
                 (# v: @boolean
                 do associations <> gppProp.associations->v
                 exit v
                 #);
               update: (#  do associations->gppProp.associations #)
            do gppProp.associations->value
            #);
          edgedSpecializationConnectors: @gppProp.BooleanDefault
            (#
               isChanged:
                 (# v: @boolean
                 do
                    edgedSpecializationConnectors <>
                    gppProp.edgedSpecializationConnectors->v
                 exit v
                 #);
               update:
                 (# 
                 do
                    edgedSpecializationConnectors
                      ->gppProp.edgedSpecializationConnectors
                 #)
            do gppProp.edgedSpecializationConnectors->value
            #);
          subRegionsOfSuper: @gppProp.BooleanDefault
            (#
               isChanged:
                 (# v: @boolean
                 do subRegionsOfSuper <> gppProp.subRegionsOfSuper->v
                 exit v
                 #);
               update:
                 (#  do subRegionsOfSuper->gppProp.subRegionsOfSuper #)
            do gppProp.subRegionsOfSuper->value
            #);
          changed:
            (# value: @boolean
            do
               SimpleDeclDisplay.isChanged or showAttributes.isChanged or
               references.isChanged or specializations.isChanged or
               associations.isChanged or
               edgedSpecializationConnectors.isChanged or
               subRegionsOfSuper.isChanged->value
            exit value
            #);
          updateGppProp:
            (# 
            do
               SimpleDeclDisplay.update;
               showAttributes.update;
               references.update;
               specializations.update;
               associations.update;
               edgedSpecializationConnectors.update;
               subRegionsOfSuper.update
            #);
          dump:
            (# 
            do
               newline;
               '-----------localProp----------'->putline;
               'SimpleDeclDisplay: '->puttext;
               SimpleDeclDisplay->putint;
               newline;
               'showAttributes: '->puttext;
               showAttributes->putBoolean;
               newline;
               'references: '->puttext;
               references->putBoolean;
               newline;
               'specializations: '->puttext;
               specializations->putBoolean;
               newline;
               'associations: '->puttext;
               associations->putBoolean;
               newline;
               'edgedSpecializationConnectors: '->puttext;
               edgedSpecializationConnectors->putBoolean;
               newline;
               'subRegionsOfSuper: '->puttext;
               subRegionsOfSuper->putBoolean;
               newline;
               newline
            #)
       #);
     localProp: ^props;
     popup:
       (# 
       do
          &props[]->localProp[];
          (if alreadyOpen then
              showModal
           else
              open; showModal; true->alreadyOpen
          if)
       #)
  #)  

