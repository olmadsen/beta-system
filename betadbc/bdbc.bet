ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '~beta/containers/v1.6/list';
BODY 'private/bdbcbody';
-- lib: Attributes --
DBEnv:
(* Handles connections to ODBC-compliant databases.
 * A DBEnv must be opened before any other operation on it is meaningful
 *)
  (#
     <<SLOT DBEnvLib:Attributes>>;
     Connection:
     (* A connection to an ODBC compliant database *)
       (#
          <<SLOT ConnectionLib:Attributes>>;
          Info: @
            (#
               table:
                 (#
                    CatalogName,SchemaName,
                      TableName,TableType: ^text
                 #);
               tableList: list (# element:: table #);
               getTables:
                 (# res: ^tableList <<SLOT getTables:DoPart>> exit res[] #);
               TransactionsSupported: @
                 (# isSupported: @boolean
                 <<SLOT ConnectionInfoTransactionsSupported:DoPart>>
                 exit
                 isSupported
                 #);
               AutoCommitMode:
               (* In AutoCommitMode, every database operation is a transaction
                * that is committed when performed 
                *) @
                 (# AutoCommitOn: @boolean
                 enter
                   (# 
                   enter AutoCommitOn
                   <<SLOT ConnectionInfoAutoCommitModeEnter:DoPart>>
                   #)
                 exit
                   (# 
                   <<SLOT ConnectionInfoAutoCommitModeExit:DoPart>>
                   exit
                   AutoCommitOn
                   #)
                 #);
               TransactionLevel: @
                 (#
                    READ_UNCOMMITTED: integerValue
                      (#  <<SLOT ConnectionInfoREAD_UNCOMMITTED:DoPart>> #);
                    READ_COMMITTED: integerValue
                      (#  <<SLOT ConnectionInfoREAD_COMMITTED:DoPart>> #);
                    REPEATABLE_READ:
                     integerValue
                      (#  <<SLOT ConnectionInfoREPEATABLE_READ:DoPart>> #);
                    SERIALIZABLE:
                     integerValue
                      (#  <<SLOT ConnectionInfoSERIALIZABLE:DoPart>> #);
                    level:
                    (* One of the above defined transaction levels *) @integer
                 enter
                   (# 
                   enter level
                   <<SLOT ConnectionInfoTransactionLevelEnter:DoPart>>
                   #)
                 exit
                   (# 
                   <<SLOT ConnectionInfoTransactionLevelExit:DoPart>>
                   exit level
                   #)
                 #);
               infoException:< BDBCException (#  do INNER infoException #);
               infoWarning:< BDBCWarning (#  do INNER infoWarning #);
               private: @<<SLOT ConnectionInfoPrivate:Descriptor>>
            #);
          commit:
          (* Commits all transactions pertaining to this connection.
           * Use Info.TransactionSupported to check if this is meaningful *)
            (#  <<SLOT ConnectionCommit:DoPart>> #);
          rollBack:
          (* Rolls back all transactions pertaining to this connection *)
            (#  <<SLOT ConnectionRollBack:DoPart>> #);
          SQLStatement:
            (#
               Contents: @text;
               execute:<
                 (# res: ^ResultSet
                 <<SLOT SQLStatementExecute:DoPart>>
                 exit Res[]
                 #);
               private: @<<SLOT SQLStatementPrivate:Descriptor>>
            #);
          DirectSQLStatement:
           SQLStatement
            (#
               execDirectException:< BDBCException
                 (#  do INNER execDirectException #);
               execDirectWarning:< BDBCWarning
                 (#  do INNER execDirectWarning #);
               execute::  (#  <<SLOT DirectSQLStatementExecute:DoPart>> #);
               private:
                 @<<SLOT DirectSQLStatementPrivate:Descriptor>>
            #);
          ResultSet:
          (* Result of a SQLStatement. 
           * If Info.UpdateCount = 0 then the ResultSet is scanable *)
            (#
               Result:
                 (#
                    getColumn: (* Get data in column 'i' *)
                      (# i: @integer
                      enter i
                      <<SLOT ResultGetColumn:DoPart>>
                      #);
                    getInteger: getColumn
                      (# res: @integer
                      <<SLOT ResultGetInteger:DoPart>>
                      exit res
                      #);
                    getReal: getColumn
                      (# res: @real
                      <<SLOT ResultGetReal:DoPart>>
                      exit res
                      #);
                    getText: getColumn
                      (# res: ^text
                      <<SLOT ResultGetText:DoPart>>
                      exit res[]
                      #);
                    getBoolean: getColumn
                      (# res: @boolean
                      <<SLOT ResulGetBoolean:DoPart>>
                      exit res
                      #);
                    getColumnByName:
                    (* Get the data in the first column in the result designated by 'name'*)
                      (# name: ^text
                      enter name[]
                      <<SLOT ResultGetColumnByName:DoPart>>
                      #);
                    getIntegerByName:
                     getColumnByName
                      (# res: @integer
                      <<SLOT ResultGetIntegerByName:DoPart>>
                      exit res
                      #);
                    getRealByName: getColumnByName
                      (# res: @real
                      <<SLOT ResultGetRealByName:DoPart>>
                      exit res
                      #);
                    getTextByName:
                     getColumnByName
                      (# res: ^text
                      <<SLOT ResultGetTextByName:DoPart>>
                      exit res[]
                      #);
                    getBooleanByName:
                     getColumnByName
                      (# res: @boolean
                      <<SLOT ResultGetBooleanByName:DoPart>>
                      exit res
                      #);
                    resultException:< BDBCException
                      (#  do INNER resultException #);
                    resultWarning:< BDBCWarning
                      (#  do INNER resultWarning #);
                    private: @<<SLOT ResultPrivate:Descriptor>>
                 #);
               Info: @
                 (#
                    NoOfCols: @integer;
                    UpdateCount: @integer;
                    ColumnInfo:
                      (#
                         ColumnName: ^Text;
                         ColumnNo: @Integer;
                         DataType:
                         (* The BETA pattern corresponding to the
                          datatype for this column *) ##Object;
                         DataTypeName: (* DBMS specific type name *) ^Text;
                         DataTypeNo:
                         (* ODBC specific numbering of SQL datatypes *)
                           @integer;
                         NullAble: @Boolean
                      #);
                    getColumnInfo:
                      (# i: @Integer; res: ^ColumnInfo
                      enter i
                      <<SLOT ResultSetInfoGetColumnInfo:DoPart>>
                      exit res[]
                      #);
                    getColumnInfoByName:
                      (# name: ^Text; res: ^ColumnInfo
                      enter name[]
                      <<SLOT ResultSetInfogetColumnInfoByName:DoPart>>
                      exit res[]
                      #)
                 #);
               scan: (# current: @Result <<SLOT resultSetScan:DoPart>> #);
               resultSetException:< BDBCException
                 (#  do INNER resultSetException #);
               resultSetWarning:< BDBCWarning
                 (#  do INNER resultSetWarning #);
               private: @<<SLOT ResultSetPrivate:Descriptor>>
            #);
          connectionException:<
           BDBCException (#  do INNER connectionException #);
          connectionWarning:< BDBCWarning
            (#  do INNER connectionWarning #);
          open:<
          (* Name of the connection to open must be supplied.
           * userName and password is voluntary
           *)
            (#
               name: ^text;
               userName: ^text;
               password: ^text;
               openException:< BDBCException (#  do INNER openException #);
               openWarning:< BDBCWarning (#  do INNER openWarning #);
               private: @<<SLOT ConnectionOpenPrivate:Descriptor>>
            enter
            (name[],userName[],
             password[])
            <<SLOT ConnectionOpen:DoPart>>
            #);
          close:<
            (#
               closeException:< BDBCException
                 (# 
                 do INNER closeException
                 #);
               closeWarning:< BDBCWarning
                 (# 
                 do INNER closeWarning
                 #);
               private:
                 @<<SLOT ConnectionClosePrivate:Descriptor>>
            <<SLOT ConnectionClose:DoPart>>
            #);
          private:
            @<<SLOT ConnectionPrivate:Descriptor>>
       #);
     open:< (#  <<SLOT DBEnvOpen:DoPart>> #);
     close:< (#  <<SLOT DBEnvClose:DoPart>> #);
     BDBCException:
     (* Low level interface for catching exceptions.
      * A general exception message is supplied in msg,
      * SQL states and native error codes in SQLState, 
      * NativeError in a comma-separated list.
      * BDBCException should NOT be called directly by an application
      *) Exception
       (#
          SQLState: @text;
          NativeError: @text;
          HandleType: @integer;
          Handle: @integer
       enter (HandleType,Handle)
       <<SLOT DBEnvBDBCException:DoPart>>
       #);
     BDBCWarning: BDBCException
       (# 
       do true->continue; INNER BDBCWarning
       #);
     private:
       @<<SLOT DBEnvPrivate:Descriptor>>
  do INNER DBEnv
  #);
  

