ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '../bdbc'
        '~beta/basiclib/v1.6/file';
-- program: Descriptor --
(#
   executesqlfile:
     (#
        FileName,DataSource,UserName,PassWord: ^text;
        theCon: @Connection (# theStmt: @DirectSQLStatement #);
        theFile: @File;
        doneReading: @boolean
     enter (FileName[],DataSource[],UserName[],PassWord[])
     do
        (DataSource[],UserName[],PassWord[])->theCon.open;
        FileName[]->theFile.name;
        theFile.openRead;
        createLoop:
          (# createTxt: ^text
          do
             (if not theFile.EOS then
                 &text[]->createTxt[];
                 false->doneReading;
                 readLoop:
                   (# txt: ^text
                   do
                      (if not doneReading then
                          theFile.getLine->txt[];
                          (if txt.empty then
                              true->doneReading
                           else
                              txt[]->createTxt.append
                          if);
                          restart readLoop
                      if)
                   #);
                 createTxt[]->putLine;
                 createTxt[]->theCon.theStmt.Contents;
                 theCon.theStmt.execute;
                 restart createLoop
             if)
          #);
        theCon.theStmt.close;
        theCon.close
     #);
   FileName,DataSource,UserName,PassWord: ^text
do
   (if noOfArguments
    // 3 then
       (2->arguments,3->arguments,none ,none )->executesqlfile
    // 4 then
       (2->arguments,3->arguments,4->arguments,none )->executesqlfile
    // 5 then
       (2->arguments,3->arguments,4->arguments,5->arguments)->executesqlfile
    else
       'Usage: executesqlfile <sqlfile> <datasource> [<username> [<password>]]'
         ->putLine
   if)
#)  

