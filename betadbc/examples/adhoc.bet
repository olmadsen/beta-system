ORIGIN '~beta/basiclib/betaenv';
INCLUDE '../betadbc'
        '~beta/basiclib/formatio'
        '~beta/basiclib/numberio';
-- program: Descriptor --
(#
   myCon: @Connection;
   myStmt: @myCon.DirectSQLStatement
     (#
        execDirectException:: 
          (# 
          do SQLState.copy->msg.append; msg[]->putline; true->continue
          #)
     #);
   myRS: ^myCon.ResultSet;
   con,user,password: ^text;
   stmt: ^text;
   theInfo: ^myRS.column
do
   'Connection to open: '->puttext;
   getline->con[];
   'Username: '->puttext;
   getline->user[];
   'Password: '->puttext;
   getline->password[];
   'Connecting to %s...'->putFormat (#  do con[]->s #);
   (con[],user[],password[])->myCon.open;
   'done'->putLine;
   Loop: cycle
     (# 
     do
        'Statement to execute: '->puttext;
        getline->stmt[];
        (if stmt.empty then leave Loop if);
        stmt[]->myStmt;
        myStmt.Execute->myRS[];
        (if myRS[] <> none then
            '(Found %i row(s))\n'->putFormat (#  do myRS.rowCount->i #);
            (for i: myRS.noOfColumns repeat
              i->myRS.getColumn->theInfo[];
              '%s: %s (#%i)\t'
                ->putFormat
                  (# 
                  do
                     theInfo.name[]->s;
                     theInfo.DataTypeName[]->s;
                     theInfo.DataTypeNo->i
                  #)
            for);
            newline;
            (if myRS.noOfColumns > 0 then
                myRS.scan
                  (# resType: ##object; t: ^text
                  do
                     (for i: myRS.noOfColumns repeat
                       i->myRS.getColumn->theInfo[];
                       (if theInfo.DataType##
                        // text## then
                           current.s->t[]
                        // integerObject## then
                           &text[]->t[]; current.i->t.putInt
                        // realObject## then
                           &text[]->t[]; current.f->t.putReal
                        // booleanObject## then
                           &text[]->t[];
                           (if current.b then 'Yes'->t[];  else 'No'->t[] if)
                        else
                           'Unknown data type!!!'->t[]
                       if);
                       t[]->puttext;
                       '\t'->puttext
                     for);
                     newline
                  #)
             else
                'DML/DDL statement executed successfully!'->putLine
            if)
        if)
     #);
   myStmt.close
#)  

