ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '../bdbc'
        '~beta/basiclib/v1.6/formatio'
        '~beta/basiclib/v1.6/numberio';
-- program: Descriptor --
(#
   myDBEnv: @DBEnv;
   myCon: @myDBEnv.Connection;
   myStmt: @myCon.DirectSQLStatement
     (# execDirectException::  (#  do SQLState.copy->msg.append #) #);
   myRS: ^myCon.ResultSet;
   con,user,password: ^text;
   stmt: ^text;
   theInfo: ^myRS.Info.columnInfo
do
   myDBEnv.open;
   'Connection to open: '->puttext;
   getline->con[];
   'Username: '->puttext;
   getline->user[];
   'Password: '->puttext;
   getline->password[];
   'Connecting to %s...'->putFormat (#  do con[]->s #);
   (con[],user[],password[])->myCon.open;
   'done'->putLine;
   Loop: cycle
     (# 
     do
        'Statement to execute: '->puttext;
        getline->stmt[];
        (if stmt.empty then leave Loop if);
        stmt[]->myStmt.Contents;
        myStmt.Execute->myRS[];
        (for i: myRS.Info.NoOfCols repeat
          i->myRS.Info.getColumnInfo->theInfo[];
          '%s: %s (#%i)\t'
            ->putFormat
              (# 
              do
                 theInfo.ColumnName[]->s;
                 theInfo.DataTypeName[]->s;
                 theInfo.DataTypeNo->i
              #)
        for);
        newline;
        (if myRS.info.noOfCols > 0 then
            myRS.scan
              (#
                 resType: ##object;
                 t: ^text;
                 b: @boolean;
                 k: @integer;
                 r: @real
              do
                 (for i: myRS.Info.NoOfCols repeat
                   i->myRS.info.getColumnInfo->theInfo[];
                   (if theInfo.DataType##
                    // text## then
                       i->current.getText->t[]
                    // integerObject## then
                       i->current.getInteger->k; &text[]->t[]; k->t.putInt
                    // realObject## then
                       i->current.getReal->r; &text[]->t[]; r->t.putReal
                    // booleanObject## then
                       i->current.getBoolean->b;
                       &text[]->t[];
                       (if b then 'Yes'->t[];  else 'No'->t[] if)
                    else
                       'Unknown data type!!!'->t[]
                   if);
                   t[]->puttext;
                   '\t'->puttext
                 for);
                 newline
              #)
         else
            'DML/DDL statement executed successfully!'->putLine
        if);
        
     #);
   myDBEnv.close
#)  

