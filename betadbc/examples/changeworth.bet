ORIGIN '/users/kursus/dprog2/BetaDBC/betadbc';
--- program: descriptor ---
(# rdbConnection: @connection
     (# changeWorth:
          (* this pattern implements the embedded SQL example from
           * page 187??? in the book: 'A First Course in Database
           * Systems' by Ullman and Widom.
           *)
          (# worth: @integer;
             selectStmt: @directSQLstatement;
             execResultSet: ^resultSet;
          do (* declare 'worth' to be a host variable *)
             'worth'->declareInteger(# set:: (# do value->worth #);
                                       get:: (# do worth->value #)
                                    #);
             (* note that we also keep the 'certno' key-field *)
             'SELECT networth, certno FROM movieexec'->selectStmt;
             (* execute the SQL query and place the result in 'execResultSet' *)
             selectStmt.execute->execResultSet[];
             (* in BetaDBC, scanning through the result is used
              * instead of stepping a resultSet
              *)
             ':worth %i'->execResultSet.scan
             (# modifyStmp: @directSQLstatement;
                (* Note that BetaDBC does not implement the resultSet
                 * CURRENT facility, which is why the following
                 * statements use to 'certno' key-value to specify
                 * which tuple to DELETE or UPDATE
                 *)
             do (if worth<1000 then
                    'DELETE FROM movieexec WHERE certno = %i'
                      ->modifyStmp
                 else
                    'UPDATE movieexec SET networth = 2 * networth WHERE certno = %i'
                      ->modifyStmp
                if);
                (* sets %i in 'modifyStmt' to the 'certno' key value: *)
                current.i->modifyStmp.i;
                modifyStmp.execute;
                modifyStmp.close
             #);
             selectStmt.close
          #)
     #)
do (2->arguments,3->arguments,none)->rdbConnection.open;
   rdbConnection.changeWorth;
   rdbConnection.close;
#)
