ORIGIN '~beta/basiclib/v1.6/betaenv';
INCLUDE '../bdbc'
        '~beta/basiclib/v1.6/file';
-- lib: Attributes --
executesqlfile:
  (#
     FileName,DataSource,UserName,PassWord: ^text;
     theCon: @Connection (# theStmt: @statementType #);
     statementType:< theCon.directSqlStatement;
     theFile: ^File;
     doneReading: @boolean;
     fileNoneException:< Exception
       (#  do 'file argument none'->msg.append #)
  enter (theFile[],DataSource[],UserName[],PassWord[])
  do
     (if theFile[] = none then fileNoneException if);
     (DataSource[],UserName[],PassWord[])->theCon.open;
     executeLoop:
       (# executeTxt: ^text
       do
          (if not theFile.EOS then
              &text[]->executeTxt[];
              false->doneReading;
              readLoop:
                (# txt: ^text
                do
                   (if not doneReading then
                       theFile.getLine->txt[];
                       (if txt.empty then
                           true->doneReading
                        else
                           txt[]->executeTxt.append
                       if);
                       restart readLoop
                   if)
                #);
              executeTxt[]->putLine;
              executeTxt[]->theCon.theStmt.Contents;
              theCon.theStmt.execute;
              restart executeLoop
          if)
       #);
     theCon.theStmt.close;
     theCon.close
  #)  

