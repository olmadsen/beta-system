ORIGIN 'betadbc';
BODY 'private/transactionsbody';
-- connectionLib: Attributes --
transactionsSupported:
(* Returns true iff this(connection) supports transactions *)
  (# isSupported: @boolean
  <<SLOT BetaDBCTransactionsSupported:DoPart>>
  exit isSupported
  #);
autoCommitMode:
(* In autoCommitMode, every database operation is a transaction
 * that is committed when performed. Disabling autoCommitMode is 
 * only meaningful when this(connection) supports transactions.
 * The default in BetaDBC is autoCommitMode
 *)
  (# autoCommit: @boolean
  enter (#  enter autoCommit <<SLOT BetaDBCAutoCommitModeEnter:DoPart>> #)
  exit
    (# 
    <<SLOT BetaDBCAutoCommitModeExit:DoPart>>
    exit autoCommit
    #)
  #);
readUncommitted: (* Allows dirty reads, nonrepeatable reads, and phantoms *)
 integerValue (#  <<SLOT BetaDBCreadUncommitted:DoPart>> #);
readCommitted:
(* Disallows dirty reads, but allows nonrepeatable reads and phantoms *)
 integerValue (#  <<SLOT BetaDBCReadCommitted:DoPart>> #);
repeatableRead:
(* Disallows dirty reads and nonrepeatable reads, but allows phantoms *)
 integerValue (#  <<SLOT BetaDBCRepeatableRead:DoPart>> #);
serializable:
(* Disallows dirty reads, nonrepeatable reads, and phantoms *) integerValue
  (#  <<SLOT BetaDBCSerializable:DoPart>> #);
transactionLevel:
(* Sets the transaction level of the current transaction in this(connection).
 * serializable is the default
 *)
  (# level: (* One of the above defined transaction levels *) @integer
  enter (#  enter level <<SLOT BetaDBCTransactionLevelEnter:DoPart>> #)
  exit
    (# 
    <<SLOT BetaDBCTransactionLevelExit:DoPart>>
    exit level
    #)
  #);
commit:
(* Commits all transactions pertaining to this connection.
 * Use transactionsSupported to check whether this is meaningful *)
  (#  <<SLOT BetaDBCCommit:DoPart>> #);
rollBack:
(* Rolls back all transactions pertaining to this(connection)
 * Use transactionsSupported to check whether this is meaningful.
 *) (#  <<SLOT BetaDBCRollBack:DoPart>> #)  

