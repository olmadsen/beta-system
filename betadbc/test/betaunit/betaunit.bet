ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/containers/list'
        '~beta/sysutils/objinterface';
-- lib: Attributes --
test:
  (#
     <<SLOT TestLib:Attributes>>;
     result: ^testResult;
     start:< object;
     end:< object;
     
  enter result[]
  do start; INNER ; end
  #);
testSuite: test
  (#
     <<SLOT TestSuiteLib:Attributes>>;
     assertNo:
       @integer;
     startTestCase:< (#  do 0->assertNo; INNER #);
     endTestCase:< object;
     testCase: test
       (#
          <<SLOT TestCaseLib:Attributes>>;
          msg: @text;
          assertion:
            (# 
            do
               assertNo+1->assertNo;
               msg.clear;
               INNER
            #);
          assert: assertion
            (# value: @boolean
            enter value
            do
               INNER ;
               (if value then
                   THIS(testCase)[]
                     ->result.addSuccess
                else
                   (if msg.empty then
                       'Assert no '->msg.puttext;
                       assertNo->msg.putint
                   if);
                   (THIS(testCase)[],msg[])->result.addFailure
               if)
            exit value
            #);
          assertTextEqual: assertion
            (# l,r: ^text; value: @boolean
            enter (l[],r[])
            do
               INNER ;
               l[]->r.equalNCS->value;
               (if value then
                   THIS(testCase)[]->result.addSuccess
                else
                   (if msg.empty then
                       '(Assert no '->msg.puttext;
                       assertNo->msg.putint;
                       ')'->msg.put
                   if);
                   msg.newline;
                   '[    \''->msg.append;
                   l[]->msg.puttext;
                   '\''->msg.putline;
                   '[ <> \''->msg.append;
                   r[]->msg.puttext;
                   '\''->msg.putline;
                   (THIS(testCase)[],msg[])->result.addFailure
               if)
            exit value
            #);
          assertIntegerEqual: assertion
            (# l,r: @integer; value: @boolean
            enter (l,r)
            do
               INNER ;
               (l = r)->value;
               (if value then
                   THIS(testCase)[]->result.addSuccess
                else
                   (if msg.empty then
                       '(Assert no '->msg.puttext;
                       assertNo->msg.putint;
                       ')'->msg.put
                   if);
                   msg.newline;
                   '[    '->msg.append;
                   l->msg.putint;
                   msg.newline;
                   '[ <> '->msg.append;
                   r->msg.putint;
                   msg.newline;
                   (THIS(testCase)[],msg[])->result.addFailure
               if)
            exit value
            #)
       do startTestCase; INNER ; endTestCase
       #);
     tests: @list (# element:: test #);
     addTest: (# aTest: ^test enter aTest[] do aTest[]->tests.append #)
  do INNER ; tests.scan (#  do result[]->current #)
  #);
testResult:
  (#
     <<SLOT TestResultLib:Attributes>>;
     failures: @list (# element:: test #);
     successes: @list (# element:: test #);
     addFailure:<
       (# failure: ^test; msg: ^text
       enter (failure[],msg[])
       do failure[]->failures.append; INNER
       #);
     addSuccess:<
       (# success: ^test
       enter success[]
       do success[]->successes.append; INNER
       #)
  #)  

