ORIGIN '../../betadbc';
INCLUDE '../../transactions'
        '../betaunit/textbetaunit';
-- program: Descriptor --
(#
   testPositionedUpdate: testSuite
     (#
        sqlCon: @connection;
        updateableStmtType: sqlCon.directSQLStatement
          (#
             cursorType::  (# readOnly::  (#  do false->value #) #);
             resultSetType:: 
               (# resultSetNotification::  (#  do msg[]->putline #) #)
          #);
        stmt: ^updateableStmtType;
        res: ^sqlCon.resultSet;
        startTestCase::  (#  do &updateableStmtType[]->stmt[]; stmt.open #);
        endTestCase::  (#  do stmt.close #);
        start:: 
          (# stmt: @sqlCon.directSQLStatement
          do
             (2->arguments,3->arguments,4->arguments)->sqlCon.open;
             stmt.open;
             'CREATE TABLE myTable (myTuple VARCHAR(55))'->stmt;
             stmt.execute;
             'INSERT INTO myTable VALUES (\'a\')'->stmt;
             stmt.execute;
             'INSERT INTO myTable VALUES (\'b\')'->stmt;
             stmt.execute;
             'INSERT INTO myTable VALUES (\'c\')'->stmt;
             stmt.execute;
             stmt.close
          #);
        end:: 
          (# stmt: @sqlCon.directSQLStatement
          do
             stmt.open;
             'DROP TABLE myTable'->stmt;
             stmt.execute;
             stmt.close;
             sqlCon.close
          #);
        testName: testCase
          (# 
          do
             'SELECT * FROM myTable'->stmt;
             stmt.execute->res[];
             (res.cursorName->(res.cursorName).equalNCS)->assert
          #);
        testSimple: testCase
          (#
             updateStmtTxt: ^text;
             stmt: @sqlCon.directSQLStatement
               (# cursorType::  (# name::  (#  do 'C1'->value[] #) #)
               #);
             updateStmt: @sqlCon.directSQLStatement
          do
             stmt.open;
             'SELECT * FROM myTable FOR UPDATE OF myTuple'->stmt;
             stmt.getExpanded->putline;
             stmt.execute->res[];
             '1'->putline;
             updateStmt.open;
             res.scan
               (# 
               do
                  'UPDATE myTable SET myTuple=\'marius\' WHERE CURRENT OF C1'
                    ->updateStmtTxt[];
                  res.cursorName->putline;
                  updateStmtTxt[]->updateStmt;
                  updateStmt.getExpanded->putline;
                  '1'->putline;
                  updateStmt.execute;
                  '1'->putline
               #)
          #)
     do &testName[]->addTest; &testSimple[]->addTest
     #)
do &writingTestResult[]->testPositionedUpdate
#)  

