ORIGIN '../../betadbc';
INCLUDE '../../transactions'
        '../betaunit/textbetaunit';
-- program: Descriptor --
(#
   testSensivity: testSuite
     (#
        myTestCase: testCase
          (#
             sqlCon: @connection
               (#
                  testResultSet: resultSet
                    (# resultSetException::  (#  do true->continue #) #)
               #);
             start:: 
               (# stmt: @sqlCon.directSQLStatement
               do
                  (2->arguments,3->arguments,4->arguments)->sqlCon.open;
                  stmt.open;
                  'CREATE TABLE myTable (myTuple VARCHAR(55))'->stmt;
                  stmt.execute
               #);
             end:: 
               (# stmt: @sqlCon.directSQLStatement
               do
                  stmt.open;
                  'DROP TABLE myTable'->stmt;
                  stmt.execute;
                  stmt.close;
                  sqlCon.commit;
                  sqlCon.close
               #)
          do INNER
          #);
        testSensivityCase: myTestCase
          (# stmt: @sqlCon.directSQLStatement; res: ^connection.resultSet
          do
             stmt.open;
             'SELECT * FROM MyTable'->stmt;
             stmt.execute->res[];
             stmt.close
          #);
        testInsensivityCase: myTestCase
          (#
             stmt: @sqlCon.directSQLStatement
               (#
                  cursorType:: 
                    (# insensitive::  (#  do true->value #) #)
               #);
             res: ^connection.resultSet
          do
             stmt.open;
             'SELECT * FROM MyTable'->stmt;
             stmt.execute->res[];
             stmt.close
          #)
     do &testSensivityCase[]->addTest; &testInsensivityCase[]->addTest
     #)
do &writingTestResult[]->testSensivity
#)  

