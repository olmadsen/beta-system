ORIGIN '../../betadbc';
INCLUDE '../../transactions'
        '../betaunit/textbetaunit';
-- program: Descriptor --
(#
   testAccess: testSuite
     (#
        sqlCon: @connection;
        start:: 
          (# stmt: @sqlCon.directSQLStatement
          do
             (2->arguments,3->arguments,4->arguments)->sqlCon.open;
             stmt.open;
             'CREATE TABLE navne (navn VARCHAR(55))'->stmt;
             stmt.execute;
             
          #);
        end:: 
          (# stmt: @sqlCon.directSQLStatement
          do
             stmt.open;
             'DROP TABLE navne'->stmt;
             stmt.execute;
             stmt.close;
             sqlCon.commit;
             sqlCon.close
          #);
        testAutoCommitMode: testCase
          (# 
          do
             true->sqlCon.autoCommitMode;
             sqlCon.autoCommitMode->assert;
             false->sqlCon.autoCommitMode;
             (not sqlCon.autoCommitMode)->assert
          #);
        testTransactionsSupported: testCase
          (#  do (sqlCon.transactionsSupported)->assert;  #);
        testTransactionLevelsSupported: testCase
          (# 
          do
             (not (sqlCon.readUncommitted->sqlCon.transactionLevelSupported))
               ->assert;
             (not (sqlCon.readCommitted->sqlCon.transactionLevelSupported))
               ->assert;
             (not (sqlCon.repeatableRead->sqlCon.transactionLevelSupported))
               ->assert;
             (not (sqlCon.serializable->sqlCon.transactionLevelSupported))
               ->assert;
             
          #);
        testTransactions: testCase
          (# studioName: @text; stmt: @sqlCon.directSQLStatement
          do
             'studioName'
               ->sqlCon.declareText
                 (#
                    set::  (#  do value->studioName #);
                    get::  (#  do studioName[]->value[] #)
                 #);
             'marius'->studioName;
             stmt.open;
             'INSERT INTO Navne VALUES ( :studioName )'->stmt;
             stmt.execute;
             stmt.close;
             sqlCon.commit;
             (size = 1)->assert;
             'hansen'->studioName;
             stmt.open;
             'INSERT INTO Navne VALUES ( :studioName )'->stmt;
             stmt.execute;
             stmt.close;
             (size = 2)->assert;
             sqlCon.rollBack;
             (size = 1)->assert;
             
          #);
        size: integerValue
          (# stmt: @sqlCon.directSQLStatement; res: ^sqlCon.resultSet
          do
             stmt.open;
             'SELECT COUNT(*) FROM Navne'->stmt;
             stmt.execute->res[];
             res.scan
               (#  do current.i->value #);
             stmt.close
          #);
        
     do
        &testAutoCommitMode[]->addTest;
        &testTransactionsSupported[]->addTest;
        &testTransactionLevelsSupported[]->addTest;
        &testTransactions[]->addTest;
        
     #)
do &writingTestResult[]->testAccess
#)  

