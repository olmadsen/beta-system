ORIGIN '~beta/basiclib/betaenv';
INCLUDE '../private/patternparser'
        'betaunit/textbetaunit';
-- program: Descriptor --
(#
   testPatternParser: testSuite
     (#
        t: ^text;
        p: @PatternParser;
        startTestCase::  (#  do &text[]->t[] #);
        start::  (#  do  #);
        end::  (#  do  #);
        testSimpleNamedResultSetPattern: testCase
          (#
             res: ^P.ResultSetPattern;
             res2: ^p.NamedResultSetPattern;
             aNamedSpecifier: ^p.NamedSpecifier
          do
             'def1%c1 d2:varName2'->t[];
             t.reset;
             t[]->p.parseResultSetPatternString->res[];
             (if (res## = p.NamedResultSetPattern##)->assert then
                 res[]->res2[];
                 (2 = res2.NamedSpecifiers.size)->assert;
                 T1:
                   (# thePlaceHolder: ^p.placeholder
                   do
                      (res2.namedSpecifiers.head).elm[]->aNamedSpecifier[];
                      ('def1'->aNamedSpecifier.theNameDecl.value.equalNCS)
                        ->assert;
                      (if
                      (p.PlaceHolder## = aNamedSpecifier.theUnnamedSpecifier##)
                        ->assert then
                          aNamedSpecifier.theUnnamedSpecifier[]
                            ->thePlaceholder[];
                          ('c' = thePlaceholder.mark)->assert
                      if)
                   #);
                 T2:
                   (# theVariable: ^p.Variable
                   do
                      (res2.namedSpecifiers.last).elm[]->aNamedSpecifier[];
                      ('d2'->aNamedSpecifier.theNameDecl.value.equalNCS)
                        ->assert;
                      (if
                      (p.Variable## = aNamedSpecifier.theUnnamedSpecifier##)
                        ->assert then
                          aNamedSpecifier.theUnnamedSpecifier[]->theVariable[];
                          ('varName2'->theVariable.value.equalNCS)->assert
                      if)
                   #);
                 T3:
                   (# s: ^text
                   do
                      res[]->p.unparseNode->t[];
                      t.copy->s[];
                      s.reset;
                      s[]->p.parseResultSetPatternString->res[];
                      res[]->p.unparseNode->s[];
                      (s[]->t.equalNCS)
                        ->assert (#  do 'parseunparse'->msg #)
                   #)
             if)
          #);
        testSimpleUnnamedResultSetPattern: testCase
          (#
             res: ^p.ResultSetPattern;
             res2: ^p.UnnamedResultSetPattern;
             anunNamedSpecifier: ^p.UnnamedSpecifier
          do
             ' %c1   :varName2 '->t[];
             t.reset;
             t[]->p.parseResultSetPatternString->res[];
             (if (res## = p.UnnamedResultSetPattern##)->assert then
                 res[]->res2[];
                 (2 = res2.UnnamedSpecifiers.size)->assert;
                 T1:
                   (# thePlaceHolder: ^p.placeholder
                   do
                      (res2.unnamedSpecifiers.head).elm[]->anunNamedSpecifier[];
                      (if (p.PlaceHolder## = anUnnamedSpecifier##)->assert then
                          anUnnamedSpecifier[]->thePlaceholder[];
                          ('c' = thePlaceholder.mark)->assert
                      if)
                   #);
                 T2:
                   (# theVariable: ^p.Variable
                   do
                      (res2.unnamedSpecifiers.last).elm[]->anunNamedSpecifier[];
                      (if (p.Variable## = anUnnamedSpecifier##)->assert then
                          anUnnamedSpecifier[]->theVariable[];
                          ('varName2'->theVariable.value.equalNCS)->assert
                      if)
                   #);
                 T3:
                   (# s: ^text
                   do
                      res[]->p.unparseNode->t[];
                      t.copy->s[];
                      s.reset;
                      s[]->p.parseResultSetPatternString->res[];
                      res[]->p.unparseNode->s[];
                      (s[]->t.equalNCS)
                        ->assert (#  do 'parseunparse'->msg #)
                   #)
             if)
          #);
        testStatementPattern: testCase
          (#
             start::  (#  do putline; false->p.theTracer #);
             end::  (#  do false->p.theTracer #);
             res: ^p.StatementPattern;
             res2: ^p.StatementSpecifier;
             anunNamedSpecifier: ^p.UnnamedSpecifier;
             t2: ^text;
             i: @integer
          do
             'xyz def\\%t var1:var  var2%f :var3 %t err'->t[];
             t.reset;
             t[]->p.parseStatementPatternString->res[];
             T1:
               (# 
               do
                  (7 = res.StatementStringOrSpecifiers.size)
                    ->assert (#  do 'size'->msg #)
               #);
             T2:
               (# 
               do
                  0->i;
                  res.StatementStringOrSpecifiers.scan
                    (# 
                    do
                       i+1->i;
                       (if i
                        // 1 then
                           current## = p.StatementString##
                             ->assert
                               (# 
                               do 'current## = p.StatementString##'->msg
                               #)
                        // 2 then
                           current## = p.StatementString##
                             ->assert
                               (# 
                               do 'current## = p.StatementString##'->msg
                               #)
                        // 3 then
                           current## = p.StatementSpecifier##
                             ->assert
                               (# 
                               do 'current## = p.StatementSpecifier##'->msg
                               #)
                        // 4 then
                           current## = p.StatementSpecifier##
                             ->assert
                               (# 
                               do 'current## = p.StatementSpecifier##'->msg
                               #)
                        // 5 then
                           current## = p.StatementSpecifier##
                             ->assert
                               (# 
                               do 'current## = p.StatementSpecifier##'->msg
                               #)
                        // 6 then
                           current## = p.StatementSpecifier##
                             ->assert
                               (# 
                               do 'current## = p.StatementSpecifier##'->msg
                               #)
                        // 7 then
                           current## = p.StatementString##
                             ->assert
                               (# 
                               do 'current## = p.StatementString##'->msg
                               #)
                       if)
                    #)
               #);
             T3:
               (# 
               do
                  res[]->p.unparseNode->t[];
                  t.reset;
                  t.copy->t2[];
                  t[]->p.parseStatementPatternString->res[];
                  res[]->p.unparseNode->t[];
                  (t2[]->t.equal)->assert (#  do 'parseunparse'->msg #)
               #)
          #)
     do
        &testSimpleNamedResultSetPattern[]->addTest;
        &testSimpleUnnamedResultSetPattern[]->addTest;
        &testStatementPattern[]->addTest
     #)
do &writingTestResult[]->testPatternParser
#)  

