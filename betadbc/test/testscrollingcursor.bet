ORIGIN '../betadbc';
INCLUDE '../scrollingresultset'
        'betaunit/textbetaunit';
-- program: Descriptor --
(#
   testScrollingResultSet: testSuite
     (#
        sqlCon: @connection;
        stmt: ^connection.directSQLStatement;
        startTestCase::
          (# 
          do
             &sqlCon.directSQLStatement
               (# resultSetType:: sqlCon.scrollingResultSet #)[]->stmt[];
             stmt.open
          #);
        start::
          (# stmt: @sqlCon.directSQLStatement
          do
             (2->arguments,3->arguments,4->arguments)->sqlCon.open;
             stmt.open;
             'CREATE TABLE myTable (myTuple VARCHAR(55))'->stmt;
             stmt.execute;
             'INSERT INTO myTable VALUES (\'a\')'->stmt;
             stmt.execute;
             'INSERT INTO myTable VALUES (\'b\')'->stmt;
             stmt.execute;
             'INSERT INTO myTable VALUES (\'c\')'->stmt;
             stmt.execute;
             stmt.close
          #);
        end::
          (# stmt: @sqlCon.directSQLStatement
          do 'DROP TABLE myTable'->stmt; stmt.execute; stmt.close; sqlCon.close
          #);
        testSimple: testCase
          (# res: ^sqlCon.scrollingResultSet
          do 'SELECT * FROM myTable'->stmt; stmt.execute->res[]; stmt.close
          #);
        testFetchAbsolute: testCase
          (# res: ^sqlCon.scrollingResultSet
          do
             'SELECT * FROM myTable'->stmt;
             stmt.execute->res[];
             (for i: 3 repeat
               i->res.fetchAbsolute; (res.current).s->putline
             for);
             stmt.close
          #)
     do &testSimple[]->addTest; &testFetchAbsolute[]->addTest
     #)
do &writingTestResult[]->testScrollingResultSet
#)  

