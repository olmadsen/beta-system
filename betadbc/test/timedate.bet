ORIGIN '../betadbc';
INCLUDE '~beta/basiclib/formatio';
-- program: Descriptor --
(#
   sqlCon: @connection;
   stmt: @sqlCon.directSqlStatement;
   line: (#  do (for i: 40 repeat '-'->put for); newline #);
   todayT,todayC,todayD: @time;
   testMarkers:
     (# 
     do
        'TESTMARKERS'->putline;
        'Testing time markers:'->putline;
        'insert into datoer(dato) values (%d)'->stmt;
        timenow->stmt.d;
        stmt.execute;
        stmt.getExpanded->putline;
        'insert into datoer(punkt) values (%d)'->stmt;
        timenow->stmt.d;
        stmt.getExpanded->putline;
        stmt.execute;
        'insert into datoer(tid) values (%c)'->stmt;
        timenow->stmt.c;
        stmt.getExpanded->putline;
        stmt.execute;
        'insert into datoer(dato) values (%t)'->stmt;
        timenow->stmt.t;
        stmt.getExpanded->putline;
        stmt.execute
     #);
   testResultMarkers:
     (# 
     do
        'TESTRESULTMARKERS'->putline;
        'select dato from datoer'->stmt;
        'Column as clock: '->putline;
        (stmt.execute).scan
          (#  do current.c->puttime; newline #);
        'Column as date: '->putline;
        (stmt.execute).scan
          (#  do current.d->puttime; newline #);
        'Column as time: '->putline;
        (stmt.execute).scan
          (#  do current.t->puttime; newline #)
     #);
   testEmbedded:
     (# 
     do
        'TESTEMBEDDED'->putline;
        'Insert into datoer(dato) values (:todayT)'->stmt;
        timeNow->todayT;
        stmt.getExpanded->putline;
        'Insert into datoer(dato) values (:todayD)'->stmt;
        timeNow->todayD;
        stmt.getExpanded->putline;
        'Insert into datoer(dato) values (:todayC)'->stmt;
        timeNow->todayC;
        stmt.getExpanded->putline
     #);
   testFetch:
     (# 
     do
        'TESTFETCH'->putline;
        'select dato from datoer'->stmt;
        'Fetching as clock:'->putline;
        ':todayC'->(stmt.execute).scan (#  do todayC->putTime; newline #);
        'Fetching as date:'->putline;
        ':todayD'->(stmt.execute).scan (#  do todayD->putTime; newline #);
        'Fetching as time:'->putline;
        ':todayT'->(stmt.execute).scan (#  do todayT->putTime; newline #)
     #);
   testDataTypes:
     (#
        rs: ^connection.resultSet;
        printDataType:
          (# col: ^connection.resultset.column
          enter rs.getColumnByName->col[]
          do
             'Name:'->puttext;
             col.name[]->putline;
             'No:'->puttext;
             col.no->putint;
             newline;
             (if col.dataType##
              // time## then 'Type: time [sic]'->putline
              else
                 'Strange datatype'->putline
             if);
             'dataTypeName:'->puttext;
             col.dataTypeName[]->putline;
             'dataTypeNo:'->puttext;
             col.dataTypeNo->putint;
             newline;
             'nullAble:'->puttext;
             (if col.nullAble then 'yes'->putline else 'no'->putline if)
          #)
     do
        'TESTDATATYPE 1'->putline;
        'select * from datoer'->stmt;
        stmt.execute->rs[];
        'dato'->printDataType;
        'TESTDATATYPE 2'->putline;
        'tid'->printDataType
     #);
   createTables:
     (# 
     do
        'Creating table:'->putline;
        'CREATE TABLE datoer (dato TimeStamp, tid Time, punkt date)'->stmt;
        stmt->puttext;
        stmt.execute;
        '--- done'->putline
     #);
   dropTables:
     (# 
     do
        'Creating table:'->putline;
        'DROP TABLE datoer'->stmt;
        stmt->puttext;
        stmt.execute;
        '--- done'->putline
     #)
do
   (2->arguments,3->arguments,4->arguments)->sqlCon.open;
   'todayT'
     ->sqlCon.declareTime
       (#
          set::  (#  do value->todayT #); get::  (#  do todayT->value #)
       #);
   'todayC'
     ->sqlCon.declareClock
       (#
          set::  (#  do value->todayC #); get::  (#  do todayC->value #)
       #);
   'todayD'
     ->sqlCon.declareDate
       (#
          set::  (#  do value->todayD #); get::  (#  do todayD->value #)
       #);
   line;
   createTables;
   line;
   testMarkers;
   line;
   testResultMarkers;
   line;
   testEmbedded;
   line;
   testDataTypes;
   line;
   testFetch;
   line;
   dropTables;
   stmt.close;
   sqlCon.close
#)  

