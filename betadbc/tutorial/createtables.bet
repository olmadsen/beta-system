ORIGIN '/users/kursus/dprog2/BetaDBC/betadbc';
INCLUDE '~beta/basiclib/file';
-- lib: Attributes --
executeSqlFile:
(* Reads the file 'fileName' and executes 
 * the SQL statements in that file while connected to 'connectionName' as user
 * 'userName' with password 'passWord'.
 * The SQL statements in the file must be separated by empty lines *)
  (#
     fileName,connectionName,userName,passWord: ^text;
     sqlFile: @file;
     sqlCon: @connection (# stmt: @directSqlStatement #)
  enter (fileName[],connectionName[],userName[],passWord[])
  do
     (connectionName[],userName[],passWord[])->sqlCon.open;
     fileName[]->sqlFile.name;
     sqlFile.openRead;
     executeLoop:
       (# executeTxt: @text
       do
          executeLoop:
          (if not sqlFile.EOS then
              readLoop:
              (if (sqlFile.getLine->executeTxt.append).empty then
                  leave readLoop
               else
                  restart readLoop
              if);
              executeTxt[]->sqlCon.stmt->putline;
              sqlCon.stmt.execute;
              restart executeLoop
          if)
       #);
     sqlCon.stmt.close;
     sqlCon.close
  #)  

-- program: Descriptor --
(* Usage: executesqlfile <sqlfile> <connection> [<username> [<password>]] *)
  (# 
  do
     (if (noOfArguments >= 3) and (noOfArguments <= 5) then
         (if noOfArguments
          // 3 then
             (2->arguments,3->arguments,none ,none )->executesqlfile
          // 4 then
             (2->arguments,3->arguments,4->arguments,none )->executesqlfile
          // 5 then
             (2->arguments,3->arguments,4->arguments,5->arguments)
               ->executesqlfile
         if)
      else
         'Usage: executesqlfile <sqlfile> <connection> [<username> [<password>]]'
           ->putLine
     if)
  #)  

