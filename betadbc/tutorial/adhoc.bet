ORIGIN '~beta/betadbc/betadbc_mysql';
INCLUDE '~beta/basiclib/formatio'
        '~beta/basiclib/numberio' '~beta/basiclib/file';
-- program: Descriptor --
(#
   sqlCon: @Connection; stmt: @sqlCon.DirectSQLStatement; res: ^sqlCon.resultSet
do
   'hi there'->putline;
   (2->arguments,3->arguments,4->arguments)->sqlCon.open;
   'fgcbase'->sqlCon.selectdb;
   stmt.open;
   L: cycle
     (# 
     do
        'Statement to execute: '->puttext;
        getline->stmt;
        (if (stmt).empty then leave L if);
        stmt.execute->res[];
        (for j: res.columnCount repeat
          '%s (%s)\t'
            ->putFormat
              (# 
              do
                 (j->res.getColumn).name[]->s;
                 (j->res.getColumn).dataTypeName[]->s
              #)
        for);
        newline;
        (if res.columnCount > 0 then
            res.scan
              (# 
              do 
                 (for i: res.columnCount repeat
                   (if (i->res.getColumn).DataType##
                    // text## then
                       current.s->puttext
                    // integerObject## then
                       current.i->putint
                    // realObject## then
                       current.f->putreal
                    // booleanObject## then
                       (if current.b then
                           'true'->puttext; 
                        else
                           'false'->puttext
                       if)
                    // time## then
                       current.t->puttime
                    else
                       'Unknown data type!!!'->puttext
                   if);
                   '\t'->puttext
                 for);
                 newline
              #)
         else
            'DML/DDL statement executed successfully!'->putLine
        if)
     #);
   stmt.close;
   sqlCon.close
#)  

