ORIGIN '~beta/betadbc/betadbc_odbc';
INCLUDE '~beta/basiclib/formatio';
-- program: Descriptor --
(#
   sqlCon: @connection;
   stmt: @sqlCon.directSqlStatement;
   res: ^connection.resultSet;
   studioName,presName: @text;
   presNetWorth: @integer
do
   'studioName'
     ->sqlCon.declareText
       (#
          set::  (#  do value->studioName #);
          get::  (#  do studioName[]->value[] #)
       #);
   'presName'
     ->sqlCon.declareText
       (#
          set::  (#  do value->presName #);
          get::  (#  do presName[]->value[] #)
       #);
   'presNetWorth'
     ->sqlCon.declareInteger
       (#
          set::  (#  do value->presNetWorth #);
          get::  (#  do presNetWorth->value #)
       #);
   (2->arguments,3->arguments,4->arguments)->sqlCon.open;
   'Input a studio name: '->puttext;
   getLine->studioName.puttext;
   'SELECT MovieExec.name, netWorth FROM Studio, MovieExec WHERE presCNo = certNo AND Studio.name = :studioName'
     ->stmt.open;
   stmt.getExpanded->putline;
   stmt.execute->res[];
   ':presName :presNetWorth'
     ->res.scan
       (# 
       do
          'The net worth of the president %s \nof %s is $ %i\n'
            ->putFormat
              (#  do presName[]->s; studioName[]->s; presNetWorth->i #)
       #);
   stmt.close;
   sqlCon.close
#)  

