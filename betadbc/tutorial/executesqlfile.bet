ORIGIN '~beta/betadbc/betadbc_odbc';
INCLUDE '~beta/basiclib/file';
-- lib: Attributes --
executeSqlFile:
(* Reads the file 'fileName' and executes 
 * the SQL statements in that file while connected to 
 * 'connectionName' as user 'userName' with password 'passWord'.
 * The SQL statements in the file must be separated by empty lines *)
  (#
     fileName,connectionName,userName,passWord: ^text;
     sqlFile: @file;
     sqlCon: @connection;
     stmt: @sqlCon.directSQLStatement
  enter (fileName[],connectionName[],userName[],passWord[])
  do
     (connectionName[],userName[],passWord[])->sqlCon.open;
     stmt.open;
     fileName[]->sqlFile.name;
     sqlFile.openRead;
     executeLoop:
       (# executeTxt: @text; txt: ^text
       do
          (if not sqlFile.EOS then
              executeTxt.clear;
              readLoop:
              (if (sqlFile.getLine->txt[]).empty then
                  leave readLoop
               else
                  txt[]->executeTxt.append; restart readLoop
              if);
              executeTxt[]->stmt->putline;
              newline;
              stmt.execute;
              restart executeLoop
          if)
       #);
     stmt.close;
     sqlCon.close
  #)  

-- program: Descriptor --
(* Usage: executesqlfile <sqlfile> <connection> [<username> [<password>]] *)
  (# 
  do
     (if (noOfArguments >= 3) and (noOfArguments <= 5) then
         (2->arguments,3->arguments,4->arguments,5->arguments)->executesqlfile
      else
         'Usage: executesqlfile <sqlfile> <connection> [<username> [<password>]]'
           ->putLine
     if)
  #)  

