ORIGIN '../betadbc';
INCLUDE '~beta/basiclib/formatio';
-- program: Descriptor --
(#
   sqlCon: @connection;
   stmt: @sqlCon.directSqlStatement;
   rs: ^sqlCon.resultSet;
   digits: @integer;
   counts: [15] @integer;
   worth: @integer
do
   'worth'
     ->sqlCon.declareInteger
       (# set::  (#  do value->worth #); get::  (#  do worth->value #)
       #);
   (2->arguments,3->arguments,4->arguments)->sqlCon.open;
   'SELECT netWorth FROM MovieExec'->stmt;
   ':worth'
     ->(stmt.execute).scan
       (# 
       do
          1->digits;
          L:
          (if (worth / 10->worth) > 0 then digits+1->digits; restart L if);
          (if digits <= 15 then counts[digits]+1->counts[digits] if)
       #);
   (for j: 15 repeat
     'digits = %d: number of execs = %d\n'
       ->putFormat (#  do j->d; counts[j]->d #)
   for);
   stmt.close;
   sqlCon.close
#)  

