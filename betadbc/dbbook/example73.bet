ORIGIN '../betadbc';
INCLUDE '~beta/basiclib/formatio';
-- program: Descriptor --
(#
   sqlCon: @connection;
   stmt: @sqlCon.directSqlStatement;
   studioName: @text;
   presNetWorth: @integer
do
   'studioName'
     ->sqlCon.declareText
       (#
          set::  (#  do value->studioName #);
          get::  (#  do studioName[]->value[] #)
       #);
   'presNetWorth'
     ->sqlCon.declareInteger
       (#
          set::  (#  do value->presNetWorth #);
          get::  (#  do presNetWorth->value #)
       #);
   (2->arguments,3->arguments,4->arguments)->sqlCon.open;
   'Input a studio name: '->puttext;
   getLine->studioName.puttext;
   'SELECT netWorth FROM Studio, MovieExec WHERE presCNo = certNo AND Studio.name = :studioName'
     ->stmt.open;
   stmt.getExpanded->putline;
   ':presNetWorth'
     ->(stmt.execute).scan
       (# 
       do
          'The net worth of the president of %s is %i $\n'
            ->putFormat (#  do studioName[]->s; presNetWorth->i #)
       #);
   stmt.close;
   sqlCon.close
#)  

