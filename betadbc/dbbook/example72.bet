ORIGIN '../betadbc';
INCLUDE '~beta/basiclib/formatio';
-- program: Descriptor --
(#
   sqlCon: @connection;
   stmt: @sqlCon.directSqlStatement;
   studioName,studioAddr: @text
do
   (2->arguments,3->arguments,4->arguments)->sqlCon.open;
   'studioName'
     ->sqlCon.declareText
       (#
          set::  (#  do value->studioName #);
          get::  (#  do studioName[]->value[] #)
       #);
   'studioaddr'
     ->sqlCon.declareText
       (#
          set::  (#  do value->studioAddr #);
          get::  (#  do studioAddr[]->value[] #)
       #);
   'Input a studio name: '->puttext;
   getLine->studioName.puttext;
   ' and address: '->puttext;
   getLine->studioAddr.puttext;
   'INSERT INTO Studio(name, address) VALUES (:studioName, :studioAddr)'
     ->stmt.open;
   stmt.getExpanded->putline;
   (stmt.execute).tupleCount->putint;
   ' row(s) affected by the INSERT statement'->putline;
   stmt.close;
   sqlCon.close
#)  

