ORIGIN '~beta/guienv/figureitems';
INCLUDE '../png';
INCLUDE '../pngalpha';
INCLUDE '~beta/guienv/graphics';
INCLUDE '~beta/guienv/utils/guienvadds';
-- windowLib: attributes --

redish:
  (# exit (0xDDDD, 0x3333, 0x3333)#);
bluish:
  (#exit (0x2222, 0x3333, 0xAAAA) #);
yellowish:
  (# exit (0xDDDD, 0xDDDD, 0xCCCC) #);
white:
  (# exit (0xFFFF, 0xFFFF, 0xFFFF) #);
FigureEditor: Canvas
  (# Station: Oval
       (# xpos, ypos: @integer;
          anchor:
            (# setAnchor:
                 (# w, h: @integer;
                 enter (xpos, ypos)
                 do size -> (w, h);
                    (xpos - w div 2, ypos - h div 2) -> position; 
                 #);
            enter setAnchor
            exit (xpos, ypos)
            #);
          open::
            (#
            do redish -> pen.foregroundColor;
               2 -> pen.size;
               yellowish -> fill.foregroundColor;
               yellowish -> fill.backgroundColor;
               patterns.white[] -> fill.tile;
               yellowish -> fill.foregroundColor;
               yellowish -> fill.backgroundColor;
               (40, 40) -> size;
            #);
       #);
     
     logo: figureItem
       (# pm: ^pixmap;
          eventHandler::
            (#
               onRefresh::
                 (# x, y: @integer;
                 do (if pm[] <> NONE then
                        father.graphics
                        (#
                        do position -> (x, y);
                           (pm[], x, y)
                             -> drawRasterAlpha
                        #);
                    if);
                 #);
            #);
       #);
     newStation:
       (# x, y: @integer;
          theStation: ^Station;
       enter (x, y)
       do &Station[] -> theStation[];
          theStation.open;
          (x, y) -> theStation.anchor;
          theStation.bringBack;
       #);
     open::<
       (#
       do white -> backgroundColor;
          INNER;
       #);
     eventHandler::
       (#
          onMouseDown::
            (# 
            do localPosition -> newStation;
            #);
       #);
  #);


-- program: descriptor --
GUIenv
(# pict: @pixmap;
   
   main: @Window
     (# menubarType::
          (# filemenu: @Menu
               (# save: @menuItem
                    (# eventHandler::
                         (#
                            onSelect::
                              (#
                                 
                              do ('image.png') -> editor.writePNG;
                              #);
                         #);
                       open::
                         (#
                         do 'Save' -> name;
                         #);
                    #);
                  saveArea: @menuItem
                    (# eventHandler::
                         (#
                            onSelect::
                              (# area: @rectangle;
                                 
                              do 40 -> area.left;
                                 40 -> area.top;
                                 (398, 201) -> area.size;
                                 ('image.png', area) -> editor.writePNGarea;
                              #);
                         #);
                       open::
                         (#
                         do 'Save Area' -> name;
                         #);
                    #);
                  open::
                    (#
                    do 'File' -> name;
                       save.open;
                       save[] -> append;
                       saveArea.open;
                       saveArea[] -> append;
                    #);
               #);
             open::
               (#
               do filemenu.open;
                  filemenu[] -> append;
               #);
          #);
        editor: @FigureEditor
          (# theLogo: @Logo
               (#
                  open::
                    (# 
                    do pict[] -> pm[];
                       (113, 13) -> position;
                       (0xFFFF, 0xFFFF, 0xFFFF) -> pm.transparentColor;
                    #);
               #);
             open::
               (#
               do theLogo.open;
                  (0xDDDD, 0xDDDD, 0xDDDD) -> backgroundColor;
               #);
          #);
        open::
          (#
          do 'logo.png' -> pict.readAlpha;
             'Stations' -> title;
             (600, 500) -> size;
             editor.open;
             (600, 500) -> editor.size;
             true -> editor.bindRight;
             true -> editor.bindBottom;
          #);
     #);
do main.open;
#)


