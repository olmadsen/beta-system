ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/toollibs/utils/PNG/pngalpha';


-- program: descriptor --
GUIenv
(#
   pm: @pixmap;
   alpha: @pixmap;
   rgb: @pixmap;
   filename: ^Text;
   arial: @TextStyle;
   darkBlue: @color;
   
   main: @window
     (# content: @canvas
          (# eventHandler::
               (# onRefresh::
                    (#
                    do graphics
                       (#
                          drawChecker:
                            (# x, y, width, height: @integer;
                               h, v: @integer;
                               box: @rectangle;
                              rowswitch, colswitch: @boolean;
                            enter (x, y, width, height)
                            do width div 15 -> h;
                               height div 15  -> v;
                               (for i: 15  repeat
                                    not colswitch -> colswitch -> rowswitch;
                                    (for j: 15 repeat
                                         x + (i - 1) * h -> box.left;
                                         y + (j - 1) * v -> box.top;
                                         box.left + h -> box.right;
                                         box.top + v -> box.bottom;
                                         (if rowswitch then
                                             box -> fillRect;
                                         if);
                                         not rowswitch -> rowswitch;
                                    for)
                               for);
                            #);
                       do arial[] -> style;
                          darkBlue -> pen.foregroundcolor;
                          (13, 20 + 20 + arial.ascent) -> MoveTo;
                          'RGB + alpha channel' -> drawText;
                          (pm[], 230, 20) -> drawRasterAlpha;
                          (13, 20 + 40 + pm.height + arial.ascent) -> MoveTo;
                          'RGB channel' -> drawText;
                          (rgb[], (0, 0), (230, 40 + pm.height), pm.width, pm.height) -> drawRaster;
                          (13, 20 + 3*20 + 2*pm.height + arial.ascent) -> MoveTo;
                          'Alpha channel' -> drawText;
                          (alpha[], (0, 0), (230, 3*20 + 2*pm.height), alpha.width, alpha.height) -> drawRaster;
                          (13, 20 + 4*20 + 3*pm.height + arial.ascent) -> MoveTo;
                          'With background' -> drawText;
                          (0.7 * 0xFFFF, 0.5 * 0xFFFF, 0.3 * 0xFFFF) -> pen.foregroundcolor;
                          (230 - 13, 4*20 + 3*pm.height, pm.width + 26, pm.height + 26) -> drawChecker;
                          (pm[], 230, 4*20 + 3*pm.height + 13) -> drawRasterAlpha;
                       #);
                    #);
               #);
             open::
               (#
               do (0xFFFF, 0xFFFF, 0xFFFF) -> backgroundColor;
                  (700, 700) -> size;
                  true -> bindRight;
                  true -> bindBottom;
               #);
          #);
        open::
          (#
          do 'Main' -> title;
             (700, 700) -> size;
             content.open;
             
   
          #);
     #);
   width, height: @integer;
   hbmp: @integer;
   result: @integer;
do (if NoOfArguments = 2 then
       2 -> arguments -> filename[];
    else
       'toucan.png' -> filename[];
   if);
   'Arial' -> arial.name;
   14 -> arial.size;
   0.1 * 0xFFFF -> darkBlue.red;
   0.2 * 0xFFFF -> darkBlue.green;
   0.4 * 0xFFFF -> darkBlue.blue;
   filename[] -> pm.readAlpha;
   filename[] -> alpha.readAlphaImage;
   filename[] -> rgb.readRGB;
   main.open;
#)
