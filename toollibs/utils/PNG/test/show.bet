ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/graphics';

-- program: descriptor --
GUIenv
(#
   pm: @pixmap;
   filename: ^Text;
   
   main: @window
     (# menubarType::
          (# filemenu: @Menu
               (# saveItem: @MenuItem
                    (# eventHandler::
                         (# onSelect::
                              (#
                              do ('image.png', ((0, 0), (pm.width, pm.height)), 8) 
                                   -> pm.writePNG;
                              #);
                         #);
                       open::
                         (#
                         do 'Save' -> name;
                         #);
                    #);
                  open::
                    (#
                    do saveItem.open;
                       saveItem[] -> append;
                    #);
               #);
             open::
               (#
               do filemenu.open;
                  filemenu[] -> append;
               #);
             
          #);
        content: @canvas
          (# eventHandler::
               (# onRefresh::
                    (#
                    do graphics
                       (#
                       do (pm[], (0, 0), (13, 13), pm.width, pm.height) -> drawRaster;
                       #);
                    #);
               #);
             open::
               (#
               do (0xFFFF, 0xFFFF, 0xFFFF) -> backgroundColor;
                  (pm.width + 26, pm.height + 26) -> size;
               #);
          #);
        open::
          (#
          do 'Main' -> title;
             (pm.width + 26, pm.height + 26) -> size;
             content.open;
             
          #);
     #);
do (if NoOfArguments = 2 then
       2 -> arguments -> filename[];
       filename[] -> pm.read;
	   (0, 0, 0) -> pm.transparentColor;
       main.open;
   else
       'toucan'  -> pm.read;
       main.open;
   if);
#)
