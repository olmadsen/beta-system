ORIGIN '../png';

INCLUDE '~beta/guienv/private/winnt/guienv_ntiprivate';
INCLUDE '~beta/guienv/figureitems';
INCLUDE 'winpnginterface';
INCLUDE '~beta/win32lib/dcmanagement';
INCLUDE '~beta/win32lib/windowsmisc';
INCLUDE '~beta/win32lib/bitmapsupport';

-- lib: attributes --

BetaCreateBitmap: external
  (# hdc: @integer;
     pixels: @integer;
     width, height: @integer;
     hbmp: @integer;
  enter (hdc, pixels, width, height)
  exit hbmp
  #);
-- CanvasWritePng: doPart --
do (# hwnd: @integer;
      hdc: @integer;
      result: @integer;
      area: [4] @integer;
      left, top, right, bottom: @integer;
      hbmMem, hbmOld: @integer;
      hdcWin: @integer;
      brush: @integer;
      col: @integer;
      red, green, blue: @integer;
      pixels: @integer;
   do interfaceObjectID -> hwnd;
      hwnd -> GetDC -> hdcWin;
      
      (hwnd, @@area[1]) -> GetClientRect;
      area[1] -> left;
      area[2] -> top;
      area[3] -> right;
      area[4] -> bottom;
      
      hdcWin -> CreateCompatibleDC -> hdc;
      
      (* (hdcWin, right - left, bottom - top) 
        -> CreateCompatibleBitmap 
        -> hbmMem;
       *)
      (hdcWin, @@pixels, right - left, bottom - top) 
        -> BetaCreateBitmap 
        -> hbmMem;
      
      (hdc, hbmMem) -> SelectObject -> hbmOld;
      
      hdc -> windowItemDevice;
      backgroundColor -> (red, green, blue);
      (@@col, red div 256) -> TOS'%PutByte';
      (@@col + 1, green div 256) -> TOS'%PutByte';
      (@@col + 2, blue div 256) -> TOS'%PutByte';
      (@@col + 3, 2) -> TOS'%PutByte';
      
      col -> CreateSolidBrush -> brush;
      (hdc, @@area[1], brush) -> ntFillRect;
      brush -> DeleteObject;
      0 -> brush;
      
      scan
      (#
      do (if current## <= figureItem## then
             current.theEventHandler.onRefresh;
         if);
      #);
      (hdc, hbmMem, filename) -> write_dib_to_png;
      
      (hdc, hbmOld) -> SelectObject;
      
      
      hbmMem -> DeleteObject;
      0 -> windowItemDevice;
      (hwnd, hdcWin) -> ReleaseDC -> result;
      hdc -> DeleteDC;
   #)
   
-- CanvasWritePNGarea: doPart --
do (# hwnd: @integer;
      hdc: @integer;
      result: @integer;
      left, top, right, bottom: @integer;
      ntarea: [4] @integer;
      hbmMem, hbmOld: @integer;
      hdcWin: @integer;
      brush: @integer;
      col: @integer;
      red, green, blue: @integer;
   do interfaceObjectID -> hwnd;
      hwnd -> GetDC -> hdcWin;
      area.left -> left;
      area.right -> right;
      area.top -> top;
      area.bottom -> bottom;
      0 -> ntarea[1];
      0 -> ntarea[2];
      right - left -> ntarea[3];
      bottom - top -> ntarea[4];
      
      hdcWin -> CreateCompatibleDC -> hdc;
      
      (hdcWin, right - left, bottom - top) 
        -> CreateCompatibleBitmap 
        -> hbmMem;
      (hdc, hbmMem) -> SelectObject -> hbmOld;
      
      hdc -> windowItemDevice;
      backgroundColor -> (red, green, blue);
      (@@col, red div 256) -> TOS'%PutByte';
      (@@col + 1, green div 256) -> TOS'%PutByte';
      (@@col + 2, blue div 256) -> TOS'%PutByte';
      (@@col + 3, 2) -> TOS'%PutByte';
      
      col -> CreateSolidBrush -> brush;
      (hdc, @@ntarea[1], brush) -> ntFillRect;
      brush -> DeleteObject;
      0 -> brush;
      (hdc, left,  top, 0) -> SetWindowOrgEx;
      
      scan
      (#
      do (if current## <= figureItem## then
             current.theEventHandler.onRefresh;
         if);
      #);
      (hdc, 0, 0, 0) -> SetWindowOrgEx;
      
      (hdc, hbmMem, filename) -> write_bmp_to_png;
      
      (hdc, hbmOld) -> SelectObject;
      
      
      hbmMem -> DeleteObject;
      0 -> windowItemDevice;
      (hwnd, hdcWin) -> ReleaseDC -> result;
      hdc -> DeleteDC;
   #);
