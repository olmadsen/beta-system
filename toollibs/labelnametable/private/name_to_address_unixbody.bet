ORIGIN 'name_to_address_body';
LIB_ITEM 'name_to_address_table';
INCLUDE '~beta/unixlib/dlfcn';

-- name_to_address_tableMDPrivate:descriptor --
(# 
   self: @integer;
   handle: @integer;
   init_nm:
     (# one_done:< object;
        exeName: ^text
     enter exeName[]
     do ExeFileExtension->exeName.appendExtension;
        (if traceStream[] <> none then
            'initReadNameTable for: ' -> traceStream.puttext; 
            exeName[] -> tracestream.puttext; '...' -> tracestream.puttext
        if);
        (exeName,true)->initReadNameTable->handle;
        (if traceStream[] <> none then 'done.'->traceStream.putline;  if);
        loop:
          (# adr: @Integer; label: @Text
          do
             handle->nextAddress->adr;
             (if adr = - 1 then leave loop if) (* datpete 5/1/98 *) ;
             handle->nextLabel->label;
             (if traceStream[] <> none then
                 'Label:'->traceStream.puttext;
                 adr->traceStream.puthex;
                 ' '->traceStream.put;
                 label[]->traceStream.putline;
                 
             if);
             (label.copy,adr)->add;
             
             (* datpete 5/1/98: changed to use -1 test above.
              * The '_end' test is too UNIX dependant!
              * (if not ('_end'->Label.equal) then restart loop if)
              *)
             restart loop;
          #);
     #)
#)

-- Name_to_address_tableInit: DoPart --
do
   private.H.init;
   (if out_of_process then
       (if ExternalLookup##=TextToInteger## then
           (* ExternalLookup not further bound - use nm *)
           exeName[] -> MDprivate.init_nm(# one_done::(# do INNER init #)#);
        else
           (* ExternalLookup further bound - use it instead of nm *)
           &ExternalLookup[] -> THIS(Name_to_address_table).ExternalLookup[];
       if);
    else
       (* in_process - no need for nm *)
       (0,RTLD_NOW %Bor RTLD_GLOBAL)->dlopen_integer->mdPrivate.self;
   if)
     

-- Name_to_address_tableLookupNative: Descriptor --
(#  
do (if out_of_process and (ExternalLookup[]<>NONE) then
       name[] -> ExternalLookup -> adr;
       (if tracestream[]<>NONE then
           ' [ExternalLookup: \''->tracestream.puttext;
           name[] -> tracestream.puttext;
           '\' resolved to address 0x' -> tracestream.puttext;
           adr -> tracestream.puthex;
           '] ' -> tracestream.puttext;
       if);
    else
       (mdPrivate.self,name)->dlsym->adr;
       (if tracestream[]<>NONE then
           ' [dlsym('->tracestream.puttext;
           name[] -> tracestream.puttext;
           ') returns address 0x' -> tracestream.puttext;
           adr -> tracestream.puthex;
           '] ' -> tracestream.puttext;
       if);
   if);
#)  

-- Name_to_address_tableLookupCFixName: descriptor --
(# #)
