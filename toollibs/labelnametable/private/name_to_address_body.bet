ORIGIN '../name_to_address_table';
LIB_ITEM 'name_to_address_table';
MDBODY nti 'name_to_address_ntibody'
       ppcmac 'name_to_address_macbody'
       hpux9pa 'name_to_address_hpbody'
       default 'name_to_address_unixbody';
INCLUDE '~beta/basiclib/numberio';

--name_to_address_table_private: descriptor--
(# 
   H: @honeyMan;
#)

--name_to_address_table_element_display:dopart--
do
   (if s[] = none then screen[]->s[] if);
   'Label:'->s.puttext;
   adr->s.puthex;
   ' '->s.put;
   name[]->s.putline;
   
--name_to_address_table_dotsinit:dopart--
do (if (i mod 500 = 0) and (i > 0) then
       (if infoStream[] <> none then '.'->infoStream.put if)
   if);
   i+1->i
   
--name_to_address_table_hashfunction:dopart--
do e.name[]->private.H.hash->value
   
--name_to_address_table_equal:dopart--
do left.name[]->right.name.equal->value
   
--name_to_address_table_add:dopart--
do &element[]->e[]; name[]->e.name[]; adr->e.adr; e[]->insert;
   
--name_to_address_table_thelookup:descriptor--
(# 
   E: @element;
   LookUpNative: @<<SLOT name_to_address_tableLookupNative:Descriptor>>;
   hash: @hashfunction;
   search: @findIndexed
     (#
        predicate::
          (# 
          do
             (e[],current[])
               ->equal->value
          #)
     do (*current.display;*)
        current.adr->adr; 
     #);
do (if tracestream[]<>NONE then
       'Lookup: '->tracestream.puttext; 
       name[]->tracestream.puttext; 
   if);
   0->adr;
   name[]->E.name[]; 
   E[]->hash->search;
   (if adr=0 then
       LookUpNative; 
       (name[],adr)->add;
       (if (adr=0) then
           'name_to_address_table.LookupNative returned 0 for symbol \'' 
             ->screen.puttext; 
           name[] -> screen.puttext;
           '\'' -> screen.putline;
       if);
   if);
   (if tracestream[] <> none then
       ' adr: '->tracestream.puttext;
       adr->tracestream.putint;
       tracestream.newline;
   if);
#)
