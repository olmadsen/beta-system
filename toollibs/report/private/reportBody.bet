ORIGIN '../report'

--- reportFieldSetup:dopart ---
do this(field)[]->private.columns.append

--- reportFieldWrite: dopart ---
do value -> elements.append;
   INNER

--- reportFieldBlank: dopart ---
do value -> elements.append;
   (width,'') -> private.theReportFormat.writeText

--- reportFieldElements: dopart ---
do (*THIS(list).*)last -> pos[];
   (if index<size then
       (for i:index repeat pos.pred[] -> pos[] for);
       pos.elm[] -> elm[]
    else
   if)

--- reportTextFieldWrite: dopart ---
do (width,value) -> private.theReportFormat.writeText;

--- reportIntegerFieldWrite: dopart ---
do (width,value) -> private.theReportFormat.writeInt

--- reportFieldPrevValue: dopart ---
do index -> elements.get -> value[];

--- reportSplitTable: dopart ---
do true->private.split

--- reportBody: descriptor ---
(# elm: ^element;
do &reportFormat[] -> private.theReportFormat[];
   (if not private.columns.empty then
       private.columns.scan
	 (# elm: ^outputFormat.fields.element;
	 do &private.theReportFormat.fields.element[]->elm[];
	    current.name -> elm.name[];
	    current.width -> elm.w;
	    elm[] -> private.theReportFormat.fields.append;
	 #);
   if);
   (reportStream[],
      caption,
      private.columns.size,rowsPrTable,tableNumbering)
     ->private.theReportFormat.init;
   (&private.theReportFormat.Report[]).start;
   (&private.theReportFormat.Table[]).start;
   (if not (private.columns.empty or (*this(container).*)empty) then
       (*THIS(container).*)scan
	 (# insertSep: @boolean
	 do (if insertSep then
		(&private.theReportFormat.row[]).sep
	    if);
	    current[]->elm[];
	    (if private.split then
		private.theReportFormat.splitTable; false->private.split
	    if);
	    (&private.theReportFormat.Row[]).start;
	    private.columns.scan
	      (# insertSep: @boolean
	      do (if insertSep then
		     (&private.theReportFormat.cell[]).sep
		 if);
		 (&private.theReportFormat.cell[]).start;
		 elm[]->current.elm[];
		 current.calculate;
		 (if current.predicate then
		     current.write
		  else current.blank
		 if);
		 (&private.theReportFormat.cell[]).end;
		 true->insertSep;
	      #);
	    (&private.theReportFormat.Row[]).end;
	    true->insertSep;
	 #);
     if);
   (&private.theReportFormat.Report[]).end;
#)

--- reportPrivate: descriptor ---
(# theReportFormat: ^reportFormat;
   split: @boolean;
   columns: @list
     (* List containing the columns (i.e. the columns of the report)
      * which can be either text or integers.
      *)
     (# element::< field;
	get:
	  (# elm: ^element;
	     name: ^text;
	     pos: ^theCellType;
	     find:
	       (# do
		  (if (pos.elm.name -> name.equal) then
		      pos.elm[] -> elm[];
		   else
		      pos.succ[] -> pos[];
		      restart find
		  if)
	       #)
	  enter name[]
	  do (*THIS(list).*)head -> pos[];
	     find
	  exit elm[]
	  #);
     #);
#)
