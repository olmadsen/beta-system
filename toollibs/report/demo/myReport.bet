ORIGIN '~beta/toollibs/report/v1.0/report';
INCLUDE '~beta/basiclib/v1.6/file';
INCLUDE '~beta/toollibs/report/v1.0/latexFormat';
INCLUDE '~beta/toollibs/report/v1.0/htmlFormat';
INCLUDE '~beta/toollibs/report/v1.0/asciiFormat';

---- program: descriptor ----

(# record:
     (#	cpr: @integer;
	name: ^text;
	car: ^text;
     #);

   recordList: list
     (# element:: record;
	recordReport: report
	  (# tableNumbering:: trueObject;
	     count: @integer;
	     numberField: @integerField
	       (# width:: (# do 3->value #);
		  calculate:: (# do count+1->count -> value #);
		  name:: (# do 'Number' -> name[] #);
	       #);
	     CPRfield: @integerField
	       (# width:: (# do 5->value #);
		  calculate:: (# do elm.cpr -> value #);
		  name:: (# do 'CPR' -> name[] #);
	       #);
	     nameField: @textField
	       (# width:: (# do 15->value #);
		  calculate:: (# do elm.name[] -> value #);
		  name:: (# do 'Name' -> name[] #);
	       #);
	     carField: @textField
	       (# width:: (# do 5->value #);
		  calculate:: (# do elm.car[] -> value #);
		  name:: (# do 'Car' -> name[] #);
	       #);
	     summation: @sumField
	       (# width:: (# do 3->value #);
		  name:: (# do 'Sum'->name[] #)
	       #);
	     accField: @integerAccField
	       (# width:: (# do 3->value #);
		  calculate:: (# do (summation.value).value/count -> value  #);
		  predicate:: (* only to be printed, iff CPRfield mod 3 = 0 *)
		    (# val: ^integerObject
		    do 1->CPRfield.prevvalue -> val[];
		       (if val[]<>NONE then
			   ((val mod 3) = 0) -> predicate;
			   (if predicate then splittable if);
			else
			   false -> predicate
		       if)
		    #);
		  name:: (# do 'Accumulate' -> name[] #);
	       #);
	  do numberField.setup;
	     CPRfield.setup;
	     nameField.setup;
	     carField.setup;
	     numberField[]->summation.setup;
	     accField.setup;
	  #);
	LaTeXReport: recordReport
	  (# caption:: (# do 'LaTeX table example'->caption[] #);
	     reportFormat:: LaTeXFormat
	       (# Report::
		    (# start::
			 (#
			 do '\\documentclass{article}\n'->theStream.puttext;
			    '\\begin{document}\n'->theStream.puttext
			 #);
		       end::
			 (# do '\\end{document}\n'->theStream.puttext #)
		    #)
	       #)
	  #);
	htmlReport: recordReport
	  (# caption:: (# do 'HTML table example'->caption[] #);
	     reportFormat:: htmlFormat
	       (# Report::
		    (# start::
			 (#
			 do '<HTML>\n'->theStream.puttext;
			    '<BODY>\n'->theStream.puttext
			 #);
		       end::
			 (#
			 do '<\BODY>\n'->theStream.puttext;
			    '<\HTML>\n'->theStream.puttext;
			 #)
		    #)
	       #)
	  #);
	asciiReport: recordReport
	  (# caption:: (# do 'ASCII table example'->caption[] #);
	     reportFormat:: asciiFormat
	  #);
     #);

   p: ^record;
   reportStream: @file; cpr: @integer;
   recList: @recordList;

do 12345->cpr;
   (for 10 repeat
	&record[]->p[];
	'Jens Hansen'->p.name[];
	cpr->p.CPR; cpr+1->cpr;
	'Fiat' -> p.car[];
	p[]->recList.append;
   for);

   screen[]-> recList.latexReport;
   'myReportFile.tex' -> reportStream.name; reportStream.openWrite;
   reportStream[]-> recList.latexReport; reportStream.close;

   screen[]-> recList.htmlReport;
   'myReportFile.html' -> reportStream.name; reportStream.openWrite;
   reportStream[]-> recList.htmlReport; reportStream.close;

   screen[]-> recList.asciiReport;
   'myReportFile.txt' -> reportStream.name; reportStream.openWrite;
   reportStream[]-> recList.asciiReport; reportStream.close;
#)
