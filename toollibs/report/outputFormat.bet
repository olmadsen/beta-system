ORIGIN '~beta/containers/v1.5/container';
INCLUDE '~beta/containers/v1.5/list';

--- Lib: attributes ---

(* Writing to the stream is done in the write routines.
 * 
 * Report, Table, Row, Cell specify the general actions which should
 * be taken in the given situations.  These actions can be further
 * specialised by virtual bindings when specifing the format of the
 * output
 * 
 * See asciiFormat.bet, latexFormat.bet or htmlFormat.bet for examples
 * of creating different outputFormat's.
 *)

outputFormat:
  (# write:
       (# w: @integer
       enter w
       do INNER
       #);
     writeInt:< write
       (# int: ^integerObject
       enter int[]
       do INNER
       #);
     writeText:< write
       (# txt: ^text
       enter txt[]
       do INNER
       #);
     Report:<
       (# start:<
	    (# do INNER #);
	  end:<
	    (#
	    do (&Table[]).end;
	       INNER;
	    #)
       #);
     Table:<
       (# start:<
	    (#
	    do currentTableNo+1 -> currentTableNo;
	       INNER;
	       (&Row[]).start;
	       fields.scan
		 (# insertSep: @boolean
		 do (if insertSep then
			(&cell[]).sep
		    if);
		    (&cell[]).start;
		    (current.w,current.name[])->writeText;
		    (&cell[]).end;
		    true->insertSep;
		 #);
	       (&Row[]).end;
	       headSep;
	    #);
	  headSep:<
	    (# do INNER #);
	  sep:<
	    (# do INNER #);
	  end:<
	    (# do INNER #);
       #);
     Row:<
       (# start:<
	    (#
	    do 0 -> fieldNo;
	       (if (rowsPrTable>0) and (currentRowNo>rowsPrTable) then
		   splitTable
	       if);
	       INNER;
	       currentRowNo+1 -> currentRowNo
	    #);
	  sep:<
	    (# do INNER #);
	  end:<
	    (#
	    do INNER;
	    #)
       #);
     Cell:<
       (# start:<
	    (# do INNER #);
	  sep:<
	    (# do INNER #);
	  end:<
	    (# do INNER #)
       #);
     theCaption: ^text;

     (* The rest is PRIVATE stuff *)
     currentRowNo,
       currentTableNo,
       noOfColumns,
       fieldNo,
       rowsPrTable: @integer;
     tablenumbering: @boolean;
     fields: @list(# element:: (# name: ^text; w: @integer #) #);
     theStream: ^stream;
     init:
       (#
       enter (theStream[],
		theCaption[], noOfColumns,
		rowsPrTable, tableNumbering)
       do 0 -> currentTableNo;
	  0 -> fieldNo;
       #);
     splitTable:
       (#
       do 0 -> currentRowNo;
	  (&Table[]).end; (&Table[]).sep; (&Table[]).start;
       #);
  #);
