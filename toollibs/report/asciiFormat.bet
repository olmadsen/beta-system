ORIGIN 'outputFormat';
INCLUDE '~beta/basiclib/v1.5/formatio';

--- Lib: attributes ---

asciiFormat: OutputFormat
  (# fmt: @text; width: @integer;
     writeInt::
       (# fmt: @text;
       do '%'->fmt.puttext; w->fmt.putint; 'i'->fmt.put;
	  fmt[] -> theStream.putformat(# do int->i #)
       #);
     writeText::
       (# fmt: @text;
       do '%'->fmt.puttext; w->fmt.putint;
	  '.'->fmt.puttext; w->fmt.putint;
	  's'->fmt.put;
	  fmt[] -> theStream.putformat(# do txt[]->s #)
       #);

     Row::
       (# start:: (# do ' | ' -> theStream.puttext #);
	  end:: (# do ' |\n' -> theStream.puttext #);
       #);

     Cell::
       (# sep:: (# do ' | ' -> theStream.puttext #);
       #);

     Report::
       (# start::
	    (#
	    do 0->width;
	       fields.scan
		 (# do current.w+width+3->width #);
	       (width-5-theCaption.length)/2->width
	    #)
       #);
     Table::
       (# start:: (# do headSep #);
	  headSep::
	    (#
	    do fields.scan
		 (# start:: (# do ' '->theStream.put #);
		    end:: (# do '+\n'->theStream.puttext #)
		 do '+-'->theStream.puttext;
		    (for current.w repeat '-'->theStream.put for);
		    '-'->theStream.put
		 #)
	    #);
	  sep:: (# do '\n\n'->theStream.puttext #);
	  end::
	    (#
	    do fields.scan
		 (# start:: (# do ' '->theStream.put #);
		    end:: (# do '+\n'->theStream.puttext #)
		 do '+-'->theStream.puttext;
		    (for current.w repeat '-'->theStream.put for);
		    '-'->theStream.put;
		 #);
	       (for width repeat ' '->theStream.put for);
	       (if tableNumbering then
		   'Table '->theStream.puttext;
		   currentTableNo->theStream.putint;
		   ': '->theStream.puttext;
	       if);
	       theCaption[] -> theStream.puttext; theStream.newline;
	    #);
       #)
  #);
