ORIGIN 'outputFormat'

--- Lib: attributes ---

latexFormat: outputFormat
  (# writeInt::
       (# do int -> theStream.putint #);
     
     writeText::
       (# do txt[] -> theStream.puttext #);

     Row::
       (# end:: (# do ' \\\\\\hline\n' -> theStream.puttext; #); #);

     Cell::
       (# sep:: (# do ' & ' -> theStream.puttext; #); #);

     Table::
       (# start::
	    (#
	    do '\\begin{table}\\begin{center}\\begin{tabular}{|' -> theStream.puttext;
	       (for i:noOfColumns repeat 'l|' -> theStream.puttext for);
	       '}\\hline\n' -> theStream.puttext;
	    #);
	  headSep:: (# do '\\hline\n' -> theStream.puttext; #);
	  end::
	    (#
	    do '\\end{tabular}' -> theStream.puttext;
	       '\\caption{'-> theStream.puttext;
	       (if tableNumbering then 
		   'Table '->theStream.puttext;
		   currentTableNo->theStream.putint;
		   ': '->theStream.puttext;
	       if);
	       theCaption[] -> theStream.puttext;
	       '}\n' -> theStream.puttext;
	       '\\end{center}\\end{table}\n' -> theStream.puttext
	    #);
       #)
  #);
