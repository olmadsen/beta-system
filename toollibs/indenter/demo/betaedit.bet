ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/fields'
        '~beta/guienv/stddialogs'
        '~beta/basiclib/file'
        '~beta/guienv/utils/betaTextField';
-- program: Descriptor --
guienv
(#
   (*
    onKeyDown::
    (# 
    do
    (if control then
    (if key = 'a' then 'asdfasdf'->putline; true->done;  if); 
    if)
    #); *)
   textWindow: @window
     (#
        menubarType::
          (#
             fileMenu: @menu
               (#
                  textFile: @file;
                  openItem: @menuitem
                    (#
                       eventHandler::
                           (#
                              onSelect::
                                (# tmpText: @styledText; fileName: ^text; 
                                do
                                   textWindow[]->fileSelectionDialog
                                     ->fileName[];
                                   (if fileName[] <> none then
                                       fileName[]->textFile.name;
                                       textFile.openRead;
                                       textFile.scan
                                         (# while:: (#  do true->value #)
                                         do ch->tmpText.put
                                         #);
                                       tmpText[]->txtEdit.contents.contents;
                                       textFile.close
                                   if)
                                #);
                              onStatus:: (#  do true->value #)
                           #);
                         open::
                           (# 
                           do
                              'Open'->name;
                              (* (speckeys.F2,false,false,false)->specialkey *)
                              
                           #)
                      #);
                    saveItem: @menuitem
                      (#
                         eventHandler::
                           (#
                              onSelect::
                                (# 
                                do
                                   (if textFile.name <> none then
                                       (if (textFile.name).length > 0 then
                                           textFile.openWrite;
                                           txtEdit.contents.contents
                                             ->textFile.puttext;
                                           textFile.close
                                       if);
                                       
                                   if)
                                #)
                           #);
                         open:: (#  do 'Save'->name #)
                      #);
                    a: @menuitem
                      (#
                         eventHandler::
                           (# onSelect:: 
                                (#  do txtEdit.contents.comment_out_selection #) #);
                         open::
                           (# 
                           do 'comment out selection'->name; 'a'->key (* '+' -> key; *)
                           #)
                      #);
                    d: @menuitem
                      (#
                         eventHandler::
                           (# onSelect:: 
                                (# pos,endPos: @integer;
                                do 
                                   txtEdit.contents.selection.start -> txtEdit.contents.getIndenterLinePos
                                     -> txtEdit.contents.rowColTopos -> pos;
                                   txtEdit.contents.selection.end -> txtEdit.contents.getIndenterLinePos
                                     -> txtEdit.contents.rowColToPos -> endPos;
                                   (pos,endPos)-> txtEdit.contents.remove_comment
                           #) #);
                         open::
                           (# 
                           do 'remove comment'->name; 'd'->key (* '+' -> key; *)
                           #)
                      #);
                    s: @menuitem
                      (#
                         eventHandler::
                           (# onSelect:: 
                                (#  pos,endpos : @integer;
                                do txtEdit.contents.selection.start -> txtEdit.contents.getIndenterLinePos
                                     -> txtEdit.contents.theBetaIndenter.surrounding_comment
                                     -> (pos,endPos);
                                   (pos,endPos) -> txtEdit.contents.selection;                                   
                                #) 
                           #);
                         open::
                           (# 
                           do 'find surrounding comment'->name; 's'->key (* '+' -> key; *)
                           #)
                      #);
                    w: @menuitem
                      (#
                         eventHandler::
                           (# onSelect:: 
                                (# a,b: @integer;
                                do txtEdit.contents.selection 
                                     -> txtEdit.contents.make_real_comments 
                                #) 
                           #);
                         open::
                           (# 
                           do 'make real'->name; 'w'->key (* '+' -> key; *)
                           #)
                      #);
                    open::
                      (# 
                      do
                         'File'->name;
                         openItem.open;
                         openItem[]->append;
                         saveItem.open;
                         saveItem[]->append;
                         a.open;
                         a[] -> append;
                         d.open;
                         d[] -> append;
                         s.open;
                         s[] -> append;
                         w.open;
                         w[] -> append;
                      #)
                 #);
               betaMenu: @menu
                 (#
                    programItem: @menuitem
                      (#
                         eventHandler::
                           (#
                              onSelect::
                                (# 
                                do
                                   (0,0)->txtEdit.contents.selection;
                                   'ORIGIN \'~beta/basiclib/betaenv\';\n' '--PROGRAM: descriptor--\n' '(# \n' 'do \n' 'patternname::<\n' '(#\n' 'i: @integer;\n' 'do\n' '\'hello\'->putline;\n' '(if ch\n' '// 4 then\n' '// 6 then\n' '\'hello2\'->putline;\n' 'else\n' '\'asdf\'->putline;\n' 'if);\n' '#);\n' ' \n' 'p_name2: john (#\n' 't: ^text;\n' 'enter t[]\n' 'do\n' 'sjkdf\n' '(if true then\n' 'p_name3::\n' '(# \n' 'do\n' 'fsdjao\n' 'asdf \n' 'exit 42 \n' '#)\n' 'else\n' 'sdfjk\n' 'if);\n' '#)\n' '\'Hello World!\'->putline;\n' '#)\n' 'trace: \n' '(# exit false #)\n\n\n\n\n'
                                     ->txtEdit.contents.insert;
                                   (0,0)->txtEdit.contents.selection;
                                   
                                #)
                           #);
                         open:: (#  do 'Program Skeleton'->name #)
                      #);
                    libItem: @menuitem
                      (#
                         eventHandler::
                           (#
                              onSelect::
                                (# 
                                do
                                   (0,0)->txtEdit.contents.selection;
                                   'ORIGIN \'~beta/basiclib/betaenv\';\n' '--LIB: Attributes--\n'
                                     ->txtEdit.contents.insert;
                                   (0,0)->txtEdit.contents.selection;
                                   
                                #)
                           #);
                         open:: (#  do 'Library Skeleton'->name #)
                      #);
                    open::
                      (# 
                      do
                         'BETA'->name;
                         programItem.open;
                         programItem[]->append;
                         libItem.open;
                         libItem[]->append;
                         
                      #)
                 #);
               open::
                 (# 
                 do
                    fileMenu.open;
                    fileMenu[]->append;
                    betaMenu.open;
                    betaMenu[]->append;
                    
                 #);
               
            #);
          txtEdit: @textEditor
            (#
               open::<
                 (# 
                 do textWindow.size->size; true->bindBottom; true->bindRight; 
                 #);
               contentsType::< betaTextField
                 (#
                    texteditingExtent::< (#  do 5->start; length-5->end;  #);
                    texteditmode::< (#  do true->value #);
                    open::< (#  do init; INNER #);
                 #);
               
            #);
          eventhandler:: (# onAboutToClose:: (#  do terminate #) #);
          open::
            (#  do 'BETA Editor'->title; (400,400)->size; txtEdit.open #)
       #);
     
  do textWindow.open; 
  #)  

