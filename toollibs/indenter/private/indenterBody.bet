ORIGIN '../indenter';
-- init: DoPart --
do 1->top; &indentRegexps[]->regexp[]; regexp.init; INNER  

-- setContents: DoPart --
do
     (#
        setLine:
          (# t: ^text; 
          enter t[]
          do
             t.length->contents[line].length;
             t[]->regexp.eat_indent
               ->(contents[line].indentValue,contents[line].line[]);
             
          #);
        
     do
        0->unformattet.pos;
        1->line->top;
        l:
        (if unformattet.pos < unformattet.length then
            (if top > contents.range then top*2->contents.extend;  if);
            &textLine[]->contents[line][];
            unformattet.pos->contents[line].pos;
            unformattet.getLine->setLine;
            INNER setContents;
            line+1->line;
            top+1->top;
            restart l
        if)
     #)  

-- updateIndentValue: DoPart --
do
   indentValue-contents[line].indentValue->contents[line].dirty;
   indentValue->contents[line].indentValue;
     

-- indentLine: DoPart --
do INNER indentLine; (line,indentValue)->updateIndentValue;   

-- getIndent: DoPart --
do contents[line].indentValue->indentValue; INNER getIndent  
   
-- getActualIndent: doPart --
do contents[line].indentValue -> indent;
   Inner
   
-- updatePos: DoPart --
do
   (for i: top-2 repeat
     contents[i].pos+contents[i].length+1->contents[i+1].pos
   for)  

-- getBlockIndent: DoPart --
do
     (# count: @integer; b,finished,firstBlock: @boolean; 
     do
        (if blockNumber <> 0 then
            toploop: cycle
              (# 
              do
                 contents[line]->t[];
                 loop: cycle
                   (# 
                   do
                      (t[],blockNumber)->regexp.RemoveBlock->(b,firstBlock);
                      (if not (b) then
                          leave loop; 
                       else
                          (if firstBlock then
                              count-1->count;
                              (if count = 0 then
                                  (line->getIndent)+t.length->col;
                                  true->finished;
                                  leave loop
                              if)
                           else
                              count+1->count
                          if)
                      if);
                      
                   #);
                 (if finished then
                     leave toploop; 
                  else
                     (if line->firstLine then
                         (0,0)->(line,col); leave toploop
                     if);
                     line-1->line;
                     true->differentLines
                 if)
              #)
        if)
     #)  

