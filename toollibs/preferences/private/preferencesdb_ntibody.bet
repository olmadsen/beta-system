ORIGIN 'preferencesDBbody';
LIB_ITEM 'preferencesdb';
INCLUDE '~beta/win32lib/registry';

--- lib:attributes ---
keyname:(# exit  'SOFTWARE\\Mjolner System' #);

--- preferencesLoad: dopart ---
do 
   (# keytext:@text;
   do
      keyname->keytext;
      '\\'->keytext.append;
      OptionsName[]->keytext.append;
      '\\'->keytext.append;
      sectionname[]->keytext.append;
      
      private.prefs.scan
      (# valuetext:^text;
      do
         (HKEY_CURRENT_USER,keytext[],current.name[])->readFromRegistry->valuetext[];
         (if valuetext[]<>none then
             valuetext[]->current.value.read;
         if);
      #);
   #);
   
--- preferencesSave: dopart ---
do (if changed then
       (# keytext:@text;
       do
          
          keyname->keytext;
          '\\'->keytext.append;
          OptionsName[]->keytext.append;
          
          private.prefs.scan
          (#
             type: ##preference; vt: ^text;
          do
             current.value.write->vt[];
             (HKEY_CURRENT_USER,keytext[],sectionname[],REG_SZ,current.name[],vt[])
               ->SetRegKeyValue
             (#
                nosuchroot::(# do 'noroot'->putline; #);
                createkeyfailed::(# do msg[]->putline; #);
                setvaluefailed::(# do msg[]->putline; #);
                othererror::(# do 'othererror'->putline; #);
             #);
          #);
       #)
   if);
   
