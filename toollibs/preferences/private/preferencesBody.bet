ORIGIN '../preferences';
INCLUDE '~beta/containers/v1.6/list';
INCLUDE '~beta/sysutils/v1.6/objinterface';

--- preferencesPrivate: descriptor ---
(# prefType: (# name: ^text; type: ##preference #);
   types: @list(# element:: prefType #);
   typesMaxWidth: @integer;
   
   prefVar: (# name: ^text; type: ##preference; value: ^preference #);
   prefs: @list(# element:: prefVar #);
   prefsMaxWidth: @integer;
   
#)

--- preferencesType: dopart ---
do private.types.find
   (# predicate:: (# do name[]->current.name.equalNCS->value #);
      notFound::
        (# newType: ^private.prefType
        do &private.prefType[]->newType[];
           (if name[]=NONE then
               (# proto: @prototype
               do type##->getProtoTypeForStruc->proto; proto.labid->name[];
                  (1,name.length-1)->name.sub->name[]
               #)
           if);
           name.copy->newType.name[];
           type##->newType.type##;
           newType[]->private.types.append;
           (name.length,private.typesMaxWidth)->max->private.typesMaxWidth
        #)
   do alreadyDefined
   #)

--- preferencesDefine: dopart ---
do private.prefs.find
   (# predicate:: (# do name[]->current.name.equalNCS->value #);
      notFound::
        (# newPref: ^private.prefVar
        do &private.prefVar[]->newPref[];
           type##->newPref.type##;
           &type[]->newPref.value[]->pref[];
           (if name[]=NONE then
               (# proto: @prototype
               do type##->getProtoTypeForStruc->proto; proto.labid->name[];
                  (1,name.length-1)->name.sub->name[]
               #)
           if);
           name.copy->newPref.name[];
           false->newPref.value.changed;
           newPref[]-> private.prefs.append;
           (name.length,private.prefsMaxWidth)->max->private.prefsMaxWidth
        #)
   do alreadyDefined
   #);
   INNER;
   
--- preferencesChanged: dopart ---
do l: private.prefs.scan
     (# do (if current.value.changed then true->value; leave l if) #)
   
--- preferencesSetValue: dopart ---
do value[]->this(get).value[];
   
--- preferencesGet: dopart ---
do INNER get;
   (if name[]=NONE then
       (# proto: @prototype
       do this(get)##->getProtoTypeForStruc->proto; proto.labid->name[];
          (1,name.length-1)->name.sub->name[]
       #)
   if);
   private.prefs.find
   (# predicate:: 
        (#
        do (name[]->current.name.equalNCS)->value
        #);
      notFound::
        (# do notDefined #)
   do (if value[]<>NONE then
          value[]->current.value[];
          true->current.value.changed
       else
          current.value[]->value[]
      if)
   #)
   
--- preferencesOpen: dopart ---
do 'boolean'->type(# type:: booleanPreference #);
   'char'->type(# type:: charPreference #);
   'integer'->type(# type:: integerPreference #);
   'real'->type(# type:: realPreference #);
   'text'->type(# type:: textPreference #);
   INNER open
   
