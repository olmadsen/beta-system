ORIGIN '../cmdOptions';
INCLUDE 'preferencesBody'

--- preferencesReadArguments: dopart ---
do (# readArgs:
        (# peekArg:< (# arg: ^text do INNER exit arg[] #);
           getArg:< (# arg: ^text do INNER exit arg[] #);
           moreArgs:< booleanValue;
           init:< (# do INNER #);
           option, optionName: ^text; shortCutChar: @char;
           combinedShortcuts: @boolean;
           activateCmd:
             private.prefs.find
             (# predicate:: 
                  (#
                  do (current.type##<=cmdOption##) and
                     (optionName[]->current.name.equalNCS)->value
                  #);
                notFound::
                  (#
                  do '*** Unknown command option "'->puttext;
                     option[]->puttext;
                     '" - ignored'->putline
                  #);
                pref: ^cmdArgs
             do (if (current.type##<=cmdArgs##) then
                    current.value[]->pref[];
                    0->pref.noOfArgs;
                    l: (if (moreArgs) and
                           ('-'<>(1->(peekArg).inxGet)) then
                           (if ((0=pref.maxArgs) or
                               (pref.noOfArgs<pref.maxArgs)) and
                               (peekArg->pref.legalArg) then
                               (if pref.noOfArgs=pref.args.range then
                                   1->pref.args.extend
                               if);
                               pref.noOfArgs+1->pref.noOfArgs;
                               getArg->pref.args[pref.noOfArgs][];
                               restart l
                           if)
                       if);
                    0->args.new
                if);  
                (current.value[]->qua(# as:: cmdOption #)).onAction
             #);
           activateCmdToggle:
             private.prefs.find
             (# predicate:: 
                  (#
                  do (current.type##<=cmdToggle##) and
                     (optionName[]->current.name.equalNCS)->value
                  #);
                notFound::
                  (#
                  do '*** Unknown command option "'->puttext;
                     option[]->puttext;
                     '" - ignored'->putline
                  #)
             do (current.value[]->qua(# as:: cmdToggle #)).offAction
             #);
           activateShortCut:
             private.prefs.find
             (# predicate:: 
                  (#
                  do (current.type##<=cmdOption##) and
                     (shortCutChar=(current.value[]->qua(# as:: cmdOption #)).shortCut)
                       ->value
                  #);
                notFound::
                  (#
                  do '*** Unknown command shortcut "'->puttext;
                     shortCutChar->put;
                     '" - ignored'->putline
                  #);
                pref: ^cmdArgs
             do (if (current.type##<=cmdArgs##) then
                    (if not combinedShortcuts then
                        current.value[]->pref[];
                        0->pref.noOfArgs;
                        l: (if (moreArgs) and
                               ('-'<>(1->(peekArg).inxGet)) then
                               current.value[]->pref[];
                               (if ((0=pref.maxArgs) or
                                   (pref.noOfArgs<pref.maxArgs)) and
                                   (peekArg->pref.legalArg) then
                                   (if pref.noOfArgs=pref.args.range then
                                       1->pref.args.extend
                                   if);
                                   pref.noOfArgs+1->pref.noOfArgs;
                                   getArg->pref.args[pref.noOfArgs][];
                                   restart l
                               if)
                           if)
                     else
                        '*** Shortcut forms of command options with arguments are '
                          ->putline;
                        '*** only allowed as the last shortcut in a shortcut list'
                          ->putline;
                        '*** Offending shortcut "'->puttext; shortCutChar->put;
                        '" - ignored'->putline
                    if)
                if);
                (current.value[]->qua(# as:: cmdOption #)).onShortcutAction
             #)
        do init;
           l: (if moreArgs then
                  getArg->option[];
                  (if true
                   // '--no'->((1,4)->option.sub).equalNCS then
                      (if noOfArgs>0 then INNER readArgs; 0->noOfArgs if);
                      (5,option.length)->option.sub->optionName[];
                      activateCmdToggle
                   // '--'->((1,2)->option.sub).equalNCS then
                      (if noOfArgs>0 then INNER readArgs; 0->noOfArgs if);
                      (3,option.length)->option.sub->optionName[];
                      activateCmd
                   // '-'=(1->option.inxGet) then
                      (if noOfArgs>0 then INNER readArgs; 0->noOfArgs if);
                      1->option.setpos;
                      option.scan
                      (# while:: (# do true->value #)
                      do ch->shortCutChar;
                         (option.pos<option.length)->combinedShortcuts;
                         activateShortCut
                      #);
                   else
                      (* rest is 'plain' text arguments *)
                      (if noOfArgs=args.range then 1->args.extend if);
                      noOfArgs+1->noOfArgs;
                      option[]->args[noOfArgs][];
                  if);
                  restart l
              if)
        #);
   do (if argStream[]=NONE then
          readArgs
          (# i: @integer;
             init:: (# do 1->i #);
             moreArgs:: (# do (i<noOfArguments)->value #);
             peekArg:: (# do i+1->arguments->arg[] #);
             getArg:: (# do i+1->i->arguments->arg[] #);
          do INNER readArguments
          #);
       else
          readArgs
          (# args: [0]^text; i: @integer;
             init::
               (#
               do argStream.reset;
                  l: (if not argStream.eos then
                         1->args.extend;
                         argStream.getAtom->args[args.range][];
                         restart l
                     if);
               #);
             moreArgs:: (# do (i<args.range)->value #);
             peekArg:: (# do args[i+1][]->arg[] #);
             getArg:: (# do args[i+1->i][]->arg[] #);
          do INNER readArguments
          #)
      if);
      INNER readArguments
   #)
   
--- dumpCmds: dopart ---
do private.prefs.scan
   (# where:: (# do (current.type##<=cmdOption##)->value #);
      pref: ^cmdOption
   do current.value[]->pref[];
      ' --'->puttext; current.name[]->puttext;
      (for private.typesMaxWidth-current.name.length repeat ' '->put for);
      (if (pref##<=cmdtoggle##) and
          (pref.onShortcutAction##=(pref[]->qua(# as:: cmdToggle #)).offAction##) then
          '*    '->puttext
       else
          (if pref.default then '*'->put else ' '->put if); ' '->put;
          (if pref.shortcut<>0 then
              '-'->put; pref.shortcut->put; ' '->put
           else '   '->puttext
          if);
      if);
      pref.description.scanAll
      (#
      do ch->put;
         (if ch//ascii.nl//ascii.cr then
             (for private.typesMaxWidth+8 repeat ' '->put for)
         if)
      #);
      newline;
      (if pref##<=cmdtoggle## then
          ' --no'->puttext; current.name[]->puttext;
          (for private.typesMaxWidth-current.name.length-2 repeat ' '->put for);
          (if (pref.onShortcutAction##=(pref[]->qua(# as:: cmdToggle #)).offAction##) then
              (if pref.shortcut<>0 then
                  '  -'->puttext; pref.shortcut->put; ' '->put
               else '   '->puttext
              if)
           else
              '*   '->puttext
          if);
          newline;
      if);
      newline;   
   #)
