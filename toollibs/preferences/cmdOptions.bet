ORIGIN 'preferences';
BODY 'private/cmdOptionsBody'

--- lib: attributes ---
cmdOption: preference
  (# shortCut: @char;
     default:< booleanValue;
     onAction:< object;
     onShortCutAction:< object;
     write::< (# do &text[]->t[]; INNER #);
  #);
cmdToggle: cmdOption
  (# value: @boolean;
     onAction::< (# do true->value; INNER #);
     offAction:< (# do false->value; INNER #);
     read::
       (#
       do (if true
           // 'on'->t.equalNCS then true->value
           // 'off'->t.equalNCS then false->value
           else false->value    
          if)
       #);
     write::
       (#
       do (if value then
              'on'->t[]
           else
              'off'->t[]
          if)
       #);
  enter value
  exit value
  #);
cmdArgs: cmdOption
  (# args: (* the arguments to this cmdOption *)
       [0]^text;
     noOfArgs: (* the number of arguments after this cmdOption *)
       @integer;
     maxArgs:<
       (* max. number of arguments to this option.  0 implies an
        * unlimited number of arguments
        *)
       integerValue;
     legalArg:<
       (* called for each arg.  Must return true, if the arg. is
        * legal
        *)
       booleanValue(# arg: ^text enter arg[] do true->value; INNER #);
     scan:
       (# current: ^text
       do (for i: args.range repeat args[i][]->current[]; INNER for)
       #);
     read::
       (#
       do l: (if not t.eos then 
                 1->args.extend; t.getAtom->args[args.range][];
                 restart l
             if)
       #);
     write::
       (#
       do scan(# do current[]->t.puttext; ' '->t.put #)
       #);
  #)

--- preferencesLib: attributes ---
registerCmdOptions:
  (#
  do 'cmdOption'->type(# type:: cmdOption #);
     'cmdToggle'->type(# type:: cmdToggle #);
     'cmdArgs'->type(# type:: cmdArgs #);
     INNER
  #);
  
dumpCmds:
  (#
  <<SLOT dumpCmds: dopart>>
  #);
readArguments:
  (# args: (* the arguments after the last option *)
       [0]^text;
     noOfArgs: (* the number of arguments after the last option *)
       @integer;
     argStream: ^stream
  enter argStream[]
  <<SLOT preferencesReadArguments: dopart>>
  exit args
  #)
