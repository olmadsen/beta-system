ORIGIN 'preferences';
BODY 'private/cmdOptionsBody'

--- lib: attributes ---
cmdPreference: preference
  (# shortCut: @char;
     default:< booleanValue;
     onAction:< object;
     onShortCutAction:< object;
     write::< (# do &text[]->t[]; INNER #);
  #);
cmdOption: cmdPreference
  (# value: @boolean;
     onAction::< (# do true->value; INNER #);
  enter value
  exit value
  #);
cmdToggle: cmdOption
  (# offAction:< (# do false->value; INNER #);
     read::
       (#
       do (if true
	   // 'on'->t.equalNCS then true->value
	   // 'off'->t.equalNCS then false->value
	   else false->value    
	  if)
       #);
     write::
       (#
       do (if value then
	      'on'->t[]
	   else
	      'off'->t[]
	  if)
       #);
  #);
cmdArgs: cmdPreference
  (# args: (* the arguments to this cmdOption *)
       [0]^text;
     noOfArgs: (* the number of arguments after this cmdOption *)
       @integer;
     maxArgs:<
       (* max. number of arguments to this option.  0 implies an
	* unlimited number of arguments
	*)
       integerValue;
     legalArg:<
       (* called for each arg.  Must return true, if the arg. is
	* legal
	*)
       booleanValue(# arg: ^text enter arg[] do true->value; INNER #);
     scan:
       (# current: ^text
       do (for i: args.range repeat args[i][]->current[]; INNER for)
       #);
     read::
       (#
       do l: (if not t.eos then 
		 1->args.extend; t.getAtom->args[args.range][];
		 restart l
	     if)
       #);
     write::
       (#
       do scan(# do current[]->t.puttext; ' '->t.put #)
       #);
  exit this(cmdArgs)[]
  #);

--- preferencesLib: attributes ---
registerCmdOptions:
  (#
  do 'cmdPreference'->type(# type:: cmdPreference #);
     'cmdOption'->type(# type:: cmdOption #);
     'cmdToggle'->type(# type:: cmdToggle #);
     'cmdArgs'->type(# type:: cmdArgs #);
     INNER
  #);

getCmdPreference: get (* used to get a cmdPreference preference *)
  (# type::< cmdPreference do INNER #);

getCmdOption: get (* used to get a cmdOption preference *)
  (# type::< cmdOption do INNER #);

getCmdToggle: get (* used to get a cmdToggle preference *)
  (# type::< cmdToggle do INNER #);

getCmdArgs: get (* used to get a cmdArgs preference *)
  (# type::< cmdArgs do INNER #);
  
dumpCmds:
  (# programName:<
       (# name: ^text do 'program'->name[]; INNER programName exit name[] #);
     argumentName:<
       (# name: ^text do 'arg'->name[]; INNER argumentName exit name[] #);
     strm: ^stream
  enter strm[]
  <<SLOT dumpCmds: dopart>>
  #);

readArguments:
  (# argStream: ^stream
  enter argStream[]
  <<SLOT preferencesReadArguments: dopart>>
  #);

noOfArgs: integerValue
  (* the number of arguments after the last option *)
  (# <<SLOT preferencesNoOfArgs: dopart>> #);

scanArguments:
  (* scan through the arguments after the last option *)
  (# current: ^text;
  <<SLOT preferencesScanArguments: dopart>>
  #);
