ORIGIN '../preferencesDB';
INCLUDE '~beta/basiclib/v1.5/formatio';

--- lib: attributes ---
rect: (# x,y,h,w: @integer enter ((x,y),(h,w)) exit  ((x,y),(h,w)) #);
rectPreference: preference
  (# value: @rect;
     read::
       (#
       do t.reset;
          '((%i,%i),(%i,%i))'
            ->t.getFormat(# do ((i,i),(i,i))->value #)
       #);
     write::
       (#
       do &text[]->t[];
          '((%i,%i),(%i,%i))'
            ->t.putFormat(# do value->((i,i),(i,i)) #)
       #);
  enter value
  exit value
  #);  
  
--- preferencesLib: attributes ---
getRect: get(# type:: rectPreference do INNER #);
defaultRect: getRect(# do 'defaultRect'->name[] #)



--- program: descriptor ---
(# r1, r2: @rect;
   prefs: @preferences
do 
   prefs.open(# do 'rect'->prefs.type(# type:: rectPreference #) #);
   
   'delay'->prefs.define(# type:: integerPreference #);
   'reloadOnRerun'->prefs.define(# type:: booleanPreference #);
   'defaultRect'->prefs.define(# type:: rectPreference #);
  
   ('testPreferences', 'Rect')->prefs.load;
   
   ((1,2),(3,4))->prefs.defaultRect;
   ((5,6),(7,8))->r1;
   
   r1->prefs.defaultRect;
   prefs.defaultRect->r2;
 
   '***final preferences:'->putline;
   (screen[],true)->prefs.dump;
   
   ('testPreferences', 'Rect')->prefs.save;
#)
