ORIGIN '~beta/toollibs/preferences/v1.1/cmdOptions';

--- preferencesLib: attributes ---
registerBETACmdOptions: registerCmdOptions
  (#
  do 'help'
       ->define(# type:: cmdOption
		    (# onAction::
                         (#
                         do '\tON: help'->putline;
                            dumpCmds
                            (# programName:: (# do 'cmdOptionsDemo'->name[] #);
                               argumentName:: (# do 'file'->name[] #)
                            #)
                         #);
		       onShortcutAction:: onAction;
		    #)
	       do 'Print this help info'->pref.description[];
		  'h'->pref.shortCut;
	       #);
     'repeat'
       ->define(# type:: cmdToggle
		    (# onAction:: (# do '\tON: repeat'->putline #);
		       offAction:: (# do '\tOFF: repeat'->putline #);
		       onShortcutAction:: onAction;
		    #)
	       do true->pref.value;
		  'Run compiler in repeating mode'->pref.description[];
		  'r'->pref.shortCut;
	       #);
     'link'
       ->define(# type:: cmdToggle
		    (# onAction:: (# do '\tON: link'->putline #);
		       offAction:: (# do '\tOFF: link'->putline #);
		       onShortcutAction:: offAction;
		    #)
	       do true->pref.value;
		  'Link program'->pref.description[];
		  'x'->pref.shortCut;
	       #);
     'static'
       ->define(# type:: cmdOption
		    (# onAction:: (# do '\tON: static'->putline #);
		       onShortcutAction:: onAction;
		    #)
	       do 'Use static linking'->pref.description[];
	       #);
     'dynamic'
       ->define(# type:: cmdOption
		    (# onAction:: (# do '\tON: dynamic'->putline #);
		       onShortcutAction:: onAction;
		       default:: trueObject;
		    #)
	       do 'Use dynamic linking'->pref.description[];
	       #);
     'switch'
       ->define(# type:: cmdArgs
		    (# onAction::
                         (#
                         do '\tON: switch'->puttext;
                            (for i: noOfArgs repeat
                                 '\t'->put; args[i][]->puttext
                            for);
                            newline;
                         #);
		       onShortcutAction:: onAction;
		       maxArgs::
                         (#
                         do (if (noOfArgs>0) and
                                ('0'->args[noOfArgs].equal) then
                                noOfArgs->value
                             else 0->value
                            if)
                         #);
		       legalArg::
                         (#
                         do arg.reset; true->value;
                            arg.getint
                            (# syntaxerror::
                                 (# do false->this(legalArg).value;
                                    true->continue #)
                            #)
                         #)
		    #)
	       do 'Set/unset one or more compiler switches'
		    ->pref.description[]; pref.description.newline;
		  'See the Compiler Manual for details'
		    ->pref.description.puttext;
		  's'->pref.shortCut;
	       #);
     'linkOpts'
       ->define(# type:: cmdArgs
		    (# onAction:: (# do '\tON: linkOpts'->putline #);
		       onShortcutAction:: onAction;
		       maxArgs:: (# do 1->value #);
		    #)
	       do 'Specify text string to be appended to the link directive'
		    ->pref.description[];
	       #);
       #)

--- program: descriptor ---
(# t: @text;
   prefs: @preferences
do prefs.open(# do prefs.registerBETACmdOptions #);
  
   'Read arguments from commandline:'->putline;   
   prefs.readArguments;
   'Process arguments:'->putline;   
   prefs.scanArguments
   (#
   do '\tARGUMENT: '->puttext; current[]->puttext;
      '\t...PROCESSING COMMAND...'->putline;
   #);
   
   'Read and process arguments from stream:'->putline;   
   '--switch 1 2 0 x --linkOpts -xh arg3 arg4'->t;
   t[]->prefs.readArguments
   (#
   do prefs.scanArguments(# do '\tARGUMENT: '->puttext; current[]->putline #);
      '\t\t...PROCESSING COMMAND...'->putline;
   #);
   
#)
