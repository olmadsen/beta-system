ORIGIN '~beta/basiclib/v1.4/betaenv';
INCLUDE '~beta/basiclib/v1.4/numberio';
BODY 'private/preferencesBody'

--- lib: attributes ---
preference:
  (# name: ^text;
     changed: @boolean
       (* true, iff this(preference) have been read in from some
        * preferences file, or it have been explicitly changed
        *);
     description: ^text
       (* Text to be used to descripe this(preference), e.g. in menues *);
     read:<
       (* used to convert a textual representation of a value of
        * this(preference) into the value of this(preference).
        * 
        * Used to read preference values from preference files.
        *)
       (# t: ^text
       enter t[]
       do INNER
       #);
     write:<
       (* used to convert the value of this(preference) into textual
        * form.
        * 
        * Used to write preferences onto preference files.
        *)
       (# t: ^text
       do INNER
       exit t[]
       #);
  do INNER
  #);
booleanPreference: preference
  (# value: @boolean;
     read::
       (#
       do (if true
           // 'true'->t.equalNCS then true->value
           // 'false'->t.equalNCS then false->value
           else false->value    
          if)
       #);
     write::
       (#
       do (if value then
              'true'->t[]
           else
              'false'->t[]
          if)
       #);
  enter value
  exit value
  #);
charPreference: preference
  (# value: @char;
     read:: (# do t.reset; t.get->value #);
     write:: (# do &text[]->t[]; value->t.put #)
  enter value
  exit value
  #);
integerPreference: preference
  (# value: @integer;
     read:: (# do t.reset; t.getint->value #);
     write:: (# do &text[]->t[]; value->t.putint #)
  enter value
  exit value
  #);
realPreference: preference
  (# value: @real;
     read:: (# do t.reset; t.getreal->value #);
     write:: (# do &text[]->t[]; value->t.putreal #)
  enter value
  exit value
  #);
textPreference: preference
  (# value: @text;
     read:: (# do t->value#);
     write:: (# do value.copy->t[] #);
  enter value
  exit value
  #);

preferences:
  (# <<SLOT preferencesLib: attributes>>;
     type: (* used to register a new preference type *)
       (# type:< preference;
          name: ^text;
          alreadyDefined:< exception
            (#
            do 'Preference type: "'->msg.append;
               name[]->msg.append; '" already defined'->msg.append
            #)
       enter name[]
       <<SLOT preferencesType: dopart>>
       #);
     define:
       (* used to define a new preference variable.  If name[]=NONE,
        * then type.name[] is assumed to contain the name of the
        * preference variable
        *)
       (# type:< preference;
          name: ^text;
          pref: ^type;
          alreadyDefined:< exception
            (#
            do 'Preference named: "'->msg.append;
               name[]->msg.append; '" already defined'->msg.append
            #)
       enter name[]
       <<SLOT preferencesDefine: dopart>>
       #);
     changed: booleanValue
       (* returns true, if any preferences in this(prerefences) have
        * changed=true
        *)
       (# <<SLOT preferencesChanged: dopart>> #);
     get: (* used to get a preference variable from this(preferenceDB) *)
       (# type:< preference;
          name: ^text;
          value: ^type;
          notDefined:< exception
            (#
            do 'Preference named: "'->msg.append;
               name[]->msg.append; '" not defined'->msg.append
            #);
          setValue:
            (# value: @type enter value <<SLOT preferencesSetValue: dopart>> #);
       enter setValue
       <<SLOT preferencesGet: dopart>>
       exit value
       #);
     getBoolean: get (* used to get a boolean preference *)
       (# type::< booleanPreference do INNER #);
     getChar: get (* used to get a char preference *)
       (# type::< charPreference do INNER #);
     getInteger: get (* used to get an integer preference *)
       (# type::< integerPreference do INNER #);
     getReal: get (* used to get a real preference *)
       (# type::< realPreference do INNER #);
     getText: get (* used to get a text preference *)
       (# type::< textPreference do INNER #);
     open: (* used to register preference types *)
       (# <<SLOT preferencesOpen: dopart>> #);
     private: @<<SLOT preferencesPrivate: descriptor>>;
  #);
