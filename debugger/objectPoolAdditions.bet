ORIGIN '~beta/basiclib/v1.4/betaenv';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
INCLUDE '~beta/basiclib/v1.4/private/betaenvbody';

--- lib:attributes ---
newObjectPoolPut:
  (* Like objectPool.put, but if the alreadyThere exception is caught and
   * returns, the existing element in the objectPool is replaced by the
   * new element. *)
  (# obj: ^object;
     exact:< BooleanValue;
     alreadyThere:< Exception;
     isExact: @Boolean;
     type: ##Object;
  enter obj[]
  do
     obj##->type##;
     exact->isExact;
     find: 
       (for i: objectPool.private.top repeat
            (if isExact then
                (if type## =objectPool.private.table[i].type## then
                    alreadyThere; 
                    (* Ok. Remove the existing element. *)
                    objectPool.private.table[objectPool.private.top][]
                      ->objectPool.private.table[i][];
                    leave find;
                if);
             else
                (if objectPool.private.table[i].type## <= type## then
                    alreadyThere; 
                    objectPool.private.table[objectPool.private.top][]
                      ->objectPool.private.table[i][];
                    leave find;
                if);
            if);
       for);
     (obj[], obj##) -> objectPool.private.insert;
  #)
