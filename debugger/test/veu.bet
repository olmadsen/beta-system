ORIGIN '~beta/basiclib/v1.5/basicsystemenv';
BODY '~beta/guienv/v1.4/private/X11/guienvxsystemenvbody';

INCLUDE '~beta/guienv/v1.4/guienv';

INCLUDE '~beta/objectbrowser/v2.1.1/systemenvcoroutinespawner';
INCLUDE '~beta/objectbrowser/v2.1.1/windowedbrowser';
INCLUDE '~beta/objectbrowser/v2.1.1/options';
INCLUDE '~beta/objectbrowser/v2.1.1/optionlist';
INCLUDE '~beta/objectbrowser/v2.1.1/UI/icons';
INCLUDE '~beta/objectbrowser/v2.1.1/UI/iconlist';
INCLUDE '~beta/guienv/v1.4/fields';
INCLUDE '~beta/basiclib/v1.5/formatio';
INCLUDE '~beta/sysutils/v1.5/envstring';

INCLUDE '~beta/debugger/v2.2/valhalla_application_interface';

(*BETARUN default '~beta/debugger/v2.2/betarun/$/betarunv.o';*)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- lib: attributes ---

initialMAINSize: (# exit (600,400) #);

--- program:descriptor ---
systemenv
(# setWindowEnv:: (# do gui[]->theWindowEnv[] #);
   
   thespawner: @systemenvspawner;
   
   gui: @guienv
     (# 
        main: @window
          (# menuBarType::<
               (# fileMenu: @Menu
                    (# startValhallaItem: @menuitem
                         (# open:: (# do 'Start Valhalla'->name; #);
                            eventHandler::
                              (# onSelect::
                                   (# 
                                   do startValhalla
                                   #);
                                 onStatus::
                                   (# 
                                   do (connected_to_valhalla=0)->value;
                                   #);
                              #);
                         #);
                       breakItem: @menuitem
                         (# open:: (# do 'Break'->name; 'B'->key #);
                            eventHandler::
                              (# onSelect::
                                   (# 
                                   do breakpoint;
                                   #);
                              #);
                         #);
                       errorItem: @menuitem
                         (# open:: (# do 'Error'->name #);
                            eventHandler::
                              (# onSelect::
                                   (# i: [1]@Integer;
                                   do i[2]->putInt
                                   #);
                              #);
                         #);
                       newItem: @menuitem
                         (# open:: (# do 'New' -> name; 'N' -> key #);;
                            eventHandler::
                              (# onSelect::<
                                   (# h: ^humleencl;
                                   do 
                                      &humleencl[]->h[];
                                      (10,'a',5,TRUE,4.5,main[]) -> h.humle;
                                      h.humle[]->browser.showObject;
                                   #)
                              #)
                         #);
                       closeItem: @menuitem
                         (# open:: (# do 'Close' -> name; 'W' -> key #);
                            eventHandler:: 
                              (# onSelect:: 
                                   (# sml: ^selectedMoveablesList;
                                   do getSelectedMoveablesList->sml[];
                                      (if sml[]<>NONE then
                                          sml.scan (# do current.close #);
                                          sml.clear;
                                      if);
                                   #);
                                 onStatus:: 
                                   (# sml: ^selectedMoveablesList;
                                   do getSelectedMoveablesList->sml[];
                                      (if sml[]<>NONE then
                                          sml.size>0->value;
                                       else
                                          FALSE->value
                                      if);
                                   #);
                              #)
                         #);
                       quitItem: @menuitem
                         (# open:: (# do 'Quit' -> name; 'Q' -> key #);
                            eventHandler:: (# onSelect::< (# do Terminate #)#)
                         #);
                       open::
                         (# 
                         do '0####'->putline;
                            'File' -> name;
                            'a####'->putline;
                            startValhallaItem.open; startValhallaItem[]->append;
                            '1#####'->putline;
                            breakItem.open; 
                            '2#####'->putline;
                            breakItem[]->append;
                            '3#####'->putline;
                            errorItem.open; 
                            '4#####'->putline;
                            errorItem[]->append;
                            '5#####'->putline;
                            newItem.open; 
                            '6#####'->putline;
                            newItem[] -> append;
                            '7#####'->putline;
                            closeItem.open;
                            '8#####'->putline;
                            closeItem[] -> append;
                            '9#####'->putline;
                            quitItem.open; 
                            '10#####'->putline;
                            quitItem[] -> append;
                            '11#####'->putline;
                         #);
                    #);
                  open::< (# do fileMenu.open; fileMenu[] -> append #)
               #);
             
             open::
               (# 
               do initialMAINsize -> size; 'Valhalla Test Program' -> title;
                  contents->info.open; contents->objworld.open;
                  objworld[]->browser.init;
               #);
             
             objworld: @Canvas
               (# open::
                    (# sz: @Point; tmp: @Point;
                    do initialMAINsize->sz;
                       info.size->tmp;
                       sz.v-tmp.v->sz.v;
                       sz->size;
                       TRUE->bindLeft->bindRight->bindBottom->bindTop;
                       installSelectHandlers;
                    #);
               #);
             
             info: @staticText
               (# open::<
                    (# sz: @Point; ts: ^TextStyle;
                       pos: @Point;
                    do 
                       style->ts[]; 
                       
                       InitialMAINsize->sz;
                       ts.lineHeight+6->sz.v;
                       sz->size;
                       
                       InitialMAINsize->pos;
                       0->pos.h;
                       pos.v-sz.v->pos.v;
                       pos -> Position;
                       
                       TRUE->bindLeft->bindRight->bindBottom; FALSE->bindTop;
                       
                       TRUE->border.visible;
                       borderStyles.shadowIn->border.style;
                    #);
               #);

             browser: @windowedbrowser
               (# init:: 
                    (# options: ^optionDB;
                    do getOptionDB -> options[];
                       (charRepOnALine,TRUE) -> options.setBooleanOption;
                       (invisibleOrigins,TRUE) -> options.setBooleanOption;
                    #);
                  mpsinterfaceType::
                    (# 
                       onOpenGroup::
                         (# t: ^Text;
                         do 'opening '->t[];
                            groupName[]->t.append;
                            t[]->main.info.label;
                            true->main.info.update;
                         #);
                       onOpenGroupDone::
                         (# 
                         do ''->main.info.label;
                            true->main.info.update;
                         #);
                       onOldGroup::
                         (#
                         do 'ERROR: "%s.%s" is older than "%s.%s". Recompile!!!\n'
                              -> putFormat
                            (# 
                            do groupName[]->s; AST.astFileExtension->s;
                               groupName[]->s; 'bet'->s;
                            #)
                         #);
                       onGroupNotFound::
                         (#
                         do 'ERROR: "%s.%s" not found !!! \n' -> putFormat
                            (# 
                            do groupName[]->s; AST.astFileExtension->s;
                            #);
                         #);
                    #);
               #);
          #);
        
        
        humleencl: 
          (# 
             humle: @IntegerObject
               (# c: @Char;
                  s: @ShortInt;
                  obj: @IntegerObject;
                  b: @Boolean;
                  r: @Real;
                  ref: ^Object;
                  t: @Text;
                  p: ##Object;
                  pNone: ##Object;
                  ir: [50]@Integer;
                  orep: [10]^Object;
                  prep: [10]##Object;
               enter (c,s,b,r,ref[])
               do 'dette er en humle ' -> t;
                  (for i:50 repeat i->ir[i] for);
                  (for i:10 repeat THIS(humle)[]-> orep[i][] for);
                  (for i:5 repeat main##-> prep[i]## for);
                  humle##->p##;
               #);
          #);
        
        myIcons: @iconDB
          (# movIcon: ^bitmap;
             getIcon::
               (# 
               do (if ObjectViewIcon -> name.equalNCS then
                      (if movIcon[]=NONE then
                          &bitmap[] -> movIcon[];
                          '$(BETALIB)/bitmap/ObjectViewIcon.xbm' 
                            -> expandEnvVar
                          (# defaultvalue::
                               (# do '/usr/local/lib/beta/'->envvarvalue[] #) 
                          #) -> movIcon.readFromX11File;
                      if);
                      movIcon[] -> theIcon[];
                  if);
               #);       
          #);

     do main.open; myIcons[]->objectPool.put; 
        thespawner.init; thespawner[]->objectPool.put;
     #)
#)
