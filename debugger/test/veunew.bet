ORIGIN '~beta/guienv/v1.2/guienv';
INCLUDE '~beta/debugger/v2.0/UI/veulib';
INCLUDE '~beta/guienv/v1.2/fields';
INCLUDE '~beta/guienv/v1.2/scrolllists';
INCLUDE '~beta/objectbrowser/v2.0/localdumper';

--- lib: attributes ---

initialVEUsize: (# exit (400,600) #);

--- windowLib: attributes ---

objectView: TextScrollList
  (# od: ^ObjectDumper;  bo: ^od.betaObject;
     
     w,h,lc: @Integer;
     title: ^Text;
     
     open::<
       (#
       do lc -> prepend;
          INNER;
          update; 
       #);
     
     init:
       (# dummy: @Boolean;
       enter (od[],bo[])
       do bo[] -> od.lineCount -> lc;
          bo.ObjectDescriptor -> od.dumpPatternName -> (title[],dummy);
          lc*system.systemTextStyle.lineHeight -> h;
          2*title.length*system.systemTextStyle.maxChWidth -> w;
       #);
     
     update:
       (# line: @Integer;
          dump:
            (# enter od.dumpObject
               (# forEachLine::
                    (# s: @Text;
                    do line+1 -> line;
                       s.clear;
                       (for nestingLevel repeat '   ' -> s.putText for);
                       (if attType //OriginAttribute then
                           attName[] -> s.putText;
                           ' Origin' -> s.putText;
                        else
                           attName[] -> s.putText;
                           ': ' -> s.putText;
                           staticQua[] -> s.putText;
                       if);
                       ' = ' -> s.putText;
                       theValue[] -> s.putText;
                       (line,s[]) -> setText;
                       (if attType //StatRefAttribute then
                           (offset -> bo.PartObject, nestingLevel+1) -> dump;
                       if);
                    #);
                  nestinglevel: @Integer;
               enter nestinglevel
               #)
            #);
          
       do 0 -> line; (bo[],0) -> dump;
       #);
     
     eventHandler::
       (# 
          onmouseDown::
            (# 
            do (if doubleClick then
                   Selection.first -> getText -> putLine;
               if)
            #);
       #);
  #);

MoveableObjectView: moveable
  (# open::<
       (# 
       do 
          contents.view.title[] -> title;
          (contents.w,contents.h) -> (cw,ch);
          true -> border.visible;
          borderStyles.shadowOut -> border.style;
       #);
     init: (# enter contents.view.init #);
       
     contentsType::< objectView
       (# open::<
            (# 
            do (cw,ch) -> size;
               size -> putpoint;
               (inrect.left,inrect.top+hh) -> position;
               position -> putpoint;
            #)
       #);
     icon::<
       (# bm: ^bitmap;
       do &bitmap[] -> bm[];
          'News.bitmap' -> bm.readFromX11File;
          bm[] -> theIcon[];
       #);
  #);

  

--- program:descriptor ---
guienv
(# veu: @window
     (# menuBarType::<
          (# fileMenu: @Menu
               (# newItem: @menuitem
                    (# open::< (# do 'New' -> name; 'N' -> key #);;
                       eventHandler::<
                         (# onSelect::< 
                              (# new: ^moveableObjectView;
                                 bo: ^be.od.betaObject;
                                 h: ^humle;
                              do &moveableObjectView[] -> new[];
                                 &be.od.betaObject[] -> bo[];
                                 &humle[]->h[]->bo.o[];
                                 (10,'a',5,TRUE,4.5) -> h;
                                 (be.od[],bo[]) -> new.init;
                                 new.open;
                              #)
                         #)
                    #);
                  quitItem: @menuitem
                    (# open::< (# do 'Quit' -> name; 'Q' -> key #);
                       eventHandler::< (# onSelect::< (# do Terminate #)#)
                    #);
                  open::< 
                    (# 
                    do newItem.open; newItem[] -> append;
                       quitItem.open; quitItem[] -> append;
                    #);
               #);
             open::< (# do fileMenu.open; fileMenu[] -> append #)
          #);
        open::< 
          (# 
          do initialVEUsize -> size;
             'Valhalla Execution Universe' -> title;
          #);
     #);
   
   humle: IntegerObject
     (# c: @Char;
        s: @ShortInt;
        obj: @IntegerObject;
        b: @Boolean;
        r: @Real;
     enter (c,s,b,r)
     #);
           
   be: @browserEnv
     (# od: @localdumper;
        init::<
          (# 
          do (be[],betaGrammar[]) -> od.init;
          #)
     #);
   
do be; veu.open;
#)
