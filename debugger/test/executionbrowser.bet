ORIGIN '~beta/uienv/v1.1.8/uienv';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

[[
--- include '~beta/uienv/v1.1.8/controls'
--- include '~beta/uienv/v1.1.8/figureitems'
--- include '~beta/uienv/v1.1.8/fields'

--- windowItemLib: attributes ---

resizeRelative: 
  (# action: ^theEventHandler.onFatherFrameChanged;
     tmpFrame: @rectangle;
     ofw,ofh,nfw,nfh: @Integer;
  enter action[]
  do
     action.oldFrame.size -> (ofw,ofh);
     (* OldFatherWidth, OldFatherHeight *)
          
     action.newFrame.size -> (nfw,nfh);
     (* NewFatherWidth, NewFatherHeight *)
          
     frame -> tmpFrame;
          
     ((tmpFrame.left*nfw) div ofw) -> tmpFrame.left;
     ((tmpFrame.top*nfh) div ofh) ->  tmpFrame.top;
     
     ((tmpFrame.right*nfw) div ofw) -> tmpFrame.right;
     ((tmpFrame.bottom*nfh) div ofh) -> tmpFrame.bottom;
          
     INNER;
          
     tmpFrame -> frame;
  #);

--- windowLib: attributes ---

initialBrowserSize: (# exit (200,300) #);

Framed: Canvas
  (# frame: @rect
       (# open::<
            (# 
            do (1,1) -> pen.size; patterns.black[] -> pen.stipple;
               father.size -> size;
               true -> bindBottom -> bindRight -> bindTop -> bindLeft;
            #);
       #);
     contentsType:< Canvas 
       (# open::< 
            (# fw,fh: @Integer;
            do 
               true -> bindBottom -> bindRight -> bindTop -> bindLeft; 
               (2,2) -> position;
               father.size -> (fw,fh);
               (fw-4,fh-4) -> size;
               INNER;
            #);
       #);
     contents: @contentsType;
     open::<
       (# 
       do INNER;
          frame.open;
          contents.open;
       #);
  #);
     
executionBrowser: canvas
  (# codeWindowImpl:< Framed
       (# contentsType::<
            (# eventhandler::<
                 (# onMouseDown::<
                      (# 
                      do 'codeWindow: ' -> putText;
                         localPosition -> putPoint;
                         newLine;
                      #);
                 #);
               
               groupInfo: @Framed
                 (# contentsType::< 
                      (# info: @staticText
                           (# open::<
                                (# 
                                do 'Group: ## Fragment: ##' -> label;
                                #);
                              eventhandler::<
                                (# onMouseDown::<
                                     (# 
                                     do 'groupInfo: ' -> putText;
                                        localPosition -> putPoint;
                                        newLine;
                                     #);
                                #);
                           #);
                         open::< (# do info.open #);
                      #);
                    open::<
                      (# fw,fh: @Integer;
                      do true -> bindLeft -> bindBottom;
                         false -> bindTop;
                         father.size -> (fw,fh);
                         (0,fh-system.systemTextStyle.lineHeight - 10)
                           -> position;
                         (fw,system.systemTextStyle.lineHeight + 10)
                           -> size;
                      #);
                 #);
               
               open::< (# do groupInfo.open #);
            #);
          
          eventhandler::<
            (# onFatherFrameChanged::<
                 (# 
                 do THIS(onFatherFrameChanged)[] -> resizeRelative;
                 #);
            #);
          
          open::<
            (# fh,fw: @Integer;
            do initialBrowserSize -> (fw,fh);
               (fw,fh div 3) -> size;
               (0,0) -> position;
               INNER;
            #);
       #);
     codeWindow: @codeWindowImpl;
     
     objectWindowImpl:< Framed
       (# contentsType::<
            (# eventHandler::<
                 (# onMouseDown::<
                      (# 
                      do 'objectWindow: ' -> putText;
                         localPosition -> putPoint;
                         newLine;
                      #);
                 #)
            #);
          
          eventhandler::<
            (# onFatherFrameChanged::<
                 (# 
                 do THIS(onFatherFrameChanged)[] -> resizeRelative;
                 #);
            #);
          
          open::<
            (# fh,fw: @Integer;
            do initialBrowserSize -> (fw,fh);
               (fw,fh div 3 -> fh) -> size;
               (0,fh) -> position;
               INNER;
            #);
       #);
     objectWindow: @objectWindowImpl;
     
     stackWindowImpl:< Framed
       (# contentsType::<
            (# eventHandler::<
                 (# onMouseDown::<
                      (# 
                      do 'stackWindow: ' -> putText;
                         localPosition -> putPoint;
                         newLine;
                      #);
                 #)
            #);
          
          eventhandler::<
            (# onFatherFrameChanged::<
                 (# 
                 do THIS(onFatherFrameChanged)[] -> resizeRelative;
                 #);
            #);
          
          open::<
            (# fh,fw: @Integer;
            do initialBrowserSize -> (fw,fh);
               (fw,fh div 3 -> fh) -> size;
               (0,2*fh) -> position;
               INNER;
            #);
       #);
     
     stackWindow: @stackWindowImpl;
     
     open::< (* executionBrowser.open *)
       (# 
       do initialBrowserSize -> size;
          codeWindow.open;
          objectWindow.open;
          stackWindow.open; 
          true -> bindBottom -> bindRight;
       #);
  #);

putPoint:
  (# pt: @point
  enter pt
  do '(' -> put;
     pt.h -> putInt;
     ',' -> put;
             pt.v -> putInt;
     ')' -> put;
  #);
        

--- program:descriptor ---
uienv
(# main: @window
     (# exec: @executionBrowser;
        menuBarType::<
          (# fileMenu: @Menu
               (# quitItem: @menuitem
                    (# open::< (# do 'Quit' -> name; 'Q' -> key #);
                       eventHandler::< (# onSelect::< (# do Terminate #)#)
                    #);
                  open::< (# do quitItem.open; quitItem[] -> append #);
               #);
             open::< (# do fileMenu.open; fileMenu[] -> append #)
          #);
        open::< (# do initialBrowserSize -> size; exec.open #);
     #);
do main.open
#)

---]]
