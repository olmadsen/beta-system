ORIGIN '~beta/basiclib/v1.4/betaenv';
BODY 'private/valhallaparamsbody';

INCLUDE 'namevaluedb';
INCLUDE '~beta/containers/v1.4/list';

(* valhalla commandline parameters:
 * 
 *    -EXECNAME <Name> 
 * 
 *       Use <Name> as the name of executable to debug. Needed in any case,
 *       either to start a new process, or in order to generate the name of
 *       the ..db cache for that executable. This command line parameter is
 *       inserted into the global valhallaParamDB by parseValhallaParams.
 * 
 *       Allowed abbreviation: -EXEC
 * 
 *    -PID <number>   
 * 
 *       Used to inform valhalla that it is supposed to debug the process 
 *       with process id <number>. Used if valhalla is started after the
 *       debugged process. This command line parameter is inserted into the global 
 *       valhallaParamDB by parseValhallaParams. Alternatively it may get 
 *       inserted on process fork. (processComm.init).
 * 
 *    -ENVIRONMENT <Name> <Value>
 * 
 *       Used to set environment variables for the debugged process. Default
 *       is to hand over the Valhalla environment. However, by using the
 *       -ENVIRONMENT command-line parameter, the default values may be
 *       overridden.
 *       -ENVIRONMENT has no effect if the -PID command line parameter is
 *       used.
 *       At start up, these command line parameters are added to the global
 *       environment database (environment.bet).
 * 
 *       Allowed abbreviation: -ENV
 * 
 *   -BOOLEANOPTION <Name> <Value>
 * 
 *       Used to set a boolean option in the global options database.
 *       See '~beta/objectbrowser/v2.0/optionlist.bet' and
 *       'valhallaoptionslist.bet' for known boolean options.
 * 
 * In addition to command line parameters, valhalla may be given parameters
 * in the VALHALLAOPTS environment variable. If a parameter is given in
 * VALHALLAOPTS as well as on the command line, the command line overrides
 * the contents of VALHALLAOPTS.
 *)

--- lib:attributes ---

getValhallaParamDB: objectPool.get
  (# type::< valhallaParamDB; init:: (# do obj.init #)#);

valhallaParamDB: NameValueDB (# #);

     
(* PARSEVALHALLAPARAMS
 * ===================
 * 
 * Parses valhalla command line parameters and adds the retrieved 
 * information to the global databases environDB and valhallaParamDB. 
 * Should be called BEFORE INIT of any processInterface.
 *)

parseValhallaParams: (# do <<SLOT parseValhallaParams:descriptor>> #);



getEXECNAMEparam:
  (# execName: ^Text;
  do 
     scanParams: (getValhallaParamDB).scan
       (#
       do (if true
           //('-EXECNAME'->curname.equalNCS) then
              curvalue[]->execName[];
              leave scanParams;
          if);
       #);
  exit execName[]
  #);

setEXECNAMEparam:
  (# execName: ^Text;
  enter execName[]
  do ('-EXECNAME',execName[])->(getValhallaParamDB).add;
  #);

getPIDparam:
  (# PID: @Integer;
  do 
     scanParams: (getValhallaParamDB).scan
       (#
       do (if true
           //('-PID'->curname.equalNCS) then
              curvalue.reset; curvalue.getInt->PID;
              leave scanParams;
          if);
       #);
  exit PID
  #);

setPIDparam:
  (# PID: @Integer; PIDt: ^Text;
  enter PID
  do
     (if PID=0 then
         '-PID'-> (getValhallaParamDB).remove;
      else
         &Text[]->PIDt[]; PID->PIDt.putInt;
         ('-PID',PIDt[])->(getValhallaParamDB).add;
     if);
  #);

getExecutableParamDB: objectPool.get
  (# type::< executableParamDB; init:: (# do obj.init #)#);

executableParamDB: List (# element:: Text #);
