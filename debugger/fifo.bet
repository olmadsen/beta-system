ORIGIN '~beta/basiclib/v1.4/betaenv';

--- include '~beta/basiclib/v1.4/file'

(* FILE fifo.bet
 * =============
 * 
 * Part of valhalla, v2.0.
 * 
 * On UNIX, Communication with the debugged process is done over a pair of 
 * named pipes. The naming conventions are "/tmp/betadebugToPID" and 
 * "/tmp/BetaDebugFromPID" where PID is the process ID of the debugged 
 * process. The first fifo is used for communication from valhalla to
 * the debugged process. The second fifo is used for communication in
 * the opposite direction. *)

--- lib:attributes ---

mkfifo: external (* See man mkfifo (3c) *)
  (# path: [1]@Char;
     mode: @Integer;
  enter (path,mode)
  #);

fromName: 
  (# pid: @Integer; name: ^Text;
  enter pid
  do
     '/tmp/betadebugFrom' -> name[];
     name.lgth+1 -> name.setPos;
     pid -> name.putInt;
  exit name[]
  #);
   
toName:
  (# pid: @Integer; name: ^Text;
  enter pid
  do
     '/tmp/betadebugTo' -> name[];
     name.lgth+1 -> name.setPos;
     pid -> name.putInt;
  exit name[]
  #);
   
createFIFOS:
  (# pid: @Integer;
     name: ^Text;
  enter pid
  do 
     pid -> fromName -> name[]; (name,8x777) -> mkfifo;
     pid -> toName -> name[]; (name,8x777) -> mkfifo;
  #);

fifo: File
  (# binPutInt:
       (# val: @Integer; pr: @putRep;
       enter val
       do (@@val,1) -> pr;
       #);
     binGetInt:
       (# val: @Integer; gr: @getRep;
       do (@@val,1) -> gr;
       exit val
       #);
  #);
