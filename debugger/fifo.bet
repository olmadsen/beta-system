ORIGIN '~beta/basiclib/v1.4/betaenv';

--- include '~beta/basiclib/v1.4/file'
--- include '~beta/basiclib/v1.4/private/file_unixbody'

(* fifo.bet
 * ========
 * 
 * Part of valhalla, v2.0.
 * 
 * On UNIX, Communication with the debugged process is done over a pair of 
 * named pipes. The naming conventions are "/tmp/betadebugToPID" and 
 * "/tmp/BetaDebugFromPID" where PID is the process ID of the debugged 
 * process or the process ID of valhalla, depending on who started who. 
 * The first fifo is used for communication from valhalla to the debugged 
 * process. The second fifo is used for communication in
 * the opposite direction. *)

--- lib:attributes ---

mkfifo: external (* See man mkfifo (3c) *)
  (# path: [1]@Char;
     mode: @Integer;
  enter (path,mode)
  #);

fromName: 
  (# pid: @Integer; name: ^Text;
  enter pid
  do
     '/tmp/betadebugFrom' -> name[];
     name.lgth+1 -> name.setPos;
     pid -> name.putInt;
  exit name[]
  #);
   
toName:
  (# pid: @Integer; name: ^Text;
  enter pid
  do
     '/tmp/betadebugTo' -> name[];
     name.lgth+1 -> name.setPos;
     pid -> name.putInt;
  exit name[]
  #);
   
createFIFOS:
  (# pid: @Integer;
     name: ^Text;
  enter pid
  do 
     pid -> fromName -> name[]; (name,8x777) -> mkfifo;
     pid -> toName -> name[]; (name,8x777) -> mkfifo;
  #);

fifo: File
  (# binPutInt:
       (# val: @Integer;
          error:< Exception;
       enter val
       do (if (@@val,4) -> binPutBytes //4 then else error if);
       #);
     binGetInt:
       (# error:< Exception;
          val: @Integer;
          ok: @Boolean;
       do (if (@@val,4) -> binGetBytes //4 then else error if)
       exit val
       #);
     binGetBytes: @
       (# repAdr: @Integer; (* Address of buffer *)
          length: @Integer; (* In bytes *)
       enter (repAdr,length)
       do (repAdr,1,length,private.unixStream) -> fread -> length;
       exit length
       #);
     binPutBytes: @
       (# repAdr: @Integer; (* Address of buffer *)
          length: @Integer; (* In bytes *)
       enter (repAdr,length)
       do (repAdr,1,length,private.unixStream) -> fwrite -> length;
       exit length
       #);
  #)
