ORIGIN 'processInterface';

--- processInterfaceLib:attributes ---

breakPoint:
  (# 
     PCvalue: @Integer; (* Code address of this breakpoint  *)
     enabled: @Boolean; (* TRUE if this breakpoint is currently set *)
     cleared: @Boolean; (* TRUE when this breakpoint has been cleared. *)
     imp: ^ImperativeInfo;
     
     description: ^Text; (* Textual description of this(breakPoint). *)
     
     stop:< BooleanValue (# do true -> value; INNER; #);
     (* Called when this breakpoint is hit. If it returns FALSE, the 
      * process will continue as if the breakpoint was not hit. *)
     
     Init: 
       (* Initialize and set this breakpoint *)
       (# alreadySet:< Notification 
            (* Called if breakpoint already set at PCvalue *) 
            (# bp: ^breakPoint;
            enter bp[]
            #);
          errorCode: @Integer;
       enter (PCvalue,description[],imp[])
       <<SLOT breakpointInit:dopart>>
       exit errorCode
       #);
     
     onStatusChange: ^breakpointNotifier;
     (* Called when the state of the state of this(breakpoint) changes. *)
     
     clear: (# <<SLOT breakpointClear:dopart>> #);
     disable: (# <<SLOT breakpointDisable:dopart>> #);
     enable:  (# <<SLOT breakpointEnable:dopart>>  #); 
     
     reSet: (# <<SLOT breakPointReSet: dopart>> #);
     (* Reset this breakpoint after a rerun of the process *)
     
     bprivate: @<<SLOT breakpointPrivate: descriptor>>;
  #);

breakpointNotifier:
  (# bp: ^breakPoint;
  enter bp[]
  do INNER
  #);
