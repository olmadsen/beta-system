ORIGIN '~beta/guienv/v1.2/guienv';
BODY 'private/codemoveablebody';

INCLUDE '../processInterface';
INCLUDE '~beta/objectbrowser/v2.0/UI/moveable';
INCLUDE '~beta/objectbrowser/v2.0/UI/icons';
INCLUDE '~beta/objectbrowser/v2.0/options';
INCLUDE '~beta/editor/v5.0/codeviewer';

INCLUDE '~beta/interfacebuilder/v1.0d/guienvadds/guienvadds';
(* windowItem.{PreferredSize,BringToFront} *)

--- windowLib:attributes ---

codemoveableEditorList: List 
  (# element:: codemoveableEditor;
     onInsert:<
       (# theCodemoveableEditor: ^codemoveableEditor;
       enter theCodemoveableEditor[]
       do INNER
       #);
     onDelete:<
       (# theCodemoveableEditor: ^codemoveableEditor;
       enter theCodemoveableEditor[]
       do INNER
       #); 
  #);

getcodemoveableEditorList: objectPool.get
  (# type::< codemoveableEditorList; init:: (# do obj.init #)#);

codemoveableEditor: sifTextEditor
  (# debuggee: ^processInterface;
     
     <<SLOT codemoveableEditorLib:attributes>>;
     open::< 
       (# 
       enter debuggee[]
       do <<SLOT codemoveableEditorOpen:descriptor>> 
       #);
     close::<
       (# 
       do <<SLOT codemoveableEditorClose:descriptor>>
       #);
     cmeprivate: @<<SLOT codemoveableEditorPrivate:descriptor>>;
     contentsType::<
       (# eventHandler::<
            (# onMouseDown::<
                 (# 
                 <<SLOT codemoveableEditorOnMouseDown:dopart>>
                 #);
               onKeyDown::<
                 (# 
                 <<SLOT codemoveableEditorOnKeyDown:dopart>>
                 #);
            #);
       #);
     codeViewerType::<
       (# makeSomethingElse::<
            (# 
            do
               (if cs.node.symbol
                //debuggee.BETA.doPart   
                //debuggee.BETA.mainPart then
                   cs.node[]->setContraction;
                   (cs.node[],cs.node[])->astView.pp.update;
                   cs->setFocus
                else 
                   INNER
               if);
            #)   
       #);
     
     select:
       (# node: ^astInterface.ast;
       enter node[]
       <<SLOT codemoveableEditorSelect:dopart>>
       #);
     update:
       (# 
       <<SLOT codemoveableEditorUpdate:dopart>>
       #);
     
     encl: ^codemoveableEditorEncloser;
     getencloser:<
       (# theEncloser: ^codemoveableEditorEncloser;
       do INNER; theEncloser[]->encl[];
       exit theEncloser[]
       #);
  #);

codemoveableEditorEncloser:
  (# generalInfo:<
       (* Called with describing text when a breakpoint was set or cleared. *)
       (# inf: ^Text;
       enter inf[]
       do INNER
       #);
     errorInfo:<
       (* Called with describing text when some error occurred. *)
       (# inf: ^Text;
       enter inf[]
       do INNER
       #);
     newcodeview:<
       (# node: ^astInterface.ast;
       enter node[]
       do INNER
       #);
     continue:< (# do INNER #);
     processIsReady:< BooleanValue;
     onHighlight:< Object;
  #);

codemoveable: moveable
  (# contentsType::<
       (# debuggee: ^processInterface; 
          initialNode: ^astInterface.ast;
          editorType:< codeMoveableEditor
            (# charWidthType:: (# do moveableHeader.style->ts[] #);
               getencloser:: (# do editorEncloser[]->theEncloser[] #);
            #);
          editor: @editorType;
          open::<
            (# 
            do <<SLOT codemoveableContentsOpen:descriptor>>
            #);
          dobindings::<
            (# 
            do TRUE->editor.bindRight->editor.bindBottom;
            #);
       #);
     
     editorEncloserType:< codemoveableEditorEncloser
       (# onHighLight:: (# do wriggle #);
       #);
     editorEncloser: @editorEncloserType;
     
     
     moveableMenu::<
       (# open::<
            (# mi: ^actionedMenuItem;
            do &actionedMenuItem[]->mi[];
               (codemoveableFitAction##,NONE,NONE,'Fit to contents')->mi.open;
               INNER;
            #);
       #);
     codemoveableFitAction: actionedMenuItemAction
       (# 
       do contents.editor.preferredSize->newPreferredContentsSize;
       #);
     codemoveableOpenInWindowAction: actionedMenuItemAction
       (# 
       <<SLOT codemoveableOpenInWindowAction:dopart>>
       #);
     open::<
       (#
       enter (contents.debuggee[],contents.initialnode[])
       do <<SLOT codemoveableopen:descriptor>>
       #);
     close::<
       (# 
       do <<SLOT codemoveableclose:descriptor>>
       #);
     icon::
       (#
       do 'CodeView_Icon'->(getIconDB).getIcon->theIcon[];
       #);
  #);
