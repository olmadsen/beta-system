ORIGIN '~beta/guienv/v1.3.1/guienv';
BODY 'private/codemoveablebody';

INCLUDE '../processInterface';
INCLUDE '~beta/objectbrowser/v2.0/UI/moveable';
INCLUDE '~beta/objectbrowser/v2.0/UI/icons';
INCLUDE '~beta/objectbrowser/v2.0/options';
INCLUDE '~beta/editor/v5.0/codeviewer';

INCLUDE '~beta/guienv/v1.3.1/utils/guienvadds';
(* windowItem.{PreferredSize,BringToFront} *)


--- guienvlib:attributes ---

codemoveableEditorList: List 
  (# (*element:: window.codemoveableEditor;*)
     onInsert:<
       (# theCodemoveableEditor: ^window.codemoveableEditor;
       enter theCodemoveableEditor[]
       do INNER
       #);
     onDelete:<
       (# theCodemoveableEditor: ^window.codemoveableEditor;
       enter theCodemoveableEditor[]
       do INNER
       #); 
     newscan: scan
       (# cur: ^window.codemoveableEditor;
       do current[]->cur[]; INNER;
       #);
  #);

getcodemoveableEditorList: objectPool.get
  (# type::< codemoveableEditorList; init:: (# do obj.init #)#);

--- windowLib:attributes ---

codemoveableEditor: sifTextEditor
  (# debuggee: ^processInterface;
     
     <<SLOT codemoveableEditorLib:attributes>>;
     open::< 
       (# defaultSize:: (# do mysize->value #);
          mysize: @Point;
       enter (debuggee[],mysize)
       do <<SLOT codemoveableEditorOpen:descriptor>> 
       #);
     close::<
       (# 
       do <<SLOT codemoveableEditorClose:descriptor>>
       #);
     cmeprivate: @<<SLOT codemoveableEditorPrivate:descriptor>>;
     contentsType::<
       (# eventHandler::<
            (# onMouseDown::<
                 (# 
                 <<SLOT codemoveableEditorOnMouseDown:dopart>>
                 #);
               onKeyDown::<
                 (# 
                 <<SLOT codemoveableEditorOnKeyDown:dopart>>
                 #);
            #);
       #);
     codeViewerType::<
       (# (* Removed by datpete 19-9-95: Ess claims it is always done now.
           * makeSomethingElse::<
           *             (# 
           *             do
           *                (if cs.node.symbol
           *                 //debuggee.BETA.doPart   
           *                 //debuggee.BETA.mainPart then
           *                    cs.node[]->setContraction;
           *                    (cs.node[],cs.node[])->astView.pp.update;
           *                    cs->setFocus
           *                 else 
           *                    INNER
           *                if);
           *             #);
           *)
          makeSearchSLOTbinding::<
            (# unexp: ^astInterface.unexpanded;
               ff: ^astInterface.fragmentForm;
               ofi: ^Object;
            do cs.node[]->unexp[];
               unexp.theSlot->debuggee.utilities.SLOTtoFragment->(ff[],ofi[]);
               ff.root[]->a[];
               TRUE->doneInInner;
            #);
          selectNode::<
            (# 
            do node[]->encl.newcodeview;
            #);
       #);
     
     select:
       (# node: ^astInterface.ast;
       enter node[]
       <<SLOT codemoveableEditorSelect:dopart>>
       #);
     update:
       (# 
       <<SLOT codemoveableEditorUpdate:dopart>>
       #);
     
     encl: ^codemoveableEditorEncloser;
     getencloser:<
       (# theEncloser: ^codemoveableEditorEncloser;
       do INNER; theEncloser[]->encl[];
       exit theEncloser[]
       #);
  #);

codemoveableEditorEncloser:
  (# generalInfo:<
       (* Called with describing text when a breakpoint was set or cleared. *)
       (# inf: ^Text;
       enter inf[]
       do INNER
       #);
     errorInfo:<
       (* Called with describing text when some error occurred. *)
       (# inf: ^Text;
       enter inf[]
       do INNER
       #);
     newcodeview:<
       (# node: ^astInterface.ast;
       enter node[]
       do INNER
       #);
     tryclose:< Object; (* Called to close this codemoveable. *)
     continue:< (# do INNER #);
     processIsReady:< BooleanValue;
     onHighlight:< Object;
  #);

codemoveable: moveable
  (# contentsType::<
       (# debuggee: ^processInterface; 
          initialNode: ^astInterface.ast;
          editorType:< codeMoveableEditor
            (# charWidthType:: (# do moveableHeader.style->ts[] #);
               getencloser:: (# do editorEncloser[]->theEncloser[] #);
            #);
          editor: @editorType;
          open::<
            (# 
            do <<SLOT codemoveableContentsOpen:descriptor>>;
               INNER
            #);
          dobindings::<
            (# 
            <<SLOT codemoveableContentsDoBindings:dopart>>
            #);
       #);
     
     editorEncloserType:< codemoveableEditorEncloser
       (# onHighLight:: (# do wriggle #);
          tryclose:: (# do THIS(codemoveable).close #);
       #);
     editorEncloser: @editorEncloserType;
     
     
     moveableMenu::<
       (# open::<
            (# mi: ^actionedMenuItem;
            do &actionedMenuItem[]->mi[];
               (codemoveableFitAction##,NONE,NONE,'Fit to contents')->mi.open;
               INNER;
            #);
       #);
     codemoveableFitAction: actionedMenuItemAction
       (# 
       do contents.editor.preferredSize->newPreferredContentsSize;
       #);
     codemoveableOpenInWindowAction: actionedMenuItemAction
       (# 
       <<SLOT codemoveableOpenInWindowAction:dopart>>
       #);
     open::<
       (#
       enter (contents.debuggee[],contents.initialnode[])
       do <<SLOT codemoveableopen:descriptor>>
       #);
     close::<
       (# 
       do <<SLOT codemoveableclose:descriptor>>
       #);
     icon::
       (#
       do 'CodeView_Icon'->(getIconDB).getIcon->theIcon[];
       #);
  #);
