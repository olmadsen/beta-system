ORIGIN 'valhallaGUI';
BODY 'private/codemoveablebody';

INCLUDE 'GUIprocess';
INCLUDE '~beta/objectbrowser/v2.0/UI/moveable';
INCLUDE '~beta/objectbrowser/v2.0/options';
INCLUDE '~beta/editor/v5.0/codeviewer';

INCLUDE '~beta/interfacebuilder/v1.0d/guienvadds/guienvadds';
(* windowItem.{PreferredSize,BringToFront} *)

--- valhallaMainlib:attributes ---

codemoveable: moveable
  (# debuggee: ^GUIprocess;
     frag: ^debuggee.AST.fragmentForm;
     lastcast: ^debuggee.AST.ast;
     
     contentsType::<
       (# editor: @sifTextEditor 
            (# open:: (# do <<SLOT codemoveableEditorOpen:descriptor>> #);
            #);
          open::<
            (# 
            do <<SLOT codemoveableContentsOpen:descriptor>>
            #);
          dobindings::<
            (# 
            do TRUE->editor.bindRight->editor.bindBottom;
            #);
       #);
     moveableMenu::<
       (# open::<
            (# mi: ^moveableMenuitem;
            do &moveableMenuItem[]->mi[];
               (codemoveableFitAction##,NONE,NONE,'Fit to contents')->mi.open;
               INNER;
            #);
       #);
     codemoveableFitAction: moveableMenuItemAction
       (# 
       do contents.editor.preferredSize->newPreferredContentsSize;
       #);
     open::<
       (#
       enter (debuggee[],lastcast[])
       do <<SLOT codemoveableopen:descriptor>>
       #);
     close::<
       (# 
       do <<SLOT codemoveableclose:descriptor>>
       #);
     icon::
       (#
       do 'CodeView_Icon'->(getIconDB).getIcon->theIcon[];
       #);
     select:
       (#
       enter lastcast[]
       <<SLOT codemoveableSelect:dopart>>
       #);
  #);
