ORIGIN '~beta/guienv/v1.4/guienv';
BODY 'private/stackviewbody';

INCLUDE '../remotedumper';
INCLUDE '../componentstack';
INCLUDE '../processInterface';
INCLUDE '~beta/basiclib/v1.5/formatio';
INCLUDE '~beta/objectbrowser/v2.1/UI/listmoveable';
INCLUDE '~beta/objectbrowser/v2.1/UI/moveable';
INCLUDE '~beta/objectbrowser/v2.1/UI/icons';
INCLUDE '~beta/objectbrowser/v2.1/options';
INCLUDE '~beta/objectbrowser/v2.1/UI/objectview';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- guienvlib:attributes ---
stackviewlistList: List
  (# (*element:: stackViewList;*)
     onInsert:<
       (# theStackviewList: ^window.stackViewList;
       enter theStackviewList[]
       do INNER
       #);
     onDelete:<
       (# theStackviewList: ^window.stackViewList;
       enter theStackviewList[]
       do INNER
       #);
     newscan: scan
       (# cur: ^window.stackViewList;
       do current[]->cur[]; INNER;
       #);
  #);

getstackviewlistList: objectPool.get
  (# type::< stackviewlistList; init:: (# do obj.init #)#);

--- windowLib:attributes ---
stackviewStack: componentStack
  (# codeTypes:  [1]@Integer;
     viewIndex:  [1]@Integer;
     viewText: [1]^Text;
     lineCount: @Integer;
  #);

unknownCode: (# exit 0 #);
betaCode:    (# exit 1 #);
Gcode:       (# exit 2 #);
Ccode:       (# exit 3 #);
attCode:     (# exit 4 #);

stackviewList: listmoveableList
  (# <<SLOT stackviewListLib:attributes>>;
     
     open::< (# <<SLOT stackviewlistopen:dopart >> #);
     close::< (# do <<SLOT stackviewlistclose:descriptor>> #);
     eventHandler::<
       (# onMouseDown::<
            (# do <<SLOT stackviewOnMouseDown:descriptor>> #);
          onSelect::<
            (# do <<SLOT stackviewOnSelect:descriptor>> #)
       #);
     
     encl: ^stackViewEncloser;
     getencloser:<
       (# theEncloser: ^stackViewEncloser
       do INNER; theEncloser[]->encl[];
       exit theEncloser[]
       #);
     
     doUpdate::< (# <<SLOT stackviewlistdoupdate:dopart>> #);
     onDeIconify:: (# <<SLOT stackviewlistOnDeiconify:dopart>> #);
     
     debuggee: ^processInterface;
     component: ^processInterface.remoteBetaObject;
     lastStack: ^stackviewStack;
     shortnames,shorttitle,numbernames: @Boolean;
     odb: ^optiondb;
     objects: ^objectdb;
     updatedeferred: @Boolean; (* TRUE if an update was deferred due to this
                                * stackview being iconified. *)
     
     debug: @Boolean; (* TRUE if extra debug information (for valhalla 
                       * debugging) should be added to the stackview. *)
     
     svPrivate: @<<SLOT stackViewPrivate:descriptor>>;
  #);

stackViewEncloser:
  (# gettarget:<
       (# debuggee: ^processInterface; component: ^processInterface.remoteBetaObject;
       do INNER
       exit (debuggee[],component[])
       #);
     openNewObjectView:< OpenObjectView;
     openNewCodeView:<
       (# cast: ^astInterface.ast;
       enter cast[]
       do INNER
       #);
     selectObjectView:< OpenObjectView;
     selectCodeView:<
       (# cast: ^astInterface.ast;
       enter cast[]
       do INNER
       #);
     selectExternalView:<
       (# msg: ^text;
       enter msg[]
       do INNER
       #);
     settitle:< (# t: ^Text enter t[] do INNER #);
     tryclose:< Object;
     onHighLight:< Object;
     alertInfo:<
       (# inf,title: ^Text;
       enter (inf[],title[])
       do INNER          
       #);
  #);

stackviewmoveable: listmoveable
  (# <<SLOT stackviewlib:attributes>>;
     debuggee: ^processInterface;
     component: ^processInterface.remoteBetaObject;
     
     contentsType::< 
       (# listType::< stackviewList
            (# getencloser:: (# do listEncloser[]->theEncloser[] #)#);
       #);
     
     listEncloserType:< stackViewEncloser
       (# settitle:: (# do t[]->THIS(stackviewmoveable).title #);
          gettarget::
            (# 
            do THIS(stackviewmoveable).debuggee[]->debuggee[];
               THIS(stackviewmoveable).component[]->component[];
            #);
          tryclose:: (# do THIS(stackviewmoveable).close #);
          onHighLight:: (# do wriggle #);
       #);
     listEncloser: @listEncloserType;
     
     open::<
       (# 
       enter (debuggee[],component[])
       do <<SLOT stackviewopen:descriptor>>
       #);
     icon::
       (#
       do 'StackView_Icon'->(getIconDB).getIcon->theIcon[];
       #);
  #);
