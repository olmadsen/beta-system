ORIGIN '~beta/guienv/guienv';

INCLUDE '~beta/toollibs/UI/moveable';
INCLUDE '~beta/guienv/fields';
INCLUDE '~beta/guienv/scrolllists';

-- windowLib: attributes --
textList: list
  (# element:: Text;
  #);

objectListView: moveable
  (# onFollow:<
       (# inx: @integer;
       enter inx
       do INNER;
       #);
     onShow:<
       (# inx: @integer;
       enter inx
       do INNER;
       #);
     onBack:<
       (#
       do INNER;
       #);
     set:
       (# data: ^textList;
       enter data[]
       do (if contents.dataFld.numberOfItems > 0 then
              contents.dataFld.numberOfItems -> contents.dataFld.deleteFirst;
          if);
          data.size -> contents.dataFld.append;
          data.scan
          (# inx: @integer;
          do inx + 1 -> inx;
             (inx, current[]) -> contents.dataFld.setText;
          #);
       #);
     contentsType::<
       (# showBtn: @pushButton
            (# eventHandler::
                 (# onMouseUp::
                      (#
                      do dataFld.selection.first -> onShow; 
                      #);
                 #);
               open::
                 (#
                 do 'Show' -> label;
                 #);
            #);
          followBtn: @pushButton
            (# eventHandler::
                 (# onMouseUp::
                      (#
                      do dataFld.selection.first -> onFollow; 
                      #);
                 #);
               open::
                 (#
                 do 'References' -> label;
                 #);
            #);
           backBtn: @pushButton
            (# eventHandler::
                 (# onMouseUp::
                      (#
                      do onBack; 
                      #);
                 #);
               open::
                 (#
                 do 'Back' -> label;
                    disable;
                 #);
            #);
          dataFld: @textScrollList
            (#
            #);
          doBindings::
            (#
            do true -> dataFld.bindBottom;
               true -> dataFld.bindRight;
            #);
          open::<
            (#
            do showBtn.open;
               (3, 5) -> showBtn.position;
               (60, 20) -> showBtn.size;
               followBtn.open;
               (3 + 60 + 13, 5) -> followBtn.position;
               (60, 20) -> followBtn.size;
               backBtn.open;
               (3 + 60 + 13 + 60 + 13, 5) -> backBtn.position;
               (60, 20) -> backBtn.size;
               dataFld.open;
               (3, 5 + 20 + 5) -> dataFld.position;
               (defaultSize.h - 2*3, defaultSize.v - (5 + 20 + 5 + 5)) -> dataFld.size;
               INNER;
            #);
       #);
  #);
