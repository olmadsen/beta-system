ORIGIN '../rowcolumn';

--- rcAddChild:descriptor ---
(#  thisSize: @Point; new: ^rcprivate.children.element;
enter (child[],spacing)
do 
   (if rcprivate.children.size=0 then
       (if border.visible then (2,2)->nextpos if)
   if);
   
   child.size->thisSize;
   (if rcmode
    //RC_VERTICAL then
       spacing+nextpos.v->nextpos.v;
       nextpos->child.position;
       thisSize.v+nextpos.v->nextpos.v;
       (thisSize.h,sz.h)->max->sz.h;
       thisSize.v+sz.v+spacing->sz.v;
    //RC_HORISONTAL then
       spacing+nextpos.h->nextpos.h;
       nextpos->child.position;
       thisSize.h+nextpos.h->nextpos.h;
       (thisSize.v,sz.v)->max->sz.v;
       thisSize.h+sz.h+spacing->sz.h;
   if);
   &rcprivate.children.element[]->new[];
   child[]->new.child[];
   child.bindTop->new.bindTop;
   child.bindLeft->new.bindLeft;
   child.bindBottom->new.bindBottom;
   child.bindRight->new.bindRight;
   TRUE->child.bindTop->child.bindLeft;
   FALSE->child.bindBottom->child.bindRight;
   new[]->rcprivate.children.append;
#)

--- rcopen:descriptor ---
(# 
do rcprivate.children.init;
   INNER open; (* Call addChild for each child. *)
   (if rcmode
    //RC_VERTICAL then
       trailingspace+sz.v->sz.v;
    //RC_HORISONTAL then
       trailingspace+sz.h->sz.h;
   if);
   
   (if rcalign<>RC_LEFT then
       rcprivate.children.scan
       (# cursize, curpos: @Point;
       do
          current.child.size->cursize;
          current.child.position->curpos;
          (if rcalign
           //RC_RIGHT (* Same as RC_BOTTOM *) then
              (if rcmode
               //RC_VERTICAL then
                  ((sz.h-cursize.h)+curpos.h,curpos.v)->current.child.position;
               //RC_HORISONTAL then
                  (curpos.h,(sz.v-cursize.v)+curpos.v)->current.child.position;
              if)
           //RC_MIDDLE then
              (if rcmode
               //RC_VERTICAL then
                  (((sz.h-cursize.h) div 2)+curpos.h,curpos.v)
                    ->current.child.position;
               //RC_HORISONTAL then
                  (curpos.h, ((sz.v-cursize.v) div 2)+curpos.v)
                    ->current.child.position;
              if)
          if);
       #);
   if);
   
   (if border.visible then (4,4)->sz.add if);
   sz->size;
   
   rcprivate.children.scan
   (# 
   do 
      current.bindTop->current.child.bindTop;
      current.bindLeft->current.child.bindLeft;
      current.bindBottom->current.child.bindBottom;
      current.bindRight->current.child.bindRight;
   #);
   sz->totalSize;
#)

--- rcScanChildren:dopart ---
do rcprivate.children.scan 
   (# 
   do current.child[]->THIS(scanChildren).current[];
      INNER scanChildren;
   #)
   
--- rcPrivate:descriptor ---
(# children: @List 
     (# element:: 
          (# child: ^WindowItem;
             bindTop,bindLeft,bindBottom,bindRight: @Boolean
          #);
     #);
#)
