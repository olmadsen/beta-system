ORIGIN '../enveditor';

---envEditorWarning:descriptor---
(# warned: @boolean;
do (if not warned then
       (this(envEditor)[],'Please note, that changes will not have effect\nuntil you RERUN the program'
       ,'Warning')->alertUser;
       true->warned
   if)
#)

---envEditorRecalc:doPart--
do this(envEditor).size->(windowWidth,windowHeight);
   (windowWidth-5*distance-4*buttonWidth)/2->buttonDistance;
   windowWidth-3*distance-labelWidth->editWidth;
   windowHeight-distance-distance-2*(height+8)-distance-height-distance
     ->scrollHeight;

--- envEditorScrollList:descriptor ---
textscrolllist
(# eventHandler::
     (# onMouseUp::
          (# i: @integer
          do selection.first->i;
             scan: envVars.scan
               (#
               do i-1->i;
                  (if i=0 then
                      (* the selected envVar found !! *)
                      current.e.name[]->envVarNameFrame.envVarNameEdit.contents;
                      current.e.oldValue[]->envVarValueFrame.envVarValueEdit.contents;
                      leave scan
                  if)
               #)
          #);
        onFatherFrameChanged::
          (#
          do recalc;
             (windowWidth-distance*2,scrollHeight)->size;
             (distance,distance)->position;
          #);
     #);
   multipleSelection::< falseObject;
   open::<
     (#
     do (windowWidth-distance*2,scrollHeight)->size;
        (distance,distance)->position;
     #)
#)

---enveditorClearButton:descriptor---
pushbutton
(# eventHandler::<
     (# onMouseUp::<
          (# (* clear the edit fields and deselect in scroll-list *)
          do ''->envVarNameFrame.envVarNameEdit.contents;
             ''->envVarValueFrame.envVarValueEdit.contents;
             envVarScrollList.selection.clear;
          #);
        onFatherFrameChanged::<
          (#
          do recalc;
             (buttonWidth,height)->size;
             (distance,distance+scrollHeight+distance+(height+8)*2+distance)
               -> position;
          #)
     #);
   open::<
     (#
     do (buttonWidth,height)->size;
        (distance,distance+scrollHeight+distance+(height+8)*2+distance)
          ->position;
        'Clear'->label
     #)
#)

---envEditorDeleteButton:descriptor---
pushbutton
(# eventHandler::<
     (# onMouseUp::<
          (#
          do l: (if (envVarNameFrame.envVarNameEdit.contents).length>0 then
                    envVars.iterate
                    (# i: @integer
                    do i+1->i;
                       (if envVarNameFrame.envVarNameEdit.contents->current.elm.e.name.equal then
                           (* We have found the envVar name as
                            * the name of one of the envVar's.
                            * Let's delete this envVar:
                            *)
                           ''->envVarNameFrame.envVarNameEdit.contents;
                           ''->envVarValueFrame.envVarValueEdit.contents;
                           current.elm.e.delete;
                           current[]->envVars.delete;
                           (i,1)->envVarScrollList.delete;
                           ((i,envVarScrollList.numberOfItems)->max,FALSE)
                             ->envVarScrollList.selection.select;
                           leave l
                       if)
                    #);
                    (* OK, we didn't find the envVar name in the
                     * list of envVar's.  We ignore the delete
                     *)
                    beep
                if)
          #);
        onFatherFrameChanged::<
          (#
          do recalc;
             (buttonWidth,height)->size;
             (distance+buttonWidth+buttonDistance,
             distance+scrollHeight+distance+(height+8)*2+distance)
               -> position;
          #)
     #);
   open::<
     (#
     do (buttonWidth,height)->size;
        (distance+buttonWidth+buttondistance+distance,
        distance+scrollHeight+distance+(height+8)*2+distance)
          ->position;
        'Delete'->label
     #)
#)


---envEditorSetButton:descriptor---
pushbutton
(# eventHandler::<
     (# onMouseUp::<
          (# t: ^text; newEnvVar: ^envVars.element
          do l: (if (envVarNameFrame.envVarNameEdit.contents).length>0 then
                    envVars.scan
                    (# i: @integer
                    do i+1->i;
                       (if envVarNameFrame.envVarNameEdit.contents->current.e.name.equal then
                           (* We have found the envVar name as
                            * the name of one of the envVar's.
                            * Let's change its value:
                            *)
                           (if not (envVarValueFrame.envVarValueEdit.contents->current.e.oldValue.equal) then
                               (* the value of the selected
                                * envVar have been changed - we
                                * update the value
                                *)
                               envVarNameFrame.envVarNameEdit.contents->t[]; '='->t.puttext;
                               envVarValueFrame.envVarValueEdit.contents->t.append;
                               (i,t[])->envVarScrollList.settext;
                               envVarValueFrame.envVarValueEdit.contents->current.e.oldValue[]->current.e.set;
                               (i,false)->envVarScrollList.selection.select;
                               envVarScrollList.selection.scrollIntoView;
                           if);
                           leave l
                       if)
                    #);
                    (* OK, we didn't find the envVar name in the
                     * list of envVar's.  We define a new envVar
                     * with the given value - and inserts the new
                     * envVar in the scroll-list etc.
                     *)
                    &envVars.element[]->newEnvVar[];
                    &envVarType[]->newEnvVar.e[];
                    envVarNameFrame.envVarNameEdit.contents
                      ->newEnvVar.e.name[];
                    envVarValueFrame.envVarValueEdit.contents
                      ->newEnvVar.e.oldValue[];
                    1->envVarScrollList.append;
                    newEnvVar.e.name.copy->t[];
                    '='->t.puttext;
                    newEnvVar.e.oldValue[]->t.append;
                    (envVarScrollList.numberOfItems,t[])
                      ->envVarScrollList.settext;
                    envVarValueFrame.envVarValueEdit.contents
                      ->newEnvVar.e.new;
                    (envVarScrollList.numberOfItems,false)
                      ->envVarScrollList.selection.select;
                    envVarScrollList.selection.scrollIntoView;
                    newEnvVar[]->envVars.append;
                if)
          #);
        onFatherFrameChanged::<
          (#
          do recalc;
             (buttonWidth,height)->size;
             (distance+buttonWidth*2+buttonDistance*2,
             distance+scrollHeight+distance+(height+8)*2+distance)
               -> position;
          #)
     #);
   open::<
     (#
     do (buttonWidth,height)->size;
        (distance+buttonWidth*2+buttonDistance*2 ,
        distance+scrollHeight+distance+(height+8)*2+distance)
          -> position;
        'Set'->label
     #);
#)

---envEditorCloseButton:descriptor---
pushButton
(# 
   eventHandler::<
     (# onMouseUp::<
          (# 
          do onClose;
             this(window).close;
          #);
        onFatherFrameChanged::<
          (#
          do recalc; 
             (buttonWidth,height)->size;
             (WindowWidth-distance-buttonwidth, distance+scrollHeight+distance+(height+8)*2+distance) -> position;
          #)
     #);
   open::<
     (# 
     do 'Close'->label;
        (buttonWidth,height)->size;
        (WindowWidth-distance-buttonwidth, distance+scrollHeight+distance+(height+8)*2+distance)->position;
     #);
#)

---enveditorSep:descriptor---
separator
(# open::< (#  
           do (4,50)->size; 
              (windowWidth-distance*3-buttonwidth,windowheight-height*2)->position; 
           #);
   eventHandler::<
     (# 
        onFatherFrameChanged::<
          (#
          do recalc; 
             (4,50)->size; 
             (windowWidth-distance*3-buttonwidth,windowheight-height*2)->position;  
          #)
     #);
   vertical:: (#  do true->value #)
#)


---envEditorOpendopart:doPart---
do 
   tst.open; tst.close; (* just to get the width and height of fonts *)
   300->scrollHeight;
   200->labelWidth; 
   400->editWidth; 
   4->distance;
   distance+labelWidth+distance+editWidth+distance->windowWidth;
   distance+scrollHeight+distance+(height+8)*2+distance+height+distance
     ->windowHeight;
   (windowWidth,windowHeight)->size;
   recalc;
   
   sep.open;
   
   envVarScrollList.open;
   envVarNameFrame.open; envVarValueFrame.open;

   clear.open; delete.open; set.open; closePB.open; 
   (windowWidth,windowHeight)->size;

   getEnvironDB->edb[];
   (# ev: ^myEnvVar;
   do edb.scan
      (#
      do &myEnvVar[]->ev[];
         curname.copy->ev.name[];
         ev[]->attach;
      #);
   #);

   envVars.scan
   (# (* create the elements in the scroll-list *)
   do current.e.init;
      1->envVarScrollList.append;
      current.e.name.copy->t[]; '='->t.puttext;
      current.e.oldValue[]->t.append;
      (envVarScrollList.numberOfItems,t[])->envVarScrollList.settext;
   #);
   (1,FALSE)->envVarScrollList.selection.select;
   'Environment Variable Editor'->title;
