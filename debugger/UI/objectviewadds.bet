ORIGIN '~beta/objectbrowser/v2.0/UI/objectview';
INCLUDE '~beta/objectbrowser/v2.0/UI/moveable';
INCLUDE '~beta/objectbrowser/v2.0/objectdumper';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- guienvlib:attributes ---
showStack:
  (# comp: ^betaObject;
  enter comp[]
  do INNER
  #);
--- moveableObjectViewLib:attributes ---
addObjectViewactionedMenuItems:
  (# show:< showStack;
     ssp: @movMenu.showStackParam
       (# getbo:: (# do THIS(moveableObjectView).theBo[]->bo[]#);
          show:: (# do comp[]->THIS(addObjectViewactionedMenuItems).show #);
       #);
  do (movMenu.showStackAction##,movMenu.showStackOnStatus##,
     ssp[],'Show component stack')
       ->(&movMenu.actionedMenuItem[]).open;
  #);
--- objectViewLib:attributes ---
addObjectViewItemMenuItems:
  (# show:< showStack;
     ssp: @itemMenu.showStackParam
       (# getbo:: 
            (# 
            do (if itemMenu.targetatt.attType=StatCompRefAttribute then
                   (itemMenu.targetatt[]->Qua(#as::complexAttribute#)).bo[]->bo[];
               if);
            #);
          show:: (# do comp[]->THIS(addObjectViewItemMenuItems).show #);
       #);
  do (itemMenu.showStackAction##,itemMenu.showStackOnStatus##,ssp[],'Show component stack')
       ->(&itemMenu.actionedMenuItem[]).open;
  #);
--- menuLib:attributes ---
showStackParam:
  (# getbo:< (# bo: ^betaObject; do INNER exit bo[] #);
     show:< showStack;
  #);
showStackAction: actionedMenuItemAction
  (# paramType:: showStackParam;
  do (if param[]->showStackOnStatus then
         param.getbo->param.show;
     if);
  #);
showStackOnStatus: actionedMenuItemOnStatus
  (# paramType:: showStackParam;
     bo: ^betaObject;
  do param.getbo->bo[];
     (if bo[]<>NONE then bo.IsComponentObject->value if);
  #);
