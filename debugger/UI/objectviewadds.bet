ORIGIN '~beta/objectbrowser/v2.1.x/UI/objectview';
INCLUDE '~beta/objectbrowser/v2.1.x/UI/moveable';
INCLUDE '~beta/objectbrowser/v2.1.x/objectdumper';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- guienvlib:attributes ---
showStack:
  (# comp: ^betaObject;
  enter comp[]
  do INNER
  #);
showCode:
  (# ast: ^astinterface.ast;
  enter ast[]
  do INNER
  #);
--- moveableObjectViewLib:attributes ---
addObjectViewactionedMenuItems:
  (# show:< showStack;
     ssp: @movMenu.showStackParam
       (# getbo:: (# do THIS(moveableObjectView).theBo[]->bo[]#);
          show:: (# do comp[]->THIS(addObjectViewactionedMenuItems).show #);
       #);
     
     showC:< showCode;
     scp: @movMenu.showCodeParam
       (# getast:: 
            (# bo: ^betaObject;
            do THIS(moveableObjectView).theBo[]->bo[];
               bo[]->bo.dumper.betaObjectObjectDescriptor->ast[];
            #);
          show:: 
            (# 
            do ast[]->THIS(addObjectViewactionedMenuItems).showC 
            #);
       #);
     
  do (movMenu.showStackAction##,movMenu.showStackOnStatus##,
     ssp[],'Show component stack')
       ->(&movMenu.actionedMenuItem[]).open;
     (movMenu.showCodeAction##,movMenu.showCodeOnStatus##,
     scp[],'Show Code')
       ->(&movMenu.actionedMenuItem[]).open;
  #);
--- objectViewLib:attributes ---
addObjectViewItemMenuItems:
  (# show:< showStack;
     ssp: @itemMenu.showStackParam
       (# getbo:: 
            (# 
            do (if itemMenu.targetatt.attType=StatCompRefAttribute then
                   (itemMenu.targetatt[]->Qua(#as::complexAttribute#)).bo[]->bo[];
               if);
            #);
          show:: (# do comp[]->THIS(addObjectViewItemMenuItems).show #);
       #);
     
     showC:< showCode;
     scp: @itemMenu.showCodeParam
       (# getast:: 
            (# bo: ^betaObject;
            do (if (itemMenu.targetatt.attType=StatCompRefAttribute)
                   or (itemMenu.targetatt.attType=StatRefAttribute)
                   
                   then
                   (itemMenu.targetatt[]->Qua(#as::complexAttribute#)).bo[]->bo[];
                   bo[]->bo.dumper.betaObjectObjectDescriptor->ast[];
               if);
            #);
          show:: 
            (# 
            do ast[]->THIS(addObjectViewItemMenuItems).showC 
            #);
       #);
     
     
  do (itemMenu.showStackAction##,itemMenu.showStackOnStatus##,ssp[],'Show component stack')
       ->(&itemMenu.actionedMenuItem[]).open;
     (itemMenu.showCodeAction##,itemMenu.showCodeOnStatus##,
     scp[],'Show Code')
       ->(&itemMenu.actionedMenuItem[]).open;
  #);
--- menuLib:attributes ---
showStackParam:
  (# getbo:< (# bo: ^betaObject; do INNER exit bo[] #);
     show:< showStack;
  #);
showStackAction: actionedMenuItemAction
  (# paramType:: showStackParam;
  do (if param[]->showStackOnStatus then
         param.getbo->param.show;
     if);
  #);
showStackOnStatus: actionedMenuItemOnStatus
  (# paramType:: showStackParam;
     bo: ^betaObject;
  do param.getbo->bo[];
     (if bo[]<>NONE then bo.IsComponentObject->value if);
  #);

showCodeParam:
  (# getast:< (# ast: ^astInterface.ast; do INNER exit ast[] #);
     show:< showCode;
  #);
showCodeAction: actionedMenuItemAction
  (# paramType:: showCodeParam;
  do (if param[]->showCodeOnStatus then
         param.getast->param.show;
     if);
  #);
showCodeOnStatus: actionedMenuItemOnStatus
  (# paramType:: showCodeParam;
     ast: ^astInterface.ast;
  do param.getast->ast[];
     (if ast[]<>NONE then
         TRUE->value;
     if);
  #);


