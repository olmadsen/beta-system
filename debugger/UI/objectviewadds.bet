ORIGIN '~beta/objectbrowser/v2.0/UI/objectview';
INCLUDE '~beta/objectbrowser/v2.0/UI/moveable';
INCLUDE '../remotedumper';

--- guienvlib:attributes ---
showStack:
  (# comp: ^remoteBetaObject;
  enter comp[]
  do INNER
  #);
--- moveableObjectViewLib:attributes ---
addObjectViewMoveableMenuItems:
  (# show:< showStack;
     ssp: @movMenu.showStackParam
       (# getbo:: (# do THIS(moveableObjectView).theBo[]->bo[]#);
          show:: (# do comp[]->THIS(addObjectViewMoveableMenuItems).show #);
       #);
  do (movMenu.showStackAction##,movMenu.showStackOnStatus##,
     ssp[],'Show component stack')
       ->(&movMenu.moveableMenuItem[]).open;
  #);
addObjectViewItemMenuItems:
  (# show:< showStack;
     ssp: @contents.list.itemMenu.showStackParam
       (# getbo:: 
            (# 
            do (if contents.list.itemMenu.targetatt.attType
                //StatCompRefAttribute then
                   (contents.list.itemMenu.targetatt[]
                     ->Qua(#as::complexAttribute#)
                   ).bo[]->bo[];
               if);
            #);
          show:: (# do comp[]->THIS(addObjectViewItemMenuItems).show #);
       #);
  do (contents.list.itemMenu.showStackAction##,
     contents.list.itemMenu.showStackOnStatus##,
     ssp[],'Show component stack')
       ->(&contents.list.itemMenu.moveableMenuItem[]).open;
  #);
--- menuLib:attributes ---
showStackParam:
  (# getbo:< (# bo: ^remoteBetaObject; do INNER exit bo[] #);
     show:< showStack;
  #);
showStackAction: moveableMenuitemAction
  (# paramType:: showStackParam;
  do (if param[]->showStackOnStatus then
         param.getbo->param.show;
     if);
  #);
showStackOnStatus: moveableMenuitemOnStatus
  (# paramType:: showStackParam;
     bo: ^remoteBetaObject;
  do param.getbo->bo[];
     (if bo[]<>NONE then bo.iscomp->value if);
  #);
