ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/toollibs/UI/moveable'
        '~beta/containers/list'
        '~beta/guienv/utils/treeview';
-- windowlib: Attributes --
heapBrowser: treeView
  (#
     rootItem: folder
       (#
          onExpand::  (#  do updateTree;  #);
          onCollapse::  (#  do items.clear;  #);
          setUp: (#  do 'HEAP'->label[] #);
          
       #);
     attributeItem: item (#  #);
     protoItem: folder
       (#
          theName: ^text;
          num: @integer;
          setUp:
            (# 
            enter (theName[],num)
            do
               &text[]->label[];
               num->label.putint;
               ' '->label.puttext;
               theName[]->label.puttext;
               
            #);
          onExpand::< 
            (# tempItem: ^instanceItem; i: @integer
            do
               0->i;
               L:
                 (# 
                 do
                    (if i >= num then leave L;  if);
                    &instanceItem[]->tempItem[];
                    tempItem[]->items.append;
                    theName[]->tempItem.setup;
                    i+1->i;
                    restart L;
                    
                 #);
               INNER
            #);
          onCollapse::<  (#  do items.clear; INNER #);
          
       #);
     instanceItem: Folder
       (#
          theName: ^text;
          setUp: (#  enter theName[] do theName[]->label[];  #);
          onExpand::<  (#  do updateTree; INNER #);
          onCollapse::<  (#  do items.clear; INNER #);
          
       #);
     theRoot: @rootItem;
     open::<  (#  do theRoot.setUp; theRoot[]->root #);
     updateTree:< (#  do INNER ;  #);
     
  #);
heapview: moveable
  (#
     sizeX,sizeY: @integer;
     theHeapBrowser: heapBrowser
       (#
          updateTree::< 
            (# 
            do
               theRoot.items.clear;
               objects.scan
                 (# tempItem: ^protoItem
                 do
                    &protoItem[]->tempItem[];
                    tempItem[]->theRoot.items.append;
                    (current.name[],current.num)->tempItem.setUp;
                    
                 #)
            #);
          
       #);
     theObj:
       (#
          name: ^text;
          theRef: @list (# element:: object #);
          num: @integer
       #);
     objects: @list (# element:: theObj #);
     contentsType::< 
       (#
          theScroller: @scroller
            (#
               contentsType:: theHeapBrowser;
               open:: 
                 (#  do (10,10)->position; (sizeX-10,sizeY-10)->size;  #);
               eventHandler:: 
                 (# onRefresh::  (#  do (sizeX-10,sizeY-10)->size;  #)
                 #);
               
            #);
          eventHandler::< 
            (# onRefresh::<  (#  do size->(sizeX,sizeY); INNER #); 
            #);
          open::<  (#  do size->(sizeX,sizeY); theScroller.open; INNER #);
          
       #);
     open::<  (#  do size->(sizeX,sizeY); INNER #);
     insertTreeObj:<
       (#
          tempObj: ^theObj;
          found: @boolean;
          theName: ^text;
          theRef: ^object;
          
       enter (theRef[],theName[])
       do
          false->found;
          L:
            (# 
            do
               objects.scan
                 (# 
                 do
                    (if current.name[]->theName.equal then
                        current.num+1->current.num;
                        theRef[]->current.theRef.append;
                        true->found;
                        leave L;
                        
                    if);
                    
                 #);
               
            #);
          (if not found then
              &theObj[]->tempObj[];
              theName[]->tempObj.name[];
              theRef[]->tempObj.theRef.append;
              1->tempObj.num;
              tempObj[]->objects.append;
              
          if);
          
       #);
     updateTree:< (#  do contents.theScroller.contents.updateTree #);
     
  #)  

