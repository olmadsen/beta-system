ORIGIN '~beta/guienv/v1.3.1/guienv';

INCLUDE 'rowcolumn';
INCLUDE '~beta/guienv/v1.3.1/fields';
INCLUDE '~beta/guienv/v1.3.1/controls';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- windowLib:attributes ---
maxPromptWidth: (# exit 150 #);

textList: List 
  (# element:: Text;
     lastStyleName: ^Text;
     mx: @Integer;
     maxLength:
       (# ts: ^textStyle;
          compute:
            (# 
            do ts.name->lastStyleName[];
               scan
               (# 
               do (current[]->ts.widthOfText,mx)->max->mx;
               #);
            #);
       enter ts[]
       do (if lastStyleName[]<>NONE then
              (if not (ts.name->lastStyleName.equal) then compute if)
           else
              compute; 
          if);
       exit mx
       #);
  #);

textPrompt: RowColumn
  (# infoType:< StaticText 
       (# open::<
            (# info: ^Text; ts: ^textStyle; allinf: ^TextList;
            enter (info[],allinf[])
            do info[]->label;
               (((style->ts[]->allinf.maxLength)+10,maxPromptWidth)->min,
               ts.lineHeight+10)->size;
               INNER;
            #);
       #);
     info: @infoType;
     
     promptType:< editText
       (# open::<
            (# prmpt: ^Text; ts: ^textStyle;
            enter prmpt[]
            do prmpt[]->contents;
               style->ts[];
               (((prmpt[]->ts.widthOfText)+50,maxPromptWidth)->min,
               ts.lineHeight+10)->size;
               TRUE->border.visible; borderStyles.shadowIn->border.style;
               TRUE->bindRight;
               INNER;
            #);
          eventHandler::<
            (# onKeyDown::<
                 (# 
                 do (if ch < 32 then
                        '<'->put; ch->putInt; '>'->put;
                     else
                        ch->put;
                    if);
                 #);
            #);
       #);
     prompt: @promptType;
     
     open::<
       (# trailingSpace:: (# do 5->value #);
          inf, prmpt: ^Text; allinf: ^TextList;
          sz: @Point;
       enter (inf[],prmpt[],allinf[])
       do RC_HORISONTAL->rcmode;
          RC_MIDDLE->rcalign;
          (NONE,inf[],allinf[])->info.open; (info[],5)->addChild;
          (NONE,prmpt[])->prompt.open; (prompt[],10)->addChild;
       #);
     
     onReturn:<
       (# prmpt: ^Text;
       enter prmpt[]
       do INNER
       #);
  #);

--- guienvLib:attributes ---
paramDialog: window
  (# dialog: @RowColumn
       (# t1: @textPrompt (# onReturn:: (# do prmpt[]->putLine; #);
          t2: @textPrompt (# #);
          t3: @textPrompt (# #);
          open::<
            (# totalSize::<
                 (# csz: @Point;
                 do t1.size->csz; (sz.h,csz.v)->t1.size;
                    t2.size->csz; (sz.h,csz.v)->t2.size;
                    t3.size->csz; (sz.h,csz.v)->t3.size;
                 #);
               trailingSpace::< (# do 2->value #);
               tl: @TextList;
            do RC_VERTICAL->rcmode;
               tl.init;
               'dyt'->tl.append; 'baat'->tl.append; 'flyyyyyt'->tl.append;
               (NONE,'dyt','Something',tl[])->t1.open; 
               (NONE,'baat','Something wider',tl[])->t2.open;
               (NONE,'flyyyyyt','smaller',tl[])->t3.open;
               (t1[],2)->addChild;
               (t2[],4)->addChild;
               (t3[],4)->addChild;
            #);
       #);
     open::<
       (# sz: @Point;
       do true->floating;
          'Debuggee Options'->title;
          dialog.open;
          dialog.size->sz->THIS(paramDialog).size;
          sz->THIS(paramDialog).minSize;
          sz->THIS(paramDialog).maxSize;
       #);
  #);

