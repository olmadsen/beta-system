ORIGIN '~beta/guienv/v1.2/guienv';

INCLUDE '~beta/objectbrowser/v2.0/UI/objectview';
INCLUDE '~beta/objectbrowser/v2.0/UI/moveable';

--- lib:attributes ---

initialVEUsize: (# exit (600,400) #);

valhallaGUI: guienv
  (# <<SLOT valhallaGUIlib:attributes>>;
     
     putinfo: 
       (# t: ^Text; 
       enter t[] 
       do t[]->veu.info.label; true->veu.info.update;
       #);
     
     onQuit:< Object;
     onTerminate:< Object;
     
     onMoveablesInsert:<
       (# theMoveable: ^veu.moveableObjectView;
       enter theMoveable[]
       do INNER;
       #);
     
     onMoveablesDelete:<
       (# theMoveable: ^veu.moveableObjectView;
       enter theMoveable[]
       do INNER;
       #);
     
     veu: @veuType;
     
     veuType:< window
       (# moveables: @moveableList
            (# onInsert:: (# do theMoveable[]->onMoveablesInsert #);
               onDelete:: (# #);
            #);
          
          clr: @rectangle; clearInProgress: @Boolean;
          
          menuBarType::<
            (# fileMenu: @fileMenuType;
               windowMenu: @windowMenuType;
               fileMenuType:< menu
                 (# quitItem: @menuitem
                      (# open:: (# do 'Quit' -> name; 'Q' -> key #);
                         eventHandler:: 
                           (# onSelect:: (# do OnQuit; Terminate #)#)
                      #);
                    open::<
                      (# 
                      do 'File' -> name;
                         INNER;
                         quitItem.open; quitItem[] -> append;
                      #);
                 #);
               windowMenuType:< menu
                 (# closeItem: @menuitem
                      (# open:: (# do 'Close Framed' -> name; 'W' -> key #);
                         eventHandler:: 
                           (# onSelect:: 
                                (# 
                                do moveables.scan
                                   (#
                                   do (if clr->current.inRect then 
                                          current.close 
                                      if);
                                   #);
                                   clr -> objworld.veuDrawRect;
                                   false -> clearInProgress;
                                #);
                              onStatus:: (# do clearInProgress -> value #);
                           #)
                      #);
                    open::<
                      (# 
                      do 'Window' -> name;
                         closeItem.open; closeItem[] -> append;
                         INNER;
                      #);
                 #);
               open::< 
                 (# 
                 do fileMenu.open; fileMenu[]->append; 
                    windowMenu.open; windowMenu[]->append;
                    INNER 
                 #)
            #);
          
          open::
            (# 
            do moveables[] -> objectPool.put;
               initialVEUsize -> size; 'Valhalla Object Universe' -> title;
               contents->info.open; contents->objworld.open;
            #);
          
          objworld: @Canvas
            (# open::
                 (# sz: @Point; tmp: @Point;
                 do initialVEUsize->sz;
                    info.size->tmp;
                    sz.v-tmp.v->sz.v;
                    sz->size;
                    TRUE->bindLeft->bindRight->bindBottom->bindTop;
                 #);
               eventHandler::
                 (# onMouseDown::
                      (#
                      do (if buttonState //1 then
                             (if clearInProgress then clr->veuDrawRect if);
                             localPosition->SimpleDefineRect->clr;
                             (if not clr.isEmpty then
                                 true -> clearInProgress;
                                 clr->veuDrawRect;
                              else 
                                 false -> clearInProgress;
                             if);
                         if);
                      #);
                 #);
            #);
          
          info: @staticText
            (# open::<
                 (# sz: @Point; ts: ^TextStyle;
                    pos: @Point;
                 do 
                    style->ts[]; 
                    
                    InitialVEUsize->sz;
                    ts.lineHeight+6->sz.v;
                    sz->size;
                    
                    InitialVEUsize->pos;
                    0->pos.h;
                    pos.v-sz.v->pos.v;
                    pos -> Position;
                    
                    TRUE->bindLeft->bindRight->bindBottom; FALSE->bindTop;
                    
                    TRUE->border.visible;
                    borderStyles.shadowIn->border.style;
                 #);
            #);
       #);
     init:<
       (# 
       do veu.open; INNER;
       #);
  #);
