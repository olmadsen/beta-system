ORIGIN '~beta/guienv/v1.4/guienv';

INCLUDE '~beta/guienv/v1.4/stddialogs';
INCLUDE '~beta/guienv/v1.4/utils/pane';
INCLUDE '~beta/guienv/v1.4/utils/simplemenu';

INCLUDE '~beta/objectbrowser/v2.1/options';
INCLUDE '~beta/objectbrowser/v2.1/optionlist';

INCLUDE '~beta/objectbrowser/v2.1/UI/objectview';
INCLUDE '~beta/objectbrowser/v2.1/UI/moveable';
INCLUDE '~beta/objectbrowser/v2.1/UI/icons';
INCLUDE '~beta/objectbrowser/v2.1/UI/iconlist';
INCLUDE '~beta/objectbrowser/v2.1/UI/labelled';

INCLUDE '~beta/sourcebrowser/v1.1/ymer';

INCLUDE '../valhallaoptionlist';
INCLUDE '../valhallaiconlist';
INCLUDE '../processInterface';

INCLUDE '~beta/sysutils/v1.5/envstring';

(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- lib:attributes ---

valhallaGUI: ymerApplication
  (# <<SLOT valhallaGUIlib:attributes>>;
     sources: @ymerBrowserWindow;
     newCodeView:<
       (# cast: ^astInterface.ast
       enter cast[]
       do INNER
       #);
     mpsType::
       (# onOpenGroup::
            (# t: @Text;
            do 'Opening %s'->t.putFormat (# do groupName[]->s #);
               t[]->putinfo;
            #);
          onOpenGroupDone::
            (# 
            do ''->putinfo; (if not fg.isRealOpen then fg.realOpen if);
            #);
          onOldGroup::
            (# t: @Text;
            do '"%s%s" is older than "%s.%s"'->t.putFormat
               (# 
               do groupName[]->s; MPS.AST.astFileExtension->s;
                  groupName[]->s; 'bet'->s;
               #);
               (t[],'Debug Info Error: Recompilation required')->alertInfo;
               false->allowParsing; true->continue; true->doneInInner;
            #);
          onNewGroup::
            (# t: @Text;
            do '"%s%s" is newer than executable'->t.putFormat
               (# 
               do groupName[]->s; MPS.AST.astFileExtension->s;
               #);
               (t[],'Debug Info Error: Recompilation required')->alertInfo;
               true->continue;
            #);
          onUncheckedGroup::
            (#  t: @Text;
            do '"%s%s" has not been checked by compiler'->t.putFormat
               (# 
               do groupName[]->s; MPS.AST.astFileExtension->s;
               #);
               (t[],'Debug Info Error: Recompilation required')->alertInfo;
               false->allowChecking; true->continue;
            #);
          modtimeOk::
            (#
            do (if debuggee[]<>NONE then
                   (modtime<debuggee.executable.modtime)->value
                else true->value
               if)
            #);
       #);
     
     initialMAINsize: @point;
     
     editMode:: readOnlyMode;
     
     ymerBrowserWindow::<
       (# <<SLOT debuggerBrowserLib: attributes>> #);
     debuggee: ^processInterface;
     icons: @iconDB
       (# movIcon, stackIcon, codeIcon: ^bitmap;
          getIcon::
            (# newIcon:
                 (# fileName: ^Text;
                    icon: ^bitmap;
                 enter fileName[]
                 do
                    &bitmap[] -> icon[];
                    '$(BETALIB)/bitmap/objectbrowser/'->fileName.prepend;
                    fileName[]->expandEnvVar
                    (# defaultvalue::
                         (# do '/usr/local/lib/beta/'->envvarvalue[] #) 
                    #)->icon.readFromX11File;
                 exit icon[]
                 #);
            do (if TRUE
                //ObjectViewIcon -> name.equalNCS then
                   (if movIcon[]=NONE then
                       'ObjectViewIcon.xbm'->newIcon->movIcon[];
                   if);
                   movIcon[]->theIcon[];
                //StackViewIcon -> name.equalNCS then
                   (if stackIcon[]=NONE then
                       'StackViewIcon.xbm'->newIcon->stackIcon[];
                   if);
                   stackIcon[]->theIcon[];
                //CodeViewIcon -> name.equalNCS then
                   (if codeIcon[]=NONE then
                       'CodeViewIcon.xbm'->newIcon->codeIcon[];
                   if);
                   codeIcon[]->theIcon[];
               if);
            #);       
       #);
     
     globalOptions: @optionDB;
     getOption: globalOptions.getBooleanOption
       (# optvalue: @Boolean;
          found:: (# do value->optvalue #);
       exit optvalue
       #);
     setOption: globalOptions.setBooleanOption (# #);
     
     putinfo: 
       (# t: ^Text; 
       enter t[] 
       do t[]->main.status.info.vinfo.contents.label; 
          true->main.status.info.vinfo.contents.update;
       #);
     
     putpinfo: 
       (# t: ^Text; 
       enter t[] 
       do t[]->main.status.info.pinfo.contents.label; 
          true->main.status.info.pinfo.contents.update;
       #);
     
     alertInfo:
       (# t: ^Text; title: ^Text;
       enter (t[],title[])
       do 
          (main[],t[],title[])->alertUser;
       #);
     
     onMoveablesInsert:<
       (# theMoveable: ^main.moveable;
       enter theMoveable[]
       do INNER;
       #);
     
     onMoveablesDelete:<
       (# theMoveable: ^main.moveable;
       enter theMoveable[]
       do INNER;
       #);
     
     onTerminate:< Object;
     
     newDebuggee:< Object;
     debuggeeStatus:< BooleanValue;
     debuggeeRerun:< Object;
     rerunStatus:< BooleanValue;
     killDebuggee:< Object;
     killStatus:< BooleanValue;
     source:< Object;
     sourceStatus:< BooleanValue;
     quit:< (# do INNER; Terminate #);
     debuggeeContinue:< Object;
     continueStatus:< BooleanValue;
     debuggeeStepOver:< Object;
     stepOverStatus:< BooleanValue;
     debuggeeStepInto:< Object;
     stepIntoStatus:< BooleanValue;
     debuggeeStop:< Object;
     stopStatus:< BooleanValue;
     debuggeeActiveStack:< Object;
     activeStackStatus:< BooleanValue;
     debuggeeCurObj:< Object;
     curObjStatus:< BooleanValue;
     debuggeeCurCode:< Object;
     curCodeStatus:< BooleanValue;
     debuggeeCurComp:< Object;
     curCompStatus:< BooleanValue;
     editenv:< Object;
     editenvStatus:< BooleanValue;
     editcmdline:< Object;
     editcmdlineStatus:< BooleanValue;
       
     main: @mainType;
     mainType:< window
       (# <<SLOT valhallaMainLib:attributes>>;
          
          myCodemoveable: codemoveable
            (# contentsType::<
                 (# editorType::<
                      (# selectNode::<
                           (# t: ^text
                           do (if true
                               // (theEditorRoot.frag.root[]
                                    ->theEditorRoot.equal)
                                  and separate then
                                  node[]->newCodeView
                               // (theEditorRoot.frag.root[]
                                    ->theEditorRoot.equal)
                                  and not separate then
                                  node[]->newCodeView
                               // (not (theEditorRoot.frag.root[]
                                    ->theEditorRoot.equal)) and separate then
                                  node[]->newCodeView
                               else
                                  'myview.contents.editor.selectnode: theEditorRoot[]<> theEditorRoot.frag.root[] and not separate'
                                    ->putLine
                              if)  
                           #)
                      #)
                 #)
            #);

          
          eventHandler::<
            (# onAboutToClose::<
                 (# 
                 do quit;
                 #);               
            #);
          
          closeMoveables:
            (# sml: ^selectedMoveablesList;
            do getSelectedMoveablesList->sml[];
               (if sml[]<>NONE then
                   sml.scan (# do current.close #);
                   sml.clear;
               if);
            #);
          closeMoveablesStatus: BooleanValue
            (# sml: ^selectedMoveablesList;
            do getSelectedMoveablesList->sml[];
               (if sml[]<>NONE then
                   sml.size>0->value;
                else
                   FALSE->value
               if);
            #);
          
          refresh:
            (#
            do moveables.scan (# do current.doUpdate #);
               objworld.update 
            #);
          
          moveables: @moveableList
            (# onInsert:: 
                 (# 
                 do theMoveable[]->onMoveablesInsert;
                    theMoveable[]->mainMenubar.windowMenu.appendMoveable;
                 #);
               onDelete::
                 (# 
                 do theMoveable[]->onMoveablesDelete;
                    theMoveable[]->mainMenuBar.windowMenu.deleteMoveable;
                 #);
            #);
                    
          mainMenuBar: ^menuBarType;
          menuBarType::<
            (# fileMenu: @fileMenuType;
               fileMenuType:< simplemenu
                 (# inew: @item (# onSelect:: (# do newDebuggee #)#);
                    isource: @item (# onSelect:: (# do source #);
                                      onStatus:: (# do sourceStatus->enabled #)
                                   #);
                    
                    iquit: @item (# onSelect:: (# do quit #)#);
                    open::<
                      (# 
                      do INNER; 
                         ('New...')         -> inew.new;
                         ('Source Browser...')  -> isource.new;
                         ('Quit','q')       -> iquit.newkey;
                      #);
                 #);
               editMenu: @editMenuType;
               editMenuType:< simplemenu
                 (# ienv: @item
                      (# onSelect:: (# do editenv #);
                         onStatus:: (# do editenvStatus->enabled #);
                      #);
                    icmdline: @item
                      (# onSelect:: (# do editcmdline #);
                         onStatus:: (# do editcmdlineStatus->enabled #)
                      #);
                    irefresh: @item
                      (# onSelect:: (# do refresh #) #);
                    iclose: @item
                      (# onSelect:: (# do closeMoveables #);
                         onStatus:: (# do closeMoveablesStatus->enabled #);
                      #);
                    open::<
                      (# 
                      do 'Command Line...'->icmdline.new;
                         'Environment...'->ienv.new;
                         ('Refresh')   -> irefresh.new;
                         ('Close','w') -> iclose.newkey;
                         INNER;
                      #);
                    
                 #);
               windowMenu: @windowMenuType;
               windowMenuType:< simplemenu
                 (# icurobj: @item
                      (# onSelect:: (# do debuggeeCurObj #);
                         onStatus:: (# do curObjStatus->enabled #);
                      #);
                    icurcode: @item
                      (# onSelect:: (# do debuggeeCurCode #);
                         onStatus:: (# do curCodeStatus->enabled #);
                      #);
                    icurcomp: @item
                      (# onSelect:: (# do debuggeeCurComp #);
                         onStatus:: (# do curCompStatus->enabled #);
                      #);
                    iactivestack: @item
                      (# onSelect:: (# do debuggeeActiveStack #);
                         onStatus:: (# do activeStackStatus->enabled#)
                      #);
                    
                    open::<
                      (# 
                      do ('Current Code')    -> icurcode.new;
                         ('Current Object')  -> icurobj.new;
                         ('Current Component')   -> icurcomp.new;
                         ('Active Stack')    -> iactivestack.new;
                      #);
                    
                    appendMoveable:
                      (# theMoveable: ^moveable;
                      enter theMoveable[]
                      do 
                      #);
                    deleteMoveable:
                      (# theMoveable: ^moveable;
                      enter theMoveable[]
                      #);
                 #);
               optionsMenu: @optionsMenuType;
               optionsMenuType:< simplemenu
                 (# optionitem: toggleitem
                      (# option: ^Text; 
                         defaultvalue: @Boolean;
                         newopt: new 
                           (#
                           enter (option[],defaultvalue) 
                           do option[]->getOption
                              (# notfound:: 
                                   (#
                                   do (option[],defaultvalue->optvalue)->setOption;
                                   #)
                              #)->checked;
                           #);
                         onSelect:: (# do (option[],checked)->setOption #);
                         onStatus:: 
                           (# 
                           do option[]->getOption
                              (# notfound:: (# do defaultvalue->optvalue #)#)
                                ->checked;
                           #);
                      #);
                    
                    open::<
                      (# 
                      do scanoptions
                         (# 
                         do (description[],current[],default)
                              ->(&optionitem[]).newopt;
                         #);
                         scanvalhallaoptions
                         (# 
                         do (description[],current[],default)
                              ->(&optionitem[]).newopt;
                         #);
                         INNER;
                      #);
                 #);
               open::< 
                 (# 
                 do 'File'->fileMenu.new; fileMenu[]->append;
                    'Edit'->editMenu.new; editMenu[]->append;
                    INNER;
                    'Windows'->windowMenu.new; windowMenu[]->append;
                    'Preferences'->optionsMenu.new; optionsMenu[]->append;
                 #)
            #);
          
          maxButtonText: @Text;

          open::<
            (#
            do moveables[] -> objectPool.put;
               
               'Step Over'->maxButtonText;
                    
               (# tst: @pushButton
                    (# width: @integer;
                       open:: 
                         (# ts: ^textStyle;
                            buttonWidth: @integer
                         do hide;
                            maxButtonText[]->label;
                            style->ts[];
                            maxButtonText[]->ts.widthOfText->width
                         #)
                    #);
               do (600,400)->initialMAINsize;
                  tst.open; tst.close;
                  (8*(tst.width+9)+20+3,400)->initialMAINsize;
               #);
               
               initialMAINsize -> size; 'valhalla' -> title;
               contents->status.open; contents->objworld.open;
               INNER;
            #);
          
          objworld: @Canvas
            (# open::
                 (# sz: @Point; tmp: @Point;
                 do initialMAINsize->sz;
                    status.size->tmp;
                    sz.v-tmp.v->sz.v;
                    sz->size;
                    TRUE->bindLeft->bindRight->bindBottom->bindTop;
                    installSelectHandlers;
                 #);
            #);
          
          status: @Canvas
            (# setenabling:
                 (# 
                 do ccodeButton.setenabling; 
                    cobjButton.setenabling;
                    cstackButton.setenabling;
                    goButton.setenabling;
                    stepButton.setenabling;
                    overButton.setenabling;
                    stopButton.setenabling;
                    rerunButton.setenabling;
                 #);
               info: @pane
                 (# infoText: labelled
                      (# contentsType::< staticText
                           (# open::<
                                (#
                                do ''->label;
                                   INNER;
                                #);
                           #);
                         open::<
                           (# sz: @Point; 
                           do 
                              initialMAINsize->sz;
                              2*labelHeight+3->sz.v;
                              (sz.h-9) div 2->sz.h;
                              sz->size;
                              INNER;
                           #);
                      #);
                    pinfo: @infoText
                      (# contentsType::
                           (# open::
                                (# do 'No Process'->label #);
                           #);
                         open::
                           (# do 'Process Info'->label #);
                      #);
                    vinfo: @infoText
                      (# open::
                           (# do 'Valhalla Info'->label #);
                      #);
                    open::
                      (#
                      do FALSE->verticalStacking;
                         3->panewidth;
                         10->minsize;
                         pinfo.open;
                         vinfo.open;
                         pinfo[]->appendMember;
                         vinfo[]->appendMember; appendsDone;
                         (3,3)->position;
                      #);
                    dobind:
                      (# 
                      do TRUE->bindRight->bindLeft->bindTop;
                         FALSE->bindBottom;
                      #);
                 #);
               shortcutButton: pushButton
                 (# onSelect: ##Object;
                    enabled: ##BooleanValue;
                    setenabling:
                      (# 
                      do (if enabled then enable else disable if);
                      #);
                    open::
                      (# prev: ^shortcutButton;
                         fr,pfr: @rectangle;
                         sz: @Point;
                         h,v: @Integer;
                         name: ^Text;
                         ts: ^textStyle;
                         prevOffset:< IntegerValue;
                      enter (prev[],name[],onSelect##,enabled##)
                      do 
                         father.size->sz;
                         sz.v-3->fr.bottom;
                         (if prev[]=NONE then
                             3->fr.left;
                          else
                             prev.frame->pfr;
                             pfr.right+3+prevOffset->fr.left;
                         if);
                         style->ts[];
                         fr.left+(maxButtonText[]->ts.widthOfText)+6->fr.right;
                         fr.bottom-ts.lineHeight-6->fr.top;
                         fr->frame;
                         name[]->label;
                         TRUE->bindLeft->bindBottom; FALSE->bindRight->bindTop;
                         INNER;
                      #);
                    eventHandler::<
                      (# onMouseUp::
                           (# 
                           do (if enabled then THIS(shortcutButton).onSelect if);
                           #);
                      #);
                 #);
               ccodeButton, cobjButton, cstackButton: @shortcutButton;
               goButton,stepButton,overButton,stopButton,rerunButton: @shortcutButton;
               
               open::
                 (# pos,sz: @Point;
                 do 
                    TRUE->bindRight->bindLeft->bindBottom; FALSE->bindTop;
                    TRUE->border.visible;
                    borderstyles.shadowOut->border.style;
                    
                    info.open;
                   
                    (NONE,NONE,'Code',debuggeeCurCode##,curcodeStatus##)
                      ->ccodeButton.open;
                    (NONE,ccodeButton[],'Object',debuggeeCurObj##,curObjStatus##)
                      ->cobjButton.open;
                    (NONE,cobjButton[],'Stack',debuggeeActiveStack##,ActiveStackStatus##)
                      ->cstackButton.open;
                    (NONE,cstackButton[],'Go',debuggeeContinue##,continueStatus##)
                      ->goButton.open (# prevOffset:: (# do 20->value #)#);
                    
                    (NONE,goButton[],'Step',debuggeeStepInto##,StepIntoStatus##)
                      ->stepButton.open;
                    (NONE,stepButton[],'Step Over',debuggeeStepOver##,stepOverStatus##)
                      ->overButton.open;
                    (NONE,overButton[],'Stop',debuggeeStop##,stopStatus##)
                      ->stopButton.open;
                    (NONE,stopButton[],'Rerun',debuggeeRerun##,rerunStatus##)
                      ->rerunButton.open;
                    
                    ccodeButton.size->sz.add;
                    info.size->sz.add;
                    (0,9)->sz.add;
                    sz->size;
                    
                    initialMAINsize->pos;
                    size->pos.subtract;
                    0->pos.h;
                    pos->position;
                    
                    info.dobind;
                    
                    setenabling;
                 #);
            #);
       #);
     init::<
       (# 
       do globalOptions.init; globalOptions[]->objectPool.put;
          icons[]->objectPool.put;
          main.open; INNER; main.theMenuBar->main.mainMenuBar[];
          
          sources.open;
          &ymerCall(# getFragmentGroup:: 
                        (# 
                        do name[]
                             ->mps.fragmentGroupTable.getFragmentGroup
                             ->fg[]
                        #);
                      machineType:: 
                        (#  do THIS(valhallaGUI).machineType[]->t[] #);
                   #)[]->sources.edenv.ymerCallback[];
          sources.loadSettings;
       #);
  #);
