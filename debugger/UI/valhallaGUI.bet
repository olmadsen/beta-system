ORIGIN '~beta/guienv/v1.2/guienv';

INCLUDE '~beta/objectbrowser/v2.0/UI/objectview';
INCLUDE '~beta/objectbrowser/v2.0/UI/moveable';
INCLUDE '~beta/guienv/v1.2/stddialogs';

--- lib:attributes ---

initialMAINsize: (# exit (600,400) #);

valhallaGUI: guienv
  (# <<SLOT valhallaGUIlib:attributes>>;
     
     putinfo: 
       (# t: ^Text; 
       enter t[] 
       do t[]->main.info.label; true->main.info.update;
       #);
     
     alertInfo:
       (# t: ^Text; title: ^Text;
       enter (t[],title[])
       do 
          (main[],t[],title[])->alertUser;
       #);
     
     onQuit:< Object;
     onTerminate:< Object;
     
     onMoveablesInsert:<
       (# theMoveable: ^main.moveable;
       enter theMoveable[]
       do INNER;
       #);
     
     onMoveablesDelete:<
       (# theMoveable: ^main.moveable;
       enter theMoveable[]
       do INNER;
       #);
     
     main: @mainType;
     mainType:< window
       (# <<SLOT valhallaMainLib:attributes>>;
          
          moveables: @moveableList
            (# onInsert:: 
                 (# 
                 do theMoveable[]->onMoveablesInsert;
                    theMoveable[]->mainMenubar.windowMenu.appendMoveable;
                 #);
               onDelete::
                 (# 
                 do theMoveable[]->onMoveablesDelete;
                    theMoveable[]->mainMenuBar.windowMenu.deleteMoveable;
                 #);
            #);
          
          mainMenuBar: ^menuBarType;
          menuBarType::<
            (# fileMenu: @fileMenuType;
               fileMenuType:< menu
                 (# quitItem: @menuitem
                      (# open:: (# do 'Quit' -> name; 'Q' -> key #);
                         eventHandler:: 
                           (# onSelect:: (# do OnQuit; Terminate #)#)
                      #);
                    open::<
                      (# 
                      do 'File' -> name;
                         INNER;
                         quitItem.open; quitItem[] -> append;
                      #);
                 #);
               windowMenu: @windowMenuType;
               windowMenuType:< menu
                 (# appendMoveable:
                      (# theMoveable: ^moveable;
                      enter theMoveable[]
                      #);
                    deleteMoveable:
                      (# theMoveable: ^moveable;
                      enter theMoveable[]
                      #);
                    closeItem: @menuitem
                      (# open:: (# do 'Close' -> name; 'W' -> key #);
                         eventHandler:: 
                           (# onSelect:: 
                                (# 
                                do objworld.selectedmoveables.scan
                                   (#
                                   do current.close 
                                   #);
                                #);
                              onStatus:: 
                                (#
                                do (objworld.selectedmoveables.size<>0)->value 
                                #);
                           #)
                      #);
                    refreshItem: @menuitem
                      (# open:: (# do 'Refresh' -> name; 'R' -> key #);
                         eventHandler:: 
                           (# onSelect:: 
                                (# 
                                do moveables.scan
                                   (#
                                   do current.doUpdate;
                                   #);
                                   objworld.update;
                                #);
                           #);
                      #);
                    open::<
                      (# 
                      do 'Window' -> name;
                         closeItem.open; closeItem[]->append;
                         refreshItem.open; refreshItem[]->append;
                         INNER;
                      #);
                 #);
               open::< 
                 (# 
                 do fileMenu.open; fileMenu[]->append; 
                    windowMenu.open; windowMenu[]->append;
                    INNER 
                 #)
            #);
          
          open::
            (# 
            do moveables[] -> objectPool.put;
               initialMAINsize -> size; 'valhalla' -> title;
               contents->info.open; contents->objworld.open;
            #);
          
          objworld: @Canvas
            (# open::
                 (# sz: @Point; tmp: @Point;
                 do initialMAINsize->sz;
                    info.size->tmp;
                    sz.v-tmp.v->sz.v;
                    sz->size;
                    TRUE->bindLeft->bindRight->bindBottom->bindTop;
                    selectedMoveables.init;
                 #);
               eventHandler::
                 (# onMouseDown::
                      (# clr: @rectangle;
                      do 
                         (if TRUE
                          //(buttonState=1) and not doubleClick then
                             (if not shiftkey then
                                 selectedMoveables.deselect;
                             if);
                             localPosition->SimpleDefineRect->clr;
                             (if not clr.isEmpty then
                                 moveables.scan
                                 (#
                                 do (if clr->current.inRect then 
                                        (if not current.FrameSelected then
                                            current[]
                                              ->selectedMoveables.append;
                                            current.SelectFrame;
                                        if);
                                    if);
                                 #);
                             if);
                         if);
                      #);
                 #);
               selectedMoveables: @List
                 (# element:: moveable;
                    deselect:
                      (# 
                      do scan
                         (# 
                         do (if not current.wasClosed then
                                current.UnSelectFrame 
                            if);
                         #);
                         clear;
                      #);
                 #);
            #);
          
          info: @staticText
            (# open::<
                 (# sz: @Point; ts: ^TextStyle;
                    pos: @Point;
                 do 
                    style->ts[]; 
                    
                    InitialMAINsize->sz;
                    ts.lineHeight+6->sz.v;
                    sz->size;
                    
                    InitialMAINsize->pos;
                    0->pos.h;
                    pos.v-sz.v->pos.v;
                    pos -> Position;
                    
                    TRUE->bindLeft->bindRight->bindBottom; FALSE->bindTop;
                    
                    TRUE->border.visible;
                    borderStyles.shadowIn->border.style;
                 #);
            #);
       #);
     init:<
       (# 
       do main.open; INNER; main.theMenuBar->main.mainMenuBar[];
       #);
  #);
