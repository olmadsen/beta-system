ORIGIN '~beta/guienv/guienv';
LIB_DEF 'valhalenved' '../../lib';
(* Environment variable editor for valhalla. By JLK. *)

INCLUDE '~beta/containers/list'
        '~beta/guienv/scrolllists'
        '~beta/guienv/controls'
        '~beta/guienv/stddialogs';
INCLUDE '~beta/guienv/utils/group'
        '~beta/guienv/utils/private/groupbody';
INCLUDE '../environment';
BODY 'private/enveditorbody';
(*
 * COPYRIGHT
 *   Copyright (C) Aarhus University
 *   All rights reserved.
 *)

--- lib: attributes ---
envVar:
  (# name: ^text;
     oldValue: ^text;
     get:< (# value: ^text do INNER get exit value[] #);
     set:< (# value: ^text enter value[] do INNER set #);
     new:< (# value: ^text enter value[] do INNER new #);
     delete:< (# do INNER delete #);
     init:< (# enter name[] do get->oldValue[]; INNER init #)
  #);

--- windowLib:attributes ---
envGroup: group
  (# labelHeight:
       (# ts: ^textStyle;
       do private.theLabel.style->ts[];
       exit ts.lineHeight+3
       #);
     innerFrame:
       (# p: @Point; fr: @rectangle;
       do (3,labelHeight)->fr.topleft;
	  size->p; (3,3)->p.subtract; p->fr.bottomright;
       exit fr
       #);
  #);

--- guienvLib: attributes ---
envEditor: window
  (# onClose:<
       (* Called when this envEditor is closed by user. *)
       (#
       do INNER
       #);
     eventHandler::<
       (# onAboutToClose::< (# do THIS(envEditor).onClose; INNER #);
       #);
     edb: ^environDB;
     warning: @<<SLOT envEditorWarning:descriptor>>;
     myEnvVar: envVar
       (# get::<
	    (#
	    do l: edb.scan
		 (#
		 do (if curName[]->name.equal then
			curValue.copy->value[]; leave l
		    if)
		 #)
	    #);
	  set::<
	    (# do (name[],value[])->edb.add; warning #);
	  new::<
	    (# do (name[],value[])->edb.add; warning #);
	  delete::<
	    (# do name[]->edb.remove; warning #)
       #);

     height,labelWidth,editWidth,
     distance,buttonWidth,buttonDistance,scrollHeight,
     windowWidth,windowHeight: @integer;
     recalc:
       (#
       <<SLOT envEditorRecalc:dopart>>
       #);
     
     envVarScrollList: @<<SLOT envEditorScrollList:descriptor>>;

     envVarType:< myEnvVar;
     envVars: @list(# element::< (# e: ^envVarType enter e[] exit e[] #) #);
     envVarNameFrame: @envGroup
       (# envVarNameEdit: @edittext
	    (# name: ^text;
	       open::<
		 (#
		 do innerframe->frame;
		    true->bindright->bindBottom
		 #)
	    #);
	  eventHandler::<
	    (# onFatherFrameChanged::<
		 (#
		 do recalc;
		    (labelWidth,(height+8)*2)->size;
		    (distance,distance+scrollHeight+distance)->position;
		 #)
	    #);
	  open::<
	    (#
	    do (labelWidth,(height+8)*2)->size;
	       (distance,distance+scrollHeight+distance)->position;
	       'Variable Name'->label;
	       envVarNameEdit.open
	    #)
       #);
     envVarValueFrame: @envGroup
       (# envVarValueEdit: @editText
	    (# open::<
		 (#
		 do innerframe->frame;
		    true->bindright->bindBottom
		 #)
	    #);
	  eventHandler::<
	    (# onFatherFrameChanged::<
		 (#
		 do recalc;
		    (editWidth,(height+8)*2)->size;
		    (distance+labelWidth+distance,
		    distance+scrollHeight+distance)
		      ->position;
		 #)
	    #);
	  open::<
	    (#
	    do (editWidth,(height+8)*2)->size;
	       (distance+labelWidth+distance,
	       distance+scrollHeight+distance)
		 ->position;
	       'Variable Value'->label;
	       envVarValueEdit.open
	    #)
       #);
     clear: @<<SLOT enveditorClearButton:descriptor>>;

     delete: @<<SLOT envEditorDeleteButton:descriptor>>;
     
     set: @<<SLOT envEditorSetButton:descriptor>>;
     
     sep:@<<SLOT envEditorSep:descriptor>>;
     
     closePB:@<<SLOT envEditorCloseButton:descriptor>>;
     
     tst: @pushButton
       (# open::<
	    (# ts: ^textStyle; buttonLabel: (# exit 'Delete' #);
	    do buttonLabel->label;
	       style->ts[];
	       ((ButtonLabel->ts.widthOfText)+5,ts.lineHeight+3)
		 ->(buttonWidth,height);
	    #)
       #);
     open::<
       (#  t: ^text;
       <<SLOT envEditorOpendopart:doPart >>
       #);
     attach: (* attach a new envVar *)
       (# current: ^envVar;
	  newElm: ^envVars.element
       enter current[]
       do &envVars.element[]->newElm[];
	  current[]->newElm.e[];
	  newElm[]->envVars.append;
       #)
  #)
