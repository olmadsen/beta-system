ORIGIN '~beta/guienv/v1.6/guienv'; 

(* Environment variable editor for valhalla. By JLK. *)

INCLUDE '~beta/containers/v1.6/list'
        '~beta/guienv/v1.6/scrolllists'
        '~beta/guienv/v1.6/controls';
INCLUDE '~beta/guienv/v1.6/utils/group'
        '~beta/guienv/v1.6/utils/private/groupbody';

INCLUDE '../environment.bet';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- lib: attributes ---
envVar: 
  (# name: ^text;
     oldValue: ^text;
     get:< (# value: ^text do INNER get exit value[] #);
     set:< (# value: ^text enter value[] do INNER set #);
     new:< (# value: ^text enter value[] do INNER new #);
     delete:< (# do INNER delete #);
     init:< (# enter name[] do get->oldValue[]; INNER init #)
  #);

--- windowLib:attributes ---
envGroup: group
  (# labelHeight: 
       (# ts: ^textStyle;
       do private.theLabel.style->ts[];
       exit ts.lineHeight+3
       #);
     innerFrame:
       (# p: @Point; fr: @rectangle;
       do (3,labelHeight)->fr.topleft;
          size->p; (3,3)->p.subtract; p->fr.bottomright;
       exit fr
       #);
  #);

--- guienvLib: attributes ---
envEditor: window
  (# onClose:<
       (* Called when this envEditor is closed by user. *)
       (# 
       do INNER
       #);
     eventHandler::<
       (# onAboutToClose::< (# do THIS(envEditor).onClose; INNER #);
       #);
     edb: ^environDB;
     myEnvVar: envVar
       (# get::<
            (#
            do l: edb.scan
                 (#
                 do (if curName[]->name.equal then
                        curValue.copy->value[]; leave l
                    if)
                 #)
            #);
          set::<
            (# do (name[],value[])->edb.add #);
          new::<
            (# do (name[],value[])->edb.add #);
          delete::<
            (# do name[]->edb.remove #)
       #);
     height,labelWidth,editWidth,
     distance,buttonWidth,buttonDistance,scrollHeight,
     windowWidth,windowHeight: @integer;
     recalc:
       (#
       do this(envEditor).size->(windowWidth,windowHeight);
          (windowWidth-2*distance-3*buttonWidth)/2->buttonDistance;
          windowWidth-2*distance-labelWidth->editWidth;
          windowHeight-distance-distance-2*(height+8)-distance-height-distance
            ->scrollHeight;
       #);
     envVarScrollList: @textscrolllist
       (# eventHandler::
            (# onMouseUp::
                 (# i: @integer
                 do selection.first->i;
                    scan: envVars.scan
                      (#
                      do i-1->i; 
                         (if i=0 then
                             (* the selected envVar found !! *)
                             current.e.name[]->envVarNameFrame.envVarNameEdit.contents;
                             current.e.oldValue[]->envVarValueFrame.envVarValueEdit.contents;
                             leave scan
                         if)
                      #)
                 #);
               onFatherFrameChanged::
                 (#
                 do recalc;
                    (windowWidth-distance*2,scrollHeight)->size;
                    (distance,distance)->position;
                 #);
            #);
          open::<
            (#
            do true->singleSelection;
               (windowWidth-distance*2,scrollHeight)->size;
               (distance,distance)->position;
            #)
       #);
     envVarType:< myEnvVar;
     envVars: @list(# element::< (# e: ^envVarType enter e[] exit e[] #) #);
     envVarNameFrame: @envGroup
       (# envVarNameEdit: @edittext
            (# name: ^text;
               open::< 
                 (#
                 do innerframe->frame;
                    true->bindright->bindBottom
                 #)
            #);
          eventHandler::<
            (# onFatherFrameChanged::<
                 (#
                 do recalc;
                    (labelWidth,(height+8)*2)->size;
                    (distance,distance+scrollHeight+distance)->position;
                 #)
            #);
          open::< 
            (# 
            do (labelWidth,(height+8)*2)->size;
               (distance,distance+scrollHeight+distance)->position;
               'Variable Name'->label;
               envVarNameEdit.open
            #)
       #);
     envVarValueFrame: @envGroup
       (# envVarValueEdit: @editText
            (# open::<
                 (#
                 do innerframe->frame;
                    true->bindright->bindBottom
                 #)
            #);
          eventHandler::<
            (# onFatherFrameChanged::<
                 (#
                 do recalc;
                    (editWidth,(height+8)*2)->size;
                    (distance+labelWidth+distance,
                    distance+scrollHeight+distance)
                      ->position;
                 #)
            #);
          open::<
            (#
            do (editWidth,(height+8)*2)->size;
               (distance+labelWidth+distance,
               distance+scrollHeight+distance)
                 ->position;
               'Variable Value'->label;
               envVarValueEdit.open
            #)
       #);
     clear: @pushbutton
       (# eventHandler::< 
            (# onMouseUp::<
                 (# (* clear the edit fields and deselect in scroll-list *)
                 do ''->envVarNameFrame.envVarNameEdit.contents;
                    ''->envVarValueFrame.envVarValueEdit.contents;
                    envVarScrollList.selection.clear;
                 #);
               onFatherFrameChanged::<
                 (#
                 do recalc;
                    (buttonWidth,height)->size;
                    (distance,distance+scrollHeight+distance+(height+8)*2+distance)
                      -> position;
                 #)
            #);
          open::<
            (#
            do (buttonWidth,height)->size;
               (distance,distance+scrollHeight+distance+(height+8)*2+distance)
                 ->position;
               'Clear'->label
            #)
       #);
     delete: @pushbutton
       (# eventHandler::<
            (# onMouseUp::< 
                 (#
                 do l: (if (envVarNameFrame.envVarNameEdit.contents).length>0 then
                           envVars.iterate
                           (# i: @integer
                           do i+1->i;
                              (if envVarNameFrame.envVarNameEdit.contents->current.elm.e.name.equal then
                                  (* We have found the envVar name as
                                   * the name of one of the envVar's.
                                   * Let's delete this envVar:
                                   *)
                                  ''->envVarNameFrame.envVarNameEdit.contents;
                                  ''->envVarValueFrame.envVarValueEdit.contents;
                                  current.elm.e.delete;
                                  current[]->envVars.delete;
                                  (i,1)->envVarScrollList.delete;
                                  ((i,envVarScrollList.numberOfItems)->max,FALSE)
                                    ->envVarScrollList.selection.select;
                                  leave l
                              if)
                           #);
                           (* OK, we didn't find the envVar name in the
                            * list of envVar's.  We ignore the delete
                            *)
                           system.beep
                       if)
                 #);
               onFatherFrameChanged::<
                 (#
                 do recalc;
                    (buttonWidth,height)->size;
                    (distance+buttonWidth+buttonDistance,
                    distance+scrollHeight+distance+(height+8)*2+distance)
                      -> position;
                 #) 
            #);
          open::<
            (#
            do (buttonWidth,height)->size;
               (distance+buttonWidth+buttonDistance,
               distance+scrollHeight+distance+(height+8)*2+distance)
                 ->position;
               'Delete'->label
            #)
       #);
     set: @pushbutton
       (# eventHandler::< 
            (# onMouseUp::< 
                 (# t: ^text; newEnvVar: ^envVars.element
                 do l: (if (envVarNameFrame.envVarNameEdit.contents).length>0 then
                           envVars.scan
                           (# i: @integer 
                           do i+1->i;
                              (if envVarNameFrame.envVarNameEdit.contents->current.e.name.equal then
                                  (* We have found the envVar name as
                                   * the name of one of the envVar's.
                                   * Let's change its value:
                                   *)
                                  (if not (envVarValueFrame.envVarValueEdit.contents->current.e.oldValue.equal) then
                                      (* the value of the selected
                                       * envVar have been changed - we
                                       * update the value
                                       *)
                                      envVarNameFrame.envVarNameEdit.contents->t[]; '='->t.puttext;
                                      envVarValueFrame.envVarValueEdit.contents->t.append;
                                      (i,t[])->envVarScrollList.settext;
                                      envVarValueFrame.envVarValueEdit.contents->current.e.oldValue[]->current.e.set;
                                      (i,false)->envVarScrollList.selection.select;
                                      envVarScrollList.selection.scrollIntoView;
                                  if);
                                  leave l
                              if)
                           #);
                           (* OK, we didn't find the envVar name in the
                            * list of envVar's.  We define a new envVar
                            * with the given value - and inserts the new
                            * envVar in the scroll-list etc.
                            *)
                           &envVars.element[]->newEnvVar[];
                           &envVarType[]->newEnvVar.e[];
                           envVarNameFrame.envVarNameEdit.contents
                             ->newEnvVar.e.name[];
                           envVarValueFrame.envVarValueEdit.contents
                             ->newEnvVar.e.oldValue[];
                           1->envVarScrollList.append;
                           newEnvVar.e.name.copy->t[];
                           '='->t.puttext;
                           newEnvVar.e.oldValue[]->t.append;
                           (envVarScrollList.numberOfItems,t[])
                             ->envVarScrollList.settext;
                           envVarValueFrame.envVarValueEdit.contents
                             ->newEnvVar.e.new;
                           (envVarScrollList.numberOfItems,false)
                             ->envVarScrollList.selection.select;
                           envVarScrollList.selection.scrollIntoView;
                           newEnvVar[]->envVars.append;
                       if)
                 #);
               onFatherFrameChanged::<
                 (#
                 do recalc;
                    (buttonWidth,height)->size;
                    (distance+buttonWidth*2+buttonDistance*2,
                    distance+scrollHeight+distance+(height+8)*2+distance)
                      -> position;
                 #)
            #);
          open::<
            (#
            do (buttonWidth,height)->size;
               (distance+buttonWidth*2+buttonDistance*2,
               distance+scrollHeight+distance+(height+8)*2+distance)
                 -> position;
               'Set'->label
            #);
       #);
     tst: @pushButton
       (# open::<
            (# ts: ^textStyle; buttonLabel: (# exit 'Delete' #);
            do buttonLabel->label;
               style->ts[];
               ((ButtonLabel->ts.widthOfText)+5,ts.lineHeight+3)
                 ->(buttonWidth,height);
            #)
       #);
     open::< 
       (#  t: ^text;
       do tst.open; tst.close; (* just to get the width and height of fonts *)
          300->scrollHeight;
          200->labelWidth; 400->editWidth; 2->distance;
          distance+labelWidth+distance+editWidth+distance->windowWidth;
          (windowWidth-2*distance-3*buttonWidth)/2->buttonDistance;
          distance+scrollHeight+distance+(height+8)*2+distance+height+distance
            ->windowHeight;
          (windowWidth,windowHeight)->size;
          
          envVarScrollList.open;
          envVarNameFrame.open; envVarValueFrame.open;

          clear.open; delete.open; set.open;
          (windowWidth,windowHeight)->size;
          
          getEnvironDB->edb[];
          (# ev: ^myEnvVar;
          do edb.scan
             (#
             do &myEnvVar[]->ev[];
                curname.copy->ev.name[];
                ev[]->attach;
             #);
          #);
          
          envVars.scan
          (# (* create the elements in the scroll-list *)
          do current.e.init;
             1->envVarScrollList.append;
             current.e.name.copy->t[]; '='->t.puttext;
             current.e.oldValue[]->t.append;
             (envVarScrollList.numberOfItems,t[])->envVarScrollList.settext;
          #);
          (1,FALSE)->envVarScrollList.selection.select;
          'Environment Variable Editor'->title;
       #);
     attach: (* attach a new envVar *)
       (# current: ^envVar;
          newElm: ^envVars.element
       enter current[]
       do &envVars.element[]->newElm[];
          current[]->newElm.e[];
          newElm[]->envVars.append;
       #)
  #)
