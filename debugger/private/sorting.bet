ORIGIN '~beta/basiclib/v1.5/betaenv';
--LIB: attributes--
qsort: 
  (# swap:< 
       (# i,j: @Integer;
       enter (i,j)
       do INNER
       #);
     doSwap: @swap;
     less:<
       (# i,j: @Integer;
          value: @Boolean;
       enter (i,j)
       do INNER
       exit value
       #);
     isLess: @less;
     sort:
       (# left, right, last: @Integer;
       enter (left, right)
       do L: (if left >= right then (* stop if rep. contains *) 
                 leave L;           (* fewer than two elements *)
              else
                 (left, (left+right)/2) -> swap;
                 left->last;
                 (* partition *)
                 (for i: right-left repeat
                      (if (i+left,left)->isLess then
                          last+1->last;
                          (last,i+left) -> doSwap;
                      if);
                 for);
                 (left,last) -> doSwap; (* restore partition element *)
                 (left,last) -> sort;
                 (last+1,right) -> sort;
             if);
       #);
  enter sort
  #)
