ORIGIN '../valhalla';
-- debuggeeCurObj: DoPart --
do
   (if curObjStatus then
       (debuggee.currentRemoteBetaObject,FALSE)
         ->gui.main.OpenMoveableObjectView
           (#
              moveableObjectViewType:: gui.main.moveableObjectViewEvaluator
                (#
                   openEvaluatorMenuEntry::< 
                     (#  do 'DynamicCompilation'->getOption->value #);
                   onEvaluateButton::< 
                     (#
                        originFGname,evaluatorCode,dynamicFGname: ^text;
                        aRemoteBetaObject: ^debuggee.remoteBetaObject;
                        FF: ^MPS.AST.fragmentForm;
                        FG,originFG: ^MPS.AST.fragmentGroup;
                        im: ^imagePair;
                        error: @boolean;
                        constructUniqueID:
                          (#
                             T: @Text;
                             timeSec,timeMiliSec: @integer;
                             timeInMiliSec: @real
                          do
                             preciseTime->(timeSec,timeMiliSec);
                             timeSec->timeInMiliSec;
                             timeInMiliSec*1000000->timeInMiliSec;
                             timeInMiliSec+timeMiliSec->timeInMiliSec;
                             timeInMiliSec->t.putReal (#  do 0->precision #)
                          exit t.copy
                          #);
                        aPTI: ^debuggee.prototypeInfo;
                        anOD: ^MPS.BETACFL.objectDescriptor;
                        anIndex: @MPS.AST.index;
                        dataAreaStart,codeAreaStart: @integer;
                        allocateMemory:
                          (#
                             allocationFailed:< exception (#  do INNER #);
                             IMG: ^IM.loadableImage;
                             anImage: ^image;
                             numBytes,allocatedAreaStart: @integer
                          enter IMG[]
                          do IMG.im[] -> anImage[];
                             (if trace then
                                 'LIP: ' -> puttext;
                                 anImage.LIP -> puthex;
                             if);
                             (if IMG.isCode then
                                 anImage.LIP -> numBytes
                              else
                                 (if trace then
                                     ' GOTnoOfBytes: ' -> puttext;
                                     IM.GOTnoOfBytes -> puthex;
                                 if);
                                 anImage.LIP + IM.GOTnoOfBytes -> numBytes
                             if);
                             (if trace then newline if);
                             (if anImage.buffer_ptr <> 0 then
                                 'Normal externally allocated data buffer'
                                   ->putLine;
                                 'Buffer size: %i\n'
                                   ->putFormat
                                     (#  do anImage.buffer_size->i #);
                                 'Still needs to be implemented'->putLine;
                                 stop
                              else
                                 numBytes
                                   ->
                                     debuggee.processComm.processData.
                                       allocateMemory->allocatedAreaStart;
                                 (if allocatedAreaStart = 0 then
                                     allocationFailed
                                 if)
                             if)
                          exit allocatedAreaStart
                          #);
                        strucAddress,objectAddress: @integer;
                        trace: @boolean;
                        dumpMemoryArea:
                          (#
                             address,length: @integer;
                             memoryContents: @integer;
                             adr,con: @text
                          enter (address,length)
                          do
                             'Address\t\tContents'->putLine;
                             (for g: length repeat
                               address+(g-1)*4
                                 ->adr.putHex
                                   (#
                                      format::< 
                                        (# 
                                        do true->zeroPadding; true->uppercase
                                        #)
                                   #);
                               '0x'->adr.prepend;
                               address+(g-1)*4
                                 ->debuggee.processComm.processData.peekLong
                                 ->con.putHex
                                   (#
                                      format::< 
                                        (# 
                                        do true->zeroPadding; true->uppercase
                                        #)
                                   #);
                               '0x'->con.prepend;
                               '%s\t%s\n'
                                 ->putFormat (#  do adr[]->s; con[]->s #);
                               adr.clear;
                               con.clear
                             for)
                          #);
                        originObjectAddress,dynamicProtoAddress,
                          originObjectProtoAddress: @integer;
                        nameOfDynamicPattern: @text;
                        theCexternalEntry: ^MPS.BETACFL.ObjectDenotation;
                        theAttributes: ^MPS.BETACFL.attributes
                     do
                        initCompiler;
                        debuggee.currentRemoteBetaObject->aRemoteBetaObject[];
                        aRemoteBetaObject.protoAdr
                          ->debuggee.utilities.protoAddressToPrototypeInfo
                          ->aPTI[];
                        aPTI.ofi.groupName[]->originFGname[];
                        originFGname[]->MPS.BETACFL.openBETAfragmentGroup
                          ->originFG[];
                        aPTI.objdesc[]->anOD[];
                        (anOD.index,anOD.frag[])->anIndex;
                        contents.evaluator.editor.contents.contents
                          ->evaluatorCode[];
                        'dynamicPattern'->nameOfDynamicPattern.append;
                        dynamicCodeCounter->nameOfDynamicPattern.putInt;
                        ': external '->evaluatorCode.prepend;
                        nameOfDynamicPattern[]->evaluatorCode.prepend;
                        'dynamicCode'->MPS.BETACFL.makeBETAfragmentForm->FF[];
                        (MPS.BETACFL.ObjectDenotation,'cExternalEntry',FF[])
                          ->MPS.BETACFL.parseText->theCexternalEntry[];
                        (MPS.BETA.attributes,evaluatorCode[],FF[])
                          ->MPS.BETACFL.parseText
                            (#
                               parseError::< 
                                 (#  do 'Parse errors'->putLine #)
                            #)->theAttributes[];
                        MPS.BETACFL.doPart
                          ->theAttributes.suffixWalkForProd
                            (#
                               theImperatives: ^MPS.BETACFL.imperatives;
                               aDoPart: ^MPS.BETACFL.doPart
                            do
                               current[]->aDoPart[];
                               aDoPart.getImperatives->theImperatives[];
                               (1,theCexternalEntry[])->theImperatives.insert
                            #);
                        constructUniqueID->dynamicFGname[];
                        dynamicFGname[]->MPS.BETACFL.openBETAfragmentGroup
                          ->FG[];
                        FF.name->FG.fragmentList.deleteLocalName;
                        FF[]->FG.fragmentList.addFragment;
                        (if (FG.prop[] <> none ) then
                            'ORIGIN'
                              ->FG.prop.addProp
                                (#  do originFGname[]->addString #);
                            'doneCheck'
                              ->FG.prop.addProp (#  do 0->addConst #);
                            
                         else
                            'Prop[] is none'->putLine; stop
                        if);
                        FG.markAsChanged;
                        L:
                        (FG.name,anIndex[],originFG[])
                          ->dc.activateCompiler
                            (#
                               semanticErrorsNotification::< 
                                 (# 
                                 do
                                    true->error;
                                    'Semantic errors'->putLine;
                                    leave L
                                 #)
                            #)->error;
                        (if not error then
                            true->dc.getImagePair->IM[];
                            (if false then dc.imageList.scan (#  #) if);
                            (if 'DebugDynamic'->getOption then
                                true->trace;
                                true->IM.trace;
                                newLine;
                                newLine;
                                'ALLOCATION:'->putLine
                            if);
                            
                            (* Collect external symbols for building possible
                             * Global Offset Table (GOT) in data segment.
                             * This is currently only needed for SGI
                             *)
                            DC.NT[] -> IM.xCollectExternals;
                                 
                            allocation:
                              (# 
                              do
                                 IM.xData[]
                                   ->allocateMemory
                                     (#
                                        allocationFailed::< 
                                          (# 
                                          do
                                             'data: Allocation failed'
                                               ->msg.append
                                          #)
                                     #)->dataAreaStart;
                                 IM.xCode[]
                                   ->allocateMemory
                                     (#
                                        allocationFailed::< 
                                          (# 
                                          do
                                             'code: Allocation failed'
                                               ->msg.append
                                          #)
                                     #)->codeAreaStart
                              #);
                            (if 'DebugDynamic'->getOption then
                                'Allocation Succeed'->putLine;
                                'Datasegment buffer: %s\tCode-segment buffer: %s\n\n'
                                  ->putFormat
                                    (# st: @text
                                    do
                                       dataAreaStart
                                         ->st.putHex
                                           (#
                                              format::< 
                                                (# 
                                                do
                                                   true->zeroPadding;
                                                   true->uppercase
                                                #)
                                           #);
                                       '0x'->st.prepend;
                                       st[]->s;
                                       st.clear;
                                       codeAreaStart
                                         ->st.putHex
                                           (#
                                              format::< 
                                                (# 
                                                do
                                                   true->zeroPadding;
                                                   true->uppercase
                                                #)
                                           #);
                                       '0x'->st.prepend;
                                       st[]->s
                                    #);
                                newLine;
                                newLine;
                                'RELOCATION:'->putLine
                            if);
                            relocation:
                              (# 
                              do (dataAreaStart,codeAreaStart)->IM.xAllocImage;
                                 dc.NT[]->IM.xCollectEntries;
                                 dc.NT[]->IM.xRelocate
                              #);
                            (if 'DebugDynamic'->getOption then
                                'Relocation Succeed'->putLine;
                                newLine;
                                newLine;
                                'WRITING RELOCATED SEGMENTS TO DEBUGGEE:'
                                  ->putLine
                            if);
                            writeToDebuggee:
                              (# 
                              do
                                 IM.xTransferImage
                                   (# 
                                   do
                                      (outProcessAdr,item)
                                        ->
                                          debuggee.processComm.processData.
                                            pokeLong
                                          (#
                                             accessFailure::< 
                                               (# 
                                               do
                                                  'writeToDebuggee: Failed to move segments'
                                                    ->msg.append
                                               #)
                                          #)
                                   #)
                              #);
                            (if 'DebugDynamic'->getOption then
                                'writeToDebuggee Succeed'->putLine;
                                newLine;
                                newLine;
                                'ADDING GROUP TO DEBUGGEE'->putLine
                            if);
                            addingGroup:
                              (# 
                              do
                                 IM.dataStart
                                   ->debuggee.processComm.processData.addGroup
                                     (#
                                        accessFailure::< 
                                          (# 
                                          do 'AddGroup failed'->msg.append
                                          #)
                                     #)
                              #);
                            (if 'DebugDynamic'->getOption then
                                'addingGroup Succeed'->putLine;
                                'Adding header located at: %s\n\n'
                                  ->putFormat
                                    (# st: @text
                                    do
                                       IM.dataStart
                                         ->st.putHex
                                           (#
                                              format::< 
                                                (# 
                                                do
                                                   true->zeroPadding;
                                                   true->uppercase
                                                #)
                                           #);
                                       '0x'->st.prepend;
                                       st[]->s
                                    #);
                                newLine;
                                newLine;
                                'CONSTRUCTING STRUC IN DEBUGGEE'->putLine
                            if);
                            constructStrucInDebuggee:
                              (# 
                              do
                                 24
                                   ->
                                     debuggee.processComm.processData.
                                       allocateMemory
                                     (#
                                        accessFailure::< 
                                          (# 
                                          do
                                             'constrcutStrucInDebuggee: Allocating room for struc object failed'
                                               ->msg.append
                                          #)
                                     #)->strucAddress;
                                 (strucAddress,StructurePTValue)
                                   ->debuggee.processComm.processData.pokeLong
                                     (#
                                        accessFailure::< 
                                          (# 
                                          do
                                             'constrcutStrucInDebuggee: Setting type of prototype failed'
                                               ->msg.append
                                          #)
                                     #);
                                 (strucAddress+4,0)
                                   ->debuggee.processComm.processData.pokeLong
                                     (#
                                        accessFailure::< 
                                          (# 
                                          do
                                             'constrcutStrucInDebuggee: Setting ?? failed'
                                               ->msg.append
                                          #)
                                     #);
                                 (if aRemoteBetaObject.isComp then
                                     aRemoteBetaObject.getAddress+24
                                       ->objectAddress
                                  else
                                     aRemoteBetaObject.getAddress->objectAddress
                                 if);
                                 (strucAddress+8,objectAddress)
                                   ->debuggee.processComm.processData.pokeLong
                                     (#
                                        accessFailure::< 
                                          (# 
                                          do
                                             'constrcutStrucInDebuggee: Setting origin failed'
                                               ->msg.append
                                          #)
                                     #);
                                 (strucAddress+12,(1->IM.indexedOff))
                                   ->debuggee.processComm.processData.pokeLong
                                     (#
                                        accessFailure::< 
                                          (# 
                                          do
                                             'constrcutStrucInDebuggee: Setting address of object prototype in struc failed'
                                               ->msg.append
                                          #)
                                     #)
                              #);
                            (if 'DebugDynamic'->getOption then
                                'STRUC OBJECT: ' -> putline;
                                (strucAddress,4) -> dumpMemoryArea;
                                
                                '\nORIGIN OBJECT:'->putLine;
                                strucAddress + 8
                                  -> debuggee.processComm.processData.peeklong
                                (# accessFailure::
                                     (# 
                                     do 'Peek struc[origin] failed'->msg.append
                                     #)
                                #) -> originObjectAddress;
                                (originObjectAddress,8)->dumpMemoryArea;

                                '\nORIGIN PROTOTYPE:'->putLine;
                                originObjectAddress
                                  -> debuggee.processComm.processData. peeklong
                                (# accessFailure::
                                     (# 
                                     do 'Peek struc[strucOrigin] failed'->msg.append
                                     #)
                                #) -> originObjectProtoAddress;
                                
                                (originObjectProtoAddress,8)-> dumpMemoryArea;

                                '\nDYNAMIC PROTOTYPE:'->putLine;
                                strucAddress+12
                                  -> debuggee.processComm.processData.peeklong
                                (# accessFailure::
                                     (# 
                                     do 'Peek struc[proto] failed'->msg.append
                                #)#)
                                  -> dynamicProtoAddress;
                                (dynamicProtoAddress,8) -> dumpMemoryArea;
                                
                                (if IM.GOTstart <> 0 then
                                    '\nGLOBAL OFFSET TABLE:' -> putline;
                                    (IM.GOTstart, IM.GOTnoOfBytes div 4)
                                    -> dumpMemoryArea
                                if);
                                
                                '\nDYNAMIC CODE SEGMENT:'->putline;
                                (codeAreaStart,IM.xCode.IM.LIP div 4)
                                  -> dumpMemoryArea;
                                '\nDYNAMIC DATA SEGMENT:'->putline;
                                (dataAreaStart,IM.xData.IM.LIP div 4)
                                  -> dumpMemoryArea;
                                
                                '\nEXECUTE OBJECT'->putLine
                            if);
                            executeObjectInDebuggee:
                              (# 
                              do
                                 strucAddress
                                   ->
                                     debuggee.processComm.processData.
                                       executeObject
                                     (#
                                        accessFailure::< 
                                          (# 
                                          do
                                             'executeObjectInDebuggee: failed'
                                               ->msg.append
                                          #)
                                     #)
                              #);
                            (*
                             *  Dumping the inf in the struc
                             *)
                            (if 'DebugDynamic'->getOption then
                                'executeObjectInDebuggee Succeed'->putLine;
                                'Executing struc located at: %s'
                                  ->putFormat
                                    (# st: @text
                                    do
                                       strucAddress
                                         ->st.putHex
                                           (#
                                              format::< 
                                                (# 
                                                do
                                                   true->zeroPadding;
                                                   true->uppercase
                                                #)
                                           #);
                                       st[]->s
                                    #)
                            if)
                         else
                            'An error happend during compilation'->putLine
                        if);
                        
                     #);
                   contentsType::< 
                     (#
                        evaluatorType::< 
                          (#
                             open::< 
                               (#
                                  T: @styledText;
                                  track: @trackMouse;
                                  cursorPos: @integer
                               do
                                  '(#\ndo \n#)'->T.putText;
                                  T[]->evaluator.editor.contents.contents;
                                  evaluator.editor.contents.all
                                    ->evaluator.editor.contents.scanText
                                      (# 
                                      do
                                         (if ch = 'd' then
                                             start+3->cursorPos
                                         if)
                                      #);
                                  (cursorPos,cursorPos)
                                    ->evaluator.editor.contents.selection;
                                  evaluator.editor[]->main.target
                               #)
                          #)
                     #)
                #);
              getFather::  (#  do gui.main.objworld[]->father[] #)
           #)
   if);
     

