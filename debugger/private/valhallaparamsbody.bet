ORIGIN '../valhallaparams';
INCLUDE '../environment'
        '~beta/toollibs/utils/options'
        '~beta/basiclib/formatio'
        '~beta/sysutils/envstring';
LIB_ITEM 'valhallaparams';
(*
 * COPYRIGHT
 *   Copyright (C) Aarhus University
 *   All rights reserved.
 *)
-- parseValhallaParams: Descriptor --
(#
   environ: ^environDB;
   valhallaParams: ^valhallaParamDB;
   execParams: ^executableParamDB;
   prefs: ^preferences;
   name,val1,val2,prefsArgumentsText: ^Text;
   nextParam: @|
     (# value: ^Text; valhallaopts: ^Text; i: @Integer; 
     do
        '$(VALHALLAOPTS)'
          ->expandEnvVar
            (# defaultValue::<  (#  do none ->envvarvalue[] #) #)
          ->valhallaopts[];
        (if valhallaopts[] <> none then
            valhallaopts.reset;
            loop:
              (# 
              do
                 valhallaopts.getAtom->value[];
                 (if value.length = 0 then leave loop if);
                 SUSPEND;
                 restart loop;
                 
              #)
        if);
        (if prefs[] <> none then
            &text[]->prefsArgumentsText[];
            prefs.gettext (#  do 'ValhallaArguments'->name[] #)
              ->prefsArgumentsText;
            loop:
              (# 
              do
                 valhallaopts.getAtom->value[];
                 (if value.length = 0 then leave loop if);
                 SUSPEND;
                 restart loop;
                 
              #)
        if);
        2->i;
        loop:
        (if (i <= NoOfArguments) then
            i->arguments->value[]; SUSPEND; i+1->i; restart loop; 
        if);
        none ->value[];
        loop: (#  do SUSPEND; restart loop #);
        
     exit value[]
     #);
   gotExecName: @Boolean;
   
do
   getEnvironDB->environ[];
   getValhallaParamDB->valhallaParams[];
   getExecutableParamDB->execParams[];
   getObjectPrefs->prefs[];
   loop:
   (if (nextParam->name[]) <> none then
       (if true
        // ('-PID'->name.equalNCS) then
           (if (nextParam->val1[]) <> none then
               ('-PID',val1[])->valhallaParams.add;
               (* Check format of -PID command line parameter: *)
               val1.reset;
               val1.getInt
                 (#
                    syntaxError:: 
                      (# 
                      do
                         'Wrong format of -PID command line parameter (not an integer): '
                           ->msg.putText;
                         val1[]->msg.putline;
                         
                      #);
                    
                 #)
            else
               'Error in commandline argument: %s\n\tusage: %s <processId>\n'
                 ->putformat (#  do name[]->s; name[]->s;  #);
               
           if);
           
        // ('-PORT'->name.equalNCS) then
           (if (nextParam->val1[]) <> none then
               ('-PORT',val1[])->valhallaParams.add;
               (* Check format of -PORT command line parameter: *)
               val1.reset;
               val1.getInt
                 (#
                    syntaxError:: 
                      (# 
                      do
                         'Wrong format of -PORT command line parameter (not an integer): '
                           ->msg.putText;
                         val1[]->msg.putline;
                         
                      #);
                    
                 #)
            else
               'Error in commandline argument: %s\n\tusage: %s <processId>\n'
                 ->putformat (#  do name[]->s; name[]->s;  #);
               
           if);
           
        // ('-ENVIRONMENT'->name.equalNCS) // ('-ENV'->name.equalNCS) then
           nextParam->val1[];
           nextParam->val2[];
           (if val2[] <> none then
               (val1[],val2[])->environ.add; 
            else
               'Error in commandline argument: %s\n\tusage: %s <envName> <envValue>\n'
                 ->putFormat (#  do name[]->s; name[]->s;  #);
               
           if);
           
        // ('-COMMLINE'->name.equalNCS) // ('-CL'->name.equalNCS) then
           (if (nextParam->val1[]) <> none then
               val1[]->execParams.append; 
            else
               'Error in commandline argument: %s\n\tusage: %s <Command line parameter>\n'
                 ->putFormat (#  do name[]->s; name[]->s;  #);
               
           if);
           
        // ('-EXECNAME'->name.equalNCS) // ('-EXEC'->name.equalNCS) then
           (if (nextParam->val1[]) <> none then
               ('-EXECNAME',val1[]->trimExecName)->valhallaParams.add;
               TRUE->gotExecName;
               
            else
               'Error in commandline argument: %s\n\tusage: %s <executableName>\n'
                 ->putFormat (#  do name[]->s; name[]->s #);
               
           if);
           
        // ('-BOOLEANOPTION'->name.equalNCS) // ('-BOPT'->name.equalNCS) then
           nextParam->val1[];
           nextParam->val2[];
           (if val2[] <> none then
               (if true
                // ('TRUE'->val2.equalNCS) then
                   TRUE->prefs.getBoolean (#  do val1[]->name[] #); 
                // 'FALSE'->val2.equalNCS then
                   FALSE->prefs.getBoolean (#  do val1[]->name[] #); 
                else
                   'Param error: %s in "%s %s %s" should be TRUE or FALSE\n'
                     ->putFormat
                       (# 
                       do val2[]->s; name[]->s; val1[]->s; val2[]->s; 
                       #)
               if);
               
            else
               'Error in commandline argument: %s\n\tusage: %s <optionName> [TRUE|FALSE]\n'
                 ->putFormat (#  do name[]->s; name[]->s;  #);
               
           if);
           
        else
           (if not gotExecName then
               ('-EXECNAME',name[]->trimExecName)->valhallaParams.add;
               (* read command line paramerers from the rest of the line: *)
               l:
               (if (nextParam->val1[]) <> none then
                   val1[]->execParams.append; restart l
               if)
           if);
           
       if);
       restart loop;
       
   if);
   
#)  

