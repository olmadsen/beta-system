ORIGIN '../valhallaparams';
INCLUDE '../environment';


--- parseValhallaParams:descriptor ---
(# environ: ^environDB;
   valhallaParams: ^valhallaParamDB;
   execParams: ^executableParamDB;
   
   i: @Integer; name,value: ^Text;
do 
   getEnvironDB->environ[];
   getValhallaParamDB->valhallaParams[];
   getExecutableParamDB->execParams[];
   
   2->i;
   loop:
     (if (i <= NoOfArguments) then
         i -> Arguments -> name[];
         (if true
          // ('-PID'->name.equalNCS) then
             (if (i<NoOfArguments) then
                 i+1->Arguments->value[]; 
                 ('-PID',value[])->valhallaParams.add;
                 (* Check format of -PID command line parameter: *)
                 value.reset; 
                 value.getInt
                 (# syntaxError::
                      (# 
                      do 'Wrong format of -PID command line parameter'
                           -> msg.putText;
                      #);
                 #) 
             if);
             i+2->i;
          // ('-ENVIRONMENT'->name.equalNCS) 
          // ('-ENV' -> name.equalNCS)  then
             (if (i+1<NoOfArguments) then
                 i+1->Arguments->name[];
                 i+2->Arguments->value[];
                 (name[],value[])->environ.add;
             if);
             i+3->i;
          // ('-EXECNAME'->name.equalNCS) 
          // ('-EXEC'->name.equalNCS) then
             (if (i<NoOfArguments) then
                 i+1->Arguments->value[];
                 ('-EXECNAME',value[])->valhallaParams.add;
             if);
             i+2->i;
          else
             'Unknown command line parameter: ' -> putText;
             name[] -> putLine;
             i+1 -> i;
         if);
         restart loop;
     if);
#)
