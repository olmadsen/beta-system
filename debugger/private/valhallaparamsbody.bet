ORIGIN '../valhallaparams';
INCLUDE '../environment';
INCLUDE '~beta/objectbrowser/v2.0.1/options';
INCLUDE '~beta/basiclib/v1.4/formatio';
INCLUDE '~beta/sysutils/v1.4/envstring';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- parseValhallaParams:descriptor ---
(# environ: ^environDB;
   valhallaParams: ^valhallaParamDB;
   execParams: ^executableParamDB;
   options: ^optionDB;
   
  name,val1,val2: ^Text;
   
   nextParam: @|
     (# value: ^Text;
        valhallaopts: ^Text;
        i: @Integer; 
     do 
        '$(VALHALLAOPTS)' -> expandEnvVar
          (# defaultValue::< (# do NONE -> envvarvalue[] #)
          #) -> valhallaopts[];
        
        (if valhallaopts[]<>NONE then
            valhallaopts.reset;
            loop:
              (# 
              do valhallaopts.getAtom->value[];
                 (if value.length=0 then leave loop if);
                 suspend;
                 restart loop;
              #)
        if);
        
        2->i;
        loop:
          (if (i<=NoOfArguments) then
              i->arguments->value[];
              suspend;
              i+1->i;
              restart loop;
          if);
        
        NONE->value[];
        loop: (# do suspend; restart loop #);
     exit value[]
     #);
   
   gotExecName: @Boolean;
   
do 
   getEnvironDB->environ[];
   getValhallaParamDB->valhallaParams[];
   getExecutableParamDB->execParams[];
   getOptionDB->options[];
   
   loop:
     (if (nextParam->name[])<>NONE then
         (if true
          // ('-PID'->name.equalNCS) then
             (if (nextParam->val1[])<>NONE then
                 ('-PID',val1[])->valhallaParams.add;
                 (* Check format of -PID command line parameter: *)
                 val1.reset; 
                 val1.getInt
                 (# syntaxError::
                      (# 
                      do 'Wrong format of -PID command line parameter'
                           -> msg.putText;
                      #);
                 #)
              else
                 '%s usage: %s <processId>'->putformat
                 (# 
                 do name[]->s; name[]->s;
                 #);
             if);
          //('-PORT'->name.equalNCS) then
             (if (nextParam->val1[])<>NONE then
                 ('-PORT',val1[])->valhallaParams.add;
                 (* Check format of -PORT command line parameter: *)
                 val1.reset; 
                 val1.getInt
                 (# syntaxError::
                      (# 
                      do 'Wrong format of -PORT command line parameter'
                           -> msg.putText;
                      #);
                 #)
              else
                 '%s usage: %s <processId>'->putformat
                 (# 
                 do name[]->s; name[]->s;
                 #);
             if);             
          // ('-ENVIRONMENT'->name.equalNCS) 
          // ('-ENV' -> name.equalNCS)  then
             nextParam->val1[]; nextParam->val2[];
             (if val2[]<>NONE then
                 (val1[],val2[])->environ.add;
              else
                 '%s usage: %s <envName> <envValue>'->putFormat
                 (# 
                 do name[]->s; name[]->s;
                 #);
             if);
          //('-COMMLINE'->name.equalNCS)
          //('-CL'->name.equalNCS) then
             (if (nextParam->val1[])<> NONE then
                 val1[]->execParams.append;
              else
                 '%d usage: %s <Command line parameter>'->putFormat
                 (# 
                 do name[]->s; name[]->s;
                 #);
             if);
          // ('-EXECNAME'->name.equalNCS) 
          // ('-EXEC'->name.equalNCS) then
             (if (nextParam->val1[])<>NONE then
                 ('-EXECNAME',val1[])->valhallaParams.add;
                 TRUE->gotExecName;
              else
                 '%s usage: %s <executableName>'->putFormat
                 (# 
                 do name[]->s; name[]->s
                 #);
             if);
          //('-BOOLEANOPTION'->name.equalNCS)
          //('-BOPT'->name.equalNCS) then
             nextParam->val1[]; nextParam->val2[];
             (if val2[]<>NONE then
                 (if true
                  //('TRUE'->val2.equalNCS) then
                     (val1[],TRUE)->options.setBooleanOption;
                  //'FALSE'->val2.equalNCS then
                     (val1[],FALSE)->options.setBooleanOption;
                  else
                     'Param error: %s in "%s %s %s" should be TRUE or FALSE\n'
                       ->putFormat
                     (# 
                     do val2[]->s; name[]->s; val1[]->s; val2[]->s;
                     #)
                 if);
              else
                 '%s usage: %s <optionName> [TRUE|FALSE]'->putFormat
                 (# 
                 do name[]->s; name[]->s;
                 #);
             if);
          else
             (if not gotExecName then
                 ('-EXECNAME',name[])->valhallaParams.add;
                 (* read command line paramerers from the rest of the line: *)
                 l: (if (nextParam->val1[])<> NONE then
                     val1[]->execParams.append;
                     restart l
                 if)
             if);
         if);
         restart loop;
     if);
#)
