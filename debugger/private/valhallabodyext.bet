ORIGIN '../valhalla';
INCLUDE '../breakpointOperations';
-- valhallatrybreakonfirst:descriptor ---
(# p:@gui.breakpointcontroller
     (# getFocus::
          (# i: @integer
          do lasteditor.sifviewer.cs->(node[],i,i,i)
          #);
     #);
   node:^astinterface.ast;
   e:^astinterface.expanded;
   i:@integer;
   t:@text;
do
   p.init;
   ('Trying to set breakPoint in first statement!','Warning Debuggee not started!')->gui.alertinfo;
 
   lasteditor.sifviewer.cs->(node[],i,i,i);
   (*   
    * 'Kind: '->puttext;node.kind->putint;newline;
    * 'Symbol '->puttext;node.symbol->putint;newline;
    *)
   l:(if node[]<>none then 
         (if node.hascomment then (* find descriptor *)
             node[]->e[];
             e.getson1->node[];
             (if node[]=none then leave l; if);
         if);
         (if (node[],FALSE,debuggee[])->p.setOneShotBreakpoint then
             gui.debuggeeContinue;
             true->breaksetSucceded;
             True->continue;
         if);
     if);
#)


--- valhalladoFinish:descriptor ---
(#  
   p:@gui.breakpointcontroller
     (# getFocus::
          (# i: @integer
          do lasteditor.sifviewer.cs->(node[],i,i,i)
          #);
     #);
   node:^astinterface.ast;
   dummy:@integer;
   cs:^componentstack;
   comp:^processInterface.remoteBetaObject;
   returnadr:@integer;
   impi:^debuggee.ImperativeInfo;
do (* getFocus->node[];  *)
   p.init;
   lasteditor.sifviewer.cs->(node[],dummy,dummy,dummy);
   debuggee.currentRemoteComponent->comp[];  (* this component *)
   comp.getAddress->debuggee.getStack->cs[];  (* stack of this component *)
   
   cs.returnAdrs[2]->returnadr;  (* adrs[1] is this object, 2 is calling address *)
   (if returnadr=0 then 
       ('Trying to Finish descriptor!','Warning Could not find return address!')->gui.alertinfo;
   if);
 
   returnadr->debuggee.utilities.codeAddressToImperative
   (# notBetaCode::
        (# 
        do ('Warning return address! not in Beta-code\nAllowing debuggee to continue;','Trying to Finish descriptor!')->gui.alertinfo;
           doContinue;
        #);
   #) ->impi[];  (* imp corresp. to addr *)
   (if impi[]=none then leave doFinish; if); (* error occured *)
   impi.cast[]->node[];   (* ast corresp. to addr *)
   
   (if (node[],FALSE,debuggee[])->p.setOneShotBreakpoint then
       doContinue; (* go *)
   if);
#)
