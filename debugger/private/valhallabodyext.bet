ORIGIN '../valhallaapplication';
INCLUDE '../breakpointOperations';
-- valhallatrybreakonfirst: Descriptor --
(#
   p: @ (*FGC, gui.*) breakpointcontroller
     (#
        getFocus:: 
          (# i: @integer do lasteditor.sifviewer.cs->(node[],i,i,i) #);
        
     #);
   node: ^astinterface.ast;
   e: ^astinterface.expanded;
   i: @integer;
   t: @text;
   
do
   p.init;
   ('Trying to set breakPoint in first statement!',
    'Warning Debuggee not started!')-> (*FGC, gui.*) alertinfo;
   lasteditor.sifviewer.cs->(node[],i,i,i);
   (*   
    * 'Kind: '->puttext;node.kind->putint;newline;
    * 'Symbol '->puttext;node.symbol->putint;newline;
    *)
   l:
   (if node[] <> none then
       (if node.hascomment then (* find descriptor *)
           node[]->e[];
           e.getson1->node[];
           (if node[] = none then leave l;  if);
           
       if);
       (if (node[],FALSE,debuggee[])->p.setOneShotBreakpoint then (*FGC, gui.*)
           debuggeeContinue; true->breaksetSucceded; True->continue; 
       if);
       
   if);
   
#)  

-- valhalladoFinish: Descriptor --
(#
   p: @ (*gui.*) breakpointcontroller
     (#
        getFocus:: 
          (# i: @integer do lasteditor.sifviewer.cs->(node[],i,i,i) #);
        
     #);
   node: ^astinterface.ast;
   dummy: @integer;
   cs: ^componentstack;
   comp: ^processInterface.remoteBetaObject;
   returnadr: @integer;
   impi: ^debuggee.ImperativeInfo;
   
do (* getFocus->node[];  *)
   p.init;
   lasteditor.sifviewer.cs->(node[],dummy,dummy,dummy);
   debuggee.currentRemoteComponent->comp[];
   (* this component *)
   comp.getAddress->debuggee.getStack->cs[];
   (* stack of this component *)
   cs.returnAdrs[2]->returnadr;
   (* adrs[1] is this object, 2 is calling address *)
   (if returnadr = 0 then
       ('Trying to Finish descriptor!','Warning Could not find return address!')
         -> (*FGC, gui.*) alertinfo;
       
   if);
   returnadr
     ->debuggee.utilities.codeAddressToImperative
       (#
          notBetaCode:: 
            (# 
            do
               ('Warning return address! not in Beta-code\nAllowing debuggee to continue;',
                'Trying to Finish descriptor!')-> (*gui.*) alertinfo;
               doContinue;
               
            #);
          
       #)->impi[];
   (* imp corresp. to addr *)
   (if impi[] = none then leave doFinish;  if);
   (* error occured *)
   impi.cast[]->node[];
   (* ast corresp. to addr *)
   (if (node[],FALSE,debuggee[])->p.setOneShotBreakpoint then
       doContinue; (* go *) 
   if);
   
#)  

-- ShowObjectReferencesDP:doPart --
do
   (# 
      t:@gui.window
        (# scrlist:@textscrolllist
             (# 
                open::
                  (# 
                  do (200,200)->size;
                     true->bindright;
                     true->bindbottom
                  #);
                eventhandler::
                  (# onSelect::
                       (# bo:^debuggee.remoteBetaObject;
                       do (if doubleClick then
                              item->gettext->putline;
                              (item->h.get).adr->debuggee.newRemoteObject->bo[];
                              bo[]->newObjectMoveable
                          if)
                       #)
                  #)
             #);
           open::
             (# t:@text;
             do scrlist.open;
                'References to '->t.append;
                rb.description->t.append;
                (200,200)->size;
             #);
        #);
      adr:@integer;
   do
      'Address is %d\n'->putformat(# do rb.address->d#);
      rb.getAddress->adr->putint;newline;
      rb.description->putline;
      adr->debuggee.processcomm.getRefsToObject->h[];
      'object has %d external references\n'
        ->putformat (#  do h.size->d #);
      
      t.open;
      h.size->t.scrlist.append;
      
      h.scan
      (# bo:^debuggee.remotebetaobject;
         i:@integer;
         tt:^text
      do current.adr->debuggee.newRemoteObject->bo[];
         i+1->i;
         &text[]->tt[];
         'Address:%8X, description:%s'->tt.putformat
         (# do current.adr->uX; bo.description->s #);
         (i,tt[])->t.scrlist.settext;
      #);
   #);    
   
--valhalladoGoUntil:descriptor--
(# 
    p: @ (*gui.*) breakpointcontroller
     (#
        getFocus:: 
          (# i: @integer do lasteditor.sifviewer.cs->(node[],i,i,i) #);
        
     #);
   node: ^astinterface.ast;
   dummy: @integer;
do p.init;
   lasteditor.sifviewer.cs->(node[],dummy,dummy,dummy);   
   (if (node[],FALSE,debuggee[])->p.setOneShotBreakpoint then
       doContinue; (* go *) 
   if);
#)
