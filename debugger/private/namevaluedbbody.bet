ORIGIN '../namevaluedb';
INCLUDE '~beta/containers/v1.6/list';
INCLUDE '~beta/sysutils/v1.6/cstring';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)

--- NameValueDBinit:dopart ---
do nvprivate.init; INNER;
   
--- namevalueDBadd:dopart ---
do 
   doscan:
     (# new: ^nvprivate.nvlist.element;
     do nvprivate.nvlist.scan
        (# 
        do (if current.name[]->name.equal then
               value[]->current.value[];
               leave doscan;
           if);
        #);
        (* Not found *);
        &nvprivate.nvlist.element[]->new[];
        name[]->new.name[];
        value[]->new.value[];
        new[]->nvprivate.nvlist.append;
     #);
   
--- namevalueDBremove:dopart ---
do doscan: nvprivate.nvlist.iterate
     (# 
     do (if current.elm.name[]->name.equal then
            current[]->nvprivate.nvlist.delete;
            leave doscan;
        if);
     #);
   
--- namevalueDBscan:dopart ---
do nvprivate.nvlist.scan
   (# 
   do current.name[]->curname[]; current.value[]->curvalue[];
      INNER scan;
   #);
   
--- NameValuDBsize:dopart ---
do nvprivate.nvlist.size->value;
   
--- namevalueDBprivate:descriptor ---
(# nvlist: @list (# element:: (# name, value: ^Text #)#);
   initialized: @Boolean;
   
   init:
     (#
     do (if initialized then nvlist.clear;
         else nvlist.init; true->initialized;
        if);
     #);
#)
   
