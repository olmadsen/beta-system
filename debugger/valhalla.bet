ORIGIN '~beta/basiclib/v1.4/basicsystemenv';
BODY '~beta/guienv/v1.2/private/X11/guienvxsystemenvbody';

INCLUDE 'UI/valhallaGUI';
INCLUDE 'processInterface';
INCLUDE 'processCommCodes';
INCLUDE 'UI/GUIprocess';
INCLUDE 'UI/stackview';
INCLUDE 'UI/objectviewadds';
INCLUDE 'valhallaoptionlist';
INCLUDE 'valhallaiconlist';

INCLUDE '~beta/objectbrowser/v2.0/options';
INCLUDE '~beta/objectbrowser/v2.0/optionlist';
INCLUDE '~beta/objectbrowser/v2.0/UI/icons';
INCLUDE '~beta/objectbrowser/v2.0/UI/iconlist';

INCLUDE '~beta/guienv/v1.2/stddialogs';

INCLUDE '~beta/sysutils/v1.4/envstring';

(*BETARUN default '~beta/debugger/v2.0/betarun/$/betarun.debug';*)

OBJFILE default '~beta/debugger/v2.0/betarun/$/C/valhallaFIFOS.o';

--- program:descriptor ---
systemenv
(# windowEnvType:: valhallaGUI;
   setWindowEnv:: (# do gui[]->theWindowEnv[] #);
   
   debuggee: ^gui.GUIprocess;
   
   debuggeeIsRunning:
     (# value: @Boolean
     do (if debuggee[]<>NONE then debuggee.running->value 
         else FALSE->value
        if);
     exit value
     #);
   debuggeeIsReady: 
     (# value: @Boolean
     do (if debuggee[]<>NONE then not debuggee.running->value 
         else FALSE->value
        if);
     exit value
     #);
   
   remoteObjects: @remoteObjectDB;
   globalOptions: @optionDB;
   getOption: globalOptions.getBooleanOption
     (# optvalue: @Boolean;
        found:: (# do value->optvalue #);
        notfound: (# do FALSE->optvalue #);
     exit optvalue
     #);
   setOption: globalOptions.setBooleanOption (# #);
   
   icons: @gui.iconDB
     (# movIcon: ^bitmap;
        stackIcon: ^bitmap;
        getIcon::
          (# newIcon:
               (# fileName: ^Text;
                  icon: ^bitmap;
               enter fileName[]
               do
                  &bitmap[] -> icon[];
                  '$(BETALIB)/bitmap/'->fileName.prepend;
                  fileName[]->expandEnvVar
                  (# defaultvalue::
                       (# do '/usr/local/lib/beta/'->envvarvalue[] #) 
                  #)->icon.readFromX11File;
               exit icon[]
               #);
          do (if TRUE
              //ObjectViewIcon -> name.equalNCS then
                 (if movIcon[]//NONE then
                     'ObjectViewIcon.xbm'->newIcon->movIcon[];
                 if);
                 movIcon[]->theIcon[];
              //StackViewIcon -> name.equalNCS then
                 (if stackIcon[]//NONE then
                     'StackViewIcon.xbm'->newIcon->stackIcon[];
                 if);
                 stackIcon[]->theIcon[];
             if);
          #);       
     #);
   
   newExecName:
     (# execName: ^Text;
     do 
        gui.main[]->gui.fileSelectionDialog->execName[];
        (if execName[]<>NONE then
            execName[]->setEXECNAMEparam;
        if);
     exit execName[]
     #);
   
   newDebuggee:
     (# success: @Boolean;
        rerun: @Boolean;
     enter rerun
     do
        TRUE->success;
        
        (if debuggee[]<>NONE then
            (if not debuggee.terminated then debuggee.kill if);
            (if not rerun then NONE->debuggee[] if);
        if);
        
        (if getEXECNAMEparam=NONE then
            (if newExecName=NONE then leave newDebuggee if);
        if);
        
        rerun and (debuggee[]<>NONE) -> rerun;
        
        doinit:
          (if not rerun then 
              &gui.GUIprocess[]->debuggee[];
        
              remoteObjects[]->debuggee.init
              (# doingExpensiveRead:: 
                   (# t: @Text;
                   do 'Creating debug info cache (%s)' -> t.putFormat
                      (# 
                      do dbName[]->s;
                      #);
                      t[]->gui.putinfo;
                   #);
                 doingCheapRead:: 
                   (# t: @Text;
                   do 'Reading debug info cache (%s)' -> t.putFormat
                      (# 
                      do dbName[]->s;
                      #);
                      t[]->gui.putinfo;
                   #);
                 createFragInfo:: TrueObject;
                 creationFailed::
                   (# t: @Text;
                   do 'Failed to create debug info cache (%s)'->t.putFormat
                      (# 
                      do dbName[]->s
                      #);
                      t[]->gui.putinfo; TRUE->continue;
                   #);
                 execNotFound::
                   (# t: @Text;
                   do 'Executable "%s" not found'->t.putFormat
                      (# 
                      do getEXECNAMEparam->s
                      #);
                      t[]->gui.putinfo; FALSE->success; leave doinit;
                   #);
                 forkFailed::
                   (# t: @Text;
                   do 'Could not fork "%s"'->t.putFormat
                      (# 
                      do getEXECNAMEparam->s
                      #);
                      t[]->gui.putinfo; FALSE->success; leave doinit;
                   #);
              #);
           else
              debuggee.rerun
              (# execNotFound::
                   (# t: @Text;
                   do 'Executable "%s" not found'->t.putFormat
                      (# 
                      do execName[]->s
                      #);
                      t[]->gui.putinfo; FALSE->success; leave doinit;
                   #);
                 forkFailed::
                   (# t: @Text;
                   do 'Could not fork "%s"'->t.putFormat
                      (# 
                      do getEXECNAMEparam->s
                      #);
                      t[]->gui.putinfo; FALSE->success; leave doinit;
                   #);
                 modtimeChanged::
                   (# s: @|system
                        (#  t:@Text;
                        do 'Executable "%s" changed. \n Rereading debug info'
                             ->t.putFormat
                           (# 
                           do execName[]->s;
                           #);
                           (gui.main[],t[],'Rereading...')->gui.alertUser;
                        #);
                   do s[]->fork; pause; FALSE->rerun; restart doinit;
                   #);
              #)
          if);
        
        (if not success then
            NONE->debuggee[];
         else
            (# t: @Text;
            do 'Ready to debug %s (PID=%d)'->t.putFormat
               (# 
               do getEXECNAMEparam->s; getPIDparam->d;
               #);
               t[]->gui.putinfo; 
            #)
        if);
     exit success
     #);
   
   procconn:
     (# bo: ^remoteBetaObject;
     do
        debuggee.processComm.continue;
        'Debuggee is running'->gui.putinfo;
        debuggee.processComm.wait;
        
        (if debuggee[]<>NONE then
            'Debuggee stopped'->gui.putinfo;
            (debuggee.curObj,debuggee.rd[])->NewRemoteObject->bo[];
            
            (if not bo.isNone then
                (bo[],FALSE)->gui.main.OpenMoveableObjectView
                (# getFather:: (# do gui.main.objworld[]->father[]#)#);
            if);
         else
            'Debuggee terminated'->gui.putinfo;
        if);
     #);
   
   newstackview:
     (# comp: ^remoteBetaObject;
     enter comp[]
     do findStack: gui.stackViewList.scan
          (# end::
               (# 
               do (gui.main.objworld[],comp[],debuggee[])
                    ->(&gui.main.stackview[]).open;
               #);
          do (if current.component[]->comp.equal then
                 current.wriggle;
                 leave findStack;
             if);
          #); 
     #);
   
   gui: @valhallaGUI
     (# onQuit::
          (# 
          do (if debuggeeIsReady then debuggee.kill; onTerminate if);
          #);
        onTerminate::
          (# 
          do 0->setPIDparam;
             main.moveables.scan (# do current.close #)
          #);
        
        onMoveablesInsert::
          (# mov: ^main.moveableObjectView;
             addmovItems: mov.addObjectViewItems
               (# show:: (# do comp[]->newstackview #)#);
          do (if true
              //(theMoveable## <= main.stackview##) then
                 theMoveable[]->stackViewList.append;
              //(theMoveable## <= main.moveableObjectView##) then
                 theMoveable[]->mov[];
                 addmovItems##->mov.onmovMenuOpen##;
             if);
          #);
        
        stackViewList: @list (# element:: main.stackview #);
        
        onMoveablesDelete::
          (# 
          do (if theMoveable## <= main.stackview## then
                 theMoveable[]->stackViewList.at->stackViewList.delete;
             if);
          #);
        
        mainType::
          (# menuBarType::
               (# fileMenuType::
                    (#
                       newItem: @menuItem
                         (# open:: (# do 'New'->name; 'N'->key #);
                            eventHandler::
                              (# onSelect::
                                   (#
                                   do (if enabled then
                                          (if debuggee[]<>NONE then
                                              debuggee.kill; onTerminate;
                                          if);
                                          (if newExecName<>NONE then
                                              newDebuggee;
                                          if);
                                      if);
                                   #);
                                 onStatus:: (# do enabled->value #);
                                 enabled: 
                                   (# 
                                   exit debuggeeIsReady or (debuggee[]=NONE)
                                   #);
                              #);
                         #);
                       
                       rerunItem: @menuItem
                         (# open:: (# do 'Rerun'->name; 'R'->key; #);
                            eventHandler::
                              (# onSelect::
                                   (#
                                   do (if enabled then
                                          (if debuggee[]<>NONE then
                                              debuggee.kill; onTerminate;
                                          if);
                                          TRUE->newDebuggee;
                                      if);
                                   #);
                                 onStatus:: (# do enabled->value #);
                                 enabled: 
                                   (# 
                                   exit (debuggeeIsReady or (debuggee[]=NONE))
                                      and (getEXECNAMEparam<>NONE)
                                   #);
                              #);
                         #);
                       
                       killItem: @menuItem
                         (# open:: (# do 'Kill'->name; 'K'->key; #);
                            eventHandler::
                              (# onSelect::
                                   (#
                                   do (if enabled then
                                          debuggee.kill; onTerminate;
                                      if);
                                   #);
                                 onStatus:: (# do enabled->value #);
                                 enabled: (# exit debuggeeIsReady #);
                              #);
                         #);
                       
                       continueItem: @menuItem
                         (# open:: (# do 'Continue'->name; 'C'->key; #);
                            eventHandler::
                              (# onSelect::
                                   (# 
                                   do (if debuggeeIsReady then procconn if);
                                   #);
                                 onStatus:: (# do debuggeeIsReady->value #);
                              #)
                         #);
                       
                       showStackItem: @menuItem
                         (# open:: (# do 'Show Active Stack'->name #);
                            eventHandler::
                              (# onSelect::
                                   (# 
                                   do (if debuggeeIsReady then
                                          (debuggee.curComp,debuggee.rd[])
                                            ->newRemoteObject->newstackview
                                      if);
                                   #);
                                 onStatus:: (# do debuggeeIsReady->value #);
                              #)
                         #);
                       
                       open::
                         (# 
                         do newItem.open; newItem[]->append;
                            rerunItem.open; rerunItem[]->append;
                            killItem.open; killItem[]->append;
                            continueItem.open; continueItem[]->append;
                            showStackItem.open; showStackItem[]->append;
                         #);
                    #);
                  optionsMenu: @Menu
                    (# optionItem: menuItem
                         (# optionName: ^Text;
                            itemName: ^Text;
                            optvalue: @Boolean;
                            open::<
                              (# 
                              do INNER; (* Set optionName and itemName *)
                                 itemName[]->name;
                                 optionName[]->getOption->optvalue->checked;
                              #);
                            eventHandler::
                              (# onSelect:: 
                                   (# 
                                   do not optvalue->optvalue->checked;
                                      (optionName[],optvalue)->setOption;
                                   #);
                              #);
                         #);
                       
                       charRepOnAlineItem: @optionItem
                         (# open:: 
                              (#
                              do 'One-line char repetitions'->itemName[];
                                 charRepOnALine->optionName[];
                              #);
                         #);
                       invisibleOriginsItem: @optionItem
                         (# open:: 
                              (# 
                              do 'Invisible Origins'->itemName[];
                                 invisibleOrigins->optionName[]
                              #);
                         #);
                       shortObjectTitlesItem: @optionItem
                         (# open::
                              (# 
                              do 'Short Object Types'->itemName[];
                                 useShortObjectTitles->optionName[]
                              #);
                         #);
                       shortCodeItem: @optionItem
                         (# open::
                              (# 
                              do 'Short Code Descriptions'->itemName[];
                                 useShortCodeNames->optionName[];
                              #);
                         #);
                       numberObjectsItem: @optionItem
                         (# open::
                              (# 
                              do 'Number Objects'->itemName[];
                                 numberObjects->optionName[]
                              #);
                         #);
                       readLabelsItem: @optionItem
                         (# open::
                              (# 
                              do 'Read labels on startup'->itemName[];
                                 readLabelsOnStartUp->optionName[];
                              #);
                         #);
                       open::
                         (# 
                         do  'Preferences'->name;
                            charRepOnALineItem.open; 
                            charRepOnALineItem[]->append;
                            invisibleOriginsItem.open; 
                            invisibleOriginsItem[]->append;
                            shortObjectTitlesItem.open; 
                            shortObjectTitlesItem[]->append;
                            shortCodeItem.open;
                            shortCodeItem[]->append;
                            numberObjectsItem.open;
                            numberObjectsItem[]->append;
                            readLabelsItem.open;
                            readLabelsItem[]->append;
                         #);
                    #);
                  open::<
                    (#
                    do optionsMenu.open; optionsMenu[]->append;
                    #);
               #);
          #);
        init::
          (#
          do stackViewList.init;
          #);
     #);
do remoteObjects.init; remoteObjects[]->objectPool.put;
   globalOptions[]->objectPool.put;

   (charRepOnALine,TRUE) -> globalOptions.setBooleanOption;
   (invisibleOrigins,TRUE) -> globalOptions.setBooleanOption;
   (useShortObjectTitles,TRUE)->globalOptions.setBooleanOption;
   (useShortCodeNames,TRUE)->globalOptions.setBooleanOption;
   (numberObjects,TRUE)->globalOptions.setBooleanOption;
   (readLabelsOnStartUp,FALSE)->globalOptions.setBooleanOption;
   icons[]->objectPool.put;
   
   parseValhallaParams;
   gui.init;
   
   (if getEXECNAMEparam<>NONE then newDebuggee if);
#)

