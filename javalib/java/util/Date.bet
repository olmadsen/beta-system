ORIGIN '~beta/basiclib/betaenv'; 
INCLUDE '../lang/String';
BUILD jvm 'jvm/beta/DateHelper.class' 'DateHelper.java' 'javac -classpath jvm -d jvm $1';

--LIB: attributes--

(* Java Date class.  See
 * http://java.sun.com/j2se/1.4.1/docs/api/java/util/Date.html*)
Date: ExternalClass
  (* Deprecated methods not included *)
  (#   
     (* Constructors - currently not possible to overload *)
     _init:
       (# time: @int64
       enter time
       #);  
     
     after: proc
       (* Tests if this date is after the specified date. *)
       (# when: ^Date;
          result: @boolean;
       enter when[]
       exit result
       #);
     before: proc
       (* Tests if this date is before the specified date. *)
       (# when: ^Date;
          result: @boolean;
       enter when[]
       exit result
       #);
     clone: proc
       (* Return a copy of this object. *)
       (# result: ^Object;
       exit result[]
       #);
     compareTo_Date: proc
       (* Compares two Dates for ordering. *)
       (# anotherDate: ^Date;
          result: @int32;
       enter anotherDate[]
       do 'compareTo' -> procname
       exit result
       #);
     compareTo_Object: proc
       (* Compares this Date to another Object. *)
       (# o: ^Object;
          result: @int32;
       enter o[]
       do 'compareTo' -> procname
       exit result
       #);
     equals: proc
       (* Compares two dates for equality. *)
       (# obj: ^Object;
          result: @boolean;
       enter obj[]
       exit result
       #);
     
     getTime: proc
       (* Returns the number of milliseconds since January 1,
        * 1970, 00:00:00 GMT represented by this Date object.
        *)
       (# result: @int64 (*long*);
       exit result
       #);
     
     hashCode: proc
       (* Returns a hash code value for this object. *)
       (# result: @int32;
       exit result
       #);
     
     setTime: proc
       (* Sets this Date object to represent a point in time that
        * is time milliseconds after January 1, 1970 00:00:00
        *)
       (# time: @int64 (*long*);
       enter time
       #);
     
     toString: proc
       (* Converts this Date object to a String *)
       (# result: ^String;
       exit result[]
       #);
  do 'java/util/Date' -> classname;
  #);

(* Static methods *)

(* BETA Helpers *)

DateNow: external
  (* Returns Date object corresponding to current time *)
  (# D: ^Date;
  do 'static beta/DateHelper/DateNow()Ljava/util/Date;' -> callC
  exit D[]
  #);
