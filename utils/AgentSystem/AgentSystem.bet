ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/colorTable';
INCLUDE '~beta/containers/sets';
INCLUDE 'WorldWindow';
---lib:attributes---
Point:
  (# x,y: @integer;
     print:
       (#
       do 'Point('->puttext; x -> putint; ','->put; y ->putint; ')'->put
       #)
  enter(x,y)
  exit(x,y)
  #);
random:
  (# V,R: @integer
  enter V
  do 111 mod V -> R
  exit R
  #);
World: guienv
  (# 
     BasicAgent:
       (#
       do inner
       exit  this(BasicAgent)[]
       #);
     Agent: BasicAgent
       (# pos: @Point;
          c: @color;
          setColor:
            (# sc: @color; 
               T: @theWorldW.tCanvas
                 (# open::
                      (#
                      do (patch_size,patch_size) -> size 
                 #)#)
            enter sc
            do sc -> c; 
               (none,pos.x,pos.y,sc) -> T.open;
            #);
          bred: ^BredSet
       do inner
       #);
     Turtle: Agent
       (# who: @ integer;
          
          size: @integer;
          pen: @
            (# down: (# do  true -> isDown #);
               up: (# do  false -> isDown #);
               isDown: @boolean;
               draw:
                 (# l: @theWorldW.tLine; s1,s2: @integer;
                    out:
                      (# x,y: @integer
                      enter(x,y)
                      do '(' -> put; x -> putint; ','->put; y->putint; ')'->put
                      #)
                 enter(s1,s2)
                 do (pos.x,pos.y)->out; 
                    (pos.x + s1 * 4, pos.y + s2 *4) -> out;
                    newline;
                    (none,pos.x,pos.y,pos.x + s1 * 4, pos.y + s2 *4,c) -> l.open
                 #)
            #);
          fd:
            (# s: @integer
            enter s
            do (s,s) -> pen.draw;
               pos.x + s * 4 -> pos.x;
               pos.y + s * 4 -> pos.y;
            #);
          rd:
            (# d: @integer
            enter d
            do (d,0) -> pen.draw; pos.x + d * 4 -> pos.x
            #);
          ld:
            (# d: @integer
            enter d
            do (0,d) -> pen.draw; pos.y + d * 4 -> pos.y
            #);
          print:
            (# 
            do 'Agent:[' -> puttext; who -> putint;  ']'->put; pos.print; 
            #);
       enter who
       do (who * 20, who) -> pos;
          inner
       #);
     Patch: Agent
       (# size: @integer;
          setSize: (# N: @integer enter n do n -> size #);
          c: @Color
       #);
     patch_size: @integer;
     setPatch_size: 
       (# n: @integer 
       enter n 
       do n -> patch_size  -> theWorldW.penSize;
       #);
     Breed: (# name: ^text do inner #);
     
     AgentSet: Set
       (# element::< Agent;
          set_default_shape:
            (# enter shape[]#);
          shape: ^Text;
          ask:
            (# with:< BooleanValue
                 (#
                 do true -> value; inner
                 #);
               current: ^Element
            do scan
               (#
               do (if with then
                      current[] -> this(ask).current[];
                      inner ask
                  if)
               #);
            #)
       #);
     Turtles: @AgentSet(# element:: Turtle #);
     patches: @AgentSet
       (# element:: Patch;
          init:
            (# p: ^Patch
            do (for i: 50 repeat
                    (for j: 50 repeat
                         Patch -> p[];
                         ((i - 1) * patch_size,(j-1) * patch_Size) -> P.pos;
                         P[] -> insert
               for)for)
            #)
       #);
     BredSet: Set(##);
     theWorldW: @WorldWindow;
     purple: (#exit (40960,8192,61440) #);
     gold1: (#exit (65280,55040,0) #);
     clear_all:
       (# 
       do turtles.clear
       #);
     create_turtles:
       (# n: @integer; t: ^Turtle;
       enter n
       do (for  i: n repeat 
               i -> Turtle -> T[];
               (if (i mod 2) = 0  then
                   purple(* theWorldW.blue*) -> T.c
                else
                   gold1(*theWorldW.gray*) -> T.c
               if);
               T[] -> Turtles.insert  
          for)
       #);
  do theWorldW.open;
     4 -> setPatch_size;
     patches.init;
     inner World
  #);

     
