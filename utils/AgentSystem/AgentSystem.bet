ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/colorTable';
INCLUDE '~beta/containers/sets';
INCLUDE 'WorldWindow';
---lib:attributes---
Point:
  (# x,y: @integer;
     print:
       (#
       do 'Point('->puttext; x -> putint; ','->put; y ->putint; ')'->put
       #)
  enter(x,y)
  exit(x,y)
  #);
random:
  (# V,R: @integer
  enter V
  do 111 mod V -> R
  exit R
  #);
World: guienv
  (# 
     BasicAgent:
       (#
       do inner
       exit  this(BasicAgent)[]
       #);
     Agent: BasicAgent
       (# pos: @Point;
       do inner
       #);
     Turtle: Agent
       (# who: @ integer;
          c: @color;
          size: @integer;
          pen: @
            (# down: (# do  true -> isDown #);
               up: (# do  false -> isDown #);
               isDown: @boolean;
               draw:
                 (# l: @theWorldW.tLine; s1,s2: @integer;
                    out:
                      (# x,y: @integer
                      enter(x,y)
                      do '(' -> put; x -> putint; ','->put; y->putint; ')'->put
                      #)
                 enter(s1,s2)
                 do (pos.x,pos.y)->out; 
                    (pos.x + s1 * 4, pos.y + s2 *4) -> out;
                    newline;
                    (none,pos.x,pos.y,pos.x + s1 * 4, pos.y + s2 *4,c) -> l.open
                 #)
            #);
          fd:
            (# s: @integer
            enter s
            do (s,s) -> pen.draw;
               pos.x + s * 4 -> pos.x;
               pos.y + s * 4 -> pos.y;
            #);
          rd:
            (# d: @integer
            enter d
            do (d,0) -> pen.draw; pos.x + d * 4 -> pos.x
            #);
          ld:
            (# d: @integer
            enter d
            do (0,d) -> pen.draw; pos.y + d * 4 -> pos.y
            #);
          print:
            (# 
            do 'Agent:[' -> puttext; who -> putint;  ']'->put; pos.print; 
            #);
       enter who
       do (who * 20, who) -> pos;
          inner
       #);
     Patch: Agent
       (# size: @integer;
          setSize: (# N: @integer enter n do n -> size #);
          c: @Color
       #);
     patch_size: @integer;
     setPatch_size: 
       (# n: @integer 
       enter n 
       do n -> patch_size  -> theWorldW.penSize;
       #);
     TurtleSet: Set
       (# element:: Turtle;
          ask:
            (# current: ^Element
            do scan
               (#
               do current[] -> this(ask).current[];
                  inner ask
               #);
            #)
       #);
     Turtles: @TurtleSet;
     theWorldW: @WorldWindow;
     purple: (#exit (40960,8192,61440) #);
     gold1: (#exit (65280,55040,0) #);
     create_turtles:
       (# n: @integer; t: ^Turtle;
       enter n
       do (for  i: n repeat i -> Turtle -> T[];
               (if (i mod 2) = 0  then
                  purple(* theWorldW.blue*) -> T.c
                else
                   gold1(*theWorldW.gray*) -> T.c
               if);
               T[] -> Turtles.insert  
          for)
       #);
     Observer: @ Agent
       (# go: 
            (#
            do 5 -> create_turtles;
               (for i: 5 repeat
                    turtles.ask
                    (#
                    do (*current.print;*)
                       1 -> current.fd;
                       current.who + i -> random -> current.rd;
                       current.who + 17  * i -> random -> current.ld;
                       theWorldW.show;
                       10 -> guienvWait;
                    #);
                    newline
               for)
            #)
       #);
  do theWorldW.open;
     4 -> setPatch_size;
     inner World
  #);
---program:descriptor---
(#
do World(#do observer.go #)
#)

     
