ORIGIN '~beta/guienv/guienv';
include '~beta/guienv/graphicscanvas';
INCLUDE '~beta/guienv/utils/colorTable';
INCLUDE '~beta/guienv/figureitems';
INCLUDE 'colors'
-- guienvLib:attributes---
WorldWindow: window
  (# open::<
       (#
       enter(world_width,world_height,patch_size)
       do (world_width * patch_size,world_height * patch_size) -> size;
          (world_width * patch_size) div 2 -> orgX;
          (world_height * patch_size) div 2 -> orgY;
          'WorldWindow:open:width:'->puttext; world_width -> putint;
          'height:'-> puttext; world_height -> putint;
          ' patch_size:'->puttext; patch_size -> putint;
          ' orgX: ' -> puttext; orgX -> putint; ',  orgY:'->puttext;
          orgY -> putint; newline;
          grey(*purple*) -> backgroundcolor;
       #);
     eventhandler:: (# onMouseDown::(##)#);
     
     patchCanvas: Canvas
       (# open::<
            (# x,y: @integer; c: @color
            enter(x,y,c)
            do inner;
               (patch_size,patch_size) -> size;
               c -> backGroundColor;
               (x,y) -> position
            #)
       #);
     
     turtleLine: Line
       (# open::            
            (# sx,sy,ex,ey: @integer; c: @color
            enter(sx,sy,ex,ey,c)
            do (if false then
                   'origin:('->puttext; orgX -> putint; ','->put; 
                   orgY -> putint; ')\nstart:(' -> puttext;
                   sx -> putint; ','->put; sy -> putint; ') adj:('->puttext;
               if);
               sx + orgX -> sx;
               orgY - sy -> sy;
               (if false then
                   sx -> putint; ','->put; sy -> putint; '):to:('->puttext;
                   ex -> putint; ','->put; ey -> putint; ') adj:('->puttext;
               if);
               ex + orgX -> ex;
               (orgY - ey)  -> ey;
               (if false then
                   ex -> putint; ','->put; ey -> putint; ')'->put;
                   newline;
               if);
               (sx,sy) -> start;
               (ex,ey) -> end;
               penSize -> pen.size; 
               c -> pen.backGroundColor;
               c -> pen.foreGroundColor;
               (*patterns.gray[] -> lx.pen.stipple;*)
               true -> visible;
               (*true -> hilite;*) (*lx.drag*) show
            #)
       #);
     polygonEditor: polygon
       (# open::
            (# do patterns.white[] -> fill.tile #);
          eventHandler::
            (# onMouseDown::
                 (#
                 do drag;
                    this(polygonEditor)[] -> father.selection.set
                 #)
            #)
       #);
     createPolygon:
       (# ps: [0] ^point; psx:[0] ^point;
          poly: ^polygon; size: @integer
       enter ps
       do (if false then
              'CreatePolygon: ' -> puttext;  ps.range -> putint; newline;
          if);
          L:
            (for i: ps.range repeat
                 (if ps[i][] = none then 
                     &Point[]  -> ps[i][];
                     ps[1] -> ps[i];
                     i -> size;
                     leave L 
                 if);
                 ps[i].h + orgX -> ps[i].h;
                 orgY - ps[i].v -> ps[i].v;
                 (if false then
                     '(' -> put; ps[i].h -> putint; ',' -> put;
                     ps[i].v -> putint; ')'-> put; ' ' -> put
                 if);
            for);
          size -> psx.new;
          (for i: size repeat
               &Point[] -> psx[i][];
               ps[i] -> psx[i]
          for);
          &polygonEditor[] -> poly[];
          poly.open;
          psx -> poly.points;
       #);     
     orgX,orgY: @integer;
     world_width,world_height,patch_size,penSize: @integer;
  #);

