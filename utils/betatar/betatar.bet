ORIGIN '~beta/dependency/v1.3/dependency';
INCLUDE '~beta/mps/v5.2/findgrammar';
INCLUDE '~beta/betaast/v5.2/index'; (* for easy ast init in this example *)
INCLUDE '~beta/process/v1.6/osinterface';
---program: descriptor---

(# AST: @astinterface(# defaultGrammarFinder:: findgrammar #);
   filename,resultname: ^text;
   resultfile: @file;
   ph: @pathHandler; OS: @osInterface;
   doDomain: @boolean;
   usage:
     (#
     do 'Usage: '->screen.puttext;
	1 -> arguments->screen.puttext;
	' filename [extent|domain] result'->screen.putline;
     #);
do l: (if noOfArguments <> 4 then
	  'ERROR: Illegal number of arguments: '->screen.puttext;
	  noOfArguments->screen.putint; screen.newline;
	  usage;
	  leave l
       else
	  2 -> arguments -> filename[];
	  (if true
	   // 'domain'->(3->arguments).equal then true ->doDomain
	   // 'extent'->(3->arguments).equal then false->doDomain
	   else
	      'ERROR: illegal option: '->screen.puttext;
	      3->arguments->screen.putline;
	      usage;
	      leave l
	  if);

	  4->arguments->resultname[];
	  resultname[]->resultfile.name; resultfile.openWrite;

	  OS.init;

	  AST.astlevelInit;
	  ph.init;

	  AST.DependencyGraph
	  (# f: @file;
	  do Init;  xverboselevel.nothing->xverboselevel;
	     OS.hostMachine->TargetMachine;
	     OS.hostMachine->TargetDirectory;

	     (filename[],ph.currentDirectory)->ph.convertFilePath->f.name;

	     f.entry.path->filename[];

	     (if doDomain then
		 scan: filename[]->scandomain
		 (# announce: (# do 'Error during dependency analysis:\n\t'->puttext #);
		    MPSexception::
		      (# do announce; T[]->puttext; ' too large (MPS overflow)'->putline; leave scan #);
		    DoubleFormException::
		      (# do announce; 'Double form declaration'->putline; leave scan #);
		    propertyException::
		      (# do announce; 'Error concerning property: '->puttext; p[]->putline; leave scan #);
		    circularDependencyException::
		      (# do announce; 'Circular dependency on: '->puttext; fg.name->putline; leave scan #);
		    unknownPropertyException::
		      (# do announce; 'Unknown property: '->puttext; leave scan #);
		    emptyFragmentException::
		      (# do announce; 'Empty fragment file: '->puttext; fg.name->putline; leave scan #);
		    transAccessException::
		      (# do announce; 'No read access to: '->puttext; FN[]->putline; leave scan #);
		    notExistingException::
		      (# do announce; 'Fragment file does not exist: '->puttext; fullFN[]->putline; leave scan; #);
		    parseException::
		      (# do announce; 'Parse errors in: '->puttext; fullFN[]->putline; leave scan #);
		    transCreateDirException::
		      (# do announce; 'Unable to create directory: '->puttext; FN[]->putline; leave scan #);
		 do (current.fullname,'.')->ph.localpath->
		    resultfile.putline
		 #)
	      else
		 scan: filename[]->scanextent
		 (# announce: (# do 'Error during dependency analysis:\n\t'->puttext #);
		    MPSexception::
		      (# do announce; T[]->puttext; ' too large (MPS overflow)'->putline; leave scan #);
		    DoubleFormException::
		      (# do announce; 'Double form declaration'->putline; leave scan #);
		    propertyException::
		      (# do announce; 'Error concerning property: '->puttext; p[]->putline; leave scan #);
		    circularDependencyException::
		      (# do announce; 'Circular dependency on: '->puttext; fg.name->putline; leave scan #);
		    unknownPropertyException::
		      (# do announce; 'Unknown property: '->puttext; leave scan #);
		    emptyFragmentException::
		      (# do announce; 'Empty fragment file: '->puttext; fg.name->putline; leave scan #);
		    transAccessException::
		      (# do announce; 'No read access to: '->puttext; FN[]->putline; leave scan #);
		    notExistingException::
		      (# do announce; 'Fragment file does not exist: '->puttext; fullFN[]->putline; leave scan; #);
		    parseException::
		      (# do announce; 'Parse errors in: '->puttext; fullFN[]->putline; leave scan #);
		    transCreateDirException::
		      (# do announce; 'Unable to create directory: '->puttext; FN[]->putline; leave scan #);
		 do (current.fullname,'.')->ph.localpath->
		    resultfile.putline
		 #)
	     if)

	  #);
	  resultfile.close;
      if);
#)
