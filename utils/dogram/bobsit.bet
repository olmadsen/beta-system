ORIGIN 'execute';
--LIB:attributes--
debug: (# exit true #);

--PROGRAM: descriptor--
systemenv
(# usage:
     (# 
     do 'Usage: bobsit [-b] <grammar-name>' -> putline;
        stop;
     #);
   boot: @boolean;
   grammarname: ^text;
   cmd, args, bobsfile, parserlst, output: ^text;
   betalib, objdir: ^text;
   f: @file;
do (if (NoOfArguments<2) or (NoOfArguments>3) then
       usage;
   if);
   (if '-b' -> (2->Arguments).equal then
       true -> boot;
       3 -> Arguments -> grammarname[];
    else
       2 -> Arguments -> grammarname[];
   if);
   
   'yes' -> putline;
   
   (* bobsit *)
   'bobsit ' -> puttext; grammarname[]->puttext; '-parser.bobs'->putline;
   (if boot and ('metagrammar' -> grammarname.equal) then
       betalib.copy -> bobsfile[];
       'boot/grammars/metagrammar/metagrammar-parser.bobs' -> bobsfile.append;
    else
       grammarname.copy -> bobsfile[];
       '-parser.bobs' -> bobsfile.append;
       'bobsfile: ' -> puttext;
       bobsfile[] -> putline;
   if);
   betalib[] -> putline;
   
   betalib.copy -> cmd[];
   (if boot then
       'boot/' -> cmd.append;
   if);
   'bin/' -> cmd.append;
   objdir[] -> cmd.append;
   '/exbobs' -> cmd.append;
   exeFileExtension -> cmd.appendExtension;
   
   'command: ' -> putline;
   cmd[] -> putline;
   
   (if debug then
       'Executing ' -> puttext;
       cmd[] -> puttext;
       ' '->put;
       args[] -> puttext;
       ' < ' -> puttext;
       bobsfile[] -> putline;
   if);
   (cmd[], args[], bobsfile[]) -> execReadTextAndPipeFileIn -> output[];
   output[] -> puttext;
   
   (* mv -f bobslist $grammarname-parser.lst *)
   grammarname.copy -> parserlst[];
   '-parser.lst' -> parserlst.append;
   parserlst[] -> f.name;
   (if f.entry.exists then f.delete; if);
   'bobslist' -> f.entry.path;
   parserlst[] -> f.entry.rename;
   
   'tables' -> f.entry.path;
   (if (not f.entry.exists) or (f.entry.size = 0) then
       'ERRORS IN GRAMMAR' -> putline;
       'Look at ' -> puttext;
       parserlst[] -> puttext;
    else
       (* tabc *)
       'Creating tables' -> putline;
       'tables ' -> args[];
       grammarname[] -> args.append;
       '-parser ' -> args.append;
       grammarname[] -> args.append;
       ('tabc', args[], boot) -> execute;
   if);

#)
