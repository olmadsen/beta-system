ORIGIN '~beta/process/activate';
INCLUDE '~beta/sysutils/pathhandler';
INCLUDE '~beta/basiclib/file';
INCLUDE '~beta/toollibs/utils/fileExtensions';
--systemlib: attributes--

debug: (# exit false#);

execute:
  (# args: ^text;
     command: ^text;
     cmd: ^text;
     boot: @boolean;
     machine_type: external (# T: [1]@ char  exit T #);
     objdir: @text;
     PH: @pathhandler;
     betalib: ^text;
     
  enter (command[], args[], boot)
  do machine_type -> objdir;
     PH.init;
     PH.betalib -> betalib[];
     
     betalib.copy -> cmd[];
     (if boot then
         'boot/' -> cmd.append;
     if);
     'bin/' -> cmd.append;
     objdir[] -> cmd.append;
     '/' -> cmd.append;
     command[] -> cmd.append;
     exeFileExtension -> cmd.appendExtension;
     (if debug then
         'Executing ' -> puttext;
         cmd[] -> puttext;
         ' '->put;
         args[] -> putline;
     if);
     (if false then
         (* execReadtext fails with more than one argument *)
         (cmd[], args[]) -> execReadText -> puttext;
      else
         (# system: external
              (# commandAndArgs: [0]@char;
              enter commandAndArgs
              #);
         do cmd.copy -> cmd[];
            ' ' -> cmd.append;
            args[] -> cmd.append;
            cmd -> system;
         #);
     if)
  exit (betalib.copy, objdir.copy)
  #);
