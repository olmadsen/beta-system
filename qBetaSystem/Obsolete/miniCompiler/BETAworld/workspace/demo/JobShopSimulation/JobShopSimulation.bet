JobShopSimulation: @
  (# With Simulation;
     MachineGroup:
       (* Perhaps a Resource super pattern *)
       (# 
          init: 
            (# 
            enter (id[],nm) 
            do display; 
               &Queue[] -> Q[] 
            #);
          request: 
            (# 
            do 'Request:'.print; display; 
               nm - 1 -> nm; (if nm < 0 then Q[] -> (current).wait if) 
            #);
          release: 
            (# 
            do 'Release:'.print; display; 
               nm + 1 -> nm; (if nm <= 0 then Q.first -> (current).activate if) 
            #);
          display:
            (#
            do id.print; ':'->put; nm -> putint; newline;
            #);
          id: ^text;
          nm: @integer; (* no. of machines in group *)
          Q: ^Queue
       #);
     Order: Process(#do inner #);
     init:
       (# mkId: (# i: @integer; id: ^text enter i do 'Machine:' -> id[]; i -> id.putint; exit(id[],i*2) #)
       do 3 -> nmg;
          &MachineGroup[] -> MG1[]; 1->mkId -> MG1.init;
          &MachineGroup[] -> MG2[]; 2->mkId -> MG2.init;
          &MachineGroup[] -> MG3[]; 3->mkId -> MG3.init;
       #);
     MG1,MG2,MG3: ^MachineGroup; nmg: @integer; (* as for now, 3 MachineGroups*)
     
     O1: @ Order
       (#
       do MG1.request; display; ' using: '.print; MG1.display;
          5 -> hold; 
          MG1.release;
          MG2.request; display; ' using: '.print; MG2.display;
          15 -> hold;
          MG2.release;
          MG3.request; display; ' using: '.print; MG3.display;
          7 ->  hold;
          MG3.release
       #);
     O2: @ Order
       (#
       do MG2.request; display; ' using: '.print; MG2.display;
          8 -> hold;
          MG2.release;
          MG1.request; display; ' using: '.print; MG1.display;
          29 -> hold;
          MG1.release;
          MG3.request; display; ' using: '.print; MG3.display;
          18 ->  hold;
          MG3.release
       #);
     
  do 'Job shop simulation: '.print; newline;
     init;
     ('Order1',3) -> O1; 
     ('Order2',6) -> O2; 

     (O1[],120) -> Simulate(#do O2[] -> SQS.insert; #); 
  #)


