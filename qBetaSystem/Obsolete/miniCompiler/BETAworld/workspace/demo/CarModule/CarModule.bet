CarModule: @
  (#           
     head: 
       (# thisHead: ^head2; 
          detach: (# do thisHead.suspend #);     
       enter thisHead[] 
       do inner 
       #);
     
     head2: head(# current: ^CarElement do inner #);
     
     CarElement:
       (# accept:< (# V: ^Visitor enter V[] do inner #);    
       #);
     Wheel: CarElement
       (# name: ^text;
          enum: head (# do this(Wheel)[] -> thisHead.current[]; detach #);
          accept:: (# do this(Wheel)[] -> V.visitWheel #)
       #);
     Engine: CarElement
       (# accept:: (# do this(Engine)[] -> V.visitEngine #)
       #);
     Body: CarElement
       (# accept:: (# do this(Body)[] -> V.visitBody #)
       #);
     Car: CarElement
       (# FL,FR,BL,BR: @Wheel;
          E: @engine;
          B: @Body;

          enum: head2
            (# 
            do (*FL[] -> current[];*)
               this(enum)[] -> FL.enum;
               this(enum)[] -> FR.enum;
               BL[] -> current[];
               this(enum).suspend;
               BR[] -> current[];
               this(enum).suspend;
               E[] -> current[];
               this(enum).suspend;
               B[] -> current[];
               this(enum).suspend;
            #);
          scan: 
            (# enm: @enum; current: ^CarElement 
            do (for i: 6 repeat 
                    enm;
                    enm.current[] -> current[];
                    inner;
               for)
            #);
          init:
            (#
            do 'FrontLeft' -> FL.name[];
               'FrontRight' -> FR.name[];
               'FrontRight' -> FR.name[];
               'BackRight' -> BR.name[];
               'BackLeft' -> BL.name[];
            #)
       #);
     
     Visitor: 
       (# visitWheel:< (# w: ^Wheel enter w[] do inner #);
          visitEngine:< (# e: ^Engine enter e[] do inner #);
          visitBody:< (# b: ^Body enter b[] do inner #);
       #);
     PrintVisitor: Visitor
       (# visitWheel::(# do 'Wheel: '.print; w.name.print; newline  #);
          visitEngine:: (# do 'Engine:'.print; newline  #);
          visitBody:: (# do 'Body:'.print; newline #)
       #);
     doVisitor: Visitor
       (# visitWheel::(# do 'Do Wheel: '.print; w.name.print; newline  #);
          visitEngine:: (# do 'do Engine:'.print; newline  #);
          visitBody:: (# do 'do Body:'.print; newline #)
       #);
     C: @Car
       
  do C.init;
     C.scan
     (# 
     do &PrintVisitor [] -> current.accept;
        &doVisitor[] -> current.accept
     #);
     newline
  #)

