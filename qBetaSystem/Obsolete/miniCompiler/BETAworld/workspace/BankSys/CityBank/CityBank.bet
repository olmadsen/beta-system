CityBank: @
  (# with Customers;
     Account: 
       (# deposit:< 
            (# amount,newBalance: @ integer
            enter amount
            do balance + amount -> balance;
               balance -> newBalance;
               ('Deposit: ',this(Account)[],amount) -> Transactions.add
               inner
            exit newBalance
            #);
          withdraw:< 
            (# amount,bal: @ integer
            enter amount
            do (if (balance - amount) >= 0 then
                   balance - amount
                     -> balance;
                   ('Withdraw: ',this(Account)[],amount) -> Transactions.add
               if);
               balance -> bal
            exit bal
            #);
          Transactions: @
            (# add: 
                 (# msg: @text; subj: ^Account;
                    amount: @ integer;
                    R: ^Text
                 enter (R[],subj[],amount)
                 do 'Transfer ' -> msg.putText;
                    subj.owner.name[] -> msg.puttext;
                    ' amount: ' -> msg.puttext;
                    amount -> msg.putint;
                    10 -> msg.putx;
                    msg[] -> owner.notify
                 #)
            #);
          print: 
            (# 
            do 'Account for:'.print;
               owner.print;
               ' with amount: '.print;
               balance -> putint;               
            #);
          init: 
            (# own: ^Person
            enter own[]
            do own[] -> owner[];
            #);
          balance: @ integer;
          owner: ^Person;
          trans: ^Transactions
       #);
     CheckingAccount: Account
       (# deposit:: (#  do cost + 1 -> cost #);
          cost: @ integer
       #);
     SavingsAccount: Account(#  #);
     transfer: 
       (# from,to: ^Account;
          amount: @ integer
       enter (from[],to[],amount)
       do amount -> from.withDraw;
          amount -> to.deposit;
          'Transfer '.print;
          amount -> putint;
          ' from '.print;
          from.print;
          ' to '.print;
          to.print;
          newline
       #);
     Joe,Mary: ^CheckingAccount;
     open: 
       (# JoeBal,MaryBal: @ integer
       do &CheckingAccount[] -> Joe[];
          customers.Joe[] -> Joe.init;
          &CheckingAccount[] -> Mary[];
          customers.Mary[] -> Mary.init;
          300 -> Joe.deposit -> JoeBal;
          JoeBal - 100
             -> Mary.deposit
             -> MaryBal;
          Joe.print;
          newline;
       #)
  #)
