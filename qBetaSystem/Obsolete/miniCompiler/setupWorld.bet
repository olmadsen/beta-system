ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'directoryComponents'
---lib:attributes---
getCurrentDir:
  (# getCurDir: External(# cwd: [1]@char exit cwd #);
     cwd: ^text;
  do &text[]->cwd[];
     getCurDir->cwd;
  exit cwd[]
  #);

listBETA:
  (# path,dir: ^text; ind: @integer;
     indent: (# do (for i: ind repeat ' ' -> put for) #);
     line: (# do (for i: 40 - ind repeat '-' -> put for); newline;#);
  enter(path[],dir[],ind)
  do  (path[],false) -> getBETAdirectories
     (#
     do scan
        (#
        do (if currentIsFile then
               current[] -> puttext; ' ' -> put
        if)#);
        scan
        (#
        do (if not CurrentIsFile then
               current[] -> puttext; ' ' ->put
        if)#);
        newline;
        indent;
        line;
        ind + 2 -> ind;
        scan
        (# P: ^text;
        do (if not CurrentIsFile then
               path.copy -> P[];
               '/' -> P.put;
               current[]-> P.append; (*'Xdir: ' -> puttext; P[] -> putline;*)
               indent;
               current[] -> puttext; ':' -> put; ' '-> put;
               (P[],current[],ind) -> listBETA
        if)#);
     #) 
  #)     
---program:descriptor---
(#
do getCurrentDir -> puttext; ':' -> put; newline;
   ('.','.',0) -> listBETA
#)
