Sieve: @ ActorSystem.System
  (# max: (# exit 30 #);
     primeMax: (# exit 1000 #);
     primeNumber: Message(##);
     Sieve: Actor 
       { start:: (# enter succ[] do #);
         print:: 
           (# 
           do ',' -> put; id -> putint; (if succ[] <> none then succ.print if);
           #);
          printPrime:
            (# P: @integer
            enter P
            do console.display(# do 'Prime: '.print; P -> putint; newline #)
            #);
          display:
            (#
            do console.display(# do 'chain:'.print; print; newline #)
            #);
          P: @integer;
          succ: ^Sieve
       do inner
       };
     sieve0: @ Sieve
       (# 
       do display;
          2 -> P; 
          P -> printPrime;
          3 -> P;
          Loop:
            (if P < primeMax then
                P -> PrimeNumber -> succ.send;
                p + 2 -> P;
                restart Loop
            if);
          0 -> PrimeNumber -> succ.send;
       #);
     bSieve: Sieve
       (# 
       do select
          (# PP: ^PrimeNumber; 
          do (if PrimeNumber## -> receive then
                 M[] -> PP[];
                 PP.id -> P;
                 P -> printPrime
             if)
          #);
          loop:
            select
            (# PP: ^PrimeNumber; mp,mx: @integer
            do (if PrimeNumber## -> receive then
                   M[] -> PP[];
                   PP.id -> mx;
                   (if mx = 0 then
                        mx -> Primenumber -> succ.send
                    else
                       P -> mp;
                       L: (if mx > mp then mp + P -> mp; restart L if);
                       (if mx < mp then 
                           mx -> Primenumber -> succ.send
                       if);
                       restart loop
               if)if)
            #)
       #);
     sieve31: @ Sieve
       (#
       do loop:
            select
            (# PP: ^PrimeNumber; 
            do
               (if PrimeNumber## -> receive then
                   M[] -> PP[];
                   PP.id -> P;
                   P -> printPrime;
                   (if P > 0 then
                       restart loop
                   if)
               if)
            #);
       #);
     pred,S: ^Sieve
  do 
     sieve0[] -> pred[];
     (for i: max repeat
          &bSieve[] -> S[];
          (i - 1,S[]) -> pred.start;
          S[] -> pred[]
     for);
     (30,sieve31[]) -> pred.start;
     (max + 1,none) -> sieve31.start;
  #)

     
