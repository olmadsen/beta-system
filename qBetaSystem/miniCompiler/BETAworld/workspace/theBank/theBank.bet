theBank: @
  (# with dk.au.cs.Accounts;
    with dk.au.cs.Persons;
    
    People: @ 
      (# Joe,Lisa,Peter: ^Person;
        open: 
          (# 
          do &Person[] -> Joe[];
             &Person[] -> Lisa[];
             &Person[] -> Peter[];
             'Joe Smith' -> Joe.open;
             'Lisa Lind' -> Lisa.open;
             'Peter Pan' -> Peter.open
          #)
      #);
    aBank: @ 
      (# JoeA,LisaA,PeterA: ^Account;
        open: 
          (# 
          do &Account[] -> JoeA[];
             &Account[] -> LisaA[];
             &Account[] -> PeterA[];
             (people.Joe[],300)
                -> JoeA.open;
             (people.Lisa[],400)
                -> LisaA.open;
             (people.Peter[],500)
                -> PeterA.open
          #);
        transfer: 
          (# from,to: ^Account;
            amount: @ integer;
            display: 
              (# N: ^text
              do 'Transfer from: '.print;
                 (from.ownerName).print;
                 ' EUR '.print;
                 amount -> out.putint;
                 ' to: '.print;
                 (to.ownerName).print;
                 newline
              #)
          enter (from[],to[],amount)
          do display;
             (to[],amount) -> from.withdraw;
             (from[],amount) -> to.deposit
          #)
      #);
    go: 
      (# 
      do (aBank.peterA[],aBank.JoeA[],120)
            -> aBank.transfer;
         (aBank.peterA[],aBank.LisaA[],110)
            -> aBank.transfer
      #);
    out: @ DefaultScreen
  do People.open;
     aBank.open;
     (aBank.peterA[],aBank.JoeA[],120)
        -> aBank.transfer;
     (aBank.peterA[],aBank.LisaA[],110)
        -> aBank.transfer
  #)