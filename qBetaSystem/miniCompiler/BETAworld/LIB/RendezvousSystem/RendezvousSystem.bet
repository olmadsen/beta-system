RendezvousSystem: @BasicSystem
  (# %OSDvisibility=disguised %
     System: BasicSystem
       (# Semaphore: 
            (# id: ^text;
               init: 
                 (# V: @ integer
                 enter (id[],V)
                 do V -> cnt; id[] -> M.init;
                 #);
               wait: 
                 (# P: ^Process
                 do M.get; 
                    cnt - 1 -> cnt;
                    (if cnt < 0 then
                        thisCore.main.active[] -> P[];
                        P_status.WAITING -> P.myAS.active.status;
                        P.myAS.incrWaiting;
                        P.myAS.active[] -> Q.insert;
                        console.display_t
                        (#do '\nWait:'.print; P.id->putint; 
                           ':'->put; P.myAs.active.id->putint
                        #);
                        M.free;
                        P.myAS.active.suspend
                     else
                        M.free
                    if)
                 #);
               signal: 
                 (# A: ^Process.Activity; cP: ^Process
                 do M.get; 
                    cnt + 1 -> cnt;
                    (if cnt <=  0 then
                        Q.removeNext -> A[];
                        A.resume;
                        (*P_status.RESUMED -> A.status;
                        A[] -> A.sch.insert;
                        A.sch.decrWaiting;*)
                        thisCore.main.active[] -> cP[];
                        console.display_t
                        (#do '\nSignal:'.print; cP.id -> putint;
                           ':'->put; A.ID->PUTINT
                        #);
                        M.free; 
                        cP.myAS.active.suspend
                     else
                        M.free; 
                    if)
                 #);
               display: 
                 (# 
                 do console.display_t
                    (# 
                    do 'Semaphore: '.print;
                       id.print;
                       Q.display
                    #)
                 #);
               cnt: @ integer;
               M: @Lock;
               Q: @ProcessQueue;
            #);
          Process: BasicProcess
            (# start::< 
                  (# 
                  do myAS.init;
                     this(Process)[] -> SQS.insert; 
                     inner
                  #);
               Activity: BasicProcess
                 (# start::< 
                       (# 
                       do this(Activity)[] -> myAS.insert;
                          (*myAs[] -> sch[];*)
                          inner
                       #);
                    wait: BooleanValue
                      (# P: ^Process
                      do testCondition:
                           (if true then
                               inner;
                               (if not value then
                                   myAS.active.suspend;
                                   restart testCondition
                               if)
                           if)
                      #);
                    resume:
                      (#
                      do P_status.WAITING -> status;
                         this(Activity)[] -> myAS.insert;
                         myAS.decrWaiting;
                      #);
                    xsch: ^ActivityScheduler
                 do inner
                 #);
               port: 
                 (# id: ^text;
                    m,mutex: @ semaphore;
                    init: 
                      (# T1,T2: @ text
                      enter id[]
                      do this(port).id[] -> T1.puttext;
                         ':m' -> T1.puttext;
                         (T1[],0) -> m.init;
                         this(port).id[] -> T2.puttext;
                         ':mutex' -> T2.puttext;
                         (T2[],0) -> mutex.init;
                      #);
                    display: 
                      (# msg: ^text
                      enter msg[]
                      do console.display_t
                         (# 
                         do '\nPort:'.print;
                            id.print;
                            msg.print;
                            m.display;
                            mutex.display
                         #)
                      #);
                    entry: 
                      (# 
                      do m.wait;
                         inner;
                         mutex.signal;
                      #);
                    accept:< 
                      (# 
                      do m.signal;
                         mutex.wait;
                         inner
                      #);
                 #);
               activityScheduler: 
                 (# active: ^ Activity;
                    idf: ^text;
                    init:
                      (# 
                      do 'activityScheduler' -> idf[]
                      #);
                    L: @lock;
                    entry: (# do L.get; inner; L.free #);
                    insert:  entry
                      (# P: ^activity
                      enter P[] 
                      do P[] -> aSQS.insert 
                      #);
                    remove: entry
                      (# P: ^Activity
                      enter P[] 
                      do P[] -> aSQS.remove 
                      #);
                    waitingActivities: entry
                      (# value: @boolean
                      do (if noOfWaiting > 0 then
                             true -> value
                         if)
                      exit value
                      #);
                    incrWaiting: entry
                      (# 
                      do noOfWaiting + 1 -> noOfWaiting;
                         console.display_t
                         (#
                         do '\nProcess:'.print;
                            id -> putint;
                            ' activity:'.print;
                            active.id -> putint;
                            ' +now:'.print; noOfwaiting -> putint 
                         #);
                      #);
                    decrWaiting: entry
                      (# 
                      do noOfWaiting - 1 -> noOfWaiting;
                         console.display_t
                         (#
                         do '\nProcess:'.print;
                            id -> putint;
                            ' activity:'.print;
                            (if active[] = none then 
                                'none'.print
                             else
                                active.id ->putint
                            if);
                            ' -now:'.print; noOfwaiting -> putint 
                         #);
                      #);
                    display: (# T: ^text enter T[] do T.print #);
                    noOfWaiting: @ integer;
                    P: ^Process; 
                 do Loop:
                      (if true then
                          disable;
                          aSQS.removeNext -> active[];
                          (if active[] <> none then
                              P_status.ACTIVE -> active.status;
                              enable;                              ;
                              active;
                              disable;
                              (if active.status = P_status.ACTIVE then
                                  active[] -> aSQS.insert
                              if)
                          if);
                          (if aSQS.isEmpty then 
                              (if waitingActivities then
                                  thisCore.main.active[] -> P[];
                                  enable;
                                  P.suspend;
                                  restart Loop
                              if);
                              enable
                           else
                              enable; restart Loop
                          if)
                      if);
                 #);
               myAS: @ActivityScheduler;
               aSQS: @ ProcessQueue;

            do inner;
               myAS;
               terminate;
            #)
       do 'Rendezvous system'.print; inner
       #)
  #)
