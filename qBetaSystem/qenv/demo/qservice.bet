ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '~beta/qBetaSystem/compiler_IF/directoryComponents';
INCLUDE '~beta/qBetaSystem/qbeta/qgenerator';
INCLUDE '~beta/qBetaSystem/VM/betaVM';
BODY '~beta/qBetaSystem/qbeta/unithandlerBody';
BODY '~beta/qBetaSystem/VM/objectcode_VM';
-- program: descriptor --
SystemEnv
(# gui: @guienv;
   setWindowEnv:: (# do gui[] -> theWindowEnv[] #);
   compile: @generator;
   VM: @ betaVM
     (# putCH:: (# do ch -> put #);
        getCh:: (# do get -> ch #);
        done::
          (#
          do stop
          #)
     #);
   LG: @log;
   fullPath,fileExt,BetaWorld,errors: ^Text;
   iMain: ^compile.IModule;
   runMode: @integer;
do 'C:\\beta\\r5.5\\qBetaSystem\\qbeta\\BETAworld\\workspace\\hello\\hello.qbeta' -> fullPath[];
   '/qBetaSystem/qbeta/BETAworld/' -> BetaWorld[];
   '.qbeta' -> fileExt[];
   (BetaWorld[],fileExt[],'.xbeta') -> compile.initDirectoryModules;
   ('qservice.log', false) -> LG.open;

   true -> compile.newFatComma;
   true -> compile.isXbeta;
   true -> compile.altParser;
   run_Beta_INT -> runMode;

   'compiling: ' -> putText;
   fullPath[] -> putline;

   (LG[], true, NONE, fullPath[]) -> compile -> (iMain[],errors[]);
   (if errors[] <> none then
       'errors:'-> putline; errors[] -> putline;
   if);

   (runMode,true) -> VM.init;

   (if not compile.onlyLex
       and not compile.hasSyntaxError
       and not compile.semanticErrors then
       true -> VM.noPause;
       (compile.runtimeDescs,compile.mainDescNo,fullPath[],runMode)
         -> VM.execute.init;
       VM.execute[] -> fork;
       VM.reactivate
   if);
   'done' -> putLine;
   stop
#)
