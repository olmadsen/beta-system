ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/numberio';
---LIB:attributes---
Tracer:
  (# lineMax:< IntegerValue
       (#
       do 65 -> value;
          inner
       #);   
     noSpace:< BooleanValue;
     prompt:
       (# S: ^text
       do inner
       exit S[]
       #);
     pPrompt:< prompt
       (#
       do inner
       #);
     pPrompt2: prompt(#do  '**   ' -> S[] #);
     cPrompt:< prompt
       (#
       do inner
       #);
     nl:
       (#
       do lx.newline;
          0 -> cCount
       #);
     TT:
       (# S: ^text; inText: @Boolean; noSpace:< BooleanValue
       enter S[]
       do (if S[] <> none then
              (if true then
                  cCount + S.length + 1 -> cCount;
                  (if cCount > lineMax then nl if);
                  S[] -> lx.puttext
               else
                  S.scanAll
                  (#
                  do (if ch
                      // '"' then
                         '"' -> lx.put;
                         not inText -> inText
                      // ascii.newline then
                         nl 
                      else
                         ch -> lx.put
                     if);
                     (if (cCount + 1 -> cCount) > lineMax then
                         nl
                     if)
                  #)
              if)
           else
              (if true then
                  '-none-'-> TT
               else
                  '-none-'-> lx.puttext;
                  cCount + 6 -> cCount
          if)if);
          (if not noSpace then ' ' -> lx.put if)
       #);
     TL:
       (# S: ^text; lineCount: @integer; inText,emitNl: @Boolean;
          hasNL: BooleanValue
            (#
            do L:
                 S.scanAll
                 (#
                 do (if (ch = ascii.newline) -> value then leave L if)#)
            #); 
       enter S[]
       do (if S[] <> none then
              L:
                (if not hasNL then 
                    cCount + 3 -> cCount;
                    '"'->lx.put; S[] -> TT(# noSpace:: TrueValue #); '"'->lx.put;
                    (if not noSpace then ' ' -> lx.put if);                  
                 else
                    nl;
                    S.scanAll
                    (#
                    do (if ch 
                        // '"' then
                           '"' -> lx.put;
                           not inText -> inText;
                           true -> emitNL
                        // ascii.newline then
                           (if inText then '"' -> lx.put; false -> inText if);
                           (if (lineCount + 1 -> lineCount) > 3 then 
                               leave L 
                           if);
                           nl;
                           false -> emitNL;
                        else
                           ch -> lx.put;
                           true -> emitNL;
                       if)
                    #);
                    (if emitNL then lx.newline if)
          if)if)
       #);
     TQ: 
       (# S: ^text;
       enter S[]
       do '"' -> lx.put; S[]->TT(# noSpace:: TrueValue #); '"' -> lx.put; ' ' -> lx.put; cCount + 3 -> cCount;
       #);
     PT:
       (# S:  ^text; R: ^Object
       enter(S[],R[])
       do S[]->TT; (if R[] = none then 'none'->TT else inner if)
       #);
     CC: (# ch: @char enter ch do ch -> lx.put #);
     II: (# enter lx.putint do ' ' -> lx.put; cCount + 3 -> cCount  #);
     TI: (# S: ^text; V: @integer enter(S[],V) do S[] -> TT; V -> II #);
     BB: 
       (# Bx: @boolean 
       enter Bx 
       do (if Bx then 'true' -> TT else 'false' -> TT if);
       #);
     FF: 
       (# F: @real enter F do F->lx.putreal; ' '->lx.put; cCount + 10->cCount#);
     doPrint:
       (#
       do (if print then 
              pPrompt -> puttext;
              lx.scanAll
              (#do ch -> put; 
                 (*(if ch = ascii.newline then pPrompt2 -> puttext if)*)
              #);
              newline
          if);
       #);
     cCount: @integer;
     print,doEmitCom: @ Boolean;
     lx: ^Text
  do &Text[] -> lx[];
     inner;
     doPrint     
  #)
