MapLib: obj
   %requires SetLib
   %include StringLib
   Map: 
      in Domain:< Object
      in Range:< Object
      Entry:
         in inx: ref Domain
         in elm: ref Range
      add:
         in S: ref Domain
         in D: ref Range
         if (head == none) :then
            head := link(S,D,none)
         :else
            head:= link(S,D,head)
      lookup:
         in V: ref Domain
         out R: ref Range
         scan
           if (V == current.inx) :then
               R := current.elm
               leave(lookup)
      forAll:
         %id "forAll"
         in pred:<
            in E: ref Entry
            out return: var Boolean
            innerBool: inner
               out B: var Boolean
            return := innerBool -- no innerBool(forAll)
         out B: var Boolean

         B := true
         scan
            B := B && pred(current)
      scan:
         current: ref Entry
         nxt: ref Link
         Loop:
            if (nxt =/= none) :then
               current:= nxt
               inner(scan)
               nxt:= nxt.next
               restart(Loop)
            :else
               nxt:= nxt    
         nxt := head
         loop
      reduce:
         in F:<
            in x: ref Entry
            in b: var Boolean
            out z: var Boolean
         in IV: var Boolean    
         out V: var Boolean
         V := IV
         scan
            V := F(current,V)
      select:<
         in pred:<
            in IX: ref Domain
            in E: ref Range
            out V: var Boolean
         out R: ref SetLib.Set(#Domain)
         done: var boolean -- inner(select) and done for test purposes
         R:= SetLib.Set(#Domain)
         scan
            if (pred(current.inx,current.elm)) :then
                R.insert(current.inx)
                inner(select)
         done := true
         inner(select)
      link: Entry
         in next: ref link
      head: ref link 
  
