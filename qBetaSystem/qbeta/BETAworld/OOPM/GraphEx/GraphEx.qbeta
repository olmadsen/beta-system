GraphEx: obj
   %visible Collections.Set
   class Graph:
      Node(id: var String):<
         addEdge(to: ref Node)-> E: ref Edge:<
            E := Edge(this(Node),to)
            inner(addEdge)
            edges.insert(E)
         edges: obj Set(#Edge)
         display:<
            inner(display)
         displayEdges:<
            newline
            edges.scan
               " edge to: ".print
               current.to.display
               put(' ')	       
               current.display
               newline	       
      Edge(from: ref Node,to: ref Node):< 
         display:<
            inner(display)
      nodes: obj Set(#Node)
      addNode(nm: var String) -> N: ref Node:
         N := Node(nm)
         nodes.insert(N)
      display:
         nodes.scan
            current.display
            current.displayEdges
      inner(Graph)
   class  RoadAndCityGraph: Graph 
      Node::< 
         cityName -> cn: ref String:
            cn := id	 
         addEdge::<
            ...
         display::
             cityName.print
             ...
      Edge::<
         roadLength: var Integer
         ...
         display::
            "roadLength: ".print
            putint(roadLength)
      addCity(nm: var String) -> N: ref Node:
         N := addNode(nm)
      addRoad(from: ref Node, to: ref Node, dist: var integer) -> E: ref Edge:
         E := from.addEdge(to)
         E.roadLength := dist	 
      inner(RoadAndCityGraph)
   class Network: Graph
      class Node::< 
         send(receiver: ref Node,P: ref Packet):
            E: ref Edge
            E := map(receiver)
            E.send(receiver,P)
            ...
         receive(send: ref Node,P: ref Packet): 
            ...
         map(receiver: ref Node) -> E: ref Edge:
            -- find E in edges where E is the fastest route to receiver
         ...
      class Edge::<
         bandWidth: var integer
         send(receiver: ref Node,P: ref Packet):
            ...
         ...
      class Packet(S: var String):
          ...
   Europe: obj RoadAndCityGraph
      setUp:
         N1: ref Node
         N2: ref Node
         E: ref Edge
         N1 := addCity("Paris")	 
         N2 := addCity("London")
         E := addRoad(N1,N2,291) -- 291 miles
         E := addRoad(N2,N1,291)
         N2 := addCity("Berlin")
         E := addRoad(N1,N2,1054) -- 1054 km
   Europe.setUp
   "Europe:\n".print
   Europe.display