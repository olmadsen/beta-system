DecoratorLib: obj
   %visible StringLib
   § General structure of Decorator pattern - not used in the Beverage ex
   class Component:
      operation:<
         ...
      ...
   class ConcreteComponent: Component
      operation::<
         ...
      ...
   class Decorator: Component
      operation::<
         ...
      comp: ref Component
      ...
   class ConcreteDecorator: Decorator
      operation::<
         ...
      ...
   § Beverage classes
   class Beverage:
      drink:<
          inner(drink)
   class Coffee: Beverage
      drink::<
         "Drinking coffee".print
         inner(drink)
   class Soda: Beverage
      drink::<
         "Drinking Soda".print
         inner(drink)

   § Beverage decorators
   class BeverageDecorator(comp: ref Beverage): Beverage
      drink::<
         comp.drink
         inner(drink)
   class Milk(percentage: var integer): BeverageDecorator
      drink::<
         " with milk of richness {I2S(percentage)} %".print
   class IceCubes(n: var integer): BeverageDecorator
      drink::<
         " with {I2S(n)} ice cubes".print
   main: do
      aCoffee: ref Beverage      § must be type Beverage and not Coffee
      aCoffee := Coffee          § a plain Coffe object
      aCoffee := Milk(aCoffee,3) § Adding a Milk decorator
      aCoffee.drink
      newline
      aSoda: ref Beverage        § must be type Beverage and not Soda
      aSoda := Soda              § A plain Soda object
      aSoda := IceCubes(aSoda,5) § Adding an IceCube decorator
      aSoda.drink

      
   



   