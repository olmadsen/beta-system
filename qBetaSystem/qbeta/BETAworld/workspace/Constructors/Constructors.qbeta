Constructors: obj
   class Person:
       in name: var String
       age: var integer
       adr: ref String
       setAge:
          in ag: var integer
          out thisC: obj this(Person)
          age := ag
       setAdr:
          in Ad: var String
          out thisC: obj this(Person)
          adr := Ad
       display:
          (name + " age: {I2S(age)} address: " + adr + "\n").print
   class Rectangle:
       width: var 1.0
       length: var 1.0
       asSquare:
          in side: var float
          out thisRec: obj this(Rectangle)
          width := side
          length := side
       asNonSquare:
          in w: var float
          in h: var float
          out thisRec:  obj this(Rectangle)
          width := w
          length := h
       print:
          putfloat(width)
          put(',')
          putfloat(length)
          newline

   p1: ref #Person
   p2: obj Person("Joe")
   r: ref Rectangle
   "\nA new module Constructors  has been created!\n".print
   p1 := Person("Mary")
   p1.display
   p1 := Person("Mary").setAge(17)
   p1.display
   p2.display
   p1 := Person("Thea").setAge(20).setAdr("Holme")
   p1.display
   r := Rectangle
   r.print
   r := Rectangle.asSquare(2.3)  -- actually same syntax as classname.constructor
   r.print
   r := Rectangle.asNonSquare(3.0,5.0)
   r.print
