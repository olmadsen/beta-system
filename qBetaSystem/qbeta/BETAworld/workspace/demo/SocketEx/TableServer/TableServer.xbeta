TableServer: obj LIB.SocketSystem.SocketSystem
   Table: obj 
      L: obj Lock
      entries: obj 
         entry: 
            in name: ref String.String
            in address: ref String.String
            next: ref Entry
            find: 
               in nm: ref String.string
               out adr: ref String.String
               if (nm = name) :then 
                  adr := address 
               :else 
                  if (none = next) :then 
                     adr := "Not found" 
                  :else 
                     adr := next.find(nm)
         head: ref Entry
      insert: 
         in nm: ref String.String
         in adr: ref String.String
         L.get
         elm: obj entries.entry(nm,adr)
         elm.next := entries.head
         entries.head := elm
         L.free
      lookup: 
         in nm: ref String.String
         out adr: ref String.String
         L.get
         -- head may be none
         
         adr := entries.head.find(nm)
         L.free
   Producer: obj BasicProcess("Producer")
      PS: obj Socket
      aProd: ref Socket
      "Producer:\n".print
      PS.init(3000)
      PS.bind
      PS.listen
      aProd := PS.accept(Producer)
      cycle
         name: ref String.String
         adr: ref String.String
         answer: ref String.String
         name := aProd.receive(Producer)
         "TS: ".print
         name.print
         newline
         adr := aProd.receive(Producer)
         adr.print
         newline
         Table.insert(name,Adr)
         answer := "Inserted: " + name + ":" + adr + "\n"
         answer.print
         aProd.send(answer)
   Consumer: obj BasicProcess("Consumer")
      CS: obj Socket
      aCons: ref Socket
      "Producer:\n".print
      CS.init(3001)
      CS.bind
      CS.listen
      aCons := CS.accept(Consumer)
      cycle
         name: ref String.String
         adr: ref String.String
         answer: ref String.String
         name := aCons.receive(Consumer)
         adr := Table.lookup(name)
         answer := "Address of: " + name + " is " + adr + "\n"
         answer.print
         aCons.send(answer)
   cycle
      Producer.call
      Consumer.call