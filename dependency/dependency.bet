ORIGIN '~beta/mps/v5.0.1/astlevel';
BODY 'private/control'

--astInterfaceLib: attributes--

DependencyGraph:
  (#
  
     (* Generic library for building dependency graphs for BETA
	  * fragments.
	  *
	  * Usage:
	  *   AST.DependencyGraph
      *     (# 
      *     do graphInit; 
	  *        'ppc'->TargetMachine;      {* name used as selector MDBODY etc.         *}
	  *        'ppc'->TargetDirectory;    {* name of code directory (for OBJFILE etc.) *}
	  *        '<filename>'->scanExtent(# do ... current[] ... #);
      *     #);
	  *
	  * or
	  *   (# AST: @AstInterface;
	  *      dg: @AST.DependencyGraph;
	  *   do ... {* init astlevel *}
	  *      dg.graphInit;
	  *      'ppc'->dg.TargetMachine;      
	  *      'ppc'->dg.TargetDirectory;
	  *      '<filename>'->dg.scanExtent(# do ... current[] ... #);
      *   #)
      *)
  
     <<SLOT xDependencyGraphLib: attributes>>;
     TargetDirectory: @text; 
     TargetMachine: @text;
     
     AsmExt:< (# ext: @text do '.a'->ext; INNER exit ext[] #);
     BinExt:< (# ext: @text do '.o'->ext; INNER exit ext[] #);
     LibExt:< (# ext: @text do '.o'->ext; INNER exit ext[] #);
     JobExt:< (# ext: @text do '..job'->ext; INNER exit ext[] #);
     ExeExt:< (# ext: @text do ''->ext; INNER exit ext[] #);
	 
     graphInit: (# <<SLOT xDependencyGraphInit: dopart>> #);
	 
     scanner: (* abstract superpattern for scanExtent and scanDomain *)
       (# MPSexception:< exception
            (# T: ^text; 
            enter T[]
            do INNER
            #);
          DoubleFormException:< exception
            (# n: ^text
            enter n[]
            do INNER
            #);
          transAccessException:< exception
            (# FN: ^text
            enter FN[] 
            do INNER;
            #);
          notExistingException:< exception
            (# FullFN: ^text
            enter FullFN[]
            do INNER;
            #);
          parseException:< exception
            (# FullFN: ^text
            enter FullFN[]
            do INNER;
            #);
          transCreateDirException:< exception
            (# FN: ^text
            enter FN[] 
            do INNER;
            #);
          current: ^FragmentGroup;
          root: ^Text;
       enter root[]
       do INNER
       #);
     scanExtent: scanner (* scan all beta files in extent *)
       (# propertyFile:< (* called for each non BETA file in properties *)
            (# fullname: ^Text;
            enter fullname[]
            do INNER
            #);
       do <<SLOT xscanExtentDoPart: descriptor>>
       #);
     scanDomain: scanner (* scan beta files in domain (ORIGIN and INCLUDES) *)
       (# 
       do <<SLOT xscanDomainDoPart: descriptor>>
       #);
     DependencyGraphPrivate: @<<SLOT xDependencyGraphPrivate: descriptor>>;
  do INNER;
  #)
