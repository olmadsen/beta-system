ORIGIN 'dependency';
INCLUDE '~beta/betaast/v5.0.1/index'; (* for easy ast init in this example *)
INCLUDE '~beta/process/v1.4/osinterface';
---program: descriptor---

(# AST: @astinterface;
   filename: ^text;
   ph: @pathHandler; OS: @osInterface;
   doDomain: @boolean;
   usage:
     (#
     do 'Usage: '->screen.puttext; 
        1 -> arguments->screen.puttext;
        ' filename [extent|domain]'->screen.putline;
     #);
do l: (if noOfArguments <> 3 then
          'ERROR: Illegal number of arguments: '->screen.puttext;
          noOfArguments->screen.putint; screen.newline;
          usage;
          leave l
       else
          2 -> arguments -> filename[];
          (if true
           // 'domain'->(3->arguments).equal then true ->doDomain
           // 'extent'->(3->arguments).equal then false->doDomain
           else
              'ERROR: illegal option: '->screen.puttext;
              3->arguments->screen.putline; 
              usage;
              leave l
          if);
          OS.init;

          ('',true)->AST.init; (* Note: this is init in index.bet
                                * used here for easy ast init
                                *)
          
          ph.init;
          
          AST.DependencyGraph
          (# f: @file;
          do Init;
             OS.hostMachine->TargetMachine;
             OS.hostMachine->TargetDirectory;
             
             (filename[],ph.currentDirectory)->ph.convertFilePath->f.name;
             
             f.entry.path->filename[];
             
             (if doDomain then
                 filename[]->scandomain
                 (#
                 do (current.fullname,'.')->ph.localpath->
                    screen.putline
                 #)
              else
                 filename[]->scanextent
                 (#
                 do (current.fullname,'.')->ph.localpath->
                    screen.putline
                 #)
             if)
             
          #);
      if);
#)

