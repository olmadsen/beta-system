ORIGIN 'dependency';
INCLUDE '~beta/betaast/v4.9.1/index'; (* for easy ast init in this example *)
INCLUDE '~beta/process/v1.4/osinterface';
---program: descriptor---

(# AST: @astinterface;
   filename: ^text;
   ph: @pathHandler; f: @file; OS: @osInterface;
   doDomain: @boolean;
do l: (if noOfArguments <> 3 then
          'ERROR: Illegal number ('->screen.puttext;
          noOfArguments->screen.putint;
          ') of arguments'->screen.putline;
          'Usage: BETAdependence filename [extent|domain]'->screen.putline;
          leave l
       else
          2 -> arguments -> filename[];
          (if true
           // 'domain'->(3->arguments).equal then true ->doDomain
           // 'extent'->(3->arguments).equal then false->doDomain
           else
              'ERROR: illegal option ('->puttext;
              3->arguments->puttext; ')'->putline;
              leave l
          if);
          OS.init;

          ('',true)->AST.init; (* Note: this is init in index.bet
                                * used here for easy ast init
                                *)
          
          ph.init;
          
          AST.DependencyGraph
          (#
          do graphInit;
             OS.hostMachine->TargetMachine;
             OS.hostMachine->TargetDirectory;
             
             (filename[],ph.currentDirectory)->ph.convertFilePath->f.name;
             
             f.entry.path->filename[];
             
             (if doDomain then
                 filename[]->scandomain
                 (#
                 do (current.fullname,f.entry.path.head)->ph.localpath->
                    screen.putline
                 #)
              else
                 filename[]->scanextent
                 (#
                 do (current.fullname,f.entry.path.head)->ph.localpath->
                    screen.putline
                 #)
             if)
             
          #);
      if);
#)

