origin '~beta/mps/v5.2/astlevel'; 
include '../dependency';
include '../scanNonCompiled'
(************************************************
 * test program showing how to use 
 *      dependency.bet
 * to compute extent and doamin of a fragment.
 * Usage
 *       testdep                  computes extent and domain for testdep
 *       testdep foo              computes extent and domain for foo
 *)
---program:descriptor---
(# AST: @ astinterface;
   D: @AST.dependencyGraph;
   propError:
     (# FG: ^AST.fragmentGroup;
        p,t: ^text;
        n: @integer;
        CloseGroup: ^Object;
     enter(FG[],p[],t[],n,CloseGroup[])
     do 'Property error: '->puttext; p[] -> putline;
        'ErrorNo: '->puttext; n -> putint; newline;
        FG.name -> putline;
        t[] -> putline;

     #);
   FileName: ^text;
do AST.astLevelInit;
   (*(ast.trace.onParse,true)->ast.trace.set;
    (ast.trace.fragmentOpen,true)->ast.trace.set;
    (ast.trace.topOpen,true)->ast.trace.set;*)
   D.Init;
   'sun4s'->D.targetDirectory;
   'sun4s'->D.targetMachine;
   D.xverboselevel.actions -> D.xverboselevel;
   
   (if noOfArguments > 1 then
       2 -> arguments -> Filename[]
    else 'testdep'->Filename[]
   if);
   
   'Extent for : ' -> puttext; FileName[] -> putline;
   FileName[] -> D.scanExtent
   (#  propertyException::
        (#do (FG[],p[],t[],n,CloseGroup[]) -> propError;
           (if n >= 5 then true -> continue if)
        #)
   do '\t in extent: '->puttext; current.fullname->putline 
   #);
   
   'Domain for : ' -> puttext; FileName[] -> putline;
   FileName[] -> D.scanDomain
   (# propertyException::
        (#do (FG[],p[],t[],n,CloseGroup[]) -> propError;
           (if n >= 5 then true -> continue if)
        #)
   do '\t in domain: '->puttext; current.fullname->putline
   #);
   
   'Noncompiled for : ' -> puttext; FileName[] -> putline;
   FileName[] -> D.scanNonCompiled
   (# propertyException::
        (#do (FG[],p[],t[],n,CloseGroup[]) -> propError;
           (if n >= 5 then true -> continue if)
        #);
      translate:: 
        (#do '\t must be compiled: '->puttext; fg.fullname->putline;
        #)
   #);
#)

