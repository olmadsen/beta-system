origin '~beta/mps/v5.2/astlevel'; 
include '../dependency';
include '../scanNonCompiled';
include '../checkslots'
(************************************************
 * test program showing how to use 
 *      checkslots.bet
 * that reports unbound descriptor- and doPart slotd
 * Usage
 *       testcheckslots           computes extent and domain for testcheckslots
 *       testcheckslots foo       computes extent and domain for foo
 *)
---program:descriptor---
(# AST: @ astinterface;
   D: @AST.dependencyGraph;
   propError:
     (# FG: ^AST.fragmentGroup;
        p,t: ^text;
        n: @integer;
        CloseGroup: ^Object;
     enter(FG[],p[],t[],n,CloseGroup[])
     do 'Property error: '->puttext; p[] -> putline;
        'ErrorNo: '->puttext; n -> putint; newline;
        FG.name -> putline;
        t[] -> putline;

     #);
   FileName: ^text;
do AST.astLevelInit;
   (*(ast.trace.onParse,true)->ast.trace.set;
    (ast.trace.fragmentOpen,true)->ast.trace.set;
    (ast.trace.topOpen,true)->ast.trace.set;*)
   (*true -> D.switch[10];*)
   D.Init;
   'sun4s'->D.targetDirectory;
   'sun4s'->D.targetMachine;
   D.xverboselevel.actions -> D.xverboselevel;
   
   (if noOfArguments > 1 then
       2 -> arguments -> Filename[]
    else 'testcheckslots'->Filename[]
   if);
   
   'Checking for unbound descriptor- and dopart slots in : ' -> puttext; 
   FileName[] -> putline;
   
   (* A scanExtent or scanDomain is necessary before calling checkSlots *)
   FileName[] -> D.scanExtent
   (#  propertyException::
        (#do (FG[],p[],t[],n,CloseGroup[]) -> propError;
           (if n >= 5 then true -> continue if)
        #)
   #);
   
   true -> D.checkSlots
   (# unBoundSlot::
        (# 
        do '\n*** The slot: "'->puttext;
           slot[] -> puttext;
           '" defined in fragment:\n\t'->puttext;
           FG.fullname->putline;
           'is not bound by any fragment!'->putline
        #);
      DoubleDeclaration::
        (#
        do '\n**** The slot: "'->puttext;
           slot[] -> puttext; 
           '" is defined in the following two fragments:\n\t'
             -> puttext;
           FG1[] -> putline; '\t'->puttext;
           FG2[] -> putline
        #);
      PerhapsMissingBinds::
        (#
        do '\n**** Warning!\n     The slot: "'->puttext;
           slot[] -> puttext;
           '" is defined in '->puttext;
           noOfDefs->putint;
           ' fragment(s),\n     but only bound in '->puttext;
           noOfbinds->putint;
           ' fragment(s)!'->puttext;
           '\n     This may cause an "Undefined symbol"'->puttext;
           '\n     error message from the linker!\n'->puttext
        #);

   #)

#)
