ORIGIN '~beta/mps/v5.0.1/astlevel';
INCLUDE '~beta/betaast/v5.0.2/donecheck';
---astInterfaceLib:attributes---
TargetDirectory: 
  (# dir: @text do <<SLOT TargetDirectory: descriptor>> exit dir[] #);
AsmExt: (# ext: @text do <<SLOT AsmExt: descriptor>> exit ext[] #);
BinExt: (# ext: @text do <<SLOT BinExt: descriptor>> exit ext[] #);
LibExt: (# ext: @text do <<SLOT LibExt: descriptor>> exit ext[] #);
JobExt: (# ext: @text do <<SLOT JobExt: descriptor>> exit ext[] #);
ExeExt: (# ext: @text do <<SLOT ExeExt: descriptor>> exit ext[] #);

nti: booleanvalue(##);
b2c: booleanValue(##);
InsertSubDir:
  (* Inserts the directory name TargetDirectory immedidately before the
   * base name of the entered path name. E.g. if TargetDirectory='sun3'
   * and path='/home/frede/beta/system/v3.9/control' then the result will
   * be:      '/home/frede/beta/system/v3.9/sun3/control'.
   * Another example:  path='fusk' will result in: 'sun3/fusk'.
   *)
  (# path: ^text; dirCh: @text;
     lastSlash,L: @integer;
     asmDir: @boolean
  enter(path[],asmDir)
  do thePathHandler.DirectoryChar -> path.findCh(# do inx -> lastSlash #);
     thePathHandler.DirectoryChar -> dirCh.put;
     (if true
      // b2c then
         (# T: @text
         do 'b2c'->T; 3->L;
            (T[],lastSlash+1) -> path.insert;
            (if not asmDir then
                T.clear;
                thePathHandler.DirectoryChar->T.put;
                (T[],lastSlash+4)->path.insert;
                (TargetDirectory,lastSlash+5) -> path.insert;
                4+(TargetDirectory).length->L;
         if)#)
      // nti then
         3->L;
         (TargetDirectory,lastSlash+1) -> path.insert;
         (if not asmDir then
             (dirCh[],lastSlash+4)->path.insert;
             (sdkName[],lastSlash+5) -> path.insert;
             sdkName.length->L;
         if)
      else
         (TargetDirectory).length->L;
         (TargetDirectory,lastSlash+1) -> path.insert;
     if);
     (dirCh[],lastSlash+1+L)-> path.insert;
     inner;
  exit path[]
  #);

CreateAsmFileName: 
  (# fg: ^fragmentGroup; T: ^text
  enter fg[]
  do (* nti places the assembler files in the object directory and should
      * therefore send 'false' as asmDir parameter to InsertSubDir. Other
      * platforms should use 'true' as asmDir parameter (especially b2c).
      *)
     (fg.nameT.copy,(not nti))
       -> InsertSubDir(#do AsmExt->path.append #)
       -> T[]
  exit T[]
  #);
CreateObjFileName: 
  (# g: ^FragmentGroup; T: ^text
  enter g[]
  do (g.NameT.copy,false)
       -> InsertSubDir(# do BinExt -> path.append #)
       -> T[]
  exit T[]
  #);
