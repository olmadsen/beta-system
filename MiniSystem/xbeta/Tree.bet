ORIGIN '~beta/basiclib/betaenv';
---lib:attributes---
Tree:
  (# node:
       (# doPT:
            (#
            enter mark
            do &text[] -> lx[];
               PT 
            exit lx[]
            #);
          length:< integerValue(# do inner #);
          PT:<
            (# pos: @integer; break: @boolean;
               block:
                 (# pos,width: @integer; break: @boolean
                 enter(pos,width)
                 do lineWidth < (pos + width) -> break;
                    inner
                 #);
               mkBreak:
                 (# pos: @integer; break: @boolean;
                 enter(pos,break)
                 do (if lx.length > 0 then (* no break or space if first line *)
                        (if break then
                            lx.newline;
                            (for i: pos repeat ' ' -> lx.put for)
                         else
                            inner
                    if)if)
                 #);
               mkBreak0: mkBreak(##);
               mkBreak1: mkBreak(# do ' ' -> lx.put #);
               printComment: (# do #);
            enter(pos,break) 
            do (if mark then lx.getpos -> beginPos  if);
               (if comment[] <> none then printComment if);
               inner;
               (if mark then lx.getpos -> endPos if)
            exit lx[]
            #);
          son,next,last: ^node;
          append:
            (# N: ^node
            enter N[]
            do (if N[] = none then 
                   (dumpStack,'append') -> stop
               if);
               (if son[] = none then
                   N[] -> son[]
                else
                   N[] -> last.next[]
               if);
               N[] -> last[]
            #);
          scanSons:
            (# current: ^node;
               isLast: (# exit current.next[] = none #);
            do son[] -> current[];
               Loop:
                 (if current[] <> none then
                     inner;
                     current.next[] -> current[];
                     restart loop
                 if)
            #)                
       do inner
       exit this(node)[]
       #);

     lineWidth: @integer;
     
     mark: @boolean;  (* True if beginPos and endPos are updated in AST
                       * mark = True when using doPT for doing PT to
                       *             edit buffer
                       * mark = false when using doPT for debugging purposes
                       *)
     lx: ^text;
     comment: ^text;
     beginPos,endPos: @integer; (* position of this(nd) in .bet file *)
     beginBCP,endBCP: @integer; (* bytecode position at beginning and end of this
                                 * node set by generator::imperative::gen
                                 *)
  do 40-> linewidth;
     inner
  #)
