ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'xchecker';
INCLUDE '../miniCompiler/objectcode'
---lib:attributes---
generator: checker
  (# MkCom: (# t1,t2: ^text enter (t1[],t2[]) do t2[] -> T1.append -> cd.comment #);
     Module::
       (# gen::
            (#
            do ('Comp',newDesc.noOfDescs) -> cd.newComponent;
               SO.gen;
               this(Module)[] -> main[]
            #);
       #);
     StaticObject::
       (# gen::
            (#
            do (* Not for simple patterns like 
                *    X: @integer
                *)
               (if not OG.OI.isSimple then 
                   'not Simple' -> cd.comment;
                   (if origin[] <> none then 
                       OG.OI.gen; 
                   if);
                   (* only if singular - not for
                    * x:@integer; R: @Foo
                    *)
                   (if not OG.MP.isEmpty then
                       (sig.name[],OG[]) ->  ObjTmpStack.add
                   if)
               if)
            #);
          load::
            (#
            do 'SO:load:'->puttext; dopt -> putline;
               (sig.name[],off) -> cd.pushg
            #);
          store::
            (#
            do 'SO:store:'->putline; (sig.name[],off) -> cd.push
            #);
       #);
     ObjectGeneration::
       (* OI: ObjectInvocation, MP: MainPart *)
       (# gen::
            (# 
            do (*('OG: ',OI.inv.compoundSymb[]) -> MkCom;*)
               (*OI.inv.ATd.doPT -> putline*)
               OI.gen;
               (if not mp.isEmpty then
                   'OG:add:singular: '-> puttext; doPT -> putline;
                   ('Singular',this(ObjectTemplate)[]) -> ObjTmpStack.add
               if)
            #);
          genClass::
            (# 
            do 
               (N[], descNo, false, 1,1) -> cd.class;
               (* OI? *)
               (* super ? *)
               MP.gen;
            #);
          doCall::
            (#
            do 'doCall'->cd.comment; true -> hasDoPart;
               (if (exitType->TP[]) <> none then
                   (if TP.isRef then
                       2 -> xType
                    else
                       1 -> xType
               if)if);
               (if false then
                   doPt->putline;
                   'exitType: ' -> puttext; xType -> putint; newline;
               if);
               (if LMR
                // 'S' then 
                   (if hasDoPart then (N[],'D') -> cd.call if);
                   cd.rtnEvent;                        
                // 'L' then
                   (if hasDoPart then (N[],'D') -> cd.call if);
                   (N[],'X') -> cd.call;
                   xType -> cd.rtnEvent
                // 'M' then
                   (N[],'N') -> cd.call;
                   (if hasDoPart then (N[],'D') -> cd.call if);
                   (N[],'X') -> cd.call;
                   xType -> cd.rtnEvent
                // 'R' then
                   (N[],'N') -> cd.call;
                   (if hasDoPart then (N[],'D') -> cd.call if);
                   cd.rtnEvent;                     
               if);
            #);
       #);
     ObjectDescriptor::
       (# gen:: (# (* never come here*) #);
          genClass::
            (# 
            do 
               (N[], descNo, false, 1,1) -> cd.class;
               (* OI? *)
               (* super ? *)
               MP.gen;
               (4,3,1) -> CD.endClass
            #);
       #);
     MainPart::
       (# gen:: 
            (# 
            do (if not isEmpty then
                   ('origin',originOff) -> cd.rstore; 
                   att.gen;
                   cd.markEnterE;
                   false (* (superDesc[] <> none)*) -> cd.actions;                   
                   act.gen;
                   (vsize,rsize,1) -> CD.endClass
               if)
            #)
       #);
     Attributes::
       (# gen:: 
            (#
            do scanSons (#do current.gen #);
            #)
       #);
     Pattern::
       (# gen:: (# do (compoundSymb[],OD[]) -> objTmpStack.add #);
          activate::
            (#
            do 'Pattern:alloc'->puttext; doPT -> putline;
               (if '+' -> sig.name.equal then lex.symbols.add -> cd.op if)
            #);
       #);
     Actions::
       (# gen:: 
            (# 
            do scanSons(#do (true,0) -> current.gen #);
               (if false (*superDesc[] <> none *) then 
                   cd.rtnInner 
                else
                   'D' -> cd.rtn
               if)
            #);
       #);
     ObjectInvocation::
       (* a := 17;       a = rec, inv = := 17
        * a := b + 1     a = rec, inv = := b + 1
        *                b = rec, inv = + 1        *                
        * X: @integer - also an object invocation
        *)
       (# gen:: 
            (# 
            do 
               (if true (*not isSimple*) then 
                   (false,(isLeft,0) -> rec.gen) -> inv.gen
                else
                   'OI:isSimple: ' -> cd.comment; dopt -> cd.comment;
               if)
            #);
       #);
     ReceiverPath::
       (# gen:: 
            (# 
            do 'RP: ' -> cd.comB; scanSons(# do current.doPT -> cd.comA #); cd.comE;
               (* 
                * R.foo     R = receiver 
                * R.S.foo   R.S = receiver
                *)
               cd.pushThis;
               scanSons
               (# notFirst: @boolean
               do (*(if not notFirst  then cd.pushThis; true ->notFirst if);*)
                  (true,off) -> current.gen -> off
               #)
            #);
       #);
     Invocation:: 
       (# gen:: (# do scanSons(#do (isLeft,off) -> current.gen #)#)
       #);
     Keyword::
       (# gen::
            (#
            do 'KW:' -> cd.comment;
               scansons(#do ('KW:: ',current.doPT) -> cd.com2; current.gen #);
               ATd.activate;
               cd.comE
            #);
       #);
     Binary::
       (# gen::
            (#
            do ('Bin: ',operator.N[]) -> MkCom;
               (if '+' -> operator.N.equal then 
                   ('$x',off) -> cd.pushg 
               if);
               (false,0) -> argument.gen;
               (if isAssign then 
                   ('var',off) -> cd.storeg
                else
                   'Binary:gen:ATd: '->puttext;
                   (if ATd[] = none then 
                       'none' -> putline
                    else
                       ATd.doPT -> putline;
                       ATd.activate
                   if);
               if)
            #);
       #);
     Variable::
       (# gen:: 
            (# inv: ^invocation
            do ('Var: ',V.N[]) -> MkCom;
               (* 'Var: ' -> puttext; V.N[] -> puttext;
                *  ' ' -> put; (if ATd.isSimple then 'isSimple' -> putline if);
                *  ATd.doPT -> putline;
                *)
               (if not isLeft (*or ATd.isSimple*) then 
                   ' right'->putline;
                   ATd.load 
                else
                   ' left'->putline;
               if);
               ATd.off -> off
            #);
       #);
     const::
       (# gen:: (#  do V.N.setpos; V.N.getInt -> cd.pushConst #)
       #);
          
     FuncBracket::
       (# gen:: (# do ('FuncB:',N[]) -> MkCom #)
       #);
     Lexem:: 
       (# gen:: (# do ('Lexem: ',N[]) -> MkCom #)
       #);
     genMain:
       (# descno: @integer
       do ('main',newDesc,false,1,0) -> cd.class;
          cd.pushThis;
          (*  (if false then
           *               'main: topModule: ' -> puttext;
           *               markIncludes.topModule.name.lexemSy -> puttext;
           *           if);
           *           ('BETAworld',(markIncludes.topModule.desc).descno,true) -> cd.alloc;
           *           (for i: markIncludes.top repeat
           *                ('origin',markIncludes.orgOff[markIncludes.top - i + 1]) -> cd.rpushg;
           *           for);
           *
           * ('x','S') -> (markIncludes.main.desc).doCall;          
           *)
          ('BETAworld',1,true) -> cd.alloc;
          ('x','S') -> (main.desc).doCall;          
          cd.stop; (* we should generate stop here and not in betaVM *)
          
          (*   (if markIncludes.main## <> ObjectModule## then
           *               '\n\n***** Only ObjectModules can be executed *****\n\n' -> putline
           *           if)
           *)
       #);
     genEventProcessor:
       (#
       do ('EventProcessor',newDesc,false,1,0) -> cd.class;
          cd.stop (* we should generate stop here and not in betaVM *)
       #);
     
     ObjTmpStack: @
       (# item: 
            (# N: ^text; OT: ^ObjectTemplate;
            enter(N[],OT[])
            exit this(item)[]
            #);
          add:
            (# N: ^text; OT: ^ObjectTemplate;
            enter(N[],OT[])
            do (if (top+1-> top) > OTL.range then OTL.range -> OTL.extend if);
               (N[],OT[]) -> item -> OTL[top][]
            #);          
          next:
            (# inx: @integer
            do Loop:
                 (#
                 do inx +1 -> inx;
                    (if inx <= top then
                        OTL[inx].N[] -> OTL[inx].OT.genClass;
                        restart Loop
                    if);
                 #)
               #);               
          OTL: [10] ^Item;top: @integer;
       #);
     main: ^Module;
     mainDescNo: (# exit newDesc.noOfDescs -1 (*- 2*) (* ad hoc *) #);
     cd: @ObjectCode;
     descs: ^RunTimeDescriptors;
     bclst: @File;
  do 'GENERATE'->putline;
     (if true then
         'xcode'-> bclst.name;
         bclst.openWrite;
         bclst[] -> cd.init;
         'xbeta'-> cd.comment;
         m.gen;
         ObjTmpStack.next;
         genMain;
         genEventProcessor;
         cd.dump;
         cd.descs[] -> descs[]; (* why ?*)         
         bclst.close;
     if)
  #)
