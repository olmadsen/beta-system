ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/file';
INCLUDE '../compiler_IF/directoryComponents';
---lib:attributes---
mkAuxName:
  (# FN,ext: ^text; auxF: ^text
  enter(FN[],ext[])
  do (1,FN.length-6) -> FN.sub -> auxF[];
     (*auxF[] -> screen.putline;*)
     ext[] -> auxF.append;
     (* auxF[] -> screen.putline*)
  exit auxF[]
  #);   

Lexer:
  (# init:< 
       (# 
       enter(fileName[],in[]) 
       do special.init;
          indent.init;
          inner 
       #); 
     initWithText:
       (# inn: ^text
       enter inn[]
       do inn[] -> in[];
          0 -> bTop; 0 -> nextCh.pos; false -> inDecl;
          special.init;
          indent.init;
          &text[] -> errorStream[]; 
       #);
     fileName: ^Text;
     hasLexError: @boolean;
     errorStream: ^text;
     isXbeta: @boolean;
     buffer: [1000] @ char; bTop: @integer;
     nextCh: @
       (# pos,linePos,posBeforeName: @ integer;
          lastWasCR: @boolean;
          onlyWhiteSpace: @boolean
       do (if in.eos then
              255 -> ch
           else
              in.get -> ch; 
              pos+1 -> pos;
          if);
          (if ch <= 13 then
              0 -> linePos;
              true -> onlyWhiteSpace
           else
              linePos + 1 -> linePos;
              (if (ch > ' ') and onlyWhiteSpace then
                  (if (ch = '-') or (ch = '§') then
                   else
                      false -> onlyWhiteSpace
              if)if)
          if);
          (* pos -> putint; ' ' -> put;   ch -> put; *)
          (if (bTop + 1 -> bTop) > buffer.range then buffer.range -> buffer.extend if);
          ch -> buffer[bTop]
       #);
     printLines:
       (# n,lc,c: @integer; ch: @char
       enter n
       do bTop - 1 -> bTop; (* last char is white space and may be newline, 
                             * which should not be counted below
                             *)
          1 -> lc;
          (for i: btop repeat (if buffer[i] = ascii.newline then lc + 1 -> lc if)for);
          L:
            (for i: bTop repeat
                 (if (i = bTop) or (buffer[bTop - i + 1] = ascii.newline) then
                     n - 1 -> n; lc - 1 -> lc;
                     (if (n = 0) or (i = bTop) then   
                         lc + 1 -> lc -> putint; ':' -> put; ' ' -> put; 
                         (for j: i repeat 
                              buffer[bTop - i + j] -> ch -> put;
                              c + 1 -> c;
                              (if ch = ascii.newline then 
                                  lc + 1 -> lc -> putint; ':' -> put;
                                  0 -> c;
                              if)
                         for);
                         leave L
                     if)
                 if);
            for);
          newline;
          (for i: c + 2 repeat '*'-> put for);
          '^' -> put
       #);      
     peek:
       (#
       do indent.skipBlanks; 
       exit ch
       #);
     
     indent: @
       (# init: (# do 0 -> push ;(* true -> atStartOfLine*) #);
          dumpStack:
            (# inx: @integer
            enter inx
            do '\niStack: ' -> puttext; inx -> putint; ':'->put;
               (for i: top repeat
                    iStack[i] -> putint; ' ' -> put
               for);
               newline;
            #);
          skipBlanks:
            (# i: @ integer; inOut: @integer;
            do 0 -> i; (if not bob then false -> bob if);
               (if ch = 0 then nextch if); 
               L: (if (ch = 32) then 
                      i + 1 -> i;
                      nextCh; 
                      restart L 
                  if);
               (if ch <= 13 then (* newline *)
                   nextCh;
                   true -> atStartOfLine; true -> bob;
                   restart skipBlanks
                else 
                   (if atStartOfLine then
                       L:
                         (if true 
                          // i = iStack[top] then
                             0 -> inOut;
                             maxInt -> inOut;
                          // i < iStack[top] then
                             (for j: top - 1 repeat
                                  (if i = iStack[top - j] then
                                      - j -> InOut;
                                      top - j -> top;
                                      leave L
                                  if)
                             for);
                             3 -> printlines;
                             '\n   The indentation of this line does not '
                               -> puttext;
                             'match any previous lines\n' 
                               ->puttext; 
                             '   The level here is: ' -> puttext; i -> putint;
                             '. Previous lines are at : ' -> puttext;
                             (for i: top repeat
                                  i -> putint; ' ' -> put
                             for);
                             newline;
                             '   In file: \n   ' -> puttext;
                             fileName[] -> putline;
                             newline;
                             true -> hasLexError;
                          // i > iStack[top] then
                             (if (i - iStack[top]) < 2 then
                                 3 -> printLines;
                                 '\n   The indentation of this line is less than 3 blanks' 
                                   -> putline;
                                 '   It must be at least 3 blanks' -> putline
                             if);
                             i -> push;
                             1 -> InOut
                         if);
                       false -> atStartOfLine;
                   if);
                   (* newline;
                    (if atStartOfLine then 
                    'T:'->puttext
                    else
                    'F:'->puttext
                    if);
                    i -> putint; ':'->put;
                    (for i: top repeat istack[i] -> putint; ','->put for);
                    newline*)
               if)
            exit inOut 
            #);
          atStartOfLine,bob: @ boolean;

          iStack: [4] @ integer; top: @integer;
          push:
            (# v: @integer enter V 
            do (if (top + 1 -> top) > iStack.range then
                   iStack.range -> iStack.extend
               if);
               V -> iStack[top]
            #);
          revert:
            (# ind: @integer
            enter ind
            do top + ind -> top
            #);
       #);
     readName:
       (# c,prevCh: @char;
          isConst: @boolean;
       enter c
       do nextCh.pos - 1 -> nextCh.posBeforeName;
          ch -> ascii.isDigit -> isConst; (* what about c?*)
          &text[] -> sym[];
          (if c > 0 then c -> sym.put if);
          L: (if (ch->ascii.isLetter) or (ch-> ascii.isDigit) 
                 or (ch = '_') or (ch = '$') or (ch = '%') or (ch = '@') then
                 ch -> sym.put;
                 ch -> prevCh;
                 nextCh;
                 restart L;
             if);
          (if isConst and (ch = '.') then
              (*'*** got float:' -> puttext;*)
              ch -> sym.put;
              ch -> prevCh;
              nextCh;
              LL:
                (if ch -> ascii.isDigit then
                     ch -> sym.put;
                 ch -> prevCh;
                 nextCh;
                    restart LL
                if);
              (*sym[] -> putline;*)
              symbols.floatConst -> symb
          else
              (if prevCh = '%' then
                  symbols.fatComma -> symb 
               else
                  (if isConst then
                      symbols.const -> symb
                   else    
                      (if true
                       // 'var' -> sym.equalNCS then
                          symbols.varVal -> symb
                       // 'val' -> sym.equalNCS then
                          symbols.constVal -> symb
                       // 'ref' -> sym.equalNCS then
                          symbols.varRef -> symb
                       // 'obj' -> sym.equalNCS then
                          symbols.constRef -> symb
                       // 'in' -> sym.equalNCS then
                          symbols.inArg -> symb
                       // 'out' -> sym.equalNCS then
                          symbols.outArg -> symb
                       else                      
                          symbols.idf -> symb
                      if)
          if)if)if)
       #);
     
     readText:
       (# symb: @integer
       do &text[] -> sym[];
          nextCh;
          L:
            (if ch
             // '"' then
                (* end  of  text *)
             // '\\' then
                ch -> sym.put;
                nextCh;
                ch -> sym.put;
                nextCh;
                restart L
             // ascii.nl then
                '\n\n*** ERROR: end-of-line when parsing String' -> putline;
                true -> hasLexError;
                3 -> printLines
             else
                ch -> sym.put;
                nextCh;
                restart L
            if);
          symbols.string -> symb
       exit symb
       #);
     getComment:
       (# T: ^text
       do skipComment.get -> T[];
       exit T[]
       #);
     skipComment: @
       (# put:
            (# ch: @char
            enter ch
            do ch -> comment.put;
            #);
          get: 
            (# T: ^text 
            do (if comment.length > 0 then 
                   comment.copy -> T[];
                   comment.clear
               if) 
            exit T[] 
            #);
          comment: @text;
       do (*comment.clear;*)
          (if comment.length > 1 then
              (* multi-line comment? *)
              ascii.newline -> put
          if);
          (if nextCh.onlyWhiteSpace then
              '<' -> put
          if);
          nextCh; (* moved from the place of call of skipComment *)
          nextch.linePos -> comment.putint; '>' -> put;
          ch -> put;
          Loop:
            (#
            do nextCh; 
               (if ch 
                // ascii.newline then
                // 255 then
                else                   
                   ch -> put;
                   restart Loop
               if);
            #);
       #);                            
     
     special: @
       (# init:
            (#
            do true -> chars['!'];
               true -> chars['#'];
               true -> chars['$'];
               true -> chars['%'];
               true -> chars['&'];
               true -> chars['?'];
               true -> chars['*'];
               true -> chars['+'];
               true -> chars['-'];
               true -> chars['/'];
               true -> chars['<'];
               true -> chars['='];
               true -> chars['>'];
               true -> chars['<'];
               true -> chars['\\'];
               true -> chars['|'];
            #);
          in: booleanValue
            (# ch: @char
            enter ch
            do chars[ch] -> value 
            #);
          read:
            (# c: @char
            enter c
            do &text[] -> sym[];
               (if c > 0 then c -> sym.put if);
               L:
                 (if (ch -> in) then
                     ch -> sym.put;
                     nextCh;
                     restart L
                 if);
               symbols.special -> symb;
               (*'\nSpecial: ' -> puttext; sym[] -> putline*)
            #);
          chars: [256] @boolean
       #);
     
     in: ^stream;
     symb,oldPos: @integer;
     sym: ^text;
     ch: @ char;
     
     current:
       (# T: ^text
       do symb -> symbols.asText -> T[];
          (if symb
           // symbols.const // symbols.idf // symbols.string
           // symbols.keyword // symbols.fatComma then
              ' ' -> T.put;
              sym[] -> T.append
          if)
       exit T[]
       #);
     symbols: @ 
       (# print: 
            (# sy: @integer
            enter sy
            do sy -> astext -> puttext
            #);
          asText:
            (# sy: @integer; T: @text
            enter sy
            do '<<' -> T; sy -> T.putint; '>>' -> T.puttext;
               (if sy
                // 0 then '<zero>' -> T                   
                // begin then '{' -> T
                // end then '}' -> T
                // leftBrack then '(' -> T
                // rightBrack then ')' -> T
                // constRef then 'obj' -> T
                // varRef then 'ref' -> T 
                // colon then ':' -> T
                // comma then ',' -> T 
                // semiColon then ';' -> T
                // varVal then 'var' -> T
                // constVal then 'val' -> T
                // dot then '.' -> T
                // objModule then '/' -> T
                // value then '->' -> T
                // leftSquare then '[' -> T          
                   
                // virtual then ':<' -> T
                // further then '::<' -> T
                // final then '::' -> T
                // include then '%include' -> T
                // rightSquare then ']' -> T          
                // const then 'const' -> T
                // idf then 'name' -> T
                // special then 'special' -> T
                // keyword then 'keyword' -> T
                // fatComma then 'fatComma' -> T
                // prim then '%' -> T
                // innerSy then 'inner' -> T          
                // charSy then'char' -> T
                // string then 'text' -> T
                // alternative then '//' -> T
                // thisObj then 'this' -> T
                // comBegin then '(' -> T; '*' -> T.put; (* avoid confusing beta-indent *)
                // eq then '=' -> T
                // lt then '<' -> T                   
                // le then '<= ' -> T
                // gt then '>' -> T
                // ge then '>=' -> T
                // ne then '<>' -> T
                // add then '+' -> T
                // sub then '-' -> T
                // mult then '*' -> T
                // orr then 'or' -> T
                // xorr then 'xor' -> T          
                // rdiv then '/' -> T
                // idiv then 'div' -> T
                // modd then 'mod' -> T
                // andd then 'and' -> T       
                // nott then 'not' -> T
                // inArg then 'in ' -> T
                // outArg then 'out ' -> T
                // illegalCh then '???' -> T
                // eos then  '-eos-' -> T                 
                else
                   sy -> T.putint
               if)
            exit T[]
            #);

          begin: (#exit 1 #);
          end: (#exit 2 #);
          leftBrack: (#exit 3 #);
          rightBrack: (#exit 4 #);
          colon: (#exit 5 #);          
          constRef: (# exit 6 #);
          varRef: (# exit 7 #);
          comma: (#exit 8#);

          semiColon: (#exit 10 #);
          keyword: (# exit 11 #);
          fatComma: (# exit 12 #);
          
          varVal:(# exit 13 #);
          constVal: (# exit 14 #);
          
          dot: (#exit 16 #);
          objModule: (#exit 67 (* 17 - double use here see rdiv *) #);
          value: (#exit 18#); (* return value of pattern *)

          leftSquare: (#exit 20 #);
          
          virtual: (# exit 21 #);
          further: (# exit 22 #);
          final: (# exit 26 #);
          
          leaveSy: (#exit 23#);
          restartSy: (#exit 24#);
          
          include: (# exit 25 #);

          rightSquare: (# exit 27 #);
          property: (#exit 28 #);
          
          special: (#exit 31 #);
          const: (#exit 32 #);
          idf: (#exit 33 #);
          prim: (#exit 34 #);          
          innerSy: (# exit 35 #);
          
          charSy: (# exit 36 #);
          string: (# exit 37 #);
          alternative: (# exit 38 #);
          thisObj: (# exit 39 #);
          comBegin: (# exit 40 #);
          
          inArg: (# exit 41 #);
          outArg: (# exit 42 #);
          
          floatConst: (# exit 43 #);
          eq: (#exit 50#);
          lt: (#exit 51#);
          le: (#exit 52#);
          gt: (#exit 53#);
          ge: (#exit 54#);
          ne: (#exit 55#);          

          add: (#exit 61#);
          sub: (#exit 62#);

          orr: (#exit 63#);
          xorr: (#exit 64 #);
          
          mult: (#exit 66 #);
          rdiv: (#exit 67 #);
          idiv: (#exit 68 #);
          modd: (#exit 69#);
          andd: (#exit 70#);
          nott: (# exit 71 #);
          
          component: (#exit 73 #);
          strucVar: (#exit 74 #);
          nonTbegin: (# exit 75 #);
          nonTend: (# exit 76 #);
          adr: (#exit 77 #);
          frag: (# exit 78 #);
          ptnModule: (# exit 79 #);
          illegalCh: (# exit 81 #);
          eos: (# exit 82 #); 
       #);
     tokenBuf: @
       (# B: [5] @ integer; 
          Sy: [5] ^text;
          L,R: @ integer;
          add:
            (# T: @integer; S: ^text
            enter(T,S[])
            do (if (R + 1 -> R) > B.range then 
                   B.range -> B.extend;
                   Sy.range -> Sy.extend
               if);
               T -> B[R];
               S[] -> Sy[R][]
            #);
          get:
            (# T: @integer; S: ^Text
            do B[L + 1 -> L] -> T;
               Sy[L][] -> S[]
            exit(T,S[])
            #);
          getTokenNo:
            (# n: @integer
            enter n
            exit B[L + n]
            #);
          isEmpty: booleanValue(#do (if L = R then 0 -> L -> R; true -> value if) #)
       #);
     inDecl,second: @boolean;
     readNextToken:
       (# hasComment: @boolean;      
       do nextCh.pos -> oldPos; false -> hasComment;
          (if inDecl then true -> second if);
          &text[] -> sym[]; (* just for debugging purposes
                             * - to ensure that sym is empty
                             * if lexem is not idf, const, char, text or special
                             *)
          L:
            (if ch
             // '{' then
                    symbols.begin -> symb;
                    nextCh
                  // '}' then
                symbols.end -> symb;
                nextCh
             // '(' then 
                symbols.leftBrack -> symb;
                nextCh;
             // ')' then symbols.rightBrack -> symb; nextCh;
             // ':' then 
                nextCh;
                (if ch 
                 // '=' // '-'then
                    ':' -> special.read
                 // '<' then 
                    symbols.virtual -> symb;
                    nextCh;
                    true -> inDecl
                 // ':' then
                    nextCh;
                    (if ch = '<' then
                        nextCh;
                        symbols.further -> symb
                     else
                        symbols.final -> symb
                    if);
                    true -> inDecl
                 else
                    (if ch -> ascii.isLetter then
                        ':' -> readName;
                        symbols.fatcomma -> symb
                     else
                        symbols.colon -> symb;
                        true -> inDecl
                    if)
                if)
             // '=' then
                (if inDecl then
                    nextCh;
                    (if ch 
                     // '$' then 
                        symbols.constVal -> symb; nextCh
                     // '^' then
                        symbols.constRef -> symb; nextch
                     else
                        symbols.constRef -> symb
                    if)
                 else
                    nextCh;
                    '=' -> special.read
                if)
             // '?' then 
                nextCh;
                (if ch 
                 // '$' then 
                    symbols.varVal -> symb; nextCh
                 // '^' then 
                    symbols.varRef -> symb; nextch
                 else
                    symbols.varRef -> symb
                if)
             // ',' then symbols.comma -> symb; nextCh
             // ';' then symbols.semiColon -> symb; nextCh
             // '.' then symbols.dot -> symb; nextCh
             // '-' then
                nextCh;
                (if ch 
                 // '>' then 
                    symbols.value -> symb; nextCh;
                    '\n*** got value' -> putline;
                    5 -> printlines;
                 // '-' then                                    
                    Skipcomment; (* nextCh moved to skipComment to avoid
                                  * onlyWhiteSpace being set to false.
                                  * Will happen for: "   --snorf"
                                  * but not for: "   -- snorf"
                                  *)
                    (* We need to call skipBlanks at outer level *)
                    (if isXbeta then
                        true -> hasComment
                     else
                        true -> hasComment
                    if)
                 else 
                    '-' -> special.read
                if)
             // '>' then
                nextCh;
                (*(if ch = '>' then
                    symbols.inArg -> symb; nextCh;
                 else*)
                    '>' -> special.read
                (*if)*)
             // '<' then
                nextCh;
                (*(if ch // '-' // '<' then
                    symbols.outArg -> symb; nextCh;
                 else*)
                    '<' -> special.read
                (*if)                *)
             // '§' then
                nextCh;
                SkipComment;
                (if isXbeta then
                    true -> hasComment
                 else
                    true -> hasComment;
                if)
             // '[' then
                nextCh;
                symbols.leftSquare -> symb
             // ']' then 
                nextCh;
                symbols.rightSquare -> symb   
             // '@' then
                nextCh;
                (if (ch -> ascii.isLetter) then
                    '@' -> readName;
                    (if '@include' -> sym.equalNCS then
                        symbols.include -> symb
                     else 
                        symbols.keyword -> symb
                if)if)
             // '%' then
                nextCh;
                (if (ch -> ascii.isLetter) then
                    '%' -> readName;
                    symbols.property -> symb;
                 else
                    '%' -> special.read
                if);
             // '#' then
                nextCh;
                (if (ch -> ascii.isLetter) or (ch = '%') or (ch = '@')then
                    '#' -> readName;
                    symbols.idf -> symb
                if)
             // '\'' then 
                nextch; 
                (if ch = '\\' then
                    ch -> sym.put;
                    nextch                    
                if);
                ch -> sym.put;
                nextCh;
                (if ch = '\'' then 
                    symbols.charSy -> symb
                 else
                    symbols.illegalCh -> symb
                if);
                nextCh
             // '"' then
                readText -> symb; nextCh   
             // 255 then symbols.eos -> symb
             else
                (if (ch -> ascii.isLetter) or ( ch-> ascii.isDigit) or (ch = '_') then
                    ReadName
                 else
                    (if (ch -> special.in) then
                        special.read
                     else
                        '\nIllegal ch: ' -> puttext; ch -> putint; ' ' -> put; 
                        ch -> put; newline;
                        symbols.illegalCh -> symb;
                        nextCh
            if)if)if);
          (if second then false -> second -> inDecl if)
       exit hasComment
       #);
     nextTokens:
       (# inOut,cSymb: @integer
       do indent.skipBlanks -> inOut;
          (*'<'->put; inout -> putint; '>'->put;*)
          (if inOut 
           // 0 then (* continue on current line *)
              (if readNextToken then
                  (* readNextToken parsed a comment *)
                  restart nextTokens
              if);
              (symb,sym[]) -> tokenBuf.add;
           // 1 then (* indent *)
              symbols.begin -> symb;
              (symb,sym[]) -> tokenBuf.add;
           // maxInt then (* new line, but same block level *)
              (if readNextToken then
                  (* comment *)
                  restart nextTokens
              if);
              (if symb <> symbols.fatComma then
                  (symbols.semicolon,'') -> tokenBuf.add
              if);
              (symb,sym[]) -> tokenBuf.add; 
           else (* outdent -inOut levels *)
              (* if nextToken is comment, then skipComment and restart *)
              (if readNextToken then 
                  (* got comment *)
                  -inOut -> indent.revert;
                  restart nextTokens
              if);
              (if inOut < 0 then
                  (for i: - inOut repeat (symbols.end,'') -> tokenBuf.add for);
                  (if symb <> symbols.fatComma then
                      (symbols.semicolon,'') -> tokenBuf.add
                  if);
                  (symb,sym[]) -> tokenBuf.add;
              if)
          if)
       #) ;
     peekNext:
       (* read and return next token
        * put token in tokenBuf
        * keep current value of sym[]
        * peeked token sym[] should also be buffered
        *)
       (# T: @integer; 
          currentSymb: @integer;
          currentSymT: ^text;
          n: @integer
       enter n
       do symb -> currentSymb;
          sym[] -> currentSymT[];
          (for i: n repeat 
               nextTokens
          for);
          n -> tokenBuf.getTokenNo -> T;
          currentSymb -> symb;
          currentSymT[] -> sym[]
       exit T
       #);
  do (if not tokenBuf.isEmpty then 
           tokenBuf.get -> (symb,sym[])
      else
         nextTokens;
         tokenBuf.get -> (symb,sym[])
     if);
  #)
