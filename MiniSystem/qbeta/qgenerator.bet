ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qsemchecker';
INCLUDE '../miniCompiler/objectcode'
---lib:attributes---
generator: semchecker
  (# traceModules: (# exit false #);
     MkCom: 
       (# t1,t2: ^text enter (t1[],t2[]) do t2[] -> T1.append -> cd.comment #);
     
     Module::
       (# gen::
            (#
            do ('Comp',newDesc.noOfDescs) -> cd.newComponent;
               SO.gen;
            #);
            findNextImp:: (# do bcp -> SO.OG.IS.findNextImp -> impx[] #);
       #);
     ModuleItem::
       (# gen::
            (#
            do 
               (if MD.isIncluded then
                   ('ModuleItem:gen',sig.dopt) -> cd.com2;
                   (useReturnValue,superAdj,staticOff) -> MD.SO.gen
                else
                   (if traceModules then
                       'Module not included: ' -> puttext; sig.doPT -> putline
               if)if);

            #);
          pushThis:: (# do cd.pushThis #);
          load:: (# do '\nModuleItem:load'->putline #);
          store:: (# do '\nModuleItem:store'->putline #);
          pushOff:: (# do MD.SO.pushOff #);
          popReturn:: (# do '\nModuleItem:popReturn'->putline #);
          findNextImp:: (# do bcp -> md.findNextImp -> impx[] #);          
       #);
     DataItem::
       (* Cases
        *    C1: = integer(12)
        *    C2: = integer
        *    C3: = Fool
        *    C4: = Fool -- singular
        *       ...
        *    V1: ? integer(12)
        *    V2: ? integer
        *    V3: ? Fool
        *    V4: ? Fool -- singular
        *       ...
        *)
       (# gen::
            (#
            do (if true 
                // OG.isSimpleValue then
                   (* C1,C2,V1,V2 *)
                   (if true 
                    // 'Function' -> ((OG.super.last).label).equal
                    // 'Binary' -> ((OG.super.last).label).equal
                    // 'Const:' -> ((OG.super.last).label).equal
                    // 'String:' -> ((OG.super.last).label).equal
                       then
                       (* pretty ad hoc - to handle 
                        * X: = integer(7)
                        * X: ? 7
                        * X: = a + b
                        * and avoid calling OG.gen for 
                        * X: = integer
                        * Cf the discussion of #foo versus foo 
                        *)
                       (true,0,this(DataItem).off) -> OG.gen;
                       cd.pushThis;
                       (sig.id[],this(DataItem).off) -> cd.storeg
                   if);
                // (OG.primNo = String_prim) and isConst then
                   (true,0,this(DataItem).off) -> OG.gen;
                   cd.pushThis;
                   (sig.id[],this(DataItem).off) -> cd.rstoreg
                else
                   (* C3,C4,V3,V4 *)
                   (if isConst or not OG.IS.isEmpty then
                       (* C3,C4,V4 *)
                       (if origin[] <> none then 
                           ('DataItem::',sig.doPT) -> cd.com2; 
                           cd.bcPos -> beginBCP;
                           (false,0,this(DataItem).off) -> OG.gen;
                           cd.bcPos -> endBCP;
                        else 
                           (* A module object - why special handling? 
                            * Apparenly cd.bcPos is not defined here!?
                            *)
                           (if not OG.IS.isEmpty then
                               (sig.id[],none,OG[],OG.super[]) -> ObjTmpStack.add
                           if)   
                       if) 
                       (* else V3 - just a qualifcation 
                        * -cf the discussion of #foo versus foo *)
                   if)
               if)
            #);
          pushThis:: (# do cd.pushThis #);
          load::
            (* called when not receiver - i.e. value to be pushed *)
            (#
            do (if OG.isSimpleValue then 
                   (sig.id[],off) -> cd.pushg
                else
                   'DataItem:load' -> cd.comment;
                   (sig.id[],off) -> cd.rpushg
               if)               
            #);
          pushOff::
            (#
            do (if OG.isSimpleValue then 
                   ('DataItem:pushOff:isBasic:',sig.id[]) -> cd.com2;
                   (if next[] <> none then
                       ('SO:pushOff:',next.doP) -> cd.com2;
                       (if next.ATd.primNo = cmpAndSwap_prim then 
                           off -> cd.pushConst
                       if)
                   if);
                   (* May happen for V := 117 where := is assignment 
                    * - no offset
                    * shall be pushed for V since V is assigned afterwards
                    * For R ++ 117 offset for R should be pushed since it
                    * is origin
                    *)
                else
                   (sig.id[],off) -> cd.rpushg
               if)
            #);
          store::
            (* Called from Signature::StoreParameters *)
            (#
            do (* 'DataItem:store: '->putline;*)
               (if OG.isSimpleValue then 
                   (sig.id[],off) -> cd.storeg
                else
                   (if not noSwap then cd.rswap if); 
                   (sig.id[],off) -> cd.rstoreg
               if)
            #);
          popReturn::
            (#
            do (if OG.isSimpleValue then 
                   cd.vpop
                else
                   cd.rpop
               if)
            #)          
       #);
     Signature::
       (# 
          storeParameters:
            (#
            do (*cd.setThisStack;*)
               (* 'storeParameters: ' -> puttext;*)
               scanParametersReverse
               (# D: ^Decl
               do (*currentPar.dopt -> puttext; ' ' -> put;*)
                  (if not (currentPar[] -> D[]).isVirtual then
                      cd.pushThis;
                      true -> currentPar.store
                  if)
               #)
            #);
          pushReturnValues:
            (#
            do (if val[] <> none then
                   cd.setThisStack;
                   val.scanSons
                   (# D: ^Decl
                   do current[] -> D[];
                      cd.pushThis;
                      D.load
                   #)
                else
                   cd.setThisStack;
                   cd.pushThis;
               if)
            #);
          popReturn:
            (# 
            do (if val[] <> none then
                   val.scanSons
                   (# D: ^Decl
                   do (current[] -> D[]).popReturn
                   #)
                else
                   cd.rpop
               if)
            #)
       #);
     ObjectGenerator::
       (# gen::
            (# name,T,T1: ^text
            do (*('OG: ',OI.inv.compoundSymb[]) -> MkCom;*)
               (*IS.inv.ATd.doPT -> putline*)
               'OG:staticOff: '-> T[]; staticOff -> T.putint; 
               
               cd.bcPos -> beginBCP;
               (*'OG:beginBCP: '->puttext;
                beginBCP -> putint; newline;
                'beginBCP: '->T1[];
                cd.bcPos -> T1.putint;
                T1[] -> cd.comment;*)
               'OG:gen:'->cd.comment;
               super.doPT -> cd.comment;
               IS.doPT -> cd.comment;
               (if IS.isEmpty 
                   and not super.hasVirtualArgs 
                   or (super.isBasic and IS.isEmpty) then
                   (* check this - called when R.foo *)
                   ':empty' -> T.append;
                   T[] -> cd.comment;
                   (useReturnValue,superAdj,staticOff) -> super.gen
                else
                   (* This also includes pattern invocations with virtual arguments
                    * like %if .. %then% ... %else ...
                    *)
                   ':singular' -> T.append;
                   T[] -> cd.comment;                   
                   (* called when R.foo{ } *)
                   (* (if super.hasVirtualArgs then
                       'ObjectGenerator:gen:hasVirtualArgs: ' -> putline;
                       super.doP -> putline;
                       'descNo: ' -> puttext; IS.descNo -> putint; newline;
                       'SuperDesc: ' -> putline; IS.superDesc.doP -> putline;
                       'Super.descNo: ' -> puttext;
                       IS.superDesc.descNo -> putint; 
                       '\n---'->putline
                    if);*)
                   cd.pushThis; (* origin for a singular object *)
                   (if singularName[] <> none then
                       singularName[] -> name[]
                    else
                       (if super.ATd[] = none then 
                           '' -> name[]
                        else
                           Super.ATd.sig.id.copy -> name[]
                       if);
                       '#S#' -> name.append;
                       IS.descNo -> name.putint;
                   if);
                   (if primNo = indexed_prim then
                       '\n*** InvokeIndexed'->putline
                   if);
                   ('OG:invoke:singular',doPT) -> cd.com2;
                   (* missing load args ?
                    * See genclass wich may load the argumenst
                    *)
                   (name[],IS.descno,staticOff,true) -> cd.invoke;

                   (if not useReturnValue  then cd.rpop if);
                   
                   (name[],none,this(ObjectGenerator)[],super[]) -> ObjTmpStack.add
               if);
               cd.bcPos -> endBCP;
               (* 'OG:endBCP: '->puttext; label -> puttext; ' ' -> put;
                endBCP -> putint; newline;*)
            #);
          genClass::
            (#
            do (N[],sig[],IS.descNo,super[]) -> IS.genClass
            #)
       #);
     PatternId::
       (# gen::
            (#
            do 'PatternId:gen: ' -> puttext; dopt -> putline;
               (useReturnValue,superAdj,staticOff) -> IV.gen
            #)
       #);
     items::
       (# genClass:
            (# N: ^text; sig: ^Signature; descNo: @integer; super: ^Invocation;
               T: @text; ptn: ^Pattern;
               vSuperDesc: ^Items;
               nd: ^Node
            enter(N[],sig[],descNo,Super[])
            do (*'GenClass: ' -> puttext; doPT -> putline;*)
               (if (superDesc[] <> none) then
                   superDesc.father.father -> nd[];
                   (*'Super: ' -> puttext; nd.doPP->putline;*)
                   nd[] -> ptn[];
                   (if not superDesc.done then
                       (* 'HasSuper: ' -> puttext; *)
                       (* super is always a pattern - I think!*)
                       (# 
                       do (ptn.sig.id[],ptn.sig[],superdesc.descNo,ptn.OD.super[]) 
                            -> superDesc.genClass
                       #)
               if)if);
               (if not done then
                   true -> done;
                   (N[],descNo,false, (topSuper).descNo,originOff) -> cd.class;
                   (* thisObj = the new instance of this class 
                    * thisStack = caller
                    * Possible arguments on callerStack
                    *)
                   cd.bcPos -> beginBCP;
                   (* store parameters + origin *)
                   (if sig[] <> none then sig.storeParameters if);
                   ('origin',originOff) -> cd.rstore;
                   not doHasCode -> cd.allocEventQ;
                   
                   (if (superDesc[] <> none) and not superDesc.isBasic
                       or (superDesc[] <> none) and (superDesc.primNo = stringDecl.primNo) then
                       (ptn.sig.id[],superDesc.descNo)  -> cd.super; 
                       superDesc.descNo -> cd.vdtTableCopy;
                       (if superDesc.innerInx > 0 then
                           (superDesc.innerInx,descno) -> cd.vdtTable;
                           true -> hasSuperAndInner
                       if);
                       (* push super arguments *)
                       
                       (if superApl.isImplSuper then
                           (*'Implicit super:\n' -> puttext; doP -> putline;*)
                           (if superApl.isSingularImplSuper then
                               cd.pushThis;
                               ('iOrigin',originOff) -> cd.rpushg
                            else
                               (*'Non singular Implict super:\n'->puttext;
                               doP -> putline;*)
                               superApl.ATd[] -> ptn[];
                               origin[] -> vSuperDesc[]; (* find desc enclosing vDcl *)
                               (for i: superApl.pn repeat
                                    vSuperDesc.superDesc[] -> vSuperDesc[]
                               for);
                               (*'vSuperDesc: ' -> putline; 
                                * (0,true) -> vSuperDesc.doP -> putline;*)
                               cd.pushThis;
                               ('origin',originOff) -> cd.rpushg; 
                               ptn.OD.super.on -> vSuperDesc.goOrigin
                           if)                                   
                        else
                           ('super:gen:pushArguments',super.doPT) -> cd.com2;
                           (* This has to be revised:
                            * Is close to super.gen, but sup.gen executes super
                            * and super shall not be executed here
                            *)
                           (# E,rec: ^Exp;
                              loadOrigin:
                                (# superAdj: @integer; E,rec: ^Exp
                                enter superAdj
                                do S.scanSons
                                   (#
                                   do current[] -> E[];
                                      ('loadOrigin:',E.doPT) -> cd.com2;
                                      (if isFirst then 
                                          superAdj -> E.loadOrigin 
                                      if);
                                      (if not isLast then
                                          E[] -> rec[];
                                          (*(rec[],useReturnValue,staticOff,superAdj,Ex[]) *)
                                          (rec[],true,0,superAdj,none) 
                                          -> E.pushOff;

                                      if);
                                   #);
                                exit(rec[],E[])
                                #);
                              S: ^Invocation; BE: ^BracketedExp
                           do L: super.scanSons
                                (* this is ugly - but necessary since we
                                 * cannot just call super.gen
                                 *)
                                (#
                                do (if current## = BracketedExp## then
                                       current[] -> BE[];
                                       BE.IV[] -> S[];
                                    else
                                       super[] -> S[]
                                   if);
                                   leave L
                                #);
                              L:
                                S.scanSons 
                                (# E: ^Exp;
                                do current[] -> E[];
                                   ('arg:',E.doP) -> cd.com2;
                                   E.pushThis;
                                   leave L
                                #);
                              originOff -> loadOrigin -> (rec[],E[]);
                              originOff -> E.loadArgs 
                           #);
                       if);
                       superDesc.descNo -> cd.toSuper;
                       cd.markEnterE;  
                   if); 
                   (if descNo = 1 then
                       cd.pushThis;
                       cd.saveBETAworld
                   if);
                   (if (superDesc[] <> none) and not superDesc.isBasic  then
                    else cd.mvStack
                   if);
                   scanSons
                   (# I: ^Item
                   do current[] -> I[];
                      (if I.isDecl then (false,0,0) -> current.gen if) 
                   #);
                   (if innerInx > 0 then innerInx -> cd.innerA if);
                   (if (superDesc[] <> none) and not superDesc.isBasic  then
                       cd.rtnInner 
                    else
                       (if doHasCode then 
                           1 -> cd.rtnEventQ 
                        else
                           0 -> cd.rtnEventQ 
                       if)
                   if);
                   
                   (* Code for action part *)
                   (false,doHasCode) -> cd.actions;
                   restartLab.new;
                   leaveLab.new;
                   restartLab -> cd.defLab;
                   scanSons
                   (# I: ^Item; 
                   do current[] -> I[];
                      (if not I.isDecl then (false,0,0) -> current.gen if) 
                   #);
                   leaveLab -> cd.defLab;
                   (* we must push return values *)
                   (* push return value 
                    * - for DataItem: R: @Foo
                    *   Foo should return (this(Foo)
                    *   perhaps default?
                    * - otherwise push variables in -> 
                    *)
                   (if (superDesc[] <> none) and not superDesc.isBasic then
                    else
                       0 -> cd.rtnEventQ;
                       (if sig[] <> none then 
                           sig.pushReturnValues 
                        else
                           (* ad hoc *)
                           cd.setThisStack;
                           cd.pushThis
                   if)if);

                   (if (superDesc[] <> none) and not superDesc.isBasic then
                       cd.rtnInner
                    else
                       (*0 -> cd.rtnEventQ;*)
                       'D' -> cd.rtn;
                   if);
                   (vsize,rsize,1) -> CD.endClass;
                   cd.bcPos -> endBCP
               if)
            #);
          goOrigin:
            (# on: @integer; org: ^Items
            enter on
            do (if on > 0 then
                   (if basicNo = ifthen_prim then
                       on  -> (origin[]->org[]).goOrigin -> org[]
                    else
                       ('origin',originOff) -> cd.rpushg;
                       on - 1 -> (origin[]->org[]).goOrigin -> org[]
                   if)
                else
                   this(Items)[] -> org[]
               if)
            exit org[]
            #); 
          hasSuperAndInner: @Boolean;
          restartLab,leaveLab: @LabelHandler.label;

          findNextImp::
            (#
            do L:
                 scanSons
                 (#
                 do (if (bcp -> current.findNextImp -> impx[]) <> none then
                        leave L
                 if)#);
            #)
            
       #);
     Pattern::
       (#  gen:: 
            (# 
            do (if OD.primNo
                // 0 // string_prim // indexed_prim then
                   (if ptnKind 
                    // plainPtn then
                       (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add 
                    // virtualPtn then
                       (if OD.IS.isEmpty then
                           (off,(OD.super.desc).descNo) -> cd.vdtTable
                        else
                           (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add;
                           (off,OD.IS.descNo) -> cd.vdtTable
                       if)                            
                    // furtherPtn // finalPtn then
                       (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add;
                       (off,OD.IS.descNo) -> cd.vdtTable
                   if)
                // resume_prim then
                   ('Resume','D') -> cd.call;                   
               if)
            #);
          pushThis:: 
            (# T: @text
            do ('Pattern:pushThis',sig.dopt) -> cd.com2;
               (*OD.primNo -> T.putint; T[] -> cd.comment;*)
               (if OD.primNo
                // 0 
                // indexedGet_prim (* not tested*)
                // indexedGet_prim (* ditto *)
                // stringGet_prim 
                // indexed_prim then
                   cd.pushThis 
                else (* ugly *)
                   (if ptnKind // virtualPtn // furtherPtn // FinalPtn then
                       cd.pushThis 
                   if)
               if)
            #);
          loadArgs::
            (# args: [6]^ObjectGenerator; top: @integer;
            do E.args.scanArgs
               (#
               do (if (top+1 -> top) > args.range then 
                      args.range -> args.extend 
                  if);
                  current[] -> args[top][];
               #);
               sig.args.scanArgs
               (# D: ^Decl; i: @integer; 
                  OG: ^ObjectGenerator; name: ^text;
               do i + 1 -> i; 
                  current[] -> D[]; 
                  (if D.isVirtual then
                      D.sig.id.copy -> name[];
                      '#SB#' -> name.append;
                      (args[i].desc).descno -> name.putint;
                      (name[],none,args[i][],args[i].super[]) 
                        -> ObjTmpStack.add;
                      E.father.father[] -> OG[]; (* need OG.descNo for VDT *)
                      (D.off,(args[i].desc).descno,(OG.desc).descNo) 
                        -> cd.vdtTableAt
                   else
                      (true,superAdj,0) -> args[i].gen
                  if);
               #)
            #);
          pushOff::
            (#
            do ('*** pattern:pushOff:',sig.doPT) -> cd.com2;
               '*** pattern:pushOff:' -> puttext; sig.doPT -> putline;
               (if od.primNo = 0 then 
                   (rec[],superAdj) -> loadArgs; 
                   (rec[],true(*useReturnValue*),staticOff) -> load 
               if)
            #);
          load::
            (# org: ^Items; exitLab: @labelHandler.label;
               args: [6]^ObjectGenerator; top: @integer;
               hasVirtualArgs: @boolean
            do ('Pattern:load: ',sig.doPT) -> cd.com2; 
               (if OD.primNo
                // ifthen_prim then
                   exitLab.new;                   
                   E.args.scanSons
                   (# N: @integer
                   do N + 1 -> N;
                      (if N
                       // 1 // 3 then
                       // 2 then 
                          (true,0,staticOff) -> current.gen;
                          exitlab -> cd.jmpFalse;
                       // 4 then
                          current.gen;
                          exitLab -> cd.defLab;
                       else
                          'load:%if%then: too many arguments' -> putline;
                          E.doPt -> putline
                      if)
                   #);   
                // this_prim then
                   (* Must check that argument is the name of an enclosing 
                    * object or the name of the superpattern of an enclosing
                    * object 
                    *)
                   E.args.scanArgs
                   (# A: ^ObjectGenerator; org: ^Items
                   do current[] -> A[]; 
                      cd.pushThis;
                      A.super.on - 1 -> (E.origin[]->org[]).goOrigin; 
                   #);
                // inner_prim then
                   (if E.args.length > 0 then
                       E.args.scanArgs
                       (# A: ^ObjectGenerator; org: ^Items
                       do current[] -> A[]; 
                          cd.pushThis;
                          A.super.on - 1 -> (E.origin[]->org[]).goOrigin -> org[]; 
                          org.innerInx -> cd.innerP; 
                          cd.rpopThisObj
                       #)
                    else
                       E.origin[] -> org[];
                       org.innerInx -> cd.innerx; 
                   if)
                // indexedPut_prim then
                   (* scan args and push index and element *)
                   E.args.scanSons(#do (true,0,staticOff) -> current.gen #);
                   '%put%at!' -> cd.comment;
                   ('inx',1) -> cd.xstoreg
                // indexedGet_prim // stringGet_prim then
                    E.args.scanSons(#do (true,0,staticOff) -> current.gen #);
                   '%get' -> cd.comment;
                   ('inx',1) -> cd.xpushg
                // none_prim then 
                   cd.pushNone
                // suspend_prim then
                   cd.suspendd
                // resume_prim then
                   ('Resume','D') -> cd.call;
                   cd.rpop
                // disable_prim then
                   ('disable',disable_prim) -> cd.callPrim
                // enable_prim then  
                   ('enable',enable_prim) -> cd.callPrim   
                // put_prim then 
                   ('put',OD.primNo) -> cd.Callprim
                // attach_prim then
                   ('attach',OD.primNo) -> cd.callPrim;
                   cd.rPop;
                // cmpAndSwap_prim then
                   (# I: ^Invocation
                   do E.father -> I[];
                      L:
                        I.scanSons
                        (# R: ^Exp
                        do current[] -> R[];
                           cd.swap;
                           ('cmpAndSwap',OD.primNo ) -> cd.callPrim;
                           leave L
                        #)
                   #)
                // fork_prim then
                   ('fork',OD.primNo ) -> cd.callPrim
                // sleep_prim then
                   ('sleep',OD.primNo ) -> cd.callPrim;
                // indexed_prim then
                   (*100 -> cd.pushConst; -- range - pushed as argument *)
                   0 -> cd.pushConst;   (* isRindexed *)
                   OD.IS.rsize -> cd.pushConst; (* dinx *)
                   ('StaticRep',OD.IS.descNo,false (*OD.isRindexed*),true) 
                     -> cd.allocIndexed;
                   cd.pushThis;
                   ('indexed',staticOff) -> cd.rstoreg              
                else
                   (if (sig.type ## = Sig.Binary##) and (OD.primNo > 0) then
                       OD.primNo -> cd.op
                    else
                       (if true and (sig.type## = sig.KeyWord##) then
                           (* 'KWP:load: ' -> puttext; sig.dopt -> putline;*)
                           E.args.scanArgs
                           (# 
                           do (*'Actual arg: ' -> puttext; current.doP -> putline;*)
                              (if (top+1 -> top) > args.range then 
                                  args.range -> args.extend 
                              if);
                              current[] -> args[top][]
                           #);
                           sig.args.scanArgs
                           (# D: ^Decl; i: @integer
                           do i + 1 -> i;
                              current[] -> D[]; (*D.doP -> putline;*)
                              (if D.isVirtual then
                                  true -> hasVirtualArgs
                               else
                                  args[i].gen
                              if);
                           #);
                           (if hasVirtualArgs then 
                               '\n>>> Has Virtual arguments'->putline;
                               (* father.father[] -> OG[];
                                ('KeyWord',none,OG[],OG.super[]) -> ObjTmpStack.add*)
                            else
                               (sig.id[],OD.IS.descno,staticOff,false) -> cd.invoke;
                           if)
                       else            
                       (if ptnKind
                        // virtualPtn // furtherPtn // finalPtn then
                           (sig.id[],off) -> cd.sendVirtual;
                        else
                           (*'Invoke: '-> puttext; sig.doPT->puttext;
                           ' ' -> put; OD.primNo -> putint;
                            (if isBasicPtn then 
                            *    ' isBasic' -> putline else newline 
                            * if)
                            * ;*)
                           (if not isBasicPtn then
                               (sig.id[],OD.IS.descno,staticOff,not OD.IS.doHasCode) 
                                 -> cd.invoke;
                           if)
                       if)if);
                       (if not useReturnValue then sig.popReturn if)
               if)if)
            #);          
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #);   
       #);
     UnaryPattern::
       (# gen:: (# do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          loadArgs::
            (#
            do E.args.scanArgs
               (#
               do (true,superAdj,0) -> current.gen 
               #)
            #);
          load::
            (#
            do ('UnaryPattern:load: ',sig.doPT) -> cd.com2; 
               (sig.id[],OD.IS.descno,staticOff,not OD.IS.doHasCode) -> cd.invoke;
               (if not useReturnValue then
                   sig.popReturn
                   (* reconsider if return values *)
               if)               
            #);
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #);   
       #);
     BasicUnaryPattern::
       (# gen::
            (#
            do (if OD.primNo = string_prim then
                   (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add
               if)
            #);
          loadArgs::
            (#
            do E.args.scanArgs
               (#
               do (true,superAdj,0) -> current.gen 
               #)
            #);
          load::
            (# org: ^Items
            do (if OD.primNo
                // inner_prim then
                   E.origin[] -> org[];
                   org.innerInx -> cd.innerx; 
                // none_prim then 
                   cd.pushNone
                // suspend_prim then
                   cd.suspendd
                // resume_prim then
                   ('Resume','D') -> cd.call;
                   cd.rpop
                // disable_prim then
                   ('disable',disable_prim) -> cd.callPrim
                // enable_prim then  
                   ('enable',enable_prim) -> cd.callPrim   
                // indexed_prim then
                   'load indexed: ' -> cd.comment
               if);
            #);
       #);
     VirtualPattern::
       (# gen::
            (#
            do(* 'Gen Virtual: ' -> puttext; sig.doPT -> putline;*)
               (if OD.IS.isEmpty then
                   (*  'Empty virtualPTN: ' -> putline;*)
                   (off,(OD.super.desc).descNo) -> cd.vdtTable
                else
                   (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add;
                   (off,OD.IS.descNo) -> cd.vdtTable
               if)
            #);
          pushThis:: (# do cd.pushThis #);
          load::
            (#
            do ('VirtualPtn:load: ',sig.doPT) -> cd.com2; 
               (sig.id[],off) -> cd.sendVirtual;
               (if not useReturnValue then
                   sig.popReturn
                   (* reconsider if return values *)
               if);               
            #);
          store::
            (#
            do 'VirtualPattern:store: '->puttext; doPT -> putline;
            #);          
       #);
     FurtherPattern::
       (# gen:: (# do '\n\n**** Further pattern is not implemented!\n' -> putline #);
       #);
     FinalPattern::
       (# gen::
            (#
            do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add;
               (off,OD.IS.descNo) -> cd.vdtTable
            #);
          pushThis:: (# do cd.pushThis #);
          load::
            (#
            do (*'Load Final: ' -> puttext; doPt -> putline;*)
               ('FinalPtn:load: ',sig.doPT) -> cd.com2; 
               (sig.id[],off) -> cd.sendVirtual;
               (if not useReturnValue then
                   sig.popReturn
                   (* reconsider if return values *)
               if);               
            #);
          store::
            (#
            do 'FinalPattern:store: '->puttext; doPT -> putline;
            #);          
       #);          
     BinaryPattern::
       (# gen:: (# do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add #);
          load::
            (#
            do ('BinaryPattern:load: ',sig.doPT) -> cd.com2;
               (sig.id[],OD.IS.descno,staticOff,false) -> cd.invoke;
               (if not useReturnValue then
                   sig.popReturn
               if);               
            #);
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #); 
       #);
     BasicBinaryPattern::
       (# load:: 
            (# 
            do OD.primNo (*OD.IS.props.basicProp.primno*) -> cd.op
            #);          
       #);
     FunctionalPattern::
       (# gen:: (# do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          loadArgs::
            (# args: [6]^ObjectGenerator; top: @integer;
            do E.args.scanArgs
               (#
               do (if (top+1 -> top) > args.range then 
                      args.range -> args.extend 
                  if);
                  current[] -> args[top][];
               #);
               sig.args.scanArgs
               (# D: ^Decl; i: @integer; OG: ^ObjectGenerator; name: ^text;
               do i + 1 -> i; 
                  current[] -> D[]; 
                  (if D.isVirtual then
                      D.sig.id.copy -> name[];
                      '#SB#' -> name.append;
                      (args[i].desc).descno -> name.putint;
                      (name[],none,args[i][],args[i].super[]) 
                        -> ObjTmpStack.add;
                      E.father.father[] -> OG[]; (* need OG.descNo for VDT *)
                      (D.off,(args[i].desc).descno,(OG.desc).descNo) 
                        -> cd.vdtTableAt
                   else
                      (true,superAdj,0) -> args[i].gen
                  if);
               #);               
            #);          
          load:: 
            (# 
            do ('FunctionalPattern:load: ',sig.doPT) -> cd.com2;
               (if primNo = indexed_prim then
                   '\n***FP:invoke indexed'-> putline
               if);
               (sig.id[],OD.IS.descno,staticOff,false) -> cd.invoke;
               (if not useReturnValue then
                   sig.popReturn
               if);               
            #);
       #);
     BasicFunctionalPattern::
       (# gen:: 
            (# 
            do (if primNo 
                // indexed_prim // string_prim then
                   (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add 
                // resume_prim then
                   ('Resume','D') -> cd.call;
               if)
            #);
          pushThis:: (# do (if primNo = indexed_prim then cd.pushThis if) #);
          loadArgs::
            (#
            do E.args.scanArgs
               (#
               do (true,superAdj,0) -> current.gen 
               #)
            #);
          load:: 
            (# 
            do (if OD.primNo
                // integer_prim then
                // put_prim then 
                   ('put',OD.primNo) -> cd.Callprim
                // attach_prim then
                   ('attach',OD.primNo) -> cd.callPrim;
                   cd.rPop;
                // cmpAndSwap_prim then
                   (# I: ^Invocation
                   do E.father -> I[];
                      L:
                        I.scanSons
                        (# R: ^Exp
                        do current[] -> R[];
                           cd.swap;
                           ('cmpAndSwap',OD.primNo ) -> cd.callPrim;
                           leave L
                        #)
                   #)
                // fork_prim then
                   ('fork',OD.primNo ) -> cd.callPrim
                // sleep_prim then
                   ('sleep',OD.primNo ) -> cd.callPrim;
                // indexed_prim then
                   (*100 -> cd.pushConst; -- range - pushed as argument *)
                   0 -> cd.pushConst;   (* isRindexed *)
                   OD.IS.rsize -> cd.pushConst; (* dinx *)
                   ('StaticRep',OD.IS.descNo,false (*OD.isRindexed*),true) 
                     -> cd.allocIndexed;
                   cd.pushThis;
                   ('indexed',staticOff) -> cd.rstoreg
               if)
            #);
       #);
     KeyWordPattern::
       (# gen:: (# do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          loadArgs::
            (# args: [6]^ObjectGenerator; top: @integer;
            do ('KWP:loadArgs:',doPT) -> cd.com2;
               E.args.scanArgs
               (#
               do (if (top+1 -> top) > args.range then 
                      args.range -> args.extend 
                  if);
                  current[] -> args[top][];
                  ('KWP:Arg:',current.doPT) -> cd.com2;
               #);
               sig.args.scanArgs
               (# D: ^Decl; i: @integer; OG: ^ObjectGenerator; name: ^text;
               do i + 1 -> i; 
                  current[] -> D[]; 
                  (if D.isVirtual then
                      D.sig.id.copy -> name[];
                      '#SB#' -> name.append;
                      (args[i].desc).descno -> name.putint;
                      (name[],none,args[i][],args[i].super[]) 
                        -> ObjTmpStack.add;
                      E.father.father[] -> OG[]; (* need OG.descNo for VDT *)
                      (D.off,(args[i].desc).descno,(OG.desc).descNo) 
                        -> cd.vdtTableAt
                   else
                      (true,superAdj,0) -> args[i].gen
                  if);
               #);               
            #);
          load::
            (# args: [6]^ObjectGenerator; top: @integer;
               hasVirtualArgs: @boolean
            do (* 'KWP:load: ' -> puttext; sig.dopt -> putline;*)
               E.args.scanArgs
               (# 
               do (*'Actual arg: ' -> puttext; current.doP -> putline;*)
                  (if (top+1 -> top) > args.range then 
                      args.range -> args.extend 
                  if);
                  current[] -> args[top][]
               #);
               sig.args.scanArgs
               (# D: ^Decl; i: @integer
               do i + 1 -> i;
                  current[] -> D[]; (*D.doP -> putline;*)
                  (if D.isVirtual then
                      true -> hasVirtualArgs
                   else
                      args[i].gen
                  if);
               #);
               (if hasVirtualArgs then 
                   '\n>>> Has Virtual arguments'->putline;
                   (* father.father[] -> OG[];
                    ('KeyWord',none,OG[],OG.super[]) -> ObjTmpStack.add*)
                else
                   (sig.id[],OD.IS.descno,staticOff,false) -> cd.invoke;
                   (if not useReturnValue then
                       sig.popReturn
                   if)
               if)               
            #)
       #);
     BasicKeyWordPattern::
       (# pushThis::
            (#
            do ('BasicKeyWordPattern:pushThis',doPT) -> cd.com2;
               (if OD.primNo
                // indexedGet_prim (* not tested*)
                // indexedGet_prim (* ditto *)
                // stringGet_prim then
                   cd.pushThis
               if)                   
            #);
          load::
            (#  exitLab: @labelHandler.label
            do (if OD.primNo
                // ifthen_prim then 'BKP:'->cd.comment; doPT -> cd.comment;
                   exitLab.new;                   
                   E.args.scanSons
                   (# N: @integer
                   do N + 1 -> N;
                      (if N
                       // 1 // 3 then
                       // 2 then 
                          (true,0,staticOff) -> current.gen;
                          exitlab -> cd.jmpFalse;
                       // 4 then
                          current.gen;
                          exitLab -> cd.defLab;
                       else
                          'load:%if%then: too many arguments' -> putline;
                          E.doPt -> putline
                      if)
                   #);   
                // this_prim then
                   (* Must check that argument is the name of an enclosing 
                    * object or the name of the superpattern of an enclosing
                    * object 
                    *)
                   E.args.scanArgs
                   (# A: ^ObjectGenerator; org: ^Items
                   do current[] -> A[]; 
                      cd.pushThis;
                      A.super.on - 1 -> (E.origin[]->org[]).goOrigin; 
                   #);
                // inner_prim then
                   E.args.scanArgs
                   (# A: ^ObjectGenerator; org: ^Items
                   do current[] -> A[]; 
                      cd.pushThis;
                      A.super.on - 1 -> (E.origin[]->org[]).goOrigin -> org[]; 
                      org.innerInx -> cd.innerP; 
                      cd.rpopThisObj
                   #)
                // indexedPut_prim then
                   (* scan args and push index and element *)
                   E.args.scanSons(#do (true,0,staticOff) -> current.gen #);
                   '%put%at!' -> cd.comment;
                   ('inx',1) -> cd.xstoreg
                // indexedGet_prim // stringGet_prim then
                    E.args.scanSons(#do (true,0,staticOff) -> current.gen #);
                   '%get' -> cd.comment;
                   ('inx',1) -> cd.xpushg
               if);
            #);
          loadArgs:: 
            (# 
            do 'BasicKW:loadArgs'->cd.comment;
               E.args.scanSons(# do  (true,superAdj,0) -> current.gen #)
            #)
       #);            
     Invocation::
       (* E1.E2.E3
        *    E1.pushThis
        *    E1.on -> gorigin
        *    E1.pushOff
        *    E2.pushOff
        *    E3.execute
        * R.foo
        * R.foo{...}
        * X:= y + 12
        * R ++ E  -- problem E1 = R ++ E , i.e. first = true 
        * perhaps same problem for keyWord
        * We must reconsider parsing/AST and/or gen below
        *)
       (# gen:: 
            (# rec,E: ^Exp; T: ^Text
            do cd.bcPos -> beginBCP;
               (if false then
               L:
                 scanSons 
                 (#
                 do current[] -> E[];
                    'Invocation:gen:pushThis' -> cd.comment;
                    E.pushThis;
                    (* cd.pushThis does not work, 
                     * E may be a constant or basic pattern,
                     * where pushThis shall not be generated
                     *)
                    leave L
                 #);
               if);
               ('Invocation:gen:A',doPT) -> cd.com2;
               (useReturnValue,staticOff,superAdj) -> loadOrigin -> (rec[],E[]);
               
               'Invocation:gen:B:' -> T[]; superAdj -> T.putint; ':' -> T.put;
               (T[],doPT) -> cd.com2;
               (rec[],useReturnValue,staticOff,superAdj) -> E.execute;
               cd.bcPos -> endBCP
            #);
          loadOrigin:
            (# usereturnValue: @boolean;
               staticOff,superAdj: @integer;
               rec,E,nE: ^Exp;
            enter(useReturnValue,staticOff,superAdj)
            do
               scanSons
               (# Ex: ^Exp
               do current[] -> E[]; 
                  (*current.doP -> cd.comment;*)
                  (if isFirst then 
                      E.pushThis;
                      superAdj -> E.loadOrigin 
                  if); 
                  (if not isLast then
                      E[] -> rec[];
                      current.doP -> cd.comment;
                      (if not (next -> nE[]).isAssign then
                          ('next not assign:',(next).doP) -> cd.com2;
                          (next).doP -> cd.comment;
                          E.doPT -> cd.comment;
                          next -> Ex[];
                          Ex.doPT -> cd.comment;
                          (rec[],useReturnValue,staticOff,superAdj,Ex[]) -> E.pushOff (* Often calls execute *)
                      if);                      
                  if);
               #)
            exit(rec[],E[])
            #);
       #);
     adjustOrigin:
       (# originOff: @integer
       enter originOff
       do 'AdjustOrigin' -> cd.comment;
          (if originOff > 0 then
              ('origin',originOff) -> cd.rpushg
          if)
       #);
     ObjectInvocation::
       (* May be 
        *  A simple variable X declared as X:@integer; 
        *  An object R declared as R: @Foo
        *  A pattern Foo declared as Foo: Bar{ ... }
        *  ATd = operator.Decl
        *)
       (# pushThis:: (# do cd.bcPos -> beginBCP; ATd.pushThis #);; 
          loadOrigin::
            (# org: ^Items
            do ('ObjectInvocation:loadOrigin:A',doPT) -> cd.com2;
               (if ATD.primNo
                // 0 
                // indexed_prim 
                // string_prim
                // stringGet_prim // indexedGet_prim then 
                   superAdj -> AdjustOrigin;
                   on -> (origin[]->org[]).goOrigin; 
                // this_prim then
                   (* AD hoc and clumsy !*)
                   ('ObjectInvocation:loadOrigin:this:',doPT) -> cd.com2;
                   (this(Exp)[],true,0) -> ATd.load
                // thisCore_prim then 
                   ('thisCore',thisCore_prim) -> cd.callPrim
               if)
            #);
          pushOff:: 
            (# 
            do (rec[],useReturnValue,staticOff,superAdj,next[]) -> ATd.pushOff 
            #);
          loadArgs:: 
            (# 
            do (if (*(true and not isAssign) or*) (type ## = KeyWord##) then
                   ('OI:loadArgs:',doPT) -> cd.com2;
                   (this(Exp)[],superAdj) -> ATd.loadArgs
                else
                   args.scanSons(# do (true,superAdj,0) -> current.gen #) 
               if)
            #);
          execute:: 
            (# doBreak:
                 (# OD: ^Items; E : ^ObjectGenerator; on: @integer;
                    primNo: @integer
                 enter primNo
                 do args.scanSons
                    (# isArg: @boolean
                    do (* current.doP -> puttext;  ' ' -> put; *)
                       (if isArg then
                           current[] -> E[];
                           E.super.on -> on;
                           (*  on -> putint; ' ' -> put *)
                       if);
                       noT isarg -> isarg
                    #);
                    (* ' on:' -> puttext; on -> putint; newline;*)
                    origin[] -> OD[];
                    (for i: ON - 1 repeat 
                         (if OD.basicNo = ifthen_prim then OD.origin[] -> OD[] if);
                         OD.origin[] -> OD[] 
                    for);
                    (if OD.basicNo = ifthen_prim then OD.origin[] -> OD[] if);
                    (*OD.doP -> putline;
                     ' descNo:' -> puttext; OD.descNo -> putint;*)
                    (if primNo
                     // restart_prim then 
                        (if on = 1 then
                            OD.restartLab -> cd.jmp
                         else
                            (on - 1, OD.restartLab) -> cd.break
                        if)
                     // leave_prim then 
                        (if on = 1 then
                            OD.leaveLab -> cd.jmp
                         else
                            (on - 1, OD.leaveLab) -> cd.break
                        if)
                    if)
                 #);
               T: @text
            do (if isAssign then
                  superAdj -> loadArgs;
                   rec.ATd.store;
                else 
                   (if ATd.primNo
                    // restart_Prim then
                       restart_Prim -> doBreak
                    // leave_Prim then 
                       leave_Prim -> doBreak
                    // this_prim then
                       ('ObjectInvocation:execute',doPT) -> cd.com2;
                    // asString_prim then
                       ('asString_prim:',doPT) -> cd.com2;
                       ('asString',ATd.primNo) -> cd.callPrim
                    // thisCore_prim then
                       (* Currently we dont come here with thisCore.foo
                        * Perhaps with myCore := thisCore?
                        *)
                       ('ObjectInvocation:execute:thisCore:',doPT) -> cd.com2;
                    else 
                       ('OI:execute:', dopt) -> cd.com2;
                       (if (rec[] <> none) 
                           and (rec.ATD## <= DataItem##)
                           and (rec.ATd.getQual).isSimpleValue 
                           and (ATd.primno <> cmpAndSwap_prim) then 
                           rec.ATd.primNo -> T.putint;
                           ('Binary:load rec:primNo:',T[]) ->cd.com2;
                           (rec.ATd.doPT,(rec.ATd.getQual).doPT) -> cd.com2;
                           (this(Exp)[],useReturnValue,staticOff) -> rec.ATd.load
                       if);
                       'OI:exe:before:loadArgs'->cd.comment;
                       (if (*true or*) (type## <> Keyword##) then 
                           superAdj -> loadArgs
                       if);
                       'OI:exe:after:loadArgs'->cd.comment;
                       (this(Exp)[],useReturnValue,staticOff) -> ATd.load;
                       'OI:exe:after:load'->cd.comment;
               if)if);
               cd.bcPos -> endBCP;
            #);
       #);       
     BracketedExp::
       (* a := b + (c * d)
        *     execute c * d
        * (foo).x
        *    loadOrigin foo
        * (foo(e1,e2)).x
        *    execute foo(e1,e29?
        * (%this P).suspend
        *     loadOrigin?
        *)
       (# gen:: 
            (# 
            do ('BE:gen:',doPT) -> cd.com2;
               (*(useReturnValue,superAdj,staticOff) -> IV.gen *)
            #);
          loadOrigin:: 
            (# 
            do ('BE:loadOrigin:',doPT) -> cd.com2;
               (* OBS! useReturnValue, staticOff must be handled!
                *)
               (false,0,superAdj)  -> IV.loadOrigin 
            #);
          pushThis:: 
            (# 
            do ('BE:pushThis',doPT) -> cd.com2; (* IV.pushThis* ) 
            #);
          loadArgs:: 
            (# 
            do ('BE:loadArgs!',doPT) -> cd.com2; (* superAdj -> IV.loadArgs *) 
            #);
          execute:: 
            (#
            do ('BE:execute',doPT) -> cd.com2;
               (*(rec[],useReturnValue,staticOff,superAdj) -> IV.execute *)
               (useReturnValue,0,staticOff) -> IV.gen
            #);
       #);
     Const::
       (# gen:: (# do execute #);
          pushOff:: (# do execute #);
          execute:: (# do T.setpos; T.getInt -> cd.pushConst #);
       #);
     CharObj::
       (# gen:: (* Never called? *) (# do execute #);
          pushOff:: (# do execute #);
          execute:: 
            (# ch: @char
            do (if T.length = 1 then
                   1 -> T.inxget -> cd.pushConst 
                else
                   (if  2 -> T.inxget -> ch
                    // 'n' then 10 -> cd.pushConst
                    else
                       'CharObj:Special symbol: ' -> puttext; ch -> putint;
                       ':' -> put; '"' -> put; ch -> put; '"' -> put;
                       newline;
                       ch -> cd.pushConst
                   if)
               if)
            #);
       #);
     StringObj::
       (# gen:: 
            (* Never called ? *)
            (# do 'String:gen:' ->puttext; doPT -> putline; execute #);
          loadOrigin::
            (* In "hello".foo, "hello" plays the same role as R in R.foo,
             * i.e. we must generate a pushText "hello"
             * For an exp "Hello", loadOrigin then also generates a pushText "hello"
             * Execute is called only for the last exp in R1.R2.exp 
             * and 'just store the string!
             *)
            (# 
            do (*'StringObj:loadOrigin:' -> puttext; dopt->putline; *)
               T[] -> cd.pushText ; 
            #);
          execute:: 
            (# 
            do (*cd.pushThis;
               ('String',staticOff) -> cd.rstoreg*)
            #)
       #);
     ObjTmpStack: @
       (# item: 
            (# N: ^text; sig: ^Signature; OT: ^ObjectGenerator; super: ^Invocation
            enter(N[],sig[],OT[],super[])
            exit this(item)[]
            #);
          add:
            (# N: ^text; sig: ^Signature; OT: ^ObjectGenerator; super: ^Invocation
            enter(N[],sig[],OT[],super[])
            do (if (top+1-> top) > OTL.range then OTL.range -> OTL.extend if);
               (N[],sig[],OT[],super[]) -> item -> OTL[top][]
            #);          
          next:
            (# inx: @integer
            do Loop:
                 (#
                 do inx + 1 -> inx;
                    (if inx <= top then
                        (OTL[inx].N[],OTL[inx].sig[],OTL[inx].super[]) -> OTL[inx].OT.genClass;
                        restart Loop
                    if);
                 #)
            #); 
          OTL: [10] ^Item; top: @integer;
       #);
     labelHandler: @
       (# labCount: @integer;
          label:
            (# labNo: @integer;
               new: (# do  labCount + 1 -> labCount -> labNo #);
            enter labNo
            exit labNo
            #);
          init: (# do 0 -> labCount #);
       #);
     markIncludes,EPpath: @ ModulePath;
     
     ModulePath:
       (* mark all modules used by main module,and
        * find origin offsets for the nested modules enclosing main
        * We dont seem to use the origin offsets for QBETA 
        * -- needed for BETA, but for QBETA Alloc executes the object,
        * -- i.e. allocation of Main in BETAworld executes Main
        * -- with correct origin
        *)
       (# find:
            (# md: ^module;
               MI: ^ModuleItem;
               org: ^TopNode;
               D: ^Items;
               trace: (#exit false #);
            enter md[]
            do (if md[] <> none then
                   (if false then
                       '\nModule: ' -> puttext; md.SO.sig.doPT -> puttext;
                       ' descNo: ' -> puttext; (md.desc).descNo -> putint; 
                       ' imports: ' -> putline;
                   if);
                   md.mark;
                   (if md.origin[] <> none then
                       md.SO.off -> addOff;
                       (* else top module BETAworld *)
                   if);    
                   md.origin[] -> org[];
                   FindModule: (* same as encModule?*)
                     (if org[] <> none then 
                         (if org## <> Module## then 
                             org.father[] -> org[]; restart FindModule 
                         if);
                         org[] -> find 
                      else 
                         md[] -> topModule[] 
                     if)
               if)
            #);
          
          topModule: ^module; (* BETAworld *)
          orgOff: [6] @integer; top: @integer;
          addOff: (* not needed, see above *)
            (# off: @integer
            enter off
            do (if (top+1 -> top) > orgOff.range then 
                   orgOff.range -> orgOff.extend 
               if);
               off -> orgOff[top];
               (*' addOff: ' -> puttext; off -> putint; ' ' ->put*)
            #);
          main: ^Module
       enter main[]
       do 0 -> top;
          main[] -> find;
       #);
     genStubs:
       (# genMain:
            (# descno: @integer
            do ('main',newDesc,false,1,0) -> cd.class;
               cd.pushThis;
               (if false then
                   'TopModule: ' -> puttext;
                   markIncludes.topModule.SO.sig.doPT -> putLine;
                   'Main: ' -> puttext; main.doPt -> putline
               if);
               ('BETAworld',(markIncludes.topModule.desc).descno,0,true) 
                 -> cd.invoke;
               cd.stop; (* We should generate stop here and not in betaVM *)
               (0,0,0) -> CD.endClass;
               
               (if markIncludes.main## <> Module## then
                   '\n\n***** Only Modules can be executed *****\n\n'
                     -> putline
               if)
            #);
          genThreadStub:
            (# descNo: @integer;
            do newDesc -> descNo;
               ('ThreadStub',descNo,false,descNo,0) -> cd.class;
               (false,false) -> cd.actions;
               ('ThreadStub','D') -> cd.call;
               cd.stop
            #);
          genEventProcessor:
            (#
            do ('EventProcessor',newDesc,false,1,0) -> cd.class;
               cd.stop (* we should generate stop here and not in betaVM *)
            #);                   
       do genMain;
          genEventProcessor;     
          genThreadStub;
       #);

     mainDescNo:: (# do newDesc.noOfDescs - 2 -> value (* ad hoc *) #);
     cd: @ObjectCode;
     runTimeDescs:: (# do descs[] -> rtDescs[] #);
     descs: ^RunTimeDescriptors;
     BC: @File;
  do '**** GENERATOR ****' -> putline;     
     (FN[],'..s') -> mkAuxName -> BC.name;
     BC.openWrite;
     BC[] -> cd.init;
     'QBETA'-> cd.comment;
         
     labelHandler.init;
     0-> ObjTmpStack.top;
     main[] -> markIncludes;

     rootModule.gen;

     ObjTmpStack.next;
         
     genStubs;
     
     (if stringDecl[] <> none then
         (*'String descNo: ' -> puttext; (stringDecl.desc).descNo -> putint; 
          * ' primNo: ' -> puttext; stringDecl.primNo -> putint; newline;
          *)
         (stringDecl.desc).descNo -> CD.descs.textDescNo
      else
         rootModule[] -> SemanticError(#do 'StringDecl not found!' -> msg #)
     if);
     (*  (if StructureRefDesc[] <> none then
      *          StructureRefDesc.descNo -> CD.descs.structureRefDescNo
      *       else
      *          rootModule[] 
      *            -> SemanticError(#do 'StructureRefDesc not found!' -> msg #)
      *    if);
      *)
     
     cd.descs[] -> descs[]; (* descs is transferred to betaVM ?*)    
         
     cd.close;
     BC.close
  #)

