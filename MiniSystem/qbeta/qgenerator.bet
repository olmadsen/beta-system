ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qchecker';
INCLUDE '../miniCompiler/objectcode'
---lib:attributes---
generator: checker
  (# traceModules: (# exit false #);
     MkCom: 
       (# t1,t2: ^text enter (t1[],t2[]) do t2[] -> T1.append -> cd.comment #);
     
     Module::
       (# gen::
            (#
            do ('Comp',newDesc.noOfDescs) -> cd.newComponent;
               SO.gen;
            #);
       #);
     ModuleItem::
       (# gen::
            (#
            do (if MD.isIncluded then
                   ('ModuleItem:gen',sig.dopt) -> cd.com2;
                   MD.SO.gen
                else
                   (if traceModules then
                       'Module not included: ' -> puttext; sig.doPT -> putline
               if)if)
            #);
          pushThis:: (# do cd.pushThis #);
       #);
     StaticObject::
       (# gen::
            (# T: @text
            do (if not OG.super.isBasic then 
                   'StaticObject is not Basic' -> cd.comment;
                   sig.doPt -> cd.comment;
                   (if origin[] <> none then 
                       OG.IS.descNo -> T.putint;
                       ('staticObject: alloc: ',T[]) -> cd.com2; 
                       (true,0) -> OG.gen;
                       cd.pushThis;
                       (sig.id[],this(StaticObject).off) -> cd.rstoreg;
                    else 
                       (* A module object *)
                       (if not OG.IS.isEmpty then
                           (sig.id[],none,OG[],OG.super[]) ->  ObjTmpStack.add
                       if)   
                   if)
               if)
            #);
          pushThis:: (# do cd.pushThis #);
          load::
            (* called when not receiver - i.e. value to be pushed *)
            (#
            do (if OG.super.isBasic then 
                   (sig.id[],off) -> cd.pushg
                else
                   (sig.id[],off) -> cd.rpushg
               if)               
            #);
          pushOff::
            (#
            do (if OG.super.isBasic then 
                   (*'\n!!! StaticObj:LoadOff:basic: ' -> puttext;
                    dopt -> putline;*)
                   (* May happen for V := 117 where := is assignment 
                    * - no offset
                    * shall be pushed for V since V is assigned afterwards
                    * For R ++ 117 offset for R should be pushed since it
                    * is origin
                    *)
                else
                   (sig.id[],off) -> cd.rpushg
               if)
            #);
          store::
            (* Called from Signature::StoreParameters *)
            (#
            do (* 'StaticObject:store: '->putline;*)
               (if OG.super.isBasic then 
                   (sig.id[],off) -> cd.storeg
                else
                   (sig.id[],off) -> cd.rstoreg
               if)
            #);
          popReturn::
            (#
            do (if OG.super.isBasic then 
                   cd.vpop
                else
                   cd.rpop
               if)
            #)
       #);
     DynamicObject::
       (# gen:: (# do #);
          pushThis:: (# do cd.pushThis #);
          load:: 
            (# 
            do 'DynObj:load' -> cd.comment;
               (sig.id[],this(DynamicObject).off) -> cd.rpushg               
            #);
          store:: (# do cd.rswap; (sig.id[],off) -> cd.rstoreg #);
          popReturn:: (# do cd.rpop #)            
       #);
     Signature::
       (# 
          storeParameters:
            (#
            do (*cd.setThisStack;*)
               (* 'storeParameters: ' -> puttext;*)
               scanParametersReverse
               (# D: ^Decl
               do (*currentPar.dopt -> puttext; ' ' -> put;*)
                  (if not (currentPar[] -> D[]).isVirtual then
                      cd.pushThis;
                      currentPar.store
                  if)
               #)
            #);
          pushReturnValues:
            (#
            do (if val[] <> none then
                   cd.setThisStack;
                   val.scanSons
                   (# D: ^Decl
                   do current[] -> D[];
                      cd.pushThis;
                      D.load
                   #)
                else
                   cd.setThisStack;
                   cd.pushThis;
               if)
            #);
          popReturn:
            (# 
            do (if val[] <> none then
                   val.scanSons
                   (# D: ^Decl
                   do (current[] -> D[]).popReturn
                   #)
                else
                   cd.rpop
               if)
            #)
       #);
     ObjectGenerator::
       (# gen::
            (# name: ^text
            do (*('OG: ',OI.inv.compoundSymb[]) -> MkCom;*)
               (*IS.inv.ATd.doPT -> putline*)
               (if IS.isEmpty and not super.hasVirtualArgs or super.isBasic then
                   (* check this - called when R.foo *)
                   (useReturnValue,superAdj) -> super.gen
                else
                   (* called when R.foo{ } *)
                   (* (if super.hasVirtualArgs then
                       'ObjectGenerator:gen:hasVirtualArgs: ' -> putline;
                       super.doP -> putline;
                       'descNo: ' -> puttext; IS.descNo -> putint; newline;
                       'SuperDesc: ' -> putline; IS.superDesc.doP -> putline;
                       'Super.descNo: ' -> puttext;
                       IS.superDesc.descNo -> putint; 
                       '\n---'->putline
                   if);*)
                   cd.pushThis; (* origin for a singular object *)
                   (if singularName[] <> none then
                       singularName[] -> name[]
                    else
                       'Singular#' -> name[];
                       IS.descNo -> name.putint;
                   if);
                   (name[],IS.descno) -> cd.invoke;

                   (if not useReturnValue  then cd.rpop if);
                   
                   (name[],none,this(ObjectGenerator)[],super[]) -> ObjTmpStack.add
               if)
            #);
          genClass::
            (#
            do (N[],sig[],IS.descNo,super[]) -> IS.genClass
            #)
       #);
     PatternId::
       (# gen::
            (#
            do 'PatternId:gen: ' -> puttext; dopt ->putline;
               IV.gen
            #)
       #);
     items::
       (# genClass:
            (# N: ^text; sig: ^Signature; descNo: @integer; super: ^Invocation;
               T: @text; ptn: ^Pattern;
               vSuperDesc: ^Items;
            enter(N[],sig[],descNo,Super[])
            do (*'GenClass: ' -> puttext; N[] -> putline;*)
               (if (superDesc[] <> none) then
                   superDesc.father.father -> ptn[];
                   (if not superDesc.done then
                       (* 'HasSuper: ' -> puttext; *)
                       (* super is always a pattern - I think!*)
                       (# 
                       do (ptn.sig.id[],ptn.sig[],superdesc.descNo,ptn.OD.super[]) 
                            -> superDesc.genClass
                       #)
               if)if);
               (if not done then
                   true -> done;
                   (N[],descNo,false, (topSuper).descNo,originOff) -> cd.class;
                   (* thisObj = the new instance of this class 
                    * thisStack = caller
                    * Possible arguments on callerStack
                    *)

                   (* store parameters + origin *)
                   (if sig[] <> none then sig.storeParameters if);
                   ('origin',originOff) -> cd.rstore;
                   
                   (if (superDesc[] <> none) and not superDesc.isBasic then
                       (if superDesc.isBasic then
                           'SuperDesc:isBasic: ' -> putline;
                           superDesc.doP -> putline
                       if);
                       (ptn.sig.id[],superDesc.descNo)  -> cd.super; 
                       superDesc.descNo -> cd.vdtTableCopy;
                       (if superDesc.innerInx > 0 then
                           (superDesc.innerInx,descno) -> cd.vdtTable;
                           true -> hasSuperAndInner
                       if);
                       (* push super arguments *)
                       
                       (if superApl.isImplSuper then
                           'Implicit super:\n' -> puttext; doP -> putline;
                           (if superApl.isSingularImplSuper then
                               cd.pushThis;
                               ('iOrigin',originOff) -> cd.rpushg
                            else
                               'Non singular Implict super:\n'->puttext;
                               doP -> putline;
                               superApl.ATd[] -> ptn[];
                               origin[] -> vSuperDesc[]; (* find desc enclosing vDcl *)
                               (for i: superApl.pn repeat
                                    vSuperDesc.superDesc[] -> vSuperDesc[]
                               for);
                               (*'vSuperDesc: ' -> putline; 
                                * (0,true) -> vSuperDesc.doP -> putline;*)
                               cd.pushThis;
                               ('origin',originOff) -> cd.rpushg; 
                               ptn.OD.super.on -> vSuperDesc.goOrigin
                           if)                                   
                        else
                           'super.gen: push arguments' -> cd.comment;
                           (*'super.gen: push arguments: ' -> puttext;
                            super.doP -> putline;*)
                           (# E,rec: ^Exp;
                              loadOrigin:
                                (# superAdj: @integer; E,rec: ^Exp
                                enter superAdj
                                do super.scanSons
                                   (#
                                   do current[] -> E[];
                                      (if isFirst then 
                                          superAdj -> E.loadOrigin 
                                      if);
                                      (if true or not isLast then
                                          E.pushOff;
                                          E[] -> rec[]
                                      if);
                                   #);
                                exit(rec[],E[])
                                #)
                           do L:
                                super.scanSons 
                                (# E: ^Exp;
                                do current[] -> E[];
                                   (*'arg: ' -> puttext; E.doP -> putline;*)
                                   E.pushThis;
                                   leave L
                                #);
                              originOff -> loadOrigin -> (rec[],E[]);
                              originOff -> E.loadArgs 
                           #);
                       if);
                       superDesc.descNo -> cd.topSuper;
                       cd.markEnterE;  
                   if);   
                   (if (superDesc[] <> none) and not superDesc.isBasic  then
                    else cd.mvStack
                   if);
                   scanSons
                   (# I: ^Item
                   do current[] -> I[];
                      (if I.isDecl then (false,0) -> current.gen if) 
                   #);
                   (if innerInx > 0 then innerInx -> cd.innerA if);
                   (if (superDesc[] <> none) and not superDesc.isBasic  then
                       cd.rtnInner 
                   if);
                   
                   (* Code for action part *)
                   false -> cd.actions;
                   restartLab.new;
                   leaveLab.new;
                   restartLab -> cd.defLab;
                   scanSons
                   (# I: ^Item
                   do current[] -> I[];
                      (if not I.isDecl then (false,0) -> current.gen if) 
                   #);
                   leaveLab -> cd.defLab;
                   (* we must push return values *)
                   (* push return value 
                    * - for staticObject: R: @Foo
                    *   Foo should return (this(Foo)
                    *   perhaps default?
                    * - otherwise push variables in -> 
                    *)
                   (if (superDesc[] <> none) and not superDesc.isBasic then
                    else
                       (if sig[] <> none then 
                           sig.pushReturnValues 
                        else
                           (* ad hoc *)
                           cd.setThisStack;
                           cd.pushThis
                   if)if);

                   (if (superDesc[] <> none) and not superDesc.isBasic then
                       cd.rtnInner
                    else
                       'D' -> cd.rtn;
                   if);
                   (vsize,rsize,1) -> CD.endClass     
               if)
            #);
          goOrigin:
            (# on: @integer; org: ^Items
            enter on
            do (if on > 0 then
                   ('origin',originOff) -> cd.rpushg;
                   on - 1 -> (origin[]->org[]).goOrigin
               if)
            #); 
          hasSuperAndInner: @Boolean;
          restartLab,leaveLab: @LabelHandler.label
       #);
     UnaryPattern::
       (# gen:: (# do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          load::
            (#
            do ('UnaryPattern:load: ',sig.doPT) -> cd.com2; 
               (sig.id[],OD.IS.descno) -> cd.invoke;
               (if not useReturnValue then
                   sig.popReturn
                   (* reconsider if return values *)
               if)               
            #);
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #);   
       #);
     BasicUnaryPattern::
       (# load::
            (# org: ^Items
            do (if OD.IS.props.basicProp.primNo
                // inner_prim then
                   'INNER'-> cd.comment;
                   E.origin[] -> org[];
                   org.innerInx -> cd.innerx;                   
               if);
            #)
       #);
     VirtualPattern::
       (# gen::
            (#
            do(* 'Gen Virtual: ' -> puttext; sig.doPT -> putline;*)
               (if OD.IS.isEmpty then
                   (*  'Empty virtualPTN: ' -> putline;*)
                   (off,(OD.super.desc).descNo) -> cd.vdtTable
                else
                   (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add;
                   (off,OD.IS.descNo) -> cd.vdtTable
               if)
            #);
          pushThis:: (# do cd.pushThis #);
          load::
            (#
            do ('VirtualPtn:load: ',sig.doPT) -> cd.com2; 
               (sig.id[],off) -> cd.sendVirtual;
               (if not useReturnValue then
                   sig.popReturn
                   (* reconsider if return values *)
               if);               
            #);
          store::
            (#
            do 'VirtualPattern:store: '->puttext; doPT -> putline;
            #);          
       #);
     FinalPattern::
       (# gen::
            (#
            do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add;
               (off,OD.IS.descNo) -> cd.vdtTable
            #);
          pushThis:: (# do cd.pushThis #);
          load::
            (#
            do (*'Load Final: ' -> puttext; doPt -> putline;*)
               ('FinalPtn:load: ',sig.doPT) -> cd.com2; 
               (sig.id[],off) -> cd.sendVirtual;
               (if not useReturnValue then
                   sig.popReturn
                   (* reconsider if return values *)
               if);               
            #);
          store::
            (#
            do 'FinalPattern:store: '->puttext; doPT -> putline;
            #);          
       #);          
     BinaryPattern::
       (# gen:: (# do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add #);
          load::
            (#
            do ('BinaryPattern:load: ',sig.doPT) -> cd.com2;
               (sig.id[],OD.IS.descno) -> cd.invoke;
               (if not useReturnValue then
                   sig.popReturn
               if);               
            #);
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #); 
       #);
     BasicBinaryPattern::
       (# load:: 
            (# 
            do OD.IS.props.basicProp.primno -> cd.op               
            #);          
       #);
     FunctionalPattern::
       (# gen:: (# do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          load:: 
            (# 
            do ('FunctionalPattern:load: ',sig.doPT) -> cd.com2;
               (sig.id[],OD.IS.descno) -> cd.invoke;
               (if not useReturnValue then
                   sig.popReturn
               if);               
            #);
       #);
     BasicFunctionalPattern::
       (# load:: (# do ('put',OD.IS.props.basicProp.primNo) -> cd.Callprim;  #);
       #);
     KeyWordPattern::
       (# gen:: (# do (sig.id[],sig[],OD[],OD.super[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          loadArgs::
            (# args: [6]^ObjectGenerator; top: @integer;
            do 'KeyWordPtn:LoadArgs: ' -> puttext; 
               E.args.scanArgs
               (#
               do (if (top+1 -> top) > args.range then 
                      args.range -> args.extend 
                  if);
                  current[] -> args[top][];
                  current.doP -> putline
               #);
               sig.args.scanArgs
               (# D: ^Decl; i: @integer; OG: ^ObjectGenerator; name: ^text;
                  VP: ^VirtualPattern;
               do i + 1 -> i;
                  current[] -> D[]; 
                  (*D.doPt -> putline; args[i].dopt -> putline;*)
                  (if D.isVirtual then
                      'SingularBinding#' -> name[];
                      (args[i].desc).descno -> name.putint;
                      (args[i].desc).doPT -> putline;
                      (args[i].desc).descno -> putint;newline;
                      'Binding: ' -> putline; args[i].doP -> putline;
                      args[i].adjustON;
                      (name[],none,args[i][],args[i].super[]) -> ObjTmpStack.add;
                      E.father.father[] -> OG[]; (* need OG.descNo for VDT *)
                      (*(OG.desc).descNo -> putint; newline;*)
                      
                      (D.off,(args[i].desc).descno,(OG.desc).descNo) -> cd.vdtTableAt
                   else
                      (false,superAdj) -> args[i].gen
                  if);
               #);               
            #);
          loadAdr::
            (# args: [6]^ObjectGenerator; top: @integer;
            do (*'KeyWordPattern:loadAdr: ' -> puttext; sig.doPT -> putline;*)
               E.scanSons
               (# isArg: @boolean
               do (if isArg then
                      (*'Actual arg: ' -> puttext; current.doPT -> putline;*)
                      (if (top+1 -> top) > args.range then 
                          args.range -> args.extend 
                      if);
                      current[] -> args[top][]
                  if);
                  not isArg -> isArg                  
               #);
               sig.args.scanArgs
               (# D: ^Decl; i: @integer; OG: ^ObjectGenerator; name: ^text
               do i + 1 -> i;
                  current[] -> D[]; 
                  (*D.doPt -> putline;
                   args[i].dopt -> putline;*)
                  (if D.isVirtual then
                      'SingularBinding#' -> name[];
                      (args[i].desc).descno -> name.putint;
                      (* (args[i].desc).doPT -> putline;
                       (args[i].desc).descno -> putint;newline;*)
                      E.father.father[] -> OG[]; 
                      (* 'Binding: ' -> putline; args[i].doPT -> putline;*)
                      (name[],none,args[i][],args[i].super[]) -> ObjTmpStack.add;
                      
                      (*(OG.desc).descNo -> putint; newline;*)
                      
                      (D.off,(args[i].desc).descno,(OG.desc).descNo) -> cd.vdtTableAt
                   else
                      args[i].gen
                  if);
               #);
            #);
          load::
            (# args: [6]^ObjectGenerator; top: @integer;
               hasVirtualArgs: @boolean
            do 'KWP:load: ' -> puttext; sig.dopt -> putline;
               E.args.scanArgs
               (# 
               do 'Actual arg: ' -> puttext; current.doP -> putline;
                  (if (top+1 -> top) > args.range then 
                      args.range -> args.extend 
                  if);
                  current[] -> args[top][]
               #);
               sig.args.scanArgs
               (# D: ^Decl; i: @integer
               do i + 1 -> i;
                  current[] -> D[]; D.doP -> putline;
                  (if D.isVirtual then
                      true -> hasVirtualArgs
                   else
                      args[i].gen
                  if);
               #);
               (if hasVirtualArgs then 
                   '\n>>> Has Virtual arguments'->putline;
                   (* father.father[] -> OG[];
                    ('KeyWord',none,OG[],OG.super[]) -> ObjTmpStack.add*)
                else
                   (sig.id[],OD.IS.descno) -> cd.invoke;
                   (if not useReturnValue then
                       sig.popReturn
                   if)
               if)               
            #)
       #);
     BasicKeyWordPattern::
       (# load::
            (# exitLab: @labelHandler.label
            do (if OD.IS.props.basicProp.primNo
                // ifthen_prim then
                   exitLab.new;                   
                   E.args.scanSons
                   (# N: @integer
                   do N + 1 -> N;
                      (if N
                       // 1 // 3 then
                       // 2 then 
                          (true,0) -> current.gen;
                          exitlab -> cd.jmpFalse;
                       // 4 then
                          current.gen;
                          exitLab -> cd.defLab;
                       else
                          'load:%if%then: too many arguments' -> putline;
                          E.doPt -> putline
                      if)
                   #);                 
               if);
            #)
       #);            
     Invocation::
       (* E1.E2.E3
        *    E1.pushThis
        *    E1.on -> gorigin
        *    E1.pushOff
        *    E2.pushOff
        *    E3.execute
        * R.foo
        * R.foo{...}
        * X:= y + 12
        * R ++ E  -- problem E1 = R ++ E , i.e. first = true 
        * perhaps same problem for keyWord
        * We must reconsider parsing/AST and/or gen below
        *)
       (# gen:: 
            (# rec,E: ^Exp;
            do L:
                 scanSons 
                 (#
                 do current[] -> E[];
                    E.pushThis;
                    (* cd.pushThis does not work, 
                     * E may be a constant or basic pattern,
                     * where pushThis shall not be generated
                     *)
                    leave L
                 #);
               superAdj -> loadOrigin -> (rec[],E[]);
               (rec[],useReturnValue) -> E.execute
            #);
          loadOrigin:
            (# superAdj: @integer;
               rec,E: ^Exp;
            enter superAdj
            do scanSons
               (# 
               do current[] -> E[];
                  (if isFirst then superAdj -> E.loadOrigin if); 
                  (if not isLast then 
                      E.pushOff;
                      E[] -> rec[]
                  if);
               #)
            exit(rec[],E[])
            #);
       #);
     adjustOrigin:
       (# originOff: @integer
       enter originOff
       do (if originOff > 0 then
              ('origin',originOff) -> cd.rpushg
          if)
       #);
     Unary::
       (* May be 
        *  A simple variable X declared as X:@integer; 
        *  An object R declared as R: @Foo
        *  A pattern Foo declared as Foo: Bar{ ... }
        *  ATd = operator.Decl
        *)
       (# pushThis:: (# do ATd.pushThis #);; 
          loadOrigin::
            (# org: ^Items
            do (if ATd## <> BasicUnaryPattern## then
                   superAdj -> AdjustOrigin;
                   on -> (origin[]->org[]).goOrigin; 
               if)
            #);
          pushOff:: (# do ATd.pushOff #);
          execute:: 
            (# 
            do (this(Exp)[],useReturnValue) -> ATd.load
            #);
       #);
     Binary::
       (# pushThis:: (# do ATd.pushThis #);
          loadOrigin::
            (# org: ^Items
            do superAdj -> AdjustOrigin;
               on -> (origin[]->org[]).goOrigin;               
            #);
          loadArgs:: (# do args.scanSons(# do (true,0) -> current.gen #) #);
          execute::
            (#
            do (if isAssign then 
                   loadArgs; (*argument.gen;*)
                   (if true then
                       rec.ATd.store
                    else
                       ('var',rec.ATd.off) -> cd.storeg
                   if)
                else
                   (if rec.ATd.isBasic then 
                       (this(Exp)[],useReturnValue) -> rec.ATd.load
                   if);
                   loadArgs;
                   (this(Exp)[],useReturnValue) -> ATd.load
               if)
            #)
       #);
     Function::
       (# pushThis:: (# do ATd.pushThis #);
          loadOrigin::
            (# org: ^Items
            do (if ATD.isPrim = 0 then 
                   superAdj -> AdjustOrigin;
                   on -> (origin[]->org[]).goOrigin;   
               if)
            #);
          loadArgs:: 
            (# 
            do (*'Function:loadArgs: ' -> puttext; superAdj -> putint; 
               ' ' -> put; doP -> putline;*)
               args.scanArgs
               (# OG: ^ObjectGenerator
               do (false(*?*),superAdj) -> current.gen;
               #);
            #);
          execute::
            (# 
            do loadArgs;
               (this(Exp)[],useReturnValue) -> ATd.load
            #)          
       #);
     KeyWord::
       (# pushThis:: (# do ATd.pushThis #);
          loadOrigin::
            (# org: ^Items
            do (if (ATD.isPrim = 0) (*or (ATd.isPrim = ifThen_prim)*) then 
                   superAdj -> AdjustOrigin;
                   on  -> (origin[]->org[]).goOrigin
               if)
            #);
          loadArgs::
            (* We should add args as an attribute to Exp
             * Perhaps arg = current.gen below
             * should be current.loadArg
             * Could then fix VDT for viruals!
             * Would be Keyword::loadArg:: (# do Atd.loadArg #)
             *)
            (# isArg: @boolean; ptn: ^Pattern
            do 'Keyword:loadArgs: ' -> putline; doP -> putline;
               ATd[] -> ptn[];
               (if ptn.OD.super.hasVirtualArgs then
                   (* does not work *)
                   'hasVirtualArgs'->putline;
               if);
               (if true then
                   (this(Exp)[],superAdj) -> ATd.loadArgs
                else
                   args.scanArgs(# do current.gen #)
               if)
            #);               
          execute::
            (# doBreak:
                 (# OD: ^Items; E : ^ObjectGenerator; on: @integer;
                    primNo: @integer
                 enter primNo
                 do args.scanSons
                    (# isArg: @boolean
                    do (* current.doP -> puttext;  ' ' -> put; *)
                       (if isArg then
                           current[] -> E[];
                           E.super.on -> on;
                           (*  on -> putint; ' ' -> put *)
                       if);
                       noT isarg -> isarg
                    #);
                    (* ' on:' -> puttext; on -> putint; newline;*)
                    origin[] -> OD[];
                    (for i: ON - 1 repeat OD.origin[] -> OD[] for);
                    (*OD.doP -> putline;
                     ' descNo:' -> puttext; OD.descNo -> putint;*)
                    (if primNo
                     // restart_prim then 
                        (if on = 1 then
                            OD.restartLab -> cd.jmp
                         else
                            (on - 1, OD.restartLab) -> cd.break
                        if)
                     // leave_prim then 
                        (if on = 1 then
                            OD.leaveLab -> cd.jmp
                         else
                            (on - 1, OD.leaveLab) -> cd.break
                        if)
                    if)
                 #);
            do (*'KeyWord:execute: ' -> puttext; doPt -> putline;*)
               (if ATd.isPrim
                // restart_Prim then
                   (*'Restart: ' -> puttext;*) restart_Prim -> doBreak
                // leave_Prim then 
                   (*'Leave: ' -> puttext;*) leave_Prim -> doBreak
                else
                   (this(Exp)[],useReturnValue) -> ATd.load
               if)
            #);
       #);
     Textt::
       (# gen:: (# do execute #);
          execute:: 
            (# 
            do (if T.length = 1 then
                   1 -> T.inxget -> cd.pushConst 
                else
                   'Text: ' -> putline
               if)
            #);
       #);
     Const::
       (# gen:: (# do execute #);
          execute:: (# do T.setpos; T.getInt -> cd.pushConst #);
       #);
     ObjTmpStack: @
       (# item: 
            (# N: ^text; sig: ^Signature; OT: ^ObjectGenerator; super: ^Invocation
            enter(N[],sig[],OT[],super[])
            exit this(item)[]
            #);
          add:
            (# N: ^text; sig: ^Signature; OT: ^ObjectGenerator; super: ^Invocation
            enter(N[],sig[],OT[],super[])
            do (if (top+1-> top) > OTL.range then OTL.range -> OTL.extend if);
               (N[],sig[],OT[],super[]) -> item -> OTL[top][]
            #);          
          next:
            (# inx: @integer
            do Loop:
                 (#
                 do inx + 1 -> inx;
                    (if inx <= top then
                        (OTL[inx].N[],OTL[inx].sig[],OTL[inx].super[]) -> OTL[inx].OT.genClass;
                        restart Loop
                    if);
                 #)
            #); 
          OTL: [10] ^Item; top: @integer;
       #);
     labelHandler: @
       (# labCount: @integer;
          label:
            (# labNo: @integer;
               new: (# do  labCount + 1 -> labCount -> labNo #);
            enter labNo
            exit labNo
            #);
          init: (# do 0 -> labCount #);
       #);
     markIncludes,EPpath: @ ModulePath;
     
     ModulePath:
       (* mark all modules used by main module,and
        * find origin offsets for the nested modules enclosing main
        * We dont seem to use the origin offsets for QBETA 
        * -- needed for BETA, but for QBETA Alloc executes the object,
        * -- i.e. allocation of Main in BETAworld executes Main
        * -- with correct origin
        *)
       (# find:
            (# md: ^module;
               MI: ^ModuleItem;
               org: ^node;
               D: ^Items;
               trace: (#exit false #);
            enter md[]
            do (if md[] <> none then
                   (if false then
                       '\nModule: ' -> puttext; md.SO.sig.doPT -> puttext;
                       ' descNo: ' -> puttext; (md.desc).descNo -> putint; 
                       ' imports: ' -> putline;
                   if);
                   md.mark;
                   (if md.origin[] <> none then
                       md.SO.off -> addOff;
                       (* else top module BETAworld *)
                   if);    
                   md.origin[] -> org[];
                   FindModule: (* same as encModule?*)
                     (if org[] <> none then 
                         (if org## <> Module## then 
                             org.father[] -> org[]; restart FindModule 
                         if);
                         org[] -> find 
                      else 
                         md[] -> topModule[] 
                     if)
               if)
            #);
          
          topModule: ^module; (* BETAworld *)
          orgOff: [6] @integer; top: @integer;
          addOff: (* not needed, see above *)
            (# off: @integer
            enter off
            do (if (top+1 -> top) > orgOff.range then 
                   orgOff.range -> orgOff.extend 
               if);
               off -> orgOff[top];
               (*' addOff: ' -> puttext; off -> putint; ' ' ->put*)
            #);
          main: ^Module
       enter main[]
       do 0 -> top;
          main[] -> find;
       #);
     genMain:
       (# descno: @integer
       do ('main',newDesc,false,1,0) -> cd.class;
          cd.pushThis;
          (if false then
              'TopModule: ' -> puttext;
              markIncludes.topModule.SO.sig.doPT -> putLine;
              'Main: ' -> puttext; main.doPt -> putline
          if);
          ('BETAworld',(markIncludes.topModule.desc).descno) -> cd.invoke;
          cd.stop; (* We should generate stop here and not in betaVM *)
          (0,0,0) -> CD.endClass;
          
          (if markIncludes.main## <> Module## then
              '\n\n***** Only Modules can be executed *****\n\n'
                -> putline
          if)
       #);
     genEventProcessor:
       (#
       do ('EventProcessor',newDesc,false,1,0) -> cd.class;
          cd.stop (* we should generate stop here and not in betaVM *)
       #);
     
     mainDescNo: (# exit newDesc.noOfDescs -1 (*- 2*) (* ad hoc *) #);
     cd: @ObjectCode;
     runTimeDescs:: (# do descs[] -> rtDescs[] #);
     descs: ^RunTimeDescriptors;
     BC: ^File;
  enter BC[]
  do 'GENERATOR' -> putline;
     (if true then
         BC.openWrite;
         BC[] -> cd.init;
         'QBETA'-> cd.comment;
         
         labelHandler.init;

         main[] -> markIncludes;

         rootModule.gen;

         ObjTmpStack.next;
         
         genMain;
         genEventProcessor;     
         
         cd.descs[] -> descs[]; (* descs is transferred to betaVM ?*)    
         
         cd.close;
         BC.close
     if)
  #)

