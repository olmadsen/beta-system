ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qchecker';
INCLUDE '../miniCompiler/objectcode'
---lib:attributes---
generator: checker
  (# MkCom: (# t1,t2: ^text enter (t1[],t2[]) do t2[] -> T1.append -> cd.comment #);
     
    Module::
       (# gen::
            (#
            do ('Comp',newDesc.noOfDescs) -> cd.newComponent;
               SO.gen;
               (*this(Module)[] -> main[];*)
            #);
       #);
     ModuleItem::
       (# gen::
            (#
            do (if MD.isIncluded then
                   MD.SO.gen
                else
                   'Module not included: ' -> puttext; sig.doPT -> putline
               if)
            #);
       #);
     StaticObject::
       (# gen::
            (# T: @text
            do (if not OG.IV.isBasic then 
                   'StaticObject is not Basic' -> cd.comment;
                   sig.doPt -> cd.comment;
                   (if origin[] <> none then 
                       cd.pushThis;
                       OG.IS.descNo -> T.putint;
                       ('staticObject: alloc: ',T[]) -> cd.com2; 
                       true -> OG.gen;
                       (*cd.rswap;*)
                       cd.pushThis;
                       (sig.id[],this(StaticObject).off) -> cd.rstoreg;
                    else 
                       (* A module object *)
                       (if not OG.IS.isEmpty then
                           (sig.id[],none,OG[]) ->  ObjTmpStack.add
                       if)   
                   if)
               if)
            #);
          pushThis:: (# do cd.pushThis #);
          loadAdr:: 
            (* Called when Receiver 
             * R: @ Foo or R: @ Foo{...} -> push R = adr of object
             * X: @integer -> no action
             *)
            (# 
            do (if not OG.IV.isBasic then 
                   ('StaticObject:loadADr: ',sig.dopt) -> cd.com2;
                   (sig.id[],this(StaticObject).off) -> cd.rpushg
               if)
            #);
          load::
            (* called when not receiver - i.e. value to be pushed *)
            (#
            do (if OG.IV.isBasic then 
                   (sig.id[],off) -> cd.pushg
                else
                   (sig.id[],off) -> cd.rpushg
               if)
            #);
          store::
             (* Called from Signature::StoreParameters
             *)
            (#
            do 'StaticObject:store: '->putline;
               (if OG.IV.isBasic then 
                   (sig.id[],off) -> cd.store
                else
                   (sig.id[],off) -> cd.rstore
               if)
            #);
          swapr::
            (#
            do (if not OG.IV.isBasic then
                   CD.rswap
               if)
            #)
       #);
     DynamicObject::
       (# gen:: (# do #);
          pushThis:: (# do cd.pushThis #);
          loadAdr:: 
            (# 
            do 'DynObj:loadAdr' -> cd.comment;
               (sig.id[],this(DynamicObject).off) -> cd.rpushg 
            #);
          load:: 
            (# 
            do 'DynObj:load' -> cd.comment;
               (sig.id[],this(DynamicObject).off) -> cd.rpushg
            #);
          store:: (# do (sig.id[],off) -> cd.rstore #);
          swapr:: (# do cd.rswap #)
       #);
     Signature::
       (# 
          storeParameters:
            (#
            do cd.setThisStack;
               scanParametersReverse
               (#
               do currentPar.store
               #)
            #);
          pushReturnValues:
            (#
            do (if val[] <> none then
                   cd.setThisStack;
                   val.scanSons
                   (# D: ^Decl
                   do current[] -> D[];
                      cd.pushThis;
                      D.load
                   #)
                else
                   (* pushThis? 
                    * Or add default val - perhaps special This-decl*)
               if)
            #)
       #);
     ObjectGeneration::
       (# gen::
            (# name: ^text
           do (*('OG: ',OI.inv.compoundSymb[]) -> MkCom;*)
               (*IS.inv.ATd.doPT -> putline*)
               (if IS.isEmpty then
                   (* check this - called when R.foo *)
                   isStaticObj -> IV.gen
                else
                   (* called when R.foo{ } *)
                   IV.loadOrigin;
                   (if singularName[] <> none then
                       singularName[] -> name[]
                    else
                       'Singular:' -> name[];
                       IS.descNo -> name.putint;
                   if);
                   (name[],IS.descno,true) -> cd.alloc;
                   (name[],none,this(ObjectTemplate)[]) -> ObjTmpStack.add
               if)
            #);
          genClass::
            (#(* N: ^text; sig: ^Signature
            enter(N[],sig[])*)
            do (N[],sig[],IS.descNo) -> IS.genClass
            #)
       #);
     ObjectDesc::
       (#
          genClass::
            (# (*N: ^text; sig: ^Signature
            enter(N[],sig[])*)
            do (N[],sig[],IS.descNo) -> IS.genClass
            #)
       #);
     items::
       (# genClass:
            (# N: ^text; sig: ^Signature; descNo: @integer; T: @text
            enter(N[],sig[],descNo)
            do (N[], descNo, false, 1,1) -> cd.class;
               (* super *)
               ('origin',originOff) -> cd.rstore; 
               (if sig[] <> none then sig.storeParameters if);
               
               false -> cd.actions;
               
               scanSons
               (# I: ^Item
               do current[] -> I[];
                  (if I.isDecl then 
                      current.gen; 
                  if) 
               #);
               scanSons
               (# I: ^Item
               do current[] -> I[];
                  (if not I.isDecl then 
                      current.gen
                  if) 
               #);
               (* push return value 
                * - for staticObject: R: @Foo
                *   Foo should return (this(Foo)
                *   perhaps default?
                * - otherwise push variables in -> 
                *)
               (if sig[] <> none then 
                   sig.pushReturnValues 
                else
                   (* puhsThis?*)
               if);
               'D' -> cd.rtn;
               (vsize,rsize,1) -> CD.endClass     
            #);
          goOrigin:
            (# on: @integer; org: ^Items
            enter on
            do (if on > 0 then
                   ('origin',originOff) -> cd.rpushg;
                   on - 1 -> (origin[]->org[]).goOrigin
               if)
            #);               
       #);
     UnaryPattern::
       (# gen:: (# do (sig.id[],sig[],OD[]) -> objTmpStack.add #);
          loadAdr::
            (#
            do 'UnaryPattern:loadAdr: '->puttext; doPT -> putline;
               cd.pushThis;
            #);
          load::
            (#
            do ('UnaryPattern:load: ',sig.doPT) -> cd.com2; 
               (sig.id[],OD.IS.descno,true) -> cd.alloc;
               (if not isStaticObj then
                   cd.rpop;
                   (* reconsider if return values *)
               if);
               (* and perhaps a difference
                *    R: @foo -- no rpop
                *    foo;    -- if foo on rstack it should be popped
                *)
            #);
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #);   
       #);
     BinaryPattern::
       (# gen:: (# do (sig.id[],sig[],OD[]) -> objTmpStack.add #);
          loadAdr::
            (#
            do 'BinaryPattern:loadAdr: '->puttext; sig.doPT -> putline;
            #);
          load::
            (#
            do ('BinaryPattern:load: ',sig.doPT) -> cd.com2;
               (sig.id[],OD.IS.descno,true) -> cd.alloc;
               (if not isStaticObj then
                   cd.rpop; (* depend on the return type *)
               if)
            #);
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #); 
       #);
     BasicBinaryPattern::
       (# load:: 
            (# 
            do OD.IS.props.basicProp.primno -> cd.op 
            #);
       #);
     FunctionalPattern::
       (# gen:: (# do (sig.id[],sig[],OD[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          loadAdr:: (# #);
          load:: 
            (# 
            do ('FunctionalPattern:load: ',sig.doPT) -> cd.com2;
               (sig.id[],OD.IS.descno,true) -> cd.alloc;
               (if not isStaticObj then
                   cd.rpop;
                   (* reconsider if return values *)
               if);
            #);
       #);
     BasicFunctionalPattern::
       (# load:: (# do ('put',OD.IS.props.basicProp.primNo) -> cd.Callprim #);
       #);
     KeyWordPattern::
       (# gen:: (# do (sig.id[],sig[],OD[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          loadAdr:: (# #);
          load:: 
            (# 
            do (if not isStaticObj then
                   (sig.id[],OD.IS.descno,true) -> cd.alloc 
               if)
            #);          
       #);
     BasicKeyWordPattern::
       (# load:: 
            (# exitLab: @labelHandler.label
            do (if OD.IS.props.basicProp.primNo
                // 100 then
                   exitLab.new;
                   1111 -> cd.pushConst;                   
                   exitlab -> cd.jmpFalse;
                   2222 -> cd.pushConst;
                   exitLab -> cd.defLab
               if)
            #)
       #);
            
     Invocation::
       (* E1.E2.E3
        *    E1.pushThis
        *    E1.loadAdr
        *    E2.loadAdr
        *    E3.execute
        * R.foo
        * R.foo{...}
        * X:= y + 12
        * R ++ E  -- problem E1 = R ++ E , i.e. first = true 
        * perhaps same problem for keyWord
        * We must reconsider parsing/AST and/or gen below
        *)
       (# gen:: 
            (# first: @boolean
            do true -> first;
               scanSons
               (# E: ^Exp; rec: ^Exp
               do current[] -> E[];
                  (if first then E.pushThis if);
                  (if not isLast then 
                      E.loadAdr 
                   else
                      (rec[],isStaticObj) -> E.execute
                  if);
                  E[] -> rec[];
                  false -> first
               #)
            #);
          loadOrigin:
            (# 
            do 'Invocation:loadOrigin' -> cd.comment
            #);
       #);
     Unary::
       (* May be 
        *  A simple variable X declared as X:@integer; 
        *  An object R declared as R: @Foo
        *  A pattern Foo declared as Foo: Bar{ ... }
        * ATd = operator.Decl
        *)
       (# pushThis:: 
            (# T: @text; org: ^Items
            do cd.pushThis; 
               'Unary:GoOrigin: ' -> T; on -> T.putint; T[] -> cd.comment;
               origin[]-> org[];
               on -> org.goOrigin
            #); 
          loadAdr:: 
            (# T: @text;
            do 'GoOrigin: ' -> T; on -> T.putint; T[] -> cd.comment;
               ATd.loadAdr 
            #);
          execute:: (# do isStaticObj -> ATd.load #);
       #);
     Binary::
       (# pushThis:: 
            (#
            do ('Binary:pushThis: ',doPT) -> MkCom;
               ATd.pushThis 
            #);
          loadAdr:: 
            (# 
            do ('Binary:loadAdr:', dopt) -> MkCom;
               ATd.doPT -> cd.comment;
               ATd.loadAdr;
               (* execute *)
            #);
          execute::
            (#
            do (if isAssign then 
                   argument.gen;
                   ('var',rec.ATd.off) -> cd.storeg
                else
                   (if rec.ATd.isBasic then 
                       isStaticObj -> rec.ATd.load
                   if);
                   argument.gen;
                   isStaticObj -> ATd.load
               if)
            #)
       #);
     Function::
       (# pushThis:: (#do ATd.pushThis #);
          loadAdr:: (# do ATd.loadAdr; (*execute*) #);
          execute::
            (# first: @boolean
            do (*ATd.loadAdr;*)
               true -> first;               
               scanSons
               (# E: ^Exp; I: ^Invocation; OG: ^ObjectGeneration
               do (if not first then 
                      current.gen;
                      (if current## <= objectGeneration## then 
                          current[] -> OG[];
                          OG.IV.swapr
                       else
                          '\n!!! Function: NOT OGV: ' -> puttext; 
                          current.dopt -> putline;
                          current.PP -> putline
                      if)
                  if);
                  false -> first
               #);
               (if ATd[] = none then 
                   'none' -> putline
                else
                   isStaticObj -> ATd.load
               if)
            #)          
       #);
     KeyWord::
       (# pushThis:: (#do ATd.pushThis #);
          loadAdr:: (# do execute #);
          execute::
            (# isArg: @boolean
            do scanSons
               (#
               do (if isArg then
                      current.gen
                  if);
                  not isArg -> isArg
               #);
               isStaticObj -> ATd.load
            #);
       #);
     Const::
       (# execute:: (# do T.setpos; T.getInt -> cd.pushConst #);
       #);
     ObjTmpStack: @
       (# item: 
            (# N: ^text; sig: ^Signature; OT: ^ObjectTemplate;
            enter(N[],sig[],OT[])
            exit this(item)[]
            #);
          add:
            (# N: ^text; sig: ^Signature; OT: ^ObjectTemplate;
            enter(N[],sig[],OT[])
            do (if (top+1-> top) > OTL.range then OTL.range -> OTL.extend if);
                   (N[],sig[],OT[]) -> item -> OTL[top][]
            #);          
          next:
            (# inx: @integer
            do Loop:
                 (#
                 do inx +1 -> inx;
                    (if inx <= top then
                        (OTL[inx].N[],OTL[inx].sig[]) -> OTL[inx].OT.genClass;
                        restart Loop
                    if);
                 #)
            #);               
          OTL: [10] ^Item;top: @integer;
       #);
     labelHandler: @
       (# labCount: @integer;
          label:
            (# labNo: @integer;
               new: (# do  labCount + 1 -> labCount -> labNo #);
            enter labNo
            exit labNo
            #);
          init: (# do 0 -> labCount #);
       #);
      markIncludes,EPpath: @ ModulePath;
     
     ModulePath:
       (* mark all modules used by main module,and
        * find origin offsets for the nested modules enclosing main
        * We dont seem to use the origin offsets for QBETA 
        * -- needed for BETA, but for QBETA Alloc executes the object,
        * -- i.e. allocation of Main in BETAworld executes Main
        * -- with correct origin
        *)
       (# find:
            (# md: ^module;
               MI: ^ModuleItem;
               org: ^node;
               D: ^Items;
               trace: (#exit false #);
            enter md[]
            do (if md[] <> none then
                   (if false then
                       '\nModule: ' -> puttext; md.SO.sig.doPT -> puttext;
                       ' descNo: ' -> puttext; (md.desc).descNo -> putint; 
                       ' imports: ' -> putline;
                   if);
                   md.mark;
                   (if md.origin[] <> none then
                       md.SO.off -> addOff;
                       (* else top module BETAworld *)
                   if);    
                   md.origin[] -> org[];
                   FindModule: (* same as encModule?*)
                     (if org[] <> none then 
                         (if org## <> Module## then 
                             org.father[] -> org[]; restart FindModule 
                         if);
                         org[] -> find 
                      else 
                         md[] -> topModule[] 
                     if)
               if)
            #);
          
          topModule: ^module; (* BETAworld *)
          orgOff: [6] @integer; top: @integer;
          addOff: (* not needed, see above *)
            (# off: @integer
            enter off
            do (if (top+1 -> top) > orgOff.range then 
                   orgOff.range -> orgOff.extend 
               if);
               off -> orgOff[top];
               (*' addOff: ' -> puttext; off -> putint; ' ' ->put*)
            #);
          main: ^Module
       enter main[]
       do 0 -> top;
          main[] -> find;
       #);
     genMain:
       (# descno: @integer
       do ('main',newDesc,false,1,0) -> cd.class;
          cd.pushThis;
          (if true then
              'TopModule: ' -> puttext;
              markIncludes.topModule.SO.sig.doPT -> putLine;
              'Main: ' -> puttext; 
              main.doPt -> putline
           if);
          ('BETAworld',(markIncludes.topModule.desc).descno,true) -> cd.alloc;
          cd.stop; (* We should generate stop here and not in betaVM *)
          (0,0,0) -> CD.endClass;
          
          (if markIncludes.main## <> Module## then
              '\n\n***** Only Modules can be executed *****\n\n'
                -> putline
          if)
       #);
     genEventProcessor:
       (#
       do ('EventProcessor',newDesc,false,1,0) -> cd.class;
          cd.stop (* we should generate stop here and not in betaVM *)
       #);
     
     mainDescNo: (# exit newDesc.noOfDescs -1 (*- 2*) (* ad hoc *) #);
     cd: @ObjectCode;
     descs: ^RunTimeDescriptors;
     BC: ^File;
  enter BC[]
  do 'GENERATOR' -> putline;
     (if true then
         BC.openWrite;
         BC[] -> cd.init;
         'QBETA'-> cd.comment;
         
         labelHandler.init;

         main[] -> markIncludes;

         rootModule.gen;

         ObjTmpStack.next;
         
         genMain;
         genEventProcessor;
         
         cd.dump;
         
         cd.descs[] -> descs[]; (* why ?*)         
         BC.close;
     if)
  #)

