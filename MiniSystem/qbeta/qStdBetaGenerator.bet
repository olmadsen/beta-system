ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qStdBetaAllocator';
INCLUDE '../VM/objectcode'
---LIB:attributes---
qStdBetaGenerator: qStdBetaAllocator
  (# StdBetaModule::<
       (# gen:: 
            (# 
            do '**** StdBetaModule:gen:'-> dumpT; moduleName.doPT->dumpTN;
               cd.fcom
               (#
               do 'StdBetaModule:gen:'->TT; moduleName.dopt->TQ; #);
               frags.gen
            #)
       #);   
     BetaFragments:: (# gen:: (#do scanSons(#do current.gen #); #)#);
     AttFrag::
       (# gen:: (# do cd.fcom(#do thename.dopt->TT #); att.gen #)
       #);   
     ObjDescFrag::
       (# gen:: 
            (# 
            do OD[] -> objTmpStack.add;  
               cd.fcom(#do thename.dopt->TT #) 
            #)
       #);   
     DoPartFrag::
       (# gen:: 
            (# 
            do cd.fcom(#do 'DoPartFrag:'->TT; thename.dopt->TT #);
               DP.gen
            #)
       #);
     AttSlot::<
       (# gen:: (#do scanBindings(#do current.gen #)#);
       #);
     ObjDescSlot::<
       (# gen::
            (#
            do scanBindings
               (# ODF: ^ObjDescFrag
               do (current[] -> ODF[]).OD[] -> objTmpStack.add;
               #)
            #);
       #);
     DoPartSlot::<
       (# gen::
            (#
            do cd.com(#do 'DoPartSlot:gen:'->TT; theName.dopt -> TT #);
               scanBindings
               (# DP: ^DoPartFrag
               do cd.com(#do 'call:gen:'->TT; theName.dopt->TT ;
                            (*current.dopt->TT*) #);
                  (current[] -> DP[]).gen
               #)
            #);
       #);
     Attributes::
       (# gen:: (#do scanSons(#do current.gen #);#)
       #);
          
     ObjectDesc::
       (# gen:: 
            (#
            do this(ObjectDesc)[] -> objTmpStack.add;
               cd.fcom
               (#do 'ObjectDesc:gen:'->TT; slt[] <> none -> BB(*dopt -> TT *)#);
            #);
          load::
            (#
            do this(ObjectDesc)[] -> objTmpStack.add;
               cd.fcom(#do 'ObjectDesc:load:'->TT; descNo-> II; dopt -> TT #);
               cd.pushThis;
               ('Singular',descNo,false) -> cd.alloc;
               ('singular','S') -> doCall
            #);
          activate::
            (#
            do cd.fcom(#do 'ObjectDesc:activate'->TT; descNo->II; #);
               load
            #);
          isExternal: BooleanValue
            (# primNo: @integer
            do (if sup.isExternal then
                   cd.com(#do 'External:'->TT; dopt -> TT; #);
                   L:
                     acp.imps.scanSons
                     (#
                     do cd.com(#do current.doPP -> TT#);
                        current.scanSons
                        (# i: @integer
                        do current.getPrimNo -> primNo;
                           cd.com(#do primNo -> II #);
                           true -> value;
                           leave L
                     #)#);
                   (if primNo 
                    // 2 then 
                       ('put',2) -> cd.callPrim
                    else
            if)if)#);
          genClass:
            (# handleSuper:
                 (#
                 do (if superDesc[] <> none then
                        (if not sup.isThisBlockLevel then
                            ('origin',originOff) -> cd.rstore; 
                            (*sup.loadOrigin -> N[]; *)
                            cd.pushThis; (* ??? *)
                            'super' -> N[] (* fix *)
                        if);
                        (N[],superDesc.descNo) -> cd.exeAlloc;
                        cd.rpop;
                        (N[],superDesc.descNo)  -> cd.super; 
                        (if false then
                            '**** vdtTableCopy:'->dumpt; 
                            descNo -> putint; ' ' -> put;
                            superDesc.descNo -> putint; ' ' -> put;
                            dopt -> putHeadN;
                            '**    super:'->dumpTN;
                            superDesc.dopt -> putheadN;
                        if);
                        superDesc.descNo  -> cd.vdtTableCopy;
                        (* innerInx - fix *)
                     else
                        ('origin',originOff) -> cd.rstore; (* save origin *)
                    if);
                 #);
               T,orgId,N: ^Text; orgDescNo: @integer; org: ^ObjectDesc
            do 'StdBeta$'-> T[];
               (if NS[] <> none then
                   NS.scanSons(#do current.dopt -> T.append #)
                else 
                   (if origin[] = none then
                       'betaenv' -> T.append;
                    else
                       'S$' -> T.append; descNo -> T.putint
               if)if);
               cd.fcom
               (#do 'ObjectDesc:genClass:'-> TT; T[] -> TQ; slt[]<>none->BB #);
               
               (if (superDesc[] <> none) and not superDesc.done then
                   (* pretty ad hoc - since we copy the VDtable from super, 
                    * code must have been generated for super; 
                    * done is introduced to handle this. 
                    * But fix it
                    *)
                   (*sup.name -> *) superDesc.genClass;
               if);
               (if origin[] <> none then
                   (origin[]->org[]).descNo -> orgDescNo;
                   'origin' -> orgId[]
                else
                   'origin' -> orgId[]
               if);
               (if not done then
                   true -> done;
                   (if slt[] = none then
                       (T[],descNo,false,(topSuper).descNo
                       ,originOff,orgId[],orgDescNo,false,false)
                         -> cd.class;
                       cd.bcPos -> beginBCP;
                       (* gen super, save origin, vdtTableCopy, etc  *)
                       handleSuper;
                       (if false then ('super',501) -> cd.exeAlloc; if);
                       att.gen;
                       (* check generator: more may be need here including 
                        * for-fields
                        *)
                       'A' -> cd.rtn; 
                       
                       cd.markEnterE;
                       acp.gen;
                    else
                       slt.scanBindings
                       (# ODF: ^ObjDescFrag
                       do cd.fcom
                          (#do 'ObjDesc:gen:slot:'->TT; current.dopt -> TT #);
                          (current[]->ODF[]).OD.genClass
                       #)
                   if);
                   (objSize,false,0) -> cd.endclass;
               if)
            #);   
          doCall::
             (# TP: ^type; xType: @integer
             do cd.fcom(#do 'ObjDesc:doCall'->TT; LMR ->CC #);
                (if (exitType->TP[]) <> none then
                    (if TP.isRef then
                        2 -> xType
                     else
                        1 -> xType
                if)if);
                (if false then
                    doPt->putline;
                    'exitType: ' -> puttext; xType -> putint; newline;
                if);
                (if LMR
                 // 'S' then 
                    (if hasDoPart then (N[],'D') -> cd.call if);
                    cd.rtnEvent;                        
                 // 'L' then
                    (if hasDoPart then (N[],'D') -> cd.call if);
                    (N[],'X') -> cd.call;
                    xType -> cd.rtnEvent
                 // 'M' then
                    (N[],'N') -> cd.call;
                    (if hasDoPart then (N[],'D') -> cd.call if);
                    (N[],'X') -> cd.call;
                    xType -> cd.rtnEvent
                 // 'R' then
                    (N[],'N') -> cd.call;
                    (if hasDoPart then (N[],'D') -> cd.call if);
                    cd.rtnEvent;                     
                if);
             #);
          goOrigin::
            (#
            do (if on > 0 then
                   ('origin',originOff) -> cd.rpushg;
                   on - 1 -> origin.goOrigin
            if)#);
          done: @boolean
       #);
     DoPart::<
       (# gen::
            (#
            do cd.com(#do 'DoPart:gen:'->TT; #);
               (if imps[] <> none then imps.gen if)
            #);
       #);
     BetaDecl::
       (# gen:: (# do NS.gen; IT.gen #);
          load::
            (#
            do cd.fcom
               (#do 'BetaDecl:load:'->TT; NS.label->TT; NS.doPT->TQ; #);
            #);
          doAlloc::
            (#
            do cd.fcom
               (#do 'BetaDecl:doAlloc:'->TT; NS.doPT->TQ; IT.label->TT #);
               IT.doAlloc
            #);
          
       #);
     BetaNameDecl::
       (# gen::
            (#
            do cd.fcom(#do 'NameDecl:'->TT; N.T[]->TQ #);
               theDcl.IT.gen
            #);
          load:: 
            (# 
            do cd.fcom
               (#do 'NameDecl:load:'->TT; N.T[]->TQ; theDcl.label->TT #);
               (N.T[],off) -> theDcl.IT.loadItem 
            #);
          store::
            (#
            do cd.fcom
               (#do 'NameDecl:store:'->TT; N.T[]->TQ; theDcl.label->TT #);
               (if isRef then
                   (N.T[],off) -> theDcl.IT.storeItem
                else
                   cd.fcom(#do 'nameDecl:store:exe:'->TT; #);
                   (N.T[],off) -> theDcl.IT.loadItem;
                   (N.T[],'R') -> (theDcl.IT.theDesc).doCall
               if)
            #);
          activate::
            (#
            do cd.fcom
               (#do 'NameDecl:activate:'->TT; N.T[]->TQ; 
                  theDcl.label->TT; LMR->CC; isRef->BB #);
               (this(BetaNameDecl)[],LMR,isRef) -> theDcl.IT.activate
            #);
          doAlloc::
            (#
            do cd.fcom
               (#do 'NameDecl:doAlloc:'->TT; N.T[]->TQ; theDcl.label->TT #);
               theDcl.doAlloc
            #);
       #);
     StaticItem::
       (# loadItem::
            (#
            do cd.fcom
               (#
               do 'StaticItem:loadItem:'->TT; id[]->TQ; 
                  (if NS.theDesc <> none then
                      (NS.theDesc).doPT -> TT;
                      (NS.theDesc).isPrimitive->BB;
                  if); 
               #);
               (if (NS.theDesc <> none) and (NS.theDesc).isPrimitive then
                   (id[],off) -> cd.pushg
                else
                   (id[],off) -> cd.rpushg
               if)
            #);
          storeItem::
            (#
            do cd.fcom
               (#
               do 'StaticItem:storeItem:'->TT; id[]->TQ; 
                  (if NS.theDesc <> none then
                      (NS.theDesc).doPT -> TT;
                      (NS.theDesc).isPrimitive->BB;
                  if); 
               #);
               (id[],off) -> cd.rstoreg
            #);
          activate::
            (#
            do cd.fcom
               (#do 'StaticItem:activate:'->TT; ND.dopt->TQ;LMR->CC;isRef->BB#);
               (if isRef then
                   (if LMR
                    // 'L' then 
                       (ND.dopt,ND.off) -> cd.rpushg
                    // 'M' then
                       (ND.dopt,ND.off) -> cd.rpushg;
                       cd.rdouble
                    // 'R' then
                       (ND.dopt,ND.off) -> cd.rstoreg
                   if)
                else
                   (if (NS.theDesc <> none) and (NS.theDesc).isPrimitive then
                       (if LMR
                        // 'L' then
                           (ND.dopt,ND.off) -> cd.pushg
                        // 'M' then
                           (ND.doPT,ND.off) -> cd.pushg;
                           cd.double
                        // 'R' then
                           (ND.doPT,ND.off) -> cd.storeg
                       if)
                    else
                       (ND.doPT,LMR) -> doCall
                   if)
               if)
            #);
          gen::
            (#
            do (if OS.isSingular then 
                   cd.fcom(#do 'StaticItem:gen:'->TT; NS.doPT-> TT; #);
                   OS[] -> objTmpStack.add 
               if);
            #)
       #);
     DynamicItem::
       (# loadItem::
            (#
            do (id[],off) -> cd.rpushg
            #);
          storeItem::
            (#
            do cd.fcom
               (#do 'DynamicItem:storeItem:'->TT; OS.dopt->TQ; (*isRef->BB*)#);
               (if true (*isRef*) then
                   (id[],off) -> cd.rstoreg
                else
                   (*'R' -> (OS.theDesc).doCall*)
               if)
            #);
          activate::
            (#
            do cd.fcom
               (#do'DynamicItem:activate:'->TT; ND.dopt->TQ;LMR->CC;isRef->BB#);
               (if isRef then
                   (if LMR
                    // 'L' then 
                       (ND.dopt,ND.off) -> cd.rpushg
                    // 'M' then
                       (ND.dopt,ND.off) -> cd.rpushg;
                       cd.rdouble
                    // 'R' then
                       (ND.dopt,ND.off) -> cd.rstoreg
                   if)
                else
                   (ND.doPT,LMR) -> doCall
            if)#)
       #);
     BetaPattern::
       (# loadItem::
            (#
            do cd.fcom(#do 'PTN:loadItem:'->TT; NS.dopt -> TQ #);
               (if true then
                   doAlloc
                else
                   (NS.doPT,OD.descNo,false) -> cd.alloc;
               if);
               (NS.dopt,'S') -> OD.doCall
            #);
          storeItem::
            (#
            do cd.fcom(#do 'PTN:storeItem:'->TT; NS.dopt -> TQ #);
               (if OD.isExternal then
                else
                   (if true then
                       loadItem
                    else
                       (NS.doPT,OD.descNo,OD.objSize,0,false) -> cd.invoke
               if)if)
            #);
          activate::
            (#
            do cd.fcom
               (#do 'PTN:activate:'->TT; ND.dopt->TQ; LMR->CC;isRef->BB#);
               (if OD.isExternal then
                else
                   doAlloc;
                   (NS.doPT,LMR) -> OD.doCall
               if)
            #);
          gen:: 
            (#
            do (if OD.slt[] = none then
                   OD[] -> objTmpStack.add;
                else
                   OD.slt.scanBindings
                   (# ODF: ^ObjDescFrag
                   do (current[] -> ODF[]).OD[] -> objTmpStack.add
                   #)
            if)#);
          doAlloc::
            (#
            do cd.fcom
               (#do 'PTN:doAlloc:'->TT; NS.dopt->TQ; 'isSlot:'->TT;
                  OD.slt[] <> none -> BB 
               #);
               (if OD.slt[] = none then
                   (NS.doPT,OD.descNo,false) -> cd.alloc
                else
                   cd.fcom(#do 'findBinding'->TT #);
                   OD.slt.scanBindings
                   (# ODF: ^ObjDescFrag
                   do cd.fcom(#do 'got:'->TT; current.dopt->TT #);
                      (NS.doPT,(current[] -> ODF[]).OD.descNo,false) -> cd.alloc
            #)if)#);
       #);
     BetaVirtualPattern:: 
       (# loadItem::
            (#
            do 
            #);
          gen::
            (#
            do cd.fcom(#do 'VirtualPtn:doAlloc:'->TT; NS.dopt -> TQ; #);
               (if OS.isSingular then OS[] -> objTmpSTack.add if);
               NS.scanSons
               (# ND: ^BetaNameDecl
               do ((current[]->ND[]).off,OS.getDescNo,false,'LLVMsig') 
                    -> cd.vdtTable;
               #)
            #);
          doAlloc::
            (# 
            do cd.fcom(#do 'VirtualPtn:doAlloc:'->TT; NS.dopt -> TQ;
                         'dispatch needed'->TQ #);
               (if true then
                   NS.scanSons
                   (# ND: ^BetaNameDecl
                   do current[]->ND[];
                      (ND.doPT,ND.off,0,false) -> cd.invokeV;
                      (* 0 is noOfRefArgs - must be defined *)
                   #)
                else
                   (NS.doPT,OS.getDescNo,false) -> cd.alloc
               if)
            #);
       #);
     furtherBinding:: 
       (# gen::
            (#
            do cd.fcom(#do 'FurtherPtn:doAlloc:'->TT; NS.dopt -> TQ; #);
               (if OS.isSingular then OS[] -> objTmpSTack.add if);
               NS.scanSons
               (# ND: ^BetaNameDecl
               do ((current[]->ND[]).off,OS.getDescNo,false,'LLVMsig') 
                    -> cd.vdtTable;
               #)
            #);
          doAlloc::
            (#
            do cd.fcom(#do 'FurtherPtn:doAlloc:'->TT; NS.dopt -> TQ;
                      'dispatch needed'->TQ #);
               (NS.doPT,OS.getDescNo,false) -> cd.alloc
            #);
       #);
     FinalBinding:: 
       (# gen::
            (#
            do cd.fcom(#do 'FinalPtn:doAlloc:'->TT; NS.dopt -> TQ; #);
               (if OS.isSingular then OS[] -> objTmpSTack.add if);
               NS.scanSons
               (# ND: ^BetaNameDecl
               do ((current[]->ND[]).off,OS.getDescNo,false,'LLVMsig') 
                    -> cd.vdtTable;
               #)
            #);
          doAlloc::
            (#
            do cd.fcom(#do 'FinalPTN:doAlloc:'->TT; NS.dopt -> TQ #);
               (NS.doPT,OS.getDescNo,false) -> cd.alloc
            #);
       #);
          
     ActionPart::
       (# gen::
            (#
            do cd.bcPos -> beginBCP;
               enterP.gen;
               (if imps[] <> none then 
                   (false (*(superDesc[] <> none)*)) -> cd.actions;
                   cd.mvStack;
                   imps.gen;
                   'd' -> cd.rtn;
               if);
               exitP.gen;
               cd.bcPos -> endBCP
            #);
       #);
     EnterPart::
       (# gen::
            (# 
            do cd.bcPos -> beginBCP;
               (if eval[] <> none then
                   cd.enterP; (* generate no code *)
                   (*'R' -> *) eval.gen;
                   (none,'R',false) -> eval.activate;
                   'n' -> cd.rtn 
               if);
               cd.bcPos -> endBCP;
            #)
       #);
     Imperatives::
       (# gen::
            (#
            do scanSons(#do current.gen #)
            #)
       #);
     ExitPart::
       (# gen::
            (#
            do cd.bcPos -> beginBCP;
               cd.markExitE;
               (* do gen exit-part *)
               (none,'L',false) -> eval.activate;
               'X' -> cd.rtn;
               cd.bcPos -> endBCP;
            #)
       #);

     IfImp::
       (# gen::
            (# elseLab, Xlab,exitLab: @labelHandler.label
            do cd.fcom(#do 'IfImp:gen:'->TT; dopt -> TT #);
               cd.bcPos -> beginBCP;
               exitlab.new;
               (if not elsePart.isNull then
                   elseLab.new; 
                   elseLab -> Xlab
                else
                   exitLab -> xLab
               if);               
               cd.fcom(#do 'IfImp:eval:'->TT; eval.label->TT #);
               (*'L' -> * ) Eval.gen; *)
               (none,'L',false) -> eval.activate;
                              
               (if not thenPart.isNull then
                   (* simpleif: (if e then I1 else I2 if *)
                   (Xlab,Xlab) -> cd.jmpFalse; (* cd expects a Tlab and an Flab
                                                * due to LLVM - must check *)
                   thenPart.gen
                else
                   (Xlab[],exitLab[]) -> alt.select
               if);
               
               (if not elsePart.isNull then
                   (* Sometimes extra jmp here perhaps if thenPart.isNull *)
                   exitLab -> cd.jmp;
                   elseLab -> cd.defLab;                   
                   elsePart.gen
               if);
               exitLab -> cd.defLab;
               cd.bcPos -> endBCP               
       #)#);
     Alternatives::
       (# select:
            (# xLab,exitLAb: ^LabelHandler.label; 
               A: ^Alternative;
               nLab: @LabelHandler.label
            enter(xLab[],exitLab[])
            do cd.bcPos -> beginBCP;
               scansons
               (#
               do current[] -> A[]; 
                  (if isLast then
                      (false,xLab[],exitLab[]) -> A.select
                   else
                      nLab.new;
                      (true,nLab[],exitLab[]) -> A.select;
                      nLab -> cd.defLab
                  if)
               #);
               cd.bcPos -> endBCP;
       #)#);
     Alternative::
       (# select:
            (# double: @Boolean; xLab,elseLAb: ^LabelHandler.label;
               tLab: @LabelHandler.label
            enter(double,xLab[],elseLab[])
            do cd.fcom(#do 'Alternative:'->TT; dopt -> TT #);
               cd.bcPos -> beginBCP;
               tLab.new;
               (double,tLab[],xLab[]) -> sel.select;
               tLab -> cd.defLab;
               imps.gen;
               elseLab -> cd.jmp;
               cd.bcPos -> endBCP;
       #)#);
     Selections::
       (# select:
            (# double: @Boolean; tLab,xLab: ^LabelHandler.label;
            enter(double,tLab[],xLab[])
            do cd.bcPos -> beginBCP;
               scansons
               (# S: ^Selection
               do current[] -> S[];
                  (if isLast then
                      (double,true,xLab[]) -> S.select 
                   else
                      (true,false,tLab[]) -> S.select
                  if)
               #);
               cd.bcPos -> endBCP;
       #)#);
     Selection::
       (# select:
            (# double,isLast: @boolean; lab: ^LabelHandler.Label
            enter(double,isLast,lab[])
            do (if double then cd.double if);
               (*'L' -> *) EV.gen;
               lex.symbols.eq -> cd.op;
               (if isLast then
                   (lab,lab) -> cd.jmpFalse; (* check objectcode *)
                   (if double then cd.vpop if);
                else
                   lab -> cd.jmpTrue
            if)#);
       #);
     ForImp::
       (# gen:: (# do cd.fcom(#do 'ForImp:gen:missing'->TT #); #)
       #);
     LabelledImp::
       (# gen:: (# do cd.fcom(#do 'LabelledImp:gen:missing'->TT #); #)
       #);
     LeaveImp::
       (# gen:: (# do cd.fcom(#do 'LeaveImp:gen:missing'->TT #); #)
       #);
     RestartImp::
       (# gen:: (# do cd.fcom(#do 'RestartImp:gen:missing'->TT #); #)
       #);
     InnerImp::
       (# gen:: (#do  cd.fcom(#do 'InnerImp:gen:missing'->TT #); #)
       #);
     
     Evaluation::
       (# gen::
            (#
            do cd.fcom(#do 'Eval:gen:'->TT; dopt -> TQ #);
               (if useActivate then
                   (none,'S',false) -> activate
               else
                   scanSons
                   (#
                   do (if isFirst then
                          current.load
                       else
                          (*isRef ->*) current.store
            if)#)if)#);
          load:: (#do gen #);
          activate::
            (# LMRx: @char
            do cd.fcom(#do 'Eval:activate:'->TT; dopt->TQ; LMR->CC;isRef->BB#);
               scanSons
               (#
               do (if true
                   // isFirst and isLast then
                      (if LMR 
                       // 'S' then
                          'S' -> LMRx
                       // 'L' then
                          'L' -> LMRx
                       // 'R' then
                          'R' -> LMRx
                      if)
                   // isFirst then
                      (if LMR
                       // 'S' then
                          'L' -> LMRx
                       // 'L' then
                          'L' -> LMRx
                       // 'R' then
                          'M' -> LMRx
                      if)
                   // isLast then
                      (if LMR
                       // 'S' then
                          'R' -> LMRx
                       // 'L' then
                          'L' -> LMRx
                       // 'R' then
                          'M' -> LMRx
                      if)
                   else
                      'M' -> LMRx
                  if);
                  (ND[],LMRx,isRef) -> current.activate
               #)
            #);
       #);
     PatternDen::
       (# gen::
            (#
            do cd.fcom(#do 'PatternDen:gen:'->TT; AD.dopt->TQ #);
               AD.gen
            #);
          load:: 
            (#
            do cd.fcom(#do 'PatternDen:load:'->TT; AD.label->TQ; AD.doPT->TT #);
               AD.load 
            #);
          store:: 
            (#
            do cd.fcom(#do 'PatternDen:store:'->TT; AD.label->TQ; AD.doPT->TT#);
               isRef -> AD.store 
            #);
          activate::
            (#
            do cd.fcom
               (#do 'patternDen:activate:'->TT; dopt->TT; LMR->CC; isRef->BB#);
               (ND[],LMR,isRef) -> AD.activate
            #);
          doAlloc::
            (#
            do cd.fcom(#do 'PatternDen:doAlloc:'->TT; AD.label->TT; dopt->TT #);
               AD.doAlloc
            #);
          doCall::
            (#
            do cd.fcom(#do 'PatternDen:doCall:'->TT; AD.label->TT; dopt->TT #);
               (N[],LMR) -> AD.docall
            #)
       #);
     ObjectEvaluation::
       (# load:: 
            (# 
            do cd.fcom
               (#do 'ObjectEvaluation:load'->TT; OS.label->TT; OS.dopt->TT #);
               OS.load 
            #);
          store:: 
            (# 
            do cd.fcom
               (#do 'ObjectEvaluation:store'->TT; OS.label-> TT; OS.dopt->TT #);
               isRef->OS.store 
            #);
          activate::
            (#
            do cd.fcom
               (#do 'ObjectEval:activate:'->TT; dopt->TT; LMR->CC; isRef->BB#);
               (ND[],LMR,isRef) -> OS.activate
            #);
       #);
     BetaBinaryExp::
       (# gen::
            (#
            do cd.fcom(#do 'BinExp:gen:'->TT #);
               lE.load; rE.load; 
               (if op
                // lex.symbols.eq then
                   (if true
                    // (lE.exitType).isRef then
                       cd.req
                    // (lE.exitType).isStrucRef then
                       cd.seq
                    else
                       op ->  cd.op;
                   if)
                // lex.symbols.ne then
                   (if true 
                    // (lE.exitType).isRef  then
                       cd.rne                       
                    // (lE.exitType).isStrucRef then
                       cd.sne
                    else
                       op -> cd.op;
                   if)                   
                else
                   op -> (*lex.symbols.asText ->*) cd.op;
               if);
            #);
          load:: (#do cd.fcom(#do 'BinExp:load:'->TT #); gen #);
          activate:: 
            (#
            do cd.fcom(#do 'BinExp:activate:'->TT #);
               (none,'L',false) -> lE.activate;
               (none,'L',false) -> rE.activate;
               (if op
                // lex.symbols.eq then
                   (if true
                    // (lE.exitType).isRef then
                       cd.req
                    // (lE.exitType).isStrucRef then
                       cd.seq
                    else
                       op ->  cd.op;
                   if)
                // lex.symbols.ne then
                   (if true 
                    // (lE.exitType).isRef  then
                       cd.rne                       
                    // (lE.exitType).isStrucRef then
                       cd.sne
                    else
                       op -> cd.op;
                   if)                   
                else
                   op -> (*lex.symbols.asText ->*) cd.op;
            if)#)
       #);
     BetaTerm::
       (# load:: 
            (# 
            do cd.fcom(#do 'Term:load'->TT; T.label-> TT; T.dopt -> TT #);
               T.load
            #); 
          store:: (# do isRef->T.store #); 
          activate::
            (#
            do cd.fcom
               (#do 'Term:activate:'->TT; dopt->TT; LMR->CC; isRef->BB#);
               (ND[],LMR,isRef) -> T.activate
            #);
       #);
     ObjectReference::
       (# load::
            (#
            do cd.fcom(#do 'ObjRef:load:'->TT; OV.label-> TT; OV.dopt -> TT #);
               OV.load
            #);
          store::
            (#
            do cd.fcom(#do 'ObjRef:store:'->TT; OV.label-> TT; OV.dopt -> TT #);
               true -> OV.store
            #);
          activate::
            (#
            do cd.fcom
               (#do 'ObjRef:activate:'->TT; dopt->TT; LMR->CC; isRef->BB#);
               (ND[],LMR,true) -> OV.activate
            #);
          doAlloc::
            (#
            do cd.fcom(#do 'ObjRef:doAlloc'->TT; OV.dopt->TT #); OV.doAlloc
            #)
       #);
     DynamicObjectGeneration::
       (# load::
            (#
            do cd.fcom
               (#
               do 'DynamicObjectGeneration:gen:'->TT; OS.doPT->TT;
                  (if isRef then '[]' -> TT if);
                  OS.label -> TQ
               #);
               OS.doAlloc; (* is this load or an alloc method? *)
               (* generator:
                * OS.loadOrigin
                * OS.doAlloc
                * perhaps LMR since perhaps enter or exit must be executed
                *)
            #);
          activate::
            (#
            do cd.fcom
               (#do 'DynamicObjectGeneration:activate'->TT; OS.dopt->TT #);
               OS.doAlloc
            #);
          doAlloc:: 
            (# 
            do cd.fcom(#do 'DynamicObjectGeneration:doAlloc'->TT; OS.dopt->TT#);
               OS.doAlloc
            #)
       #);
     ThisObject::<
       (# load::
            (#
            do cd.fcom(#do 'ThisObject:load:'->TT; AD.doPT->TT #);
               (* probably not AD.load
                * push adr of enclosing AD
                *)
            #);
       #);
     EvaluationList::<
       (# load::
            (#
            do scanSons
               (#
               do cd.fcom
                  (#
                  do 'EvalList:load:'->TT; current.label->TT;current.dopt->TT#);
                  current.load #)
            #);
          gen::(#do  #);
          activate::
            (#
            do cd.fcom(#do 'EvalList:activate'->TT; dopt ->TT #);
               scanSons(#do (ND[],LMR,isRef) -> current.activate #)
            #);
       #);
     NoneValue::
       (# load:: (#do cd.pushNone #);
          activate::(# do cd.pushNone #);
       #);
     NameApl::
       (# gen::
            (#
            do (if false then
                   cd.fcom(#do 'nameApl:gen:'->TT; N.dopt-> TQ #);
                   ATd.gen
               if)
            #);
          load::
            (#
            do cd.fcom(#do 'NameApl:load:'-> TT; ATd.label->TT; dopt->TQ #);
               cd.pushThis;
               on -> origin.goOrigin;
               (if ATd[] <> none then
                   ATd.load
                else
                   cd.fcom(#do 'NameApl:gen:ATd=none:'-> TT; dopt->TQ #)
            if)#);
          store::
            (#
            do cd.fcom(#do 'NameApl:store:'-> TT; dopt->TQ #);
               cd.pushThis;
               (if origin[] = none then 'NameApl:origin is none'-> dumpt;
                   dopt -> dumpTN;
               if);
               on -> origin.goOrigin;
               (if ATd[] = none then
                   'NameApl:ATd is none'->dumpT; dopt -> dumptn
               if);
               isRef->ATd.store;
            #);
          activate::
            (#
            do cd.fcom
               (#do 'NameApl:activate:'->TT; dopt->TT; LMR->CC; isRef->BB#);
               (if true (*or not isRemote*) then
                   cd.pushThis;
                   on -> origin.goOrigin;
               if);
               (ND[],LMR ,isRef) -> ATd.activate
            #);
          doAlloc::
            (#
            do cd.fcom(#do 'NameApl:doAlloc:' -> TT; dopt->TT; 
                         ATd[] = none -> BB; (*ATd.label -> TT*)  #);
               (if not isRemote then
                   cd.pushThis;
                   on -> origin.goOrigin;
               if);
               ATd.doAlloc
            #);
       #);   
     Remote::
       (# doAlloc::
            (#
            do cd.fcom(#do 'Remote:doAlloc:'->TT; dopt -> TT #);
               scanSons
               (#
               do (if not isLast then
                      not isFirst -> current.load
                   else
                      not isFirst -> current.doAlloc
               if)#)
            #)
       #);
          
     ConstExp::
       (# load:: (# do cst.T.setpos; cst.T.getInt  -> cd.pushConst #);
          gen:: (#do load #);
          activate:: (# do cst.T.setpos; cst.T.getInt  -> cd.pushConst #);
       #);
     CharExp:: 
       (# activate::
            (#do theCh.T.setpos; 1 -> theCh.T.inxGet -> cd.pushConst #);
       #);
     objTmpStack: @
       (# ODs: [30] ^ObjectDesc; next,top: @integer;
          add:
            (# OD: ^ObjectDesc; 
            enter OD[]
            do (if (top + 1 -> top) > ODs.range then
                   ODs.range -> ODs.extend
               if);
               (if OD.slt[] = none then
                   OD[] -> ODs[top][]
                else
                   (# found: @boolean
                   do cd.fcom(#do 'objTmpStack.add:SLOT'->TT; OD.slt.dopt->TT#);
                      OD.slt.scanBindings
                      (# ODF: ^ObjDescFrag
                      do cd.fcom(#do 'adding:'->TT; current.dopt-> TT #);
                         true -> found;
                         (current[] -> ODF[]).OD[] -> ODs[top][]
                      #);
                      (if not found then top - 1 -> top if)
               #)if)
            #); 
          genNext: BooleanValue
            (#
            do (if (next + 1 -> next) <= top then
                   ODs[next].genClass;
                   true -> value
            if)#)
       #);   
     labelHandler: @
       (# labCount: @integer;
          label:
            (# labNo: @integer;
               new: (# do  labCount + 1 -> labCount -> labNo #);
            enter labNo
            exit labNo
            #);
          init: (# do 0 -> labCount #);
       #);
     genStdBeta:
       (#
       do '**** Generator: StdBeta:'->dumptn;
          topModule.alloc;
          topModule.gen;
          cd.fcom(#do 'objTmpStack.gen'->TT #);
          L:
            (if objTmpStack.genNext then
                restart L
            if)
       #);
     cd:  ^ObjectCode;
  enter cd[]
  #);

