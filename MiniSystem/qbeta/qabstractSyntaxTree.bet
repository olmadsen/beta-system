ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qprimitives';
INCLUDE 'qlexer';
INCLUDE '../compiler_IF/compiler_IF';
INCLUDE '../VM/objectcode'
---lib:attributes--- 

traceIsBasicValue: (#exit false #);
AbstractSyntaxTree: Tree
  (# <<SLOT astlib:attributes>>;
     trx: @boolean; (* for dynamic on/off of selective checks *)
     traceAlloc: @boolean;
     printOffOn: (# exit false #);
     traceProps: (# exit false #);
     traceGetQual: (# exit false #);
     reportSemErr: (# exit true #);
     reportQuaCheck: (# exit false #);
     reportNone: (# exit false #);
     verbose: @boolean;
     display:
       (# T: ^text; S: ^Node
       enter(T[],S[])
       do (if trx or traceGetQual then
              T[] -> puttext; ':' -> put; ' ' -> put;
              (if S[] <> none then S.doPT -> putline 
               else ' is none' -> putline if)
          if)
       #);
     displayB:
       (# T: ^text; S: ^ObjectGenerator
       enter(T[],S[])
       do (if traceGetQual then
              T[] -> puttext; ':primNo:' -> puttext;
              (if S[] <> none then
                  S.primNo -> putint; ' ' -> put;
                  (if true 
                   // S.super.isPrimitive then
                      (if S.IS.isEmpty then
                          'super is basic and IS is empty' -> putline
                       else
                          'super is basisc and IS is NOT empty' -> putline
                      if)
                   // S.isPrimitive then
                      (if S.IS.isEmpty then
                          'is basisc and IS is empty' -> putline
                       else
                          'is basisc and IS is NOT empty' -> putline
                      if)
                   // S.IS.isEmpty then
                      'IS is empty' -> putline
                   else
                      'Not basic and IS not empty'->putline
                  if)
               else
                  'is none' -> putline;  
              if)if)
       #);
     SuperPath: (# print:< (# do inner #) do inner #);
     Node: TopNode
       (# vNode::< Node; 
          PT::<
            (# atEol: BooleanValue
                 (#
                 do (lx.length > 0) 
                    and ((lx.length -> lx.inxGet) = ascii.newline)
                      -> value
                 #);
               standAlone: BooleanValue
                 (# ch: @char; ix: @integer
                 do lx.length -> ix;  (*'"' -> put;*)
                    L:
                      (if ix > 0 then                        
                          ix -> lx.inxGet -> ch; 
                          (*ch -> put;
                           (if commentBefore then 'B'->put else 'A' -> put if);
                           *)
                        (if true
                         // ch = ascii.newline then
                            true -> value;
                            leave standAlone
                         // ch <= ' ' then;
                            ix - 1 -> ix;
                            restart L
                        if)
                     else
                        true -> value
                      if);
                    (*'"'->put*)
                 #);
               printComment:
                 (# ix,xpos: @integer
                 do (if comment[] <> none then
                        (if false then
                            '\n1234567890 comment:pos:'->puttext; 
                            pos -> putint;
                            ' commentBefore:' -> puttext; 
                            commentBefore -> putboolean;
                            newline;
                            comment[] -> puttext; '"\n'->puttext;
                        if);
                        comment.setPos;                   
                        1 -> ix;
                        emit:
                          (# doBreak,isAlone: @boolean; oldPos: @integer
                          do (* pos -> oldPos;*)
                             standAlone -> isAlone;
                             (ix -> comment.inxGet) = '<' -> doBreak;

                             (if doBreak then 
                                 comment.get;
                                 comment.getint - 3 -> xpos;
                                 (if not commentBefore then
                                     (pos,true) -> mkBreak;
                                 if)
                              else
                                 comment.getint;
                                 (if not isAlone then
                                     ' ' -> lx.put
                                 if)
                             if);
                             comment.get; (* read '>' *)
                             '--' -> lx.puttext;
                             comment.scan
                             (# 
                             do 
                                (if ch <> ascii.newline then 
                                    ch -> lx.put;
                                 else
                                    comment.pos + 1 -> ix;
                                    (pos,true) -> mkBreak;
                                    restart emit 
                             if) #);
                             (if doBreak (*and commentBefore *)
                                 or isAlone (*or (oldPos = 0)*) then
                                 (pos ,true) -> mkBreak;
                             if)
                          #)
                    if);
                 #)
            do (if not noComments and commentBefore then printComment if);
               inner;
               (if not noComments and not commentBefore then printComment if);
            #);
          PP::<
            (#
            do inner;
               (if comment[] <> none then  
                   ' --' -> lx.puttext; comment[] -> lx.puttext if)
            #);
          debug:
            (# lx: ^text;
               TT: (# enter lx.puttext #);
               TQ: 
                 (# S: ^Text enter S[] 
                 do '"'->lx.put; S[] -> TT; '"'->lx.put 
                 #);
               II: (# enter lx.putint #);
               BB:
                 (# B: @boolean
                 enter B
                 do (if B then 'True' -> TQ else 'False' -> TQ if);
                 #)
            do &text[] -> lx[]; '$' ->lx.put; label -> lx.append; ':' -> lx.put;
               inner;
               ' ' -> lx.put; '"' -> lx.put; dopt -> lx.puttext; '"' -> lx.put;
               cd.com(#do lx[] -> TT #);
            #);
          isDecl:< BooleanValue;
          isPattern:< BooleanValue;
          isDataItem:< BooleanValue;
          isArguments:< BooleanValue;
          isObjectGenerator:< BooleanValue;
          isObjectInvocation:< BooleanValue;
          isConstLiteral:< BooleanValue;
          isInteger:< BooleanValue;
          enclosingObjectGenerator:
            (# N: ^Node; theOG: ^ObjectGenerator; ptn: ^Pattern
            do father[] -> N[];
               L: (if N[] <> none then
                      (if true
                       // N.isObjectGenerator then 
                          N[] -> theOG[];
                       // N.isArguments then
                          (if N.father## = Signature## then
                              N.father.father[] -> ptn[];
                              ptn.OG[] -> theOG[]
                           else
                              N.father[] -> N[];
                              restart L
                          if)
                       else
                          N.father[] -> N[];
                          restart L
                      if)
                  if);
            exit theOG[]
            #);
          clear::<
            (#
            do false -> noArgsCheck -> genIsDone;
               inner;
            #);
          setUpOrigin::<
            (#
            do false -> noArgsCheck; inner
            #);
          check:<
            (* check is called recursively when needed
             * Controlled by isChecked and beingChecked
             * beingChecked may be redundant!?
             * We stille need to catch loops in in patterns/objectGenerators!?
             * - As 2 patterns having each other as super!?
             *)
            (# elm:< TopNode; 
               rec: ^Exp; basicScope,argScope: ^elm; 
               isFirst: @boolean;
               wasChecked: @boolean;
               beingCheckedX:< (# do inner #);
            enter(rec[],basicScope[],argScope[],isFirst)
            do (if beingChecked then 
                   beingCheckedX 
                else
                   (if not isChecked then
                       true -> beingChecked -> wasChecked;
                       inner; 
                       wasChecked -> isChecked;
                       false -> beingChecked
               if)if)
            exit basicScope[]
            #);
          getV_Qual:<
            (# encOG,qual: ^ObjectGenerator
            enter encOG[]
            do inner
            exit qual[]
            #);
          pathToDcl:<
            (# P: ^SuperPath
            enter P[]
            do inner
            exit P[]
            #);
          getActualReceiver:<
            (# rec: ^Node
            do this(node)[] -> rec[];
               inner
            exit rec[]
            #);
          alloc:<
            (# IT: ^items
            enter IT[]
            do (if traceAlloc then doPT -> putline if); 
               inner
            #);
          semCheck:<
            (# rec: ^vNode; encOG,eOG: ^ObjectGenerator; doCheck: @boolean
            enter(rec[],encOG[])
            do true -> doCheck; (* becomes false for modules not included
                                 * see semChecker
                                 *)
               inner;
               (*'bingo' -> putline;*)
               (if not noArgsCheck and doCheck then
                   encOG[]  -> eOG[];
                   scanSons
                   (# 
                   do (*'banko:'->putline; current.doPT -> putline;*)
                      (rec[],eOG[]) 
                        -> current.semCheck
                        -> eOG[];
                      current[] -> rec[]
                   #)
               if)
            exit encOG[]
            #);
          gen:<
            (# gdb:< debug(# do 'gen:'->lx.puttext; inner #);
               useReturnValue: @boolean; superAdj,staticOff: @integer;
               CC: ^CallChain;
               mainPathModule: ^Module
            enter(useReturnValue,superAdj,staticOff,CC[])
            do gdb; inner 
            exit mainPathModule[]
            #);
          markForCodeGen:<
            (#
            do inner
            #);
          Qalloc:<
            (# varNo: @integer; IT: ^Items
            enter(varNo,IT[])
            do inner
            #);
          Qgen:<
            (# gdb:< debug(# do 'gen:'->lx.puttext; inner #);
               useReturnValue: @boolean; superAdj,staticOff: @integer;
               CC: ^CallChain; (* superflous*)
               mainPathModule: ^Module
            enter(useReturnValue,superAdj,staticOff)
            do gdb;               
               inner
            exit mainPathModule[]               
            #);
          replaceEntity::<
            (#
            do inner
            #);
          insert:
            (# N,atN: ^Node; oldSon,oldSonX: ^Node;
               addTail:
                 (# tail,tailX: ^Node
                 enter tail[]
                 do (if tail[] <> none then
                        tail.next[] -> tailX[];
                        none -> tail.father[] -> tail.next[];
                        tail[] -> append;
                        tailX[] -> tail[];
                        restart addTail
                    if);
                 #);
            enter(N[],atN[])
            do (if false then
                   '**** Node:insert: ' -> putline;
                   N.dopt -> putline;
                   '**** In: ' -> putline;
                   doPt -> putline; '---'->putline;
               if);
               (if true 
                // last[] = none then
                   (*'**** last none:' -> putline;*)
                   N[] -> append
                // atN[] = none then
                   (*'**** atN none:' -> putline;*)
                   N[] -> append
                // atN[] = son[] then
                   (*'**** atN none:' -> putline;*)
                   son[] -> oldSon[];               
                   none -> son[] -> last[];
                   N[] -> append;
                   oldSon[] -> addTail
                else
                   (*'**** else:' -> putline;*)
                   son[] -> oldSon[];
                   L:
                     (if oldSon[] <> atN[] then
                         oldSon.next[] -> oldSon[];
                         restart L
                      else 
                         (*'**** found: ' -> putline;*)
                         oldSon.next[] -> oldSonX[];
                         none -> oldSon.next[];
                         oldSon[] -> last[];
                         N[] -> append;
                         oldSonX[] -> addTail
                     if)
               if);

            #);
          findNodeAtPos::
            (# 
            do L:
                 (if (beginPos <= pos) and (pos <= endPos) then
                     scanSons
                     (#
                     do  pos -> current.findNodeAtPos -> N[];
                        (if N[] <> none then 
                            (if trace then '>'-> put if);
                            leave L 
                        if)
                     #);
                     (if N[] = none then 
                         this(TopNode)[] -> N[]; 
                         (if trace then
                             newline; 
                             '[' -> put; beginPos -> putint; 
                             ',' -> put; endPos -> putint; ']' -> put;
                         if)
                     if)
                 if)
            #);
          doMapBCposToCharRange::
            (# bPos1,ePos1: @integer; trace: (# exit false #)
            do (if trace then
                   '\ndoMapBCposToCharRange: ' -> puttext; label -> putline; 
                   'Lsc: ' -> puttext; lsc -> putint; 
                   ' in: '->puttext;
                   beginBCP -> putint; ','->put; endBCP -> putint; newline;
               if);
               
               (if (beginBCP < lsc) and (lsc <= endBCP) then
                   (if trace then
                       'Inside: ' -> puttext;
                   if);
                   beginPos -> B; endPos -> E;
                   (if trace then 
                       B -> putint; ','->put; E -> putint; newline 
                   if);
                   L:
                     scanSons
                     (#
                     do (if true (*not current.isObjDesc*) then (* do not go into singular*)
                            lsc -> current.doMapBCposToCharRange -> (bPos1,ePos1);
                            (if bPos1 > 0 then 
                                bPos1 -> B; ePos1 -> E;
                                leave L 
                        if)if)
                     #)
               if)
            #);
          theModule::<
            (#
            do inner;
               (if md[] = none then 
                   (if father[] = none then
                       'theModule:father is none: ' -> puttext;
                       doPT -> putline
                    else 
                       father.theModule -> md[];                        
                   if)
               if)
            #);
          addComment:
            (# T: ^text
            enter T[]
            do (if comment[] <> none then
                   ascii.newline -> comment.put;
                   T[] -> comment.append
                else
                   T[] -> comment[]
               if)
            #);
          noArgsCheck,genIsDone: @boolean; 
          comment: ^text; commentBefore: @boolean
       do inner;
          lex.getComment -> comment[];
          (if false and (comment[] <> none) then
              'Comment: ' -> puttext; label -> putline;
              comment[] -> putline
          if)
       #);     
     Module:< Node (* perhaps sub of some SuperDataItem? 
                    * Cannot be sub of DataItem since DataItem must be virtual
                    *)
       (# DI: ^DataItem;
          PT:: 
            (# 
            do (0,length) -> block
               (#
               do (0,break) -> DI.PT 
               #)
            #);
          length:: (# do DI.length -> value #);
          label:: (# do 'Module' -> lab[] #);
          PP:: (# do label -> lx.append #);          
          
          (* Bindings from compiler_IF *)
          moduleName:: (# do DI.sig.id.copy -> N[] #);
          getEntries:: (# do DI.getEntries -> (names[],entries[]) #);
          entity:: 
            (# 
            do this(module)[] (* DI.entity*) -> E[] 
            #);
          theModule::(# do this(Module)[] -> MD[] #);
          isAmodule:: (# do &iModule[] -> M[]; this(Module)[] -> M.M[] #);
          getOrigin:: (# do origin[] -> org[] #);
          pathToModuleFN:: (# do pathToFN[] -> path[] #);
          innFile:: 
            (# 
            do (if F[] = none then inFile[] -> F[] else F[] -> inFile[] if) 
            #);
          myDesc:: (# do desc -> OD[] #);
          doHandleDirModules:: (# do handleDirModules #);
          
          pathToFN: ^Text;
          inFile: ^File;
      
          desc:<
            (# D: ^Items
            do inner
            exit D[]
            #);
          kind:: (# do '/' -> ch #);
          skind:: (# do ': /' -> S[] #);
          sort:: (# do DI.sort -> S[] #);
          replaceEntity::
            (#
            do (if true then
                   '**** Module:replaceEntity:' -> putline
               if);
               (path2Module[],T[]) -> DI.replaceEntity -> errors[]
            #);
          doInsert::
            (#
            do (if false then
                   '**** Module: doInsert: ' -> puttext; 
                   DI.sig.doPT -> putline;
               if);
               (N[],atN[]) -> DI.OG.IS.Insert
            #);          
          handleDirModules:
            (# pDirModule:<
                 (# moduleDir: ^text; (* name of direcory with BETA module *)
                    md: ^Module
                 enter moduleDir[]
                 do inner;
                 exit md[]
                 #);
               findModuleItems:
                 (# 
                 do (if trace then
                        'FindDir: find directories in:\n   ' -> puttext; 
                        path.append -> putline; 
                    if);
                    (path.append,false) -> getBETAdirectories
                    (#
                    do scan
                       (#
                       do (if not currentIsFile then
                              current[] -> addDirModuleN;
                 if)#)#)#);
               
               dirModuleN: [4] ^text; dcTop: @integer;
               addDirModuleN:
                 (# dir: ^text
                 enter dir[]
                 do (if (dcTop + 1 -> dcTop) > dirModuleN.range then 
                        dirModuleN.range -> dirModuleN.extend;
                        dirModule.range -> dirModule.extend
                    if);
                    dir[] -> dirModuleN[dcTop][]
                 #);
               (* Perhaps save ModuleItemDecl instead?
                * Perhaps factor appendModule out of scanDir
                *)
               dirModule: [4] ^ModuleItem;
               scanDir:
                 (# currentModule: ^Module;
                    N: ^text; (* module on path to ...*)
                 do (*'ScanDir:'->putline; dctop->putint; newline;*)
                    (for i: dcTop repeat
                         (*dirModuleN[i][] -> N[] -> putline;*)
                         (* See also Checker:ModuleItem:doCheck *)
                         dirModuleN[i][] 
                           -> N[] -> pDirModule -> currentModule[];
                         (N[],currentModule[]) -> ModuleItem -> dirModule[i][];
                         dirModule[i][] -> addSigFromDI -> appendModule; 
                         (if currentModule[] = none then 
                             'currentModule none' -> putline if);
                         true -> dirModule[i].isParsed; (* eliminate *)
                         INNER;
                    for);
                 #);              
               appendModule:
                 (# dcl: ^Decl 
                 enter dcl[] 
                 do (* 'AppendModule: ' -> putline; dcl.doPT -> putline; *)
                    dcl[] -> DI.OG.IS.append
                 #);
               main,m: ^Module;
            do (if trace then
                   'HandleDirModules: '->puttext; 
                   DI.sig.doPt -> putline;
               if);
               0 -> dcTop;
               (if (dirCh->(path.append).put) -> locateWorld.CWD.equal then
                   this(Module)[] -> main[];
                   (*'Found main: ' -> putline; main.doPT -> putline*)
               if);
               
               findModuleItems;
               
               scanDir 
               (#
               do currentModule.DI.sig.id[] -> path.push;
                  (if trace then
                      'EQ:'->putline; path.append-> putline; 
                      locateWorld.CWD[] -> putline; 
                  if);
                  &currentModule.handleDirModules
                  (# pDirModule::
                       (#
                       do (* calling pDirModule in enclosing handleDirModule,
                           * which is called from parser and parser has
                           * a binding of pDirModule
                           *)
                          moduleDir[] 
                            -> this(handleDirModules).pDirModule 
                            -> md[]
                       #)
                  #) -> m[];
                  (if m[] <> none then m[] -> main[] if);
                  path.pop
               #)
            exit main[]
            #);
          traceImports: (# exit false #);              
          imports: @
            (# 
               L: [4] ^module; top: @integer;
               init:< (# do 0 -> top;(* ' imports:init ' -> puttext; *) #);
               addM: 
                 (# M: ^module; MI: ^ModuleItem
                 enter M[]
                 do (if (M.father[] -> MI[]) <> none then
                        (*'***imports.addM: ' -> puttext;
                         * M.moduleName -> putline;
                         * *)
                        MI[] -> add
                    if)
                 #);
               add:
                 (# mi: ^moduleItem
                 enter mi[]
                 do (if traceImports then
                        'Imports.add: ' -> puttext; mi.md.moduleName -> puttext
                    if);
                    (if (top+1-> top) > L.range then L.range -> L.extend if);
                    mi.md[] -> L[top][];
                    (if not mi.md.isChecked then 
                        (none,MI.MD.origin[],MI.MD.origin[],true) 
                          -> MI.MD.check;
                        true -> MI.isChecked
                     (*else
                        newline*)
                    if);
                    addEnclosingModules
                 #);   
               addEnclosingModules:
                 (# org: ^Items; M: ^Module; MI: ^ModuleItem
                 do (if origin[] <> none then
                        (origin[] -> org[]).theModule -> M[];
                        (if M[] <> none then
                            (*'**** AddEnclosingModule: ' -> puttext; 
                             * M.moduleName -> putline;
                             *)
                            M[] -> M.imports.addM
                    if)if)
                 #);
               scan:
                 (# current: ^module
                 do (for i: top repeat
                         L[i][] -> current[]; inner
                 for)#);
               mark: scan
                 (# 
                 do (if false then
                        current.DI.sig.doPT -> putText; ' descNo: '->putText; 
                        (current.desc).DescNo -> putint; ' ' -> put;
                    if);
                    current.mark 
                 #);
            #);
          trace: (# exit false #);
          mark: 
            (# inMainP: @boolean
            enter inMainP
            do (if traceImports then
                   'MarkImports: '->puttext; DI.sig.doPT ->putline if);
               (if inMainP then
                   (*'Module in main path: ' -> puttext; 
                    * DI.sig.doPT ->putline;  *)
                   inMainP -> inMainPath
               if);
               
               (if not isIncluded then (* avoid circularity *)
                   true -> isIncluded; 
                   imports.mark 
               if);
            #); 
          objDescEQ:: (# do DN -> DI.objDescEQ -> value #);
          clear::<
            (#
            do (*'Module:clear: ' -> puttext; DI.sig.dopt -> putline;*)
               imports.init;
               false -> isAdded -> isIncluded -> inMainPath;  
               inner
            #);
          inMainPath: @boolean; (* Module in origin path of main program *)
          isAdded,isIncluded: @boolean;
       enter DI[] -> append
       #);
     ModuleItem:< Decl (* sub of some SuperDataItem? *)
       (# label:: (# do 'ModuleItem' -> lab[] #);
          PP:: (# do label -> lx.append #);
          PT:: 
            (#
            do (if true then
                   '--' -> lx.puttext; sig.id[] -> lx.puttext
                else
                   (pos,break) -> MD.PT 
               if);
            #);
          length:: (# do MD.length -> value #);
          getEntries:: (# do MD.getEntries -> (names[],entries[]) #);
          Entity:: (# do MD.entity -> E[]#);
          kind:: (# do MD.kind -> ch #);
          skind:: (# do MD.skind -> S[] #);
          isaModule:: (# do &iModule[] -> M[]; md[] -> M.M[] #);
          isParsed: @boolean;
          myDesc:: (# do MD.desc -> OD[] #);
          objDescEQ:: (# do DN -> MD.objDescEQ -> value #);
          replaceEntity::
            (#
            do (if false then
                   '**** ModuleItem:replaceEntity:' -> puttext; 
                   MD.DI.sig.dopt->putline;
               if);
               (path2Module[],T[]) -> MD.replaceEntity -> errors[]
            #);
          doInsert::
            (#
            do (*'**** ModuleItem:doInsert: ' -> putline;*)
               (N[],atN[]) -> MD.doInsert
            #);
          visibility:: (* used to be configure *)
            (#
            do (if (desc).props <> none then
                   (desc).props.OSDvisibility -> VI
               if) 
            #);
          MD: ^Module; (* perhaps DataItem? *)
          N:  ^Name
       enter(Name -> N[] -> append,MD[]->append)
       #);
     Items:< Scope 
       (# PT::< 
            (# 
            do (if false then
                   '['->lx.put; pos -> lx.putint;']'->lx.put;
               if);
               (pos,length) -> block
               (# ptn: ^Pattern; DI: ^DataItem
               do (* '{' -> lx.put; beginPos -> lx.putint; ' ' -> lx.put;*)
                  (if (props[] <> none) and (props.length > 0) then
                      (pos,false) -> props.PT;
                      (pos,true) -> mkbreak
                  if);
                  inner PT;
                  (if true then
                      (if (incl[] <> none) and (incl.son[] <> none) then
                          (pos,break) -> incl.PT;
                          (pos,true) -> mkbreak
                      if);
                      (if father.father[] <> none then
                      (if father.father## // pattern## 
                          (* checker must be called before sig is defined *)
                          (*sig[] <> none*) then
                          (pos,break,true) -> (father.father[] -> ptn[]).sig.args.PT;
                          (if ptn.sig.val[] <> none then
                              (pos,true) -> ptn.sig.val.PT;
                              (pos,true) -> mkbreak
                          if)
                       // dataItem## then
                          (pos,break,true) -> (father.father[] -> DI[]).sig.args.PT
                       else
                          (*father.father.label -> lx.puttext;*)
                      if)if);
                      scanSons
                      (#
                      do (if not isFirst then
                             (pos,break) -> current.PT; 
                             (if not isLast then
                                 (pos,true) -> mkbreak
                         if)if)
                      #)
                   else
                      scanSons
                      (# 
                      do (if not isFirst or (incl[] <> none) and (incl.son[] <> none) then
                             (pos,break) -> current.PT; 
                             (if not isLast then
                                 (pos,true) -> mkbreak
                         if)if)
                      #)
                  if)
                  #);
                  (*'}'-> lx.put*)
            #);
          length::
            (#
            do (if (son[] <> none) and (son.length > 1) then
                   lineWidth + 10 -> value
                else
                   (if props[] <> none then props.length -> value if);
                   scanSons(# do current.length + value -> value #)
               if)
            #);
          label:: (# do 'Items' -> lab[] #); 
          PP:: (# do label -> lx.append (* what about props?*) #);
          display:: (# do 'Items:'->putline #);
          scanDecls:
            (# currentDcl: ^decl;
            do scanSons
               (# I: ^Item
               do current[] -> I[];
                  (if I.isDecl then
                      I[] -> currentDcl[];
                      inner scanDecls
                  if);
               #)               
            #);  
          scanAllDecls:
            (# currentDcl: ^Decl;
               superOrgOff,level: @integer
            do (if (superDesc[] <> none) and (superDesc[] <> objectDecl.desc) then
                   &superDesc.scanAllDecls
                   (#
                   do currentDcl[] -> this(scanAllDecls).currentDcl[];
                      inner scanAllDecls
                   #) -> (superOrgOff,level)
               if);
               (if sig[] <> none then
                   sig.scanArgs
                   (#
                   do currentArg[] -> this(scanAllDecls).currentDcl[];
                      '!!! scanAllDecls:sig: should not come here:' 
                        -> puttext; 
                      doPT -> putline; 
                      inner scanAllDecls
               #)if);
               scanDecls
               (#
               do currentDcl[] -> this(scanAllDecls).currentDcl[];
                  inner scanAllDecls
               #)
            exit(superOrgOff,level) 
            #);
          scanAllItemDecls:
            (# currentDcl: ^Decl;
               superOrgOff,level: @integer
            do (if (superDesc[] <> none) and (superDesc[] <> objectDecl.desc) then
                   &superDesc.scanAllItemDecls
                   (#
                   do currentDcl[] -> this(scanAllItemDecls).currentDcl[];
                      inner scanAllItemDecls
                   #) -> (superOrgOff,level)
               if);
               scanDecls
               (#
               do currentDcl[] -> this(scanAllItemDecls).currentDcl[];
                  (*'scanAllItemDecls: ' -> puttext;
                  this(scanAllItemDecls).currentDcl.doPT -> putline;*)
                  inner scanAllItemDecls
               #)
            exit(superOrgOff,level) 
            #);                    
          scanItemArgs:
            (# currentArg: ^Decl
            do scanAllItemDecls
               (# 
               do currentDcl[] -> this(scanItemArgs).currentArg[];
                  (if currentArg.isInArg (*and not currentArg.isVirtual*) then
                      inner scanItemArgs
                  if)
               #)
            #);
          scanItemArgsReverse:
            (# currentArg: ^Decl
            do scanReverse
               (# OT: ^Item
               do (if (current[] -> OT[]).isDecl then
                      current[] -> this(scanItemArgsReverse).currentArg[];
                      (*current.doPT -> putline;*)
                      (if currentArg.isInArg and not currentArg.isVirtual then
                          inner scanItemArgsReverse
                  if)if)
               #)
            #);
          theObjectGenerator:: (# do father[] -> OGx[] #);
          collectSimple::
            (#
               off: @integer
            do scanAllDecls
               (# name: ^Text;
                  off,kind: @integer
               do '???'-> name[];
                  currentDcl.sig.ID[] -> name[];
                  (if true then
                      currentDcl.qoff[1] -> off
                   else
                      currentDcl.off -> off;
                  if);
                  (if off > 0 then
                      (if currentDcl.isPrimitive then
                          SimpleKind -> kind
                       else
                          (if currentDcl.isVirtual then
                              virtualKind -> kind
                           else
                              ReferenceKind -> kind
                          if)
                      if)
                   else
                      (* chef isIndexed -  see parser:collectSimple *)
                      patternKind -> kind
                  if);
                  (* 'Collect: ' -> puttext; name[] -> puttext;
                   ' off:'  -> puttext; off -> putint;
                  ' kind:' -> puttext; kind -> putint; newline;*)
                  (name[],off,kind,currentDcl[]) -> addSimple
               #);
            #);
          getEntries:: 
            (# 
            do scanDecls(# do (currentDcl.sig.id[],currentDcl[]) -> add #)
            #);
          doInsert::
            (#
            do '\n*** items:doInsert: ' -> putline;
               N.dopt -> putline;
               atN.dopt -> putline;
            #);
          primNo: integerValue
            (#
            do (if props[] <> none then 
                   (if props.basicProp[] <> none then
                       props.basicProp.primNo -> value 
                    else
                       0 -> value
                   if)
               if)
            #);
          handleProps: BooleanValue
            (#
            do (if props[] <> none then
                   props.handleProps -> value -> isPrimitive                 
               if)
            #);
          isPrimitive: @ Boolean;
          doCall:<
            (# N: ^text; LMR: @char
            enter(N[],LMR)
            do inner; 
            #);
          isCore: integerValue
            (#
            do (if (props[] <> none) and (props.basicProp[] <> none) then
                   props.basicProp.coreNo  -> value
               if)
            #);
          isExternal: integerValue
            (#
            do (if (props[] <> none) and (props.basicProp[] <> none) then
                   props.basicProp.externalNo  -> value
               if)
            #);
          scanProps:
            (# current: ^Property
            do props.scanProps
               (# 
               do current[] -> This(scanProps).current[]; 
                  inner scanProps
               #)
            #);
          visibility::
            (#
            do (if props[] <> none then
                   props.OSDvisibility -> VI
               if)
            #);
          sig: ^Signature; (* if pattern, the signature else none *)
          OT: ^ObjectGenerator;
          props: ^Properties;
          incl: ^Include;
          doHasCode:@boolean;
          superDesc: ^Items; 
          sort:: (# do this(Items)[] -> S[] #);
          getOrigin:: (#do origin[] -> org[] #);
          (* Item's are appended
           * But sometimes not so clean/smart that some attributes
           * are both explicit and appended as 'incl' and others like Item's
           * are appended
           *)
          isEmpty: @boolean;
          clear::< 
            (# 
            do 0 -> QDNtop -> descNo;
               (for i: QdescNo.range repeat 0 -> QdescNo[i] for);
               inner 
            #);          
          descNo: @integer; (* Why is descNo in Items and not in OG?
                             * Perhaps to avoid a descNo for a 
                             * non-singular invocation!?
                             *)
          QdescNo: [1] @integer; QDNtop: @integer;
       #);
     Item: Node 
       (# 
          isVirtual:< BooleanValue;
          isVirtualObject:< BooleanValue;
          isPrimitive:< BooleanValue(# do inner #);
          primNo:< IntegerValue(# do inner #);
          desc:<
            (# elm:< Node; D: ^Items
            do inner
            exit D[]
            #);
          getQual:<
            (# qual: ^ObjectGenerator
            do inner
            exit qual[]
            #);
          sort:: (# do this(Item)[] -> S[] #);
          hasCode:< BooleanValue;
          findNextImp::<
            (#
            do inner;
               (if impx[] <> none then
                   (if beginBCP > bcp then
                       this(Item)[] -> impx[]
                    else
                       L:
                         scanSons
                         (# 
                         do (if (bcp -> current.findNextImp -> impx[]) <> none then
                                leave L 
               if)#)if)if)
            #)
       do inner 
       #);
     Properties:< Node
       (* Properties = [Lexem[Properties],Lexem[Properties],...]
        * where Lexem[Properties] may be juts Lexem, ie no arguments
        *)
       (# Label:: (# do 'Properties' -> lab[] #);
          PP:: (# do Label -> lx.append #);
          PT::
            (# l: @integer; PR: ^Property
            do (if (length -> l) > 0 then
                   (if false and (l > 25) then
                       '*** Properties: [' -> puttext; 
                       scansons
                       (#
                       do current[] -> PR[]; PR.T[] -> puttext;
                          (if not isLast then ',' -> put if)#);
                       ']' -> put;
                       ' width: '->puttext; 
                       lineWidth -> putint;
                       ' length: ' -> puttext; l -> putint;
                       ' break: ' -> puttext; break -> putBoolean; newline;
                   if);
                   '['->lx.put;
                   scanSons
                   (#
                   do (pos,break) -> current.PT; 
                      (if not isLast then ','->lx.put if) 
                   #);
                   ']'->lx.put;
                   (pos,break) -> mkBreak
               if)
            #);
          length:: 
            (# 
            do scanSons
               (#
               do current.length + value -> value;
                  (if not isLast then value + 1 -> value if) (* ',' *)
               #);
               (if value > 0 then value + 2 -> value if) (* [...] *)
            #);
          scanProps:
            (# currentProp: ^Property
            do scanSons
               (#
               do current[] -> currentProp[];
                  inner scanProps
               #)
            #);
          handleProps: booleanValue
            (#
            do scanProps
               (#
               do L:
                    (if true
                     // 'basic' -> currentProp.T.equalNCS then
                        currentProp[] -> basicProp[];
                        basicProp.getPrimNo -> basicProp.primNo;
                        true -> value;
                        leave L
                     // 'external' -> currentProp.T.equalNCS then
                        currentProp[] -> basicProp[];
                        basicProp.getPrimNo -> basicProp.externalNo;
                        false -> value; (* OBS - clean-up*)
                     // 'resume' -> currentProp.T.equalNCS then
                        currentProp[] -> basicProp[];
                        resume_Prim -> basicProp.primNo;
                        (if resumeDecl <> none then
                            'Resume pattern: ' -> puttext; 
                            resume_prim -> putint; ' ' -> put;
                            doPT -> putline;
                         else
                            'resumeDecl none' -> putline
                        if);
                     // 'core' -> currentProp.T.equalNCS then
                        currentProp[] -> basicProp[];
                        basicProp.getPrimNo -> basicProp.coreNo;
                        false -> value; (* OBS - clean-up*)
                        leave L 
                     // 'globals' -> currentProp.T.equalNCS then
                        currentProp.arg[] -> globals[]
                        
                     // 'arguments' -> currentProp.T.equalNCS 
                     // 'interface' -> currentProp.T.equalNCS 
                     // 'signature' -> currentProp.T.equalNCS 
                     // 'kind' -> currentProp.T.equalNCS then
                     // 'abstract' -> currentProp.T.equalNCS 
                     // 'immutable' -> currentProp.T.equalNCS then
                        
                     // 'OSDvisibility' -> currentProp.T.equalNCS then
                        (if traceProps then
                            '\n*** OSDvisibility: ' -> puttext;
                        if);
                        (if currentProp.arg[] <> none then
                            currentProp.arg.scanSons
                            (# prop: ^Property
                            do current[] -> prop[];
                               (if true
                                // ('Disguised' -> prop.T.equalNCS) then
                                   OSDvisibility_Disguised -> OSDvisibility
                                // ('SkipInternal' -> prop.T.equalNCS) then
                                   OSDvisibility_SkipInternal -> OSDvisibility
                                // ('Show' -> prop.T.equalNCS) then
                                   OSDvisibility_open -> OSDvisibility
                                else
                                   '\n**** Unkown value for OSDvisibility: ' 
                                     -> puttext;
                                   prop.T[] -> putline
                               if);
                               (if traceProps then
                                   prop.T[] -> putline;
                                   dopt -> putline;
                               if)
                        #)if)
                     else
                        '\n**** Unknown property: ' -> puttext;
                        currentProp.T[] -> putline
                    if)
               #)
            #);         
          basicProp: ^Property;
          globals: ^Properties;
          inSub: @boolean;
          OSDvisibility: @integer
       do
       #);
     Property:< Lexem
       (# Label:: (# do 'Property:' -> lab[]; T[] -> lab.append #);
          PP:: (# do label -> lx.append #);
          PT:: 
            (# 
            do T[] -> lx.puttext;
               (if arg.length > 0 then (pos,break) -> arg.PT if) 
            #);
          length:: 
            (# 
            do (if T[] <> none then
                   T.length -> value
               if); 
               (if arg[] <> none then arg.length + value -> value if) 
            #);
          (*P: ^Lexem;*)
          arg: ^Properties;
          
          getPrimNo:
            (# V: ^Const; res: @integer
            do L:
                 (if arg[] <> none then
                     arg.ScanSons
                     (# prp: ^Property
                     do current[] -> prp[];
                        prp.T[] ->  Const -> V[];                   
                        V.T.setPos; V.T.getInt -> res;
                        leave L
                     #)
                 if)
            exit res
            #);
          primNo,coreNo,externalNo: @integer; (* clean-up here *)
       #);
     Include:< Item
       (# PT:: 
            (# 
            do '@include ' -> lx.append;
               scanSons
               (#do (pos,break) -> current.PT; 
                  (if not isLast then ',' -> lx.put if)#)
            #);
          length:: 
            (#
            do 9 -> value;
               scanSons(# do current.length + 1 + value -> value #)
            #);
          Label:: (# do 'Include' -> lab[] #);
          PP:: (# do Label -> lx.append #);
          encModule:
            (# m: ^Node; md: ^moduleItem
            do father[] -> m[];
               L:
                 (if not (m## <= moduleItem##) then 
                     (*newline; 
                     M.doPT -> putline;*)
                     m.father[] -> m[];
                     restart L
                 if);
               m[] -> md[]
            exit md[]
            #);

       #);
     Decl: Item
       (# PT::< 
            (# 
            do (if printOffOn then
                   '('->lx.put; off -> lx.putint; ')'-> lx.put;
               if);

               (if isInArg then 'in ' -> lx.puttext if);
               (if isOutArg then 'out ' -> lx.puttext if);
               inner 
            #);
          isArg: @boolean;
          sig: ^Signature;
          off: @integer;
          qoff: [1] @integer; noOfVar: @integer;
          isInArg,isOutArg: @boolean;
          setNewId:< (#do inner #);
          objSize:< IntegerValue;
          isDecl:: TrueValue;
          isPatternDecl:< BooleanValue;
          isBasicPattern:< BooleanValue;
          isBasicValue:< BooleanValue;
          isSubOfBasicValue:< BooleanValue;
          changeAttName:: 
            (# do '!!!! Decl:changeAttName: no binding' -> putline #);
          isInner:< 
            (# basicScope: ^items; args: ^Arguments enter(basicScope[],args[]) 
            do inner 
            #);
          theObjectGenerator:< (* Pattern and DataItem has an OG
                                * should be placed in Decl. 
                                * ModuleItem is also a Decl so fix this
                                *)
            (# xOG: ^ObjectGenerator
            do inner
            exit xOG[]
            #);
          checkVirtualArgs:<
            (# E: ^Exp
            enter E[]
            do inner
            #);
          checkQual:<
            (# srcOG,srcQual,dstQual: ^ObjectGenerator;
               isAssign: @boolean
            enter(srcOG[],srcQual[],dstQual[],isAssign)
            do inner 
            #);          
          checkActualArgs:<
            (# actualArgs: ^Arguments; encOG: ^ObjectGenerator;
               theOI: ^ObjectInvocation
            enter(actualArgs[],encOG[],theOI[])
            do inner
            #);

          handleArgs:< (# E: ^Exp enter E[] do inner #);
          markIfVirtualDataItem:<
            BooleanValue(# IT: ^Items enter IT[] do inner #);
          hasVirtualObjectQual:< BooleanValue;
          pushThis:< 
            (# gdb:< debug(# do 'pushThis:'->lx.puttext; inner #);
            do inner
            #);
          loadArgs:< 
            (# gdb:< debug(# do 'loadArgs:'->lx.puttext; inner #);
               E: ^Exp; superAdj,staticOff: @integer; CC: ^CallChain
            enter(E[],superAdj,staticOff,CC[])
            do gdb; inner 
            #);
          getAdr:< 
            (# gdb:< debug(#do 'getAdr:'-> TT; inner #); 
               A: ^superAdr; loadRec: @boolean
            enter A[] do gdb; inner exit(loadRec,A[] )
            #);
          load:< (# rec: ^Exp; A: ^SuperAdr enter (rec[],A[]) do inner #);
          invoke:< 
            (# gdb:< debug(# do 'invoke:'->lx.puttext; inner #);
               rec,E: ^Exp; useReturnValue: @boolean; 
               staticOff: @integer;
               CC: ^CallChain;
               adr: ^SuperAdr
            enter(rec[],E[],useReturnValue,staticOff,CC[],adr[])
            do gdb; inner 
            #);          
          isCore:< integerValue(# do inner #);
          store:< 
            (# gdb:< debug(#do 'store:' -> TT; inner #);
               noSwap: @boolean;  enter noSwap do inner #);
          popReturn:< (# do inner #);
          (*sort:: (# do theObjectGenerator -> S[] #)*)
       do inner
       #);
     Scope: Node
       (# display:<
            (# org: ^Scope
            do '>>>Scope:' -> puttext; inner;
               doP -> putline;
               (if (origin[] -> org[]) <> none then org.display if)
            #);
          localSearch:<
            (# CS: ^ Text; trySig: @boolean; ATd: ^Decl; pn: @integer
            enter(CS[],trySig)
            do(* 'localSearch: ' -> puttext; CS[] -> puttext;
               ' in: ' -> puttext; doP -> putline; *)
               inner               
            exit(ATd[],pn)
            #);
          search:<
            (# NA: ^Lexem; CS: ^ Text; isFirst: @boolean; 
               ATd: ^Decl; on,pn: @integer
            enter(NA[],isFirst)
            do (*'Search: ' -> puttext; CS[] -> puttext;
                ' in: ' -> puttext; doPP -> putline;*)
               (if (1 -> NA.T.inxGet) = '#' then 
                   (2,NA.T.length) -> NA.T.sub -> CS[]
                else
                   NA.T[] -> CS[];
               if);
               inner;
            exit(ATd[],on,pn)
            #); 
          theObjectGenerator:< (# OGx: ^ObjectGenerator do inner exit OGx[] #);
       do inner
       #);
     Signature:< Scope
       (* In xbeta, Signature have subpatterns UnarySignature, etc *)
       (# PT:: 
            (# inDecl: @boolean
            enter inDecl
            do (if inDecl then
                   (if type[] <> none then
                       type.id[] -> lx.puttext; 
                       (if type## = Binary## then ' '-> lx.put if)
                    else
                       ' type is none ' -> lx.puttext
                   if)
                else
                   (if type[] <> none then
                       (pos,break,inDecl) -> type.PT;
                       (if val[] <> none then (pos,break) -> val.PT if)
                    else
                       scanSons
                       (#do (pos,break) -> current.PT; 
                          (if not isLast then ' ' -> lx.put if)
                       #);
                          (*(if val[] <> none then val.PT if)*)
                   if)
               if)
            #);
          length:: 
            (# 
            do (if type[] <> none then
                   type.length -> value;
                   (if args[] <> none then value + args.length -> value if);
                   (if val[] <> none then val.length +value -> value if);
                else
                   scanSons(#do current.length + value -> value #)
               if)
            #);
          Label:: (# do 'Signature' -> lab[] #);
          PP::(# do Label -> lx.append #);
          display:: (# do 'Signature:'->putline #);
          type: ^SigType;
          SigType: Node 
            (# id: ^text;
               PT::< (# inDecl: @boolean enter inDecl do inner #);
            enter id[] 
            do id[] -> name -> append 
            #);
          Unary:< SigType
            (# Label:: (# do 'UnaryPtn' -> lab[] #);
               PT:: (# do id[] -> lx.puttext #);
            #);
          Binary:< SigType
            (# label :: (# do 'Binary' -> lab[] #);
               PT:: (# do id[] -> lx.puttext;
                       ' ' -> lx.put; (pos,break,inDecl) -> args.PT #);
            #);
          Function:< SigType
            (# label:: (# do 'Function' -> lab[] #);
               PT:: (# do id[] -> lx.puttext;
                       (pos,break,inDecl) -> args.PT #)
            #);
          KeyWord:< SigType
            (# label :: (# do 'KeyWord' -> lab[] #);
               PT:: (#do (pos,break,inDecl) -> args.PT #);
            #);
          getReturnOG:
            (# DI: ^DataItem
            do getReturnDecl -> DI[]
            exit DI.OG[]
            #);
          getReturnDecl:
            (# ptn: ^Pattern; DI: ^DataItem
            do L:
                 (if val[] <> none then
                     '\n!!!!! getReturnDecl:should not come here!!!' -> putline;
                     father.doPT -> putline;
                     val.scanSons(# do current[] -> DI[] #)
                  else
                     father[] -> ptn[];
                     ptn.OG.IS.scanAllItemDecls
                     (#
                     do (if currentDcl.isOutArg then
                            currentDcl[] -> DI[];
                            leave L
                     if)#);
                     (if (ptn.OG.superPtn -> ptn[]) <> none then 
                         ptn.sig.getReturnDecl -> DI[]                         
                 if)if)
            exit DI[]
            #);          
          args: ^Arguments;
          val: ^Value;
          id: ^text; (* should be a NAme *)
          isKeyWord: @boolean;
          init:
            (# 
            enter(id[],args[],isKeyWord)
            do (if not isKeyWord then
                   id[] -> Name -> Append
                else 
                   true -> args.isKeyword;
                   (*this(Signature)[] -> args.father[]*)
               if);
               (if args[] <> none then args[] -> append if);
            #);
          setId:
            (# 
            do ''-> id[];
               args.scanSons
               (# B: @boolean; L: ^Lexem
               do (if not B then 
                      current[] -> L[];
                      L.T.copy -> id.append;
                  if);
                  not B -> B
               #);
            #);
          hasVirtualArgs: BooleanValue
            (#
            do (* 'Signature:hasVirtualArgs: '->puttext; doPT -> putline;*)
               (if args[] <> none then
                   args.hasVirtualArgs -> value
            if)#);
          scanArgs:
            (# currentArg: ^Decl
            do (if args[] <> none then
                   args.scanArgs(# do current[] -> currentArg[]; inner scanArgs #)
               if)
            #);
          scanArgsReverse:
            (# currentPar: ^Decl; first: @boolean
            do (if args[] <> none then 
                   args.scanArgsReverse
                   (#do current[] -> currentPar[]; inner scanArgsReverse #)
               if)
            #);
          getQual:<
            (# qual: ^objectGenerator
            do inner
            exit qual[]
            #);         
          theObjectGenerator:: (# ptn: ^Pattern do (father[]->ptn[]).OG[] -> OGx[] #);
       enter(init) 
          (* OBS! (init[with only enter id[]],isKeyWord) does not work 
           * Perhaps compiler error!?
           *)
       #);     
     Sig_Generator:
       (# id: ^Text; args: ^Arguments; isKeyword: @boolean; sig: ^Signature
       enter(id[],args[],isKeyWord)
       do (id[],args[],isKeyWord) -> Signature -> sig[];
          inner;
       exit sig[]
       #);
     UnarySigGen: Sig_Generator(# do id[] -> sig.Unary -> sig.type[] #);
     FunctionSigGen: Sig_Generator(# do id[] -> sig.Function -> sig.type[] #);
     KeyWordSigGen: Sig_Generator(# do id[] -> sig.KeyWord -> sig.type[] #);
     BinarySigGen: Sig_Generator(# do id[] -> sig.Binary -> sig.type[] #);     
     Arguments:< Node
       (# elm:< Item;
          PT:: 
            (# inDecl: @boolean
            enter inDecl
            do (if true
                // isBinary then 
                   (if inDecl then
                       scanSons
                       (#
                       do 'in ' -> lx.puttext;
                          (pos,break) -> current.PT;
                          (pos,true) -> mkBreak
                       #)
                    else
                       scanSons(# do (pos,break) -> current.PT #);
                   if)
                // isKeyWord then 
                   (if not isNewKeyWord then
                       scanSons
                       (# 
                       do (pos,break) -> current.PT; 
                          (if not isLast then ' ' -> lx.put if) 
                       #);
                   if)
                // isFunction then 
                   (if inDecl then
                       scanSons
                       (# 
                       do 'in ' -> lx.puttext; 
                          (* if current.isDataItem then current is not marked as
                           * inArg since it is in the arg list
                           * Only dataitems in maipart are marked as inArg
                           *)
                          (pos,break) -> current.PT; 
                          (pos,true) -> mkBreak
                       #);
                    else
                       '(' -> lx.put;
                       scanSons
                       (# 
                       do (pos,break) -> current.PT; (if not isLast then ',' -> lx.put if) 
                       #);
                       ')' -> lx.put
                   if)
                else
                   (* unary *) 
                   scanSons(# do (pos,break) -> current.PT #);
               if)
            #);
          length:: (# do scanSons(# do current.length + 1 -> value #)#);
          Label:: (# do 'Arguments' -> lab[] #);
          PP:: (# do Label -> lx.append #);
          isBinary,isKeyWord,isFunction: @boolean;
          isArguments:: TrueValue;
          isNewKeyWord: @boolean; (* as in @if: ... with only >> args are used*)
          scanArgs:
            (# current: ^Elm; isLast: @boolean
            do (if isKeyWord then
                   (*'arguments:length: ' -> puttext; 
                    * noOfSons -> putint; newline;*)
                   scanSons
                   (# isArg: @boolean
                   do 
                      (if isArg then
                          current[] -> this(scanArgs).current[]; 
                          isLast -> this(scanArgs).isLast;
                          inner scanArgs
                       (*else
                          'isNotArg:' -> puttext; 
                          current.dopt -> putline*)
                      if);
                      not isArg -> isArg
                   #)
                else
                   scanSons
                   (# do current[] -> this(scanArgs).current[]; 
                      isLast -> this(scanArgs).isLast;
                      inner scanArgs #)
               if)
            #);
          scanArgsReverse:
            (# current: ^Decl; first: @boolean
            do (if isKeyword then 
                   scanReverse
                   (# isPar: @boolean
                   do not isPar -> isPar;
                      (if isPar then
                          current[] -> this (scanArgsReverse).current[];
                          inner scanArgsReverse;
                      if);
                   #)
                else
                   true -> first;
                   scanReverse
                   (# 
                   do (*'Arg: ' -> puttext; 
                       * current.doPT -> puttext; ' ' -> put;
                       *)
                      current[] -> this (scanArgsReverse).current[];
                      inner scanArgsReverse;
                      false -> first
                   #);
                   (*newline*)
               if)
            #);
          hasVirtualArgs: BooleanValue
            (#
            do (*'Arguments:hasVirtualArgs:' -> puttext; dopt -> putline;*)
               L:
                 scanArgs
                 (# D: ^Decl 
                 do current[] -> D[];
                    (*'HasVirtualArgs: ' -> puttext; D.doPT -> putline;*)
                    (if D.isVirtual then
                        true -> value;
                        leave L
                    if)
                 #)
            #)
       enter isKeyword
       #);
     Value:< Node
       (* A list of Decl's *)
       (# PT::
            (#
            do (if son[] <> none then 
                   (if true then
                       'out ' -> lx.append
                    else
                       ' -> ' -> lx.append;
                   if);
                   scanSons
                   (# 
                   do (pos,break) -> current.PT; 
                      (if not isLast then 
                          ','-> lx.put;
                          ' '-> lx.put
                      if) 
                   #)
               if)
            #);
          length:: 
            (# 
            do 4 -> value;
               scanSons(# do current.length + value -> value #)
            #);
          Label:: (# do 'Value' -> lab[] #);
          PP:: (# do Label -> lx.append #)
       #);
     DataItem:< Decl
       (# PT:: 
            (# 
            do (pos,break,true) -> sig.PT;
               ':' -> lx.put;
               ' ' -> lx.put;     
               label -> lx.puttext;
               ' ' -> lx.put; 
               (pos,break) -> OG.PT 
            #);
          length:: (# do sig.length + 4 + OG.length -> value #);
          Label:: 
            (# D: ^Decl
            do (if true
                // isValue and isConst then 'val' -> lab[]
                // isValue and not isConst then 'var' -> lab[]
                // not isValue and isConst then 'obj' -> lab[] 
                // not isValue and not isConst then 
                   'ref' -> lab[] ;
                   (# E: ^Exp; N: ^ObjectInvocation
                      (* ad hoc to get old syntax x: ?integer pretty-printed
                       * as x: var integer and not as x: ref integer
                       *)
                   do (if OG.IS.isEmpty and ((OG.super.last -> E[]) <> none) then
                          (if E## = ObjectInvocation## then
                              E[] -> N[];
                              (if true 
                               // 'integer' -> N.ptnID.T.equalNCS 
                               // 'char' -> N.ptnID.T.equalNCS
                               // 'boolean' -> N.ptnID.T.equalNCS then
                                  'var' -> lab[]
               if)if)if)#)if)
            #);
          PP:: (# do Label -> lx.append #);
          isDataItem:: TrueValue;
          theObjectGenerator::(# do OG[] -> xOG[] #);
          getEntries:: (# do OG.getEntries -> (names[],entries[]) #);
          entity:: (# do OG[] -> E[] #);
          isPrimitive:: (#do OG.isPrimitive -> value #);
          isBasicPattern:: (# do OG.isBasicPattern -> value #);
          isBasicValue:: 
            (# 
            do (if traceIsBasicValue then
                   '**** Dataitem:isBasicValue: ' -> puttext; dopt -> putline
               if);
               OG.isBasicValue -> value #);
          replaceEntity::
            (# DIx: ^DataItem
            do (if true then
                   '**** DataItem:replaceItem: '->puttext; sig.dopt -> putline;
               if);
               (if true then
                   T[] -> parseDeclTextX -> (errors[],DIx[]);
                   DIx.sig[] -> sig[];
                   DIx.OG[] -> OG[];
                else
                   (path2Module[],T[]) -> OG.replaceEntity -> errors[]
               if)
            #);
          skind:: 
            (# 
            do (if isConst then ': @' -> S[] else ': ^' -> S[] if)
            #);
          OG: ^ObjectGenerator;
          isConst,isValue: @boolean
       enter(isConst,isValue,sig[] -> append,OG[] -> append)
       #);

     plainPtn: (# exit 0#);
     virtualPtn: (# exit 1 #);
     furtherPtn: (# exit 2 #);
     finalPtn: (# exit 3 #);
     Pattern:< Decl
       (# label::< (# do 'pattern' -> lab[]; inner #);
          PP:: (# do label -> lx.append #);
          PT::< 
            (# 
            do (if false then 
                   (if sig.type[] <> none then
                       sig.type.id[] -> lx.puttext;
                       ':' -> lx.put;
                   if)
                else
                   (pos,break,true) -> sig.PT; ':' -> lx.put;
               if);
               (if ptnKind
                // virtualPtn then '<' -> lx.put
                // furtherPtn then ':' -> lx.put; '<' -> lx.put
                // finalPtn then ':' -> lx.put
               if);
               inner; 
               ' ' -> lx.put;
               (pos,break) -> OG.PT
            #);
          length:: (# do sig.length + 3 + OG.length -> value #);
          setNewId::
            (# T,first: ^text; N: ^name
            do sig.id.copy -> T[]; ':' -> T.put; 
               (2,T.length) -> T.sub -> first[];
               (*'setNewId:A: ' -> puttext; T[] -> puttext; newline;*)
               OG.IS.scanSons
               (# D: ^Decl; inx: @integer; arg: ^text
               do L:
                    (if current.isDecl then
                        (if (current[] -> D[]).isInArg then
                            D.sig.id.copy -> arg[]; 
                            ':' -> arg.put; 
                            (if (inx + 1 -> inx) = 1 then
                                (*'arg 1: "' -> puttext; arg[] -> puttext;
                                 '"  ' -> puttext; first[] -> putline;
                                 *)
                                ':' -> T.put;
                                (if arg[] -> first.equalNCS then
                                    leave L
                            if)if);

                            arg[] -> T.append; 
                            (*' ' -> put; T[] -> puttext;*)
                    if)if)
               #);
               (*'setNewId: ' -> puttext; T[] -> putline;*)
               (1,T.length - 1) -> T.sub -> sig.id[];
               T[] -> (sig.args.son[] -> N[]).T[]; (* why? *)
               (*newline*)
            #);
          isPattern:: TrueObject;
          isPatternDecl:: TrueObject;
          theObjectGenerator:: (# do OG[] -> xOG[] #);
          ptnKind: @integer;
          isPrimitivePtn: @boolean; (* probably any primitive *)
          vDcl: ^Decl; 
          primNo:: 
            (#
            do (if OG.IS.props[] <> none then 
                   (if  OG.IS.props.basicProp[] <> none then
                       OG.IS.props.basicProp.primNo -> value 
                    else
                       0 -> value
                   if)
               if)
            #);
          isPrimitive:: (# do OG.IS.isPrimitive -> value #);
          isBasicPattern::
            (#
            do (if OG.IS.isEmpty then
                   (if OG.super.ATD[] <> none then
                       OG.super.ATd.isBasicPattern -> value;
                       leave isBasicPattern
               if)if);
               OG.isBasicPattern -> value
            #);
          isBasicValueCalled: @boolean;

          isBasicValue:: 
            (# DI: ^DataItem; 
            do (if traceIsBasicValue then
                   'pattern:isBasicValue' -> putline; dopt -> putline;
               if);
               (if OG.IS.isEmpty then
                   (if traceIsBasicValue then
                       '**** pattern:isBasicValue:is.isEmpty:  ' -> puttext; 
                       dopt -> putline;
                   if);
                   (if (OG.super.ATd[] <> none) then
                       (if OG.super.ATd.isBasicValue then
                           (if traceIsBasicValue then
                               '**   isBasicValue'->putline;
                           if);
                           true -> value;
                           leave isBasicValue
                   if)if)
               if);
               (if not isBasicValueCalled then
                   (*  For a pattern
                    *    foo(...) -> res: ? foo ...
                    * isBasicValue is called on the foo return value
                    * giving a recursive call of isBasicValue
                    *)
                   true -> isBasicValueCalled;
                   (if (sig.getReturnDecl -> DI[]) <> none then
                       (if DI.isBasicValue -> value then
                           (if traceIsBasicValue then
                               ' return value is basic value'->putline;
                           if);
                           false -> isBasicValueCalled;
                           leave isBasicValue
               if)if)if);
               true -> isBasicValueCalled;
               OG.isBasicValue -> value;
               false -> isBasicValueCalled;
            #);
          isSubOfBasicValue::
            (#
            do (if not (OG. isBasicValue -> value) then
                   (if OG.super.ATd[] <> none then
                       OG.super.ATd.isSubOfBasicValue -> value
               if)if)
            #);
          SpecialPattern:
            (# invoke:< 
                 (# rec,E: ^Exp; useReturnValue: @boolean; 
                    staticOff: @integer;
                    CC: ^CallChain;
                    adr: ^SuperAdr
                 enter(rec[],E[],useReturnValue,staticOff,CC[],adr[])
                 do inner 
                 #); 
            exit this(SpecialPattern)[]
            #);
          currentPtnKind: ^SpecialPattern;
          GeneralPattern:< SpecialPattern(##);
          AssignmentPattern:< SpecialPattern(# isValueAssign: @boolean #);
          IndexedPattern:< SpecialPattern(##);
          BreakPattern:< SpecialPattern(##);
          OtherPrimPattern:< SpecialPattern(##);
          isCore:: (#do OG.IS.isCore -> value #);
          noOfArgs: IntegerValue
            (# superPtn: ^Pattern
            do (if (*(OG.super <> none) and*)
                   not OG.super.lastHasActualArgs and
                   (OG.super.ATd[] <> none) then
                   &(OG.super.ATd[]->superPtn[]).noOfArgs -> value
               if);
               (*'*** noOfArgs: ' -> puttext; sig.dopt -> puttext;
               ' ' -> put; value -> putint;*)
               value + sig.args.length -> value;
               (* ' -> put; value -> putint; newline*)
            #);
          scanArgs:
            (# currentArg: ^Decl; superPtn: ^Pattern
            do (*'pattern:scanArgs:'->puttext; noOfArgs -> putint;
               ' isNK:'->puttext; sig.args.isNewKeyWord -> putboolean;
               newline;*)
               (if (noOfArgs = 0) or sig.args.isNewKeyWord then
                   (* Formal args may be declared as: >> A: ?integer *)
                   OG.IS.scanItemArgs
                   (#
                   do currentArg[] -> this(scanArgs).currentArg[];
                      inner scanArgs 
                   #)
               else (* Formal args in signature as in foo(A: ?integer, ...) *)
                   (if (OG.super <> none) and
                       not OG.super.lastHasActualArgs and
                       (OG.super.ATd[] <> none) then
                       &(OG.super.ATd[]->superPtn[]).scanArgs
                       (#
                       do currentArg[] -> this(scanArgs).currentArg[]; 
                          inner scanArgs 
                       #)
                   if);
                   sig.scanArgs
                   (# 
                   do currentArg[] -> this(scanArgs).currentArg[];
                      inner scanArgs 
               #)if)
            #);
          getReturnOG:
            (* unify with getReturnDecl and getReurn above! *)
            (# OGx: ^ObjectGenerator
            do (if (sig.getReturnOG -> OGx[]) = none then
                   OG[] -> OGx[]
               if)
            exit OGx[]
            #);
          checkVirtualArgs::
            (* Similar to checher::Pattern:check:handleVirtualBinding
             * and generator::Pattern:handleVirtualArg
             * Should be unified
             *)
            (# aArgs: [6] ^ObjectGenerator; top: @integer;           
            do L: 
                 (if not isPrimitive and (sig[] <> none) then
                     E.args.scanArgs
                     (#
                     do (if (top + 1 -> top) > aArgs.range then
                            aArgs.range -> aArgs.extend
                        if);
                        current[] -> aargs[top][]
                     #);
                     scanArgs
                     (# i: @integer; OI: ^ObjectInvocation; E: ^Exp
                     do i + 1 -> i;
                        (if i > top then
                            (* '\n *** Too many actual arguments' -> putline;
                             E.dopt -> putline; sig.doPT -> putline;*)
                            leave L
                        if);
                        HandleVirtual:
                          (if currentArg.isVirtual then
                              (* 'FormalArg: ' -> puttext; 
                               currentArg.doPT -> putline;
                               'ActualArg: ' -> puttext; 
                               aArgs[i].doPT -> putline; 
                               *)
                              (if aArgs[i].super.isEmpty then
                                  (* Implicit super *)
                                  (*'*** super:no:sons: ' -> puttext;
                                   aArgs[i].doPP -> putline;*)
                               else
                                  (* Exlicit super: foo{...}
                                   * Must check that foo is sub of virtual qual
                                   *)
                                  (*'*** super:Has:sons: ' -> puttext;*)
                                  (if aArgs[i].IS.isEmpty then
                                      (* ' IS empty: ' -> puttext;
                                       * current.doPT -> putline;
                                       * aArgs[i].doPT -> putline; *)
                                      L:
                                        aArgs[i].super.scanSons
                                      (# do current[] -> E[]; leave L #);
                                      (if E## = ObjectInvocation## then
                                          (*'Try: ' -> puttext;
                                           * E.doPT -> putline;*)
                                          E[] -> OI[];
                                          (if (1 -> OI.ptnId.T.inxGet) 
                                              = '#' then
                                              (*'Checker:got: ' -> puttext;
                                               OI.ptnId.doPT -> putline;*)
                                              leave HandleVirtual
                                          if)
                                      if);
                                  if);
                                  aArgs[i][] -> mkObjectGenerator -> aArgs[i][];
                              if);
                              currentArg[] -> aArgs[i].super.ATd[];
                              1 -> aArgs[i].super.pn;
                              true -> aArgs[i].super.isImplSuper;
                              (* This seems wrong - check setmplSuper
                               * where it seems we are testing for an
                               * empty IS of the declaration
                               *)
                              (if not aArgs[i].IS.isEmpty then
                                  true -> aArgs[i].super.isSingularImplSuper;
                                  (*' isSingularImplSuper=T ' -> puttext;*)
                               else
                                  (* ' CatImplSuper=T ' -> puttext;*)
                              if);                                
                              (*newline;*)
                              (*aArgs[i].adjustON;*)
                          if)
                 #)if)
            #);
          OG: ^ObjectGenerator
       enter (sig[]->append,OG[]->append,ptnKind)
       #);
     PatternGen:
       (# sig: ^Signature;
          OG: ^ObjectGenerator;
          ptnKind: @integer;
          ptn: ^Pattern
       enter(sig[],OG[],ptnKind)
       do (sig[],OG[],ptnKind) -> Pattern -> ptn[];
          inner
       exit ptn[]
       #);
     GeneralPtnGen: PatternGen
       (# 
       do ptn.generalPattern -> ptn.currentPtnKind[]
       #);
     AssignmentPtnGen: PatternGen
       (# P: ^Pattern.AssignmentPattern
       do ptn.assignmentPattern -> P[] -> ptn.currentPtnKind[];
          (ptn.primNo = vAssign_prim) -> P.isValueAssign
       #);
     BreakPtnGen: PatternGen
       (#
       do ptn.BreakPattern -> ptn.currentPtnKind[]
       #);
     IndexedPtnGen: PatternGen(#do ptn.IndexedPattern -> ptn.currentPtnKind[] #);
     OtherPrimPtnGen: PatternGen(#do ptn.OtherPrimPattern -> ptn.currentPtnKind[] #);
     ObjectGenerator:< item
       (# label:: (# do 'ObjectGenerator' -> lab[] #); 
          PP:: (# do label -> lx.append #);
          PT:: 
            (# ind: @integer
            do (if false then 
                   '<'->lx.put;
                   pos -> lx.putint; ','->lx.put;
                   super.length->lx.putint; '>' -> lx.put;
               if);
               (if false then
                   (if super.length > 0 then 
                     
                       (pos,break) -> super.PT;
                       3 -> ind 
                   if);
                else
                   (if (*true or *)(lx.length > 0) or (super.length > 0) then 
                       (* lx.length = 0 -> do not indent first line 
                        * unless super.length > 0
                        *)
                       (pos,break) -> super.PT;(* '!' -> lx.put;*)
                       3 -> ind 
               if)if);
               (if not IS.isEmpty then
                   (pos + ind,true) -> mkBreak;
                   (pos + ind, length) -> block
                   (#
                   do (pos,true) -> IS.PT;
                   #);
               if)
            #);
          length :: 
            (# 
            do super.length -> value;
               (if not IS.isEmpty and (IS.son.length > 1 ) then
                   lineWidth + 10 + value -> value
               if)
            #);
          isObjectGenerator:: TrueValue;
          isPrimitive: booleanValue
            (* seems inconsistent - IS.isPrimitive is all promitives *)
            (#
            do (* perhaps return true if primNo > 0
                * I.e. return most specific primno
                *)
               (if primNo 
                // integer_prim // char_prim // boolean_prim // float_prim then
                   true -> value
                else
                   (if IS.isEmpty then
                       (if super.ATd[] <> none then
                           super.ATd.isPrimitive -> value
                       if); 
                    else
                       IS.isPrimitive -> value
               if)if)
            #); 
          isBasicPattern: BooleanValue
            (#
            do (if primNo
                // integer_prim // char_prim // boolean_prim // float_prim then
                   true -> value;
               if)
            #);
          isInteger::
            (#
            do (if primNo
                // integer_prim
                // 61 (*+ etc *)// 62 // 66 // 67 // 68 // 69 then
                   true -> value
               if)
            #);
          isBasicValue: BooleanValue
            (* This OG is a basic value like integer, boolean of char
             * or exp/function returning a basic value: a + b, (max(a,b), ...
             * ObjectGeneration in (the rightside)
             * C: = integer(12)   -- refer to pattern integer
             * C: = 12
             * C: = a + b         -- pattern +
             * C: = foo(e1,e2,e3) -- pattern foo
             * C: = X             -- data-item X
             * V: ? a + b         -- and simialr for variable
             * V:= foo(e1,e2,e3)  -- and assignment
             * And they may be singular
             * 1:   OG - non-singular
             * 1.1  OG - integer, boolean, char ==> true
             * 1.2     - else get possible return value
             * 2:   {...} - singular, no super
             * 2.1        - primno = integer, char, boolean
             * 3:   OG{...} - singular with super
             * 3.1          -- return value of OG
             * 3.2          -- OG not simpleValue?
             *)
            (# 
            do (if traceIsBasicValue then
                   '*** isBasicValue:primNo:' -> puttext; 
                   primNo -> putint; newline; 
                   doPT -> putline;
               if);
               (if IS.isEmpty then
                   (if traceIsBasicValue then
                       '\nsuper.ATd:'->puttext; super.ATd.dopt -> putline;
                   if);
                   (if super.ATd.isBasicValue -> value then
                       (if traceIsBasicValue then
                           ' super is basic' -> putline
                       if)
                   if)
                else
                   (if IS.primNo
                    // integer_prim
                    // 61 (* + *)
                    // 62 (* - *)
                    // char_prim
                    // boolean_prim 
                    // float_prim
                    // 109 (* + *)
                       then
                       true -> value; 
                   if);   
               if);
               (if traceIsBasicValue then
                   (if value then
                       '** is basicValue'->putline;
                    else
                       '** is not basicValue'->putline;
               if)if)
            #);
          
          (* Super is never none, super.ATd[] = none implies no super pattern*)
          
          (* isValue (in DataItem) and isValueObj (here) is also a mess
           * isValueObj should be a property of OG and Decl
           * clean-it up
           *)
          isSubOfBasicValue: BooleanValue
            (#
            do 
               (if isValueObj then
                   (* meter: integer
                    *    ...
                    *)
                   (if not IS.isEmpty then
                       super.ATd.isSubOfBasicValue -> value
                   if)
               if);
               (if false and value then
                   '**** isSubOfBasicValue:\n' -> puttext;
                   dopt -> putline
               if)
            #);
          isValueObj: BooleanValue
            (# D: ^Decl;  P: ^Pattern; DI: ^DataItem
            do (if (super.ATd[] -> D[]) = none then
                   primNo = value_prim -> value (* Value pattern *)
                else 
                   (if true
                    // D.isPatternDecl then
                       D[] -> P[];
                       P.OG.isValueObj -> value
                    // D.isDataitem then
                       D[] -> DI[];
                       DI.isValue and not DI.isPrimitive -> value
                       (* do we mean isPrimitive or isBasicPattern? *)
               if)if)
            #);
          isValueObjX: BooleanValue
            (* this ObjectGenerator may be
             * a singular pattern: like foo
             * a DataItem, like: X
             *)
            (# P: ^Pattern
            do (if (*(super[] <> none) and *)((superPtn -> P[]) <> none) then
                   P.OG.isValueObj -> value
                else
                   primNo = value_prim -> value
               if);
            #);
          inValueObj: BooleanValue
            (# D: ^Decl; org: ^Items; F: ^Node; ptn: ^Pattern
            do (*'InValueObj:' -> puttext;*)               
               (if isSingularX then
                   'isSingular:' -> puttext;
                   (* {             <---- check if valueObj
                    *     foo{ ... } 
                    * }
                    *)
                else
                   (*'isPtn:' -> puttext;*)
                   (super.ATd[]-> D[]).father.father[] -> F[];
                   (if F##
                    // ObjectGenerator## then
                       (*'ObjectGen:' -> puttext;*)
                       (* super.ATd.origin *)
                       (if ((D.origin[]->org[]).theObjectGenerator).isValueObj then
                           true -> value
                        else
                           (* try super *)
                       if)
                    // Signature## then
                      (* 'Signature:' -> puttext; F.father.label -> puttext; ':'->put;*)
                       (F.father[]->ptn[]).OG.isValueObj -> value;
                    else
                       '\n***inValueObj: KOKS' -> puttext; 
               if)if);
               (* (if value then 'inValue:' -> puttext if);
                * doPt -> putline;
                *)
            #); 
          isConstLiteral::
            (#
            do (if IS.isEmpty then
                   (super.last).isConstLiteral -> value
               if)
            #);
          superPtn:
            (# D: ^Decl; P: ^Pattern
            do (if ((super.ATd[]-> D[]) <> none) and D.isPatternDecl then
                   D[] -> P[]
               if)
            exit P[]
            #);
          inSuper: BooleanValue
            (# lQ: ^ObjectGenerator
            enter lQ[]
            do ('inSuper:lQ',lQ[]) -> display;
               ('rQ',lQ[]) -> display;
               L:
                 (if lQ[] = this(ObjectGenerator)[] then
                     true -> value 
                  else
                     (if (lQ[] <> none) then
                         (if (lQ.primNo <> object_prim) then
                             lQ.super.getQual -> lQ[];
                             (if lQ[] =  none then
                                 objectDecl.getQual -> lQ[]
                             if);
                             restart L;
                 if)if)if)
            #);
          primNo:: 
            (# 
            do (*'**** OG:primNo: ' -> puttext;
               IS.primNo -> putint;; newline;*)
               (if  (IS.primNo -> value) > 0 then
                else
                   (if (super.primNo -> value) = 0 then
                       IS.primNo -> value
               if)if)
            #);
          isSingularX: BooleanValue
            (#
            do (not IS.isEmpty) or hasVirtualArgs -> value;
            #);
          isSingular: (* This does nyt seem correct;
                       * any ptn or DI has sig[] = none as in
                       * { ...; P1 := P2; foo; ...}
                       * Use isSingularX above
                       *)
            BooleanValue(# do sig[] = none -> value#);
          scanActualArgs:
            (* all actual args in superchain and main 
             * Rather complicated: should be scanVirtualBindings
             * In arguments and subpatterns
             * Must be a list in each pattern keeping track of bindings
             * Like the one for VDT - start by copying from super
             * Then update for each binidng in arguments and main
             *)
            (# current: ^ObjectGenerator; OI: ^ObjectInvocation
            do (if true (* super[]  <> none*) then
                   super.last -> OI[];
                   OI.args.scanArgs
                   (#
                   do current[] -> this(scanActualArgs).current[];
                      inner scanActualArgs
                   #)
               if)
            #);
          getEntries:: (# do IS.getEntries -> (names[],entries[]) #);
          entity:: (# do IS[] -> E[] #);
          desc:: 
            (# 
            do (if IS.isEmpty and not hasVirtualArgs then 
                   super.desc -> D[]
                else
                   this(ObjectGenerator).IS[] -> D[]
               if)
            #);
          hasVirtualArgs: BooleanValue
            (# 
            do L:
                 (if super.hasVirtualArgs -> value then
                  else
                     IS.scanAllDecls
                     (#
                     do (if currentDcl.isVirtual and currentDcl.isInArg then
                            true -> value;
                            leave L
                     if)#)
                 if)    
            #);
          collectSimple:: (# do addSimple## -> IS.collectSimple #);
          genClass:< 
            (# N: ^text; sig: ^Signature; super: ^Invocation; CC: ^CallChain
            enter CC[]
            do inner 
            #);
          hasItems: BooleanValue(#do not IS.isEmpty -> value #);
          OGid:
            (# T: ^text
            do (if sig[] <> none then 
                   sig.ID.copy -> T[]
                else 
                   (if singularName[] <> none then
                       singularName.copy -> T[]
                    else
                       (if super.ATd[] = none then 
                           '' -> T[]
                        else
                           Super.ATd.sig.id.copy -> T[]
                       if);
                       '#S#' -> T.append;
                       IS.qDescNo[1](*descNo*) -> T.putint;
               if)if);
            exit T[]
            #);
          addBB:< 
            (# valueOrObject:< IntegerValue
                 (# ptn: ^Pattern; 
                 enter ptn[]
                 do inner
                 #)
            do inner
            #);
          replaceEntity::
            (# superX: ^Invocation; 
            do 'OG:replaceEntity: ' -> puttext;
               (if sig[] <> none then sig.dopt -> putline if);
               T[] -> putline;
               T[] -> parseItemsText -> (errors[],IS[]);
               (* Old IS is replaced by parsed IS
                * All sons must be re-built using append
                * append also defines te father of super and new IS
                *)
               (if errors[] <> none then
                   '\n**** Parse errors' -> putline;
                   errors[] -> putline;
                else
                   super[] -> superX[];
                   none -> son[];
                   superX[] -> append; 
                   IS[] -> append;
               if);
            #);
          enclosingImperative::
            (#
            do this(ObjectGenerator)[] -> imp[];
            #);
          getODorigin::
            (#
            do enclosingObjectGenerator -> OD[]
            #);
          adjustForSingularImp_X::<
            (# adjust:
                 (#
                 do L:
                      OD.scanSubTree
                      (# doIT::
                           (#
                           do (if current[] = this(objectGenerator)[] then
                                  '** got it:' -> putline;
                                  current.beginPos -> bPos;
                                  current.endPos -> ePos;
                                  leave L
                      if)#)#)
                 #);
               N: ^node
            do '\n**** AdjustForSingularImp: ' -> puttext; label -> puttext;
               ' beginPos: ' -> puttext; beginPos -> putint;
               ' endPos: ' -> puttext; endPos -> putint; newline;
               (*dopt -> putline;*)
               (if isSingular then
                   '**   singular'->putline;
                   bPos + beginPos -> bPos;
                   ePos + beginPos -> ePos;
                   getODorigin -> OD[];
                   true -> OD.doPT;
                   L: (if not (OD.father[]->N[]).isDecl then
                          OD.getODorigin -> OD[];
                          true -> OD.doPT;
                          '**   ODorg:beginPos,endPos:' -> puttext;
                          OD.beginPos -> putint; ',' -> put; OD.endPos -> putint; newline;

                          bPos + OD.beginPos -> bPos;
                          ePos + OD.beginPos -> ePos;
                       restart L
                   if);
                   (*OD.dopt -> putline*)
                   adjust
               if)
            #);
          addBinding: @ addBB;
          super: ^Invocation; (* Never none - super.ATd[] = none if no super *)
          IS: ^Items;
          sig: ^Signature; (* Signature if part of Pattern - none otherwise *)
          singularName: ^text;
       enter(super[] -> append,IS[] -> append)
       #);
     Invocation:< Apl
       (* <Exp> '.' <Exp> '.' ... *)
       (# label:: (# do 'Invocation' -> lab[] #);
          PP::(#do label -> lx.append #);
          PT:: 
            (#
            do (* pos -> lx.putint;'>'->lx.put;*)
               scanSons
               (# E: ^Exp
               do                   
                  (if (*not isLast and *) not isFirst then
                      current[] -> E[];
                      (*'.' *) E.dot -> lx.puttext
                  if);
                  (pos,break) -> current.PT; 
            #)#);
          length:: (# do scanSons(#do current.length + value -> value #)#);
          last: 
            (# E: ^Exp
            do scanSons
               (# 
               do (if isLast then 
                      current[] -> E[]
                  if)
               #);
            exit E[]
            #);               
          isPrimitive: booleanValue
            (#
            do scanSons
               (# E: ^Exp
               do (if isLast then 
                      current[] -> E[]; E.isPrimitive -> value;
                  if)
               #);
            #);
          primNo: IntegerValue
            (#
            do scanSons
               (# E: ^Exp
               do (if isLast then
                      current[] -> E[]; 
                      (if E.ATD[] <> none then E.ATd.primNo-> value if);
                  if)
               #);
            #);
          desc: 
            (# D: ^Items
            do (if ATd[] = none then
                   'Invocation:desc:ATd is none: ' -> puttext; 
                   primNo -> putint; newline;
                   doPT -> putline; '---'->putline;
                   doPP -> putline; '---'->putline;
                   father.father.doPT -> putline
                else
                   ATd.desc -> D[]
               if)
            exit D[]
            #);
          hasVirtualArgs: BooleanValue
            (# 
            do (*'Invocation:hasVirtualArgs: ' -> puttext; dopt -> putline;*)
               L:
                 scanSons
                 (# E: ^Exp; ptn: ^Pattern
                 do current[] -> E[]; 
                    (if (E.ATd[] <> none) then
                        (if E.ATd.sig.hasVirtualArgs -> value then
                            leave L
                         else
                            (if true then
                                (if E.ATD## = pattern## then
                                    (E.ATd[] -> ptn[]).OG.IS.scanAllDecls
                                    (#
                                    do (if currentDcl.isInArg 
                                           and currentDcl.isVirtual then
                                           true -> value;
                                           leave L
                                if)#)if)
                             else
                                (if (E.ATd.sig.args[] <> none) 
                                    and E.ATd.sig.args.isNewKeyWord then   
                                    (*'*** hasVirualArgs: ' -> puttext;*)
                                    (E.ATd[] -> ptn[]).OG.IS.scanAllDecls
                                    (#
                                    do (if currentDcl.isInArg 
                                           and currentDcl.isVirtual then
                                           (* currentDcl.dopt -> putline;*)
                                           true -> value;
                                           leave L
                    if)#)if)if)if)if)#)      
            #);
          lastHasActualArgs: BooleanValue
            (# lastE: ^Exp
            do ((last -> lastE[]).args[] <> none) and (lastE.args.length > 0) 
                 -> value
            #); 
       #);
     Apl: Node
       (# PT::< 
            (# 
            do inner
            #);
          ATd: ^Decl;
          isEmpty: @boolean;
          on,pn: @integer;
          viaIncl: ^Invocation; 
          lgth: @integer; (* length of this invocation 
                             * foo  , length = 1
                             * R.foo, length = 2
                             * A + B, length = 2
                             * etc
                           *)

          isImplSuper,  (* as in V ::< { ... } *)
          isSingularImplSuper (* where super is as in V:< foo{...}
                               * and not as in V: < A *)
          : @boolean; 
          isThisBlockLevel: BooleanValue
            (#
            do (lgth <= 1) and (on = 0) -> value
            #);
          getQual:<
             (# qual: ^ObjectGenerator 
             do inner;
                (if qual[] = none then
                   (if ATd[] <> none then
                       ATd.getQual -> qual[]
                    (*else apparantly none if empty super??
                       'Apl:getQual:ATd is none '->putline;
                       doPT -> putline*)
               if)if)
             exit qual[]                 
             #);
          isNameApl:: (# do this(Apl)[] -> NA[] #);
          getDeclModule::
            (# d: ^Decl;
               item: ^ModuleItem
            do (if ATd[] <> none then
                   ATd.theiModule -> to[]; 
                   ATd[] -> d[]; 
                   (if d.sort <> none then
                       (if d## <= ModuleItem## then
                           (*'MI: ' -> puttext; d.IT.doPT -> putline;*)
                           (*d.IT[] -> item[];*)
                           d[] -> item[];
                           &iModule[] -> to[];
                           item.md[] -> to.M[]; 
                        else
                           ATd[] -> dclx[]
                       if)
                    else
                       '\n****NameApl:getDeclModule:sort is none'->putline
                   if)
                else
                   (if this(node)## <= lexem## then
                       father.getDeclModule -> (to[],dclx[])
                    else
                       '\n****NameApl:getDeclModule:dcl is none'->putline;
                       doPP -> putline;
               if)if)
            #);         
          load:< (# rec: ^Exp; A: ^SuperAdr enter(rec[],A[]) do inner #);
       #);
     superAdr: 
       (# rec,E: ^Exp;
          superAdj,
          off,size,descNo: @integer;
          isValue,isFloat,originIsValue,isAdr,srcIsVal,destIsVal: @boolean;
          loadAdr:< (#do inner #);
          load:< (#do inner #);
          store:< (# do inner #);
          print:< (# T: @text do inner exit T[] #);
       do inner 
       #);
     Exp: Apl
       (# dot:< (# T: ^text do '.' -> T[]; inner exit T[] #);
          (*isPrimitive:< BooleanValue(# do inner #);*)
          isPrimitive: BooleanValue
            (# do (if ATd[] <> none then  ATd.isPrimitive -> value if) #);
          desc: (# D: ^Items do ATd.desc -> D[] exit D[] #);
          pushThis:< 
            (# gdb:< debug(# do 'pushThis:'->lx.puttext; inner #);
            do gdb; inner 
            #);
          getAdr:<
            (# gdb:< debug(# do 'getAdr:'->lx.puttext; inner #);
               adr: ^SuperAdr; loadRec: @boolean
            enter adr[]
            do gdb; inner
            exit(loadRec,adr[])
            #);
          loadOrigin:< 
            (# gdb:< debug(# do 'loadOrigin:'->lx.puttext; inner #);
               superAdj: @integer; rec,E: ^Exp; CC: ^CallChain;
               orgIsVal: @boolean
            enter(superAdj,CC[])
            do gdb; 
               this(Exp)[] -> E[] -> rec[];
               inner 
            exit(rec[],E[],orgIsVal)
            #);
          loadArgs:< 
            (# gdb:< debug(# do 'loadArgs:'->lx.puttext; inner #);
               superAdj,staticOff: @integer; CC: ^CallChain
            enter(superAdj,staticOff,CC[])
            do gdb; inner 
            #);
          invoke:< 
            (# gdb:< debug(# do 'invoke:'->lx.puttext; inner #);
               rec,E: ^Exp; useReturnValue: @boolean; 
               staticOff: @integer;
               CC: ^CallChain; adr: ^SuperAdr
            enter(rec[],E[],useReturnValue,staticOff,CC[],adr[])
            do gdb; inner 
            #);
          args: ^Arguments;
       #);
     ObjectInvocation:< Exp
       (# PP:: (# do label -> lx.append #);
          PT:: (# do (pos,break) -> type.PT  #);
          length:: (# do type.length  -> value #);
          label:: (# do (if type[] <> none then type.label -> lab[] else
                        'anOI'->lab[] if)#);
          dot:: (# do type.dot -> T[] #);
          isObjectInvocation:: TrueValue;
          ptnId: ^Name;
          type: ^InvType;
          InvType: Node(# dot:< (# T: ^text do '.' -> T[]; inner exit T[] #) #);
          Unary: InvType 
            (# Label:: (# do 'ObjectInvocation_Unary' -> lab[] #);
               PP:: (# do Label -> lx.append #);
               PT:: (# do (pos,break) -> ptnId.PT; 
                       (pos,break,false) -> args.PT #);
               length:: (# do ptnId.length + args.length -> value #);
            #);
          Binary: InvType
            (# Label:: (# do 'ObjectInvocation_Binary' -> lab[] #);
               PP:: (# do Label -> lx.append #);
               PT::
                 (#
                 do (pos,break) -> ptnId.PT; ' '-> lx.put;
                    (pos,break,false) -> args.pt
                 #);
               length:: (# do ptnId.length + 1 + args.length -> value #);
               dot:: (# do ' ' -> T[] #);
            #);
          Function: InvType
            (# label:: (# do 'ObjectInvocation_Function' -> lab[] #);
               PP:: (# do Label -> lx.append #);
               PT:: (# do (pos,break) -> ptnId.PT; (pos,break,false) -> args.PT #);
               length:: (#do ptnId.length + args.length -> value #);
            #);
          KeyWord: InvType
            (# Label:: (# do 'ObjectInvocation_KeyWord' -> lab[] #);
               PP:: (# do Label -> lx.append #);
               PT:: 
                 (# 
                 do (* first element in sons is receiver - may be empty *)
                    args.scanSons
                    (# no: @integer; even: @boolean; n: ^Name
                    do no + 1 -> no; 
                       (if true
                        // no = 1 then (* first keyword *)
                           current[] -> N[];
                           (if (1->N.T.inxGet) = '%' then
                               (*N.T[] -> puttext; ' ' -> put;*)
                               ('@',1) -> N.T.inxPut;
                               (*N.T[] -> puttext; ',' -> put;*)
                           if);
                        // (no > 1) and not even then (* 2,3, ... keyword *)
                           current[] -> N[];
                           (if (1->N.T.inxGet) = '%' then
                               (*N.T[] -> puttext; ' ' -> put;*)
                               (':',1) -> N.T.inxPut;
                               (1,N.T.length - 1) -> N.T.sub -> N.T[];
                               (*N.T[] -> puttext; ',' -> put;*)
                           if);
                       if);
                       not even -> even
                    #);
                    (if (pos + length) < lineWidth then  
                        (* 'KW: < lineWidth: pos: '-> puttext; pos -> putint;
                         ' length: ' -> puttext; length -> putint ; newline;*)
                        (pos,false,false) -> args.PT
                     else                            
                        (* 'KW: > lineWidth:pos: ' -> puttext; pos -> putint;
                        ' length: '->puttext; length -> putint; newline;*)
                        args.scanSons
                        (# no: @integer; even: @boolean; doBreak: @boolean
                        do no + 1 -> no; 
                           (*current.label -> puttext; ' ' -> put;*)
                           (*pos -> lx.putint;':'->lx.put;*)
                           (if true
                            // no = 1 then 
                               (* first keyword: 
                                *    if '@foo:' then ':' is not printed by name.doPT
                                * we should perhaps avoid the ':' in '@foo:'!?
                                *)
                               (pos,false) -> current.PT;
                            // (no > 1) and not even then (* 2,3, ... keyword *)
                               (if not doBreak then
                                   pos > lineWidth -> doBreak
                               if);
                               (pos,doBreak) -> mkBreak0;
                               (pos,true) -> current.PT;
                               true -> doBreak;
                            else (* arguments *)
                               (* (pos ,true) -> mkBreak0;*)
                               (pos,doBreak) -> current.PT;
                           if);
                           (if not isLast then ' ' -> lx.put if);
                           not even -> even
                        #);
                        (*newline*)
                    if)            
                 #);
               length:: 
                 (# do args.scanSons(# do current.length + value -> value #)#);
            #);
       enter(name -> ptnId[] -> append,args[] -> append)
       #);
     OI_Generator:
       (# ptnId: ^Text;
          args: ^Arguments;
          OI: ^ObjectInvocation
       enter(ptnId[],args[])
       do (*(if lex.skipcomment.comment.length > 0  then 
              '*** got comment:C:' -> putline;
              lex.skipcomment.comment[] -> putline
          if);*)
          (ptnId[],args[]) -> ObjectInvocation -> OI[];
          (if OI.ptnID.comment[] <> none then
              (* '*** got comment:D:' -> putline;
               OI.ptnID.comment[] -> putline;*)
               OI.ptnID.comment[] -> OI.comment[];
               none -> OI.ptnID.comment[]
           if);
          inner
       exit OI[]
       #);
     UnaryGen: OI_Generator(#do OI.unary -> OI.type[] #);
     BinaryGen: OI_Generator(# do OI.Binary -> OI.type[] #);
     FunctionGen: OI_Generator(# do OI.Function -> OI.type[] #);
     KeyWordGen:  OI_Generator(# do OI.KeyWord -> OI.type[] #);

     BracketedExp:< Exp
       (# Label:: (# do 'BracketedExp' -> lab[] #);
          PP:: (# do Label -> lx.append #);
          PT:: (# do '(' -> lx.put; (pos,break) -> IV.PT; ')' -> lx.put #);
          length:: (# do 2 + IV.length -> value #);
          IV: ^Invocation; 
       enter IV[] -> append
       #);
     Lexem: Exp
       (# T: ^text
       enter T[]
       #);
     Name:< Lexem
       (# PT:: 
            (# 
            do (if printOffOn then
                   '(' -> lx.put; on -> lx.putint; ')' -> lx.put 
               if);
               (if (T.length -> T.inxGet) = ':' then
                   (1,T.length - 1) -> T.sub -> lx.append
                else
                   T[] -> lx.append 
               if)
            #);
          length:: (#do  T.length -> value #);
          label:: (# do 'name:' -> lab[] #);
          PP:: (# do label -> lx.append; T[] -> lx.append #);
       #);
     Const:< Lexem
       (# PT:: (# do T[] -> lx.append #);
          length:: (#do  T.length -> value #);
          Label:: (# do 'ConstLiteral' -> lab[] #);
          PP:: (# do Label -> lx.append; T[] -> lx.append #);
          isConstLiteral:: TrueValue
       #);
     FloatConst:< Lexem
       (# PT:: (# do T[] -> lx.append #);
          length:: (#do  T.length -> value #);
          Label:: (# do 'FloatLiteral' -> lab[] #);
          PP:: (# do Label -> lx.append; T[] -> lx.append #);
          isConstLiteral:: TrueValue
       #);
     CharObj:< Lexem
       (# PT::  (# do '\'' -> lx.put;  T[] -> lx.append; '\'' -> lx.put #);
          length:: (#do  T.length + 2 -> value #);
          label:: (# do 'Char:' -> lab[] #);
          PP:: 
            (# 
            do label -> lx.append; 
               '\''->lx.put; T[] -> lx.append; '\'' ->lx.put
            #);
          isConstLiteral:: TrueValue
       #);     
     StringObj:< Lexem
       (# PT::< (# do '"' -> lx.put;  T[] -> lx.append; '"' -> lx.put #);
          length:: (#do  T.length + 2 -> value #);
          Label:: (# do 'String:' -> lab[] #);
          PP:: (# do label -> lx.append; T[] -> lx.append #);
       #);

     CallChain:
       (# elm:
            (# OG: ^ObjectGenerator;
               next: ^elm;
               valueOrObject: IntegerValue
                 (# ptn: ^Pattern
                 enter ptn[]
                 do ptn[] -> OG.addBinding.valueOrObject -> value;
                    (if (value = 0) and (next[] <> none) then
                        ptn[] -> next.valueOrObject -> value
                    if)
                 #);
            do
            #);
          top: ^elm;
          ValueOrObject: IntegerValue
            (# ptn: ^Pattern
            enter ptn[]
            do '*** CallChain:valueOrObject: "' -> puttext;
               ptn.doPT -> puttext; '"' -> put;
               (if ptn.isVirtualObject then
                   ' isVirtual Object\n'->puttext;
                   (if top[] <> none then
                       ptn[] -> top.valueOrObject -> value
                    else
                       'Top none'->putline
                   if)
               if);
               newline
            #);
          add:
            (# OG: ^ObjectGenerator; CC: ^CallChain; E: ^Elm               
            enter OG[]
            do &CallChain[] -> CC[];
               &elm[] -> E[];
               OG[] -> E.OG[];
               top[] -> E.next[];               
               E[] -> CC.top[]
            exit CC[]
            #)
       #);
     mkObjectGenerator:
       (# Itm: ^Item;
          itmFather: ^Arguments;
          IV: ^Invocation;
          IT: ^Items;
          OG,OGx: ^ObjectGenerator;              
       enter Itm[]
       do (* 'mkObjectGenerator: ' -> putline; Itm.doPP -> putline; *)
          Itm.father[] -> ItmFather[];
          
          (*'Father:'->putline; itmFather.doPP -> putline;*)
          
          Invocation -> IV[];
          true -> IV.isEmpty;
          Items -> IT[];
          Properties -> IT.props[];
          Include -> IT.incl[] -> IT.append;
          Itm[] -> IT.append;
          (IV[],IT[]) -> ObjectGenerator -> OG[] ;          
          itmFather[] -> OG.father[]; (* OLM: 28.02.2020 
                                       * otherwise null ref in qenv TST *)
          
          ReplaceNode:
            (* This code should be a general op in compiler_IF: replaceNode *)
            itmFather.scanSons
            (# pred: ^Node
            do (*':'->put;current.doPT -> putline;*)
               (if current[] = Itm[] then (* 'Found'->putline;*)
                   (if pred[] = none then
                       (* 'mkObjectGenerator: pred is none ' ->putline;
                        * itmFather.doPT -> putline;
                        * Itm.doPT -> putline;*)
                       OG[] -> itmFather.son[];
                    else                       
                       OG[] -> pred.next[];                      
                   if);
                   current.next[] -> OG.next[];
                   none -> Itm.next[];
                   leave ReplaceNode                  
               if);
               current[] -> pred[]
            #);
          itmFather.father.father.father[] -> OGx[];
          OGx.IS[] -> OG.setUpOrigin;
          (*OG.doPP -> putline; itmFather.doPP -> putline*)
       exit OG[]
       #);
     
     
     parseDeclTextX:< (* see alos binding i parser.bet *)
       (# inn: ^text; errors: ^Stream; DI: ^Decl
       enter inn[]          
       do inner
       exit(errors[],DI[])
       #);     
     parseItemsText:<
       (# inn: ^text; errors: ^Stream; IS: ^Items
       enter inn[]          
       do inner
       exit(errors[],IS[])
       #);
     objectDecl,noneDecl,errorDecl,IndexedDecl
     ,thisDecl,thisCoreDecl,coreDecl,resumeDecl,ifThenDecl: ^Decl;
     floatDecl,
     charDecl,booleanDecl,
     integerDecl,StringDecl: ^Pattern; 
     
     locateWorld: @
       (# trace: (# exit false #);
          entries: [8] ^text; top,worldPos: @integer;          
          init:< (# do 0 -> top -> worldPos #);
          deComp:
            (# last,worldPosInCWD: @integer
            do (* CWD = .../beta/.../MiniSystem/.../BETAworld/.../XXX *)
               dirCh -> CWD.put;
               CWD.setPos;
               CWD.scanAll
               (# pos: @integer; N: ^text
               do pos + 1 -> pos; 
                  (if ch // '/' // dirCh then 
                      (if (top + 1 -> top) > entries.range then
                          entries.range -> entries.extend 
                      if);
                      (if pos > 1 then 
                          (last+1,pos-1) -> CWD.sub -> N[] -> entries[top][];
                          (if 'BETAworld' -> N.equalNCS then
                              top -> worldPos;
                              pos -> worldPosInCWD;
                          if)
                      if);
                      pos -> last;
                  if)
               #);
               (if trace then
                   'Entries: ' -> putline;
                   (for i: top repeat entries[i][] -> putline for);
               if);
               (if worldPos = 0 then '\n*** BETAworld not found' -> putline if);
               (1,worldPosInCWD - 1) -> CWD.sub -> topPath[];
               topPath.copy -> path.push;
               dirCh -> topPath.put;
               topPath.copy -> pathToBetaWorld[];
               (if trace then
                   'pathToWorld: ' -> puttext; topPath[] -> putline;
               if);
               'BETAworld.xbeta' -> topPath.append;
               (if trace then
                   'Module: ' -> puttext; 
                   (last+1,CWD.length) -> CWD.sub -> putline;
                   'topPath: ' -> puttext;  topPath[] -> putline;
               if)
            #);
          CWD: ^Text;
          topPath: ^text; (* Path to .../BETAworld/BETAworld.xbeta *)
          fullPath: @boolean; (* true: FN is a full path 
                               * false: FN is just the file name *)
          FN: ^text; (* path to /file name of module to be executed *)
       enter(fullpath,FN[])
       do init;
          (if fullPath then (* from miniEnv *)
              '\n**** From minienv:' -> putline; FN[] -> putline;
              true -> withPos;
              FN[] -> getCWD -> CWD[];
           else
              false -> withPos;
              getCurrentDir -> CWD[];
          if);
          deComp; (* topPath = ' ...../BETAworld/BETAworld.bet'*)
       exit topPath[]
       #);
     addSigFromDI:
       (# MI: ^ModuleItem;
       enter MI[]
       do MI.MD.DI.sig[] -> MI.sig[]; (* clumsy*)
       exit MI[]
       #);
     lex: ^lexer;
     cd: ^ObjectCode;
  do (*true -> traceAlloc;*)
     &ObjectCode[] -> cd[];
     inner
  #)

