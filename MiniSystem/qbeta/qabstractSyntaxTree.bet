ORIGIN '~beta/basiclib/betaenv';
INCLUDE '../xbeta/Tree';
---lib:attributes---
AbstractSyntaxTree: Tree
  (#  Module:< node
       (# SO: ^StaticObject;
          PT:: 
            (# 
            do (0,length) -> block
               (#
               do (0,break) -> SO.PT 
               #)
            #);
          length:: (# do SO.length -> value #);
          PP:: (# do 'Module' -> lx.append #);
       enter SO[] -> append
       #);
     Items:< node 
       (# PT:: 
            (# 
            do (if props[] <> none then 
                   props.PT;
                   (pos,break) -> mkbreak
               if);
               scanSons
               (# 
               do (pos,break) -> current.PT; 
                  (if not isLast then
                      (pos,break) -> mkbreak
                  if)
               #)
            #);
          PP:: (# do 'Items' -> lx.append #);

          ifBasic: BooleanValue
            (#
            do (if props[] <> none then
                   props.findBasic -> value -> isBasic                 
               if)
            #);
           localSearch:
            (# CS: ^ Text; ATd: ^Decl
            enter CS[]
            do (if true then 
                   
                   search:
                     scanSons
                     (# I: ^Item; AT: ^Decl
                     do 
                        (*'LocalSearch: ' -> puttext; current.doPT -> putline;*)
                        (*current.dopp -> putline;*)
                        current[] -> I[];
                        (if I.isDecl then
                            I[] -> AT[]; 
                            (* 'Try: ' -> puttext; AT.id[] -> putline;*)
                            (if (CS[] -> AT.sig.id.equalNCS) then
                                (* 'Found in: ' -> puttext;
                                 doPT -> putline;*)
                                AT[] -> ATd[];
                                Leave search
                                (*else
                                 'No match:'->puttext; AT.id[] -> putline*)
                        if)if)
               #)if)
            exit ATd[]
            #);      
          xsearchSuperAndSig:<
            (# CS: ^ Text; ATd: ^Decl
            enter CS[]
            do inner
            exit ATd[]
            #);    
          search:
            (# CS: ^ Text; org: ^Items; ATd: ^Decl; on: @integer
            enter CS[]
            do (*'OT:Search:'->puttext; CS[] -> putline;*)
               (if (CS[] -> localSearch -> ATd[]) = none then
                   (*'Items:search ' -> puttext; CS[] -> putline;*)
                   (if OT[] <> none then
                       CS[] -> OT.SearchSuperAndSig -> ATd[];
                   if);
                   (if ATd[] = none then
                       (if (origin[] -> org[]) <> none then
                           CS[] -> org.search -> (ATd[],on);
                           on + 1 -> on
               if)if)if)
            exit(ATd[],on)
            #);
           genClass:< 
            (# N: ^text; sig: ^Signature
            enter(N[],sig[])
            do inner 
            #);
          doCall:<
            (# N: ^text; LMR: @char
            enter(N[],LMR)
            do inner; 
            #);
          descNo: @integer;
          isBasic,
          isEmpty: @boolean;
          OT: ^ObjectTemplate;
          props: ^Properties;
       #);
     Item: node 
       (# (*sig: ^Signature; (* Should be eliminated - also sig in Decl *)
          isDecl:< BooleanValue;
          isBasic:< BooleanValue(# do inner #);
          desc:<
            (# elm:< node; D: ^Items
            do inner
            exit D[]
            #);
       do inner 
       #);
     Properties:< node
       (# PT::
            (#
            do '['->lx.put;
               scanSons
               (#
               do current.pt; (if not isLast then ','->lx.put if) 
               #);
               ']'->lx.put;
            #);
          scanProps:
            (# currentProp: ^Property
            do scanSons
               (#
               do current[] -> currentProp[];
                  inner scanProps
               #)
            #);
          findBasic: booleanValue
            (#
            do scanProps
               (#
               do L:
                    (if 'basic' -> currentProp.P.L.T.equalNCS then
                        currentProp[] -> basicProp[];
                        basicProp.setPrimNo;
                        true -> value;
                        leave L
                    if)
               #)
            #);         
          basicProp: ^Property
       do
       #);
     Property:< node
       (# PT:: 
            (# 
            do P.pt; (if arg[] <> none then arg.PT if) 
            #);
          P: ^lLexem;
          arg: ^Properties;
          setPrimNo:
            (# V: ^Const
            do (if arg[] <> none then
                   arg.ScanSons
                   (# prp: ^Property
                   do current[] -> prp[];
                      prp.P[] -> V[];                   
                      V.L.T.setPos; V.L.T.getInt -> primNo
                   #);
               if)
            #);
          primNo: @integer;
       enter append
       #);
     Decl: Item
       (# PT::< 
            (# 
            do (*'('->lx.put; off -> lx.putint; ')'-> lx.put;*)
               inner 
            #);
          sig: ^Signature;
          off: @integer;
          isDecl:: TrueValue;
          SearchSig:
            (# CS: ^text; ATd: ^Decl
            enter CS[]
            do (* '\nAttribute:SearchSig: '->puttext; CS[] -> puttext; 
                *                '\n in: '->puttext; sig.dopT -> putline;
                *                ' of: ' -> puttext; dopt->putline;
                *                ' father: ' -> puttext; dopt->putline;
                *)
               CS[] -> sig.searchSig -> ATd[]
            exit ATd[]
            #);
          pushThis:< (# do inner #);
          loadAdr:< (# do inner #);
          load:< (# do inner #);
          store:< (# do inner #);
       #);
     Signature:< node
       (* In xbeta, Signature have subpatterns UnarySignature, etc 
        * Consider including arguments in the Signature
        *)
       (# PT:: 
            (# 
            do scanSons
               (#do (pos,break) -> current.PT; 
                  (if not isLast then ' ' -> lx.put if)
               #);
               (*(if val[] <> none then val.PT if)*)
            #);
          PP::(# do 'Signature'-> lx.append #);
          xS: ^Lexem; (* Arguments, and fatCommas are just appended *)
          args: ^Items;
          val: ^Value;
          id: ^text;
          isKeyWord: @boolean;
          setId:
            (# 
            do ''-> id[];
               args.scanSons
               (# B: @boolean; L: ^Lexem
               do (if not B then 
                      current[] -> L[];
                      L.T[] -> id.append;
                  if);
                  not B -> B
               #);
               true -> isKeyword
            #);
          scanParameters:
            (# currentArg: ^Decl
            do scanSons
               (# isPar: @Boolean
               do
                  (if isPar then
                      current[] -> currentArg[];
                      inner scanParameters
                  if);
                  not isPar -> isPar
               #)
            #);
          scanParametersReverse:
            (# currentPar: ^Decl; first: @boolean
            do (if args[] <> none then 
                   (if isKeyword then
                       scanReverse
                       (# isPar: @boolean
                       do 
                       #)
                    else
                       true -> first;
                       args.scanReverse
                       (# 
                       do 
                          'Arg: ' -> puttext; current.doPT -> puttext; ' ' -> put;
                          current[] -> currentPar[];
                          inner scanParametersReverse;
                          false -> first
                       #);
                       newline
               if)if)
            #);
          SearchSig:
            (# CS: ^text; ATd: ^Decl; isArg: @boolean
            enter CS[]
            do (*'\Signature:SearchSig: '->puttext; CS[] -> puttext;
               ':'->put; id[] -> putline;*)
               Loop:
                 (if args[] <> none then       
                     (if not isKeyword then true -> isArg if);
                     args.scanSons
                     (# D: ^Decl
                     do (if isArg then
                            (*(if isLast and (val[] <> none) then leave Loop if);*)
                            (*current.doPT -> putline;*)
                            current[] -> D[];
                            (if CS[] -> D.sig.id.equalNCS then 
                                D[] -> atD[];
                                leave Loop
                            if);
                            (if isKeyWord then false -> isArg if)
                         else
                            true -> isArg
                        if)
                 #)if);
               Loop:
               (if val[] <> none then
                   val.scanSons
                   (# D: ^Decl
                   do current[] -> D[];
                      (if CS[] -> D.sig.id.equalNCS then 
                            D[] -> atD[];
                            leave Loop
                        if);
                   #)
               if)
            exit ATd[]
            #); 
       enter id[] -> Lexem -> (*S[] ->*) append
       #);     
     Value:< node
       (* A list of Decl's *)
       (# PT::
            (#
            do (if son[] <> none then 
                   ' -> ' -> lx.append;
                   scanSons
                   (# 
                   do current.PT; 
                      (if not isLast then 
                          ','-> lx.put;
                          ' '-> lx.put
                      if) 
                   #)
               if)
            #);
          PP:: (# do 'Value' -> lx.append #)
       #);
     StaticObject:< Decl
       (# OG: ^ObjectGeneration;
          PT:: 
            (# 
            do (pos,break) -> sig.PT;
               ':' -> lx.put;
               ' ' -> lx.put; '@' -> lx.put; ' ' -> lx.put; 
               (pos,break) -> OG.PT 
            #);
          length:: (# do 3 + OG.length -> value #);
          PP:: (# do 'StaticObject'-> lx.append #);
          isBasic:: (# do OG.isBasic -> value #)
       enter(sig[] -> append,OG[] -> append)
       #);
     DynamicObject:< Decl
       (# PI: ^PatternId;
          PT:: 
            (# 
            do (pos,break) -> sig.PT;
               ':' -> lx.put;
               ' ' -> lx.put; '^' -> lx.put; ' ' -> lx.put; 
               (pos,break) -> PI.PT 
            #);
          length:: (# do 3 + PI.length -> value #);
          PP:: (# do 'StaticObject'-> lx.append #)
       enter(sig[] -> append,PI[] -> append)
       #);     
     Pattern: Decl
       (# PT::< 
            (# 
            do (pos,break) -> sig.PT; 
               inner;
               ' ' -> lx.put;
               (*(pos+4,true) -> mkBreak;*)
               (pos+4,true) -> OD.PT
            #);
          setUpOrigin::
            (#
            do origin[] -> sig.setUpOrigin;
               origin[] -> OD.setupOrigin
            #);
          check:: 
            (# 
            do (*'\n>>Pattern: ' -> puttext; sig.id[] -> puttext; ':'->put;
               sig.doPT -> puttext; *)
               (* (if scope[] = none then
                *     '\nPattern:check:scope is none!'->putline;
                * if);
                *)
               (*scope[] -> sig.check;*)
               OD.IS[] -> sig.check;
               scope[] -> OD.check ;
            #);
          desc::
            (# elm:: items
            do OD.IS[] -> D[]
            #);
          isBasic:: (# do OD.IS.isBasic -> value #);
          OD: ^ObjectDesc
       #);
     UnaryPattern:< Pattern
       (# PP:: (# do 'UnaryPtn:' -> lx.append; sig.id[] -> lx.append #);
          PT:: (# do ':' -> lx.put #);
       enter (sig[]->append,OD[]->append)
       #);
     BasicUnaryPattern:< Pattern
       (# PP:: (# do 'UnaryPtn:' -> lx.append; sig.id[] -> lx.append #);
          PT:: (# do ':' -> lx.put #);
       enter (sig[]->append,OD[]->append)
       #);
     BinaryPattern:< Pattern
       (# PP:: (#do 'BinaryPtn:'-> lx.append; sig.id[]-> lx.append #);
          PT:: (# do ':' -> lx.put #);
       enter(sig[]->append,OD[]->append)
       #);
     BasicBinaryPattern:< Pattern
       (# PP:: (#do 'BasicBinaryPtn:'-> lx.append; sig.id[]-> lx.append #);
          PT:: (# do ':' -> lx.put #);
       enter(sig[]->append,OD[]->append)
       #);
     FunctionalPattern:< Pattern
       (# PP:: (# do 'FunctionalPtn:' -> lx.append; sig.id[] -> lx.append #);
          PT:: (# do ':' -> lx.put #);
       enter(sig[] -> append,OD[]->append)
       #);
     BasicFunctionalPattern:< Pattern
       (# PP:: (# do 'BasicFunctionalPtn:' -> lx.append; sig.id[] -> lx.append #);
          PT:: (# do ':' -> lx.put #);
       enter(sig[] -> append,OD[]->append)
       #);     
     KeywordPattern:< Pattern
       (# PP:: (# do 'KeywordPattern:' -> lx.append; sig.id[] -> lx.append #);
          PT:: (# do ':' -> lx.put #);
       enter(sig[] -> append, OD[]->append)
       #);
     BasicKeywordPattern:< Pattern
       (# PP:: (#do 'BasicKeywordPattern:' -> lx.append; sig.id[] -> lx.append #);
          PT:: (# do ':' -> lx.put #);
       enter(sig[] -> append, OD[]->append)
       #);     
     VirtualPattern:< Pattern
       (# PP:: (# do 'VirtualPattern:' -> lx.append; sig.id[] -> lx.append #);
          PT:: (# do ':' -> lx.put; '<' -> lx.put #);
       enter(sig[] -> append, OD[]->append)
       #);
     ObjectTemplate: item
       (# IS: ^Items;
          searchSuperAndSig:<
            (# CS: ^ Text; ATd: ^Decl
            enter CS[]
            do inner
            exit ATd[]
            #);
          genClass:< 
            (# N: ^text; sig: ^Signature
            enter(N[],sig[])
            do inner 
            #);
       #);
     ObjectGeneration:< ObjectTemplate
       (# PT:: 
            (# 
            do (pos,break) -> IV.PT;
               (if not IS.isEmpty then 
                   (pos+4,true) -> mkBreak;
                   (pos+4,true) -> IS.PT;
               if)
            #);
          PP:: (# do 'ObjectGeneration' -> lx.append #);
          searchSuperAndSig:: (* needs to be fixed*)
            (# (*CS: ^text; ATd: ^Decl
            enter CS[]*)
            do (*'OG:SearchSuperAndSig:'->putline;*)
               (if IV.ATd[] <> none then
                   (*IV.doPt -> putline;*)
                   CS[] -> (IV.ATd.desc).localSearch -> ATd[]
               if)
            #); 
          isBasic: booleanValue
            (#
            do (if IS.isEmpty then
                   IV.ATd.isBasic -> value; 
                else
                   IS.isBasic -> value
               if);
            #);          
          IV: ^Invocation;

       enter(IV[] -> append,IS[] -> append)
       #);
     PatternId:< Node
       (# PT:: (# do IV.PT; (if id[] <> none then id[] -> lx.append if) #);
          PP:: (#do 'PatternId'->lx.append #);
          desc:
            (# D: ^Items
            do ATd.desc -> D[]
            exit D[]
            #);
          IV: ^Invocation;
          id: ^text;
          ATd: ^Decl;
       enter IV[] -> append
       #);
     ObjectDesc:< ObjectTemplate
       (# PP:: (# do 'ObjectDesc' -> lx.append #);
          PT:: 
            (#
            do superPI.PT;
               (pos,true) -> mkBreak;
               (pos,break) -> IS.PT 
            #);
          searchSuperAndSig:: (* needs to be fixed*)
            (# P: ^pattern;(* CS: ^text; ATd: ^Decl
            enter CS[]*)
            do (*'OG:SearchSuperAndSig:'->puttext; *)
               (if father[] <> none then
                   (*father.doPT -> puttext;*)
                   (if father## <= pattern## then
                       CS[] -> (father[] -> P[]).searchSig -> ATd[]
                    else
                       '\n!!! searchSuperAndSig: father is not sub of pattern'->putline
                   if);
                   (if (ATd[] = none) and (superPI.ATd[] <> none) then
                       (* search super *)
                       CS[] -> (superPI.ATd.desc).localSearch -> ATd[]
                   if)
               if)
            #);
          isBasic: BooleanValue
            (#
            do IS.isBasic -> value
            #);
          superPI: ^PatternId;
     
       enter(superPI[] -> append,IS[] -> append)
       #);
     
     Invocation:< Item
       (* <Exp> '.' <Exp> '.' ... *)
       (# PP::(#do 'Invocation' -> lx.append #);
          PT:: 
            (#
            do scanSons
               (# E: ^Exp
               do                   
                  (if (*not isLast and *) not first then
                      current[] -> E[];
                      (*'.' *) E.dot -> lx.puttext
                  if);
                  (pos,break) -> current.PT; 
            #)#);
          isBasic: booleanValue
            (#
            do
               scanSons
               (# E: ^Exp
               do (if isLast then 
                      current[] -> E[]; E.isBasic -> value;
                  if)
               #)
            #);
          desc: (# exit ATd.desc #);
          ATd: ^Decl
       #);
     Exp: node
       (# ATd: ^Decl;
          on: @integer;
          isAssign: @boolean;
          dot:< (# T: ^text do '.' -> T[]; inner exit T[] #);
          isBasic:< BooleanValue(# do inner #);
          pushThis:< (# do inner #);
          loadAdr:< (# do inner #);
          execute:< (# rec: ^Exp enter rec[] do inner #);
       #);
     Unary:< Exp
       (# PP:: (# do 'Unary' -> lx.append #);
          PT:: (# do M.PT #);
          isBasic:: (# do ATd.isBasic -> value #);
          M: ^Lexem
       enter M[] -> append
       #);
     Binary:< Exp
       (# PP:: (# do 'Binary' -> lx.append #);
          PT::
            (#
            do operator.PT; ' '-> lx.put;
               argument.PT
            #);
          dot:: (# do ' ' -> T[] #);
          operator: ^Lexem;
          argument: ^ObjectGeneration
       enter(operator[]->append, argument[]->append)
       #);
     Function:< Exp
       (# PP:: (# do 'Function' -> lx.append #);
          PT:: 
            (# 
            do scanSons
               (#
               do current.PT; 
                  (if not isLast then ' ' -> lx.put if)
            #)#);
          F: ^Lexem
       enter(lexem -> F[] -> append)
       #);
     KeyWord:< Exp
       (# PP:: (# do 'KeyWord' -> lx.append #);
          PT:: 
            (# first: @boolean
            do (* first element in sons is receiver - may be empty *)
               true -> first;
               scanSons
               (#
               do (pos,break) -> current.PT;
                  (if not isLast (* and not first*) then ' ' -> lx.put if);
                  false -> first
               #)
            #);
          id: ^Text
       #);
     lLexem: Exp
       (# L: ^Lexem
       enter Lexem -> L[] -> append
       #);
     Name:< lLexem
       (# PT:: (# do L.T[] -> lx.append #);
          PP:: (# do L.T[] -> lx.append #);
       #);
     Textt:< lLexem
       (# PT::< (# do '\'' -> lx.put;  L.T[] -> lx.append; '\'' -> lx.put #);
          PP:: (# do L.T[] -> lx.append #);
       #);
     Const:< lLexem
       (# PT:: (# do L.T[] -> lx.append #);
          PP:: (# do L.T[] -> lx.append #);
       #);
     Lexem:< node
       (# PT:: (# do T[] -> lx.append #);
          PP:: (# do T[] -> lx.append #);          
          T: ^text
       enter T[]
       #)     
  do
     inner
  #)
