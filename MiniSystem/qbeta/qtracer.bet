ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/numberio';
---LIB:attributes---
Tracer:
  (# lineMax:< IntegerValue
       (#
       do 70 -> value;
          inner
       #);   
     noSpace:< BooleanValue;
     prompt:
       (# S: ^text
       do inner
       exit S[]
       #);
     pPrompt:< prompt
       (#
       do inner
       #);
     pPrompt2: prompt(#do  '**   ' -> S[] #);
     cPrompt:< prompt
       (#
       do inner
       #);
     nl:
       (#
       do lx.newline;
          0 -> cCount
       #);
     TT:
       (# S: ^text; inText: @Boolean
       enter S[]
       do (if S[] <> none then
              S.scanAll
              (#
              do (if ch
                  // '"' then
                     '"' -> lx.put;
                     not inText -> inText
                  // ascii.newline then
                     nl 
                  else
                     ch -> lx.put
                 if);
                 (if (cCount +1 -> cCount) > lineMax then
                     nl
                 if)
              #)
           else
              '-none-'-> lx.puttext;
              cCount + 6 -> cCount
          if);
          (if not noSpace then ' ' -> lx.put if)
       #);
     TL:
       (# S: ^text; lineCount: @integer; inText,emitNl: @Boolean;
          hasNL: BooleanValue
            (#
            do L:
                 S.scanAll
                 (#
                 do (if (ch = ascii.newline) -> value then leave L if)#)
            #); 
       enter S[]
       do (if S[] <> none then
              (if hasNL -> emitNL then 
                  nl 
               else
                  '"'->lx.put; lineCount + 1 -> lineCount
              if);
              L:
                S.scanAll
                (#
                do (if ch 
                    // '"' then
                       '"' -> lx.put;
                       not inText -> inText
                    // ascii.newline then
                       (if inText then '"' -> lx.put; false -> inText if);
                       (if (lineCount + 1 -> lineCount) > 3 then leave L if);
                       nl
                    else
                       ch -> lx.put
                   if)
                #);
              (if emitNl then 
                  lx.newline
               else
                  '"'->lx.put; lineCount + 1 -> lineCount;
                  (if not noSpace then ' ' -> lx.put if);                  
              if)
          if);
       #);
     TQ: 
       (# S: ^text;
       enter S[]
       do '"' -> lx.put; S[]->TT; '"' -> lx.put; cCount + 2 -> cCount;
       #);
     PT:
       (# S:  ^text; R: ^Object
       enter(S[],R[])
       do S[]->TT; (if R[] = none then 'none'->TT else inner if)
       #);
     CC: (# ch: @char enter ch do ch -> lx.put #);
     II: (# enter lx.putint do ' ' -> lx.put; cCount + 3 -> cCount  #);
     TI: (# S: ^text; V: @integer enter(S[],V) do S[] -> TT; V -> II #);
     BB: 
       (# Bx: @boolean 
       enter Bx 
       do (if Bx then 'true ' -> lx.puttext else 'false ' -> lx.puttext if);
          cCount + 6 -> cCount
       #);
     FF: (# F: @real enter F do F -> lx.putreal; cCount + 10 -> cCount #); 
     doPrint:
       (#
       do (if print then 
              pPrompt -> puttext;
              lx.scanAll
              (#do ch -> put; 
                 (if ch = ascii.newline then pPrompt2 -> puttext if)
              #);
              newline
          if);
       #);
     cCount: @integer;
     print,doEmitCom: @ Boolean;
     lx: ^Text
  do &Text[] -> lx[];
     inner;
     doPrint     
  #)
