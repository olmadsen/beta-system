ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/numberio';
---LIB:attributes---
Tracer:
  (# lineMax:< IntegerValue
       (#
       do 70 -> value;
          inner
       #);   
     noSpace:< BooleanValue;
     prompt:
       (# S: ^text
       do inner
       exit S[]
       #);
     pPrompt:< prompt
       (#
       do inner
       #);     
     cPrompt:< prompt
       (#
       do inner
       #);
     nl:
       (#
       do lx.newline;
          0 -> n
       #);
     TT:
       (# S: ^text; inText: @Boolean
       enter S[]
       do (if S[] <> none then
              S.scanAll
              (#
              do (if ch
                  // '"' then
                     '"' -> lx.put;
                     not inText -> inText
                  // ascii.newline then
                     nl 
                  else
                     ch -> lx.put
                 if);
                 (if (n +1 -> n) > lineMax then
                     nl
                 if)
              #)
           else
              '-none-'-> lx.puttext;
              n + 6 -> n
          if);
          (if not noSpace then ' ' -> lx.put if)
       #);
     TL:
       (# S: ^text; lineCount: @integer; inText: @Boolean;
          hasNL: BooleanValue
            (#
            do L:
                 S.scanAll
                 (#
                 do (if (ch = ascii.newline) -> value then leave L if)#)
            #); 
       enter S[]
       do (if S[] <> none then
              (if hasNL then nl if);
              L:
                S.scanAll
                (#
                do (if ch 
                    // '"' then
                       '"' -> lx.put;
                       not inText -> inText
                    // ascii.newline then
                       (if inText then '"' -> lx.put; false -> inText if);
                       (if (lineCount + 1 -> lineCount) > 3 then leave L if);
                       nl
                    else
                       ch -> lx.put
                   if)
                #)
          if);
       #);
     CC: (# ch: @char enter ch do ch -> lx.put #);
     II: (# enter lx.putint do ' ' -> lx.put; n + 3 -> n  #);
     BB: 
       (# Bx: @boolean 
       enter Bx 
       do (if Bx then 'true ' -> lx.puttext else 'false ' -> lx.puttext if);
          n + 6 -> n
       #);
     FF: (# F: @real enter F do F -> lx.putreal; n + 10 -> n #); 
     n: @integer;
     print,doEmitCom: @ Boolean;
     lx: ^Text
  do &Text[] -> lx[];
     inner;
     (if print then 
         pPrompt -> puttext;
         lx.scanAll
         (#do ch -> put; 
            (if ch = ascii.newline then pPrompt -> puttext if)
         #);
         newline
     if);
  #)
