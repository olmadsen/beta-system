ORIGIN 'qabstractSyntaxTree'
---astlib:attributes---
(* The patterns here are called from semchecker via 
 * semchecker::ObjectInvocation::getV_qual, which is called from
 * checker::ObjectInvocation:check to get the qualification of a given OI
 *)
traceGetV_qual: (# exit false #);
isNew: (#exit false #);
OGsequence:
  (* Represents a pattern sequence as described in the OOPSLA article:
   *    Semantic Analysis of Virtual Classes and Nested Classes
   * Here we actually have a sequence of ObjectGenerators
   * OG1, OG2, ... OGn
   * where OG2 is super of OG1, OG3 of OG2, ... and OGn of OGn-1
   * OG1 is enclosing the application qualified by the virtual
   * where we want to find the most specific binding
   *)
  (# DOG: ^ObjectSpecification;
     pred: ^OGsequence;
     isRemote: @boolean;
     isNest: @Boolean;
     pathToSuper:
       (# OGsuper: ^ObjectGenerator; on,pn: @integer; E: ^Exp; 
          D: ^Decl; ptn: ^Pattern;
          i: @integer;
          isR: @boolean
       do (if trx then '*** pathToSuper:DOG:' -> putline;  DOG.doPT -> putline if);
          (if not DOG.isChecked then
              (* this happens*) (*'!!! DOG is not checked' -> putline*)
          if);
          (if trx  then
              '**  pathToSuper:DOG.super: "' -> puttext;
              DOG.super.dopt -> puttext; '"\n' -> puttext
          if);
          DOG.super.scanSons
          (# 
          do current[] -> E[];
             i + 1 -> i;
             (if trx then
                 '**  pathToSuper:DOG.super.scanSons:i=' -> puttext;
                 i -> putint; ':E:"'->puttext; E.doPT -> puttext; 
                 '":isImplSuper='->puttext; DOG.super.isImplSuper -> putboolean;
                 newline
             if)
          #);
          i > 1 -> isR;      (* try noOfSons - implicitPre? *)  
          (if trx then
              '**  pathToSuper:E:"' -> puttext; E.dopt -> puttext; 
              '" E.on=' -> puttext; E.on -> putint;
              ' E.pn=' -> puttext; E.pn -> putint;
              ' DOG.super.pn='->puttext; DOG.super.pn -> putint;
              ' isR=' -> puttext; isR -> putboolean; newline;
          if);
          (if DOG.super.isImplSuper then
              DOG.super.ATd[] -> D[];
              0 -> on;
              DOG.super.pn -> pn; 
           else 
              E.ATd[] -> D[];
              E.on -> on; 
              E.pn -> pn; 
          if);
          (if true
           // D[] = none then '!!!! pathToSuper:super.ATd is none' -> putline
           // D.isPattern then D[] -> ptn[]
           else 
              '!!!! pathToSuper:super.ATd is not Pattern or is empty: ' -> putline;
              DOG.doPT -> putline;
              leave pathToSuper
          if);
          ptn.OG[] -> OGsuper[];

          (if trx then
              '**  pathToSuper:end:on='->puttext; on ->putint;
              ':pn='->puttext; pn -> putint;
              ':DOG.super.pn='-> puttext; DOG.super.pn -> putint;
              ':isVirtual=' -> puttext; ptn.isVirtual -> putBoolean; 
              ':ptn:\n' -> puttext;
              ptn.doPT -> putline;
              print
          if);
       exit(OGsuper[],on,pn,isR)
       #);
     ENC:
       (* ENC[D] = [D.origin]
        * ENC[D1,D2,...,Dn] = PRE(j) o ENC(i) 0 ENC[D1,D2,...,Dn-1]
        *    where Path(Dn) = (i,j)
        *)
       (# S: ^OGsequence; 
          OGsuper: ^ObjectGenerator; onx,pnx: @integer; isR: @boolean
       do (if trx or traceGetV_qual then
              '**** ENC: ****'->putline;
              print
          if);
          (if true 
           // isNest then
              (if trx or traceGetV_qual then '**   isNest:true ' -> putline if);
              pred[] -> S[]
           // isRemote and isNew then
              (if traceGetV_qual then '**** ENC:isRemote'->putline if);
              Remote -> S[]
           else
              (if pred[] = none then (* head *)
                  (if trx then
                      '--  ENC:pred=none:before:origin:'->putline;
                      DOG.dopt -> putline
                  if);
                  (* this(OGsequence) is super of S *)
                  DOG.origin.father[] -> OGsequence -> S[];

                  (if trx then
                      '**   ENC:pred=none:\n' -> puttext;
                      S.print
                  if);
               else
                  pred.pathToSuper -> (OGsuper[],onx,pnx,isR);
                  (if isR and (onx > 0) then
                      '!!!! OBS ENC:isRemopt and onx > 0: ' -> putline;
                  if);
                  (if trx then
                      '**   ENC:after:pathToSuper:onx:' -> puttext;
                      onx -> putint; ':pnx:' -> puttext; pnx -> putint; 
                      ':isRemote:' -> puttext; isR -> putBoolean;
                      '\n**   OGsuper:' -> putline; 
                      (if OGsuper[] <> none then 
                          OGsuper.dopt -> putline else 'none'->puttext if);
                      '**   thisOGsuper:'->putline; print;
                  if);
                  (* thisOGS = [D1,D2,...,Dn-1,Dn], Dn = DOG *)
                  pred.ENC -> S[];
                  (if trx then 
                      '**   pred.ENC:onx: ' -> puttext; onx -> putint;
                      ' pnx: ' -> puttext; pnx -> putint;
                      ' isRemote: ' -> puttext; isR -> putboolean; newline;
                      S.print;
                  if);
                  (* S = ENC([D1,D2,...,Dn-1]) *)
                  (if isR then              
                      (* OBS! We should implement remote as in OOPSLA paper!
                       * But check if the handling in PRE is ok!
                       * Both ENC and PRE calls pred.pathToSuper, so do we
                       * 'go' twice here? It seems so!
                       *)
                      (* Dn-1: R.P{...}, where R.P = Dn 
                       * onx = 0, pnx >= 0                  
                       *)
                      '**** remote'->putline;
                      remote;
                  if);
                  (if true then
                      (for i: onx repeat S.ENC -> S[] for);
                      (for i: pnx  repeat S.PRE -> S[] for);
                      (if trx or traceGetV_qual then
                          '**   ENC:result:' -> putline;
                          S.print
                      if)
                  if);
          if)if)
       exit S[]
       #);
     Remote:
       (* this(OGs) = [D1,D2,...,Dn,R] or
        *           = [D1,D2,...,Dn,R.Q]
        * remote([D1,D2,...,Dn],R)
        *    = nest(app(super[j] o enc[i]([D1,D2,...,Dn]),Sa)Rd)
        *    where path(Ra) = (Rd,0,j)
        *    Rd: ref Sa
        *  remote([D1,D2,...,Dn],R.Q)
        *    = nest(app(super[j] o remote([D1,D2,...,Dn,R),Ta,Td)))
        *    where path(Qa) = (Qd,0,j)
        *    Qd: ref Ta
        *)
       (# TTT: (# exit false #);
          OGs,OGx: ^ObjectGenerator; onx,pnx: @integer; isRemote: @boolean;
          ptn: ^Pattern; MI: ^ModuleItem;
          S,Sx: ^OGsequence
       do (if TTT then
              '**** NEW:Remote:\n' -> puttext;
              print
          if);
          (*'***** Remote:beforePathToSuper:'->putline;
           pathToSuper -> (OGs[],onx,pnx,isRemote);*)
          
          pred[] -> Sx[];
          pred.DOG.super.scanSons
          (# E: ^Exp; D: ^SuperDataItem
          do current[] -> E[]; 
             (if traceGetV_qual then
                 '**   remote:scanSons:current:"' -> puttext; 
                 E.dopt -> puttext; '" E.ATd.label: ' -> puttext;
                 E.ATd.label -> puttext; newline;
             if);

             L:
               (if isLast then
                   (if traceGetV_qual then '**   remote:isLast:'->putline if);
                   (for i: E.on repeat Sx.ENC -> Sx[] for);
                   (for i: E.pn repeat Sx.PRE -> Sx[] for);
                   E.ATd[] -> ptn[];
                   PTN.OG[] -> OGx[] -> OGsequence -> S[];
                   (*true -> S.isNest;*)
                   Sx[] -> S.pred[];
                else
                   (* remote([D1,D2,...,Dn],R)
                    *    = nest(app(super[j] o enc[i]([D1,D2,...,Dn]),Sa),Rd)
                    *    where path(Ra) = (Rd,i,j)
                    *    Rd: ref Sa
                    *)
                   (* DOG = R.Q where Q is not remote  and R = R1.R2.Rn, n >= 1 *)
                   (if trx or traceGetV_qual then
                       '**   remote:first:E.on: '->puttext; E.on ->putint;
                       ' E.pn: ' -> puttext; E.pn -> putint;
                       ' E.ATd.label: "' -> puttext; E.ATd.label -> puttext;
                       '" E.ATd: "' -> puttext; E.ATd.dopt -> puttext; '"'->putline;
                   if);
                   (for i: E.on repeat Sx.ENC -> Sx[] for);
                   (for i: E.pn repeat Sx.PRE -> Sx[] for);
                   E.ATd[] -> Sx.append -> (Sx[],OGx[]);
                   OGx[] -> OGsequence -> Sx[];
                   true -> Sx.isNest;
               if);
          #);
          (if trx or traceGetV_qual then
              '**** remote:END'->putline; S.print
          if)
       exit S[]
       #);
     append:
       (# D: ^Decl; S: ^OGsequence; OGx: ^ObjectGenerator
       enter D[]
       do (if D.isDataItem 
           // true then
              (if trx or traceGetV_qual then
                  '**   append: got DataItem: ' -> puttext; D.dopt -> putline;
              if);
              this(OGsequence)[] -> S[];
              (if D.OG.IS.isEmpty then
                  D.OG.super.scansons
                  (# i: @integer; E: ^Exp
                  do (if (i + 1 -> i) > 1 then
                         '!!!!  append:dataItem:remote not implemented:'->putline;
                         D.dopt -> putline;
                     if);
                     current -> E[];
                     (if trx then
                         '**   append:scan:E: "'->puttext; E.doPT -> puttext;
                         '" on=' -> puttext; E.on -> putint;
                         ' E.pn='->puttext; E.pn -> putint; ' E.ATd.OG: "'->puttext;
                         E.ATd.OG.dopt -> putline
                     if);
                     (for i: E.on repeat S.ENC -> S[] for);
                     (for i: E.pn repeat S.PRE -> S[] for);
                     E.ATd.OG[] -> OGx[]
                  #)
               else
                  D.OG[] -> OGx[]
              if)
           else
              '!!!! append: not dataitem - FIX' -> putline
          if);
       exit(S[],OGx[])
       #);
     
     PRE:
       (# S,Sx: ^OGsequence; 
          OGs,OGx: ^ObjectGenerator; onx,pnx: @integer; isR: @boolean;
          ptn: ^Pattern; MI: ^ModuleItem
       do (if trx or traceGetV_qual then
              '**** PRE: ****'->putline; print
          if);
          pathToSuper -> (OGs[],onx,pnx,isR);
          (if isR then
              (* Note! isRemote ==> isImplSuper=false  *)
              (if traceGetV_qual then '**   PRE:isRemote: ' -> putline if);
              this(OGsequence)[] -> S[];
              DOG.super.scanSons
              (# E: ^Exp; D: ^SuperDataItem
              do current[] -> E[]; 
                 (if traceGetV_qual then
                     '**   PRE:scanSons:current:"' -> puttext; 
                     E.dopt -> puttext; '" E.ATd.label: ' -> puttext;
                     E.ATd.label -> puttext; newline;
                 if);
                 L:
                   (if isLast then
                       (if traceGetV_qual then '**   PRE:isLast:'->putline if);
                       E.ATd[] -> ptn[];
                       PTN.OG[] -> OGx[]
                    else   
                       (if E.ATd## 
                        // DataItem## then
                           (if traceGetV_qual then '**   PRE:DataItem:'->putline if);
                           E.ATd[] -> D[]
                        // ModuleItem## then
                           (if traceGetV_qual then '**   PRE:ModuleItem:'->putline if);
                           (E.ATd[] -> MI[]).MD[] -> D[]
                        // pattern## then
                           (* ptn in x.P.r where P is pattern
                            * compiler error:
                            *  P returns object - which is not handled, 
                            *  see mustBeObject in qchecker
                            * semantic error: catched by checker
                            *) 
                           (if traceGetV_qual then '**   PRE:pattern:'->putline if);
                           E.ATd[] -> ptn[];
                           PTN.OG[] -> OGx[];
                           leave L
                        else
                           (if traceGetV_qual then '**   PRE:???:'->putline if);
                           E.ATd[] -> D[];
                       if);
                       (if D.OG.hasItems then
                           (if traceGetV_qual then '**   PRE:OG:hasItems:'->putline if);
                           D.OG[] -> OGx[]
                        else
                           (if traceGetV_qual then '**   PRE:OG:noItems:'->putline if);
                           (D.OG.super.desc).father[] -> OGx[]
                   if)if);
                 
                 (if isNew then
                     (if true or isLast then 
                         (if traceGetV_qual then '**** NEW:PRE'->putline; if);
                         OGx[] -> OGsequence -> Sx[];
                         true -> Sx.isRemote; 
                         S[] -> Sx.pred[];    (* S = this(OGsequence) *)
                         (*Sx.print*)
                     if);
                  else
                     OGx[] -> OGsequence -> Sx[];
                     (if true or (not isLast) then 
                         true -> Sx.isNest;
                         (if traceGetV_qual then
                             '**   Remote:PRE:isNest=true:E:' -> puttext; E.doPT -> putline;
                         if)
                      else
                         (if traceGetV_qual then
                             '**   Remote:PRE:isNest=false:E:' -> puttext; E.doPT -> putline;
                         if)
                     if);
                     S[] -> Sx.pred[];                 
                     Sx[] -> S[];
                     (if traceGetV_qual then
                         '**   Remote:PRE:isNest=true:' -> putline;
                         S.print
                     if)
                 if);
              #);
              Sx[] -> S[];
           else
              OGs[] -> OGsequence -> S[]; 
              this(OGsequence)[] -> S.pred[]
          if);
          (if trx or traceGetV_qual then
              '**   end:PRE'->putline;
              S.print
          if)
       exit S[]
       #);
     
     FB: 
       (# qual: ^ObjectSpecification
       do (if isNest then
              DOG[] -> qual[]
           else
              (if pred[] <> none then
                  pred.FB -> qual[]
               else
                  DOG[] -> qual[]
          if)if);
          (if traceGetV_qual then
              '**** FB:isNest: ' -> puttext; isNest -> putboolean;
              newline; qual.doPT -> putline 
          if)
       exit qual[]
       #);
     print:
       (# L: @integer
       enter L 
       do (if L = 0 then
              '**** OGsequence:'->puttext;
           else
              '***  OGseq:sub:' -> puttext;
          if);
          'isRemote:' -> puttext; isRemote -> putBoolean;
          ':isNest:' -> puttext; isNest -> putBoolean; ' ********'->putline;
          (if DOG[] <> none then
              DOG.dopt -> putline
           else
              'DOG=NONE' -> putline
          if);
          (if pred[] <> none then
              L + 1 -> pred.print
          if);
          (if L = 0 then
              (if pred[] = none then '**** ' -> puttext if);
              ':END:OGsequence ****\n' -> putline
           else
              '**** END:SUB:' -> puttext
          if)
       #)
  enter DOG[]
  exit this(OGsequence)[]
  #);
OGPath: SuperPath
  (* Represents a path as described in the OOPSLA paper:
   *   path = (on,pn) from pred to OG 
   * for first element, pred = none, (on,pn) = (0,0)
   *)
  (# OG: ^ObjectGenerator;
     dcl: ^Decl;
     vDcl: ^Pattern; (* Is vDcl always the same as dcl? Probably yes!? *)
     OI: ^ObjectInvocation; (* Only included for debugging purposes *)
     on,pn: @integer; 
     isNest: @boolean; (* remote path - as e.g. for out R: ... *)
     pred: ^OGPath; (* sub of OG *);
     
     qual: ^ObjectGenerator; (* should be eliminated -! *)

     findQual:
       (# S: ^OGsequence; 
       do traceGetV_qual -> trx;
          (if (trx) or traceGetV_qual then
              '\n**** OGpath:findQual:of:vDcl: \n"'-> puttext; 
              vDcl.doPT -> puttext; '"\n' -> putline;
              print
          if);
          computeOGseq -> S[];
          (if trx or traceGetV_qual then
              '\n*** before:findBinding:' -> putline;
              S.print;
          if);
          (*false -> trx;*)
          S.FB -> findBinding -> qual[];
          (if qual.IS.isEmpty then
              (if trx then
                  '**** OGpath:findQual:qual is pattern: ' -> puttext; 
                  qual.dopt -> putline;
              if);
              qual.super.ATd.getQual -> qual[];
              (if trx then
                  '**   OGpath:findQual:of:super: ' -> putline; 
                  qual.dopt -> putline
              if)
          if)
       exit qual[]
       #);
     computeOGseq:
       (# S: ^OGsequence; OGx: ^ObjectGenerator; onx: @integer
       do (if trx or traceGetV_qual then
              '\n**** OGpath:computeOGseq: on=' -> puttext; on -> putint;
              ' pn=' -> puttext; pn -> putint; 
              ' (pred=none)=' -> puttext; pred[] = none -> putboolean;
              ' isNest= ' -> puttext; isNest -> putBoolean;
              newline; 
              '**   OG:' -> putline; OG.dopt -> putline;
              (*OG.doPP -> putline;*)
          if);
          
          (* OBS! Perhaps in ENC! There is already a test fo isNest in ENC!*)
          (if isNest then on - 1 -> onx else on -> onx if); 
          
          (if pred[] <> none then
              (if trx or traceGetV_qual then 
                  '**   computeOGsequence:go:pred:on=' -> puttext; on -> putint; newline
              if);
              pred.computeOGseq -> S[]; 
              (if trx then
                  '**   computeOGs:after pred.computeOG:onx='->puttext; 
                  onx -> putint; ':pn=' -> puttext; pn -> putint; newline;
                  S.print
              if);
              (for i: onx repeat S.ENC -> S[] for); 
              (if trx then 
                  '**   computeQGs:after:ENC:pn=' -> puttext; pn -> putint; newline;
                  S.print 
              if);
              (for i: pn repeat S.PRE -> S[] for);
              (if trx or traceGetV_qual then
                  '**   computeOGseq:got:OGseq:plain(head<>none):'->putline;
                  S.print
              if)
           else
              (if trx or traceGetV_qual then
                  '**   computeOGsequence:pred is none:onx=' -> puttext;
                  onx -> putint; newline
              if);
              OG[] -> OGx[];
              (if trx and false then OGx.doPT -> putline; if);
              (for i: onx repeat 
                   OGx.origin.father[] -> OGx[];
                   (if trx or traceGetV_qual then
                       '**   gorigin:' -> putline;
                       OGx.doPT -> putline;                   
                   if)
              for);
              (if trx or traceGetV_qual then '///'->putline; if);
              OGx[] -> OGsequence -> S[];
              (if trx or traceGetV_qual then 
                  S.print; 
                  '**   goPre:pn=' -> puttext; pn -> putint; newline
              if);
              (for i: pn repeat S.PRE -> S[] for);
              (if trx or traceGetV_qual then
                  '** computeOGseq:got:OGseq:single(pred=none):' -> putline;
                  S.print
              if)
          if)
       exit S[]
       #);
     findBinding:
       (# OGx: ^ObjectGenerator; qual: ^ObjectGenerator
       enter OGx[]
       do (if (trx) or traceGetV_qual then
              '***  findBindingOf:vDcl:\n' -> puttext; vDcl.doPT -> puttext;
              ' in: ' -> putline;
              OGx.doPT -> putline;
          if);
          L:
            (# P: ^Pattern
            do OGx.IS.scanDecls
               (# ptn: ^Pattern; DI: ^DataItem
               do (if currentDcl.isPatternDecl (*isVirtual?*)then
                      (if trx  then
                          '**   try:ptnDcl:' -> putline; 
                          currentDcl.doPT -> putline;
                      if);
                      currentDcl[] -> ptn[];
                      (if (trx or traceGetV_qual) then
                          (if ptn.vDcl[] <> none then
                              'ptn.vDcl: ' -> putline;
                              ptn.vDcl.doPT -> putline;
                          if);
                          (if vDcl[] <> none then
                              '**   vDcl: ' -> putline;
                              vDcl.doPT -> putline;
                              (if (vDcl.getReturnDecl -> DI[]) <> none then
                                  '**   return: ' -> puttext;
                                  DI.doPT -> putline
                              if)
                      if)if);
                      ptn[] -> P[];
                      loop:
                        (if (P.vDcl[] = vDcl[]) or (vDcl[] = ptn[]) then
                            (if trx or traceGetV_qual then
                                '***  FOUND BINDING: ' -> putline;
                                ptn.doPT -> putline; 
                                (if not ptn.isChecked then
                                    '!!! ptn is not checked '->putline
                                if);
                                newline
                            if);
                            (if ptn.OG.IS.isEmpty then
                                ptn.OG.super.ATd[] -> ptn[]
                            if);
                            ptn.OG[] -> qual[]-> this(OGpath).qual[];
                            leave L
                         else
                            P.vDcl[] -> P[];
                            (if P[] <> none then 
                                restart loop
                            if)
                        if)
                  if)
               #);
               LLL:
                 (if OGx.IS.superDesc[] <> none then (* seems odd!? *)
                     (# Ex: ^Exp; argNo: @integer
                     do (if false then
                            '***** super:OGx.isChecked:'->puttext; 
                            OGx.isChecked -> putBoolean;
                            newline;                        
                            OGx.dopt -> putline;
                        if);
                        (OGx.super.last) -> Ex[];
                        OGx.IS.superDesc.father[] -> OGx[];
                        (if (Ex.args[] <> none) and (Ex.args.noOfSons > 0) then
                            (if false then
                                '*** try super:'->putline;
                                OGx.super.dopt -> putline;
                                vDcl.dopt -> putline;
                            if);
                            OGx.IS.scanNonBoundItemArgs
                            (#
                            do argNo + 1 -> argNo;
                               (if false then currentArg.dopt -> putline; if);
                               (if currentArg[] = vDcl[] then
                                   (if false then
                                       '**** vDcl:argNo: ' -> puttext; 
                                       argNo -> putint;
                                   if);
                                   Ex.args.ScanArgs
                                   (# i: @integer
                                   do i + 1 -> i; 
                                      (if i = argNo then
                                          (if true then
                                              '**** vDcl bound to: ' -> puttext;
                                              current.doPT -> putline;
                                              current.isChecked -> putboolean;
                                          if);
                                          (none,OGx.origin[],OGx.origin[],true) -> current.check;
                                          current.getQual -> qual[];
                                          leave LLL;
                                      if)
                                   #); 
                                   newline
                               if)
                            #)
                        if);
                        
                        (if trx or traceGetV_qual then
                            '**   findBinding:go super'->putline;
                            OGx.dopt -> putline;
                        if);
                     #);
                     restart L
                  else
                     (if trx or traceGetV_qual then
                         '!!!! No binding found:using vDcl:' -> putline;
                     if);
                     vDcl.OG[] -> qual[]
                 if)
            #)
       exit qual[]
       #);   
     print::
       (# L: @integer; DI: ^DataItem
       do '**** Path: on=' -> puttext; on -> putint;
          ' pn=' -> puttext; pn -> putint;  
          ' isNest=' -> puttext; isNest -> putBoolean; newline;         
          (if vDcl[] <> none then
              '**   to:vDcl: ' -> puttext; vDcl.doPT -> putline
          if);
          (if dcl[] <> none then
              '**   to:Decl: ' -> puttext; dcl.doPT -> putline
          if);
          '**   from: ' -> puttext; 
          (if OI[] <> none then OI.doPT -> putline else 'none' -> putline if);
          (if OG[] <> none then
              '**   in:OG: ' -> putline;
              OG.doPT -> putline
           else
              'OG:none' -> putline
          if);

          (if pred[] <> none then 
              '**   Path:pred:' -> putline;
              pred.print 
           else 
              '=== end:pathToDcl\n' -> putline 
          if)
       #)
  enter(OG[],dcl[],OI[],on,pn,pred[])
  exit this(OGPath)[]
  #);


