ORIGIN 'qabstractSyntaxTree'
---astlib:attributes---
OGsequence:
  (# DOG: ^ObjectGenerator;
     pred: ^OGsequence;
     pathToSuper:
       (# OGs: ^ObjectGenerator; on,pn: @integer; E: ^Exp; ptn: ^Pattern
       do DOG.super.scanSons
          (# 
          do current[] -> E[]
          #);
          E.ATd[] -> ptn[];
          (if ptn[] <> none then
              ptn.OG[] -> OGs[];
              E.on -> on; E.pn -> pn
          if);
       exit(OGs[],on,pn)
       #);
     ENC:
       (# S: ^OGsequence; OGs: ^ObjectGenerator; onx,pnx: @integer
       do (if pred[] = none then (* head *)
              DOG.origin.father[] -> OGsequence -> S[];
              this(OGsequence)[] -> S.pred[]
           else
              pathToSuper -> (OGs[],onx,pnx);
              pred.ENC -> S[];
              (for i: onx repeat S.ENC -> S[] for);
              (for i: pnx  repeat S.PRE -> S[] for);
          if)
       exit S[]
       #);
     PRE:
       (# S: ^OGsequence; OGs: ^ObjectGenerator; onx,pnx: @integer
       do pathToSuper -> (OGs[],onx,pnx);
          OGs[] -> OGsequence -> S[];
          this(OGsequence)[] -> S.pred[]
       exit S[]
       #);
     FB: 
       (# qual: ^ObjectGenerator
       do (if pred[] <> none then
              pred.FB -> qual[]
           else
              DOG[] -> qual[]
          if)
       exit qual[]
       #);
     print:
       (#
       do '*** OGsequence:'->putline;
          DOG.dopt -> putline;
          (if pred[] <> none then
              pred.print
          if)
       #)
  enter DOG[]
  do
  exit this(OGsequence)[]
  #);
OGPath: SuperPath
  (# OI: ^ObjectInvocation; 
     OG: ^ObjectGenerator;
     on,pn: @integer; (* path frem pred to OG i (0,0) for first element*)
     hasRemoteSuper: @boolean;
     pred: ^OGPath; (* sub ptn/OG *);
     qual: ^ObjectGenerator;
     vDcl: ^Pattern;
     checkForRemoteSuper:
       (#
       do OG.super.noOfSons > 1 -> hasRemoteSuper
       #);
     getQual:
       (# S: ^OGsequence
       do '*** OGpath:getQual'-> putline;
          computeOGseq -> S[];
          S.print;
          S.FB -> findBinding -> qual[]
       #);
     computeOGseq:
       (# S: ^OGsequence; OGx: ^ObjectGenerator
       do '*** computeOG: on: ' -> puttext; on -> putint;
          ' pre: ' -> puttext; pn -> putint; newline;
          (if pred[]  <> none then
              pred.computeOGseq -> S[];
              (for i: on repeat S.ENC -> S[] for);
              (for i: pn repeat S.PRE -> S[] for);
           else
              OG[] -> OGx[];
              (for i: on repeat OGx.origin.father[] -> OGx[] for);
              OGx[] -> OGsequence -> S[];
              (for i: pn repeat S.PRE -> S[] for)
          if)
       exit S[]
       #);
     getQualX:
       (# qual,OGx,OGs: ^ObjectGenerator; DI: ^dataitem; OGm: ^ObjectGenerator;
          onx,pnx: @integer; ptn: ^Pattern
       enter(onx,pnx,OGs[])
       do (if hasRemoteSuper then
              '** hasRemoteSuper' -> putline;
              (if OGs[] = none then 
                  'OGs is none!?' -> putline;
                  OG.doPT -> putline
              if);
              (for i: onx repeat OGs.origin.father[] -> OGs[] for);
              OGs.dopt -> putline;
              OG.super.scanSons
              (# E: ^Exp
              do (if not isLast then
                     'C: ' -> puttext; current.doPT -> putline;
                     'dcl: ' -> putline;
                     (current[]->E[]).ATd[] -> DI[];
                     DI.OG.dopt -> putline;
                     DI.OG.super.atd.dopt -> putline;
                     DI.OG.super.ATd[] -> ptn[];
                     (if (ptn.OG[] -> OGs.inSuper) then
                         'Found nested/remote' -> putline;
                         ptn.OG[] -> OGm[]
                     if)
                 if);
                 OGm[] -> findBinding -> qual[]
              #)
           else
              (if pred[] <> none then
                  vDcl[] -> pred.vDcl[];
                  (on,pn,OG[]) -> pred.getQualX -> qual[] -> this(OGpath).qual[];
               else
                  '*** path:getQual:'-> putline;
                  OG[] -> OGx[];
                  (for i: on repeat
                       OGx.origin.father[] -> OGx[]
                  for);
                  OGx[] -> findBinding -> qual[]
          if)if) 
       exit qual[]
       #);
     findBinding:
       (# OGx: ^ObjectGenerator; qual: ^ObjectGenerator
       enter OGx[]
       do '*** find binding in : ' -> putline;
          L:
            (#
            do OGx.IS.scanDecls
               (# ptn: ^Pattern
               do (if currentDcl.isPatternDecl then
                      currentDcl[] -> ptn[];
                      (if ptn.vDcl[] = vDcl[] then
                          'Found binding: ' -> putline;
                          ptn.doPT -> putline;
                          ptn.OG[] -> qual[] -> this(OGpath).qual[];
                          leave L
                      if)
                  if)
               #);
               '---findBinding:go super'->putline;
               (if OGx.IS.superDesc[] <> none then
                   OGx.IS.superDesc.father[] -> OGx[];
                   restart L
                else
                   'findBinding: super is none - no binding found'->putline
               if)
            #)
       exit qual[]
       #);     
     print::
       (#
       do '*** Path: on = ' -> puttext; on -> putint;
          ' pn = ' -> puttext; pn -> putint; 
          (if hasRemoteSuper then
              ' hasRemoteSuper' -> puttext
          if);
          newline;
          (*'OI: ' -> putline;
          (if OI[] <> none then
              OI.doPT -> putline;
           else
              'none'->putline
          if);*)

          (if OG[] <> none then
              '*** OG: ' -> putline;
              OG.doPT -> putline
           else
              'OG:none' -> putline
          if);
          (if vDcl[] <> none then
              'vDcl: ' -> putline; vDcl.doPT -> putline
          if);
          (if pred[] <> none then pred.print if)
       #)
  enter(OI[],OG[],on,pn,pred[])
  do checkForRemoteSuper
  exit this(OGPath)[]
  #);


