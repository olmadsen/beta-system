ORIGIN 'qabstractSyntaxTree'
---astlib:attributes---
(* the patterns here are called from semchecker via 
 * semchecker::ObjectInvocationgetV_qual, which is called from
 * checker::ObjectInvocation:check to get the qualification of a given OI
 *)
traceGetV_qual: (# exit false #);
OGsequence:
  (* Represents a pattern sequence as described in the OOPSLA article:
   *   Semantic Analysis of Virtual Classes and Nested Classes
   * Here we actually have a sequence of ObjectGenerators
   * OG1, OG2, ... OGn
   * where OG2 is super of OG1, OG3 of OG2, ... and OGn of OGn-1
   * OG1 is enclosing the application qualified by the virtual
   * where we want to find the most specific binding
   *)
  (# DOG: ^ObjectGenerator;
     pred: ^OGsequence;
     isRemote: @boolean;
     isNest: @Boolean;
     pathToSuper:
       (# OGs: ^ObjectGenerator; on,pn: @integer; E: ^Exp; ptn: ^Pattern;
          i: @integer;
          isRemote: @boolean
       do (if DOG[] = none then'DOG is none'->putline if);
          (*(if DOG.super[] = none then 'DOG.super is none'->putline if);*)
          (if trx then
              '*** pathToSuper:' -> putline;  DOG.doPT -> putline;
          if);
          (if not DOG.isChecked then
              (* this happens*) (*'!!! DOG is not checked' -> putline*)
          if);
          (if trx and false then
              '**  pathToSuper:DOG.super:' -> puttext;
              DOG.super.dopt -> putline;
          if);
          DOG.super.scanSons
          (# 
          do current[] -> E[];
             i + 1 -> i
          #);
          i > 1 -> isRemote;      (* try noOfSons - implicitPre? *)  
          (if E[] = none then '!!! E is none'->putline if);
          (if false and trx then
              '**  pathToSuper:E:' -> puttext; E.dopt -> putline;
          if);
          E.ATd[] -> ptn[];
          (if ptn[] = none then 
              (if trx then
                  '!!!! ptn is none'->putline;
                  DOG.doPT -> putline;   
              if);
              (* not a good way to test that DOG is part of virtual ptn*)
              DOG.father [] -> ptn[];
              ptn.vDcl[] -> ptn[];
          if);
          (if ptn[] <> none then
              ptn.OG[] -> OGs[];
              (if trx then
                  '**  pathToSuper:isVirtual:' -> puttext;
                  ptn.isVirtual -> putBoolean; ':ptn:\n' -> puttext;
                  ptn.doPT -> putline;
              if);
              E.on -> on; E.pn -> pn
          if);
       exit(OGs[],on,pn,isRemote)
       #);
     ENC:
       (* ENC[D] = [D.origin]
        * ENC[D1,D2,...,Dn] = PRE(j) o ENC(i) 0 ENC[D1,D2,...,Dn-1]
        *    where Path(Dn) = (i,j)
        *)
       (# S: ^OGsequence; 
          OGs: ^ObjectGenerator; onx,pnx: @integer; isRemote: @boolean
       do (if trx or traceGetV_qual then
              '*** ENC:'->putline;
          if);
          (if isNest then
              pred[] -> S[]
           else
              (if pred[] = none then (* head *)
                  (if trx then
                      '--  ENC:pred=none:before:origin:'->putline;
                      DOG.dopt -> putline
                  if);
                  (* this(OGsequence) is super of S *)
                  DOG.origin.father[] -> OGsequence -> S[];
                  (*this(OGsequence)[] -> S.pred[];*)
                  (if trx then
                      '--  ENC:pred=none:\n' -> puttext;
                      S.print
                  if);
               else
                  pathToSuper -> (OGs[],onx,pnx,isRemote);
                  (if trx then
                      '**   ENC:after:pathToSuper:onx:' -> puttext;
                      onx -> putint; ':pnx:' -> puttext; pnx -> putint; 
                      ':isRemote:' -> puttext; isRemote -> putBoolean;
                      '\n**  OGs:' -> putline; 
                      (if OGs[] <> none then 
                          OGs.dopt -> putline else 'none'->puttext if);
                      '** thisOGs:'->putline; print;
                  if);
                  (* thisOGS = [D1,D2,...,Dn-1,Dn], Dn = DOG *)
                  pred.ENC -> S[];
                  (if trx then 
                      '** pred.ENC: ' -> putline; S.print;
                  if);
                  (* S = ENC([D1,D2,...,Dn-1]) *)
                  (if false and isRemote then
                      (* Dn-1: R.P{...}, where R.P = Dn 
                       * onx = 0, pnx >= 0                  
                       *)
                   else
                      (for i: onx repeat S.ENC -> S[] for);
                      (for i: pnx  repeat S.PRE -> S[] for);
                      (if traceGetV_qual then
                          S.print
                      if)
                  if);
          if)if)
       exit S[]
       #);
     PRE:
       (# S,Sx: ^OGsequence; 
          OGs,OGx: ^ObjectGenerator; onx,pnx: @integer; isRemote: @boolean;
          ptn: ^Pattern
       do (if trx or traceGetV_qual then
              '*** PRE:'->putline;
          if);
          pathToSuper -> (OGs[],onx,pnx,isRemote);
          (if false and trx then '**   pathToSuper:D:' -> putline if);
          (if isRemote then
               (if traceGetV_qual then
                   '*** isRemote: ' -> puttext; 
               if);
              this(OGsequence)[] -> S[];
              DOG.super.scanSons
              (# E: ^Exp; D: ^DataItem
              do current[] -> E[]; 
                 (if isLast then
                     E.ATd[] -> ptn[];
                     PTN.OG[] -> OGx[]
                  else                     
                     E.ATd[] -> D[];
                     (if D.OG.hasItems then
                         D.OG[] -> OGx[]
                      else
                         (D.OG.super.desc).father[] -> OGx[]
                 if)if);
                 OGx[] -> OGsequence -> Sx[];
                 true -> Sx.isNest;
                 S[] -> Sx.pred[];                 
                 Sx[] -> S[];
                 (if traceGetV_qual then
                     '***Remote-PRE:'->putline;
                     S.print
                 if)
              #);              
           else
              OGs[] -> OGsequence -> S[]; 
              this(OGsequence)[] -> S.pred[]
          if);
          (if traceGetV_qual then
              '***end:PRE'->putline;
              S.print
          if)
       exit S[]
       #);
       
     FB: 
       (# qual: ^ObjectGenerator
       do (if isNest then
              DOG[] -> qual[]
           else
              (if pred[] <> none then
                  pred.FB -> qual[]
               else
                  DOG[] -> qual[]
          if)if)
       exit qual[]
       #);
     print:
       (# L: @integer
       enter L 
       do (if L = 0 then
              '*** OGsequence[:'->puttext;
           else
              '**  OGS:sub[:' -> puttext;
          if);
          (if isRemote then 'isRemote:'->puttext if);
          (if isNest then 'isNest:'->puttext if);
          newline;
          (if DOG[] <> none then
              DOG.dopt -> putline;
           else
              'none'->putline
          if);
          (if pred[] <> none then
              L + 1 -> pred.print
          if);
          (if L = 0 then
              ']*** OGsequence' -> putline
           else
              ']** OGs:sub' -> putline
          if)
       #)
  enter DOG[]
  exit this(OGsequence)[]
  #);
OGPath: SuperPath
  (* Represents a path as described in the OOPSLA paper:
   *   path = (on,pn) from pred to OG 
   * for first element, pred = none, (on,pn) = (0,0)
   *)
  (# OG: ^ObjectGenerator;
     dcl: ^Decl;
     vDcl: ^Pattern; (* Is vDcl always the same as dcl? Probably yes!? *)
     OI: ^ObjectInvocation; (* Only included for debugging purposes *)
     on,pn: @integer; 
     pred: ^OGPath; (* sub of OG *);
     
     qual: ^ObjectGenerator;

     getQual:
       (# S: ^OGsequence
       do (*true -> trx;*)
          (if trx or traceGetV_qual then
              '\n**** OGpath:getQual ****'-> putline;
             print
          if);
          computeOGseq -> S[];
          (if trx or traceGetV_qual then
              '\n*** before findBinding:' -> putline;
              S.print;
          if);
          S.FB -> findBinding -> qual[]
       #);
     computeOGseq:
       (# S: ^OGsequence; OGx: ^ObjectGenerator
       do  (if trx or traceGetV_qual then
               '\n**** computeOGseq: on: ' -> puttext; on -> putint;
               ' pre: ' -> puttext; pn -> putint; 
               ' pred=none: ' -> puttext; pred[] = none -> putboolean;
               newline; 
               '---OG:' -> putline; OG.dopt -> putline;
           if);
          (if pred[] <> none then
              pred.computeOGseq -> S[]; 
              (for i: on repeat S.ENC -> S[] for); 
              (for i: pn repeat S.PRE -> S[] for);
              (if trx or traceGetV_qual then
                  '***computeOGseq:got:OGseq:A:'->putline;
                  S.print
              if)
           else
              OG[] -> OGx[];
              (for i: on repeat OGx.origin.father[] -> OGx[] for);
              OGx[] -> OGsequence -> S[];
              (for i: pn repeat S.PRE -> S[] for);
              (if trx  then
                  '** computeOGseq:got:OGseq:B:' -> putline;
                  S.print
              if)
          if)
       exit S[]
       #);
     findBinding:
       (# OGx: ^ObjectGenerator; qual: ^ObjectGenerator
       enter OGx[]
       do (if trx or traceGetV_qual then
              '*** findBinding: ' -> putline;
              OGx.doPT -> putline;
          if);
          L:
            (# P: ^Pattern
            do (if trx then
                   '** findBinding: ' -> putline
               if);
               OGx.IS.scanDecls
               (# ptn: ^Pattern; DI: ^DataItem
               do 
                  (if currentDcl.isPatternDecl (*isVirtual?*)then
                      (if trx then
                          '--  ptnDcl:' -> putline; 
                          currentDcl.doPT -> putline;
                      if);
                      currentDcl[] -> ptn[];
                      (if trx or traceGetV_qual then
                          (if ptn.vDcl[] <> none then
                              'ptn.vDcl: ' -> putline;
                              ptn.vDcl.doPT -> putline;
                          if);
                          (if vDcl[] <> none then
                              '**** vDcl: ' -> putline;
                              vDcl.doPT -> putline;
                              (if (vDcl.sig.getReturnDecl -> DI[]) <> none then
                                  '**   return: ' -> puttext;
                                  DI.doPT -> putline
                              if)
                      if)if);
                      ptn[] -> P[];
                      loop:
                        (if P.vDcl[] = vDcl[] then
                            (if trx or traceGetV_qual then
                                '*** FOUND BINDING: ' -> putline;
                                ptn.doPT -> putline;
                                (if not ptn.isChecked then
                                    '!!! ptn is not checked '->putline
                                if);
                            if);
                            (if ptn.OG.IS.isEmpty then
                                ptn.OG.super.ATd[] -> ptn[]
                            if);
                            ptn.OG[] -> qual[] -> this(OGpath).qual[];
                            leave L
                         else
                            P.vDcl[] -> P[];
                            (if P[] <> none then 
                                restart loop
                            if)
                        if)
                  if)
               #);
               (if trx or traceGetV_qual then
                   '---findBinding:go super'->putline;
               if);
               (if OGx.IS.superDesc[] <> none then
                   OGx.IS.superDesc.father[] -> OGx[];
                   restart L
                else
                   (if trx or traceGetV_qual then
                       '!!!! No binding found:using vDcl:' -> putline;
                   if);
                   vDcl.OG[] -> qual[]
               if)
            #)
       exit qual[]
       #);   
     print::
       (# L: @integer; DI: ^DataItem
       do '*** Path: on = ' -> puttext; on -> putint;
          ' pn = ' -> puttext; pn -> putint;  newline;         
          (if vDcl[] <> none then
              '--  to:vDcl: ' -> puttext; vDcl.doPT -> putline
          if);
          (if dcl[] <> none then
              '--  to:Decl: ' -> puttext; dcl.doPT -> putline
          if);
          '--  from: ' -> puttext; 
          (if OI[] <> none then OI.doPT -> putline else 'none' -> putline if);
          (if OG[] <> none then
              '--  in:OG: ' -> putline;
              OG.doPT -> putline
           else
              'OG:none' -> putline
          if);

          (if pred[] <> none then 
              (*'\n--- pathToDcl:pred:' -> putline;*)
              newline;
              pred.print 
           else 
              '\n=== end:pathToDcl' -> putline 
          if)
       #)
  enter(OG[],dcl[],OI[],on,pn,pred[])
  exit this(OGPath)[]
  #);


