ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qchecker';
INCLUDE 'qvirtual';
INCLUDE 'qunithandler';
BODY 'semchecker_trace'
---lib:attributes---
semchecker: UnitHandler
  (# Module::<
       (# semcheck:: 
            (# encModule:
                 (# org: ^Items; 
                 do (if origin[] <> none then
                        (origin[] -> org[]).theModule -> M[]
                    if)
                 exit M[]
                 #);
               M: ^Module;
            do (if false then
                   'Module:semCheck:'->puttext; sig.id[] -> putline;
               if);
               (if (encModule -> M[]) <> none then
                   M.semCheck
               if)
            #);
          getVqual::
            (#
            do (if trx then
                   '**** Module:getVqual: ' -> putline; dopt -> puthead
               if);
               OG[] -> qual[]
            #);          
          getQual::
            (#
            do (if trx then
                   '**** Module:getQual: ' -> putline; dopt -> putline
               if);
               OG[] -> qual[]
            #);
       #);
     ModuleItem::<
       (# semCheck::
            (#
            do (if false then
                   '**** ModuleItem:semcheck: ' -> puttext; sig.dopt->putline;
                   ' isIncluded: ' -> puttext; MD.isIncluded -> putboolean;
                   newline
               if);
               (* MD.isIncluded does not seem to work here - a trace shows
                * that no modules are included. It seems that isIncluded is
                * defined via ModulePath in generator. If we delete
                *  false -> doCheck, asemcheck will loop
                * We thus semcheck all modules that have been checked
                * We should fix the compiler to only check, semcheck and
                * generate code for modules actually being used
                *)
               (if not MD.isIncluded then
                   (if false then
                       'Module not included: ' -> puttext; sig.doPT -> putline;
                   if);
                   false -> doCheck
               if);
               (if isChecked then
                   (none,encOG[]) -> MD.OG.semcheck -> encOG[] -> eOG[];
               if)
            #);          
          getVqual:: 
            (# 
            do (if trx then
                   'ModuleItem:getVqual: ' -> puttext; dopt -> puthead
               if);
               md.OG[] -> qual[]
            #);          
          getQual:: 
            (# 
            do (if trx then
                   'ModuleItem:getQual: ' -> puttext; dopt -> puthead
               if);
               md.getQual -> qual[]
            #);
       #);
     NewProperty::<
       (# semcheck::
            (#
            do (if '%id' -> propId.T.equalNCS then
                   (* no semcheck of*)  true -> done 
               if)
            #)
       #);
     DataItem::<
       (# getVqual::
            (#
            do (if trx then
                   '**** DataItem:getVqual: ' -> puttext; sig.dopt->putline;
                   (if OGS[] = none then '!!!! OGS is none' -> putline if);
                   OGS.print
               if);
               (encOG[],false,OGS[]) -> OG.getVqual -> (OGS[],qual[]);
            #);
          getQual:: 
            (# U: ^Text
            do (if trx then
                   '**** DataItem:getQual: ' -> puttext; sig.dopt -> putline
               if);
               OG.getQual -> qual[];
               (if trx then
                   '***** DI:getQual:for: ' -> puttext; sig.doPT -> puttext;
                   ' qual:\n' -> puttext; qual.dopt -> puthead
               if);
            #);
          semCheck::
            (* Check that in S : ? T, V: = T, T is a pattern
             * it seems to work for singular patterns, but check if this is so!
             * And for X: var/val/obj foo(e1,e2,e3) - type(X) = type(foo.return)
             *)
            (# assignError:
                 (#
                 do
                 #);
               
               OGqual: ^ObjectGenerator
            do (if trx then
                   '**** DataItem:semCheck:\n' -> puttext; dopt -> puthead;
                   '**   OG.isPtnOrSingular:' -> puttext;
                   OG.isPtnOrSingular -> putBoolean; 
                   ' isValue:' -> puttext; isValue -> putboolean;
                   ' isPrimitive:' -> puttext; isPrimitive -> putBoolean;
                   ' OG.isValueObj: ' -> puttext; OG.isValueObj -> putBoolean;
                   '\n**  OG:'->putline; OG.dopt -> puthead;
                   OG.label -> putline;
                   '\n**   OG.getQual: ' -> puttext; 
                   (OG.getQual).dopt -> puthead;
                   newline;
               if);
               (if isConst or isValue then
                   (rec[],encOG[]) -> OG.semCheck -> OGqual[]
                else
                   OG.getQual -> OGqual[];
               if);
               (if OG.isPtnOrSingular then
                   not isConst or isArg -> noArgsCheck;
                   (* see qabstractSyntaxTree::semCheck
                    * if noArgsCheck, the sons are not checked
                    * Is this correct?
                    *)
                   (if isValue then
                       (if not (OGqual.isValueObj 
                           or OGqual.isPrimitive
                           and (OGqual.primNo <> string_prim)) then 
                           this(DataItem)[] -> SemanticError
                           (#
                           do 'The type "' -> msg;
                              OG.doPT -> msg;
                              '" of "'-> msg; sig.doPT -> msg;
                              '" must be a value pattern' -> msg;
                           #)
                       if)
                    else
                       (if OGqual.isValueObj then
                           (if OGqual.isBasicValue then
                               (* while implementing sub of integer, etc
                                * we allow decl of the form I: ?integer
                                *)
                            else
                               this(DataItem)[] -> SemanticError
                               (#
                               do 'The qualification "' -> msg;
                                  OG.doPT -> msg;
                                  '" must be an object pattern' -> msg;
                               #)                           
                   if)if)if)
                else
                   (if false then
                       (if OGqual.super.ATd[] <> none then
                           OGqual.super.ATd.doPT -> putline;
                        else
                           OGqual.father.doPT -> puthead
                   if)if);
                   this(DataItem)[] -> SemanticError
                   (#
                   do 'The qualification "' -> msg;
                      OG.doPT -> msg;
                      '" must be a pattern '-> msg;
                   #)
               if)
            #);
          checkQual::
            (* Check that enter arg srcOS can be assigned to dstOG
             * which is this(DataItem) 
             *)
            (# error:
                 (#
                 do (if reportSemErr then
                        srcOS[] -> semanticError
                        (#
                        do '"'->put; srcOS.doPT -> puttext;
                           '" cannot be assigned to "' -> puttext;
                           doPT -> puttext; '"' -> put; newline;
                           '**   in object:\n' -> putline;
                           (enclosingObjectGenerator).father.dopt -> putline;
                           inner error;
                           (if false then
                               '**** srcQual:' -> puttext; 
                               (if srcQual[] <> none then
                                   srcQual.dopt -> putline
                                else
                                   'none'->putline
                               if);
                               '**** srcOS.getQual:' -> puttext; 
                               (srcOS.getQual).dopt -> putline;  
                               '**** dstQual:' -> puttext; 
                               (if dstQual[] <> none then
                                   dstQual.dopt -> puthead
                                else
                                   'none' -> putline
                               if);
                               '**** dstOG.getQual:' -> puttext; 
                               (getQual).dopt -> puthead
                           if)
                 #)if)#);
               unitWarning:
                 (# T: ^text
                 enter T[]
                 do srcOS[] -> warning
                    (#
                    do T[] -> msg; 
                       '\n**   source: "'->msg; srcOS.dopt -> msg;
                       '" unit: "' -> msg; srcU[] -> msg; '"\n' -> msg;
                       '**   destination: "' -> msg; dopt -> msg; '"' -> msg;
                       ' unit: "' -> msg; dstU[] -> msg; '"\n' -> msg;
                       (if false then
                           'srcQual:'->msg; srcQual.doPT -> msg;
                           '\ndstQual:'->msg; dstQual.doPT -> msg
                       if)
                    #)
                 #);
               srcU,dstU,Ux,U: ^text; sameDim: @boolean; 
               BV: ^BaseVector; srcCF,dstCF,srcCFx: @real
            do (if (trx) or traceGetQual then
                   '\n**** DataItem:checkQual:' -> puttext; 
                   (if isConst then ':C:' -> puttext if);
                   (if isAssign then 'assign:' -> puttext if); newline;
                   true->trx;
                   ('**   dst',this(DataItem)[]) -> display; 
                   ('**   src',srcOS[]) -> display;
                   false -> trx;
                   (if false then
                       '**   dst.lab:dataItem' -> putline;
                       '**   src.lab: ' -> puttext; srcOS.label -> putline;
                   if);
                   '**   dstQual:'->putline; dstQual.dopt -> puthead;
                   '**   srcQual:'->putline; srcQual.dopt -> puthead;
                   '**   src: ' -> puttext; 
                   srcOS.doPT -> putline;
                   '**   srcOS.arg1Qual: ' -> puttext;
                   (if srcOS.arg1Qual[] <> none then
                       srcOS.arg1Qual.dopt -> putline
                    else
                       ' none ' -> putline
                   if)
               if);
               (if false and (dstQual.primNo = indexed_prim) then
                   '**** DI:checkQual:dst is Indexed: ' -> puttext; 
                   this(DataItem).dopt -> putline;
                   '**   src: ' -> puttext; srcOs.doPP -> puttext;
                   ' ' -> put; srcOS.dopt -> putline;
               if);
               (* We may come here with 
                * dstU = in base: var Unit
                * I.e. OG = Unit and no items
                * This may be a problem and perhpas we should get Unit.qual!?
                *)
               1 -> dstQual.getUnitPropShort -> dstU[];
               srcOS.getExpUnit -> srcU[];
               (if trx then
                  dopt -> puttext; ' := ' -> puttext; srcOS.dopt -> putline;
                   '**   dstUnit: ' -> puttext; dstU[] -> putline;
                   '**   srcUnit: ' -> puttext; srcU[] -> putline;
               if);
               (if (srcU[] <> none) and (dstU[] <> none) and
                   (srcU.length > 0) and (dstU.length > 0) then
                   (if true
                    // srcU[] -> dstU.equalNCS then
                       (* can we have #I here? *)
                       floatDecl.OG[] -> srcQual[] -> dstQual[]
                    // '#F' -> dstU.equalNCS then
                       (if traceUnit then
                           '**   dstQual:'->putline;
                           dstQual.dopt -> puthead;
                           '**   srcQual:'->putline; 
                           srcQual.dopt -> puthead;
                       if);
                       floatDecl.OG[] -> srcQual[]
                    // '#I' -> dstU.equalNCS then
                       (if true
                        // '#Q' -> srcU.equalNCS then
                           (* must check this case: see assignment of
                            * '#Q' -> U[] in qunithandler
                            *)
                           integerDecl.OG[] -> dstQual[]
                        // '#F' -> srcU.equalNCS then
                           floatDecl.OG[] -> dstQual[]
                        else
                           '!!!! Perhaps unit mismatch: dst: ' -> puttext; 
                           doPT -> puttext; ' ' -> put; dstU[] -> putline;
                           'src: ' -> puttext;
                           srcOS.dopt -> puttext; ' ' -> put; srcU[] -> putline
                       if)
                    // ('U' -> srcU.equal) or ('U' -> srcU.equal) then
                       'Should not happen: unit is "U"'->unitWarning
                    else
                       (* Check if srcU and dstU are units in the same 
                        * dimension and return possible conversion factors
                        * to the corresponding base unit
                        *)
                       (srcU[],dstU[])
                         -> unitTable.findAndCheck
                         -> (sameDim,srcCF,dstCF);
                       (if sameDim then
                           (if false then
                               srcU[] -> puttext; ' ' -> put;
                               srcOS.dopt -> putline;
                               dstU[] -> puttext; ' ' -> put;
                               dopt -> putline;
                           if);
                           srcU[] -> parseUnit -> mkBaseVector -> BV[];
                           (if false then
                               BV.print -> putline;
                               'src:convFC: ' -> puttext;
                           if);
                           BV.convFactor -> srcCFx;
                           (if false then
                               srcCFx -> putReal; ' ' -> put; srcCF -> putReal;
                               ' dstCF: ' -> puttext; dstCF -> putReal;
                           if);
                           (if srcCFx <> srcCF then srcCFX -> srcCF if);
                           (if false then
                               ' srcCF: ' -> puttext; srcCF -> putreal;
                               newline;
                           if);
                           dstU[] -> parseUnit -> mkBaseVector -> BV[];
                           (if dstCF = 0 then BV.convFactor -> dstCF if);
                           (if false then
                               ' dstCF: ' -> puttext; dstCF -> putreal; newline;
                           if);
                       if);
                       (if sameDim then
                           (if srcCF = 0 then 'srcCF is zero' ->unitWarning if);
                           (if dstCF = 0 then 'dstCF is zero' ->unitWarning if);
                           (if srcCF = dstCF then
                               (* No conversion needed
                                * If CF = 1 then srcU and dstU both
                                * are the base unit of the dimension
                                *)
                            else
                               '**** addConvFactor:src: ' -> puttext;
                               srcOS.doPT -> puttext; ' srcCF:' -> puttext;
                               srcCF -> putreal; 
                               ' dstCF:' -> puttext; dstCF -> putreal; 
                               ' src*dst:CF: ' -> puttext;
                               srcCF * dstCF -> putreal; 
                               newline;
                               srcCF * dstCF -> srcOS.addConvFactor;
                           if);
                           floatDecl.OG[] -> srcQual[] -> dstQual[]
                        else
                           (# UT1,UT2: ^UnitTree; dstBV,srcBV: ^BaseVector
                           do dstU[] 
                                -> parseUnit 
                                -> UT1[] -> mkBaseVector -> dstBV[];
                              (if false then
                                  '**   checkqual:parsed:dstU: '->puttext; 
                                  true -> UT1.print -> putline;
                                  dstBV.print;
                              if);
                              srcU[] 
                                -> parseUnit 
                                -> UT2[] -> mkBaseVector -> srcBV[];
                              (if false then
                                  '**   checkqual:parsed:srcU: '->puttext; 
                                  true -> UT2.print -> putline;
                                  srcBV.print; 
                              if);
                              (if srcBV[] -> dstBV.equal
                                  (# nonMatchingCV::
                                       (#
                                       do 'non matching conversion factor:'
                                          -> unitWarning
                                  #) #)                                  
                                  then
                               else
                                  'non matching unitsNEW:' -> unitWarning;
                                  '*** srcU:'->puttext; srcU[]  -> putline;
                                  (if false then 
                                      true -> UT2.print -> putline
                                  if);
                                  srcBV.print; 
                                  '*** dstU:'->puttext; dstU[]  -> putline;
                                  (if false then 
                                      true -> UT1.print -> putline
                                  if);
                                  dstBV.print
                              if);
                           #);
                           floatDecl.OG[] -> srcQual[] -> dstQual[]
                   if)if)
                else
                   (if false then (* this  case is not complete! *)
                       'ToBeFixed:srcU:' -> puttext; srcU[] -> puttext;
                       ' srcOS: ' -> puttext; srcOs.dopt -> puttext;
                       ' dstU: ' -> puttext; dstU[] -> puttext;
                       ' dst: ' -> puttext; dopt -> putline
                   if)
               if);
               (* if src and dst have units:  srcQual = dstQual = floatDecl *)
               (* We need to call getV_ qual at the point of the 
                * ObjectInvocation: X := exp or R. foo
                * to get the actual binding of possible virtuls
                * at the point of call
                *);
               ('srcQual',srcQual[]) -> displayB; (* there is also a Display *)
               ('dstQual',dstQual[]) -> displayB;
               (if isConst and isAssign then
                   (* OG is left-part of assign as in aPoint1 := aPoint2 *)
                   (if OG.isValueObj then
                       error
                       (#
                       do '\n**   The destination is a constant value object '
                          'as declared by "val"'->putline
                       #)
                    else
                       error
                   if)
                else
                   (if true
                    // OG.primNo = indexed_prim then
                       (* This happens  - must check if ok!? *)
                       (if false then
                           '**** assignment to Indexed:dst: ' -> puttext; 
                           dopt -> putline;
                           '**   src: ' -> puttext; srcOs.dopt -> putline;
                           srcOs.doPP -> putline
                       if)
                    // OG.primNo = rIndexed_prim then
                    // OG.isBasicValue then
                       (* The value_prim cases below is to handle
                        *    _V: ?Value 
                        * in patterns integer, char and boolean
                        *)
                       (if srcQual[] <> none then
                           (if srcQual.primNo
                            // integer_prim then
                               (if dstQual.primNo 
                                // integer_prim // char_prim 
                                // boolean_prim // float_prim then
                                else
                                   error
                               if)
                            // char_prim then
                               (if dstQual.primNo 
                                // integer_prim // char_prim // value_prim then
                                else
                                   error
                               if)
                            // boolean_prim then
                               (if dstQual.primNo 
                                // boolean_prim // integer_prim then 
                                else
                                   error 
                               if)
                            // float_prim then
                               (if dstQual.primNo 
                                // float_prim // integer_prim then 
                                else
                                   error 
                               if)
                                                              
                            // value_prim then
                               (if dstQual.primNo 
                                // integer_prim // char_prim // value_prim then
                                else
                                   error
                               if)                               
                            else
                               error
                           if)
                        else
                           error
                       if)  
                    // OG.isValueObj then
                       (if true
                        // srcQual[] = dstQual[] then (* OK *)
                        // srcQual[] -> dstQual.inSuper then 
                           (if dstQual.primNo <> value_prim then
                               srcOS[] -> Warning
                               (# 
                               do 'The right-side value: "' -> msg;
                                  srcOS.doPT -> msg;
                                  '" is truncated ' -> msg;
                                  '\n\twhen assigned to the left-side variable: "'
                                    -> msg;
                                  doPT -> msg;
                                  '"' -> msg
                           #)if)
                        // dstQual[] -> srcQual.inSuper then 
                           '\n**** OBS! source is super of dest: ' -> putline;
                           '**   source: ' -> puttext; srcOS.dopt -> putline;
                           '**   dest  : ' -> puttext; dopt -> putline;
                           srcOS.father.father.father.dopt -> putline
                        // srcQual.isInteger // srcQual.isFloat then
                           srcOS[] -> semanticError
                           (#
                           do 'The integer or float expression: "' -> msg;
                              srcOS.doPT -> msg;
                              '"\n    cannot be assigned to: "' -> msg;
                              doPT -> msg; '"\n' -> msg;
                               (if true then
                                   '**** dstQual: ' -> putline;
                                   dstQual.dopt -> puthead;
                                   '**** srcQual: ' -> putline;
                                   srcQual.doPT -> puthead;
                                   '**   srcQual.father:' -> putline;
                                   srcQual.father.doPT -> puthead;
                                   '**   srcQual.father.father:' -> putline;
                                   srcQual.father.father.doPT -> puthead
                           if)#);
                        else
                           (if srcOS.primNo = indexedGet_prim then
                               ('!!! Not handled:indexGet_prim',srcOS[]) 
                                 -> putPT;
                               (* fix that the type of the indexed DI is 
                                * consistent with the left-side
                                *)
                            else
                               (* we should spell out whether or not srcOS, etc
                                * are objectRefs or valueObjs *)
                               srcOS[] -> semanticError
                               (#
                               do 'The expression:\t"' -> puttext;
                                  srcOS.doPT -> puttext;
                                  '"\n     cannot be assigned to:\t"' 
                                    -> puttext;
                                  doPT -> puttext; '"' -> put; newline;
                                  (if trx then
                                      '**** dstQual: ' -> putline;
                                      dstQual.dopt -> puthead;
                                      '**** srcQual: ' -> putline;
                                      srcQual.doPT -> puthead;
                                      '**   srcQual.father:' -> putline;
                                      srcQual.father.doPT -> puthead;
                                      '**   srcQual.father.father:' -> putline;
                                      srcQual.father.father.doPT -> puthead
                                  if)
                       #)if)if)
                    else
                       (if true
                        // srcOS.isValueObj then
                           srcOS[] -> semanticError
                               (#
                               do 'The value expression: "' -> msg;
                                  srcOS.doPT -> msg;
                                  '"\n     cannot be assigned to '-> msg;
                                  'the object reference: "' -> msg;
                                  doPT -> msg; '"' -> msg
                               #);
                        // srcQual[] = dstQual[] then 
                           (* OK: and may be merged with the case below.
                            * Is splitted for test purposes, see below
                            *)
                        // srcQual[] -> dstQual.inSuper then (* OK *)
                           (* This is just for test purposes: 
                            * for references this is ok, but for values, 
                            * the right-side may be truncated, see above
                            *)
                           (if false then
                               srcOS[] -> Warning
                               (# 
                               do 'The right-side: "' -> msg;
                                  srcOS.doPT -> msg;
                                  '" refers to a subpattern/obj ' -> msg;
                                  'of the left-side: "' -> msg;
                                  doPT -> msg;
                                  '"' -> msg
                               #);
                           if)
                        // (srcQual[] <> none) 
                           and (dstQual[] -> srcQual.inSuper) then
                           srcOS[] -> QuaCheck
                           (# 
                           do 'It must be checked at run-time that "'
                                -> puttext;
                              srcOS.doPT -> puttext;
                              '"\n     is a subpattern of "' -> puttext;
                              doPT -> puttext; '"' -> put; newline
                           #)
                        // (srcQual[] <> none) and (srcQual.primNo = none_prim) then
                        else
                           (if reportSemErr then
                               srcOS[] -> semanticError
                               (#
                               do 'The reference expression:\t"' -> puttext;
                                  srcOS.doPT -> puttext;
                                  '"\n     cannot be assigned to:\t"' -> puttext;
                                  doPT -> puttext; '"' -> put; newline
                               #);
                               (if true then
                                   '**** srcQual:' -> putline;
                                   srcQUal.dopt -> puthead;
                                   '**** dstQual: ' -> putline;
                                   dstQual.doPT -> puthead
               if)if)if)if)if)
            #);
       #);
     LabelDecl::<
       (# getVqual::
            (#
            do (if trx then
                   '**** LabelDecl:getVqual: ' -> puttext;
                   sig.dopt->putline;
                   (if OGS[] = none then '!!!! OGS is none' -> putline if);
                   OGS.print
               if);
               (encOG[],false,OGS[]) -> OG.getVqual -> (OGS[],qual[]);
               (if false then
                   '**   qual: ' -> putline;
                   qual.doPT -> putline
               if)
            #)
       #);
     isPatternDen:< BooleanValue
       (# E: ^Exp
       enter E[]  
       do E.ATd.doPT -> puthead;
          (if E.ATd[] <> none then E.ATd.isPatternDecl -> value if)
       #); 
     pattern::<
       (#
          checkQual::
            (* We come here for assignments of the form
             *    (this Speed) := exp
             * where 
             *    Speed: integer{... }
             *    Point: Value{ ... }
             *    Record: { ... }
             * This pattern must be a value of sub of basic 
             * - the latter may later be desclared as sub of Value
             *)
            (# trace: (# exit false #)
            do (if trace then
                   '**** Pattern:checkQual:\n' -> puttext; dopt ->puttext;
                   '\n**   :isAssign:' -> puttext; isAssign -> putBoolean;
                   ' :srcOS:\n"' -> puttext; srcOS.dopt -> puttext; 
                   '"\n' -> puttext;
                   '**   isValueObj:'->puttext; OG.isValueObj->putBoolean;
                   ' :isPrimitive:' -> puttext; isPrimitive -> putBoolean;
                   newline;
               if);
               (if isAssign then
                   (if true 
                    // OG.isValueObj then 
                       '**** pattern:checkQual:isValueObj:' -> putline;
                       '**   check assignTo: \n' -> puttext;
                       doPT -> putline;
                       '**  from: "' -> puttext; srcOS.doPT -> puttext;
                       '"\n\n' -> puttext
                    // isPrimitive then 
                       '**** pattern:checkQual:isPrimitive:shouldNotHappen' 
                         -> putline;
                    // OG.isBasicValue then 
                       (* this happens - must check if ok! *)
                       (if false then
                           '**** pattern:checkQual:isBasicValue:' -> putline;
                           '**   check assignTo: \n' -> puttext;
                           doPT -> putline;
                           '**  from: "' -> puttext; srcOS.doPT -> puttext;
                           '"\n\n' -> puttext
                       if);
                    // isVirtual then
                       (* May happen for
                        *    foo: {in V:< T} where this(PTN) = in V:< T
                        * and an invocation; foo(TT)
                        * where TT is a binding of V
                        *)
                    else 
                       (* Bad message - should be fixed *)
                       srcOS[] -> SemanticError
                       (#
                       do srcOS.doPT -> msg;
                          ' cannot be assigned to :\n' -> msg;
                          dopt -> msg;
                       #)
                   if)           
               if)
            #);
          checkActualArgs::
            (* theOI is the the ObjectInvocation calling this(Pattern) *)
            (# fArgs: [6] ^Decl; inx,top: @ integer; 
               checkIfSuperIsSig:
                 (# E: ^ObjectSpecification; i: @integer; 
                    OGy: ^ObjectGenerator; ptn: ^Pattern
                 enter(E[],i)
                 do <<SLOT semC7:descriptor>>;
                    (if 'ObjectGenerator' -> (E.label).equal then
                        E[] -> OGy[];
                        (if not OGy.IS.isEmpty then
                            (if fArgs[i].isVirtual then
                                <<SLOT semC8:descriptor>>;
                                fArgs[i][] -> ptn[];
                                (if ptn.theSig.isSig 
                                    and (OGy[]->mkSig->ptn.thesig.equal) 
                                    then
                                    <<SLOT semC9:descriptor>>;
                                    none -> OGy.super.ATd[]; 
                                    none -> OGy.IS.superDesc[];
                    if)if)if)if)
                 #);
               getOGsequenceOfArg:
                 (* foo:
                  *    fArgs[inx] = R: ref T
                  *               = S: obj T { ...}
                  *               = ...
                  * current = exp = ObjectInvocation, const, ...
                  * Given foo: { in R: ref V; W:< T }, and
                  *    X: ...; foo(S,#TT)
                  * we must get qual as if
                  *    X: ...; foo(S,#TT).R; foo(S,#TT).W
                  * This includes computing the OGSequence of the argument
                  * And then calling getVqual
                  *)
                 (# OGs,OGSx: ^OGsequence; orgS: ^Scope;
                    aplOG,dclOG,superOG: ^ObjectGenerator;
                    pn: @integer
                 enter OGS[] (* the savedOGS for theOI *)
                 do (if traceGetVqual then
                        '\n**** theOI: ' -> puttext; theOI.doPT -> putline;
                        ('**   Before: ',0) -> OGS.print;
                    if);
                    theOI.ATd.OG[] -> OGsequence -> OGSx[];
                    OGS[] -> OGSx.pred[];
                    true -> OGSx.isNest;
                    OGSx[] -> OGS[];
                    (if traceGetVqual then ('**   After : ',0) -> OGS.print if);
                    theOI.ATd.OG[] -> aplOG[];
                    (if traceGetVqual then
                        '**** fArgs[inx].origin.label: ' -> puttext;
                        fArgs[inx].origin.label -> puttext; ' ' -> put;
                        fArgs[inx].dopt -> putline;
                    if);
                    (fArgs[inx].origin[]->orgS[]).encOG[] -> dclOG[];
                    
                    (if false and ('Signature'->(fArgs[inx].origin.label).equal) then
                        '**** GotSignature: dclOG: ' -> putline;
                        dclOG.dopt -> puthead;
                        '**   altDclOG: ' -> putline;
                        (# S: ^Scope; dclOGx: ^ObjectGenerator
                        do (fArgs[inx].origin[]->orgS[]).OGofScope -> dclOGx[];
                           dclOGx.dopt -> puthead
                        #)
                    if);
                           
                    aplOG.IS.superDesc.encOG[] -> superOG[];
                    0 -> pn;
                    <<SLOT sem_encOG:descriptor>>;
                    findDclOG:
                      (if aplOG[] = dclOG[] then
                          (if traceGetVqual then
                              '*** FoundDclOG:pn:' -> puttext;
                              pn -> putint; newline;
                          if)
                       else
                          aplOG.IS.superDesc.encOG[] -> aplOG[];
                          (if traceGetVqual then
                              '*** loop:superOG:'->puttext;
                              aplOG.doPT -> puthead;
                          if);
                          pn + 1 -> pn;
                          restart findDclOG
                      if);
                    <<SLOT sem_OGS_pn:descriptor>>;                   
                    (for pn repeat OGS.PRE -> OGS[] for);
                 exit OGS[]
                 #);
               checkForGenericUnit:
                 (* Given M := sqrt(A), 
                  * srcQual = A.qual and dstQual = sqrt.outQual
                  * see decl of arg1Qual in abstractSyntaxTree.bet
                  *)
                 (# current,srcQual,dstQual: ^ObjectGenerator;
                 enter(current[],srcQual[],dstQual[])
                 do (if hasGenericUnit then
                        srcQual[] -> dstQual[];                          
                        srcQual[] 
                          -> (theOI.enclosingObjectGenerator).arg1Qual[];
                        current.getExpUnit 
                            -> (theOI.enclosingObjectGenerator).arg1Unit[]
                    if)
                 exit(srcQual[],dstQual[])
                 #);
               checkForLocalsConflict:
                 (# current,srcQual,dstQual: ^ObjectGenerator;
                 enter(current[],srcQual[],dstQual[])
                 do (origin[]->org[]).scanLocals
                    (# OBS:
                         (#
                         do actualArgs[] -> warning
                            (#
                            do '*** locals conflict:\n' -> msg;
                               current.dopt -> msg;  '\n' -> msg;
                               fArgs[inx].dopt -> msg
                            #)
                         #)
                    do <<SLOT semC6:descriptor>>;
                       (if true
                        // srcQual[] -> current.inSuper then
                           (if not (dstQual[] -> current.inSuper) then obs if)
                        // dstQual[] -> current.inSuper then
                           (if not (srcQual[] -> current.inSuper) then obs if)
                       if)
                    #);
                 #);
               org: ^Items
            do <<SLOT sem_ptn_checkActualArgs:descriptor>>;
               OG.IS.scanNonBoundItemArgs
               (#
               do (if (top + 1 -> top) > fArgs.range then 
                      fArgs.range -> fArgs.extend
                  if);
                  currentArg[] -> fArgs[top][];
               #);            
               0 -> inx;
               actualArgs.scanArgs
               (# E: ^ObjectCall; OGS: ^OGsequence; 
                  srcQual,dstQual: ^ObjectGenerator; 
               do inx + 1 -> inx;
                  (if inx > top then
                      actualArgs[] -> SemanticError
                      (# 
                      do 'More actual parameters:\n\t"' -> msg;
                         actualArgs.doPT -> msg; 
                         '"\n    than formal parameters:\n\t"' -> msg;
                         sig.doPT -> msg; '"' -> msg
                      #)
                   else
                      (current[],inx) -> checkIfSuperIsSig;
                      (* theOI.savedOGS is the OGsequence for theOI
                       * calling this pattern
                       *)
                      <<SLOT sem_ptn_argAtInx:descriptor>>;
                      (* After making getOGsequenceOfArg, the prints in
                       * the trace slots may not be meaningful
                       *)
                      theOI.savedOGS[] -> getOGsequenceOfArg -> OGS[];
                      <<SLOT sem_before_getVqual:descriptor>>;
                      (encOG[],false,OGS[]) 
                        -> fArgs[inx].getVqual 
                        -> (OGS[],dstqual[]);                      
                      <<SLOT semC4:descriptor>>;
                      (if true then
                          (none,theOI.enclosingObjectGenerator)
                            -> current.semcheck -> srcQual[];
                       else
                          (none,encOG[]) -> current.semcheck -> srcQual[];
                      if);
                      (current[],srcQual[],dstQual[])
                        -> checkForGenericUnit -> (srcQual[],dstQual[]);
                      <<SLOT semC5:descriptor>>;
                      (if false then
                          '**** before:checkQual:theOI: ' -> puttext;
                          theOI.label -> puttext; ' ' -> put;
                          theOI.dopt -> putline;
                          '**   ptn: ' -> puttext;
                          dopt -> puthead;
                          '**   vEq:' -> puttext; 
                          OG.primNo = vEq_prim -> putboolean;
                          ' rEq:' -> puttext; 
                          OG.primNo = rEq_prim -> putboolean; newline;
                          '**   fArg:' -> puttext; fARgs[inx].dopt -> putline;
                          '**   aArg:isNone: ' -> puttext; 
                          current.primNo = none_prim  -> putBoolean; newline;
                          current.dopt -> putline;
                      if);
                      (current[],srcQual[],dstQual[]) -> checkForLocalsConflict;
                      (current[],srcQual[],dstQual[],false) 
                        -> fArgs[inx].checkQual;
                  if);
               #); 
               (if (inx < top)  then
                   actualArgs[] -> SemanticError
                   (# 
                   do (if  actualArgs.noOfSons = 0 then
                          'Arguments must be supplied for pattern: "' -> msg;
                          sig.doPT -> msg; '"' -> msg
                       else
                          'The pattern: "' -> msg; sig.doPT -> msg;
                          '"has more formal parameters' -> msg;
                          '\n     than listed as ' -> msg;
                          'actual parameters in the argument list "' -> msg; 
                          actualArgs.doPT -> msg; '" of\n\n"' -> msg;
                          theOI.dopt -> msg; '"' -> msg;
               if)#)if)
            #);
          getVqual::
            (#
            do (if traceGetVqual then
                   '**** Pattern:getVqual: ' -> puttext; sig.doPT -> puttext;
                   ' :isVirtual:'-> puttext; isVirtual-> putBoolean; newline;
                   OGS.print
               if);
               (if isVirtual then
                   (if traceGetVqual then
                       '**   PTN:getVqual:isVirtual:OGS: ' ->putline; OGS.print
                   if);
                   (if true then
                       this(pattern)[] -> OGS.newFindBinding -> qual[];
                    else
                       (this(pattern)[],OGS.getOGstart) -> findBinding -> qual[];
                   if);
                   (if qual.IS.isEmpty then 
                       qual.super.ATd.OG[] -> qual[]
                   if);
                   (if traceGetVqual then
                       '**** PTN:getVqual:gotBinding: ' -> puttext;
                       qual.dopt ->puthead
                   if)
                else
                   OG[] -> qual[]
               if)
            #);
          getQual::
            (# 
            do (if traceGetQual then
                   '*** Pattern:getQual: '-> puttext; sig.doPT -> putline
               if);
               (none,origin[],origin[],true) -> check;
               (if isRecursive then
                   (* For a pattern
                    *    foo(...) -> res: ? foo ...
                    * getQual is called on the foo return value
                    * giving a recursive call of getQual
                    *)
                   (if traceGetQual then
                       '*** isRecursive' -> putline
                   if);
                   OG[] -> qual[];
                   (*false -> isRecursive;*)
                   leave getQual
               if);
               inner;
               (if traceGetQual then
                   'Pattern:getQual:B: "' -> puttext; 
                   sig.doPT -> puttext; '"' ->put; newline
               if);
               true -> isRecursive;
               (* this is probably just a hack 
                * - we need to handle virtuals as in the Beta compiler
                *)
               L:
                 (if qual[] = none then
                     (if isVirtual then
                         (*'*** Virtual: ' -> puttext; dopt -> puttext; 
                          ' on: ' -> puttext; OG.super.on -> putint;
                          ' pn: ' -> puttext; OG.super.pn -> putint; newline;*)
                         OG.getQual -> qual[];
                         (if traceGetQual then
                             'getQual:Virtual: ' -> puttext; 
                             qual.doPT -> puthead;
                             OG.doPT -> puthead
                         if)
                      else
                         (if traceGetQual then
                             'Pattern:getQual:C: ' -> puttext; 
                             sig.dopt -> putline;
                         if);
                         OG.IS.scanAllItemDecls
                         (# DI: ^DataItem
                         do (if currentDcl.isOutArg then
                                (if currentDcl.isDataItem then
                                    (currentDcl[]->DI[]).getQual -> qual[];
                                    leave L;
                                if)
                            if)
                         #);                       
                         OG[] -> qual[];
                 if)if);
               false -> isRecursive;
               (if traceGetQual then
                   'Pattern:getQual:D: "\n' -> puttext; 
                   (if qual[] <> none then 
                       qual.doPT -> puttext
                    else 'none'->puttext
                   if);
                   '"' -> put; newline
               if)
            #);
          semCheck::
            (# lastSuper: ^Exp
            do (if trx then 
                   '**** Pattern:semCheck: '->puttext; sig.dopt -> putline
               if);
               (if ((OG.super.last -> lastSuper[]).args[] <> none) and
                       (lastSuper.args.length = 0) then
                   true -> lastSuper.noArgsCheck
               if);
               (if ptnKind // FinalPtn // FurtherPtn then
                   (if false then
                       '**** PTN:semCheck:binding: ' -> puttext;
                       dopt -> putline;
                       vDcl.dopt -> putline;
                   if);
                   (if vDcl.theSig.isSig and (theSig[]->vDcl.thesig.equal) then
                       (if true then
                           '**** Sub has same sig as super' -> putline;
                           doPT -> putline;
                       if);
                       none -> OG.super.ATd[]; none -> OG.IS.superDesc[]
                    else
                       (if OG.getQual -> (vDcl.OG.getQual).inSuper then
                           (* '**   qual ok'->putline*)
                        else
                           this(Pattern)[] -> SemanticError
                           (#
                           do 'The binding: \n"' -> msg; OG.doPT -> msg;
                              '"\n****of the virtual pattern:\n "' -> msg; 
                              vDcl.doPT -> msg; '"\n'->puttext;
                              '****must be a subpattern of the virtual pattern'
                                -> msg
                           #);
               if)if)if)
            #)
       #);
     ObjectCall::<
       (# semCheck::
            (#
            do (if  traceUnit or trx then
                   '**** ObjectCall:semCheck: ' -> puttext; dopt -> puthead;
               if);
               (rec[],encOG[]) -> super.semCheck -> eOG[];
               true -> done
            #);
          getVqual::
            (#
            do (if trx then
                   '**** ObjectCall:getVqual: ' -> puttext; dopt -> puthead;
               if);
               (if false and (thequal[] <> none) then
                   (if trx then
                       '**** ObjectCall:getVqual:use:theQual:'->putline;
                       theQual.dopt -> puthead
                   if);
                   theQual[] -> qual[];
                else
                   (encOG[],false,OGS[]) -> super.getVqual -> (OGS[],qual[]);
               if)
            #);
          getQual::
            (#
            do (if trx then 'ObjCall:getQual: ' -> puttext; doPT -> putline if);
               super.getQual -> qual[];
            #);
       #);
     ObjectGenerator::<
       (#
          semcheck::
            (# D: ^Decl
            do (if traceGetVqual then
                   '**** OG:semCheck:'->puttext; dopt -> puthead;
                   (if true then
                       '**   encOG: ' -> puttext;
                       (if encOG[] <> none then
                           encOG.doPT -> putHead
                        else
                           'none' -> putline
               if)if)if);
               (if false then
                   (rec[],encOG[]) -> super.semCheck -> eOG[];
                   ('**** OG:semCheck:super:eOG',eOG[]) -> display;
                   (if not IS.isEmpty then 
                       (rec[],this(ObjectGenerator)[]) -> IS.semCheck -> eOG[];
                   if);
                else
                   (rec[],encOG[]) -> super.semCheck -> eOG[];
                   (if traceGetQual then
                       ('**** OG:semCheck:super:eOG',eOG[]) -> display;
                       '**   OG:semCheck:items:'->puttext; dopt -> puthead;
                       (if encOG[] <> none then
                           '**   encOG:'->puttext; encOG.dopt -> puthead;
                       if);
                       (if eOG[] <> none then
                           '**   eOG:'->puttext; eOG.dopt -> puthead;
                   if)if);
                   (if not IS.isEmpty then 
                       (rec[],encOG[]) -> IS.semCheck -> eOG[];
                   if); 
                   (if traceGetVqual then
                       '**** OI:semcheck:IS not empty:eOG: ' -> putline;
                       eOG.dopt -> puthead;
                   if);
                   (if (getReturnDecl -> D[]) <> none then
                       (if traceGetVqual then
                           '**** OG:returnDecl: ' -> puttext; D.dopt -> putline
                       if);
                       D.getQual -> eOG[]
                   if)
               if);
               true -> done;
            #);
          checkNoSingleObj::
            (#
            do (if IS.isEmpty then
                   (*'**** OG:checkNoSingleObj: ' -> puttext; dopt->puthead;*)
                   super.CheckNoSingleObj
               if)
            #);
          getVqual::
            (#
            do (if trx then
                   '**** ObjectGenerator:getVqual: ' -> puttext; 
                   dopt -> puthead
               if);
               (if IS.isEmpty then
                   (encOG[],false,OGS[]) -> super.getVqual -> (OGS[],qual[]);
                else
                   this(ObjectGenerator)[] -> qual[]
               if)
            #);
          getQual::
            (# 
            do (if traceGetQual then
                   '*** ObjectGenerator:getQual:primNo: '->puttext; 
                   primNo -> putint; ' '-> put; 
                   IS.primNo -> putint; ' '-> put; 
                   (if super.ATd[] <> none then
                       super.Atd.primNo -> putint; ' '-> put; 
                   if);
                   ':isPrimitive: ' -> puttext; isPrimitive -> putboolean;
                   ':' -> put; doPT -> putline
               if);
               (* Should only be called for singular OGs
                * If part of pattern decl, argScope may be wrong
                *)
               (none,origin[],origin[],true) -> check;
               (if IS.isEmpty then
                   (* super (Invocation) may be object or pattern *)
                   super.getQual -> qual[]
                   (* qual = E3.ATd.getQual *)
                else
                   (* is Singular *)
                   L:  IS.scanAllItemDecls
                     (# DI: ^DataItem
                     do (if currentDcl.isOutArg then
                            (if currentDcl.isDAtaItem then
                                (currentDcl[] -> DI[]).getQual -> qual[];
                                leave L
                            if)
                        if)
                     #);
                   (if qual[] = none then
                       this(ObjectGenerator)[] -> qual[]
                   if)
               if);
               (*'qual:' -> putline; qual.dopT -> putline;*)
            #); 
          XinSuper: BooleanValue
            (# lQ: ^ObjectGenerator
            enter lQ[]
            do ('inSuper:lQ',lQ[]) -> display;
               ('rQ',lQ[]) -> display;
               L:
                 (if lQ[] = this(ObjectGenerator)[] then
                     true -> value 
                  else
                     (if (lQ[] <> none) then
                         (if (lQ.primNo <> object_prim) then
                             lQ.super.getQual -> lQ[];
                             (if lQ[] =  none then
                                 objectDecl.getQual -> lQ[]
                             if);
                             restart L;
                 if)if)if)
            #);
       #);
     Items::<
       (# semCheck::
            (# checkInterfaceRestrictions:
                 (* This decl: must be a pattern (or virtual)
                  *    foo: entry(# do ... #)
                  * where entry in interface.rQual
                  * We also need to handle several levels
                  *    M0: (# foo: Q(# ... #); ... #)
                  *    M1: M0(# [interface[E]] E: (# ... #); ... #)
                  *    M2: M1(# [interface[F]] F: (# ... #); ... #)
                  *    M3: M2(# ... #)
                  * Do M1/M2-restrictions apply to M0?
                  * M2 further restricts subpatterns ...
                  * Methods in M2 are restricted by M1 but not M2
                  * For x: ^M3
                  *   x.bar -- if bar in M3 then all restrictions
                  *                   in M2, M1 restrictions
                  *            else no restrictions
                  *            if virtual, then restrictions at point of
                  *            original declaration apply
                  *            Probably different if argument restrictions 
                  *            are added
                  * THE ABOVE IS NOT IMPLEMENTED
                  *)
                 (# 
                 do (if false then
                        '**** checkInterface: ' -> puttext; 
                        (if sig[] <> none then sig.dopt -> putline if);
                    if);
                    (if superDesc[] <> none then
                        (if true -> hasInterfaceRestrictions then
                            (if false then
                                '**** Interface restrictions:\n' -> puttext;
                            if);
                            scanDecls
                            (#
                            do (if currentDcl.isPattern then
                                   L:
                                     (#
                                     do scanInterface
                                        (# 
                                        do (if false then
                                               '**** check:\n' -> puttext;
                                               currentDcl.dopt -> puttext;
                                               '\n**   isSubOf:\n' -> puttext;
                                               current.dopt -> putline;
                                               current.ATd.OG.dopt -> puthead
                                           if);
                                           (if currentDcl.OG[] -> current.ATd.OG.inSuper then
                                               leave L
                                        if)#);
                                        currentDcl[] -> SemanticError
                                        (#
                                        do 'The pattern: "' -> msg;
                                           currentDcl.sig.dopt -> msg;
                                           '" must be a subppattern of: '
                                             -> msg;
                                           scanInterface
                                           (# i: @integer
                                           do (if (i +1 -> i) > 1 then ' or ' -> msg if);
                                          '"' -> msg;
                                          current.dopt ->msg; '"' -> msg
                    #)#)#)if)#)if)if)
                 #);               
               checkArgumentRestrictions:
                 (# nl: @boolean
                 do scanAllDecls
                    (# constraintsOK: @boolean
                    do (if currentDcl.isInArg then
                           true -> constraintsOK;  
                           (if newBasicProp.hasArguments then
                               (*'**** new prop:arguments: ' -> puttext;*)
                               (*newBasicProp.arguments.doPT -> putline;*)
                               L: newBasicProp.scanArguments
                                 (# 
                                 do current.scanSons
                                    (# OI: ^ObjectInvocation
                                    do current[] -> OI[];
                                       (if false then
                                           '\n**** checkArg: ' -> puttext; 
                                           currentDcl.dopt -> puttext;
                                           ' is: ' -> puttext; 
                                           current.dopt -> putline; 
                                       if);
                                       false -> constraintsOK;
                                       (if true
                                        // 'immutable' -> OI.ptnId.T.equalNCS then
                                           (if false then
                                               '** isImmutable?' -> puttext;
                                           if);
                                           (if currentDcl.isImmutable then
                                               (if false then
                                                   'Yes:immutable: ' -> puttext; 
                                                   currentDcl.doPT -> putline;
                                               if);
                                               true -> constraintsOK;
                                               leave L
                                            else
                                               (* need arguments[value] *)
                                               (if currentDcl.OG.isValueObj then
                                                   (if false then
                                                       'Yes:immutable value: '
                                                         -> puttext;
                                                       currentDcl.doPT -> putline;
                                                   if);
                                                   true -> constraintsOK;
                                                   leave L
                                               if)
                                           if);
                                        // 'unique' -> OI.ptnId.T.equalNCS then
                                           (if currentDcl.OG.IS.isUnique then
                                               true -> constraintsOK;
                                               leave L
                                           if)
                                        else
                                           (if false then
                                               '**  match: %argument:\n'
                                                 -> puttext;
                                               OI.ATd.OG.dopt ->putline;
                                               '**  with:dcl:\n' ->puttext;
                                               (currentDcl.desc).dopt->putline;
                                           if);
                                           (if (currentDcl.desc).father[]
                                                 -> OI.ATd.OG.inSuper
                                                 -> constraintsOk then
                                               leave L
                                           if)
                                           (* not yet complete! *)
                                    if)#);
                                 #);
                               (if not constraintsOK then
                                   currentDcl[] -> Warning
                                   (#
                                   do 'Constraints not satisfied in: "' -> msg;
                                      currentDcl.dopt -> msg;
                                      '"\n     must be one of: ' -> msg; 
                                      newBasicProp.scanArguments
                                      (# i: @integer
                                      do (if (i + 1 -> i) > 1 then ' or ' -> msg if);
                                         current.doPT -> qmsg; 
                                      #);
                                      '\n'->msg
                                   #)
                               if);
                       if)if)
                    #);
                 #);               
               checkImmutability:
                 (#
                 do (if newBasicProp.immutable and
                        (super.ATd[] <> none) and
                        not superDesc.isImmutable then 
                        this(items)[] -> semanticError
                        (#
                        do 'An objectGenerator cannot be marked "immutable"'
                             -> msg;
                           '\n     unless its superpattern is immutable'
                             -> msg
                        #)
                    if)
                 #)
            do (if trx then '**** Items:semCheck: ' -> putline if);
               this(Items).encOG[] -> encOG[] -> eOG[]; 
               (* perhaps a bad idea to use the same name here, encOG!? *)
               none -> rec[];
               checkArgumentRestrictions;
               checkInterfaceRestrictions;
               checkImmutability;
               scanSons
                   (# 
                   do (none,eOG[]) -> current.semCheck;
                      current[] -> rec[]
                   #);
               scanSons(#do current.checkNoSingleObj #);
               true -> done
            #);
          hasInterfaceRestrictions: BooleanValue
            (# isFirst: @boolean
            enter isFirst
            do (if superDesc[] <> none then
                   false -> superDesc.hasInterfaceRestrictions -> value
               if);
               (if not value and not isFirst then
                   (if not (newBasicProp.hasIFR -> value) then

               if)if)
            #);
          scanInterface:
            (# current: ^Lexem(*Property*)
            do (if superDesc[] <> none then
                   &superDesc.scanInterface
                   (#
                   do current[] -> this(scanInterface).current[];
                      inner scanInterface
                   #);
                   (if newBasicProp.hasIFR then
                       newBasicProp.interface.args.scanSons
                       (# INV: ^Invocation; N: ^Name; OI: ^ObjectInvocation
                       do (if false then
                              '** arg: ' -> puttext; current.dopt -> putline;
                          if);
                          current.scanSons
                          (#do (if false then current.label -> putline; if);
                          (if current## = ObjectInvocation## then
                              current[] -> OI[]; OI.ATd[] -> OI.ptnId.ATd[];
                              OI.ptnId[] -> this(scanInterface).current[];
                              inner scanInterface
                          if)#)
                       #);
                    if)
            if)#);
          scanLocals:
            (# current: ^ObjectGenerator; org: ^Items
            do (*'\n*** scanLocals:'->putline; dopt -> puthead;*)
               (if (superDesc[] <> none ) and (superDesc[] <> objectDecl.Desc) then 
                   &superDesc.scanLocals
                   (#
                   do
                      current[] -> this(scanLocals).current[]; 
                      inner scanLocals
               #);if);
               (if newBasicProp.locals[] <> none then
                   newBasicProp.locals.args.scanSons
                   (# inv: ^Invocation
                   do (if false then
                          '** local: ' -> puttext; 
                          current.label -> puttext;
                          ' ' -> put; current.dopt -> putline;
                      if);
                      (current[]->INV[]).ATd.OG[] 
                        -> this(scanLocals).current[];
                      inner scanLocals
                   #)                   
               if);
               (if (origin[]->org[]) <> none then
                   &org.scanLocals
                   (#
                   do current[] -> this(scanLocals).current[]; 
                      inner scanLocals
                   #)
               if)
            #)          
       #);
     Invocation::<
       (# semcheck::
            (# noneMet: @Boolean
            do (if traceGetVqual then
                   '**** Invocation:semCheck: '-> puttext; dopt -> putline;
                   (if true then
                       '**   encOG:' -> puttext;
                       (if encOG[] <> none then
                           encOG.doPT -> puthead
                        else
                           'none' -> putline
                   if)if)
               if);
               (*none -> rec[]*)
               scanSons
               (# OI: ^ObjectInvocation
               do (if not isLast and (current## = objectInvocation##) then
                      current[] -> OI[];
                      (if OI.ATd.OG.primNo = none_prim then
                          true -> noneMet;
                      if)
                   else
                      (if noneMet then
                          current[] -> OI[]; (* is this always an OI? *)
                          (if OI.ATd.OG.primNo <> rEq_prim then
                              this(Invocation)[] -> semanticError
                              (#
                              do '"none" in "' -> msg;
                                 dopt -> msg; '"'  -> msg;
                                 ' cannot access an attribute' ->  msg
                      #)if)if)
                  if);
                  (rec[],encOG[]) -> current.semCheck -> eOG[];
                  current[] -> rec[];
               #);
               true -> done
               (*(if showUnits then printUnits if);*)
            #);
          checkNoSingleObj::
            (#
            do (if noOfSons = 1 then
                   (* '**** INV:checkNoSingleObj: '->puttext; 
                    * noOfSons -> putint; ' ' -> put;
                    * dopt -> putline;
                    *)
                   scanSons(#do current.CheckNoSingleObj #)
               if)
            #);
          getVqual::
            (#
            do (if traceGetVqual then
                   '**** INV:getVqual:'->puttext; dopt -> puthead;
                   '**   OGs: ' -> puttext;
                   (if OGs[] <> none then OGs.print else 'none'->putline if)
               if);
               encOG[] -> qual[];
               scanSons
               (#
               do (qual[],not isFirst,OGS[]) 
                    -> current.getVqual -> (OGS[],qual[])
               #) 
            #);
          getQual::
            (# E: ^Exp; 
            do (if trx then 'IV:getQual:' -> puttext; doPT -> putline if);
               scanSons(# do current[] -> E[] #);
               ('Invocation:getQual:E:',E[]) -> display;
               (if E[] <> none then
                   E.getQual -> qual[]
               if);
            #);
          getActualReceiver::
            (#
            do (last).getActualReceiver -> rec[]
            #)
       #);

     ObjectInvocation::<
       (# semCheck::
            (# checkIfReceiverInImmutable:
                 (# org: ^Scope(*Items*);
                 do (if receiver[] = none then 
                        '**  receiver is none' -> puttext; ' ' -> put;
                        dopt ->putline;
                        leave checkIfReceiverInImmutable;
                    if);
                    receiver.ATd.OG.IS.origin[] -> org[];
                    (if org.isImmutable and 
                        ((receiver.on > 0) or (receiver.father.noOfSons > 2)) then
                        (* receiver, X, declared as in: 
                         *  MyPtn: ...
                         *    [immutable]
                         *    X: var integer
                         *    foo:
                         *       X:= 117 -- illegal, X.on > 0
                         *    X: = 118 -- legal X.on = 0
                         *   R: obj MyPtn
                         *   R.X := 119 -- illegal receiver.father.noOfSons > 2
                         *        -- father is invocation with more than 2 sons
                         *        -- this is a fragile test, should be improved
                         *)
                        (if false then
                            '**** dst is in immutable OG ,rec: ' -> puttext;
                            rec.dopt -> putline;
                            (rec.enclosingObjectGenerator).dopt -> putline;
                        if);
                        this(ObjectInvocation)[] -> Warning
                        (#
                        do 'The data-item "' -> msg; rec.dopt -> msg;
                           '" is in an immutable object and cannot be assigned'
                             -> msg
                        #)
                    if)
                 #);               
               receiver: ^Exp; ptn: ^Pattern; org: ^Items; 
               srcQual, dstQual,qual: ^ObjectGenerator; 
               U: ^text;N: ^Node; OGS: ^OGsequence; (*P: ^OG path; *)
               MI: ^ModuleItem; singleUnitArgQual: ^ObjectGenerator
            do (* rec = none if first OI in Invocation *) 
               <<SLOT sem_OI_semCheck_T1:descriptor>>;
               (if ATd[] = none then
                   '!!!! OI:ATd:none:X:' -> putline;
                   dopt -> putline;
                   father.father.father.dopt -> putline
               if);
               (if ATd.primNo // rAssign_prim // vAssign_prim then
                   (* rec = receiver is defined *)
                   (if trx then 
                       '**** assign: ' -> puttext; rec.dopt -> putline;
                       '**   OI: ' -> putline; dopt -> putline;
                       (*'**   encOG: ' -> putline;
                        encOG.doPT -> putline*)
                       (*rec.origin.dopt -> putline*)
                   if);
                   rec[] -> receiver[];                   
                   checkIfReceiverInImmutable;
                   
                   (if receiver.ATd.primNo = this_prim then
                       '**   this:true'->putline
                   if);
                   args.scanArgs
                   (# i: @integer; org: ^Items;
                      srcQual,dstQual,srcQual1,dstQual1: ^ObjectGenerator
                   do (if (i + 1 -> i) > 1 then
                          args[] -> semanticError
                          (#
                          do 'List too long to be assigned to: "'->puttext;
                             receiver.doPT -> puttext; '"' -> putline
                          #)
                      if);
                      (* check that current can be assigned to receiver  *)
                      (receiver[](*none*),EnclosingOGwithIS)
                        -> current.semCheck 
                        -> srcQual[];
                      (*current.getQual -> srcQual[];*)
                      receiver.getQual -> dstQual[];
                      (if trx then
                          '**   src: ' -> puttext; current.dopt->putline;
                          '**   srcQual: '->putline;srcQual.dopt->puthead;
                          '**   dst: ' -> puttext; receiver.dopt->putline;
                          '**   dstQual: ' -> putline; 
                          dstQual.dopt -> puthead
                      if);                      
                      receiver.getActualReceiver -> receiver[];
                      (origin[]->org[]).scanLocals
                      (# OBS:
                           (#
                           do this(ObjectInvocation)[] -> warning
                              (#
                              do '*** locals conflict:\n' -> msg;
                                 current.dopt -> msg;  '\n' -> msg;
                                 receiver.dopt -> msg
                              #)
                           #)
                      do (if false then
                             'got:' -> puttext; current.dopt -> putline;
                             '** src:'->putline;
                             srcQual.dopt -> puthead;
                             '** dst: ' -> putline; dstQual.dopt ->puthead
                         if);
                         (if true
                          // srcQual[] -> current.inSuper then
                             (if not ( dstQual[] -> current.inSuper) then
                                 obs
                             if)
                          // dstQual[] -> current.inSuper then
                             (if not ( srcQual[] -> current.inSuper) then
                                 obs
                             if)
                         if)
                      #);
                      (current[],srcQual[],dstQual[],true) 
                        -> receiver.ATd.checkQual; 
                      
                      (if trx then
                          '**** after receiver.ATd.checkQual: ' ->puttext;
                          receiver.dopt -> putline;
                      if);
                      enclosingOGwithIS -> encOG[] -> eOG[];
                      true -> done
                   #)
                else 
                   (if ATd.primno
                    // inner_prim // restart_prim // leave_prim 
                    // indexedPut_prim then
                       true -> noArgsCheck
                    // ifThen_prim then (* Why dont we check here ? *)
                    // this_prim then
                       args.scanArgs
                       (#
                       do current.getQual -> srcQual[] -> encOG[] ->eOG[];
                          true -> done
                       #)
                    else
                       (if (1 -> ptnId.T.inxGet) = '#' then
                           (* clean-up '#PTN: '->puttext; 
                            * ptnId.T[] -> putline*)
                        else
                           (if not noArgsCheck then
                               (if false and (ATd.primNo = resume_prim) then
                                   (*'**** resume: ' -> puttext;
                                   dopt -> putline;*)
                                   (if (rec[]->receiver[]) <> none then
                                       (*'receiver: ' -> puttext;
                                       rec.dopt -> putline*)
                                   if);
                                   (args[],encOG[],this(ObjectInvocation)[])
                                     -> receiver.ATd.checkActualArgs
                                     -> singleUnitArgQual[];
                                else
                                   (if traceGetVqual then
                                       '**** OI:before:checkActualArgs:'
                                         -> puttext;
                                       dopt ->  puthead;
                                       '**   ATd.OG.primNo=none_prim: ' 
                                         -> puttext;
                                       ATd.OG.primNo = none_prim -> putboolean;
                                       ' args: "'-> puttext; 
                                       args.dopt -> puttext;
                                       '" noOfSons: ' -> puttext; 
                                       args.noOfSons -> putint;
                                       newline;
                                       '**   encOG:'->puttext;
                                       encOG.dopt -> puthead;
                                   if);
                                   (args[],encOG[],this(ObjectInvocation)[]) 
                                     -> ATd.checkActualArgs
                                     -> singleUnitArgQual[];
                               if);
                               (if (singleUnitArgQual[] <> none) then
                                   '**** OI: '->puttext; dopt -> puthead;
                                   '**   hasSingleUnitArg:Qual:'->puttext;
                                   singleUnitArgQual.doPT -> putline
                               if);
                               true -> done
                       if)if);
                       (if (origin[] <> none) then
                           (if trx and ((savedOGS[]->OGS[]) <> none) then
                               '**** OI:getVqual:computeQual:OF:'->putline;
                               dopt -> puthead;
                               '**   ATd:' -> puttext; Atd.dopt->puthead;
                               '**   isVirtual: '->puttext;
                               ATd.isVirtual-> putboolean; newline;
                               '**** OI:getVqual:theQual:'->putline;
                               (if theQual[] <> none then
                                   theQual.dopt -> puthead;
                                else
                                   'none' -> putline
                               if);
                               '**** OI:getVQual:savedOGS:'->putline;
                               savedOGS.print
                           if);
                           (if theQual[] <> none then
                               (* is theQual always correct for declarations?*)
                               theQual[] -> qual[]
                            else
                               '\n!!!!! no saved qual:'->puttext;
                               dopt -> puthead
                           if);
                           (if (singleUnitArgQual[] <> none) and 
                               (qual[] <> singleUnitArgQual[]) then
                               '****OI:changedQualToSingleUnitArgQual:'
                                 ->putline;
                               singleUnitArgQual.doPT -> putline;
                               singleUnitArgQual[] -> qual[]
                           if);
                           qual[] -> encOG[] -> eOG[];
                           (if trx then
                               '**   OI:semCheck:qualOf: ' -> puttext;
                               dopt -> puthead;
                               '**   qual:\n'-> puttext; qual[] -> putDoPt;
                               '**   hasSingleUnitArg:qual:' -> putline;
                               (if singleUnitArgQual[] <> none then
                                   singleUnitArgQual.doPT -> putline
                                else
                                   ' none' -> putline
               if)if)if)if)if)
            #);
          checkNoSingleObj::
            (# checkATdIsPtn:
                 (#
                 do (if not ATd.isPattern then
                        this(ObjectInvocation)[] -> semanticError
                        (#
                        do '"' -> msg; ptnID.doPT -> msg;
                           '" must be a pattern' -> msg
                        #)
                    if)
                 #);
            do (*'**** OI:checkNoSingleObj: ' -> puttext; dopt -> puthead;*)
               checkATdisPtn
            #);
          viaInclude:
            (* X via %include C via %include B via %include A
             * means: A.B.C.X, path = [A,nest(B),nest(C),nest(X)]
             *)
            (# OI,OIx: ^ObjectInvocation; on,pn,onx: @integer; 
               OGS,OGSx: ^OGsequence;
               MI: ^ModuleItem; encOG,OGx: ^ObjectGenerator
            enter(encOG[],OI[],on,pn,OGS[])
            do (if trx then
                   '**** OI:viaInclude:on=' -> puttext; on->putint; ' ' ->put;
                   (if ptnID.viaIncl[] <> none then
                       ptnID.viaIncl.dopt -> puttext; ' ' -> put;
                       ptnID.viaIncl.father.dopt -> putline
                    else 
                       'top'->putline
                   if);
                   '**   OI: '->puttext; OI.dopt -> puthead;
                   '**   encOG: '->putline;
                   encOG.doPT -> puthead;
                   (if OGS[] <> none then OGS.print if)
               if);
               (if ptnId.viaIncl[] = none then
                   (* In X via %include C; via %include B; via %include A
                    * we have reached A,
                    * Fall back to B and C
                    *)
                   (if trx then
                       '**** viaInclude:top:on='->puttext; on -> putint;
                       newline;
                       (if OGS[] <> none then OGS.print if)
                   if);
                   (*(origin.father[],ATd[],this(OBjectInvocation)[],on,pn,P[])
                    -> OG path (*-> ATd.pathToDcl* ) -> P[];*)
                   (*P.print;*)
                   (if OGS[] = none then
                       encOG[] -> OGsequence -> OGSx[];
                       OGS[] -> OGSx.pred[];
                       OGSx[] -> OGS[];
                   if);
                   (for on repeat OGS.ENC -> OGS[] for);
                   (for pn repeat OGS.PRE -> OGS[] for);
                   (if trx then
                       '**** viaInclude:GOT:'->putline;
                       OGS.print
                   if);
                   OGS.DOG[] -> encOG[] (* ???*)
                else
                   (if trx then
                       '**** viaInclude:A: ' -> puttext; 
                       ptnId.viaIncl.father.dopt -> putline
                   if);
                   ptnId.viaIncl.scanSons
                   (# i: @integer
                   do (if (i + 1 -> i) > 1 then
                          (if toFix then
                              '!!!! FIX:ObjectInvocation:viaInclude: '
                                -> puttext;
                              'remote not handled! ' -> putline;
                              ptnID.viaIncl.dopt -> putline
                      if)if);
                      current[] -> OIx[];
                   #);
                   OIx.ptnId.on + 1 -> onx;
                   (if trx then
                       '**** viaInclude:B: '->puttext; OIx.dopt->puttext;
                       ' current.ptnId.on=' ->puttext; onx -> putint; 
                       newline;
                   if);
                   (* In X via %include C via %include B via %include A
                    * go via %include B and/or %include A
                    *)
                   (encOG[],OI[],on + onx,pn,OGS[]) 
                     -> OIx.viaInclude 
                     -> (encOG[],OGS[]);
                   (if trx then
                       '*** add OIx:'->puttext; OIx.dopt -> putline;
                   if);
                   (if OIx.ATd.isModuleItem then
                       (OIx.ATd[] -> MI[]).MD.OG[] -> OGx[];
                    else
                       OIx.ATd.OG[] -> OGx[]
                   if);
                   ATd.origin.father[] -> OGsequence -> OGSx[];
                   OGS[] -> OGSx.pred[];
                   true -> OGSx.isNest;
                   OGSx[] -> OGS[];
                   (for pn repeat OGS.PRE -> OGS[] for);
               if)
            exit(encOG[],OGS[])
            #);
          gettingReturnQual: @boolean;

          getVqual::
            (# mark:
                 (# n: @integer
                 enter n <<SLOT sem_OI_getVq_mark:doPart>>
                 #);
               markENC: mark(#do 'ENC:on='->puttext #);
               markPRE: mark(#do 'PRE:pn='->puttext #);
               OGSx,OGSy: ^OGsequence; OGx: ^ObjectGenerator; DI: ^Decl;
               U,T: ^Text
            do <<SLOT sem_OI_getVq:descriptor>>;
               (*dopt -> T[];*)
               (if ptnID.viaIncl[] <> none then
                   (* Given X, via %include C, path(X) to C is (on,pn), *)
                   <<SLOT sem_OI_getVq_include:descriptor>>;
                   (encOG[],this(ObjectInvocation)[],on,pn,OGS[]) 
                     -> viaInclude 
                     -> (encOG[],OGS[]);
                   (encOG[],true,OGS[]) -> ATd.getVqual -> (OGSx[],qual[]);
               else
                   (if (OGS[] -> OGSx[]) = none then
                       encOG[] -> OGx[];

                       (for on repeat 
                            OGx.origin.father[] -> OGx[]
                       for);
                       OGx[] -> OGsequence -> OGSx[];
                       (for pn repeat OGSx.PRE -> OGSx[] for);
                       <<SLOT sem_OI_getVq_initial:descriptor>>;
                       (encOG[],false,OGSx[]) -> ATd.getVqual -> (OGSx[],qual[]);
                       <<SLOT sem_OI_getVq_afterATd:descriptor>>;
                    else
                       (if isNest then
                           (# OGSz: ^OGsequence; i: @integer
                           do OGSx[] -> OGSz[];
                              loop:
                                (* encOG is a binding in one of the DOGs in OGSx
                                 * we seartch backward for the DOG having the 
                                 * binding
                                 *)
                                (# 
                                do i + 1 -> i;
                                   (if OGSz[] <> none then
                                       (if encOG[] = none then
                                          leave loop
                                       if);
                                       (if traceGetVqual then
                                           '**   OI:getVqual:findEnc:OGSz:'
                                             -> putline;
                                           OGSz.print;
                                           '**   encOG:'->puttext;
                                           encOG.dopt -> puthead;(*
                                           encOG.origin.father.dopt -> puthead;
                                           '**   OGZs.DOG:' -> puttext;
                                           OGSz.DOG.dopt -> puthead;*)
                                       if); 
                                       (if (encOG.origin.father[] = OGSz.DOG[])
                                           or OGSz.hasBinding then
                                           (if traceGetVqual and (i > 1) then
                                               '***** Bingo:' -> putline;
                                               OGSz.print;
                                           if);
                                           OGSz[] -> OGSx[];
                                           leave loop
                                       if);
                                       OGSz.pred[] -> OGSz[];
                                       restart loop
                                   if)
                                #)
                           #);
                           encOG[] -> OGsequence -> OGSy[];
                           true -> OGSy.isNest;
                           OGSx[] -> OGSy.pred[];
                           OGSy[] -> OGSx[];
                           <<SLOT sem_OI_getVq_isNest:descriptor>>;
                       if);
                       (* isNest ==> on = 0, 
                        * otherwise on >= 0, we may come from DataItem:getVqual 
                        *)
                       (for i: on repeat OGSx.ENC -> OGSx[]; i -> markENC for);
                       (for i: pn repeat OGSx.PRE -> OGSx[]; i -> markPRE for);
                       <<SLOT sem_OI_getVq_B2:descriptor>>;
                       (encOG[],false,OGSx[]) -> ATd.getVqual -> (OGSx[],qual[]);
                       <<SLOT sem_OI_getVq_cont_afterATd:descriptor>>;
               if)if);
               (# IT: ^node; wasSaved: @boolean
               do father.father.father[] -> IT[];
                  (if (savedOGS[] <> none) and not IT.isDecl then
                      true -> wasSaved
                  else
                      OGSx[] -> OGS[] -> savedOGS[]; 
                      this(ObjectInvocation)[] -> savedOI[];
                  if);
                  <<SLOT sem_OI_getVq_savedOGS:descriptor>>;
               #);
               (if (qual.getReturnDecl -> DI[]) <> none then
                   (if gettingReturnQual then
                       (* Find out if the correct qual/OGS is returned *)
                       leave getVqual
                   if);
                   true -> gettingReturnQual;
                   qual[] -> OGsequence -> OGSy[];
                   OGSx[] -> OGSy.pred[]; true -> OGSy.isNest;
                   (encOG[],false,OGSy[]) -> DI.getVqual -> (OGSx[],qual[]);
                   <<SLOT sem_OI_getVq_return:descriptor>>;
                   false -> gettingReturnQual;
               if); 
               qual[] -> theQual[];
               (if (theQual.getExpUnit (*UnitX*) -> U[]) <> none then
                   U[] -> ExpUnit[];
               if);
               OGSx[] -> OGS[]
            #);
          getQual::
            (#
            do (if (trx) or traceGetQual then
                   '**** ObjectInvocation:getQual: ' -> puttext;
                   dopt->puthead
               if);
               (if (ATd[] <> none) and (ATd.primNo = this_Prim) then
                   (* move to Apl:getQual*)
                   args.scanArgs
                   (# IV: ^ObjectSpecification
                   do current[] -> IV[]; IV.getQual -> qual[];
                      (* IV.doPT -> putline; qual.doPT -> putline*)
                   #);
                   ('Keyword:getQual:this_prim', qual[])->display;
               if);
               (if ATd[] <> none then
                   (if ATd.isVirtual then
                       ATd.getQual -> qual[]
                    else
                       ATd.getQual -> qual[]
                   if)
                else
                   '*** ATd is none ' -> putline
               if);
               (if trx or traceGetQual then
                   (if qual[] <> none then
                       '**   OI:getQual:QUAL:IS: ' -> putline;
                       qual.doPT -> puthead
                    else
                       '*** qual is none' -> putline
                   if)
                   
               if)
            #);      
          getActualReceiver::
            (#
            do (if ATD.primNo = this_prim then
                   args.scanSons
                   (# i: @integer; OG: ^ObjectCall
                   do (if (i + 1 -> i) = 2 then
                          (*'** primArg:'->puttext; current.doPT -> putline;*)
                          ((current[] -> OG[]).super).last -> rec[]
                      if)
                   #)
               if)
            #)
       #);
     IfThen::<
       (# semCheck::
            (#
            do (if false then
                   '**** IfThen:semCheck: ' -> puttext; doPT -> puthead
               if);
            #)
       #);
     IfThenElse::<
       (# semCheck::
            (#
            do (if false then
                   '**** IfThenElse:semCheck: ' -> puttext; doPT -> puthead
               if);
            #)
       #);
     BracketedExp::<
       (# semCheck::
            (# E: ^Exp
            do (if trx then
                   '**** BracketedExp:semCheck: ' -> puttext;
                   dopt -> putline;
               if);
               (if false and (noOfSons > 1) then
                   '**   got Indexed literal! ' -> puttext; doPt -> putline;
                   ('receiver',rec[]) -> putPT; 
                   (if rec[] <> none then rec.label -> putline; if)
               if);
               (if false and (rec[] <> none) 
                   and ((rec[]->E[]).ATd[] <> none)
                   and (E.ATd.OG.primno = indexed_prim) then
                   '**** receiver is Indexed: '->puttext; doPT -> putline;
                   ('receiver',rec[]) -> putPT; 
               if)
            #);
          getQual::
            (# 
            do IV.getQual -> qual[] 
            #);
          getActualReceiver::
            (#
            do IV.getActualReceiver -> rec[];
            #);
       #);
     Const::<
       (# semcheck::
            (#
            do (if trx then
                   '**** IntegerObj:semcheck:' -> puttext; dopt -> putline
               if);
               (if (unit[] <> none) and (unit.T[] <> none) then
                   (if false then
                       '**** Const:semcheck: ' -> puttext; dopt->putline
                   if);
                   unit.T[] -> expUnit[] -> unitTable.findUOGall -> encOG[] -> eOG[];
                   (if eOG[] = none then
                       '!!!! Const:findUOG is none: '->puttext;
                       dopt -> putline;
                   if)
                else
                   integerDecl.OG[] -> encOG[] -> eOG[];
               if)
            #);
          getVqual::
            (#
            do (if (unit[] <> none) and (unit.T[] <> none) then
                   (if false then
                       '**** Const:getVqual: ' -> puttext; dopt->putline
                   if);
                   unit.T[] -> expUnit[] -> unitTable.findUOGall -> qual[];
                   (if qual[] = none then
                       '!!!! Const:findUOG is none: '->puttext;
                       dopt -> putline;
                   if)
                else
                   integerDecl.OG[] -> qual[]
               if)
            #);
          getQual:: 
            (#
            do (if trx then
                   '**** Const:getQual: ' -> puttext; dopt -> putline
               if);
               integerDecl.getQual -> qual[]
            #);
       #);
     FloatConst::<
       (# semCheck:: 
            (# 
            do (if (unit[] <> none) and (unit.T[] <> none) then
                   (if false then
                       '**** FloatConst:semcheck: ' -> puttext; dopt->putline
                   if);
                   unit.T[] -> unitTable.findUOGall -> encOG[] -> eOG[];
                   (if eOG[] = none then
                       '!!!!floatConst:findUOG is none: '->puttext;
                       dopt -> putline;
                   if)
                else
                   floatDecl.OG[] -> encOG[] -> eOG[] 
               if)
            #);
          getVqual::
            (#
            do (if (unit[] <> none) and (unit.T[] <> none) then
                   (if false then
                       '**   flotConst:getqual: ' -> puttext; dopt -> putline;
                   if);
                   unit.T[] -> expUnit[] -> unitTable.findUOG -> qual[]
                else
                   floatDecl.OG[] -> qual[] 
               if)
            #);
          getQual:: 
            (# 
            do (if (unit[] <> none) and (unit.T[] <> none) then
                   (if false then
                       '**   flotConst:getqual: ' -> puttext; dopt -> putline;
                   if);
                   unit.T[] -> unitTable.findUOG -> qual[]
                else
                   floatDecl.getQual -> qual[] 
               if)
            #);
       #);
     CharObj::<
       (# semcheck::
            (#
            do (if false then
                   '**** CharObj:semcheck:' -> puttext; dopt -> putline
               if);
               charDecl.OG[] -> encOG[] -> eOG[]
            #);
          getVqual:: (#do charDecl.OG[] -> qual[] #);           
          getQual:: (#do charDecl.getQual -> qual[] #); 
       #);
     StringObj::< 
       (# semcheck::
            (#
            do (if false then
                   '**** StringObj:semcheck:' -> puttext; dopt -> putline
               if);
               stringDecl.OG[] -> encOG[] -> eOG[]
            #);
          getVqual:: (#do stringDecl.OG[] -> qual[] #);           
          getQual:: (#do stringDecl.getQual -> qual[] #); 
       #);
     decodeUnits:
       (# UT: ^UnitTree; BV: ^Basevector
       do (if false then
              '**** decodeUnits:'->putline;
          if);
          unitTable.scan
          (#
          do (if false then current.print; if);
             (for i: current.UStop repeat 
                  (if false then
                      '**   parse: ' -> puttext;
                      current.US[i][] -> putline;
                  if);
                  current.US[i][] -> parseUnit -> UT[];
                  UT[] -> mkBasevector  -> BV[];
                  (if false then
                      '**   parsed: ' -> puttext; true -> UT.print -> putline;
                      BV.print
                  if)
             for)
          #);
       #)
  do '**** SEMCHECK ****' -> putline;
     decodeUnits;
     (*main.defUnit;*)
     main (*rootModule*).semCheck;

     (if semanticErrors then
         '\n***** Semantic errors: compilation stops' -> putline
      else
         (if printHTMLfile then
             rootModule.emitHTML;
         if);
         inner;
         (if showUnits then rootmodule.printUnits if);
     if)
  #)
