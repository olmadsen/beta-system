ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qchecker';
INCLUDE 'qvirtual'
---lib:attributes---
semchecker: checker
  (# doCheckQual:
       (# srcQual,dstQual: ^ObjectGenerator
       enter(srcQual[],dstQual[])
       do  (* repeat code from DataItem - but perhaps in checkQual in Decl? *)
       #);
     Module::<
       (# semcheck:: 
            (# encModule:
                 (# org: ^Items; 
                 do (if origin[] <> none then
                        (origin[] -> org[]).theModule -> M[]
                    if)
                 exit M[]
                 #);
               M: ^Module;
            do (*'Module:semCheck:'->puttext; DI.sig.id[] -> putline;*)
               (if (encModule -> M[]) <> none then
                   M.semCheck
               if)
            #)
       #);
     ModuleItem::<
       (# semCheck::
            (#
            do (if not MD.isIncluded then
                   (*'Module not included: ' -> putline; doPT -> putline;*)
                   false -> doCheck
               if)
            #);          
          getQual:: 
            (# do (*'ModuleItem:getQual: ' -> puttext; dopt -> putline;*)
               md.DI.getQual -> qual[]
            #);
          getV_qual::
            (#
            do (if traceGetV_qual then
                   '*** ModuleItem:getV_qual:' -> puttext;
                   sig.doPT -> putline;
               if);
               MD.DI.OG[] -> qual[]
            #);
          pathToDcl::
            (#
            do (if traceGetV_qual then
                   '*** ModuleItem:pathToDcl:' -> putline;
                   sig.doPT -> putline;
               if);
               P[] -> md.DI.pathToDcl -> P[]
            #)
       #);
     DataItem::<
       (# getV_qual::
            (#
            do (if traceGetV_qual or trc then
                   '*** DataItem:getV_qual: ' -> puttext;
                   doPT -> putline
               if);
               (if OG.IS.isEmpty then
                   (* X: var TTT -- TT not virtual
                    * R: ref Person -- ditto
                    * S: obj Record -- virtual 
                    *)
                   (if not OG.super.ATd.isVirtual then
                       OG.getQual -> qual[]
                   if)
                else
                   OG[] -> qual[]
               if)
            #);
          pathToDcl::
            (* X: ?T, R: ?V where V:< Q
             * Y: = T{...}
             *)
            (#
            do (if trc or traceGetV_qual then
                   '*** DataItems:pathToDcl:' -> puttext; sig.doPT -> putline;
                   P.print;
               if);               
               P[]-> OG.pathToDcl -> P[];
               (if trc or traceGetV_qual then
                   '--- DataItems:pathToDcl:' -> puttext; sig.doPT -> putline;
                   P.print
               if)
            #);
          getQual:: 
            (# 
            do OG.getQual -> qual[];
               (*'***** DI:getQual: ' -> puttext; doPT -> putline;
               'Qual: ' -> puttext; qual.dopt -> putline*)
            #);
          semCheck::
            (* Check that in S : ? T, V: = T, T is a pattern
             * it seems to work for singular patterns, but check if this is so!
             * And for X: var/val/obj foo(e1,e2,e3) - type(X) = type(foo.return)
             *)
            (# OGqual: ^ObjectGenerator
            do (if false then
                   '**** DataItem: ' -> puttext; dopt -> putline;
                   '**   OG.isPtnOrSingular:' -> puttext;
                   OG.isPtnOrSingular -> putBoolean; 
                   ' isValue:' -> puttext; isValue -> putboolean;
                   ' isPrimitive:' -> puttext; isPrimitive -> putBoolean;
                   ' OG.isValueObj: ' -> puttext; OG.isValueObj -> putBoolean;
                   '\n**   OG.getQual: ' -> puttext; 
                   (OG.getQual).dopt -> putline;
                   newline;
               if);
               OG.getQual -> OGqual[];
               (if OGqual.isPtnOrSingular then
                   not isConst or isArg -> noArgsCheck;
                   (* see qabstractSyntaxTree::semCheck
                    * if noArgsCheck, the sons are not checked
                    * Is this correct?
                    *)
                   (if isValue then
                       (if not (OGqual.isValueObj or OGqual.isPrimitive) then 
                           this(DataItem)[] -> SemanticError
                           (#
                           do 'The qualification "' -> msg;
                              OG.doPT -> msg;
                              '" must be a value pattern' -> msg;
                           #)
                       if)
                    else
                       (if OGqual.isValueObj then
                           (if OGqual.isBasicValue then
                               (* while implementing sub of integer, etc
                                * we allow decl of the form I: ?integer
                                *)
                            else
                               this(DataItem)[] -> SemanticError
                               (#
                               do 'The qualification "' -> msg;
                                  OG.doPT -> msg;
                                  '" must be an object pattern' -> msg;
                               #)                           
                   if)if)if)
                else
                   (if OGqual.super.ATd[] <> none then
                       OGqual.super.ATd.doPT -> putline;
                    else
                       OGqual.father.doPT -> putline
                   if);
                   this(DataItem)[] -> SemanticError
                   (#
                   do 'The qualification "' -> msg;
                      OG.doPT -> msg;
                      '" must be a pattern '-> msg;
                   #)
               if)
            #);
          checkQual::                               
            (# error:
                 (#
                 do (if reportSemErr then
                        srcOG[] -> semanticError
                        (#
                        do '"'->put; srcOG.doPT -> puttext;
                           '" cannot be assigned to "' -> puttext;
                           doPT -> puttext; '"' -> put; newline;
                 #)if)#);
               srcQual,dstQual: ^ObjectGenerator
            do (if traceGetQual then
                   '\n>>> DataItem:checkQual:' -> puttext; 
                   (if isConst then ':C:' -> puttext if);
                   (if isAssign then ' := ' -> puttext if);
                   ('Source',srcOG[]) -> display;
                   ('Destination',this(DataItem)[]) -> display; 
               if);
               (if srcOG[] <> none then 
                   srcOG.getQual -> srcQual[] (* none if string " ..." *)
                else
                   '>>> srcOG none' -> putline
               if);
               getQual -> dstQual[]; (* getQual may return possible return value
                                      * whihc is not correect since we do not
                                      * assign to the retunr vlaue of OG! 
                                      * So it should be OG[] -> dstQual[]!?
                                      *)
               ('srcQual',srcQual[]) -> displayB; (* there is also a Display *)
               ('dstQual',dstQual[]) -> displayB;
               (if isConst and isAssign then 
                   (* OG is left-part of assign as in aPoint1 := aPoint2 *)
                   (if OG.isValueObj then
                    else
                       error
                   if)
                else
                   (if true
                    // OG.isBasicValue then 
                       (* OG[] -> dstQual[];*) 
                      (* '\n***Got isBasicValue:'-> puttext; 
                       OG.doPT -> putline;
                       '** srcQual:' -> putline; srcQual.dopt -> puttext;
                       ':primNo:' -> puttext; srcQual.primNo -> putint;
                       newline;
                       '** dstQual:' -> putline; dstQual.doPT -> putline;*)
                       (* The value_prim cases below is to handle
                        *    _V: ?Value 
                        * in patterns integer, char and boolean
                        *)
                       (if srcQual[] <> none then
                           (if srcQual.primNo
                            // integer_prim then
                               (if dstQual.primNo 
                                // integer_prim // char_prim // boolean_prim then
                                else
                                   error
                               if)
                            // char_prim then
                               (if dstQual.primNo 
                                // integer_prim // char_prim // value_prim then
                                else
                                   error
                               if)
                            // boolean_prim then
                               (if dstQual.primNo 
                                // boolean_prim // integer_prim then 
                                else
                                   error 
                               if)
                            // value_prim then
                               (if dstQual.primNo 
                                // integer_prim // char_prim // value_prim then
                                else
                                   error
                               if)                               
                            else
                               error
                           if)
                        else
                           error
                       if)  
                    // OG.isValueObj then
                       (*'\n***Got valueObj:'-> puttext; 
                        OG.dopt -> putline;
                        srcQual.dopt -> putline;
                        dstQual.dopt -> putline;
                       '---'->putline;*)
                       (if true
                        // srcQual[] = dstQual[] then (* OK *)
                        // srcQual[] -> dstQual.inSuper then 
                           srcOG[] -> Warning
                           (# 
                           do 'The right-side value: "' -> msg;
                              srcOG.doPT -> msg;
                              '" is truncated ' -> msg;
                              '\n\twhen assigned to the left-side variable: "' -> msg;
                              doPT -> msg;
                              '"' -> msg
                           #);
                           srcQual.dopt -> putline;
                           dstQual.dopt -> putline;
                        // dstQual[] -> srcQual.inSuper then 
                           '\n**** OBS! source is super of dest: ' -> putline;
                           '**   source: ' -> puttext; srcOG.dopt -> putline;
                           '**   dest  : ' -> puttext; dopt -> putline;
                        else
                           (if srcOG.primNo = indexedGet_prim then
                               (*'indexGet_prim'->putline*)
                               (* fix that the type of the indexed DI is consistent
                                * with the left-side
                                * *)
                            else
                               (* we should spell out wheter or not srcOG, etc
                                * are objectRefs or valueObjs *)
                               (*srcQual.dopt -> putline;
                               dstQual.dopt -> putline;*)
                               srcOG[] -> semanticError
                               (#
                               do 'The expression:\t"' -> puttext;
                                  srcOG.doPT -> puttext;
                                  '"\n     cannot be assigned to:\t"' 
                                    -> puttext;
                                  doPT -> puttext; '"' -> put; newline
                       #)if)if)
                    else
                       (*'**** Got object ref:' -> putline;*)
                       (* getQual -> dstQual[];*)
                       (if true
                        // srcOG.isValueObj then
                           srcOG[] -> semanticError
                               (#
                               do 'The value expression: "' -> msg;
                                  srcOG.doPT -> msg;
                                  '"\n     cannot be assigned to '-> msg;
                                  'the object reference: "' -> msg;
                                  doPT -> msg; '"' -> msg
                               #);
                        // srcQual[] -> dstQual.inSuper then (* OK *)
                        // (srcQual[] <> none) 
                           and (dstQual[] -> srcQual.inSuper) then
                           srcOG[] -> QuaCheck
                           (# 
                           do 'It must be checked at run-time that "'
                                -> puttext;
                              srcOG.doPT -> puttext;
                              '"\n     is a subpattern of "' -> puttext;
                              doPT -> puttext; '"' -> put; newline
                           #)
                        // (srcQual[] <> none) and (srcQual.primNo = none_prim) then
                        else
                           (if reportSemErr then
                               srcOG[] -> semanticError
                               (#
                               do 'The reference expression:\t"' -> puttext;
                                  srcOG.doPT -> puttext;
                                  '"\n     cannot be assigned to:\t"' -> puttext;
                                  doPT -> puttext; '"' -> put; newline
                               #);
               if)if)if)if)
            #);
       #);
     isPatternDen:< BooleanValue
       (# E: ^Exp
       enter E[]  
       do E.ATd.doPT -> putline;
          (if E.ATd[] <> none then E.ATd.isPatternDecl -> value if)
       #); 
     pattern::<
       (# checkQual::
            (* We come here for assignments of the form
             *    (this Speed) := exp
             * where 
             *    Speed: integer{... }
             *    Point: Value{ ... }
             *    Record: { ... }
             * This pattern must be a value of sub of basic 
             * - the latter may later be desclared as sub of Value
             *)
            (# trace: (# exit false #)
            do (if trace then
                   '**** Pattern:checkQual:\n' -> puttext; dopt -> puttext;
                   '\n**   :isAssign:' -> puttext; isAssign -> putBoolean;
                   ':srcOG: "' -> puttext; srcOg.dopt -> puttext; 
                   '"\n' -> puttext;
                   '**   isValueObj:' -> puttext; OG.isValueObj -> putBoolean;
                   ':isPrimitive:' -> puttext; isPrimitive -> putBoolean;
                   newline;
               if);
               (if isAssign then
                   (if true 
                    // OG.isValueObj then 
                       '**** pattern:checkQual:isValueObj:' -> putline;
                       '**   check assignTo: \n' -> puttext;
                       doPT -> putline;
                       '**  from: "' -> puttext; srcOG.doPT -> puttext;
                       '"\n\n' -> puttext
                    // isPrimitive then 
                       '**** pattern:checkQual:isPrimitive:shouldNotHappen' 
                         -> putline;
                    // OG.isBasicValue then 
                       '**** pattern:checkQual:isBasicValue:' -> putline;
                       '**   check assignTo: \n' -> puttext;
                       doPT -> putline;
                       '**  from: "' -> puttext; srcOG.doPT -> puttext;
                       '"\n\n' -> puttext
                    else 
                       (* Bad message - should be fixed *)
                       srcOG[] -> SemanticError
                       (#
                       do srcOG.doPT -> msg;
                          ' cannot be assigned to :\n' -> msg;
                          dopt -> msg
                       #)
                   if)           
               if)
            #);
          checkActualArgs::
            (# A: [6] ^Decl; inx,top: @ integer;
            do (if traceGetQual then
                   '\nCheck: ' -> puttext; actualArgs.doPT -> putline;
                   '  against: ' -> puttext; sig.doPT -> putline;
               if);
               scanArgs
               (#
               do (if (top + 1 -> top) > A.range then A.range -> A.extend if);
                  currentArg[] -> A[top][];
               #);
               actualArgs.scanArgs
               (# 
               do inx + 1 -> inx; 
                  (if inx > top then
                      actualArgs[] -> SemanticError
                      (# 
                      do 'More actual parameters:\n\t"' -> msg;
                         actualArgs.doPT -> msg; 
                         '"\n    than formal parameters:\n\t"' -> msg;
                         sig.doPT -> msg; '"' -> msg
                      #)
                   else
                      (current[],false) -> A[inx].checkQual;
                  if);
               #); 
               (if (inx < top)  then
                   actualArgs[] -> SemanticError
                   (# 
                   do 'More formal than actual parameters' -> msg;
                      actualArgs.doPT -> msg;
                      '\n'-> msg;
                      sig.doPT -> msg;
                   #)
               if);
            #);
          getV_Qual::
            (* P: S ... 
             * F(...): G ...
             * A(...) -> V: ? T: S ...
             * + virtual
             *    V:< A
             *    V::< A
             *    V:: A
             * + recursive
             *   foo(...) -> V: ? bar: ...
             *   bar(...) -> R: ? foo: ...
             *)
            (# DI: ^DataItem; ptn: ^Pattern
            do (if traceGetV_qual then
                   '*** Pattern:getV_qual:' -> puttext; sig.doPT -> puttext;
                   ':isVirtual:'-> puttext; isVirtual-> putBoolean; newline
               if);      
               L:
                 (if isVirtual then
                     (* V:< Q;
                      * W:< Q{ ... }
                      *)
                     (*P.print;*)
                     (*OG.getV_qual -> qual[]*)
                  else
                     (* P(...) -> V: ? S: { .... } *)
                     (if true then
                         OG[] -> qual[]; 
                         (if (sig.getReturnDecl -> DI[]) <> none then
                             (if DI.OG.IS.isEmpty then
                                 (if ((DI.OG.super.ATd[] -> ptn[]) <> none)
                                     and ptn.isVirtual then
                                     (if trc then
                                         '--- returnDecl:' -> puttext; 
                                         DI.doPT -> putline;
                                         dopt -> putline;
                                     if);
                                     none -> qual[];
                                     leave L
                                 if);
                                 DI.getQual -> qual[]
                             if);
                        else
                           OG[] -> qual[]
                       if)
                    else
                       getQual -> qual[];
                   if)
               if)
            #);
          pathToDcl::
            (* P: S{...}
             * V:< A{ ... }
             * V:< A;
             * V::< ...
             * V:: ...
             *)
            (# Px: ^OGpath; qual: ^ObjectGenerator; DI: ^DataItem
            do (if P[] = none then
                   '!!!! pathToDcl:P is none!' -> putline; doPT -> putline 
               if);
               OG[] -> (P[]->Px[]).qual[]; (* just ad hoc *)
               this(Pattern)[] -> (P[]->Px[]).vDcl[]; (* just ad hoc *)
               (if trc or traceGetV_qual then
                   '*** Pattern:pathToDcl: '->puttext;
                   sig.dopt -> putline;               
                   P.print;
               if);
               (if isRec then 
                   '**** isRecursive'->putline;
                   OG[] -> (P[]-> Px[]).qual[];
                   false -> isRec;
                   leave pathToDcl
               if);
               true -> isRec;
               (* this is probably just a hack 
                * - we need to handle virtuals as in the Beta compiler
                *)
               (if (sig.getReturnDecl -> DI[]) <> none then
                   (if trc then
                       '*** Pattern:return:DI:' -> puttext;
                       DI.doPT -> putline;
                   if);
                   Px[] -> DI.OG.pathToDcl -> Px[] -> P[];
                   (if trc then
                       '**  Pattern:return:DI:path\n' -> puttext;
                       P.print;
                   if);
                   (*Px.getQual*)
                   false -> isRec;
                   leave pathToDcl
                else
                   (if isVirtual then
                       (* V:< Q -- dont follow path to Q! *)
                       (if trc then
                           '--- Pattern:isVirtual' -> putline
                       if);
                       (*(P[]-> Px[]).getQual ;*)
                    else              
                       OG[] -> (P[]-> Px[]).qual[];
               if)if);
               false -> isRec;
            #);
          getQual::
            (# 
            do (if traceGetQual then
                   '*** Pattern:getQual: '-> puttext; sig.doPT -> putline
               if);
               (none,origin[],origin[],true) -> check;
               (if isRecursive then
                   (* For a pattern
                    *    foo(...) -> res: ? foo ...
                    * getQual is called on the foo return value
                    * giving a recursive call of getQual
                    *)
                   (if traceGetQual then
                       '*** isRecursive' -> putline
                   if);
                   OG[] -> qual[];
                   (*false -> isRecursive;*)
                   leave getQual
               if);
               inner;
               (if traceGetQual then
                   'Pattern:getQual:B: "' -> puttext; 
                   sig.doPT -> puttext; '"' ->put; newline
               if);
               true -> isRecursive;
               (* this is probably just a hack 
                * - we need to handle virtuals as in the Beta compiler
                *)
               sig.getExplicitQual -> qual[];
               L:
                 (if qual[] = none then
                     (if isVirtual then
                         (*'*** Virtual: ' -> puttext; dopt -> puttext; 
                          ' on: ' -> puttext; OG.super.on -> putint;
                          ' pn: ' -> puttext; OG.super.pn -> putint; newline;*)
                         OG.getQual -> qual[];
                         (if traceGetQual then
                             'getQual:Virtual: ' -> puttext; 
                             qual.doPT -> putline;
                             OG.doPT -> putline
                         if)
                      else
                         (if traceGetQual then
                             'Pattern:getQual:C:' -> putline;
                         if);
                         OG.IS.scanAllItemDecls
                         (# DI: ^DataItem
                         do (if currentDcl.isOutArg then
                                (if currentDcl.isDAtaItem then
                                    (currentDcl[] -> DI[]).getQual -> qual[];
                                    leave L;
                                if)
                            if)
                         #);                       
                         OG[] -> qual[];
                 if)if);
               false -> isRecursive;
               (if traceGetQual then
                   'Pattern:getQual:D: "\n' -> puttext; 
                   (if qual[] <> none then 
                       qual.doPT -> puttext
                    else 'none'->puttext
                   if);
                   '"' -> put; newline
               if)
            #);
          semCheck::
            (# lastSuper: ^Exp
            do (if ((OG.super.last -> lastSuper[]).args[] <> none) and
                       (lastSuper.args.length = 0) then
                   true -> lastSuper.noArgsCheck
               if)
            #)
       #);
     Signature::<
       (# checkActualArgs:
            (# actualArgs: ^Arguments
            enter actualArgs[]
            do (if args[] <> none then
                   actualArgs[] -> args.checkActualArgs
                else
                   (if traceGetQual then
                       'Signature:checkActualArgs: args is none: ' -> puttext;
                       this(Signature).doPT -> putline;
                       ' actuals: ' -> puttext;
                       actualArgs.doPT -> putline
                   if)
               if)
            #);
          getExplicitQual:
            (* Returns qual of possible explicit return value
             * GetQual returns this as default if no explict return is defined
             * Also try superchain 
             *)
            (# qual: ^ObjectGenerator;  ptn: ^Pattern
            do (if val[] <> none then 
                   val.getQual -> qual[] 
                else
                   father[] -> ptn[];
                   (if (ptn.OG.super.ATd[] -> ptn[]) <> none then
                       ptn.sig.getExplicitQual -> qual[]
                if)if)                           
            exit qual[]
            #);
          getQual:: 
            (# ptn: ^Pattern
            do (if traceGetQual then
                   'Signature:getQual: "\n' -> puttext; dopt->puttext;
                   '"' -> put; newline
               if);
               getExplicitQual -> qual[];
               L:
                 (if qual[] = none then
                     (* no return value - use %this P *)
                     father[] -> ptn[];
                     ptn.OG.IS.scanAllItemDecls
                     (# DI: ^DataItem
                     do (if currentDcl.isOutArg then
                            (if currentDcl.isDAtaItem then
                                (currentDcl[] -> DI[]).getQual -> qual[];
                                leave L
                            if)
                        if)
                     #);
                     (father[] -> ptn[]).OG[] -> qual[]
                 if);
            #);
       #);
     Arguments::<
       (# checkActualArgs:
            (# args: ^Arguments; A: [6] ^Decl; inx,top: @ integer
            enter args[]
            do (if traceGetQual then
                   '\nCheck: ' -> puttext; args.doPT -> putline;
                   '  against: ' -> puttext; this(Arguments).doPT -> putline;
               if);               
               scanArgs
               (# 
               do (if (top + 1 -> top) > A.range then A.range -> A.extend if);
                  current[] -> A[top][] 
               #);
               L:
                 args.scanArgs
                 (# 
                 do inx + 1 -> inx; 
                    (if inx > top then
                        (* try possible super *)
                        '\n*** More actual parameters:\n\t' ->puttext;
                        args.doPT -> puttext;
                        '\n    than formal parameters: \n\t' -> puttext;
                        this(Arguments).doPT -> putline;
                        leave L;
                     else
                        '** checkArg: ' -> puttext;  current.dopt -> puttext;
                        ' ' -> put; A[inx].doPT -> putline;
                        (current[],false) -> A[inx].checkQual;
                    if);
                 #);
               (if (inx < top)  then
                   (* problem for integer versus integer(18)
                    * and probably String ...
                    *)
                   '\n*** More formal than actual parameters' -> putline;
                   args.doPT -> putline;
                   this(Arguments).doPT -> putline;
               if);
            #);
       #);
     Value::<
       (# getQual:
            (# D: ^Decl; qual: ^ObjectGenerator 
            do scanSons(# do current[] -> D[] #);
               (* only works for one return value/decl *)
               D.getQual -> qual[];
               (if traceGetQual then
                   (if qual[] <> none then 
                       qual.doPT -> putline;
                    else 'none '->putline
               if)if);
            exit qual[]
            #)
       #);
     Include::
       (# getQual::
            (# 
            do 'Include:getQual: '->puttext; dopt -> putline;
               (if son[] <> none then
                   scanSons
                   (# IV: ^Invocation
                   do (if isLAst then
                          'include:getqual:'-> puttext; doPT -> putline;
                          current[] -> IV[];
                          IV.getQual -> qual[]
               if)#)if)
            #);
       #);
     ObjectGenerator::<
       (# getV_Qual::
            (# Px: ^OGpath
            do (* Should only be called for singular OGs
                * If part of pattern decl, argScope may be wrong
                *)
               (if traceGetV_qual then
                   '*** ObjectGenerator:getV_qual:shouldNotHappen' -> putline;
                   doPT -> putline
               if);
               (none,origin[],origin[],true) -> check;
               (if IS.isEmpty then
                   (* super (Invocation) may be object or pattern *)
                   '**** ObjectGenerator:getV_qual:' -> putline;
                   dopt -> putline;                       
                   (origin.father[],0,0,none) 
                     -> OGpath 
                     -> pathToDcl 
                     -> Px[];
                   '*** ObjectGenerator:after:pathToDcl:'-> putline;
                   Px.print;
                   Px.qual[] -> qual[];
                else
                   (* is Singular *)
                   this(ObjectGenerator)[] -> qual[]
               if)                   
            #);
          pathToDcl::
            (# Px: ^OGPath
            do (if trc or traceGetV_qual then
                   '*** ObjectGenerator:pathToDcl:' -> putline; dopt -> putline;
                   P.print;
               if);
               (if IS.isEmpty Then
                   P[] -> super.pathToDcl -> P[]
                else
                   (if origin.father[] = none then
                       'OG:pathToDcl:origin.father is none'->putline;
                   if);
                   (*(origin.father[],0,0,P[]) -> OGpath -> P[];*)
                   this(ObjectGenerator)[] -> (P[]-> Px[]).qual[]
               if)
            #);
          getQual:
            (# qual: ^ObjectGenerator 
            do (if traceGetQual then
                   '*** ObjectGenerator:getQual:primNo: '->puttext; 
                   primNo -> putint; ' '-> put; 
                   IS.primNo -> putint; ' '-> put; 
                   (if super.ATd[] <> none then
                       super.Atd.primNo -> putint; ' '-> put; 
                   if);
                   ':isPrimitive: ' -> puttext; isPrimitive -> putboolean;
                   ':' -> put; doPT -> putline
               if);
               (* Should only be called for singular OGs
                * If part of pattern decl, argScope may be wrong
                *)
               (none,origin[],origin[],true) -> check;
               (if IS.isEmpty then
                   (* super (Invocation) may be object or pattern *)
                   super.getQual -> qual[]
                   (* qual = E3.ATd.getQual *)
                else
                   (* is Singular *)
                   L:  IS.scanAllItemDecls
                     (# DI: ^DataItem
                     do (if currentDcl.isOutArg then
                            (if currentDcl.isDAtaItem then
                                (currentDcl[] -> DI[]).getQual -> qual[];
                                leave L
                            if)
                        if)
                     #);
                   (if qual[] = none then
                       this(ObjectGenerator)[] -> qual[]
                   if)
               if);
               (*'qual:' -> putline; qual.dopT -> putline;*)
            exit qual[]
            #); 
          XinSuper: BooleanValue
            (# lQ: ^ObjectGenerator
            enter lQ[]
            do ('inSuper:lQ',lQ[]) -> display;
               ('rQ',lQ[]) -> display;
               L:
                 (if lQ[] = this(ObjectGenerator)[] then
                     true -> value 
                  else
                     (if (lQ[] <> none) then
                         (if (lQ.primNo <> object_prim) then
                             lQ.super.getQual -> lQ[];
                             (if lQ[] =  none then
                                 objectDecl.getQual -> lQ[]
                             if);
                             restart L;
                 if)if)if)
            #);
          isPtnOrSingular: BooleanValue
            (#
            do (if super.ATd[] <> none then
                   super.ATd.isPatternDecl -> value
                else
                   (* no super - must be singular *)
                   not IS.isEmpty -> value
               if)
            #);
       #);
     Invocation::<
       (# checkInv:
            (# E: ^Exp; 
            do (* should check path *)
               scanSons(# do current[] -> E[] #);
            exit E[]
            #);
          getV_Qual::
            (* R, R.x, R.S.x, ...
             * R.x
             * May be a super - which we dont know here
             *)
            (# E: ^Exp; 
            do (if traceGetV_qual then
                   '*** Invocation:getV_qual: ' -> puttext; dopt -> putline;
               if);
               scanSons
               (# 
               do (* we should call ... -> current.getV_Qual -> qual[]
                   * however, then getV_qual should return a new path
                   * perhaps in general return a path and 
                   * extract the qual from the path?
                   * Or just 2 functions: getPath -> P[] 
                   * and then P[] -> getV_qual
                   *)
                  current[] -> E[] 
               #);
              E.getV_Qual -> qual[]
            #);
          pathToDcl::
            (# E: ^Exp
            do (if traceGetV_qual then
                   '*** Invocation:pathTo_dcl: ' -> puttext; dopt -> putline;
               if);
               scansons
               (#
               do current[] -> E[] (* see getV_qual comment *);
               #);
               P[] -> E.pathToDcl -> P[];
            #);
          getQual::
            (# E: ^Exp; 
            do (*'Invocation:getQual:' -> puttext; doPT -> putline;*)
               scanSons(# do current[] -> E[] #);
               ('Invocation:getQual',E[]) -> display;
               (if E[] <> none then
                   E.getQual -> qual[]
               if)
            #);
          getActualReceiver::
            (#
            do (last).getActualReceiver -> rec[]
            #)
       #);
     ObjectInvocation::<
       (# semCheck::
            (# receiver: ^Exp; ptn: ^Pattern
            do (*'**** ObjectInvocation:semCheck:rec:"' -> puttext;
               rec.doPT -> puttext;
                '" invoke:"' -> puttext; args.doPT -> puttext; '"'->putline*)
               (if (ATd.primNo <> rAssign_prim) and (ATd.primNo <> vAssign_prim) then
                   (*' Atd: ' -> puttext; ATd.doPT -> putline;*)
                   (if ATd.primno
                    // inner_prim // this_prim
                    // restart_prim // leave_prim 
                    // indexedPut_prim then
                       true -> noArgsCheck
                    // ifThen_prim then
                    else
                       (if (1 -> ptnId.T.inxGet) = '#' then
                           (* clean-up *)
                           (*'#PTN: '->puttext; ptnId.T[] -> putline*)
                        else
                           (if not noArgsCheck then
                               (* check that args can be assigned to the 
                                * formal arguments of the receiver rec *)
                               args[] -> ATd.checkActualArgs
                       if)if)
                   if)
                else
                   (* pattern is assignment := *)
                   rec[] -> receiver[];
                   
                   (*'**** assign: "' -> puttext; rec.label -> puttext;
                   '"' -> put; rec.dopt -> puttext;
                   '" := "' -> puttext; dopt -> puttext; '"' -> putline;
                   (if receiver.ATd.primNo = this_prim then
                       '**   this:true'->putline;
                   if);*)
                   
                   args.scanArgs
                   (# i: @integer
                   do (if (i + 1 -> i) > 1 then
                          args[] -> semanticError
                          (#
                          do 'List too long to be assigned to: "' -> puttext;
                             receiver.doPT -> puttext; '"' -> put; newline
                          #)
                      if);
                     (* '**   arg:"' -> puttext; current.dopt -> puttext;
                      '"' -> putline;*)
                     (* (if receiver.ATd.primNo = this_prim then
                          receiver.scanSons
                          (# i: @integer
                          do (if (i + 1 -> i) = 2 then
                                 '%thisArg: ' -> puttext; 
                                 current.dopt -> putline;
                             if)
                      #)if);*)
                      receiver.getActualReceiver -> receiver[];
                      (* check that current can be assigned to receiver  *)
                      (current[],true) -> receiver.ATd.checkQual 
                   #);
               if)
            #);
          getV_Qual::
            (# Px: ^OGpath; DI: ^DataItem; MI: ^ModuleItem; ptn: ^Pattern
            do (if trc or traceGetV_qual then
                   '*** ObjectInvocation:getV_qual:' -> putline; 
                   (if true then
                       dopt -> putline;
                       '--- EncOG: ' -> putline;
                       encOG.doPT -> putline
                   if)
               if);
               (if (ATd.getV_qual -> qual[]) = none then
                   (if trc then
                       '--- PI:getV_qual:handle:virtual:' -> putline;
                   if);
                   (encOG[],on,pn,none) 
                     -> OGpath 
                     -> ATd.pathToDcl 
                     -> Px[];
                   (if trc or traceGetV_qual then
                       '*** ObjectInvocation: found path:'->putline;
                       Px.print;
                   if);
                   Px.getQual;
                   Px.qual[] -> qual[];
               if);
               (if traceGetV_qual then
                   '*** ObjectInvocation:QUAL:OF: "'->puttext;
                   dopt -> puttext; '" IS: '->putline; qual.label -> putline;
                   (if qual[] <> none then
                       qual.doPT -> putline; 
                       'father.father.father: ' -> putline;
                       qual.father.father.father.dopt -> putline;
                    else
                       'none'->putline
               if)if)
            #);
          pathToDcl::
            (#
            do (* we should append the path to ATd *)
               (if traceGetV_qual then
                   '*** ObjectInvocation:pathToDcl:' -> putline; dopt-> putline;
               if);
               (if ATd[] = none then  
                   'ATD[] = none for:' -> putline;
                   dopt -> putline;
                   origin.father.dopt -> putline
               if);
               (origin.father[],on,pn,P[]) -> OGpath -> ATd.pathToDcl -> P[];
            #); 
          getQual::
            (#
            do (if traceGetQual then
                   '*** ObjectInvocation:getQual: ' -> puttext;
                   dopt->putline
               if);
               (*(if (ATd[] <> none) then ATd.primNo -> putint if);
               ' ' -> put; doPT -> putline;*)
               (if (ATd[] <> none) and (ATd.primNo = this_Prim) then
                   (* move to Apl:getQual*)
                   args.scanArgs
                   (# IV: ^ObjectGenerator
                   do current[] -> IV[]; IV.getQual -> qual[];
                      (* IV.doPT -> putline; qual.doPT -> putline*)
                   #);
                   ('Keyword:getQual:this_prim', qual[])->display;
               if);
               (if ATd[] <> none then
                   (if ATd.isVirtual then
                       ATd.getQual -> qual[]
               if)if);
               (if traceGetQual then
                   (if qual[] <> none then
                       qual.doPT -> putline
                    else
                       'qual is none' -> putline
               if)if)
            #);      
          getActualReceiver::
            (#
            do (if ATD.primNo = this_prim then
                   args.scanSons
                   (# i: @integer; OG: ^ObjectGenerator
                   do (if (i + 1 -> i) = 2 then
                          (*'** primArg:'->puttext; current.doPT -> putline;*)
                          ((current[] -> OG[]).super).last -> rec[]
                      if)
                   #)
               if)
            #)
       #);
     BracketedExp::<
       (# getQual::
            (# 
            do IV.getQual -> qual[] 
            #);
          pathToDcl:: 
            (# Px: ^OGpath 
            do P[] -> IV.pathToDcl -> P[];
               
            #);
          getActualReceiver::
            (#
            do IV.getActualReceiver -> rec[];
            #);
       #);
     Const::<
       (# pathToDcl::
            (# Px: ^OGpath
            do IntegerDecl.OG[] -> (P[]->PX[]).qual[]
            #)
       #);
     StringObj::< 
       (# getQual:: (#do stringDecl.getQual -> qual[] #); 
          pathToDcl::
            (# Px: ^OGpath
            do StringDecl.OG[] -> (P[]->PX[]).qual[]
            #)
       #);
     
  do '**** CHECKER   ****' -> putline;
     main (*rootModule*).semCheck;
     (if semanticErrors then
         '\n***** Semantic errors: compilation stops' -> putline
      else
         inner
     if)
  #)
