ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '../compiler_IF/directoryComponents';
INCLUDE 'qgenerator';
INCLUDE '../VM/betaVM'
---program:descriptor---
SystemEnv
(# compile: @generator;
   VM: @ betaVM
     (# putCH:: (# do ch -> put #);
        getCh:: (# do get -> ch #);
        errorEvent:: (# do msg[] -> putline; done #);
        dumpObjEvent::
          (# length: IntegerValue
               (#
               do IS.scanSons
                  (# D: ^compile.DataItem; size: @integer
                  do (if current.isDataItem then
                         current[] -> D[];
                         value 
                         + D.sig.id.length + 1 (* sig.id + zero *)
                         + 4 (* off, kind, size, descNo *)
                           -> value;
                         D.objSize -> size;
                         (if size = 0 then 1 -> size if);
                         value + size -> value; (* values *)
                  if)#);
                  (*'length: ' -> puttext; value -> putint; newline*)
               #);
             add:
               (# V: @integer
               enter V
               do v -> items[top + 1 -> top];
               #);
             top: @integer;
             IS: ^compile.Items; 
          do (*'\ndumpObjEvent:'->putline;*)
             (obj.myObjDesc).descInx -> compile.getObjectDesc -> IS[];
             length -> items.new;
             IS.scanSons
             (# D: ^compile.DataItem; size: @integer
             do (if current.isDataItem then
                    current[] -> D[];
                   (* D.newOff -> putint; ':' -> put;
                    D.objSize -> size;
                    (if size = 0 then 1 -> size if);
                    size -> putint; ' ' -> put;
                    D.sig.id[] -> puttext; 
                    (if D.isConst then 
                        ' const' -> puttext
                        else
                        ' var' -> puttext
                    if);
                    (if true
                     // D.OG.isBasic then
                        ' basic' -> puttext
                     // D.OG.isValueObj then
                        ' value' -> puttext;
                     else
                        ' ref' -> puttext;
                    if);
                    ' descNo:' -> puttext;
                    D.OG.descNo -> putint;
                    newline;*)
                    (for i: D.sig.id.length repeat 
                         i -> D.sig.id.inxGet -> add
                    for);
                    0 -> add;
                    D.newOff -> add;
                    D.objSize -> size;
                    (if (size = 0) then 1 -> size if);
                    size -> add;

                    (if true
                     // D.OG.isBasic then
                        (if true
                         // D.isConst then
                            0 -> add
                         else
                            1 -> add;
                        if);
                     // D.OG.isValueObj then
                        (if true
                         // D.isConst then
                            2 -> add
                         else
                            3 -> add;
                        if)
                     else
                        (if true
                         // D.isConst then
                            4 -> add
                         else
                            5 -> add;
                        if)
                    if);
                    D.OG.descNo -> add; 
                    (for i: size repeat 
                         0 -> add; (* value *)
                    for)
                if)
             #);
          #);
        done:: (# do stop #)
     #);
   setWindowEnv:: (# do myWindowEnv[] -> theWindowEnv[]#);
   myWIndowEnv: @ guienv;
   runMode: @integer;
   FN: ^text;
   F: @file;
   readArguments:
     (# arg: ^text
     do run_Beta_INT -> runMode;
        (for i: noOfArguments - 2 repeat
             i + 1 -> arguments -> arg[];
             (if true
              // ('-x' -> arg.equalNCS) then      
                 ' ' -> put;
                 true -> compile.onlyLex
              // ('-c' -> arg.equal) then   
                 run_C_INT -> runMode;
              // ('-C' -> arg.equal) then   
                 save_BC_Image -> runMode;
              // ('-v' -> arg.equalNCS) then   
                 true -> compile.verbose
              // ('-t' -> arg.equalNCS) then
                 true -> compile.traceEmbedding
             if);             
        for);
        noOfArguments  -> arguments -> FN[];
     #);
do 'qBETA COMPILER: ' -> puttext;
   readArguments;
   ('/MiniSystem/qbeta/BETAworld/','.xbeta') -> compile.initDirectoryModules;
   FN[] -> compile.fixExtension_xbeta -> FN[] -> putline;
   FN[] -> F.name;
   F.openRead;
   
   (none,false,none,FN[]) -> compile; 
   
   (runMode,true) -> VM.init;
   (if not compile.onlyLex and not compile.semanticErrors then
       (compile.runtimeDescs,compile.mainDescNo,FN[],runMode) -> VM.execute.init;
       VM.execute[] -> fork;
    else
       stop
   if);
#)
