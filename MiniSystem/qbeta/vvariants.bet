ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qrestrictions';
---lib:attributes---
variants: restrictions
  (#
     Module::<
       (# Qalloc:: (#do DI.Qgen #);
       #);
     ModuleItem::<
       (# Qalloc:: (# do #);
          
       #);
     DataItem::<
       (# Qalloc:: 
            (#
            do (if IT[] <> none then (* IT is none if top module *)
                   IT.qsize[varNo] + 1 -> IT.qsize[varNo] -> qoff[varNo];
               if);
               (varNo,IT[]) -> sig.Qalloc;
               (varNo,IT[]) -> OG.Qalloc
            #);
       #);
     Signature::<
       (# Qalloc:: (# do #);
       #);
     Arguments::<
       (# Qalloc:: (# do #);
       #);
     ObjectGenerator::<
       (# Qvariants: @
            (# Var: [1] ^Variant; top: @integer; 
               Variant:
                 (# vDcl: [1]  ^Pattern; top: @integer;
                    type: [1] @ integer;
                    size: [1] @ integer;
                    binding: [1] ^Pattern;
                    add:
                      (# VD,BD: ^Pattern
                      enter(VD[],BD[])
                      do L:
                           (for i: top repeat
                                (if VD[] = vDcl[i][]  then
                                    (* already a binding of VD *)
                                    (if BD[] <> binding[i][] then
                                        '\n*** inconsitent virtual binding' 
                                          -> putline
                                if)if);
                                leave L
                           for);
                         (if (top + 1 -> top) > vDcl.range then
                             vDcl.range -> vDcl.extend;
                             type.range -> type.extend;
                             size.range -> size.extend;
                             binding.range -> binding.extend; 
                         if);
                         VD[] -> vDcl[top][];
                         -1 -> type[top];
                         -1 -> size[top];
                         BD[] -> binding[top][];                         
                      #)
                 exit this(Variant)[]
                 #);
               Add:
                 (# VD,BD: ^Pattern
                 enter(VD[],BD[])
                 do (if (top + 1 -> top) > var.range then
                        var.range -> var.extend
                    if);
                    Variant -> var[top][];
                    (VD[],BD[]) -> var[top].add
                 #)
            #);
          Qalloc::
            (# E:^Exp
            do (varNo,IT[]) -> super.Qalloc;
               super.scanSons(# do current[] -> E[] #);
               (if (E[] <> none) and (E.ATd[] <> none) then 
                   E[] -> E.ATd.handleArgs  (* !!!*)
               if);
               (if isObjectDesc then
                   (varNo,IT[]) -> IS.Qalloc 
               if);               
            #);
       #);
     items::<
       (# Qalloc:: 
            (#
            do (if varNo > noOfVar then
                   1 -> qsize.extend;
                   noOfVar + 1 -> noOfVar
               if);
               (if super.ATd[] <> none then
                   (if superDesc[] = none then
                       'Items:alloc: ' -> puttext; dopt ->putline
                   if);
                   (*superDesc.vsize -> vsize;*)
                   superDesc.qsize[varNo] -> qsize[varNo];
                   (*superDesc.dsize -> dsize; *)
                   (if super.isThisBlockLevel and 
                       (not super.isImplSuper) and (not super.isSingularImplSuper)
                       then
                       superDesc.originOff -> originOff; 
                    else
                       qsize[varNo] + 1 -> qsize[varNo] -> originOff (* also array*)
                   if)
                else
                   qsize[varNo] + 1 -> qsize[varNo] -> originOff
               if);
               (if sig[] <> none then
                   (varNo,this(items)[]) -> sig.Qalloc
               if);
               scanSons(# do (varNo,this(items)[]) -> current.Qalloc #)
            #);
          qsize: [1] @integer; noOfVar: @integer
       #);
     Pattern::<
       (# Qalloc:: (# do #);
       #);
     Invocation::<
       (# Qalloc:: (# do #);
       #);
     ObjectInvocation::<
       (# Qalloc:: (# do #);
       #);
     BracketedExp::<
       (# Qalloc:: (# do #);
       #);
     Const::<
       (# #);
     CharObj::< (# #);
     StringObj::< (# #);
  do 

     inner
  #)
