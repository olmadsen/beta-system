ORIGIN '~beta/basiclib/betaenv';
INCLUDE '../compiler_IF/compiler_IF';
INCLUDE 'qStdBetaLexer'
---lib:attributes---
newVM: (# exit true #);

TrueValue: BooleanValue(# do true -> value #);
StdBetaTopAST: Tree
  (# level: @integer;
     NX_error: (# exit 1 #);
     QUA_error: (# exit 2 #);
     QUA_runTimeCheck: (# exit 3 #);
     type: 
       (# isValue:< booleanValue;
          isRef:< booleanValue;
          isStrucRef:< booleanValue;
          isNone:< booleanValue;
          isPrimitive:< booleanValue;
          isSingle:< booleanValue;
          theDesc:< (# D: ^node do inner exit D[] #);
          print:<  (# out: @text do inner exit out[] #);
          append:<
            (# TP,returnTP: ^type
            enter TP[]
            do (if TP[] <> none then 
                   inner 
                else
                  this(type)[] -> returnTP[]
               if)
            exit returnTP[]
            #);
          appendToList:<
            (# TPx,returnTP: ^type
            enter TPx[]
            do inner
            exit returnTP[]
            #);
          match:< integerValue
            (# left: ^type
            enter left[]
            do inner
            #);     
          matchList:< integerValue
            (# TPx: ^Type
            enter TPx[]
            do inner
            #);
       do inner 
       exit this(type)[]
       #);
     
     node: TopNode
       (# PT::<
            (#  hasNL: BooleanValue
                 (# comm: ^text
                 enter comm[]
                 do L: comm.scanAll
                      (# 
                      do (if ch = ascii.newline then 
                             true -> value;
                             leave L
                         if)
                      #)
                 #);
               atEol: BooleanValue
                 (#
                 do (lx.length > 0) 
                    and ((lx.length -> lx.inxGet) = ascii.newline)
                    -> value
                 #);
               breakIfNotAtEol:
                 (#
                 do (if not atEol then
                        (pos,true) -> mkBreak
                    if)
                 #);
               printComment:
                 (# NL, indented: @boolean; firstPos: @integer;
                    comm: ^Text
                 enter comm[]
                 do (if comm[] <> none then                        
                        comm.setPos;
                        pos -> firstPos;
                        pos > 0 -> indented;
                        (if comm[] -> hasNL -> NL then
                            (pos,true) -> mkBreak
                        if);
                        '(' -> lx.put; '*' -> lx.put; (* pos + 2 -> pos;*)
                        comm.scanAll
                        (#
                        do (if ch = ascii.newline then
                               firstPos -> pos;
                               (pos,true) -> mkBreak;
                               ' ' -> lx.put;
                               pos + 1 -> pos
                            else
                               ch -> lx.put; (*pos + 1 -> pos*)
                        if)#);
                        '*' -> lx.put; ')' -> lx.put; (*pos + 2 -> pos;*)
                        (if NL or indented then 
                            firstPos -> pos;
                            (pos,true) -> mkBreak 
                 if)if)#)
            do comment[] -> printComment;
               inner;
               commentAfter[] -> printComment;
            #);
          setUpOrigin::<
            (# done: @boolean
            do inner;
               (if not done then
                   scanSons(# do origin[] -> current.setUpOrigin #)
               if)
            #);          
          comment,commentAfter: ^Text;
          cat: @ integer;
          sons: ^nodeList;
          addCommentAfter:
            (#
            do (if lex.skipComment.comment.length > 1 then
                   (if commentAfter[] <> none then
                       ascii.newline -> commentAfter.put;
                       lex.skipComment.get -> commentAfter.append
                    else
                       lex.skipComment.get -> commentAfter[]
               if)if)
            #);
          markAsSuper:< (# do inner #);
          getODorigin:: 
            (# ODx: ^Node;
            do origin[] -> ODx[];
               L:
                 (if not ODx.isObjDesc then 
                     ODx.getODorigin -> ODx[];
                     restart L
                 if);
               ODx[] -> OD[]
            #); 
          isObjDesc:< booleanValue;
          hasAttributes:< booleanValue; (* true for all Decl's that define
                                         * attributes: x: @ (# ... #), etc *)
          enterType:< (# TP: ^Type do inner exit TP[] #);
          exitType:< (# TP: ^Type do inner exit TP[] #);
          Xappend:
            (# N: ^node
            enter N[]
            do (if sons[] = NONE then &nodeList[] -> sons[] if);
               N[] -> sons.append;
               this(node)[] -> N.father[]
            #);
          insert:
            (# N,atN: ^Node
            enter(N[],atN[])
            do (if sons[] = NONE then &nodeList[] -> sons[] if);
               (N[],atN[]) -> sons.insert;
            #);   
          moveUp:
            (* move son[inx] = N[] to son[inx-1]
             * To move down: 
             * son[inx] = N[] to son[inx+1], 
             * call moveUp on N.next
             *)
            (# N: ^node; pos: @integer; pred: ^nodeList
            enter N[]
            do sons[] -> pred[];
               sons.scan
               (#
               do pos + 1 -> pos;
                  (if current[] = N[] then
                      'Found: ' -> puttext; pos -> putint; newline;
                      (if pos > 1 then
                          'Do move' -> putline;
                          (* switch pred and current 
                           * x1 -> x2 -> pred -> current -> xk+2 -> ...
                           * we delete current,and
                           * insert before pred
                           *)
                          NL.next[] -> pred.next[];
                          sons.length - 1 -> sons.length;
                          (N[],pred.son[]) -> insert;
                          doPT -> putline
                      if)
                   else
                      NL[] -> pred[]
            if)#)#);
          doInsert::<
            (# 
            do this(node)[] -> N.father[]; inner
            #);
          delete:
            (* assumes N in sons *)
            (# N: ^Node; L: @integer
            enter N[]
            do sons.length - 1 -> L;
               (if true 
                //  N[] = sons.son[] then
                   (* First in list *)              
                    sons.next[] -> sons[];
                   (if sons[] <> none (* L = 0 *) then  L -> sons.length if)
                else 
                   sons.scan (* ????? *)
               if);
               (if sons[] <> none then
                   N[] -> sons.delete;
                   (if sons.length = 0 then none -> sons[] if)
               if)
            #);

          print:
            (#
            do (for i: level repeat ' ' -> LG.T for);
               PP -> LG.Tn;
               level + 1 -> level;
               (if sons[] <> none then sons.print if);
               level - 1 -> level
            #);
          
          findNodeAtPos::
            (#
            do L:
                 (if (beginPos <= pos) and (pos <= endPos) then
                     scanSons
                     (#
                     do  pos -> current.findNodeAtPos -> N[];
                        (if N[] <> none then 
                            (if trace then '>'-> put if);
                            leave L 
                        if)
                     #);
                     (if N[] = none then 
                         this(TopNode)[] -> N[]; 
                         (if trace then
                             newline; 
                             '[' -> put; beginPos -> putint; 
                             ',' -> put; endPos -> putint; ']' -> put;
            if)if)if)#);
          mapBCposToCharRange: 
            (# bcp: @integer; (* a bytecode position *)
               bPos,ePos,bPos1,ePos1: @integer; (* charposition of node *)
               trace: (# exit false #)
            enter bcp
            do (if trace then
                   'Map: ' ->putline; doPT -> puttext; ' ' -> put;
                   bcp -> putint; ' in: '->puttext;
                   beginBCP -> putint; ','->put; endBCP -> putint; newline;
                if);
               (if (beginBCP < bcp) and (bcp <= endBCP) then
                   (if trace then
                       'Inside: ' -> puttext;
                   if);
                   beginPos -> bPos; endPos -> ePos;
                   (if trace then bPos -> putint; ','->put; ePos -> putint;
                       newline if);
                   L:
                     (if sons[] <> none then
                         sons.scan
                         (#
                         do (if not current.isObjDesc then (* do not go into singular*)
                                bcp -> current.mapBCposToCharRange
                                  -> (bPos1,ePos1);
                                (if bPos1 > 0 then 
                                    bPos1 -> bPos; ePos1 -> ePos;
                                    leave L 
               if)if)#)if)if)
            exit(bPos,ePos)
            #);
          isLexem:< BooleanValue;
          LexemSy:< (# L: ^text do inner exit L[] #);
          theModule::<
            (#
            do inner;
               (if md[] = none then
                   (if origin[] = none then
                       'thModule:origin is none: ' -> puttext;
                       doPT -> putline
                    else
                       origin.theModule -> md[]; 
               if)if)
            #);
          check:< (# do inner #);
          handlePrimitives:< 
            (# dcl: ^TopNode enter dcl[] do inner exit dcl[] #);
          search:< 
            (# N: ^text; dcl,withAd,withP: ^TopNode; on: @integer  
            enter N[] 
            do inner 
            exit(withAD[],withP[],dcl[],on) 
            #);
          localSearch:< 
            (# N: ^text; dcl: ^TopNode enter N[] do inner exit dcl[] #);
          hasInner:< (# on,value: @integer enter on do inner exit value #);
          goOrigin:< 
            (# ON: @integer; OD: ^TopNode; AD: ^TopNode (*AttributeDenotation*)
            enter(ON,AD[])
            do inner 
            exit OD[] 
            #);
          gen:< (# do inner #);
          emitForFields:< (# forNo: @integer enter forNo do inner #);
          (*origin: ^node; See Tree:TopNode:origin*)
          vNode:: Node;
          theStream: ^stream; 
          T: ^Text;
          
          traceCheck:
            (# T: ^text;
            enter T[]
            do (if true then
                   T[] -> dumpT;
                   (if origin[] = none then
                       ' origin:none '->dumpT;
                   if);
                   inner
               if)
            #);
          traceCheckN: traceCheck
            (#
            do newline
            #);
          (*lex.getComment -> comment[];*)
       do inner
       #);
     lexem: Node
       (# item: ^text;
          isLexem:: TrueValue;
          PP:: (# do '\'' -> lx[]; item[] -> lx.puttext; '\'' -> lx.put  #);
          LexemSy:: (# do item[] -> L[] #);
          length:: (# do (if item[] <> none then item.length -> value if) #);
          PT:: (# do (if item[] <> none then item.copy -> lx.append if) #);
          isNameApl:: (# do father.isNameApl -> Na[] #);
       #);
     
     NodeList: TopNode
       (# Elm:< Node; (* Not used - we need a top super for Node and NodeList *)
          son: ^ elm (*Node*);
          next, last: ^NodeList;
          length: @integer;
          setFirst:
            (# N: ^Node; S: ^NodeList
            enter N[]
            do N[] -> son[]; 
               this(NodeList)[] -> last[];
               1 -> length
            #);
          append:
            (# N: ^Node; S: ^NodeList
            enter N[]
            do (if last[] = none then
                   (* first element being appended *)
                   N[] -> son[]; 
                   this(NodeList)[] -> last[];
                   1 -> length
                else
                   &NodeList[] -> S[] -> last.next[];
                   N[] -> S.son[];
                   S[] -> last[];
                   length + 1 -> length
               if)
            #);
          insert:
            (# N,atN: ^Node; before,S: ^nodeList;
            enter(N[],atN[])
            do ('NodeList:insert',N.doPT) -> LG.TTn;
               (if true 
                // last[] = none then
                   N[] -> setFirst
                // atN[] = NONE then
                   (* insert as last *)
                   N[] -> append
                // atN[] = this(nodeList).son[] then
                   (* insert first in list*)
                   &NodeList[] -> S[];
                   son[] -> S.son[];
                   next[] -> S.next[];
                   S[] -> next[];
                   N[] -> son[];
                   (if last[] = THIS(nodeList)[] then
                       S[] -> last[];
                   if);
                   length + 1 -> length;
                else (* find atN[] *)
                  ('Search: ',atN.doPT) -> LG.TTn;
                   this(nodeList)[] -> before[];
                   L:
                     scan
                     (#
                     do (if current[] = atN[] then
                           'Insert:found:beforeIs:'->putline;
                            before.son.doPT -> putline;
                            leave L
                         else
                            NL[] -> before[]
                        if)
                     #);
                   &NodeList[] -> S[];
                   N[] -> S.son[];
                   before.next[] -> S.next[];
                   S[] -> before.next[];
                   length + 1 -> length;
               if);
            #);
          delete:
            (# N: ^node; before: ^nodeList
            enter N[]
            do 'AST:sons:delete:'->puttext;
               this(nodeList)[] -> before[];
               L:
                 scan
                 (#
                 do (if N[] = current[] then (* never the first in list *)
                        'match' -> putline;
                        NL.next[] -> before.next[];
                        (if before.next[] = none then
                            (* deleted last element *)
                            before[] -> last[];
                        if);
                        leave L
                     else
                        NL[] -> before[]
                    if)
                 #)
            #);
          scan:
            (# current: ^elm (*Node*); NL: ^NodeList;
               isLast: BooleanValue(# do NL.next[] = none -> value #)
            do this(Nodelist)[] -> NL[]; 
               L: (if NL[] <> none then 
                      NL.son[] -> current[];
                      inner;
                      NL.next[] -> NL[];
                      restart L
                  if)
            #);
          scanReverse:
            (# current: ^elm (*Node*);
               L: [5] ^elm(*node*); top: @integer
            do scan(# do (if (top+1->top) > L.range then L.range -> L.extend if);
                      current[] -> L[top][]
                   #);
               (for i: top repeat 
                    L[top+1-i][] -> current[];
                    INNER
               for)
            #);
          print:
            (#
            do (if son[] = none then 'Son is none:' -> LG.Tn else son.print;  if);
               (if next[] <> none then next.print if)
            #);

       do inner
       #);
     new:
       (# cat: @integer; N: ^Node
       enter cat
       do &node[] -> N[];
          cat -> N.cat
       exit N[]
       #);
     newLexem: 
       (# sym: ^text; N: ^lexem
       enter sym[]
       do &lexem[] -> N[];
          sym[] -> N.item[]
       exit N[]
       #);     
     lex: ^StdBetaLexer;     
  do
     inner
  #)
