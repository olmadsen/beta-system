SimpleValueBench: obj
   vPoint: Value
      in x: var float
      in y: var float
      in z: var float
      vFoo:
         vBar
      vBar:<
         ...
      translate:
         in dx: var float
         in dy: var float
         x := x + dx
         y := y + dy
   mPoint:
      in x: var float
      in y: var float
      in z: var float
      equals:
         in p: ref mPoint
         out B: var Boolean
         B := (x = p.x) && (y = p.y) && (z = p.z)
      mFoo:
         mBar
      mBar:<
         ...
      translate:
         in dx: var float
         in dy: var float
         x := x + dx
         y := y + dy
   vp1: var vPoint(1,2,3)
   vp2: var vPoint(1,2,3)  
   vp3: var vPoint(1,4,5)  
   mp1: obj mPoint(10,11,12)
   mp2: obj mPoint(10,11,12)
   mp3: obj mPoint(10,1,15)
   tester:
      in n: var integer
      inner(tester)
   testValueEQ: tester
      if (n = 1) :then
         "**** testValueEQ:".print
         if (vp1 = vp2) :then
            "vp1=vp2:OK,".print
         :else
            "vp1<>vp:wrong,".print  
         if (vp1 = vp3) :then
            "vp1=vp2:wrong".print
         :else
            "vp1<>vp2:OK".print
      :else
         put('.')
      bench1: do
         i: var integer
         i := i + 1
         B: var boolean
         B := vp1 = vp2
         B := vp1 = vp3
         if (i <= max) :then
            restart(bench1)
   testMaterialEquals: tester
      if (n = 1) :then
         "**** testMaterialEquals:".print
         if (mp1.equals(mp2)) :then
            "mp1=mp2:OK,".print
         :else
            "mp1<>mp:wrong,".print  
         if (mp1.equals(mp3)) :then
            "mp1=mp3:wrong".print
         :else
            "mp1<>m3:OK".print 
      :else
         put('.')
      bench2: do
         i: var integer
         i := i + 1
         B: var boolean
         B := mp1.equals(mp2)
         B := mp1.equals(mp3)
         if (i <= max) :then
            restart(bench2)
   testMaterialEQxyz: tester
      if (n = 1) :then
         "**** testMaterialEQxyz:".print
         if ((mp1.x = mp2.x) && (mp1.y = mp2.y) && (mp1.z =   mp2.z)) :then
            "mp1.x=mp2.x...:OK,".print
         :else
            "mp1.x<>mp2.x...:wrong,".print  
         if ((mp1.x = mp3.x) && (mp1.y = mp3.y) && (mp1.z = mp3.z)) :then
            "mp1.x=mp3.x...:wrong".print
         :else
            "mp1.x<>mp3.x...:OK".print 
      :else
         put('.')
      bench3: do
         i: var integer
         i := i + 1
         B: var boolean
         B := (mp1.x = mp2.x) && (mp1.y = mp2.y) && (mp1.z = mp2.z)
         B := (mp1.x = mp3.x) && (mp1.y = mp3.y) && (mp1.z = mp3.z)
         if (i <= max) :then
            restart(bench3)
   testValueVirtual: tester
      if (n = 1) :then
         "**** testValueVirtual:".print
      :else
         put('.')
      vp1.vFoo
      bench4: do
         i: var integer
         i := i + 1
         vp1.vFoo
         if (i <= max) :then
            restart(bench4)
   testMaterialVirtual: tester
      if (n = 1) :then
         "**** testMaterialVirt:".print
      :else
         put('.')
      mp1.mFoo
      bench5: do
         i: var integer
         i := i + 1
         mp1.mFoo
         if (i <= max) :then
            restart(bench5)
   testValueTranslate: tester
      if (n = 1) :then
         "**** testValueTranslate:".print
      :else
         put('.')
      vp1.translate(0.001,0.001)
      bench6: do
         i: var integer
         h: var integer
         i := i + 1
         h := markHeap(h)
         vp1.translate(0.001,0.001)
         if (i <= max) :then
            restart(bench6)
   testMaterialTranslate: tester
      if (n = 1) :then
         "**** testMaterialTranslate:".print
      :else
         put('.')
      mp1.translate(0.001,0.001)
      bench7: do
         i: var integer
         h: var integer
         i := i + 1
         h := markHeap(h)
         mp1.translate(0.001,0.001)
         if (i <= max) :then
            restart(bench7)
   printCPUtime:
       "CPU time: ".print
       putint(cpuTime - startTime)
       " microseconds\n".print
       startTime := cpuTime
   printStat:
       in bench: ref #Array
       min: var integer
       max: var integer
       avg: var integer
       newline
       for (1) :to(bench.length) :repeat
           V: var integer
           V := bench.get[inx]
           putint(V)
           put(' ')
           if (min = 0) :then
              min := V
           if( V < min) :then
              min := V
           if (max < V) :then
              max := V
           avg := avg + V 
       "\nNo.of runs: ".print
       putint(runMax)
       ", CPU time: min = ".print
       putint(min)
       --" 0.25-frac = ".print
       --putint(bench.get[bTop//4])
       " max = ".print
       putint(max)
       " average = ".print
       avg := avg // bench.length
       putint(avg)
       newline
   startTime: var integer
   endTime: var integer
   max: val 500000
   select: val 5
   runMax: val 10
   heapTop: var Integer
   bench: obj Array(runMax,#integer)
   bTop: var integer
   startTime := cpuTime
   runTest:
      heapTop := 0
      bTop := 0
      for (1):to(runMax):repeat 
          heapTop := markHeap(heapTop)
          startTime := cpuTime
          bTop := bTop + 1
          inner(runTest)
          bench.put(cpuTime - startTime):at[bTop]
      printStat(bench)
   runTest
      testValueEQ(bTop)
   runTest
      testMaterialEquals(bTop)
   runTest
      testMaterialEQxyz(bTop)
   runTest
      testValueVirtual(bTop)
   runTest
      testMaterialVirtual(bTop)
   runTest
      testValueTranslate(bTop)
   runTest
      testMaterialTranslate(bTop)