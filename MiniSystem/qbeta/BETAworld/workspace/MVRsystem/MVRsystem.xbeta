MVRsystem: obj
   %requires Collections, Collections.Set
   %visible Collections
   BooleanValue:
      out B: var Boolean
      inner(BooleanValue)
   LicenseNumber: Value
      in V: var integer
      print:
         "LicenseNumber:".print
         putint(V)	 
   Person:
      in name: ref StringLib.String
      %association registrations, #Registration, "*", inRegistrations
      registrations: obj Set.Set(#Registration) -- # is needed!
      inRegistrations: BooleanValue
         B := registrations.reduce({in m: ref Registration; in c: var Boolean; out V: var boolean;  V := m.theOwner == (this(Person))},true)
         -- for all registrations, theOwner is this(Person)        
      print:
         "Person:".print
         name.print
         put(':')	 
   MotorVehicle:
      in lcn: var LicenseNumber(0)
      %association theRegistration, #Person, 1, isRegistration 
      theRegistration: ref Registration
      isRegistration: BooleanValue
          B := theRegistration.theVehicle == (this (MotorVehicle))
          -- the vehicle in registration is this(MotorVehicle)
      print:
         "MotorVehicle:".print
         lcn.print	 
   Registration:
      %association MVR, MotorVehicleRegister, 1 -- a new one
      %association theVehicle, vehicle, 1
      %association owner, #Person, 1
      MVR: BooleanValue
         B := MotorVehicleRegister.registrations.has(this(Registration))
      vehicle: BooleanValue
         B := theVehicle.theRegistration == (this(Registration))
      owner: BooleanValue
         B := theOwner.registrations.has(this(Vehicle))
      in theVehicle: ref MotorVehicle
      in theOwner: ref Person
      print:
         "Registration:".print
         theVehicle.print
         put(',')	 
         theOwner.print
         newline	 
   MotorVehicleRegister: obj
      %association #Registration, Registrations, "*", isValid
      registrations: obj Set.set(#Registration)
      isValid: BooleanValue
         B := registrations.reduce({in R: ref Registration; in c: var boolean; out V: var Boolean; V := R.theOwner.registrations.has(R)},true)
      register:
         in P: ref Person
         in V: ref MotorVehicle
         R: ref Registration
         R := Registration(V,P)
         registrations.insert(R)
         V.theRegistration := R
         P.registrations.insert(R)
         checkAssertions(P,V,R)
      checkAssertions:
         check:
             in id: ref StringLib.string
             in cond: var Boolean
             id.print
             @if cond :then
                ":valid:".print
             :else
                ":not valid:".print     
         in P: ref Person
         in V: ref MotorVehicle
         in R: ref Registration	          
         check("MVR",isValid)
         check("Person",P.inRegistrations)
         check("Vehicle",V.isRegistration)
         check("Registration", R.MVR || R.vehicle || R.owner)
         newline
      print:
         registrations.scan --forAll
            R: ref Registration
            R:= current
            R.print	    
   makeRegistrations:
      P: ref Person
      V: ref MotorVehicle
      R: ref Registration
      P := Person("Birger")
      V:= MotorVehicle(LicenseNumber(1234))
      MotorVehicleRegister.register(P,V)
      MotorVehicleRegister.register(Person("Kirsten"),MotorVehicle(LicenseNumber(6789)))
      MotorVehicleRegister.register(Person("Marianne"),MotorVehicle(LicenseNumber(2234)))  
   makeRegistrations
   MotorVehicleRegister.print
      