ValueEx: obj
   Point(X: var  Integer, Y: var  Integer): Value
      [globals[]]
      move(dx: var integer, dy: var integer):
          X := X + dx
          Y := Y + dy
      moveTo(P: var  Point(0,0)):
          X := P.X
          Y := P.Y
      print:<
         "Point(".print
         putint(X)
         put(',')
         putint(Y)
         %inner print
         put(')')
   ColorPoint(C: var integer): Point      
       print::<
          %if C = 1 %then%
              ",red".print
          %else%
             ",green".print
   Line(X1: var integer,X2: var integer) : Value
      [globals[]]
      PL1: var  Point(X1,3)
      PL2: var  Point(X2,13)
      moveLine(Pa: var Point(0,0),Pb: var Point(0,0)):
         PL1 := Pa
         PL2 := Pb
      print:
         "Line[".print
         PL1.print
         put(',')
         PL2.print
         put(']')
   q1: var integer
   q2: var integer
   q3: var integer
   P1: val Point(3,5)          -- 5:
   P2: var Point(0,0)          -- 7:
   P3: val Point(111,112)      -- 9
   CP: val ColorPoint(9,10,1)  -- 11:
   L1: var Line(5,3)           -- 14:
   L2: var Line(6,18)
   P1.print
   newline
   P1.move(7,11)
   P2.X := P1.X
   P2.Y := P1.Y
   P2.print
   newline
   L1.print
   newline
   CP.print
   newline
   P2 :=  P3
   P2.print
   newline
   L1 := L2
   L1.print
   newline
   P1.moveTo(P3)
   P1.print
   newline
   P1.moveTo(Point(222,333))
   P1.print
   newline
   P2 := Point(777,888)
   P2.print
   newline
   L2.moveLine(P1,Point(444,555))
   L2.print
   q1 := 117
 




