SortableEx: obj 
   Iterator: 
      Elm:< Object
      hasMore:< 
         out B: var Boolean
         inner(hasMore)
      next:< 
         out e: ref Elm
         inner(next)
      reset:< 
         inner(reset)
   Sortable: 
      Elm:< Object
      cmp:< 
         in x: ref Elm -- problem if x:< ref Elm
         in y: ref Elm
         out b: var Boolean
         inner(cmp)
      swap:< 
         in x: ref Elm
         in y: ref Elm
         inner(swap)
      sort: 
         it:< ref Iterator
         current: ref Elm
         pred: ref Elm
         wasChanged: var Boolean
         "sort\n".print
         doSort: do 
            if (it.hasMore) :then 
               pred := it.next
               pass: do 
                  if (it.hasMore) :then 
                     current := it.next
                     if (cmp(pred,current).not) :then 
                        swap(pred,current)
                        wasChanged := true
                     pred := current
                     restart(pass)
               if (wasChanged) :then 
                  wasChanged := false
                  it.reset
                  restart(doSort)
      inner(sortable)
   Record: 
      id:< var integer
   Bag: 
      A: obj IndexedRef(100,#Record)
      top: var integer
      add: 
         R:< ref Record
         top := top + 1
         A.put(R):at[top]
      asSortable: obj Sortable
         Elm:: 
            Ix:< var integer
         BagIterator: Iterator
            Elm:: Record
            hasMore:: 
               B := currentIx < top
            next:: 
               currentIx := currentIx + 1
               e := Record(currentIx)
            reset:: 
               currentIx := 0
            currentIx: var integer
         cmp:: 
            b := A.get[x.ix].id <= A.get[y.ix].id
         swap:: 
            tmp: ref Record
            tmp := A.get[x.ix]
            A.put(A.get[y.ix]):at[x.ix]
            A.put(tmp):at[y.ix]
      sort: 
         asSortable.sort(asSortable.BagIterator)
      print: 
         for(1):to(top):repeat
            putint(A.get[inx].id)
            put(' ')
         
   B: obj Bag
   B.add(Record(12))
   B.add(Record(7))
   B.add(Record(17))
   B.add(Record(2))
   B.add(Record(21))
   B.add(Record(5))
   B.add(Record(19))
   B.print
   newline
   B.sort
   B.print