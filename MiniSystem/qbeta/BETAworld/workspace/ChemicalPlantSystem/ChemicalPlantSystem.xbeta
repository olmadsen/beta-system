ChemicalPlantSystem: obj
   %include Containers
   Qualification:
      in Q: var integer
      print:
         "Qualification:".print
         putint(Q)
         newline	  
   Qualifications: Set.Set(#Qualification)
      dum: var integer
   
   Expert:
      in quali: ref Qualifications
      getQuali:
         out Q: ref Qualifications
         Q := quali
   Alarm:
      in descr: ref String.String
      in reqQuali: ref Qualification
      GetReqQuali:
         out Q: ref Qualification
         Q := reqQuali
   Alarms: Set.Set(#Alarm)

   Period:
      in start: var integer
      in end: var integer
      
   Schedule: MapLib.Map(#Period,Set.Set(#Expert))
   
   Plant:
      in theAlarms: ref Alarms
      in theSchedule: ref Schedule
      PlantInv:
          out B: var Boolean
          B := true	  
          theSchedule.forAll
              S: ref Set.Set
              S:= current.elm
              B := B && S.isEmpty.not
          theAlarms.forAll -- for all A in alarm
              A: ref Alarm
              A := current
              Q: ref Qualification	      
              Q:=A.getReqQUali
              Q.print	      
              theSchedule.forAll -- for all current in schedule
                 -- an Expert E with qualifications needed 
                 -- for the Alarm A must exist
                 S: ref Set.Set
                 S:= current.elm
                 bbb: var Boolean
                 bbb := S.exists({in E: ref Expert; out z: var Boolean; z := E.getQuali.hass(A.GetReqQuali)})
                 B := B && bbb

   Q: obj Qualification(17)
   QS: obj Qualifications
   
   E: ref Expert
   ES: obj set.set(#Expert)
   A: ref Alarm
   AS: obj Alarms
   
   S: obj Schedule
   
   P: obj Plant(AS,S)

   QS.insert(Q)

   A := Alarm("OverHeat",Q)
   ES.insert(Expert(QS))
   S.add(Period(0,8),ES)
   AS.insert(A)
   
   @if P.PlantInv :then
      "P.plantInv = true\n".print
   :else
      "P.plantInv = not true\n".print
   --AS.insert(none)
   --put('E')
   
   
   