ChemicalPlantSystem: obj
   %include Containers
   Qualification:
      in Q: var integer
      print:
         "Qualification:".print
         putint(Q)
         newline	  
   Qualifications: Set.Set(#Qualification)
      dum: var integer
   
   Expert:
      in quali: ref Qualifications
      getQuali:
         out Q: ref Qualifications
         Q := quali
   Experts: Set.Set(#Expert)
   
   Alarm:
      in descr: ref String.String
      in reqQuali: ref Qualification
      GetReqQuali:
         out Q: ref Qualification
         Q := reqQuali
   Alarms: Set.Set(#Alarm)

   Period:
      in start: var integer
      in end: var integer
   Periods: Set.Set(#Period)
   
   Schedule: MapLib.Map(#Period,#Experts)
   
   Plant:
      in theAlarms: ref Alarms
      in theSchedule: ref Schedule
      PlantInv:
          out B: var Boolean
          B := true	  
          theSchedule.forAll
              S: ref Set.Set
              S:= current.elm
              B := B && S.isEmpty.not
          theAlarms.forAll -- for all A in alarm
              A: ref Alarm
              A := current
              theSchedule.forAll -- for all current in schedule
                 -- an Expert E with qualifications needed 
                 -- for the Alarm A must exist
                 S: ref Set.Set
                 S:= current.elm
                 bbb: var Boolean
                 bbb := S.exists({in E: ref Expert; out R: var Boolean; R := E.getQuali.has(A.GetReqQuali)})
                 B := B && bbb
      ExpertToPage:
         in a: ref Alarm
         in p: ref Period
         out exp: ref Expert
         ES: ref Experts
         ES:= theSchedule.lookup(p)
         ES.scan
            B: var Boolean
            Q: ref Qualification	    
            exp := current
            Q := a.GetReqQuali
            B := exp.getQuali.has(Q)
            @if B :then
                @leave ExpertToPage
      numberOfExperts:
         in p: ref Period
         out n: var integer
         ES: ref Experts
         ES := theSchedule.lookup(p)
         n := ES.card
      expertIsOnDuty:
         in ep: ref Expert
         out fp: obj Periods
         theSchedule.forAll
            ES: ref Experts	    
            ES := current.elm 
            @if ES.has(ep) :then
                fp.insert(current.inx)

   Q: obj Qualification(17)
   QS: obj Qualifications
   
   E: ref Expert
   ES: obj set.set(#Expert)
   A: ref Alarm
   AS: obj Alarms
   
   S: obj Schedule
   
   P: obj Plant(AS,S)

   QS.insert(Q)

   A := Alarm("OverHeat",Q)
   ES.insert(Expert(QS))
   S.add(Period(0,8),ES)
   AS.insert(A)
   
   @if P.PlantInv :then
      "P.plantInv = true\n".print
   :else
      "P.plantInv = not true\n".print
   --AS.insert(none)
   --put('E')
   
   
   