RoadMap: obj 
   %module SegmentLib, RoadLib, Point3Dlib, CityLib
   maxRoads: val 100
   maxCities: val 10
   roadTop: var integer
   roads: obj IndexedRef(maxRoads,RoadLib.Road("","",50))
   cityTop: var integer
   cities: obj IndexedRef(maxCities,CityLib.City("",Point3Dlib.Point3D(0,0,0)))
   addRoad:
      in R: ref RoadLib.Road
      roadTop := roadTop + 1
      roads.put(R):at[roadTop]
      newline
   createO2:
      R: obj RoadLib.Road("O2","Ringgaden",100)
      R.addPoint(Point3Dlib.Point3D(1.2,2.2,3.3))
      R.addPoint(Point3Dlib.Point3D(2.4,3.3,2.3))
      R.addPoint(Point3Dlib.Point3D(3.4,4.3,2.3))
      R.addPoint(Point3Dlib.Point3D(4.5,5.2,2.7))
      addRoad(R)
      R.print
   createP15:
      R: obj RoadLib.Road("15","Herning motorvejen",100)
      R.addPoint(Point3Dlib.Point3D(10.2,20.2,3.3))
      R.addPoint(Point3Dlib.Point3D(20.4,30.3,2.3))
      R.addPoint(Point3Dlib.Point3D(30.4,40.3,2.3))
      R.addPoint(Point3Dlib.Point3D(40.5,50.2,2.7))
      addRoad(R)
      R.print
   createRoads:
      createO2
      newline
      createP15
      newline
   addCity:
      in c: ref CityLib.City
      cityTop := cityTop + 1
      cities.put(C):at[cityTop]
      c.print
   Aarhus: obj CityLib.City("Aarhus",Point3Dlib.Point3d(10.2,20.2,3.3))
   createAarhus:
      addCity(Aarhus)
   Herning: obj CityLib.City("Herning",Point3Dlib.Point3D(40.5,50.2,2.7))
   createHerning:
      addCity(Herning)
   createCities:
      createAarhus
      newline
      createHerning
   scanRoads:
      currentRoad: ref RoadLib.Road
      for (1):to(roadTop):repeat
         currentRoad := roads.get[inx]
         inner(scanRoads) 
   findRoads:
      in cityA: ref CityLib.City
      in cityB: ref CityLib.City
      "\n**** Find a road between:\n".print
      cityA.print
      "and\n".print
      cityB.print
      scanRoads   
         if (cityA.intersect(currentRoad)) :then
            -- cityA.print
            -- "intersect: ".print
            -- currentRoad.print
            if (cityB.intersect(currentRoad)) :then
               --cityB.print
               --"also intersect: ".print
               "*** They are connected via:\n".print
               currentRoad.print
   createRoads    
   createCities
   findRoads(Aarhus,Herning) 

