RoadLib: obj
   %module RoadMap,Point3Dlib
   class Road:
      in routeNumber: var String
      in streetName: var String
      in maxSize: var integer
      top: var integer
      coordinates: obj Array(maxSize,#Point3Dlib.Point3D(0,0,0))
      addPoint:
         P:< var Point3Dlib.Point3D(0,0,0)
         top := top + 1
         if (top <= maxSize) :then
            coordinates.put(P):at[top]
      distance:
         -- compute distance from coordinates[1]-coordinates[top]
         out dist: var float
         for (1):to(top - 1):repeat
             dist := dist + Point3Dlib.distance(coordinates.get[inx],coordinates.get[inx + 1])
      scanCoordinates:
         currentCoordinate: var Point3Dlib.Point3D(0,0,0)
         for(1):to(top):repeat
            currentCoordinate := coordinates.get[inx]
            inner(scanCoordinates)
      print:
         ("Road number: " + routeNumber + ",").print
         ("name: " + streetName + "\n").print
         "coordinates:\n".print
         for (1):to(top):repeat
             px: var Point3Dlib.Point3D(0,0,0)
             px := coordinates.get[inx]
             PX.prinT
         "length: ".print
         putfloat(distance)
      test:
         P: var Point3Dlib.Point3D(0,0,0)
         P := coordinates.get[1]
         "test:p:\n".print
         p.print
         P := coordinates.get[2]
         p.print
   RoadNetwork:
      in maxRoads: val 100
      roadTop: var integer
      roads: obj Array(maxRoads,RoadLib.Road("","",50)) 
      add:
         in R: ref RoadLib.Road
         roadTop := roadTop + 1
         roads.put(R):at[roadTop]
      scan:
         currentRoad: ref RoadLib.Road
         for (1):to(roadTop):repeat
             currentRoad := roads.get[inx]
             inner(scan) 
      