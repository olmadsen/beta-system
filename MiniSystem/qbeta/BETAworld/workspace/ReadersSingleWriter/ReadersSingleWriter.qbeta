ReadersSingleWriter: obj
   -- How do you esnure that only a Writer invokes readerEntry-Methods?
   --     perhaps return a WriterInterface
   -- How do you ensure that a Reader does not write?
   --     can we define a reader interface
   --     so all data must be encapsulated in objects that the writer can write
   --     and a reader can read
   --   ReaderEntry: %globals - none or just shared data
   --   WriterEntry: ditto
   -- perhaps we need a %reader property to specify that some objects may
   -- only read and some may write - not obvious to encapsulate in a super class
   -- since new data and methods may be added in subclasses
   AtomicCounter:
      L: obj Lock
      c: var integer
      inc:
         L.get
         c := c + 1
         L.free	 
      get -> V: var integer:
         L.get
         V := c
         L.free	 

   System: LIB.BasicSystem.BasicSystem
      writerLock: obj Lock
      ac: obj AtomicCounter      
      Reader: BasicProcess
          inner(Reader)

      Writer: BasicProcess
         writerLock.get
         inner(Writer)
         writerLock.free

      class ReaderRegion:         
         writerEntry: -- only a writer may use this ...
            ac.inc
            inner
            ac.inc
         readerEntry:   -- only a reader
            V: var integer
            V := ac.get
            inner(readerEntry)
            if (V <> ac.get) :then
               put('.')	    
               restart(readerEntry)
   ex: obj System
      start:
         W.start
         R1.start
         R2.start
         R3.start
      CR: obj ReaderRegion
         S: var String
         putS(R: var String): writerEntry  -- only Writer may invoke
            S := R
         getS -> R: var String: readerEntry -- only Reader may invoke
            R := S
      W: obj Writer("W")
         for (1):to(10):repeat
              CR.putS("Hello:" + I2S(inx))
              sleep(10)	 
      R1: obj Reader("R1")
         console.display
            CR.getS.print
      R2: obj Reader("R2")
         console.display
            CR.getS.print
      R3: obj Reader("R3")
         console.display
            CR.getS.print
   ...
   "Hello\n".print
   ex.start
   