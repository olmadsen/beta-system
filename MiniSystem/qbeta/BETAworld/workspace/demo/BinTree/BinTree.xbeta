BinTree: obj 
   @include LIB.string
   xBinTree: 
      node: 
         in elm: ref String
         left: ref node
         right: ref node
         insert: 
            in S: ref String
            @if S <= elm :then 
               @if left = none :then 
                  left := node(S) 
               :else 
                  left.insert(S) 
            :else 
               @if right = none :then 
                  right := node(S) 
               :else 
                  right.insert(S)
      root: ref node
      insert: 
         in S: ref string
         @if root = none :then 
            root := node(S) 
         :else 
            root.insert(S)
      scanner: 
         CV: ref String
         next: 
            out V: ref String
            (@this scanner).resume
            V := CV
         scan: 
            in current: ref node
            @if current <> none :then 
               scan(current.left)
               CV := current.elm
               (@this scanner).suspend
               scan(current.right)
         (@this scanner).suspend
         scan(root)
         cv := ""
         (@this scanner).suspend
         "\n*** Calling terminated coroutine\n".print
      sc: obj scanner
   boys: obj xBinTree
   girls: obj xBinTree
   merge: 
      B: ref String
      G: ref String
      tail: 
         in T: ref xBinTree
         cycle
            S: ref String
            S := T.sc.next
            @if S = "" :then 
               @leave tail 
            :else 
               S.print
               newline
      B := boys.sc.next
      G := girls.sc.next
      loop: 
         @if B <= G :then 
            @if B = "" :then 
               -- S <> "" does not work, check def of <> in String
               @if G.length > 0 :then 
                  tail(girls) 
            :else 
               B.print
               newline
               B := boys.sc.next
               @restart loop 
         :else 
            @if G = "" :then 
               "\nPrint tail of Boys: ".print
               B.print
               newline
               tail(boys) 
            :else 
               G.print
               newline
               @if B.length > 0 :then 
                  G := girls.sc.next
               @restart loop
      loop
   boys.insert("Peter")
   boys.insert("Lars")
   boys.insert("Anders")
   boys.insert("Petrus")
   girls.insert("Annabella")
   girls.insert("Anna")
   merge