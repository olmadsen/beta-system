BinTree: =
   %include LIB.string
   xBinTree:
      node(elm: ? String):
         left: ?node
         right: ?node
         insert(S: ?String):
            %if S <= elm  %then%
                %if left = none %then%
                    left := node(S)
                %else%
                    left.insert(S)
            %else%
                %if right = none %then%
                    right := node(S)
                %else%
                    right.insert(S)
      root: ?node
      insert(S: ?string):
         %if root = none %then% 
             root := node(S)
         %else%
             root.insert(S)
      scanner:
         CV: ? String
         next -> V: ?String:
             (%this scanner).resume
             V := CV
         scan(current: ?node):
             %if current <> none %then%
                 scan(current.left)
                 CV := current.elm
                 (%this scanner).suspend
                 scan(current.right)
         (%this scanner).suspend
         scan(root)
         cv := ""
         (%this scanner).suspend
         "\n*** Calling terminated coroutine\n".print
      sc: = scanner
   boys: = xBinTree
   girls: = xBinTree
   merge:
       B: ?String
       G: ?String
       tail(T: ? xBinTree):
           cycle
              S: ? String
              S := T.sc.next;
              %if S = "" %then%
                 %leave tail          
              %else%
                 S.print;
                 newline
       B:= boys.sc.next
       G:= girls.sc.next
       loop: 
          %if B <= G %then%
              %if B = "" %then%
                  -- S <> "" does not work, check def of <> in String
                  %if G.length > 0 %then% 
                      tail(girls)
              %else%
                  B.print;
                  newline
                  B:= boys.sc.next
                  %restart loop
          %else%
              %if G = "" %then%
                  "\nPrint tail of Boys: ".print;
                  B.print;
                  newline
                  tail(boys)
              %else%
                  G.print
                  newline
                  %if B.length > 0 %then%
                      G := girls.sc.next
                  %restart loop
       loop
   boys.insert("Peter")
   boys.insert("Lars")	
   boys.insert("Anders")
   boys.insert("Petrus")
   girls.insert("Annabella")
   girls.insert("Anna")
   merge


      
