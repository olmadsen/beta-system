BinTree: =
   %include LIB.string
   xBinTree:
      node(elm: ? String):
         left: ?node
         right: ?node
         insert(S: ?String):
            %if S <= elm  %then%
                %if left = none %then%
                    left := node(S)
                %else%
                    left.insert(S)
            %else%
                %if right = none %then%
                    right := node(S)
                %else%
                    right.insert(S)               

      root: ?node
      insert(S: ?string):
             %if root = none %then% 
                 root := node(S)
             %else%
                 root.insert(S)
      scanner:
         CV: ? String
         next -> V: ?String:
             (%this scanner).resume
             V := CV
         scan(current: ?node):
             %if current <> none %then%
                 scan(current.left)
                 CV := current.elm
                 (%this scanner).suspend                 
                 scan(current.right)
         (%this scanner).suspend
         scan(root)
         cv := ""
         (%this scanner).suspend
         "\n*** Calling terminated coroutine\n".print
      sc: = scanner
   B: ?Boolean
   boys: = xBinTree
   girls: = xBinTree
   merge:
       B: ?String
       G: ?String
       L1: ?integer
       L2: ?integer
       tail(T: ? xBinTree):
           loop: =
              put(10)
              L: ?integer
              S: ? String
              S := T.sc.next;
              L:= S.length
              %if L > 0 %then%
                 S.print;
                 %restart loop           
       B:= boys.sc.next
       G:= girls.sc.next
       L1 := B.length
       L2 := G.length
       loop:
          put(10)
          %if L1 = 0 %then%
              %if L2 = 0 %then%
                 "done\n".print
                 %leave loop          
          %if B <= G %then%
              B.print
              B := boys.sc.next
              L1:= B.length
              %if L1 = 0 %then%
                  tail(girls)
              %else%
                  %restart loop
          G.print
          G := girls.sc.next
          L2:= G.length
          %if L2 = 0 %then%
              tail(boys)
          %else%
              %restart loop
       loop
   S1: = "Hello"
   S2 : = "Ole"
   S3: ? String
   %if S1 = "HeLlo" %then% 
       put('?') 
   %else% 
       put('a')
   %if S1 = "Hello" %then%
       put('b')
   %else%
       put('?')
   %if S2 <= "Ola" %then%
       put('?')
   %else%
       put('c')
   %if S2 <= "Olm" %then%
       put('d')
   %else%
       put('?')
   put(10)
   boys.insert("Peter")
   boys.insert("Lars")	
   boys.insert("Anders")
   boys.insert("Petrus")
   girls.insert("Annabella")
   girls.insert("Anna")
   merge
