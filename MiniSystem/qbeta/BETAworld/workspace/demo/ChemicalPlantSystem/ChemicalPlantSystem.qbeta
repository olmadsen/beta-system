ChemicalPlantSystem: obj
   %requires Collections,MapLib,SetLib
   %visible Collections
   Qualification:
      %globals
      in Q: var integer
      print: {"Qualification:".print; putint(Q)}
   Qualifications: SetLib.Set(#Qualification)
      print: {scan{current.print}} 
   
   Expert:
      in name: ref StringLib.String   
      in quali: ref Qualifications
      %globals
      print:
         "Expert:".print
         name.print
         put(':')
         quali.print
         newline 
   Experts: SetLib.Set(#Expert)
      print: {scan{current.print}}
   
   Alarm:
      in descr: ref StringLib.String
      in reqQuali: ref Qualification
      %globals
      print:
         "alarm: ".print
         descr.print
         put(':')
         reqQuali.print
   Alarms: SetLib.Set(#Alarm)
      print:
         scan
            current.print
            put(',')
         newline
   Period:
      in start: var integer
      in end: var integer
      %globals
      print:
         "Period:".print
         putint(start)
         put(',')
         putint(end)
         newline 
   Periods: SetLib.Set(#Period)
      print:
         scan
            current.print
   
   Schedule: MapLib.Map(#Period,#Experts)
      dum: var integer	 
   
   Plant:
      in theAlarms: ref Alarms
      in theSchedule: ref Schedule
      PlantInvA:
          out B: var Boolean
          B := theSchedule.forAll({E.elm.isEmpty.not }) \
               && \
               theAlarms.forAll(
                  { in A: ref Alarm; out BB: var Boolean;
                    BB := theSchedule.forAll(
                     { E.elm.exists(
                       { in Ex: ref Expert; out R: var Boolean;
                         R := Ex.quali.has(A.reqQuali)})})})

      PlantInvB:
          out B: var Boolean
          B := true
          theSchedule.scan
              B := B && current.elm.isEmpty.not
          theAlarms.forAllX -- for all A in alarm
              A: ref Alarm
              A := current
              theSchedule.scan -- for all current in schedule
                 -- an Expert E with qualifications needed 
                 -- for the Alarm A must exist
                 bbb: var Boolean
                 bbb := current.elm.exists( 
                      {in E: ref Expert; out R: var Boolean; 
                       R := E.quali.has(A.reqQuali)})
                 B := B && bbb
      ExpertToPage(a: ref Alarm):period(p: ref Period):	
         out exp: ref Expert
         theSchedule.lookup(p).scan -- exp not defined, should be current?
            if(exp.quali.has(A.reqQuali)):then
                leave(#ExpertToPage:period)
      numberOfExperts:
         in p: ref Period
         out n: var integer
         n := theSchedule.lookup(p).size
      expertIsOnDuty:
         -- return in fp all periods where ep is on duty
         in ep: ref Expert
         out fp: obj Periods
         ep.print
         theSchedule.scan
            if (current.elm.has(ep)) :then
                fp.insert(current.inx)
         fp.print
      expertIsOnDutyR:
         in ep: ref Expert
         out fp: ref Periods	    
         fp:= theSchedule.select(
	     {in p: ref Period; in ES: ref Experts; out B: var boolean;
	        B := ES.has(ep);})
         fp.print
         "end-expertIsOnDutyR\n".print
   Q: obj Qualification(17)
   QS: obj Qualifications
   
   E1: ref Expert
   E2: ref Expert
   ES1: obj Experts -- SetLib.Set(#Expert){}
   ES2: obj Experts -- SetLib.Set(#Expert){}
   A: ref Alarm
   AS: obj Alarms
   
   S: obj Schedule
   
   P: obj Plant(AS,S){}
   QS.insert(Q)
   A := Alarm("OverHeat",Q)
   E1 := Expert("John",QS)
   ES1.insert(E1)
   E1:= Expert("Laura",QS)
   ES1.insert(E1)
   S.add(Period(0,8),ES1)
   E2:= Expert("Surayya",QS)
   ES2.insert(E2)
   S.add(Period(8,16),ES2)
   S.add(Period(16,24),ES1)
   AS.insert(A)
   AS.print
   if (P.PlantInvA) :then
      "P.plantInvA = true\n".print
   :else
      "P.plantInvA = not true\n".print
   if (P.PlantInvB) :then
      "P.plantInvB = True\n".print
   :else
      "P.plantInvB = NOT true\n".print
   newline

   "On duty:".print
   E1.print
   P.expertIsOnDuty(E1)
   "\n---\n".print
   "On duty:".print
   E2.print   
   P.expertIsOnDuty(E2)   
   newline
   
   "On duty R-variant:\n".print
   E1.print
   P.expertIsOnDutyR(E1)
   "On duty:".print
   E2.print
   P.expertIsOnDutyR(E2)    
   