MultipleServer: obj LIB.SocketSystem.SocketSystem
   Handler: BasicProcess("Handler")
      in theClient: ref Socket
      "Handler\n".print
      for (1) :to (3) :repeat
         ("Server:received: " + theClient.receive(this(Handler)) + "\n").print
         theClient.send("Got your message:-)")
      Server.terminated := true
   H: ref Handler
   Server: obj BasicProcess("Server")
      thisServer: obj Socket -- Ref=none without LIB - adr(Socket) 
      terminated: var boolean
      aClient: ref Socket
      put('B')
      "Server:\n".print
      thisServer.init(3000)
      thisServer.bind
      thisServer.listen
      aClient := thisServer.accept(Server)
      "Got accept\n".print
      H := Handler(aClient)
      waitHandler: 
         if (terminated.not) :then 
            server.suspend
            restart(waitHandler)
      waitHandler
      put('A')
      "Type a char to terminate server: ".print
      put(_iget)
      newline
      terminated := true
   Worker: obj BasicProcess("Worker")
      cycle
         for (1) :to (100):repeat
            inx := inx + 10
   "GO\n".print
   cycle
      put('*')
      if (Server.terminated) :then
          leave(cycle)
      Server.call
      if (H =/= none) :then 
         H.call
      Worker.attach(50)