TableServer: =  LIB.SocketSystem.SocketSystem

   Table: = 
       L: = Lock
       entries: =
          entry(name: ?String.String,address: ?String.String) :
                next: ?Entry
                find(nm: ?String.string) -> adr: ?String.String:
                     %if nm = name %then%
                         adr := address
                     %else%
                          %if none = next %then%
                              adr := "Not found"
                          %else%
                              adr := next.find(nm)
          head: ?Entry
       insert(nm: ?String.String, adr: ?String.String):
            L.get
            elm: = entries.entry(nm,adr)
            elm.next := entries.head
            entries.head:= elm
            L.free
       lookup(nm: ?String.String) -> adr: ?String.String:
            L.get
            -- head may be none
            adr := entries.head.find(nm)
            L.free
   Producer: = BasicProcess("Producer")
           PS: = Socket
           aProd: ?Socket 
           "Producer:\n".print
           PS.init(3000,Producer)
           PS.bind
           PS.listen
           aProd:= PS.accept
           cycle
               name: ?String.String
               adr: ?String.String
               name:= aProd.receive(Producer)
               adr:= aProd.receive(Producer)
               Table.insert(name,Adr)
               ("Inserted: " + name + ":" + adr + "\n").print
               aProd.send("Inserted: " + name + ":" + adr)
   Consumer: = BasicProcess("Consumer")
           CS: = Socket
           aCons: ?Socket 
           "Producer:\n".print
           CS.init(3001,Consumer)
           CS.bind
           CS.listen
           aCons:= CS.accept
           cycle
               name: ?String.String
               adr: ?String.String
               name:= aCons.receive(Consumer)
               adr:= Table.lookup(name)
               ("Address of: " + name + " is " + adr + "\n").print
               aCons.send("Address of: " + name + " is " + adr)
   cycle
      Producer.resume
      Consumer.resume
