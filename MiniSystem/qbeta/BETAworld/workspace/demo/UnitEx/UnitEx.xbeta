UnitEx: obj
   %include Dimensions
   Figure: 
      theArea:< 
         out A: var Area.SquareMeter
         @inner theArea
      print:< 
         printMore:< 
            @inner printMore
         AR: var Area.SquareMeter
         "Figure:".print
         @inner print
         "\nArea:".print
         AR := theArea
         AR.print
         printMore
   Rectangle: Figure
      width: var Length.Meter
      heigth: var Length.Meter
      set: 
         in w: var Length.Meter
         in h: var Length.Meter
         width := w
         heigth := h
      theArea:: 
         A := Area.mult(width,heigth)
      perimeter: 
         out V: var Length.Meter
         V := (width + heigth) * 2.0
      print:: 
         printMore:: 
            P: var Length.meter
            " Perimeter:".print
            P := Perimeter
            P.print
         "Rectangle:width:".print
         width.print
         ":heigth:".print
         heigth.print
   temperatureEx:
      C: var Temperature.Celsius
      F: var Temperature.Fahrenheit
      C := Temperature.FasCelsius(100.0)
      C.print
      newline
      F.fromBase(C.toBase)
      F.print
      newline
      C.fromBase(F.toBase)
      C.print
      newline
   L1: var Length.Meter
   L2: var Length.Foot
   L3: var Length.Meter
   R: obj Rectangle
   V: var Velocity.MperMin
   T: var Time.Minutes
   "Hello UnitEx\n".print
   L1 := Length.FasMeter(12.0)
   L1.print
   newline
   L2.fromBase(L1.toBase)
   L2.print
   newline
   L3 := Length.FasMeter(3.0)
   R.set(L1,L3)
   R.print
   newline
   T := Time.IasMinutes(3)
   T.print
   newline
   V:= Velocity.mult(L1,T)
   V.print
   newline
   "The square of ".print
   L1.print
   " is ".print
   L1.square.print
   newline
   
   "The minimum of ".print
   L1.print
   " and ".print
   L3.print
   " is ".print
   L1.min(L3).print
   newline

   "The maximum of ".print
   L1.print
   " and ".print
   L3.print
   " is ".print
   L1.max(L3).print
   newline
   A: var Area.SquareMeter
   A := L1 *** L3
   A.print
   newline
   temperatureEx