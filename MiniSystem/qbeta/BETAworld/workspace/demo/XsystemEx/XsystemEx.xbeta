XsystemEx: =
   System: LIB.BasicSystem.BasicSystem
      Process: Basicprocess
         start::
             SQS.insert(%this Process)
             %inner start
         Q: = LIB.Containers.mQueue.Queue
         entry:
            do:<
               %inner do
            Q.insert(%this entry)
         exeNext:
            E: ?entry
            Loop: =
                %if Q.isEmpty %then%
                    sleep(50)
                    %restart Loop
                E := Q.removeNext
                E.do  
         stop:< entry
             do::
                %inner do
                status := P_status.TERMINATED
                (%this Process).suspend
         %inner Process
         cycle
            exeNext
      %inner System
   Ex: = System
      Text:
         add(S: ?String.String):
             i := i + 1
             %if i = 1 %then% S1 := S
             %if i = 2 %then% S2 := S
         S1: ? String.String
         S2: ? String.STring
         i: ?integer
      P1: = Process("P1")
         stop::
            do::
              "Stop P1\n".print
              P2.stop
         P2.msg1("Hello ")
         P2.msg1("world\n")
         P2.eot
         P1.suspend
         P2.msg1("How ")
         P2.msg1("are you?\n")
         P2.eot
         stop
      P2: = Process("P2")
         msg1(m: ?String.String): entry
            do::
               m.print
               T.add(m)
         eot: entry
            do::
               "P2:eot\n".print
               P3.par(T)
               T := Text
         stop::
            do::
               "Stop P2\n".print
               P3.stop
         T: ? Text
         T := Text
      P3: = Process("P3")
          par(P: ? Text): entry
             do::
                "P3:".print
                P.S1.print
                P.S2.print
          stop::
             do::
               "Stop P3\n".print
      P1.start
      P2.start
      P3.start
