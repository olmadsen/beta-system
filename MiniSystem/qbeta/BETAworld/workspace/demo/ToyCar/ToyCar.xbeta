ToyCar: obj
   @include Dimensions, Containers
   Vehicle:
      avgVelocity: var Velocity.MperMin
      avgAcceleration: var Acceleration.MperMinP2
      computeAvgVelocity:
         out V: var Velocity.MperMin
         size: var integer	 
         AM.elms.scan
            E: ref AccMeasure
            E := current
            E.computeDistance
            E.computeAvgVelocity
            V:= V + E.avgVelocity
            size := size + 1    
         V := V / size
         "Vehicle:average velocity: ".print
         V.print
      computeAvgAcceleration:
         out V: var Acceleration.MperMinP2
         size: var integer	 
         AM.elms.scan
            E: ref AccMeasure
            E := current
            E.computeAvgAcceleration	    
            V:= V + E.avgAcceleration
            size := size + 1    
         V := V / size
         "Vehicle:average acceleration: ".print
         V.print	 
      AM: obj
         elms: ref List.List
         add:
           in E: ref AccMeasure
           elms:= List.List(E,elms)  
        	    
   AccMeasure:
      in initialPosition: var Length.Meter
      in finalPosition: var Length.Meter
      in duration: var Time.Minutes
      in initialVelocity: var Velocity.MperMin
      in finalVelocity: var Velocity.MperMin
      distance: var Length.Meter
      avgVelocity: var Velocity.MperMin
      avgAcceleration: var Acceleration.MperMinP2
      computeDistance:
          distance := finalPosition - initialPosition	  
      computeAvgVelocity:
         avgVelocity := Velocity.div(distance, duration)
      computeAvgAcceleration:
         avgAcceleration := Acceleration.div((finalVelocity - initialVelocity),duration)
      "AccMeasure:\n".print
      "   initial position: ".print
      initialPosition.print
      " final position: ".print
      finalPosition.print
      "\n   duration: ".print
      duration.print
      "\n   initialVelocity: ".print
      initialVelocity.print
      "\n   final velocity: ".print
      finalVelocity.print
      newline

   myCar: obj Vehicle
   M: ref AccMeasure
   
   M := AccMeasure(Length.F2M(0),Length.F2M(1),Time.I2M(1.7),Velocity.F2M(0),Velocity.F2M(4))
   myCar.AM.add(M)
   M := AccMeasure(Length.F2M(1),Length.F2M(2),Time.I2M(3.9),Velocity.F2M(3.9),Velocity.F2M(9.9))
   myCar.AM.add(M)

   myCar.computeAvgVelocity
   newline
   myCar.computeAvgAcceleration
   newline
   
   