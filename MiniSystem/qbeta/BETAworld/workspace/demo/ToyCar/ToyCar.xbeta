ToyCar: obj
   @include Dimensions, Associations
   Vehicle:
      avgVelocity: var Velocity.MperMin
      avgAcceleration: var Acceleration.MperMinP2
      computeAvgVelocity:
         AM.scan
            avgVelocity:= avgVelocity + current.avgVelocity
         avgVelocity := avgVelocity / AM.size
         "Vehicle:average velocity: ".print
         avgVelocity.print
      computeAvgAcceleration:
         AM.scan
            avgAcceleration:= avgAcceleration + current.avgAcceleration
         avgAcceleration := avgAcceleration / AM.size
         "Vehicle:average acceleration: ".print
         avgAcceleration.print
      AM: obj Association
         ElmType:: AccMeasure      
        
   AccMeasure:
      in initialPosition: var Length.Meter
      in finalPosition: var Length.Meter
      in duration: var Time.Minutes
      in initialVelocity: var Velocity.MperMin
      in finalVelocity: var Velocity.MperMin
      distance: var Length.Meter
      avgVelocity: var Velocity.MperMin
      avgAcceleration: var Acceleration.MperMinP2
      computeDistance:
          distance := finalPosition - initialPosition
      computeAvgVelocity:
         avgVelocity := Velocity.div(distance, duration)
      computeAvgAcceleration:
         avgAcceleration := Acceleration.div(finalVelocity - initialVelocity,duration)
      computeDistance
      computeAvgVelocity
      computeAvgAcceleration      
      "AccMeasure:\n".print
      "   initial position: ".print
      initialPosition.print
      " final position: ".print
      finalPosition.print
      "\n   duration: ".print
      duration.print
      "\n   initial velocity: ".print
      initialVelocity.print
      "\n   final velocity: ".print
      finalVelocity.print
      newline

   myCar: obj Vehicle
   M: ref AccMeasure
   
   M := AccMeasure(0"m",1"m",1.7"min",0"m/s",3.9"m/s")
   myCar.AM.add(M)
   M := AccMeasure(1"m",2"m",3.9"min",3.9"m/s",9.9"m/s")
   myCar.AM.add(M)

   myCar.computeAvgVelocity
   newline
   myCar.computeAvgAcceleration
   newline
   
   