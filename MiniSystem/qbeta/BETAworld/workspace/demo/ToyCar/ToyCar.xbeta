ToyCar: obj
   @include Dimensions, Associations
   Vehicle:
      avgVelocity: var Velocity.MperMin
      avgAcceleration: var Acceleration.MperMinP2
      computeAvgVelocity:
         size: var integer
         AM.scan
            avgVelocity:= avgVelocity + current.avgVelocity
            size := size + 1    
         avgVelocity := avgVelocity / size
         "Vehicle:average velocity: ".print
         avgVelocity.print
      computeAvgAcceleration:
         size: var integer
         AM.scan
            avgAcceleration:= avgAcceleration + current.avgAcceleration
            size := size + 1    
         avgAcceleration := avgAcceleration / size
         "Vehicle:average acceleration: ".print
         avgAcceleration.print
      AM: obj Association
         x: var integer      
         ElmType:: AccMeasure      
        
   AccMeasure:
      in initialPosition: var Length.Meter
      in finalPosition: var Length.Meter
      in duration: var Time.Minutes
      in initialVelocity: var Velocity.MperMin
      in finalVelocity: var Velocity.MperMin
      distance: var Length.Meter
      avgVelocity: var Velocity.MperMin
      avgAcceleration: var Acceleration.MperMinP2
      computeDistance:
          distance := finalPosition - initialPosition
      computeAvgVelocity:
         avgVelocity := Velocity.div(distance, duration)
      computeAvgAcceleration:
         avgAcceleration := Acceleration.div((finalVelocity - initialVelocity),duration)
      computeDistance
      computeAvgVelocity
      computeAvgAcceleration      
      "AccMeasure:\n".print
      "   initial position: ".print
      initialPosition.print
      " final position: ".print
      finalPosition.print
      "\n   duration: ".print
      duration.print
      "\n   initial velocity: ".print
      initialVelocity.print
      "\n   final velocity: ".print
      finalVelocity.print
      newline

   myCar: obj Vehicle
   M: ref AccMeasure
   
   M := AccMeasure(Length.F2M(0),Length.F2M(1),Time.I2M(1.7),Velocity.F2M(0),Velocity.F2M(4))
   myCar.AM.add(M)
   M := AccMeasure(Length.F2M(1),Length.F2M(2),Time.I2M(3.9),Velocity.F2M(3.9),Velocity.F2M(9.9))
   myCar.AM.add(M)

   myCar.computeAvgVelocity
   newline
   myCar.computeAvgAcceleration
   newline
   
   