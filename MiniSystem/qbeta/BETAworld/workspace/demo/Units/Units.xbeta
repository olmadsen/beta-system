Units: obj
   %include MetaSystem
   -- A so far incompelte example of modeling units
   Dimension: 
      Unit: Value 
         magnitude: var integer
         toBase:<
            out base: var integer
            @inner toBase
         fromBase:<
            in base: var integer
            @inner fromBase
         print:<
            "Unit:".print
            @inner print	 
            put(':')
            putint(magnitude)      
      shortName:<
         out T: var String.String
         @inner shortName
   Speed: obj Dimension       -- 2:
      KMperHour: Unit
         print::
            "KMperHour".print
         mult :
            in L: var Length.Meter
            in T: var Time.Minutes
            magnitude := L.magnitude * T.magnitude	 
      shortName::
         T := "S"
   Area: Obj Dimension
      squareMeter: Unit
          print::
             "squareMeter".print
   Volume: Obj Dimension
       cubicMeter: Unit
          print::
             "cubicMeter".print
   MeterPowerN: obj Dimension  -- 3:
       xxx: Unit
          print::
             "xxx".print
       yyy: Unit
          in n: val integer
          print::
             "yyy".print

   Length: obj Dimension       -- 4:
      Foot: Unit
         toBase::
             base:= (magnitude // 3)
         fromBase::
             magnitude := base * 3 -- 3.28	 
         print::
            "Foot".print
      Mile: Unit
         put('I')
      Meter: Unit	    
         toBase::
             base := magnitude
         fromBase::
             magnitude:= base	 
         print::
            "Meter".print
         * :          -- Meter * Time, but we alos want e.g. Meter * Meter
            in T: var Time.minutes
            out LTx: var Speed.KMperHour
            LTx.magnitude := magnitude * T.magnitude
         power:
            in n: var integer
            out P: var MeterPowerN.xxx
            @if n = 0 :then
                P.magnitude := 1
            :else
               P.magnitude := magnitude
               @for 1 :to n - 1 :do
                   P.magnitude := P.magnitude * magnitude
         power2:
            in n: var integer
            out P: var MeterPowerN.yyy(n) -- OBS! Note the use of n
            @if n = 0 :then
                P.magnitude := 1
            :else
               P.magnitude := magnitude
               @for 1 :to n - 1 :do
                   P.magnitude := P.magnitude * magnitude
      IasMeter:
         in V: var integer
         out R: var Meter	 
         R.magnitude := V
      shortName::
         T := "L"
      Meter2Foot:
         in m: var Meter
         out f: var Foot
         f.magnitude := m.magnitude * 3 -- 3.2808
   Time: obj Dimension              -- 5: 
      Minutes: Unit 
         put('N')
   Temperature: obj Dimension
      Celsius: Unit
         toBase::
             base := magnitude
         fromBase::
             magnitude := base
         print::
             "Celsius".print
      Fahrenheit: Unit
         toBase::
             base := magnitude * 18 + 32 -- we dont have reals
         fromBase::
             base := magnitude * (9 //5) + 32 
         print::
             "Fahrenheit".print	 
   L1: var Length.Meter     -- 6: 
   L2: var Length.Foot      -- 7: 
   T1: var Time.Minutes     -- 8: 
   LT: var Speed.KMperHour  -- 9:
   Pn: var MeterPowerN.xxx  -- 10:
   Pm: var MeterPowerN.yyy(2)  -- 11:
   SN: ref ObjSnap          -- 12: 
   "Unit demo:\n".print
   L1 := Length.IasMeter(17)
   L1.print
   newline
   L2 := Length.Meter2Foot(L1)
   L2.print
   newline
   L1.magnitude := 21
   L1.print
   newline
   L2.fromBase(L1.toBase)
   L2.print
   newline
   L1.fromBase(L2.toBase)
   L1.print
   newline
   T1.magnitude := 60
   LT := L1 * T1
   LT.print
   newline
   L1 := Length.IasMeter(10)
   Pn := L1.power(3)
   Pn.print
   newline
   Pn := L1.power(0)
   Pn.print
   newline
   Pm := L1.power2(2) -- OK
   Pm.print
   newline
   Pm := L1.power2(3) -- not OK
   Pm.print
   newline
   LT.mult(L1,T1)
   LT.print
   newline
   SN := ObjSnap(Units)
   newline
   SN.print