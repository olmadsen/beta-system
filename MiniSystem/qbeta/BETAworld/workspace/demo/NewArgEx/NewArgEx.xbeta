NewArgEx: obj 
   @include String,DK.AarhusUniversity.PersonLib
   --Example of qBeta program with revised syntax for
   
   --comments, arguments, return values and keyword patterns
   
   @if: 
      
      --Declaration of if then else control pattern
      in if: var Boolean
      in then:< Object
      in else:< Object
      L: obj 
         @if if :then 
            then
            @leave L
         else
   Account: 
      in id: ref String --input parameter for Account
      balance: var integer --The balance of this Account
      owner: ref Person
      deposit: 
         in amount: var integer --input parameter til desposit
         out newBalance: var integer --output værdi for deposit
         balance := balance + amount
         newBalance := balance
      withdraw: 
         in amount: var integer
         out newBalance: var integer
         @if (balance - amount) > 0 :then 
            balance := balance - amount
            newBalance := balance 
         :else 
            "Not enough money on account: ".print
            id.print
            newline
            newBalance := balance
      display: 
         id.print
         put(':')
         putint(balance)
         newline
   NB: var integer
   --Declaration of Account objects
   
   Joe: obj Account("Joe") --A const ref to An Account object
   Mary: ref Account --A var ref to an Acoount object
   Mary := Account("Mary")
   Joe.deposit(500)
   Mary.deposit(300)
   Mary.display
   NB := Joe.withdraw(150)
   "New balance for Joe: ".print
   putint(NB)
   newline
   NB := Mary.withdraw(400)
   Mary.display