NewArgEx: =
   %include String,DK.AarhusUniversity.PersonLib
   § Example of qBeta program with revised syntax for
   § comments, arguments, return values and keyword patterns
   
   @if:   § Declaration of if then else control pattern
      >> if: ?Boolean
      >> then:< Object
      >> else:< Object
      L: =
         %if if
         %then%
            then
            %leave L
         else

   Account:
      >> id: ?^ String  § input parameter for Account
      balance: ?^ integer   § The balance of this Account
      owner: ?Person
      deposit:
         >> amount: ?^ integer  § input parameter til desposit
         << newBalance: ?integer § output værdi for deposit
         balance := balance + amount
         newBalance:= balance
      withdraw:
         >> amount: ?^ integer
         << newBalance: ?integer
         @if (balance - amount) > 0
         :then
             balance := balance - amount
             newBalance:= balance
         :else
            "Not enough money on account: ".print
            id.print
            newline
            newBalance:= balance
      display: 
         id.print
         put(':')
         putint(balance)
         newline

   NB: ?integer
   § Declaration of Account objects
   Joe: =^ Account("Joe")   § A const ref to An Account object
   Mary: ?^ Account         § A var ref to an Acoount object
   Mary := Account("Mary")
   Joe.deposit(500)
   Mary.deposit(300)
   Mary.display
   NB := Joe.withdraw(150)
   "New balance for Joe: ".print
   putint(NB)
   newline
   NB:= Mary.withdraw(400)
   Mary.display
