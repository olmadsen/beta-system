MonitorDemo: obj LIB.MonitorSystem.System
         %requires MonitorSystem
         %include LIB.String
         P1: obj Process("PP1")
            fac:
               in V: var integer
               out res: var integer
               @if V = 1 :then 
                  res := 1 
               :else 
                  res := V * fac(V - 1)
            myM.print("P1 resumed\n")
            @for 1 :to 5 
            :do 
               myM.putItem("Fac: ",inx,fac(inx))
         myM: obj Monitor
            print: entry
               in S: ref String
               S.print
            putItem: entry
               in msg: ref String
               in V: var integer
               in res: var integer
               put(10)
               put('<')
               msg.print
               putint(V)
               " = ".print
               putint(res)
               put('>')
         P2: obj Process("PP2")
            sum:
               in V: var integer
               out res: var integer
               @for 1 :to 10 
               :do 
                  res := res + V
            myM.print("P2 resumed\n")
            @for 1 :to 6 
            :do 
               myM.putItem("Sum: ",inx,sum(inx))
         myM.init
         P1.start
         P2.start