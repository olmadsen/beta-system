BinTreeMerge: obj 
   %include LIB.stringLib
   BinaryTree: 
      node(elm: ref String):
         left: ref node
         right: ref node
         insert(S: ref String):
            if (S <= elm) :then 
               if (left == none) :then 
                  left := node(S) 
               :else 
                  left.insert(S) 
            :else 
               if (right == none) :then 
                  right := node(S) 
               :else 
                  right.insert(S)

         print(ind: var integer):
           indent(n: var integer):
              for (1):to(ind + n):repeat
                  put(' ' )	   
           indent(0)
           elm.print
           newline
           if (left =/= none) :then
               left.print(ind + 3)
           :else
               indent(3)	   
               "--L--\n".print	   
           if (right =/= none) :then
              right.print(ind + 3)
           :else
               indent(3)	   
               "--R--\n".print
      root: ref node
      insert(S: ref string):
         if (root == none) :then 
            root := node(S) 
         :else 
            root.insert(S)
      theScanner: obj
         CV: ref String
         next -> V: ref String:
            theScanner.call
            V := CV
         scan(current: ref node):
            if (current =/= none) :then 
               scan(current.left)
               CV := current.elm
               theScanner.suspend
               scan(current.right)
         theScanner.suspend
         scan(root)
         CV := ""
         theScanner.suspend
         "\n*** Calling terminated coroutine\n".print
      next -> V: ref String:
         V := theScanner.next
   boys: obj BinaryTree
   girls: obj BinaryTree
   merge: 
      nextBoy: ref String
      nextGirl: ref String
      tail(T: ref BinaryTree):
         cycle
            S: ref String
            S := T.next
            if (S = "") :then 
               leave(tail)
            :else 
               S.print
               newline
      nextBoy := boys.next
      nextGirl := girls.next
      loop: do
         if (nextBoy <= nextGirl) :then
            if (nextBoy = "") :then 
               -- "\nPrint tail of Girls:\n".print
               nextGirl.print
               newline
               if (nextGirl.length > 0) :then 
                  tail(girls) 
            :else 
               nextBoy.print
               newline
               nextBoy := boys.next
               restart(loop)
         :else
            -- nextBoy > nextGirl
            if (nextGirl = "") :then 
               "\nPrint tail of Boys:\n".print
               nextBoy.print
               newline
               tail(boys) 
            :else 
               nextGirl.print
               newline
               if (nextBoy.length > 0) :then 
                  nextGirl := girls.next
               restart(loop)   
   boys.insert("Peter")
   boys.insert("Lars")
   boys.insert("Anders")
   boys.insert("Petrus")
   girls.insert("Annabella")
   girls.insert("Anna")
   girls.insert("Liza")
   girls.insert("Natascha")
   girls.insert("Thea")
   girls.insert("Linnea")
   girls.insert("Maria")
   boys.insert("Robin")
   girls.insert("Tove")
   girls.insert("Cecilie")   
   girls.root.print(0)
   merge