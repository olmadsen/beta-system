ForkJoinSystem: =
   %include BasicSystem, LIB.STRING
   ForkJoinSystem: BasicSystem
      ForkJoin: 
         Q: = ProcessQueue
         M: = Lock
         run(P: ?BasicProcess):
             M.get
             Q.insert(P)
             SQS.insert(P)
             M.free
         AwaitTermination:
             loop: =
                P: ?BasicProcess
                continue: ?Boolean
                sleep(100)
                continue := false
                Q.scan
                   P:= current
                   %if P.status <> P_status.TERMINATED %then%
                       continue := true
                %if continue %then% 
                    %restart loop
                Q.clear
         M.init
         %inner ForkJoin
         AwaitTermination
      %inner ForkJoinSystem
   
   ForkJoinSystem
      PPP: BasicProcess
          (%this PPP).suspend
          %for 1 %to% 10 %do%
               console.display
                 id.print
                 put('a' + inx)
                 newline
      main: =BasicProcess("Main")
         Fib: =BasicProcess("Fib")
             last: ?integer
             prev: ?integer
             next:
                V: ?integer
                loop: = 
                   V:= last
                   last := last + prev
                   prev:= V
                   print
                   %if last < 100 %then% %restart loop
             print: 
                console.display
                   id.print
                   put(':')
                   putint(last)
                   newline
             Fib.suspend  
             last := 1 
             print
             prev := 1
             print           
             next          
         P2: =PPP("P2")
         P3: =PPP("P3")
         main.suspend
         "Starting fork-join processes\n".print
         ForkJoin
            run(Fib)
            run(P2)
            run(P3)
         "\nEnd-of-fork-join processes\n".print
      SQS.insert(main)

