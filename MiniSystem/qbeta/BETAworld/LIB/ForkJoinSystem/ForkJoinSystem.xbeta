ForkJoinSystem: =
   %include BasicSystem, LIB.STRING
   ForkJoinSystem: BasicSystem
      ForkJoin: 
         Q: = ProcessQueue
         M: = Lock
         run(P: ?BasicProcess):
             M.get
             Q.insert(P)
             SQS.insert(P)
             M.free
         AwaitTermination:
             loop: =
                P: ?BasicProcess
                continue: ?Boolean
                put('T')
                sleep(100)
                continue := false
                Q.scan
                   P:= current
                   %if P.status <> P_status.TERMINATED %then%
                       put('.')
                       continue := true
                %if continue %then% 
                    %restart loop
                Q.clear
         M.init
         %inner ForkJoin
         AwaitTermination
      %inner ForkJoinSystem
   "Starting fork join processes\n".print
   ForkJoinSystem
      PPP(id: ?String): BasicProcess
          (%this PPP).suspend
          %for 1 %to% 10 %do%
               console.display
                 id.print
                 put('a' + inx)
      P1: =PPP("P1")
      P2: =PPP("P2")
      P3: =PPP("P3")
      P4: = BasicProcess
         P4.suspend
         ForkJoin
            run(P1)
            run(P2)
            run(P3)
         "\nEnd-of-fork-join processes\n".print
      SQS.insert(P4)

