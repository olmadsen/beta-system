parser: obj
   %requires lexer,lexer.Symbols, AST
   %visible AST
   dummy: var integer
   pModule:
      sig: ref Signature:args
      out DI: ref SuperDataItem:OG
      OG: ref ObjectGenerator:Items
      trace("pModule")
      if(lexer.token = lexer.symbols.idf):then
          sig := pSignature
          if (lexer.token = lexer.symbols.colon) :then
             lexer.next
             if (lexer.token = lexer.symbols.constRef) :then
                lexer.next
                OG := pSuperDataItem
                DI := Module(sig):OG(OG);
             :else
                syntaxError(1)
          :else
             syntaxError(2)
      :else
          syntaxError(3)
   pSuperDataItem:
      --in pSuperDataItem:< Object
      out OG: ref ObjectGenerator:Items
      trace("pSuperDataItem")
      OG := pObjectGenerator
   pSignature:
      out sig: ref Signature:args
      args: ref Arguments
      tokenStr: ref String
      prevToken: var integer
      id: ref String
      trace("pSignature:")
      if(lexer.token = lexer.symbols.idf):then
         tokenStr := lexer.tokenStr
         lexer.next
         switch(lexer.token)
             case((lexer.symbols.begin
                   ,lexer.symbols.leftBrack
                   ,lexer.symbols.leftSquare))
                prevToken := lexer.token
                lexer.next
                trace("pSignature:brack")
                -- missing save of return value
                args := pSigArgs(tokenStr):leftB(prevToken)
                sig := SIgnature(tokenStr):args(args)
             trace("pSignature:unarySig")
             sig := Signature(tokenstr):args(args)
   pSigArgs(tokenStr: ref String):leftB(leftB: var char):
      out args: ref Arguments
      id: ref String
      D: ref Decl:OG
      rightB: var char
      comma: ref String
      brackAsText(sy: var integer):
         out T: ref String
         switch(sy)
            case((lexer.symbols.leftBrack))
               T := "("
            case((lexer.symbols.rightBrack))
               T := ")"
            case((lexer.symbols.begin))
               T := "{"
            case((lexer.symbols.end))
               T := "\}"
            case((lexer.symbols.leftSquare))
               T := "["
            case((lexer.symbols.rightSquare))
               T := "]" 
      -- incomplete
      trace("pSigArgs")
      rightB := leftB + 1
      args := Arguments
      --args.append(Name(lexer.tokenStr))    -- foo in foo(...)
      --args.append(Name(brackAsText(leftB)))-- ( in foo(...)
      id := lexer.tokenStr
      args.addsep(brackAsText(leftB))
      pSigArg: do
         D := pDecl
         args.append(D)
         switch(lexer.token)
             case(rightB)
                trace("pSigargs:rightB")			     
                lexer.next
                if(lexer.token = lexer.symbols.fatComma) :then
                   -- check what we append ')' :foo '('
                   -- or '):foo('		   
                   --args.append(Name(brackAsText(rightB))) -- correct?
                   comma := brackAsText(rightB) + lexer.tokenStr
                   id := id + lexer.tokenStr		   
                   trace("pSigArg:got:fatComma:")
                   lexer.next
                   trace("pSigArg:got:fatComma:B:")		   
                   switch(lexer.token)
                        case((lexer.symbols.leftBrack
                             ,lexer.symbols.leftSquare
                             ,lexer.symbols.begin))
                             leftB := lexer.token
                             rightB := leftB + 1
                             lexer.next			     
                             trace("pSigArgs:getParenth:A");
                             comma := comma + brackAsText(leftB)
                             trace("pSigArgs:getParenth:B");
                             args.addSep(comma)
                             trace("pSigArgs:getParenth:C");
                             restart(pSigArg)
                        syntaxError(111)
                :else
                   trace("pSigargs:else")		
                   --args.append(Name(brackAsText(rightB)))
                   args.addSep(brackAsText(rightB))
             case(lexer.symbols.comma)
                 args.addSep(",")
                 lexer.next
                 restart(pSigArg)
             syntaxError(112)
         args.id := id
         --args.id.print
   pObjectGenerator:
      out OG: ref ObjectGenerator:items
      IV: ref Invocation
      IS: ref Items
      trace("pObjectGenerator")
      IV := pInvocation
      IS := pMainPart
      OG := ObjectGenerator(IV):Items(IS)
   pInvocation:
      out IV: ref Invocation
      E: ref Exp
      i: var integer
      trace("pinvocation")
      IV := Invocation
      loop:
         trace("pInvocation:loop")
         i := i + 1
         E := pExp;
         IV.append(E)
         switch((lexer.token))
            case((lexer.symbols.dot))
               trace("pInvocation:special")
               lexer.next
               restart(loop)
            case((lexer.symbols.special))
               trace("pInvocation:special")
               restart(loop)
      loop
   pMainPart:
      out IS: ref Items
      trace("pMainPart")
      if (lexer.token = lexer.symbols.begin) :then
         lexer.next
         IS := pItems
         switch(lexer.token)
            case((lexer.symbols.end,lexer.symbols.eos))
                 lexer.next
            SyntaxError(4)
      :else
         trace("pMainPart:IS.isEmpty")
         IS := Items
         IS.isEmpty := true
   pItems:
      out IS: ref Items
      D: ref Decl:OG
      OG: ref ObjectGenerator:Items
      trace("pItems:")
      IS := Items
      trace("pItems:loop")
      Loop: do
         if (lexer.token = lexer.symbols.idf) :then
            switch(lexer.peekNext(1))
               case((lexer.symbols.colon
                    ,lexer.symbols.virtual
                    ,lexer.symbols.further
                    ,lexer.symbols.final))
                   D := pDecl
                   IS.append(D)
               case(lexer.symbols.leftBrack)
                  switch (lexer.peekNext(3))
                      case((lexer.symbols.colon,lexer.symbols.virtual))
                          IS.append(pDecl)
                  IS.append(pObjectGenerator)
               trace("pItems:before:pObjectGenerator")
               OG := pObjectGenerator
               IS.append(OG)
         if (lexer.token = lexer.symbols.semicolon) :then
            lexer.next
            trace("pItems:after:semicolon:next")
            restart(Loop)
   pDecl:
       out D: ref Decl:OG
       sig: ref Signature:args
       OD: ref ObjectGenerator:Items
       ptnKind: var integer
       trace("pDecl:")
       sig := pSignature
       sig.dopt(true)
       L: do       
          switch(lexer.token)
             case(lexer.symbols.colon)
                lexer.next       
                switch(lexer.token)
                   case(lexer.symbols.constRef)
                       lexer.next
                       D := pDataItem(sig):isConst(true):isValue(false)
                       leave(L)
                   case(lexer.symbols.varRef)
                       trace("pDecl:ref")
                       lexer.next
                       D := pDataItem(sig):isConst(false):isValue(false)
                       leave(L)
                   case(lexer.symbols.constVal)
                       trace("pDecl:val")  
                       lexer.next
                       D := pDataItem(sig):isConst(true):isValue(true)
                       leave(L)
                   case(lexer.symbols.varVal)
                       trace("pDecl:var")
                       lexer.next
                       D := pDataItem(sig):isConst(false):isValue(true)
                       leave(L)
                   case(lexer.symbols.doSy)
                       trace("pDecl:doSy")
                       lexer.next
                       D := pLabelDecl(sig)
                       leave(L)
                   ptnKind := plainPtn
             case(lexer.symbols.virtual)
                trace("ptn:virtual")
                lexer.next
                ptnKind := virtualPtn
             case(lexer.symbols.further)
                lexer.next
                ptnKind := furtherPtn
             case(lexer.symbols.final)
                lexer.next
                ptnKind := finalPtn
          OD := pObjectGenerator
          D := PatternGen(sig):OG(OD):kind(ptnKind)
       --syntaxError(5)
   pDataItem(sigx: ref Signature:args):isConst(isConst: var Boolean):isValue(isValue: var Boolean):
      out D: ref DataItem:isValue:sig:OG
      OG: ref ObjectGenerator:Items
      trace("pDataItem")      
      OG := pSuperDataItem
      D := DataItem(isConst):isValue(isValue):sig(sigx):OG(OG) -- ref args and sub dont work?
      --D.setSigOg(sigx,OG)
   pLabelDecl(sig: ref Signature:args):
      out LD: ref LabelDecl:OG
      OG: ref ObjectGenerator:Items
      trace("pLabelDecl")
      OG := pObjectGenerator
      LD := LabelDecl(sig):OG(OG)
   pExp:
      out E: ref Exp
      symb: ref String
      trace("pExp")
      switch(lexer.token)
         -- missing, const,floatConst,string,charSy
         case((lexer.symbols.const))
            C: ref Const
            C := Const(lexer.TokenStr)
            --C.T := lexer.TokenStr
            E := C
            lexer.next
         case(lexer.symbols.idf)
             -- incomplete
             trace("pExp:idf")
             symb:= lexer.tokenStr
             lexer.next
             trace("pExp:idf:next")
             E := UnaryGen(symb,Arguments)	-- should be UnaryGen
         case((lexer.symbols.special))
             symb := lexer.tokenStr
             lexer.next
             E := pBinary(symb)
         trace("pExp:empty")
         E := Exp
   pBinary(op: ref String):
      out B: ref Exp
      args: ref Arguments
      args := Arguments
      args.isBinary := true
      args.append(pObjectGenerator)
      B := BinaryGen(op,args)
   genTokenStream: 
      ind: var integer
      lexer.next
      --"Got: {I2S(lexer.token)} {lexer.tokenStr}\n".print
      switch(lexer.token)
         case(0)
            trace("genTokenStream:eof")
         case(lexer.symbols.begin)
            newline
            ind := ind + 2   
            for(1):to(ind):repeat
               put(' ')         
            put('{')
         case(lexer.symbols.semicolon)
            put(';')
            newline
            for(1):to(ind):repeat
               put(' ')
         case(lexer.symbols.end)
            newline
            for(1):to(ind):repeat
                put(' ')
            put('}')
            ind := ind - 2
            newline
            for(1):to(ind):repeat
               put(' ')
         case(lexer.symbols.charSy)
            put('\'')
            lexer.tokenstr.print
            put('\'')
         case(lexer.symbols.stringSy)
            put('"')
            lexer.tokenStr.print
            put('"')
         case(
           (lexer.symbols.idf,
            lexer.symbols.const,
            lexer.symbols.floatConst,
            lexer.symbols.special,
            lexer.symbols.keyWord,
            lexer.symbols.fatComma))
            lexer.tokenStr.print
         lexer.symbols.asText(lexer.token).print
      put(' ')
      if(lexer.token <> lexer.symbols.eos):then 
         restart(genTokenStream)
   syntaxError(no: var integer):
      "\nsyntax error: ".print
      putint(no)
      newline
   trace(msg: ref String):
      if (true) :then
         msg.print
         put(':')
         putint(lexer.token)
         put(' ' )
         lexer.symbols.asText(lexer.token).print
         put(' ')
         lexer.tokenStr.print
         newline
   go:
      out N: ref Node
      --genTokenStream
      "***parser.go:\n".print      
      lexer.next
      N := pModule
