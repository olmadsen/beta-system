newparser: obj
   %visible AST
   pModule:
      sig: ref Signature:args
      out DI: ref SuperDataItem:OG
      OG: ref ObjectGenerator:Items
      trace("pModule")
      if(lexer.token = lexer.symbols.idf):then
          sig := pSignature
          if (lexer.token = lexer.symbols.colon) :then
             lexer.next
             if (lexer.token = lexer.symbols.constRef) :then
                lexer.next
                OG := pSuperDataItem
                DI := Module(sig):OG(OG)
             :else
                syntaxError(1)
          :else
             syntaxError(2)
      :else
          syntaxError(3)
   pSuperDataItem:
      --in pSuperDataItem:< Object
      out OG: ref ObjectGenerator:Items
      trace("pSuperDataItem")
      OG := pObjectGenerator
   pSignature:
      out sig: ref Signature:args
      args: ref Arguments
      tokenStr: ref String
      prevToken: var integer
      id: ref String
      trace("pSignature")
      if(lexer.token = lexer.symbols.idf):then
         tokenStr := lexer.tokenStr
         lexer.next
         switch(lexer.token)
             case((lexer.symbols.begin
                   ,lexer.symbols.leftBrack
                   ,lexer.symbols.leftSquare))
                prevToken := lexer.token
                lexer.next
                trace("pSignature:brack")
                -- missing save of return value
                args := pSigArgs(tokenStr):leftB(prevToken)
                sig := Signature(tokenStr):args(args)
             trace("pSignature:unarySig")
             sig := Signature(tokenstr):args(args)
   pArgs(tokenStr: ref String):leftB(leftB: var char):
      out args: ref Arguments
      id: ref String
      rightB: var char
      comma: ref String
      trace("pArgs")
      rightB := leftB + 1
      args := Arguments
      id := lexer.tokenStr
      args.addsep(brackAsText(leftB))
      pArg: do
         inner(#pArgs:leftB)
         trace("pArgs:after:inner")
         switch(lexer.token)
             case(rightB)
                trace("pArgs:rightB")
                lexer.next
                if(lexer.token = lexer.symbols.fatComma) :then
                   -- check what we append ')' :foo '('
                   -- or '):foo('		   
                   comma := brackAsText(rightB) + lexer.tokenStr
                   id := id + lexer.tokenStr		   
                   trace("pArg:got:fatComma")
                   lexer.next
                   trace("pArg:got:fatComma:B")		   
                   switch(lexer.token)
                        case((lexer.symbols.leftBrack
                             ,lexer.symbols.leftSquare
                             ,lexer.symbols.begin))
                             leftB := lexer.token
                             rightB := leftB + 1
                             lexer.next
                             trace("pArg:getParenth:A");
                             comma := comma + brackAsText(leftB)
                             trace("pArg:getParenth:B");
                             args.addSep(comma)
                             trace("pArg:getParenth:C");
                             restart(pArg)
                        syntaxError(4)
                :else
                   trace("pArg:else")		
                   args.addSep(brackAsText(rightB))
             case(lexer.symbols.comma)
                 args.addSep(",")
                 lexer.next
                 restart(pArg)
             case(lexer.symbols.semicolon)
                 -- skip
             case(lexer.symbols.eos)
                 "\nEOS\n".print
                 args.addSep("\}")		 
             syntaxError(5)
         args.id := id
         --args.id.print
   pSigArgs(tokenStr: ref String):leftB(leftB: var char): pArgs(tokenStr):leftB(leftB)
      D: ref Decl:OG
      trace("pSigArgs:main")
      D := pDecl
      args.append(D)
   pProperty(propId: ref String):
      out P: ref Property:args
      args: ref PropertyArgs
      args := PropertyArgs
      parseArgs: do
         switch(lexer.token)
            case((lexer.symbols.idf
                 ,lexer.symbols.const,lexer.symbols.floatConst
                 ,lexer.symbols.StringSy))
                 trace("propArg")
                 args.append(pInvocation)
                 if (lexer.token = lexer.symbols.comma) :then
                     lexer.next
                     trace("propArg:next")
                     restart(parseArgs)
      P := Property(Name(propId)):args(args)
         
   pVarRefDI(sig: Ref Signature:args):isConst(isConst: var boolean):isValue(isValue: var Boolean):
      out DI : ref SuperDataItem:OG
      IV: ref Invocation
      typeid: Ref String
      E: ref Exp
      OG: ref ObjectGenerator:Items
      IV := Invocation
      next: do
         typeId := pVarRefTypeNoBrack(sig):isConst(isConst):isValue(isValue)
         E := unaryGen(typeId,Arguments)
         IV.append(E)	 
         if (lexer.token = lexer.symbols.dot) :then
            lexer.next
            restart(next)
      OG := ObjectGenerator(IV):Items(Items)	    
      DI := DataItem(isConst):isValue(isValue):sig(sig):OG(OG)
      
   pVarRefTypeNoBrack(sig: Ref Signature:args):isConst(isConst: var boolean):isValue(isValue: var Boolean):
      out typeId: ref String
      if (lexer.token = lexer.symbols.idf) :then
         typeId := lexer.tokenStr
         lexer.next
         L: do
            if (lexer.token = lexer.symbols.fatComma) :then	 
               typeId := typeId + lexer.tokenStr
               lexer.next
               restart(L)
   pDecl -> D: ref Decl:OG:
       sig: ref Signature:args
       OD: ref ObjectGenerator:Items
       isInArg: var Boolean
       isOutArg: var Boolean
       ptnKind: var integer
       trace("pDecl")
       switch(lexer.token)
          case(lexer.symbols.inArg)
              lexer.next	  
              isInArg := true
          case(lexer.symbols.outArg)
              lexer.next	  
              isOutArg := true
       sig := pSignature
       L: do
          switch(lexer.token)
             case(lexer.symbols.colon)
                lexer.next       
                switch(lexer.token)
                   case(lexer.symbols.constRef)
                       lexer.next
                       D := pDataItem(sig):isConst(true):isValue(false)
                       leave(L)
                   case(lexer.symbols.varRef)
                       trace("pDecl:ref")
                       lexer.next
                       --D := pDataItem(sig):isConst(false):isValue(false)
                       -- OBS unfinished business!
                       D := pVarRefDI(sig):isConst(false):isValue(false)
                       leave(L)
                   case(lexer.symbols.constVal)
                       trace("pDecl:val")  
                       lexer.next
                       D := pDataItem(sig):isConst(true):isValue(true)
                       leave(L)
                   case(lexer.symbols.varVal)
                       trace("pDecl:var")
                       lexer.next
                       D := pDataItem(sig):isConst(false):isValue(true)
                       leave(L)
                   case(lexer.symbols.doSy)
                       trace("pDecl:doSy")
                       lexer.next
                       D := pLabelDecl(sig)
                       leave(L)
                   ptnKind := plainPtn
             case(lexer.symbols.virtual)
                trace("ptn:virtual")
                lexer.next
                ptnKind := virtualPtn
             case(lexer.symbols.further)
                lexer.next
                ptnKind := furtherPtn
             case(lexer.symbols.final)
                lexer.next
                ptnKind := finalPtn
          OD := pObjectGenerator
          D := PatternGen(sig):OG(OD):kind(ptnKind)
   pDataItem(sigx: ref Signature:args):isConst(isConst: var Boolean):isValue(isValue: var Boolean):
      out D: ref DataItem:isValue:sig:OG
      OG: ref ObjectGenerator:Items
      trace("pDataItem")      
      OG := pSuperDataItem
      D := DataItem(isConst):isValue(isValue):sig(sigx):OG(OG) -- ref args and sub dont work?
      --D.setSigOg(sigx,OG)
   pLabelDecl(sig: ref Signature:args):
      out LD: ref LabelDecl:OG
      OG: ref ObjectGenerator:Items
      trace("pLabelDecl")
      OG := pObjectGenerator
      LD := LabelDecl(sig):OG(OG)
   pObjectGenerator -> OG: ref ObjectGenerator:items:
      IV: ref Invocation
      IS: ref Items
      trace("pObjectGenerator")
      IV := pInvocation
      if (IV.isEmpty) :then
         IS := pMainPart
      :else
         IS := Items
         IS.isEmpty := true
      -- must check if IV.last.args is actually a mainpart using convertLastArg
      OG := ObjectGenerator(IV):Items(IS)
      trace("pObjectGenerator:end")
   pInvocation ->  out IV: ref Invocation:
      IV := parser.pInvocation
   pMainPart -> IS: ref Items:
      trace("pMainPart")
      if (lexer.token = lexer.symbols.begin) :then
         lexer.next
         IS := pItems
         trace("pMainPart:after:pItems")
         switch(lexer.token)
            case((lexer.symbols.end))
                lexer.next	    
            case((lexer.symbols.end,lexer.symbols.eos))
                 lexer.next
            SyntaxError(6)
      :else
         trace("pMainPart:IS.isEmpty")
         IS := Items
         IS.isEmpty := true
      trace("pMainPart:end")
   pItems -> IS: ref Items:
      IT: ref Item
      trace("pItems")
      IS := Items
      Loop: do
         trace("pItems:loop")
         IT := pItem
         IS.append(IT)
         if (lexer.token = lexer.symbols.semicolon) :then
            lexer.next
            trace("pItems:after:semicolon:next")
            restart(Loop)
      trace("pItems:end")
   pItem -> IT: ref Item:
      trace("pItem")
      switch (lexer.token)
         case((lexer.symbols.inArg,lexer.symbols.outArg))
            "in/out arg".print
            IT := pDecl
         case(lexer.symbols.idf)
            switch(lexer.peekNext(1))
               trace("pItem:idf")
               case((lexer.symbols.colon
                    ,lexer.symbols.virtual
                    ,lexer.symbols.further
                    ,lexer.symbols.final))
                   IT := pDecl
               case(lexer.symbols.leftBrack)
                  trace("pItem:idf:leftBrack")
                  switch (lexer.peekNext(3))
                      case((lexer.symbols.colon,lexer.symbols.virtual))
                          IT := pDecl
                          trace("pItem:efter:colon,virtual")
                      IT := pTransaction
                      trace("pItem:idf:leftBrack:after:pExp")
               trace("pItems:before:pTransaction")
               --IT := pObjectGenerator
               IT := pTransaction
         case(lexer.symbols.special) -- probably not here but in pPrimary
              trace("pItems:got:special")
              lexer.next
         case(lexer.symbols.property)
             propId: ref String
             propId :=  lexer.tokenStr
             lexer.next
             IT := pProperty(propId)
         IT := pTransaction	      
   pTransaction -> asg: ref Assignment:
      trace("pTransaction")
      asg := Assignment
      L: do
         N: ref Item
         N := pExp
         trace("pTransaction:after:pExp")
         asg.append(N)
         if (lexer.token = lexer.symbols.vAssign) :then
            lexer.next
            restart(L)
      trace("pTransaction:end")	    
   pExp -> E: ref Exp:
      trace("pExp")
      E := pSimpleExp      
      trace("pExp:before:relOp")
      L: do
         rE: ref Exp
         sym: var String
         if (lexer.symbols.isRelOp(lexer.token)) :then
            trace("pExp:relOp")
            sym := lexer.tokenStr
            lexer.next
            rE := pSimpleExp
            E := BinaryExp(E,sym,rE)
            restart(L)
      trace("pExp:end")
   pSimpleExp -> E: ref Exp:
      E := pTerm
   pTerm -> E: ref Exp:
      E := pFactor
   pFactor -> E: ref Exp:
      E := pPrimary
   pPrimary -> E: ref Item:
      symb: ref String
      prevToken: var integer
      rightB: var integer
      args: ref Arguments
      trace("pPrimary")
      switch(lexer.token)
         -- missing, const,floatConst,string,charSy
         case((lexer.symbols.const))
            E := Const(lexer.TokenStr)
            lexer.next
         case(lexer.symbols.idf)
             -- incomplete
             trace("pPrimary:idf")
             E := pInvocations
         case(lexer.symbols.leftBrack)
             bE: ref BracketedExp
             bE := BracketedExp	     
             lexer.next             
             L: do
                bE.append(pTransaction)
                switch(lexer.token)
                   case((lexer.symbols.comma))
                      lexer.next
                      restart(L)		    
                   case((lexer.symbols.rightBrack))
                      lexer.next
                   "Syntax error - missign brackets\n".print
                   syntaxError(7)
             E := bE		   
             trace("pPrimary:end:bracketed")		   
         case(lexer.symbols.StringSy)
             trace("pPrimary:got:String")
             --IS.append(StringSy(lexer.TokenStr))
             --lexer.next
             E := pObjectGenerator   -- is this correct?
         case((lexer.symbols.const
              ,lexer.symbols.floatConst
              ,lexer.symbols.charSy))
              trace("pItems:got:charSy")
              E := pObjectGenerator   -- is this correct?
      trace("pPrimary:end")
   pInvocations -> IVs: ref Invocations:
      symb: ref String
      prevToken: var integer
      rightB: var integer
      args: ref Arguments
      E: ref Exp
      trace("pInvocations")
      IVs := Invocations
      L: do
         symb:= lexer.tokenStr
         lexer.next
         trace("pInvocations:before:switch")
         switch(lexer.token)
            case((lexer.symbols.begin, -- need to fix!
                 lexer.symbols.leftBrack
                 ,lexer.symbols.leftSquare))
               prevToken := lexer.token
               rightB := prevToken + 1
               lexer.next
               trace("pInvocations:keyWord")
               args := pExpArgs(symb):leftB(prevToken):prevT(prevToken)
               E := KeyWordGen(symb,args)
               IVs.append(E)
               if (lexer.token = rightB) :then
                  trace("pInvocations:got:rightB")
                  lexer.next
            trace("pPrimary:unary")
            E := UnaryGen(symb,Arguments) -- should be UnaryGen
            IVs.append(E)
            if (lexer.token = lexer.symbols.dot) :then
               trace("pInvocatios:got:dot")
               lexer.next
               if (lexer.token = lexer.symbols.idf) :then
                  restart(L)
               :else
                   synTaxError(8)

   pExpArgs(tokenStr: ref String):leftB(leftB: var integer):prevT(prevT: var integer): pArgs(tokenStr):leftB(leftB)
      --E: ref Exp;
      --E := pExp;
      IV: ref Invocation
      IS: ref Items
      trace("pExpArgs")
      if (leftB = prevT) :then
         --lexer.next
         trace("pExpArgs:leftB=prevT")
         IS := pItems
         IV := Invocation
         IV.append(Exp)
         IV.isEmpty := true
         args.append(ObjectGenerator(IV):Items(IS))
      :else
         --lexer.next
         trace("pExpArgs:leftB<>prevT")
         args.append(pObjectGenerator)
      trace("pExpArgs:end")
   syntaxError(no: var integer):
      "\nsyntax error: ".print
      putint(no)
      " at token: \"".print
      lexer.symbols.asText(lexer.token).print
      put('"')
      newline
      lexer.printLines(5)
      hasSyntaxError := true

   brackAsText(sy: var integer):
         out T: ref String
         switch(sy)
            case((lexer.symbols.leftBrack))
               T := "("
            case((lexer.symbols.rightBrack))
               T := ")"
            case((lexer.symbols.begin))
               T := "{"
            case((lexer.symbols.end))
               T := "\}"
            case((lexer.symbols.leftSquare))
               T := "["
            case((lexer.symbols.rightSquare))
               T := "]"      
   trace(msg: ref String):
      if (true) :then
         put('%')
         msg.print
         put(':')
         put(' ' )
         putint(lexer.token)
         put(' ' )
         lexer.symbols.asText(lexer.token).print
         put(' ')
         lexer.tokenStr.print
         if (lexer.tokenBuf.getTokenNo(1) <> 0) :then
            put(' ')
            putint(lexer.tokenBuf.getTokenNo(1))
            put(' ')
            lexer.symbols.asText(lexer.tokenBuf.getTokenNo(1)).print
         newline
   go:
      out N: ref Item
      --genTokenStream
      "***parser.go:\n".print      
      lexer.next
      N := pModule
      