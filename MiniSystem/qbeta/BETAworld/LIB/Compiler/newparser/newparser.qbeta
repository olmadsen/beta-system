newparser: obj
   %visible AST,lexer
   pModule:
      out DI: ref SuperDataItem:OG
      sig: ref Signature:args
      OG: ref ObjectGenerator:Items
      trace("pModule")
      if (token = symbols.idf):then
          sig := pSignature
          if (lexer.token = lexer.symbols.colon) :then
             lexer.next
             if (lexer.token = lexer.symbols.constRef) :then
                lexer.next
                OG := pSuperDataItem
                DI := Module(sig):OG(OG)
             :else
                syntaxError(1)
          :else
             syntaxError(2)
      :else
          syntaxError(3)
   pSuperDataItem:
      out OG: ref ObjectGenerator:Items
      trace("pSuperDataItem")
      OG := pObjectGenerator
   pSignature:
      out sig: ref Signature:args
      args: ref Arguments
      tokenStr: ref String
      prevToken: var integer
      id: ref String
      trace("pSignature")
      if(lexer.token = lexer.symbols.idf):then
         tokenStr := lexer.tokenStr
         lexer.next
         switch(lexer.token)
             case((lexer.symbols.begin
                   ,lexer.symbols.leftBrack
                   ,lexer.symbols.leftSquare))
                prevToken := lexer.token
                lexer.next
                trace("pSignature:brack")
                -- missing save of return value
                args := pSigArgs(tokenStr):leftB(prevToken)
                sig := Signature(tokenStr):args(args)
             trace("pSignature:unarySig")
             sig := Signature(tokenstr):args(args)
   pArgs(tokenStr: ref String):leftB(leftB: var char):
      out args: ref Arguments
      id: ref String
      rightB: var char
      comma: ref String
      trace("pArgs")
      rightB := leftB + 1
      args := Arguments
      id := lexer.tokenStr
      args.addsep(brackAsText(leftB))
      pArg: do
         inner(#pArgs:leftB)
         trace("pArgs:after:inner")
         switch(lexer.token)
             case(rightB)
                trace("pArgs:rightB")
                lexer.next
                if(lexer.token = lexer.symbols.fatComma) :then
                   -- check what we append ')' :foo '('
                   -- or '):foo('
                   comma := brackAsText(rightB) + lexer.tokenStr
                   id := id + lexer.tokenStr
                   trace("pArg:got:fatComma")
                   lexer.next
                   trace("pArg:got:fatComma:B")
                   switch(lexer.token)
                        case((lexer.symbols.leftBrack
                             ,lexer.symbols.leftSquare
                             ,lexer.symbols.begin))
                             leftB := lexer.token
                             rightB := leftB + 1
                             lexer.next
                             trace("pArg:getParenth:A");
                             comma := comma + brackAsText(leftB)
                             trace("pArg:getParenth:B");
                             args.addSep(comma)
                             trace("pArg:getParenth:C");
                             restart(pArg)
                        syntaxError(4)
                :else
                   trace("pArg:else")
                   args.addSep(brackAsText(rightB))
             case(lexer.symbols.comma)
                 args.addSep(",")
                 lexer.next
                 restart(pArg)
             case(lexer.symbols.semicolon)
                 -- skip
             case(lexer.symbols.eos)
                 "\nEOS\n".print
                 args.addSep("\}")
             syntaxError(5)
         args.id := id
         --args.id.print
   pSigArgs(tokenStr: ref String):leftB(leftB: var char): pArgs(tokenStr):leftB(leftB)
      D: ref Decl:OG
      trace("pSigArgs:main")
      D := pDecl
      args.append(D)
   pProperty(propId: ref String):
      out P: ref Property:args
      args: ref PropertyArgs
      args := PropertyArgs
      parseArgs: do
         switch(lexer.token)
            case((lexer.symbols.idf
                 ,lexer.symbols.const,lexer.symbols.floatConst
                 ,lexer.symbols.StringSy))
                 trace("propArg")
                 args.append(pInvocation)
                 if (lexer.token = lexer.symbols.comma) :then
                     lexer.next
                     trace("propArg:next")
                     restart(parseArgs)
      P := Property(Name(propId)):args(args)
         
   pVarRefDI(sig: Ref Signature:args):isConst(isConst: var boolean):isValue(isValue: var Boolean):
      out DI : ref SuperDataItem:OG
      IV: ref Invocation
      typeid: Ref String
      E: ref Exp
      OG: ref ObjectGenerator:Items
      trace("pVarRefDI")
      IV := Invocation
      next: do
         typeId := pVarRefTypeNoBrack(sig):isConst(isConst):isValue(isValue)
         E := unaryGen(typeId,Arguments)
         IV.append(E)
         if (lexer.token = lexer.symbols.dot) :then
            lexer.next
            restart(next)
      OG := ObjectGenerator(IV):Items(Items)
      DI := DataItem(isConst):isValue(isValue):sig(sig):OG(OG)
      
   pVarRefTypeNoBrack(sig: Ref Signature:args):isConst(isConst: var boolean):isValue(isValue: var Boolean):
      out typeId: ref String
      trace("pVarRefNoBrack")      
      if (lexer.token = lexer.symbols.idf) :then
         typeId := lexer.tokenStr
         lexer.next
         L: do
            if (lexer.token = lexer.symbols.fatComma) :then
               typeId := typeId + lexer.tokenStr
               lexer.next
               restart(L)
   pDecl -> D: ref Decl:OG:
       sig: ref Signature:args
       OD: ref ObjectGenerator:Items
       isInArg: var Boolean
       isOutArg: var Boolean
       ptnKind: var integer
       trace("pDecl")
       switch(lexer.token)
          case(lexer.symbols.inArg)
              lexer.next
              isInArg := true
          case(lexer.symbols.outArg)
              lexer.next
              isOutArg := true
       sig := pSignature
       L: do
          switch(lexer.token)
             case(lexer.symbols.colon)
                lexer.next       
                switch(lexer.token)
                   case(lexer.symbols.constRef)
                       lexer.next
                       D := pDataItem(sig):isConst(true):isValue(false)
                       leave(L)
                   case(lexer.symbols.varRef)
                       trace("pDecl:ref")
                       lexer.next
                       --D := pDataItem(sig):isConst(false):isValue(false)
                       -- OBS unfinished business!
                       D := pVarRefDI(sig):isConst(false):isValue(false)
                       leave(L)
                   case(lexer.symbols.constVal)
                       trace("pDecl:val")  
                       lexer.next
                       D := pDataItem(sig):isConst(true):isValue(true)
                       leave(L)
                   case(lexer.symbols.varVal)
                       trace("pDecl:var")
                       lexer.next
                       D := pDataItem(sig):isConst(false):isValue(true)
                       leave(L)
                   case(lexer.symbols.doSy)
                       trace("pDecl:doSy")
                       lexer.next
                       D := pLabelDecl(sig)
                       leave(L)
                   ptnKind := plainPtn
             case(lexer.symbols.virtual)
                trace("ptn:virtual")
                lexer.next
                ptnKind := virtualPtn
             case(lexer.symbols.further)
                lexer.next
                ptnKind := furtherPtn
             case(lexer.symbols.final)
                lexer.next
                ptnKind := finalPtn
          OD := pObjectGenerator
          D := PatternGen(sig):OG(OD):kind(ptnKind)
   pDataItem(sigx: ref Signature:args):isConst(isConst: var Boolean):isValue(isValue: var Boolean):
      out D: ref DataItem:isValue:sig:OG
      OG: ref ObjectGenerator:Items
      trace("pDataItem")      
      OG := pSuperDataItem
      D := DataItem(isConst):isValue(isValue):sig(sigx):OG(OG) -- ref args and sub dont work?
      --D.setSigOg(sigx,OG)
   pLabelDecl(sig: ref Signature:args):
      out LD: ref LabelDecl:OG
      OG: ref ObjectGenerator:Items
      trace("pLabelDecl")
      OG := pObjectGenerator
      LD := LabelDecl(sig):OG(OG)
   pObjectGenerator -> OG: ref ObjectGenerator:items:
      IV: ref Invocation
      IS: ref Items
      trace("pObjectGenerator")
      IV := pSuper -- pInvocation
      if (IV.isEmpty) :then
         IS := pMainPart
      :else
         IS := Items
         IS.isEmpty := true
      -- must check if IV.last.args is actually
      -- a mainpart using convertLastArg
      -- Currently we call pItems and not pTransactions
      -- in pExpArgs below since a mainpart may contain
      -- declarations
      OG := ObjectGenerator(IV):Items(IS)
      trace("pObjectGenerator:end")
   pSuper ->  out IV: ref Invocation:
      trace("pSuper")
      if (lexer.token <> lexer.symbols.begin):then
         trace("pSuper:idf")
         IV := pInvocation
      :else
         trace("pSuper:empty")
         IV := Exp
         IV.isEmpty := true
      trace("pSuper:end")
   pMainPart -> IS: ref Items:
      trace("pMainPart")
      if (lexer.token = lexer.symbols.begin) :then
         lexer.next
         IS := pItems
         trace("pMainPart:after:pItems")
         switch(lexer.token)
            case((lexer.symbols.end))
                lexer.next
            case((lexer.symbols.end,lexer.symbols.eos))
                 lexer.next
            SyntaxError(6)
      :else
         trace("pMainPart:IS.isEmpty")
         IS := Items
         IS.isEmpty := true
      trace("pMainPart:end")
   pItems -> IS: ref Items:
      IT: ref Item
      trace("pItems")
      IS := Items
      Loop: do
         trace("pItems:loop")
         IT := pItem
         IS.append(IT)
         trace("pItems:after:pItem")
         if (lexer.token = lexer.symbols.semicolon) :then
            lexer.next
            trace("pItems:after:semicolon:next")
            restart(Loop)
      trace("pItems:end")
   pItem -> IT: ref Item:
      trace("pItem")
      switch (lexer.token)
         case((lexer.symbols.inArg,lexer.symbols.outArg))
            "in/out arg".print
            IT := pDecl
         case(lexer.symbols.idf)
            switch(lexer.peekNext(1))
               trace("pItem:idf")
               case((lexer.symbols.colon
                    ,lexer.symbols.virtual
                    ,lexer.symbols.further
                    ,lexer.symbols.final))
                   IT := pDecl
               case(lexer.symbols.leftBrack)
                  trace("pItem:idf:leftBrack")
                  switch (lexer.peekNext(3))
                      case((lexer.symbols.colon,lexer.symbols.virtual))
                          IT := pDecl
                          trace("pItem:efter:colon,virtual")
                      IT := pTransaction
                      trace("pItem:idf:leftBrack:after:pExp")
               trace("pItem:before:pTransaction")
               IT := pTransaction
         case(lexer.symbols.property)
             propId: ref String
             propId :=  lexer.tokenStr
             lexer.next
             IT := pProperty(propId)
             trace("pItem:end:property")
         IT := pTransaction
   pTransaction -> asg: ref Assignment:
      trace("pTransaction")
      asg := Assignment
      L: do
         N: ref Item
         N := pExp
         trace("pTransaction:after:pExp")
         asg.append(N)
         if (lexer.token = lexer.symbols.vAssign) :then
            lexer.next
            restart(L)
      trace("pTransaction:end")
   pExp -> E: ref Exp:
      trace("pExp")
      E := pSimpleExp      
      trace("pExp:before:relOp")
      L: do
         rE: ref Exp
         sym: var String
         if (lexer.symbols.isRelOp(lexer.token)) :then
            trace("pExp:relOp")
            sym := lexer.tokenStr
            lexer.next
            rE := pSimpleExp
            E := BinaryExp(E,sym,rE)
            restart(L)
      trace("pExp:end")
   pSimpleExp -> E: ref Exp:
      trace("pSimpleExp")
      E := pTerm
      L: do
         rE: ref Exp
         sym: var String
         if (lexer.symbols.isAddOp(lexer.token)) :then
            trace("pSimpleExp:addOp")
            sym := lexer.tokenStr
            lexer.next
            rE := pTerm
            E := BinaryExp(E,sym,rE)
            restart(L)
      trace("pSimpleExp:end")
   pTerm -> E: ref Exp:
      trace("pTerm")   
      E := pFactor
      L: do
         rE: ref Exp
         sym: var String
         if (lexer.symbols.isMultOp(lexer.token)) :then
            trace("pTerm:multOp")
            sym := lexer.tokenStr
            lexer.next
            rE := pFactor
            E := BinaryExp(E,sym,rE)
            restart(L)
      trace("pTerm:end")
   pFactor -> E: ref Exp:
      trace("pFactor")   
      E := pInvocation -- pPrimary
      L: do
         rE: ref Exp
         sym: var String
         if (lexer.token = lexer.symbols.special) :then
            trace("pFactor:special")
            sym := lexer.tokenStr
            lexer.next
            rE := pInvocation
            E := BinaryExp(E,sym,rE)
            restart(L)
      trace("pFactor:end")
   pInvocation -> IVs: ref Invocation:
      trace("pInvocation")
      IVs := Invocation
      L: do
         E: ref Exp
         E := pPrimary
         IVs.append(E)
         if (lexer.token = lexer.symbols.dot) :then
            trace("pInvocatios:got:dot")
            lexer.next
            if (lexer.token = lexer.symbols.idf) :then
               restart(L)
            :else
                synTaxError(7)
   pPrimary -> E: ref Item:
      symb: ref String
      prevToken: var integer
      rightB: var integer
      args: ref Arguments
      trace("pPrimary")
      switch(lexer.token)
         case(lexer.symbols.idf)
             trace("pPrimary:idf")
             E := pObjectInvocation
             -- this should perhaps be pObjectInvoccation
         case(lexer.symbols.leftBrack)
             bE: ref BracketedExp
             bE := BracketedExp
             lexer.next
             L: do
                bE.append(pTransaction)
                switch(lexer.token)
                   case((lexer.symbols.comma))
                      lexer.next
                      restart(L)
                   case((lexer.symbols.rightBrack))
                      lexer.next
                   "Syntax error - missign brackets\n".print
                   syntaxError(8)
             E := bE
             trace("pPrimary:end:bracketed")
         case(lexer.symbols.StringSy)
             trace("pPrimary:String")
             E := StringSy(lexer.tokenStr)
             lexer.next
         case(symbols.const)
             trace("pPrimary:const")
             E := Const(lexer.tokenStr)
             lexer.next
         case(symbols.floatConst)
             trace("pPrimary:const")
             E := FloatConst(lexer.tokenStr)
             lexer.next
         case(symbols.charSy)
             trace("pPrimary:const")
             E := CharConst(lexer.tokenStr)
             lexer.next
         trace("pPrimary:empty")
         Ex: ref Exp
         Ex := Exp
         Ex.isEmpty := true
         E := Ex
      trace("pPrimary:end")
      
   pObjectInvocation -> OI: ref Exp:
      symb: ref String
      prevToken: var integer
      rightB: var integer
      args: ref Arguments
      trace("pObjectInvocation")
      if (token = symbols.idf) :then
          symb:= lexer.tokenStr
          lexer.next
          OI := UnaryGen(symb,pExpArgs)
   pExpArgs -> args: ref Arguments:
      args := Arguments
      switch(token)
         case((symbols.begin,symbols.leftBrack,symbols.leftSquare))
             leftB: var integer
             rightB: var integer
             comma: var String
             trace("pExpArgs")
             leftB := token
             args.addsep(brackAsText(leftB))
             rightB := leftB + 1
             next
             pArg: do
                -- args.append(pTransAction) <<<< OBS!
                args.append(pItems) -- can be mainpart
                switch(token)
                   case(symbols.comma)
                      args.addSep(",") 
                      next
                      if (token <> rightB) :then
                         restart(pArg)
                      :else
                          args.addSep(brackAsText(token))
                          next			  
                   case(leftB + 1)
                      next;
                      if (token = symbols.fatComma) :then
                         comma := brackAsText(rightB) + tokenStr
                         next
                         switch(token)
                            case((symbols.leftBrack
                                 ,symbols.leftSquare
                                 ,symbols.begin))
                               leftB := token
                               rightB := leftB + 1
                               next
                               comma := comma + brackAsText(leftB)
                               args.addSep(comma)
                               restart(pArg)
                            syntaxError(9)
                      :else
                          args.addSep(brackAsText(rightB))       

   pExpArgsX -> args: ref Arguments:
      args := Arguments
      switch(token)
         case((symbols.begin,symbols.leftBrack,symbols.leftSquare))
             leftB: var integer
             rightB: var integer
             comma: var String
             trace("pExpArgs")
             leftB := token
             args.addsep(brackAsText(leftB))
             rightB := leftB + 1
             next
             pArg: do
                -- args.append(pTransAction) <<<< OBS!
                args.append(pItems) -- can be mainpart
                if (token = leftB + 1) :then
                   next;
                   if (token = symbols.fatComma) :then
                      comma := brackAsText(rightB) + tokenStr
                      next
                      switch(token)
                         case((symbols.leftBrack,symbols.leftSquare,symbols.begin))
                            leftB := token
                            rightB := leftB + 1
                            next
                            comma := comma + brackAsText(leftB)
                            args.addSep(comma)
                            restart(pArg)
                         syntaxError(10)
                   :else
                      args.addSep(brackAsText(rightB))

   syntaxError(no: var integer):
      "\nsyntax error: ".print
      putint(no)
      " at token: \"".print
      lexer.symbols.asText(lexer.token).print
      put('"')
      newline
      lexer.printLines(5)
      hasSyntaxError := true

   brackAsText(sy: var integer):
         out T: ref String
         switch(sy)
            case((lexer.symbols.leftBrack))
               T := "("
            case((lexer.symbols.rightBrack))
               T := ")"
            case((lexer.symbols.begin))
               T := "{"
            case((lexer.symbols.end))
               T := "\}"
            case((lexer.symbols.leftSquare))
               T := "["
            case((lexer.symbols.rightSquare))
               T := "]"      
   trace(msg: ref String):
      if (false) :then
         put('%')
         msg.print
         put(':')
         put(' ' )
         putint(lexer.token)
         put(' ' )
         lexer.symbols.asText(lexer.token).print
         put(' ')
         lexer.tokenStr.print
         if (lexer.tokenBuf.getTokenNo(1) <> 0) :then
            put(' ')
            putint(lexer.tokenBuf.getTokenNo(1))
            put(' ')
            lexer.symbols.asText(lexer.tokenBuf.getTokenNo(1)).print
         newline
   go:
      out N: ref Item
      --genTokenStream
      "**   parser.go:\n".print      
      lexer.next
      N := pModule
      