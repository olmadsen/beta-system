MonitorSystem: =
   System: LIB.BasicSystem.basicSystem
       Semaphore:
           cnt: ?integer
           M: =Lock
           Q: =ProcessQueue
           init:
               cnt := 1
           wait:
              theActive: ?BasicProcess
              M.get
              cnt := cnt - 1
              %if cnt < 0 %then%
                  thisCore.main.active.status := P_status.WAITING
                  SQS.addWaiting
                  Q.insert(thisCore.main.active)
                  theActive := thisCore.main.active
                  M.free
                  theActive.suspend
              %else%
                  M.free
           signal:
              P: ?BasicProcess
              M.get
              cnt := cnt + 1
              %if cnt <= 0 %then%
                  P := Q.removeNext
                  P.status := P_status.ACTIVE
                  SQS.insert(P)
                  SQS.dcrWaiting
                  M.free
              %else%
                  M.free

       Process: BasicProcess
           start::
               P: ? Process
               P := %this Process
               "starting: ".print
               id.print; 
               newline
               SQS.insert(P)
           inner
       Monitor:
           mutex: =Semaphore
           V: ?integer
           init:
               mutex.init
           entry:
               mutex.wait
               V := V + 1
               inner
               mutex.signal
           wait:
               cond: ?boolean
               loop: =
                   %inner wait 
                   %if cond = false %then%
                       mutex.signal
                       -- P_status.ACTIVE -> thisCore.main.active.status;
                       -- thisCore.main.active.suspend;
                       mutex.wait
                       %restart loop
       inner



 

