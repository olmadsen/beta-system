MonitorSystem: =
   System: LIB.BasicSystem.basicSystem
       Semaphore:
           [globals[P_status,SQS]]
           cnt: ?integer
           M: =Lock
           Q: =ProcessQueue
           init:
               cnt := 1
               M.id:= "Semaphore"
           wait:
              theActive: ?BasicProcess
              M.get
              cnt := cnt - 1
              %if cnt < 0 %then%
                  --thisCore.dummy := 118
                  --thiscore.main.dummyB := true
                  disable
                  SQS.addWaiting
                  enable
                  Q.insert(thisCore.main.active)
                  theActive := thisCore.main.active
                  M.free
                  thisCore.main.active.status := P_status.WAITING
                  theActive.suspend
              %else%
                  M.free
           signal:
              P: ?BasicProcess
              M.get
              cnt := cnt + 1
              %if cnt <= 0 %then%
                  P := Q.removeNext
                  P.status := P_status.ACTIVE
                  disable
                  SQS.insert(P)
                  SQS.dcrWaiting
                  enable
                  M.free
              %else%
                  M.free
       Process: BasicProcess
           [interface[],globals[SQS,monitor,immutable]]
           start::
               P: ? Process
               P := %this Process
               ("starting: " + id + "\n").print
               --id.print; 
               --newline
               SQS.insert(P)
           inner
       Monitor:
           [globals[P_status,SQS],interface[entry]]
           mutex: = Semaphore
           V: ?integer
           init:
               mutex.init
           entry:
               [arguments[immutable],kind[method]]
               mutex.wait
               V := V + 1
               inner
               mutex.signal
           wait:
               cond: ?boolean
               loop: =
                   %inner wait 
                   %if cond = false %then%
                       mutex.signal
                       -- P_status.ACTIVE -> thisCore.main.active.status;
                       -- thisCore.main.active.suspend;
                       mutex.wait
                       %restart loop
       inner



 

