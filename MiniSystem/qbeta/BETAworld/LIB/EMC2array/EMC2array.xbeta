EMC2array: =
    Array:
       pos: ?integer
       pos := 1
       noof: ?integer
       rep: ?Indexed
       put(e: ?integer) -> res: ?array:
           rep.%put e %at% pos
           %if pos = rep.length %then%
               pos:= 1
           %else%
               pos := pos + 1
           res := %this Array
       get -> res: ?integer:
           res:= rep.%get pos
           %if pos = rep.length %then%
               pos:= 1
           %else%
               pos := pos + 1
       nxt -> res: ?integer:
           %if pos = noof %then%
               res := 9999999
           %else%
               res := rep.%get pos + 1
       prv -> res: ?integer:
           %if pos = noof %then%
               res := 9999999
           %else%
               res := rep.%get pos - 1
       forAll:
          current: ?integer          
          loop:
              current := rep.%get pos
              pos := pos + 1
              %inner forAll
              %if pos < noof %then%
                  %restart loop
          pos := 1
          loop
       print:<
          "Array(".print
          %inner print
          "): ".print
          %for 1 %to% rep.length %do%
               putint(rep.%get inx)
               ", ".print
          newline
       %inner Array

    Range(low: ?integer, upp:?integer):
         noof: = (upp - low) + 1
         check(inx: ?integer):
              %if (inx < low) || (inx > upp) %then%
                   "index error in range".print
         print:
            putint(low)
            put(':')
            putint(upp)
         %if noof < 1 %then%
             "range error\n".print
     
    D1_Array(R: ?Range, ElmType:< Object): Array  
       noof:= R.noof
       rep:= Indexed(noof, integer)
       putAt(inx: ?integer, exp: ?integer) -> res: ?Array:
          R.check(inx)
          rep.%put exp %at% (inx - R.low) + 1
          res := %this Array
       getAt(inx: ?integer) -> res: ?integer:
           R.check(inx)
           res := rep.%get (inx - R.low) + 1
       print::<
          R.print
       %inner D1_Array

    D2_Array(R1: ?Range, R2: ?Range) -> res: ?D2_array: Array
        rep:= indexed(R1.noof * R2.noof,integer)
        res := %this D2_Array
        putAt(inx1: ?integer, inx2: ?integer, exp: ?integer) -> res: ?D2_Array:
           R1.check(inx1)
           R2.check(inx2)
           repPos: ?integer
           repPos := (inx1 - R1.low) * R2.noof + (1 + inx2 - R2.low)
           rep.%put exp %at% repPos
        getAt(inx1: ?integer, inx2: ?integer) -> exp: ?integer:
           R1.check(inx1)
           R2.check(inx2)
           repPos: ?integer
           repPos := (inx1 - R1.low) * R2.noof + (1 + inx2 - R2.low)
           exp := rep.%get repPos
        print::<
           R1.print
           ",".print
           R2.print
        "D2_array:".print
    A: = D1_Array(Range(5,10),integer)
    A.put(11)
    A.putAt(6,12)
    putint(A.getAt(5))
    newline
    A.print
    AA: = D2_Array(Range(3,7),Range(2,9))
    AA.putAt(4,5,117)
    putint(AA.getAt(4,5))
    newline
    A.forAll
       putint(current)
       ", ".print
    newline
    AA.print
