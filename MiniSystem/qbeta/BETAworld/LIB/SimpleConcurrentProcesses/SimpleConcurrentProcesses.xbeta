SimpleConcurrentProcesses: =
   System: LIB.BasicSystem.BasicSystem
      Process: Basicprocess
         start::
            SQS.insert(%this Process)
            %inner start
         Q: = LIB.Containers.mQueue.Queue
         L: = Lock
         entry:
            L.get
            Q.insert(%this entry)
            L.free
            (%this entry).suspend       
            %inner entry 
         pendingMethods -> res: ?Boolean:
            res:= Q.isEmpty.not
         exeNext:
            E: ?entry
            Loop: =
                L.get
                %if Q.isEmpty %then%
                    L.free
                    (%this Process).suspend
                    %restart Loop
                E := Q.removeNext
                L.free
                E.resume
         stop:< entry
            %inner stop
            status := P_status.TERMINATED
            (%this Process).suspend
         %inner Process
         cycle
            exeNext
      %inner System
