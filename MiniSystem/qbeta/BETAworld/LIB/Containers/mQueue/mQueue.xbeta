mQueue: obj 
   %Public
   Queue: 
      [globals]
      Element: 
         in elm: ref Object
         out res: ref Element
         pred: ref Element
         succ: ref Element
         res := @this Element
      head: ref Element
      last: ref Element
      %Public      
      scan:
         %Public      
         current: ref Object
         currentElement: ref Element
         currentElement := head
         do: 
            loop: obj 
               @if currentElement <> none :then 
                  current := currentElement.elm
                  @inner scan
                  currentElement := currentElement.succ
                  @restart loop
         do
      scanFromLast:
         %Public            
         current: ref Object
         currentElement: ref Element
         currentElement := last
         do: 
            loop: obj 
               @if currentElement <> none :then 
                  current := currentElement.elm
                  @inner scanFromLast
                  currentElement := currentElement.pred
                  @restart loop
         do
      insert: 
         in elm: ref Object
         E: ref Element
         b: var BOOLEAN
         B := ELM <> NONE
         @if B :then 
            E := Element(elm)
            @if head = none :then 
               head := E
               last := head 
            :else 
               E.succ := head
               head.pred := E
               head := E
      remove: 
         in elm: ref Object
         scan
            @if elm = current :then 
               @if currentElement.pred <> none :then 
                  currentElement.pred.succ := currentElement.succ
                  @if currentElement.succ <> none :then 
                     currentElement.succ.pred := currentElement.pred 
                  :else 
                     last := currentElement.pred 
               :else 
                  head := currentElement.succ
                  @if head <> none :then 
                     head.pred := none 
                  :else 
                     last := none
      removeNext: 
         out elm: ref Object
         @if last <> none :then 
            elm := last.elm
            last := last.pred
            @if last <> none :then 
               last.succ := none 
            :else 
               head := none
      isEmpty: 
         out B: var Boolean
         B := head = none
      clear: 
         head := none
         last := none