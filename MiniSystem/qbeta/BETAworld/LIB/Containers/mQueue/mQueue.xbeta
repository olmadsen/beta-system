mQueue: @
 Queue:
   Element(elm: ^Object) -> res: ^Element:
       pred: ^Element
       succ: ^Element
       res := %this Element
   head: ^Element
   last: ^Element
   scan:
      current: ^Element
      currentElement: ^Element
      currentElement := head
      do:
       loop: @
          %if currentElement <> none %then%
              current := currentElement.elm
              %inner scan
              currentElement := currentElement.succ
              %restart loop
      do
   scanFromLast:
      current: ^Element
      currentElement: ^Element
      currentElement := last
      do:
       loop: @
          %if currentElement <> none %then%
              current := currentElement.elm
              %inner scanFromLast
              currentElement := currentElement.pred
              %restart loop
      do
   insert(elm: ^Object):
       E: ^Element
       %if elm <> none %then%
           E := Element(elm)
           %if head = none %then% {head := E; last := head } %else% 
               E.succ := head
               head.pred := E
               head := E
   remove(elm: ^Object):
       scan
          %if elm = current %then%
              %if currentElement.pred <> none %then% 
                  currentElement.pred.succ := currentElement.succ
                  %if currentElement.succ <> none %then%
                      currentElement.succ.pred := currentElement.pred
                  %else%
                      last := currentElement.pred 
              %else%
                   head := currentElement.succ
                   %if head <> none %then%
                       head.pred := none
                   %else%
                       last := none 
   removeNext -> elm: ^Object:
        %if last <> none %then%
            elm := last.elm
            last := last.pred
            %if last <> none %then% 
                last.succ := none
            %else%
                head := none
   isEmpty -> B: @Boolean:
         B := head = none
   clear:
         head := none
         last := none
