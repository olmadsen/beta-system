Dimensions: obj 
   %domain_boundary
   %public
   Dimension: 
      %public
      PrimaryUnit:< Unit
      Unit: Value
         %unit "U", 1
         %public
         magnitude: var Float
         thisUnit:< Unit
         argUnit:< Unit
         resUnit:< Unit	 
         +? : 
            in V: var thisUnit
            out R: var thisUnit
            R.magnitude := magnitude + V.magnitude
         -? : 
            in V: var thisUnit
            out R: var thisUnit
            R.magnitude := magnitude - V.magnitude
         *? : 
            in V: var Float
            out R: var thisUnit
            R.magnitude := magnitude * V
         /? : 
            in V: var integer
            out R: var thisUnit
            R.magnitude := magnitude / V 
         ^? : 
            in exp: var integer
            out R: var thisUnit
            R.magnitude := magnitude
            if (exp > 0) :then
                for (1) :to(exp - 1) :repeat
                    R.magnitude := R.magnitude * magnitude
            :else
                exp:= (0-exp)
                for (1) :to (exp - 1) :repeat
                    R.magnitude := R.magnitude * magnitude
                R.magnitude := 1.0 / R.magnitude
         *** :
             in V: var argUnit
             out R: var resUnit
             R.magnitude := magnitude * V.magnitude
         =+ : 
            in V: var float
            magnitude := magnitude + V
         square:
            %unit "U*U"
            out R: var Unit
            R.magnitude := magnitude * magnitude
         min:
            in V: var thisUnit
            out R: var thisUnit
            if (magnitude < V.magnitude) :then
                R.magnitude := magnitude
            :else
                R.magnitude := V.magnitude
         max:
            in V: var thisUnit
            out R: var thisUnit
            if (magnitude > V.magnitude) :then
                R.magnitude := magnitude
            :else
                R.magnitude := V.magnitude
         toPrimary:< 
            out base: var PrimaryUnit
            inner(toPrimary)
         fromPrimary:< 
            in base: var PrimaryUnit
            inner(fromPrimary)
         print:<
            "Unit:".print
            inner(print)
            put(':')
            putFloat(magnitude)
      symbol:<
         %globals         
         out T: ref StringLib.String
         inner(Symbol)
      MultArgL:< Unit
      MultArgR:< Unit
      MultRes:< Unit      
      gMult:<
          in L: var MultArgL
          in R: var MultArgR
          out U: var MultRes
          U.magnitude := L.magnitude * R.magnitude
	  
      inner(Dimension)