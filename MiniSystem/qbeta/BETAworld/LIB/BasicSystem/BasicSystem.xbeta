BasicSystem: obj 
   %include Containers,LIB.String
   BasicSystem: 
      Lock: 
         [globals]
         dummy: ref integer
         bbb: ref integer
         free_M: ref integer
         id: ref String
         init: 
            free_M := 0
         get: 
            res: ref integer
            i: ref integer
            loop: obj 
               --disable
               res := 17
               res := free_M.cmpAndSwap(1)
               %if res = 1 
               %then% 
                  --enable
                  --sleep(50)
                  i := i + 1
                  %if i > 10000 
                  %then% 
                     put('?')
                     id.print
                     i := 0
                  %restart loop
         free: 
            free_M := 0
         id := "Lock"
      BasicProcess: 
         in id: ref String
         status: ref integer
         start:<  -- need id as argument
            status := p_status.ACTIVE
            inner
         (%this BasicProcess).suspend
         inner
         status := P_status.TERMINATED
      Scheduler: 
         active: ref BasicProcess
         dummyB: ref Boolean
         inScheduler: ref Boolean
         P: ref BasicProcess
         do: 
            loop: obj 
               inScheduler := 1
               active := SQS.removeNext
               %if active <> none 
               %then% 
                  active.status := P_status.ACTIVE
                  inScheduler := 0
                  active.attach(50)
                  inScheduler := 1
                  %if active.status = P_status.ACTIVE 
                  %then% 
                     SQS.insert(active)
               %if SQS.isEmpty 
               %then% 
                  %if SQS.hasWaiting 
                  %then% 
                     sleep(100)
                     %restart Loop
                  sch.dcr
                  %if sch.running 
                  %then% 
                     sleep(50)
                     sch.add
                     %restart loop 
               %else% 
                  %restart Loop
         (%this Scheduler).suspend
         sch.add
         do
      Core: 
         [core[16]]
         main: ref Scheduler
         dummy: ref integer
         attach: 
            in M: ref Scheduler
            main := M
         do: 
            loopX: obj 
               %if main <> none 
               %then% 
                  main.resume 
               %else% 
                  sleep(100)
                  %restart loopX
         (%this Core).suspend
         --main:= none
         
         dummy := 213
         do
      noOfCores: obj 4
      cores: obj 
         C1: ref Core
         C2: ref Core
         C3: ref Core
         C4: ref Core
         -- should be array
         
         init: 
            S: ref Scheduler
            C1 := Core
            fork(C1)
            C2 := Core
            fork(C2)
            C3 := Core
            fork(C3)
            C4 := Core
            fork(C4)
            S := Scheduler
            C1.attach(S)
            S := Scheduler
            C2.attach(S)
            S := Scheduler
            C3.attach(S)
            C4.attach(Scheduler)
      P_status: obj 
         [globals]
         ACTIVE: obj 1
         WAITING: obj 2
         TERMINATED: obj 3
      ProcessQueue: LIB.Containers.mQueue.Queue
         --[globals[]]
         display: 
            put('Q')
      sch: obj 
         L: obj Lock
         M: ref integer
         entry: 
            L.get
            %inner entry
            L.free
         add: 
            entry
               M := M + 1
         dcr: 
            entry
               M := M - 1
         running: 
            out B: ref Boolean
            entry
               B := M > 0
      SQS: obj 
         Q: obj ProcessQueue
         L: obj Lock
         V: ref integer
         W: ref integer
         entry: 
            L.get
            V := V + 1
            %if V > 1 
            %then% 
               put('V')
            inner
            V := V - 1
            L.free
         init: entry
            V := 1
            L.init
            L.id := "SQS"
         insert: entry
            in P: ref BasicProcess
            Q.insert(P)
         removeNext: 
            out P: ref BasicProcess
            entry
               P := Q.removeNext
         remove: entry
            in P: ref BasicProcess
            Q.remove(P)
         isEmpty: 
            out B: ref Boolean
            entry
               B := Q.isEmpty
         display: entry
            Q.display
         addWaiting: entry
            W := W + 1
         dcrWaiting: entry
            W := W - 1
         hasWaiting: 
            out B: ref boolean
            entry
               B := W > 0
      coreLock: obj Lock
      console: obj 
         L: obj Lock
         V: ref integer
         display: 
            L.get
            inner
            L.free
         L.init
      keyboard: obj BasicProcess("Keyboard")
         B: obj Indexed(100,integer)
         next: ref integer
         top: ref integer
         echo: ref Boolean
         Kscan: 
            loopB: obj 
               %if _kbhit 
               %then% 
                  top := top + 1
                  B.%put _iget %at% top
               keyboard.suspend
               %restart loopB
         get: 
            in caller: ref BasicProcess
            out ch: ref char
            waitForChar: obj 
               %if next < top 
               %then% 
                  next := next + 1
                  ch := B.%get next 
               %else% 
                  caller.suspend
                  %restart waitForChar
         Kscan
      init: 
         SQS.init
      init
      inner
      cores.init