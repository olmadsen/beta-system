LottoEx: obj MonitorSystemLib.MonitorSystem
   %visible LIB.Collections.SetLib, RandomNumberGeneratorLib
   maxNoOfPlayers: val 5
   betSize: val 3
   class Hand(RG: ref LinearRandomGenerator):
      %globals
      numbers: obj Array(betSize,#integer)
      print:
         for (1):to(betSize):repeat
             putint(numbers.get[inx])
             put(' ')
      for (1):to(betSize):repeat
           V: var integer
           V := RG.random(1,34)
           numbers.put(V):at[inx] -- problem if no V
   class Bet(thePlayer: ref Player, theHand: ref Hand):
      %globals
      %immutable
      equal(BT: ref Bet) -> B: var boolean:
         V: var integer
         B := true
         for (1):to(betSize):repeat
             V := theHand.numbers.get[inx]
             L: do
                for (1):to(betSize):repeat
                    if (V = BT.theHand.numbers.get[inx]) :then
                       leave(L)
                B := false
                leave(equal)	 
      print:
         thePlayer.print
         " bet: ".print
         theHand.print	 
         newline
   class Player(inx: var integer): MonitorProcess
      RG: obj LinearRandomGenerator(inx,5,9,34,200)    
      H: ref Hand
      play: do
         cycle
            H := Hand(RG)
            lotto.submit(Bet(this(Player),H))
            if (lotto.stop) :then
               leave(play)
      print:
          id.print    
   lotto: obj Monitor
      possibleToSubmit: var boolean
      seedCount: var integer      
      bets: obj Set(#Bet)
      submit(B: ref Bet): entry
         wait
            cond := possibleToSubmit
         console.display
            "Submit ".print
            B.print	 
         bets.insert(B)
      clearBets: entry
         bets.clear
         possibleToSubmit := true	 
      findWinningBets: entry
         WB: ref LinearRandomGenerator
         WH: ref Hand
         winningBet: ref Bet
         possibleToSubmit := false
         seedCount := seedCount + 1	  
         WB :=  LinearRandomGenerator(seedCount,5,9,17,betSize)	
         WH := Hand(WB)	      
         winningBet := Bet(Player("Winner",0),WH)
         console.display
              "\nfindWinningBets: ".print
              winningBet.print
              newline	      
         bets.scan
             if (current.equal(winningBet)) :then
                "Winner: ".print
                current.print		
         newline		  
      stop -> B: var Boolean: entry
         B := doStop
      setStop: entry
         doStop := true
      doStop: var boolean	 
      possibleToSubmit := true	 	     
   controller: obj MonitorProcess("Controller")
      i: var integer
      L: do
         i := i + 1
         sleep(500)
         console.display
            "\nRound: ".print
            putint(i)
            newline
         lotto.findWinningBets
         lotto.clearBets
         if (i < 5) :then
            restart(L)
         lotto.setStop	    
   generatePlayers: do
       for (1):to(maxNoOfPlayers):repeat
           P: Ref Player
           P := Player("Player" + I2S(inx),inx)
           P.start	   
   lotto.init
   controller.start
   lotto.possibleToSubmit := true
	 
   