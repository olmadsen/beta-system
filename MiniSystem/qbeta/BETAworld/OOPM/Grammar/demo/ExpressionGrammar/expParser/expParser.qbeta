expParser: obj
   %visible expAst
   next:
     pos := pos + 1
     if (pos <= inn.length) :then
        ch :=  inn.@get pos
        if (ch = ' ' ) :then	
           restart(next)	
     :else
        ch := 0
     put(ch)	
   exp:
     out N: ref Node:left:right  
     N := term
     addOp: do
        rN: ref Top
        if (ch = '+') :then
           next
           rN := term
           N := expNode('+'):left(N):right(rN)	
           restart(addOp)
   term:
      out N: ref node:left:right
      N := primary
      multOp: do
         rN : ref Top      
         if (ch = '*') :then
            next
            rN := primary	 
            N := termNode('*'):left(N):right(rN)
            restart(multOP)
   primary:
     out N: ref Node:left:right
     if (ch = '(') :then
        next        
        N := exp
        if (ch = ')') :then
           next
        :else
           syntaxError(1)
     :else
        N := number     
   number:
      out N: ref node:left:right
      switch(ch)
         case(('0','1','2','3'))
            rN: ref Node:left:right
            rN := digit
            if (N == none) :then
                N  := rN
            :else
                N := NumberNode('N'):left(N):right(rN)	    
            restart(number)
   digit:
      out N: ref Node:left:right
      switch (ch)
         case(('0','1','2','3'))
            N:= digitNode(ch):left(empty):right(empty)
            next
         N:= digitNode('?'):left(empty):right(empty)      	    
   syntaxError(n: var integer):
      "\nSyntax error: ".print
      putint(n)
      newline
   parse:
      N: ref Node:left:right
      inn :=  "10 + 11 * (1 + 1)"
      inn.print
      newline
      next
      N := exp      
      if (ch <> 0) :then
          syntaxError(3)
      newline	  
      N.print
      newline
      putint(N.eval)
   inn: ref String
   pos: var integer
   ch: var integer      