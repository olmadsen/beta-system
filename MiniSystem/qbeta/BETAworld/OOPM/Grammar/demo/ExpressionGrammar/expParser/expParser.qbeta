expParser: obj
   %visible expAst
   nextChar:
     -- read the next character from inn and store it in ch
     -- skip white space like blanks and end-of-lines
     -- return ch = 0, if at the end of inn
     pos := pos + 1
     if (pos <= inn.length) :then
        ch :=  inn.get[pos]
        if (ch = ' ') :then	
           restart(nextChar)	
     :else
        ch := 0
   exp -> N: ref Node:left:right:
      -- parse an expression generated from <Exp>
      N := term
      addOp: do
         if (ch = '+') :then
            rN: ref Top
            nextChar
            rN := term
            N := expNode('+'):left(N):right(rN)	
            restart(addOp)
   term ->  N: ref node:left:right:
      -- parse an expression generated from <Term>
      N := primary
      multOp: do
         if (ch = '*') :then
            rN: ref Top
            nextChar
            rN := primary	 
            N := termNode('*'):left(N):right(rN)
            restart(multOP)
   primary -> N: ref Node:left:right:
     -- parse an expression generated from <Primary>
     if (ch = '(') :then
        nextChar        
        N := exp
        if (ch = ')') :then
           nextChar
        :else
           syntaxError(1)
     :else
        N := number     
   number -> N: ref node:left:right:
      if (isDigit(ch)) :then
         rN: ref Node:left:right
         rN := digit
         if (N == none) :then
             N := rN
         :else
             N := NumberNode('N'):left(N):right(rN)	    
         restart(number)
   digit -> N: ref Node:left:right:
      -- parse an expression generated from <Digit>
      if (isDigit(ch)) :then
         N:= digitNode(ch):left(empty):right(empty)
         nextChar
   isDigit(ch: var char) -> B: var Boolean:
       -- return true if ch is a digit
       B := ('0' <= ch) && (ch <= '9') 
   syntaxError(n: var integer):
      -- report a syntax error
      "\nSyntax error:{I2S(n)\n} ".print
   parse:
      -- parse the expression assigned to inn below
      -- return an AST in N 
      N: ref Node:left:right
      inn :=  "10 + 11 * (1 + 9)"
      "\nParse:\n".print
      inn.print
      newline
      nextChar
      N := exp      
      if (ch <> 0) :then
          syntaxError(3)
      "\nResulting AST:\n".print	  
      N.print
      "\n\nEval of exp: {I2S(N.eval)}\n".print
   inn: ref String
   pos: var integer
   ch: var integer      