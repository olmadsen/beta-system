expAst: obj
   Top:
      print:<
         inner(print)
      isEmpty:<
         out B: var Boolean
         inner(isEmpty)	 
      inner(Top)
      eval:<
          out V: var integer
          inner(eval)
   Node(label: var char):left(left: ref top):right(right: ref Top): Top
      print::
         newline
         for (1) :to (ind):repeat
             put(' ')
         ind := ind + 1	     
         put('(')
         put(label)
         left.print
         put(',')
         right.print
         put(')')
         ind := ind - 1
   expNode(label: var char):left(left: ref top):right(right: ref Top): Node(label):left(left):right(right)
      eval::
         V := left.eval + right.eval
   termNode(label: var char):left(left: ref top):right(right: ref Top): Node(label):left(left):right(right)
      eval::
         V := left.eval * right.eval
   numberNode(label: var char):left(left: ref top):right(right: ref Top): Node(label):left(left):right(right)
      eval::
         if (right.isEmpty) :then
             V := left.eval
         :else
             V := (left.eval * 10) + right.eval
   digitNode(label: var char):left(left: ref top):right(right: ref Top):  Node(label):left(left):right(right)
      eval::
         V := label - 48
   empty: top
      isEmpty::
         B := true         
      eval::
         "-:".print
         V := 0      
   ind: var integer      