SubjectObserverX: obj
   %visible StringLib, LIB.Collections.OrderedList, LIB.Collections.SetLib
   class Subject:
      observers: obj Set(#Observer) 
      subscribe(obs: ref Observer):
         "subscribe:".print
         obs.print	 
         observers.insert(obs)
      publish:
         observers.scan
             current.notify(this(Subject))

   class Observer:
      class ObservedSubject:< Subject
      notify(theSubject: ref ObservedSubject):<
         inner(notify)
      print:<
         inner(print)
   class Customer(name: var String):
      ...
      acc: ref Account
      class CustomerSubject: Subject
         issueWithCustomer:
            name.print
            ": There is an issue with your account\n".print
      asSubject: obj CustomerSubject
      alarm: obj Observer
         class ObservedSubject:: CustomerSubject
         notify::
            asSubject.issueWithCustomer
         print::
            this(Customer).print
      print:
          "Customer: ".print
          name.print
          newline
      asSubject.subscribe(alarm)
   class Account(owner: ref Customer, id: var integer, OK: var Boolean):      
      transactions: obj Set(#Transaction)
      checkTransactions:
         if (isNotOK) :then
            asSubject.publish
            owner.asSubject.publish	    
      isNotOK -> B: var Boolean:
         B := OK.not 

      class AccountSubject: Subject
         issueWithTransactions:
            "There is an issue with account no.: ".print
            putint(id)
            " of ".print
            owner.print

      asSubject: obj AccountSubject
      alarm: obj Observer
         class ObservedSubject:: AccountSubject
         notify::
            theSubject.issueWithTransactions   
         print::
            "Account: ".print
            owner.print	       
      asSubject.subscribe(alarm)
   JohnSmith: obj Customer("John Smith")
   LizaJones: obj Customer("Liza Jones")	       
   TommyJones: obj Customer("Tommy Jones")	       
   BarbaraNelson: obj Customer("Barbara Nelson")	  
   accounts: obj OrderedList(#Account)
   transaction:
      ...
   accounts.insert(Account(JohnSmith,1010,true))
   accounts.insert(Account(LizaJones,1020,false))
   accounts.insert(Account(TommyJones,1030,True))   
   accounts.insert(Account(BarbaraNelson,1040,false))

   accounts.scan
      current.checkTransactions
