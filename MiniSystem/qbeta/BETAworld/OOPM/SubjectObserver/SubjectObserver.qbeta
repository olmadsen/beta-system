SubjectObserver: obj
   %visible StringLib, LIB.Collections.OrderedList, LIB.Collections.Set
   class Subject:
      observers: obj Set(#Observer) 
      subscribe(obs: ref Observer):
         "subscribe:".print
         obs.print	 
         observers.insert(obs)
      publish:
         observers.scan
            current.notify(this(Subject))

   class Observer:
      class ObservedSubject:< Subject
      notify(sub: ref ObservedSubject):<
         inner(notify)
      print:<
         inner(print)

   class Account(id: var integer, OK: var Boolean):
      transactions: obj Set(Transaction)
      checkTransactions:
         if (isNotOK) :then
            sub.publish
      isNotOK -> B: var Boolean:
         B := OK.not 

      class AccountSubject: Subject
         issueWithTransactions:
            "There is an issue with account no.: ".print
            putint(id)
            newline	    

      sub: obj AccountSubject
      alarm: obj
         obs: obj Observer
            class ObservedSubject:: AccountSubject
            notify::
               sub.issueWithTransactions   
               -- typen til sub er AccountSubject
               -- og dermed er sub.issueWithTransactions OK
            print::
               putint(id)
               newline	       
   accounts: obj OrderedList(#Account)
   transaction:
      ...
   "Start\n".print      
   accounts.insert(Account(111,true))
   accounts.insert(Account(112,false))
   accounts.insert(Account(113,true))
   accounts.insert(Account(114,false))   
   accounts.scan
      current.sub.subscribe(current.alarm.obs)
   newline
   accounts.scan
      current.checkTransactions
