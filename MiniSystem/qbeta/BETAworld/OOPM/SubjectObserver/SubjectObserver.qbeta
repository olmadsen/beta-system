SubjectObserver: obj
   %visible StringLib, LIB.Collections.OrderedList, LIB.Collections.Set
   class Subject:
      observers: obj Set(#Observer) 
      subscribe(obs: ref Observer):
         "subscribe:".print
         obs.print	 
         observers.insert(obs)
      publish:
         observers.scan
            current.notify(this(Subject))

   class Observer:
      class ObservedSubject:< Subject
      notify(sub: ref ObservedSubject):<
         inner(notify)
      print:<
         inner(print)
   class Customer(id: var integer):
      ...
      acc: ref Account
      class CustomerSubject: Subject
         issueWithCustomer:
            "There is an issue with your account: ".print
            putint(id)
            newline	    
      sub: obj CustomerSubject
      --alarm: obj
      obs: obj Observer
            class ObservedSubject:: CustomerSubject
            notify::
               sub.issueWithCustomer
            print::
               this(Customer).print
      print:
          "Customer: ".print
          putint(id)
          newline
      
   class Account(owner: ref Customer, OK: var Boolean):
      transactions: obj Set(Transaction)
      checkTransactions:
         if (isNotOK) :then
            sub.publish
            owner.sub.publish	    
      isNotOK -> B: var Boolean:
         B := OK.not 

      class AccountSubject: Subject
         issueWithTransactions:
            "There is an issue with the account of: ".print
            owner.print

      sub: obj AccountSubject
      --alarm: obj
      obs: obj Observer
            class ObservedSubject:: AccountSubject
            notify::
               sub.issueWithTransactions   
               -- typen til sub er AccountSubject
               -- og dermed er sub.issueWithTransactions OK
            print::
               "Account: ".print
               owner.print	       

   JohnSmith: obj Customer(111)
   LizaJones: obj Customer(112)	       
   TommyJones: obj Customer(113)	       
   BarbaraNelson: obj Customer(114)	  
   accounts: obj OrderedList(#Account)
   transaction:
      ...
   accounts.insert(Account(JohnSmith,true))
   accounts.insert(Account(LizaJones,false))
   accounts.insert(Account(TommyJones,True))   
   accounts.insert(Account(BarbaraNelson,false))

   accounts.scan
      current.sub.subscribe(current.obs)
   JohnSmith.sub.subscribe(JohnSmith.obs)
   LizaJones.sub.subscribe(LizaJones.obs)
   TommyJones.sub.subscribe(TommyJones.obs)   
   BarbaraNelson.sub.subscribe(BarbaraNelson.obs)

   accounts.scan
      current.checkTransactions
