SimpleSubjectObserver: obj
   %visible StringLib, LIB.Collections.OrderedList, LIB.Collections.SetLib

   class Subject:
      -- may be observed by one or more Observers
      observers: obj Set(#Observer) 
      subscribe(obs: ref Observer):< -- now virtual
         obs.print
         " subscribes to ".print
         inner(subscribe)
         newline	 
         observers.insert(obs)
      publish:
         observers.scan
            print
            " notified: ".print      
            current.notify(this(Subject))
            newline	    
      print:<
         inner(print)
 
   class Observer:
      -- subscribes to events in a Subject
      class ObservedSubject:< Subject
      notify(theSubject: ref ObservedSubject):<
         inner(notify)
      print:<
         inner(print)

   class Alarm(id: var String): Subject
      subscribe:: -- new binding
         print
      print::
         id.print
   class SecurityService(id: var String): Observer
      notify::
          id.print
      print::
         id.print
      
   alarmA: obj Alarm("alarmA")
   alarmB: obj Alarm("alarmB")
   alarmC: obj Alarm("alarmC")
   securityServiceX: obj SecurityService("SecurityServiceX")
   securityServiceY: obj SecurityService("SecurityServiceY")   
   setup: obj
      alarmA.subscribe(SecurityServiceX)
      alarmA.subscribe(SecurityServiceY)
      alarmB.subscribe(SecurityServiceX)
      alarmA.publish
