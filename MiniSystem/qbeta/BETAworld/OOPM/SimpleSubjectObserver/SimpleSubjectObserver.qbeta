SimpleSubjectObserver: obj
   %visible StringLib, LIB.Collections.OrderedList, LIB.Collections.SetLib

   class Subject:
      -- may be observed by one or more Observers
      observers: obj Set(#Observer) 
      whatWentWrong:
         " - this went wrong ".print
         newline
      subscribe(obs: ref Observer):< -- now virtual
         obs.print
         " subscribes to ".print
         inner(subscribe)
         newline
         observers.insert(obs)
      publish:
         observers.scan
            print
            " notified: ".print      
            current.notify(this(Subject))
            newline	    
      print:<
         inner(print)
 
   class Observer:
      -- subscribes to events in a Subject
      class ObservedSubject:< Subject
      notify(theSubject: ref ObservedSubject):<
         inner(notify)
      print:<
         inner(print)

   class Alarm(id: var String): Subject
      subscribe:: -- new binding
         print
      print::
         id.print
   class Stakeholder(id: var String): Observer
      notify::
          id.print
          theSubject.whatWentWrong	  
      print::
         id.print
      
   fireAlarm: obj Alarm("fireAlarm")
   FireDepartment: obj Stakeholder("FireDepartment")
   CenterOwner: obj Stakeholder("CenterOwner")
   ShopA: obj StakeHolder("ShopA")
   ShopB: obj StakeHolder("ShopB")
   ShopC: obj StakeHolder("ShopC")      
   setup: obj
      fireAlarm.subscribe(FireDepartment)
      fireAlarm.subscribe(CenterOwner)      
      fireAlarm.subscribe(ShopA)
      fireAlarm.subscribe(ShopB)
      fireAlarm.subscribe(ShopC)      
      fireAlarm.publish
