LottoExperiment: obj MonitorSystemLib.MonitorSystem
   %visible Collections.OrderedList,Collections.SetLib,LIB.DateTimeLib, LIB.Dimensions, RandomNumberGeneratorLib

   clock:
      %globals
      waitAweek -> B: var Boolean:
         sleep(100)
      now -> T: var DateTime(Date(0,0,0),0 min):
         ...
      oneWeek -> T: var DateTime(Date(0,0,0),0 min):
         ...

   inTheMoodForPlaying(inx: var integer) -> m: var Boolean:
      ran: obj LinearRandomGenerator(inx + 3,5,9,34,10)
      %globals console
      --console.display
      --   "inTheMoodForPlaying:\n".print
      --   ran.print	 
      m := (ran.random(0,1) /% 2) <> 0

   inTheMoodForPlayingSameNumbers(inx: var integer) -> m: var Boolean:
      %globals console
      ran: obj LinearRandomGenerator(inx,5,9,34,10)
      m := (ran.random(0,1) /% 2) <> 0
      --console.display
      --   ran.print
      --   "inTheMoodForPlayingSameNumbers:".print
      --   putint(m)
      --   newline

   class Bet(timeIssued: var DateTime(Date(0,0,0),0 min), 
      playerKind: var String, numbers: ref Indexed(7, #Integer) ):
      %globals
      %immutable
      equal(BT: ref Bet) -> B: var Boolean:
         V: var integer
         B := true
         for (1):to(7):repeat
              V := numbers.get[inx]
              L: do
                 for (1):to(7):repeat
                     if (V = BT.numbers.get[inx]) :then
                        leave(L)
                 B := false
                 leave(equal)
      print:
        "Bet: ".print
        playerKind.print
        for (1):to(7):repeat
            putint(numbers.get[inx])
            put(' ')
        newline	    
   class Player(inx: var integer): MonitorProcess 
      kind: var String
      myBet: ref Bet
      sevenRandomNumbers -> R: ref Indexed(7,#integer):    
         RG: obj LinearRandomGenerator(inx,5,9,34,7)
         --console.display
         --   "sevenRandomNumbers\n".print
         R := RG.randomNumbers
      dt: var DateTime(Date(0,0,0), 0 min)
      if (inTheMoodForPlayingSameNumbers(inx)) :then 
         console.display
             "same:\n".print
         kind := "same"
      :else
         console.display
             "random\n".print
         kind := "random"

      myBet := Bet(clock.now, kind, sevenRandomNumbers)
      L: do
         cycle -- for each week
            if (inTheMoodForPlaying(inx)) :then
               if (kind = "random") :then
                  myBet := Bet(DateTime(Date(0,0,0), 0 min), kind, sevenRandomNumbers)
               lotto.submitBet(myBet)
            clock.waitAweek
            if (lotto.stop) :then
               leave(L)
   lotto: obj monitor
      bets: obj Set(#Bet)
      winningBet: ref Bet
      deadline: var DateTime(Date(0,0,0),0 min)

      clearBets: entry
         bets.clear
 
      submitBet(b: ref Bet): entry
         console.display
             "Submit ".print
             b.print	     
         if ((b.timeIssued <= deadline) &&
            (b.timeIssued >= (deadline - clock.oneWeek))) :then
               bets.insert(b)

      findWinningBets: entry
         noOfRandomWinners: var integer
         noOfSameWinners: var Integer
         WB: obj LinearRandomGenerator(7,5,9,34,7)
         console.display
              "findWinningBets:\n".print
              WB.print	      
         winningBet := Bet(clock.now, "winning bet",WB.randomNumbers)
         bets.scan
            if (current.equal(winningBet)) :then
               if (current.playerKind = "random") :then
                   noOfRandomWinners := noOfRandomWinners + 1
               :else 
                   noOfSameWinners := noOfSameWinners + 1
               console.display
                  "Randomwinners: ".print
                  putint(noOfRandomWinners)
                  " sameWinners: ".print
                  putint(noOfSameWinners)
                  newline		  
      stop -> B: var Boolean: entry
         B := doStop
      setStop: entry
         doStop := true
      doStop: var Boolean	 
   main: obj MonitorProcess("main")
      i: var integer
      L: do
         cycle
            i := i + 1      
            clock.waitAweek
            lotto.findWinningBets
            lotto.clearBets
            lotto.deadline := clock.now + clock.oneWeek
            if (i > 10) :then
               console.display
                   "stop\n".print
               leave(L)
      lotto.setStop
   maxNoOfPlayers: val 5
   inx: var Integer
   players: obj Set(#Player)
   generatePlayers: do
      inx := inx + 1
      if (inx <= maxNoOfPlayers) :then
         players.insert(Player("aPlayer" + I2S(inx),inx))
         restart(generatePlayers)
   -- starting lotto and the players:
   lotto.deadline := clock.now + clock.oneWeek
   -- deadline must be set before starting the players
   lotto.init
   main.start
   players.scan  
      current.start 
   newline
