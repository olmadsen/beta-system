GraphEx: obj
   %visible Collections.Set
   Graph:
      Node:<
         firstEdge: ref Edge  -- root?
         insertEdgeTo(to: ref Node)-> E: ref Edge:<
            E := Edge(this(Node),to)
            inner(insertEdgeTo)
            edges.insert(E)
         edges: obj Set(#Edge)
         display:<
            --"Node: ".print	 
            inner(display)
            --newline	    
         displayEdges:<
            newline
            edges.scan
               " edge to: ".print
               current.to.display
               put(' ')	       
               current.display
               newline	       
      Edge:<
         in from: ref Node
         in to: ref Node
         nextEdge: ref Edge;
         display:<
            --"edge: ".print	 
            inner(display)
      nodes: obj Set(#Node)
      edges: obj Set(#Edge)
      display:
         nodes.scan
            current.display
            current.displayEdges	    
      inner(Graph)
   RoadAndCityGraph: Graph 
      Node::< 
         cityName: ref String
         insertEdgeTo::<
            ...
         display::
             cityName.print
             ...
      Edge::<
         roadLength: var Integer
         ...
         display::
            "roadLength: ".print
            putint(roadLength)
      newCity(nm: var String) -> N: ref Node:
         N := Node
         N.cityName := nm
         nodes.insert(N)
      addRoad(from: ref Node, to: ref Node, dist: var integer) -> E: ref Edge:
         E := from.insertEdgeTo(to) -- from  -> to
         E.roadlength := dist
         edges.insert(E)
         E := to.insertEdgeTo(from)
         E.roadLength := dist	 
         edges.insert(E)
         --to.edges.insert(E)    
      inner(RoadAndCityGraph)	 
   class Network: Graph
      class Node::<  -- what do we have in a node? Mapping: host -> link
         -- receivePacket, sendPacket, ...
         ...
      class Edge::<  -- capacity/speed of connection
         ...
   DK: obj RoadAndCityGraph
      setUp:
         N1: ref Node
         N2: ref Node
         E: ref Edge
         N1 := newCity("Copenhagen")	 
         N2 := newCity("Roskilde")
         E := addRoad(N1,N2,30)
         N1 := newCity("Slagelse")
         E := addRoad(N2,N1,40)
   "GraphEx:\n".print
   DK.setUp
   "DK.print\n".print
   DK.display