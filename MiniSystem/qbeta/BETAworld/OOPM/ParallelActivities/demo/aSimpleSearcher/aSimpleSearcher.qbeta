aSimpleSearcher: obj MonitorSystemLib.MonitorSystem
   -- OBS! This module cannot compile using standard BETAworld
   -- Violates %globals restriction for Process and Monitor in MonitorSystem
   -- Using swith -U suppress warnings
   -- And Set must have a %globals restriction
   %visible Collections.Set, StringLib
   %requires MonitorSystemLib
   Person(name: ref String, age: var integer):
      %globals
      %immutable
      
   records: obj IndexedRef(100,#Person)
   search(first: var integer, last: var integer):
      %globals collector, records
      --collector.putS("Search:\n")
      for (first) :to(last):repeat
          if ((18 <= records.get[inx].age)
              && (records.get[inx].age <= 24)) :then
             collector.add(records.get[inx])
   searcherA: obj MonitorProcess("S1")
      search(1,33) 
   searcherB: obj MonitorProcess("S2")
      search(34,66)
   searcherC: obj MonitorProcess("S3")
      search(67,100)
   collector: obj Monitor
      add(P: ref Person): entry
         --putint(P.age)
         --newline	 
         --("Add: " + P.name + ",age: " + I2S(P.age) + "\n").print
         --matches.insert(P)
         top := top + 1 -- no range check!
         matches.put(P):at[top]
      --matches: obj Set(#Person)
      printMatches: entry
         for (1):to(top):repeat
            P: ref Person
            P := matches.get[inx]
            ("Found: " + P.name + ",age: " + I2S(P.age) + "\n").print
      matches: obj IndexedRef(100,#Person)
      top: var integer
      puts(S: ref String): entry
         S.print
   printer: obj MonitorProcess("Printer")
      wait: do
         if ((searcherA.isTerminated)
             && (searcherB.isTerminated)
             && (searcherC.isTerminated))
         :then
            collector.printMatches
         :else
            restart(wait)
   initRec:
      for (1):to(100):repeat
           P: ref Person
           P:= Person("Natasha",inx)
           --P.age := inx
           records.put(P):at[inx]
   initRec
   --"Start:\n".print
   collector.init
   searcherA.start
   searcherB.start
   searcherC.start
   printer.start