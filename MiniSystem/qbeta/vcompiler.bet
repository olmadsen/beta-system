ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '../compiler_IF/directoryComponents';
INCLUDE 'vgenerator';
INCLUDE '../VM/betaVM'
---program:descriptor---
SystemEnv
(# compile: @generator;
   VM: @ betaVM
     (# putCH:: (# do ch -> put #);
        getCh:: (# do get -> ch #);
        errorEvent:: (# do msg[] -> putline; done #);
        done:: (# do stop #)
     #);
   setWindowEnv:: (# do myWindowEnv[] -> theWindowEnv[]#);
   myWIndowEnv: @ guienv;
   runMode: @integer;
   FN: ^text;
   F: @file;
   readArguments:
     (# arg: ^text
     do run_Beta_INT -> runMode;
        (for i: noOfArguments - 2 repeat
             i + 1 -> arguments -> arg[];
             (if true
              // ('-x' -> arg.equalNCS) then      
                 ' ' -> put;
                 true -> compile.onlyLex
              // ('-c' -> arg.equal) then   
                 run_C_INT -> runMode;
              // ('-C' -> arg.equal) then   
                 save_BC_Image -> runMode;
              // ('-v' -> arg.equalNCS) then   
                 true -> compile.verbose
              // ('-a' -> arg.equalNCS) then
                 true -> compile.newAlloc
             if);             
        for);
        noOfArguments  -> arguments -> FN[];
     #);
do 'qBETA COMPILER: with NEWALLOC enabled: ' -> puttext;
   readArguments;
   ('/MiniSystem/qbeta/BETAworld/','.xbeta') -> compile.initDirectoryModules;
   FN[] -> compile.fixExtension_xbeta -> FN[] -> putline;
   FN[] -> F.name;
   F.openRead;
   
   true -> compile.newAlloc; (* always newAlooc *)
   (none,false,none,FN[]) -> compile; 
   
   (runMode,true) -> VM.init;
   (if not compile.onlyLex and not compile.semanticErrors then
       (compile.runtimeDescs,compile.mainDescNo,FN[],runMode) -> VM.execute.init;
       VM.execute[] -> fork;
    else
       stop
   if);
#)
