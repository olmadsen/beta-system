ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qsemchecker';
---lib:attributes---
traceAE: (# exit false #);
restrictions: semchecker
  (# AccessError: notification
       (# printHeading:: (# do 'Possible access error' -> puttext #);
          report:: (# do traceAE -> value #)
       do inner
       #);
     items::<
       (# scanGlobals:
            (# current: ^Property;
               hasGlobals:< Object
            do (*'scanGlobals: '->putline; dopt->putline;*)
               (if superDesc[] <> none then
                   &superDesc.scanGlobals
                   (# hasGlobals:: (# do this(scanGlobals).hasGlobals #)
                   do current[] -> this(scanGlobals).current[];
                      inner scanGlobals;
               #)if);
               (if props.globals[] <> none then 
                   hasGlobals;
                   props.globals.scanSons
                   (# 
                   do current[] -> this(scanGlobals).current[];
                      inner scanGlobals;
                   #)
               if)
            #);
          checkGlobalsIncluded:
            (* adIS: the AD.IS of application
             * ad.IS.globlas in this.globals
            *)
            (# adIS: ^Items; 
               aQual,vQual: ^ObjectGenerator; (* vQual := vQual *)
               dx: ^Property;
               noGlobals: @boolean
            enter adIS[]
            do true -> noGlobals;
               scanGlobals
               (# hasGlobals:: (# do #)
               do (if traceAE then
                      '\n*** checkGlobalsIncluded: This: ' -> puttext; 
                      current.doPT -> putLine;
                  if);
                  current[] -> dx[];
                  current.P.ATd.getQual -> vQual[];
                  (if traceAe THEN '?in adIS:globals: ' -> puttext; if); 
                  L:
                    (if true then
                        adIS.scanGlobals
                        (# hasGlobals:: 
                             (# 
                             do false -> noGlobals; 
                                (if traceAE then ' hasGlobals'-> putline if)
                             #)
                        do (if traceAE then current.doPT -> puttext; ' ' -> put if);
                           current.P.ATd.getQual -> aQual[];
                           (if (vQual[] -> aQual.inSuper) then 
                               (if traceAE then ' -> OK' -> putline if);
                               leave L
                           if)
                        #);
                        (* not found *)
                        (if traceAE then newline if);
                        inner checkGlobalsIncluded;
                    if)
               #);
               (if noGlobals then
                   (if traceAE then '\n-------------------'->putline if);
                   adIS[] -> accessError
                   (#
                   do adIS.father.doPT -> msg; 
                      '\n >>> has no restrictions' -> msg
                   #)
               if)
            #);
          checkGlobals:
            (# ok,first: @boolean;
               AD: ^Apl; (* The Apl being accessed *)
               OG: ^ObjectGenerator;
               adQual,rQual: ^ObjectGenerator (* adQual <= rQual *)
            enter AD[]
            do (if AD[] = none then
                   'AD none'->putline;
                   true -> ok;
                   leave checkGlobals
               if);
               true -> ok;
               true -> first;
               loop:
                 scanGlobals
                 (# hasGlobals:: 
                      (# 
                      do false -> ok;
                         (if first then 
                             (if traceAE then
                                 '\n*** checkGlobals: ' ->puttext;
                                 AD.doPT -> puttext; 
                             if);
                             false -> first;
                         if);
                      #);
                    DI: ^DataItem
                 do (if traceAE then
                        ' < ' -> puttext; current.doPT -> puttext;
                    if);
                    (*false -> ok; (*global restrictions exists *)
                    (* Current: Property has no desc att - 
                     * Property shuld probably be an Apl and checked as an Apl
                     * We then need to handle primitives 
                     * Property.P is a lexem which is an Apl, so perhaps we are ok!?
                     *)
                    (*'>>> adQual=AD.qual=' -> puttext; AD.doPT -> puttext; ':\n' -> puttext;*)
                    AD.getQual -> adQual[];
                    (* '\n>>> adQual:'->putline; *)
                    (if current.P[] = none then ' P is none'->putline if);
                    (if current.P.ATd[] = none then 'ATd is none'->putline if);
                    current.P.ATd.getQual -> rQual[]; (* Must check current*)
                    (*'>>> adQual:'->putline; adQual.doPT -> putline;*)
                    (if (adQual[] -> rQual.inSuper) then 
                        (if traceAE then ' -> OK'->putline if);
                        true -> ok;
                        leave loop
                    if);
                 #);
               (if not ok then 
                   (if AD.ATd.isPatternDecl then
                       (if traceAE then ' -> Pattern:'->putline if);
                       (*    foo: [Q1,Q2,Q3]
                        *    [Q1,Q2,Q3] must be contained in globals[this(PTN)]
                        *)
                       AD.ATd.desc -> checkGlobalsIncluded
                       (#
                       do AD[] -> accessError
                          (#
                          do AD.doPT -> msg;
                             ' is not restricted by: ' -> msg;
                             dx.doPT -> msg
                          #)
                       #)
                    else
                       AD[] -> AccessError
                       (#
                       do 'Cannot access global data-item: ' -> msg;;
                          AD.doPT -> msg;
                       #)
                   if)
               if)
            #)
       #)
  do inner
  #)
