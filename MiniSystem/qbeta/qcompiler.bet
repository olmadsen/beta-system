ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '../compiler_IF/directoryComponents';
INCLUDE 'qgenerator';
INCLUDE '../miniCompiler/betaVM'
---program:descriptor---
SystemEnv
(# compile: @generator;
   VM: @ betaVM
     (# putCH:: (# do ch -> put #);
        getCh:: (# do get -> ch #);
        errorEvent:: (# do msg[] -> putline; done #);
        done:: (# do stop #)
     #);
   setWindowEnv:: (# do myWindowEnv[] -> theWindowEnv[]#);
   myWIndowEnv: @ guienv;
   runMode: @integer;
   FN: ^text;
   F: @file;
do 'qBETA COMPILER: ' -> puttext;
   2 -> arguments -> FN[];
   (if true
    // ('-x' -> FN.equalNCS) then      
       ' ' -> put;
       3 -> arguments -> FN[];
       true -> compile.onlyLex
    // ('-c' -> FN.equal) then   
       run_C_INT -> runMode;
       3 -> arguments -> FN[]
    // ('-C' -> FN.equal) then   
       save_BC_Image -> runMode;
       3 -> arguments -> FN[]    
    else run_Beta_INT -> runMode
   if);
   ('/MiniSystem/qbeta/BETAworld/','.xbeta') -> compile.initDirectoryModules;
   FN[] -> compile.fixExtension_xbeta -> FN[] -> putline;
   FN[] -> F.name;
   F.openRead;

   (none,false,none,FN[]) -> compile; 
   
   (runMode,true) -> VM.init;
   (if true or not compile.onlyLex and not compile.semanticErrors then
       (compile.runtimeDescs,compile.mainDescNo,FN[],runMode) -> VM.execute.init;
       VM.execute[] -> fork;
    else
       stop
   if);
#)
