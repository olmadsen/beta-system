ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '../miniCompiler/directoryComponents';
INCLUDE 'qgenerator';
INCLUDE '../miniCompiler/betaVM'
---program:descriptor---
SystemEnv
(# compile: @generator;
   VM: @ betaVM
     (# putCH:: (# do ch -> put #);
        getCh:: (# do get -> ch #);
        errorEvent:: (# do msg[] -> putline; done #);
        done:: (# do stop #)
     #);
   setWindowEnv:: (# do myWindowEnv[] -> theWindowEnv[]#);
   myWIndowEnv: @ guienv;
   runC,isXbeta: @boolean;
   FN: ^text;
   F: @file;
   BC: @File;
   RTT: @ File;
do 'qBETA COMPILER: ' -> puttext;
   2 -> arguments -> FN[];
   (if true
    // ('-x' -> FN.equalNCS) then      
       ' ' -> put;
       3 -> arguments -> FN[];
       true -> compile.onlyLex
    // ('-c' -> FN.equalNCS) then   
       true -> runC;
       3 -> arguments -> FN[]
   if);
   ('/MiniSystem/qbeta/BETAworld/','.xbeta') -> compile.initDirectoryModules;
   FN[] -> compile.fixExtension_xbeta -> FN[] -> putline;
   FN[] -> F.name;
   F.openRead;
   (FN[],'..s') -> mkAuxName -> BC.name;

   (none,false,none,FN[],BC[]) -> compile; 
   
   (FN[],'..run') -> mkAuxName -> RTT.name;
   RTT.openWrite;
   runC -> VM.init;
   true -> VM.isXbeta;
   (if not compile.onlyLex then
       (compile.runtimeDescs,compile.mainDescNo,RTT[],runC) -> VM.execute.init;
       VM.execute[] -> fork;
    else
       stop
   if);
#)
