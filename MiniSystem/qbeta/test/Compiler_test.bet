ORIGIN '~beta/basiclib/betaenv'
---lib:attributes---
chdir: external
  (# dir: [0] @char; res: @integer
  enter dir
  do callC
  exit res
  #);
system: external
  (# cmd: [0] @char; res: @integer
  enter cmd
  do callC
  exit res
  #);
popen: external
  (# cmd,mode: [0]@char; res: @integer
  enter(cmd,mode)
  do callC
  exit res
  #);
fgetc: external
  (# F: @integer; ch: @char
  enter F
  do callC
  exit ch
  #);
--- program:descriptor ---
(# exe:
     (# cmd,subPath,input: ^text; 
        withB,withC,background: @boolean;
        TB,TC,TT: ^Text;
        V: @integer
     enter(cmd[],subPath[], withB,withC,background,input[])
     do subPath[] -> (WS.copy).append -> TB[]; 
        cmd[] -> (TB.copy).append -> TB[]; 
        (if (TB -> chdir -> V) <> 0 then
            '\n!!!! chdir failed: ' -> puttext; V -> putint; ' ' -> put;
            TB[] -> putline;
            stop;
        if);
        'pwd' -> system;
        'xbeta ' -> TB[];
        'xbeta ' -> TC[];
        ' -c ' -> TC.append;
        cmd[] -> TB.append;
        cmd[] -> TC.append;
        
        (if first then
            ' > ' -> TB.append; ' >> ' -> TC.append;
            false -> first
         else
            ' >> ' -> TB.append; ' >> ' -> TC.append
        if);
        log[] -> TB.append;
        log[] -> TC.append;
        (if background then '&' -> TB.append; '&' -> TC.append if);
        (if withB then TB[] -> putline; TB -> system if);
        (if withC then TC[] -> putline; TC -> system if);
        WS -> chdir;
     #); 
   checkOOPMstuff:
     (#
     do 'C:/beta/r5.5/MiniSystem/qbeta/BETAworld/OOPM/' -> WS[];
        ('SubjectObserver','',true,false,false,'') -> exe;
     #);
   checkLIBstuff:
     (#
     do 'C:/beta/r5.5/MiniSystem/qbeta/BETAworld/LIB/' -> WS[];
        ('Compiler','',true,false,false,'') -> exe;
     #);
   first: @boolean;
   arg: ^Text; resetLog: @boolean;
   WS,testDir,log: ^text
do 'qBeta test run: Compiler_test: ' -> puttext;
   (if noOfArguments > 1 then
       2 -> arguments -> arg[];
       (if '-r' -> arg.equalNCS then
           true -> resetLog;
           'Log file is reset' -> puttext
       if)
    else
       'test against masterlog.txt: ' -> puttext           
   if);
   newline;
   'pwd' -> system;
   'C:/beta/r5.5/MiniSystem/qbeta/test/' -> testDir[];
   (if resetLog then
       'C_mastertrace.txt' -> (testDir.copy).append -> log[]
    else
       'C_trace.txt' -> (testDir.copy).append -> log[]
   if);
   true -> first;
   checkOOPMstuff;
   checkLIBstuff;
   'Compiler_test:done'->putline;
   (if not resetLog then
       testDir -> chdir;
       'diff -a C_mastertrace.txt C_trace.txt' -> putline;
       'diff -a C_mastertrace.txt C_trace.txt' -> System
   if);
#)

   
