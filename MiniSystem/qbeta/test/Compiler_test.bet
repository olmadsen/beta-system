ORIGIN '~beta/basiclib/betaenv'
---lib:attributes---
chdir: external
  (# dir: [0] @char; res: @integer
  enter dir
  do callC
  exit res
  #);
system: external
  (# cmd: [0] @char; res: @integer
  enter cmd
  do callC
  exit res
  #);
popen: external
  (# cmd,mode: [0]@char; res: @integer
  enter(cmd,mode)
  do callC
  exit res
  #);
fgetc: external
  (# F: @integer; ch: @char
  enter F
  do callC
  exit ch
  #);
--- program:descriptor ---
(# WS: ^text;
   exe:
     (# cmd,subPath,input: ^text; 
        withB,withC,background: @boolean;
        TB,TC,TT: ^Text;
        V: @integer
     enter(cmd[],subPath[], withB,withC,background,input[])
     do subPath[] -> (WS.copy).append -> TB[]; 
        cmd[] -> (TB.copy).append -> TB[]; 
        (if (TB -> chdir -> V) <> 0 then
            '\n!!!! chdir failed: ' -> puttext; V -> putint; ' ' -> put;
            TB[] -> putline;
            stop;
        if);
        'xbeta ' -> TB[];
        'xbeta ' -> TC[];
        ' -c ' -> TC.append;
        cmd[] -> TB.append;
        cmd[] -> TC.append;
        
        (if first then
            ' > ' -> TB.append; ' >> ' -> TC.append;
            false -> first
         else
            ' >> ' -> TB.append; ' >> ' -> TC.append
        if);
        log[] -> TB.append;
        log[] -> TC.append;
        (if background then '&' -> TB.append; '&' -> TC.append if);
        (if withB then TB[] -> putline; TB -> system if);
        (if withC then TC[] -> putline; TC -> system if);
        WS -> chdir;
     #); 
   checkLIBstuff:
     (#
     do 'C:/beta/r5.5/MiniSystem/qbeta/BETAworld/LIB/' -> WS[];
        ('Compiler','',false,true,false,'') -> exe;
        (* We should pass 'small.x' as argument to compiler
         * Cannot use input, since this will be input to xbeta and not Compiler
         * We need a way of supplying input to module compiled by xbeta
         *)
     #);
   first: @boolean;
   T: [10000] @char; S: @text;
   FFF: @integer;
   arg: ^Text; resetLog: @boolean;
   log: ^text
do 'qBeta test run: ' -> puttext;
   (if noOfArguments > 1 then
       2 -> arguments -> arg[];
       (if '-r' -> arg.equalNCS then
           true -> resetLog;
           'Log file is reset' -> puttext
       if)
    else
       'test against masterlog.txt: ' -> puttext           
   if);
   newline;
   'pwd' -> system;
   (* The  path '/home/olm/...' has been working fine for many years,
    * but starting nov 2022, it failed for chdir - dont know why?
    * 
    * '/home/olm/beta/r5.5/MiniSystem/qbeta/BETAworld/workspace/' -> WS[];
    * By changing to 'C:/beta/...' it works again!?
    *)
   'C:/beta/r5.5/MiniSystem/qbeta/BETAworld/workspace/' -> WS[];
   (if resetLog then
       'compiler_masterlog.txt' -> (WS.copy).append -> log[]
    else
       'compiler_runlog.txt' -> (WS.copy).append -> log[]
   if);
   true -> first;
   WS -> chdir;
   checkLIBstuff;

   (if not resetLog then
       'diff -a compiler_masterlog.txt compiler_runlog.txt' -> putline;
       'diff -a compiler_masterlog.txt compiler_runlog.txt' -> System
   if);
#)

   
