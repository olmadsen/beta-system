ORIGIN '~beta/basiclib/betaenv'
---lib:attributes---
chdir: external
  (# dir: [0] @char; res: @integer
  enter dir
  do callC
  exit res
  #);
system: external
  (# cmd: [0] @char; res: @integer
  enter cmd
  do callC
  exit res
  #);
popen: external
  (# cmd,mode: [0]@char; res: @integer
  enter(cmd,mode)
  do callC
  exit res
  #);
fgetc: external
  (# F: @integer; ch: @char
  enter F
  do callC
  exit ch
  #);
SysEnv:
  (# run:
       (# cmd,subPath: ^text; 
          withB,withC,background: @boolean;
          TB,TC: ^Text; V: @integer
       enter(cmd[],subPath[], withB,withC,background)
       do subPath[] -> (WS.copy).append -> TB[]; 
          cmd[] -> (TB.copy).append -> TB[]; 
          (if (TB -> chdir -> V) <> 0 then
              '\n!!!! chdir failed: ' -> puttext; V -> putint; newline;
              'WS: ' -> puttext; WS[] -> putline;
              'TB: ' -> puttext; TB[] -> putline;
              stop;
          if);
          'xbeta ' -> TB[];
          'xbeta ' -> TC[];
          args[] -> TB.append;
          args[] -> TC.append;
          ' -c ' -> TC.append;
          cmd[] -> TB.append;
          cmd[] -> TC.append;
          
          (if first then
              ' > ' -> TB.append; ' >> ' -> TC.append;
              false -> first
           else
              ' >> ' -> TB.append; ' >> ' -> TC.append
          if);
          log[] -> TB.append;
          log[] -> TC.append;
          (if background then '&' -> TB.append; '&' -> TC.append if);
          (if withB then TB[] -> putline; TB -> system if);
          (if withC then TC[] -> putline; TC -> system if);
          WS -> chdir;
       #); 
     init:<
       (#
       enter(WS[],log[])
       do true -> first;
                    inner
       #);
     addArg:
       (# arg:  ^Text;
       enter arg[]
       do (if args[] = none then
              arg[] -> args[]
           else
              arg[] -> args.puttext;
          if);
          ' ' -> args.put;
       #);
     WS,log,args: ^Text;
     first: @Boolean; 
  #);
Tester:
  (# sys: @SysEnv; arg: ^Text;
  do (for i: noOfArguments - 1 repeat
          i + 1 -> arguments -> arg[];
          'arg: '-> puttext; arg[] -> putline;
          (if true
           // '-r' -> arg.equalNCS then
              (* skip *)
           else
              arg[] -> sys.addArg
     if)for);
     inner
  #)
  
   
