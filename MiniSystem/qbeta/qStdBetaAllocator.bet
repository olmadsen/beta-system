ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'qStdBetaChecker';
---LIB:attributes---
qStdBetaAllocator: qStdBetaChecker
  (#
     StdBetaModule::<
       (# alloc:: 
            (# 
            do (*'StdBetaModule:alloc:'-> dumpT; moduleName.doPT->dumpTN;*)
            #)
       #);
     BetaFragments::<
       (# alloc:: 
            (# 
            do (*'BetaFragments:alloc:'-> dumpT;*)
            #)
       #);
     AttSlot::<
       (# alloc::
            (#
            do scanBindings(#do current.alloc #)
            #);
          dumpAlloc:: (#do scanBindings(#do current.dumpAlloc #)#);
       #);
     ObjDescSlot::<
       (# alloc::
            (#
            do scanBindings(#do current.alloc #)
            #);
          dumpAlloc:: (#do scanBindings(#do current.dumpAlloc #)#);
       #);
     ObjectDesc::<
       (# alloc:: 
            (# 
            do (*'*** ObjectDesc:alloc:'-> dumpTN;  dopt -> putHeadN;*)
               (if (sup.theDesc = none) or (sup.theDesc = objDesc[])  then
                   1 -> originOff -> diSize
                else
                   (sup.theDesc).diSize -> diSize;
                   (sup.theDesc).vSize -> vSize;
                   (if sup.isSameBlockLevel then
                       (sup.theDesc).originOff -> originOff
                    else
                       diSize + 1 -> diSize -> originOff
                   if)
               if)
            #);
          objSize::
            (#
            do (if false then
                   '**** ObjectDesc:objSize:'->dumpT; dopt -> putheadN;
               if);
               (if isPrimitive then
                   1 -> diSize (* should be set once and for all *)
                else
                   (if diSize = 0 then
                       (*'**** objDesc:doAlloc:'->dumptn;*)
                       alloc; (* and done*)
                   if);
               if);
               (if false then
                   '**** objDesc:'->dumpT;  
                   'size:'->dumpT; diSize -> putint; newline;
                   dopt -> putHeadN;
               if);
               diSize -> size
            #);
          dumpAlloc::
            (#
            do (*'ObjectDescriptor:'*)
               ' originOff: ' -> puttext; originOff -> putint;
               ' diSize: ' -> puttext; diSize -> putint;
               ' vSize: ' -> puttext; vSize -> putint; newline;
               newOD;
            #);
          originOff,diSize,vSize: @integer
       #);
     BetaDecl::<
       (# alloc:: 
            (# 
            do (*'BetaDecl:alloc:'-> dumpT; dopt -> putheadN;*)
            #);
       #);
     StaticItem::<
       (#  alloc:: 
            (# 
            do (*'StaticItem:alloc:'-> dumpTN;*)
            #);
          isValItem::
            (#
            do (if false then
                   '**   isValItem:'->dumpT; dopt->dumpTN;
               if);
               OS.objSize -> size
            #); 
       #);
     DynamicItem::<
       (#  alloc:: 
            (# 
            do (*'DynamicItem:alloc:'-> dumpTN;*)
            #);
          isValItem::
            (#
            do (*'DynamicItem:isValItem:'->dumpt;*) 1 -> size
            #)
       #);
     BetaPattern::<
       (#
       #);
     BetaVirtualPattern::<
       (# isVirtualDecl:: TrueValue
       #);
     PatternDen::<
       (# objSize::
            (#
            do AD.objSize -> size
            #)
       #);
     BetaNameDecl::<
       (# alloc:: 
            (# size: @integer; OD: ^ObjectDesc
            do (if false then
                   'BetaNameDecl:alloc:'-> dumpT; dopt -> dumpt;
               if);
               (if origin## = ObjectDesc## then
                   origin[] -> OD[];
                   (if (theDcl.IT.isValItem -> size) > 0 then 
                       (if false then
                           'BetaNameDecl:alloc:size:'->dumpt;
                           size -> putint; ' '-> put; dopt -> dumpT;
                       if);
                       OD.diSize + 1 -> off;
                       OD.diSize + size -> OD.diSize
                    else (if false then 'size=0:'->dumpt;  dopt -> dumpT if);
                       (if theDcl.IT.isVirtualDecl then
                           (*'virtual:'->dumpT; *)
                           OD.vSize + 1 -> OD.vSize;
                           OD.vSize -> off;
                           (*'vDecl'-> dumpt*)
                   if)if);
                   (*newline*)
                else
                   '!!!! BetaNameDecl:alloc not handled: ' -> dumpt;
                   origin.label -> dumpTN
               if)
            #);
          dumpAlloc::
            (#
            do indent; 
               dopt -> dumpT; 'off: ' -> puttext; off -> putint; 
               (if theDcl.IT.isVirtualDecl then ' virtual:'->dumpT; if);
               (if not theDcl.IT.isSingular then newline if)
            #);
          off: @integer
       #);
     Nameapl::<
       (# objSize::
            (#
            do (if false then
                   '**** NameApl:theSize:'->dumpT; dopt -> dumptn;
               if);
               (theDesc).objSize -> size
            #);
       #);
  do '\n**** Alloc ****'->putline;
     topModule.alloc;
     topModule.dumpAlloc;
     inner
  #);
          
