ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/fields';
INCLUDE '../subwindow';
---program:descriptor---
guienv
(# W: @window
     (# eventHandler::
          (# onAboutToClose::< (# do inner; terminate #);
          #);
        testSubWindow: Subwindow
          (# init::< 
               (# 
               do (130,80) -> size;
                  INNER;
                  T[] -> setLabel;
                  corners.open
               #);
             setLabel:
               (# T: ^text; P: @point
               enter T[]
               do position -> P;
                  ' V:' -> T.puttext; P.V -> T.putint; 
                  ' H: ' -> T.puttext; P.H -> T.putint; 
                  T[] -> head.label
               #);
             corners: @textField
               (# open::
                    (# T: @text;
                       st: ^textStyle
                    do (borderWidth,borderWidth + 16) -> position;
                       &textStyle[] -> st[];
                       8 -> st.size;
                       st[] -> defaultStyle;
                       (112,48) -> size;
                       'Left: ' -> T.puttext; left -> T.putint;
                       ' Top: ' -> T.puttext; top -> T.putint; T.newline;
                       'Right: ' -> T.puttext; right -> T.putint;
                       ' Bottom: ' -> T.puttext; bottom -> T.putint; T.newline;
                       'Width: ' -> T.puttext; width -> T.putint;
                       ' Height: ' -> T.puttext; height-> T.putint;
                       T[] -> insert
                    #)
               #)
          #);

        SW1: @testSubWindow
          (# init:: (# do (50,100) -> position #)
          #);
        SW2: @testSubWindow
          (# init:: (# do (200,100) -> position #)
          #);
        SW3: @testSubWindow
          (# init:: (# do (50,200) -> position #)
          #);
        SW4: @testSubWindow
          (# init:: (# do (200,200) -> position #)
          #);
        
        open:: 
          (#
          do 'SW1' -> SW1.init;
             'SW2' -> SW2.init;
             'SW3' -> SW3.init;
             'SW4' -> SW4.init
          #);
     #)
do W.open
#)


