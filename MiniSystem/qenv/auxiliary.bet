ORIGIN '~beta/basiclib/betaenv';

-- lib: attribute --

(* Dublicate declaration from betaVM - not so pretty! *)
Run_BETA_INT: (# exit 0 #);
Run_C_INT: (# exit 1 #);
Save_BC_image: (# exit 3 #);

IntStack:
  (# push:
       (# value: @integer;
       enter value
       do (if top = storage.range then
              storage.range -> storage.extend;
          if);
          top + 1 -> top;
          value -> storage[top];
       #);
     pop:
       (# value: @integer;
       do (if top > 0 then
              storage[top] -> value;
              top - 1 -> top;
          if);
       exit value
       #);
     peek:
       (# value: @integer;
       do (if top > 0 then
              storage[top] -> value;
          if);
       exit value
       #);
     size:
       (# value: @integer;
       do top -> value;
       exit value
       #);
     empty:
       (# result: @boolean;
       do (top = 0) -> result;
       exit result
       #);
     scan:
       (# current: @integer;
       do (for inx: top repeat
               storage[inx] -> current;
               INNER;
          for);
       #);
     print:
       (#
       do '[ ' -> putText;
          scan
          (#
          do current -> putInt;
             ' ' -> putText;
          #);
          ']' -> putline;;
       #);
     storage: [1] @integer;
     top: @integer;
  #);
putHead:
  (# T: ^text; n: @integer;
     hasNL: BooleanValue
       (#
       do L:
            T.scanAll
            (#do (if (ch = ascii.newline) -> value then leave L if)#)
       #); 
     lines:<(#do 5 -> n; inner #);
     emitNL: @boolean
  enter T[]
  do lines;
     (if hasNl -> emitNL then newline if);
     inner;
     L:
       T.scanAll
       (#
       do ch -> put;
          (if ch = ascii.newline then n - 1 -> n if);
          (if n = 0 then '...'->puttext; leave L if)
       #);
     (if emitNL then newline if);
  #);
putheadN: putHead(#do true -> emitNL #);
