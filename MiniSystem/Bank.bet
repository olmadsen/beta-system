origin '~basiclib/betaenv'
---lib:attributes---

Account: 
  (# open:
       (# ow: ^Person;
          amount: @integer
       enter(ow[],amount)
       do ow[] -> owner[]; amount -> balance
       #);
     deposit: 
       (# subj: ^Account; amount,bal: @ integer
       enter(subj[],amount)
       do balance + amount -> balance;
          balance -> bal;
          ('TO: ', subj[], amount) -> transactions.add;
       exit bal
       #);
     withdraw: 
       (# subj: ^Account; amount,bal: @ integer
       enter(subj[],amount)
       do (if (balance - amount) >= 0 then
              balance - amount -> balance
          if);
          balance -> bal;
          ('FROM: ', subj[], amount) -> transactions.add;
       exit bal
       #);
     balance: @ integer;
     owner: ^Person;
     Transactions: @
       (# transaction:
            (# msg: ^text; subj: ^Account; amount: @integer;
            #);
          add: 
            (# msg: ^text; subj: ^Account; amount: @integer;
            enter(msg[],subj[],amount)
            do 'Transfer ' -> msg.prepend;
               subj.owner.name[] -> msg.puttext;
               ' amount: ' -> msg.puttext;
               amount -> msg.putint; msg.newline;
               msg[] -> owner.notify;
            #);     
       #);

  #);
SavingsAccount: Account
  (#
  #);
CheckAccount: Account
  (#
  #);     


Person: 
  (# name: ^text;
     notify:
       (# msg: ^text
       enter msg[]
       do name[] -> puttext; ' got message: ' -> puttext; msg[] -> putline  
       #)
  #);

---program:descriptor---
(# theBank: @
     (# open: 
          (#
          do &Account[] -> JoeA[]; 
             &Account[] -> LisaA[]; 
             &Account[] -> PeterA[]; 
             (people.Joe[],500) -> JoeA.open;
             (people.Lisa[],500) -> LisaA.open;
             (people.Peter[],500) -> PeterA.open;
          #);
        JoeA, LisaA, PeterA: ^Account;
        
        transfer:
          (# from,to: ^Account;
             amount: @integer
          enter(from[],to[],amount)
          do (to[],amount) -> from.withdraw;
             (from[],amount) -> to.deposit;
          #);
     #);
   People: @
     (# open:
          (#
          do Joe.open; Lisa.open; Peter.open
          #);
        Joe: @ Person
          (# open:(# do 'Joe Smith' -> name[] #)
          #);
        Lisa: @ Person
          (# open:(# do 'Lisa Lind' -> name[] #)
          #);
        Peter: @ Person
          (# open:(# do 'Peter Pan' -> name[] #);
             go: 
               (# 
               do (theBank.peterA[],theBank.JoeA[],120) -> theBank.transfer;
                  (theBank.peterA[],theBank.LisaA[],110) -> theBank.transfer;
               #)
          #);
        
     #)
do People.open; theBank.open;
   People.Peter.go
   
#)


