ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'ByteImage';
INCLUDE 'interpreterAPI'
---lib:attributes---
ObjDesc:
  (# name: ^text; descInx,vsize,rsize,originOff: @integer;
     superObjDesc: ^ObjDesc;
     RunTimeObject:
       (* this pattern defines the interface to run-time objects (template)
        * that is used by minienv (worldWindow,sequencediagram)
        *)
       (# isObj: @boolean;
          OSDvisibility: @integer;
          myDescInx: @integer; (* set by instructions in betaVM, 
                                * perhaps just use descInx in enclosing objDesc
                                *)
          myObjDesc: (# D: ^ObjDesc do this(ObjDesc)[] -> D[] exit D[] #); 
          myName:< (# theName: ^text do inner exit theName[] #);
          myOrigin:< (# org: ^RunTimeObject do inner exit org[]#);
          get:<
            (# inx,V: @integer
            enter inx
            do inner
            exit V
            #);
          rget:<
            (# inx: @integer; R: ^template
            enter inx
            do inner
            exit R[]
            #);
          myLscTop:< (# top: @integer do inner exit top #);
          myLsc:< (# top: @integer do inner exit top #);
          saveReturn:< (# descNo,lsc: @integer enter(descNo,lsc) do inner #);
          restoreReturn:< 
            (# descNo,lsc: @integer do inner exit(descNo,lsc) #);
          topReturn:< (# descInx,lsc: @integer do inner exit(descInx,lsc) #);
       do inner
       #);
     cTemplate: RunTimeObject
       (# myName::
            (# T: @text
            do objId -> nameOf -> T; T[] -> theName[]
            #);
          theOrg: ^cTemplate;
          myOrigin:: 
            (#
            do theOrg[] -> org[];
            #);
          get:: (# do (objId,inx) -> getV -> V #);
          rget:: 
            (# S: @integer 
            do (objId,inx) -> getR -> S; 
               (*R[] *)
            #);
          myLscTop:: (# do objId -> cMyLscTop -> top #);
          myLsc:: (# do (objId,0) -> topOfLsc -> top #);
          saveReturn:: (# do (objId,descNo,lsc) -> cSaveReturn#);
          restoreReturn:: 
            (# 
            do objId -> cRestoreReturn -> lsc; 
               objId -> cRestoreReturn -> descNo
            #);
          topReturn:: 
            (# 
            do (objId,0) -> topOfLsc -> lsc;
               (objId,-1) -> topOfLsc -> descInx
            #);
          objId: @integer
       enter objId
       do objId -> getIsObj -> isObj
       exit this(cTemplate)[]
       #);
     Template: RunTimeObject
       (# id: @integer;                   
          vfields: [16] @integer;
          rfields: [24] ^template;
          vstack: [16] @integer; (* perhaps stacks can be integrated with fields *) 
          rstack : [16] ^template;
          vtop,rtop: @integer;
          lscStack: [8] @integer; 
          lscTop: @integer;
          myLscTop:: (# do lscTop -> top #);
          vinxsize,inx: @integer;
          get::
            (# 
            do (if (inx < 1) or (inx > (vfields.range)) then
                   newline; 'index: ' -> puttext; inx -> putint; 
                   ' range: ' -> puttext; vfields.range -> putint; newline;
                   4 -> fatalError
               if);
               (*'get: ' -> puttext; inx->putint; ' : ' -> puttext; 
                * vfields[inx] -> putint; newline*)
               vfields[inx] -> V;
            #);
          rget::
            (# 
            do (if (inx < 1) or (inx > (rfields.range)) then
                   newline; 'index: ' -> puttext; inx -> putint; 
                   ' range: ' -> puttext; rfields.range -> putint; newline;
                   4 -> fatalError
               if);
               rfields[inx][] -> R[];
            #);
          assign:
            (# val,inx: @integer
            enter(val,inx)
            do (if (inx < 1) or (inx > vfields.range) then
                   4 -> fatalError
               if);
               (*'assign: ' -> puttext; inx -> putint; ' : ' -> puttext; 
                * val -> putint; newline;*)
               val -> vfields[inx]
            #);
          fatalError:
            (# errNo: @integer; msg: @text
            enter errNo
            do '\n\n*** betaVM fatal error - ' -> msg.puttext;
               (if errNo
                // 1 then
                   'ValueStack underflow' -> msg.putline
                // 2 then
                   'ReferenceStack underflow' -> msg.putline
                // 3 then
                   'Reference is none' -> msg.putline
                // 4 then
                   'Index error' -> msg.putline
                // 5 then
                   'Illegal/unimplemented op-code' -> msg.putline;
                // 6 then
                   'Execution of of object reference that is NONE' -> msg.putline
               if);
               '*** ObjecDesc: "' -> msg.puttext; myFullName -> puttext;
               '" - DescInx: ' -> msg.puttext; myDescInx -> msg.putint;
               ' - ObjectId: ' -> msg.puttext; id -> msg.putint; msg.newline;
               inner;                   
            #);

          myName:: (# do name[] -> theName[] #);
          myFullName: 
            (# N: ^text; org: ^template
            do (if myOrigin <> none then
                   myOrigin -> org[];
                   org.myFullName -> N[];
                   ':'-> N.put
                else
                   &text[] -> N[]
               if);
               myName -> N.puttext
            exit N[] 
            #);
          myCode: (# exit bc[] #);
          mySuperCode: (# exit superBC[] #);

          myOrigin:: 
            (# orgOff: @integer; 
            do originOff -> orgOff; 
               (if orgOff > 0 then
                   (* for valueRep, orgOff = 0 *)
                   rfields[orgOff][] -> org[] 
               if)            
            #);
          
          myId: 
            (# T: @text 
            do name[] -> T.puttext; ':'->t.put; id -> T.putint; 
               ':descInx:'->T.puttext; myDescInx->T.putint; ' ' -> T.put
            exit T[]
            #);
 
          myDyn: (# exit rstack[1][]#);
          myLsc:: (# do lscStack[lscTop] -> top #);

          saveReturn::
            (# 
            do (*'\nsaveReturn:' ->out.puttext; lscTop -> out.putint; ' ' -> out.put;*)
               myId -> out.puttext; out.newline;
               (if (lscTop + 2 -> lscTop) > lscStack.range then
                   lscStack.range -> lscStack.extend 
               if);
               descNo -> lscStack[lscTop-1];
               lsc -> lscStack[lscTop];
              (* '\nsaveReturn:' ->out.puttext; lscTop -> out.putint; out.newline;
               myId -> out.puttext;
               *)
            #);
          topReturn:: (* descInx,lsc *)
            (# do lscStack[lscTop-1] -> descInx; lscStack[lscTop] -> lsc #);
          restoreReturn::
            (#
            do lscTop - 2 -> lscTop;
               (*'\nrestorereturn: ' -> out.puttext; lsctop ->out.putint; 
                * out.newline; myId -> out.putline; *)
               lscStack[lscTop+1] -> descNo; lscStack[lscTop+2] -> lsc
            #);
          vstackIsEmpty: (# exit vtop=0 #);
          rstackIsEmpty: (# exit rtop=0 #);
          vpush:
            (# val: @integer
            enter val
            do (if (vtop+1->vtop) > vstack.range then vstack.range -> vstack.extend if);
               val -> vstack[vtop]
            #);
          vpop:
            (# val: @integer
            do (if vtop = 0 then 1 -> fatalError if);
               vstack[vtop] -> val; vtop-1 -> vtop
            exit val
            #);
          vdub: 
            (# 
            do (if vtop = 0 then 1 -> fatalError if); 
               vstack[vtop] -> vpush
            #);
          vTopElm: 
            (# val: @integer 
            do (if vtop = 0 then 1 -> fatalerror if);
               vstack[vtop] -> val
            exit val
            #);
          rpush:
            (# val: ^template
            enter val[]
            do (if (rtop+1->rtop) > rstack.range then rstack.range -> rstack.extend if);
               val[] -> rstack[rtop][]
            #);
          rpop:
            (# val: ^template
            do (if rtop = 0 then 2 -> fatalError if);
               rstack[rtop][] -> val[]; rtop-1 -> rtop
            exit val[]
            #);
          rswap:
            (# obj1,stack1,obj2,stack2: ^template
            enter(obj1[],stack1[])
            do rstack[1][] -> obj2[];
               rstack[2][] -> stack2[];
               obj1[] -> rstack[1][];
               stack1[] -> rstack[2][];
               (if rtop = 0 then 2 -> rtop if);
               (if obj1[] = none then  0 -> rtop if)
            exit(obj2[],stack2[])
            #);
          rdub: 
            (# 
            do (if rtop = 0 then 2 -> fatalError if); 
               rstack[rtop][] -> rpush
            #);
          rTopElm:
            (# val: ^template
            do (if rtop = 0 then 2 -> fatalError if);
               rstack[rtop][] -> val[]; 
            exit val[]
            #);
          
          lsc: @integer;
                    
          dump:
            (# msg: ^text; trace: @boolean; 
               out: ^Stream (* OBS - eliminate global out *)
            enter(msg[],trace,out[])
            do out.newline;
               msg[] -> out.putline;               
               (if trace then   
                   'Object: ' -> out.puttext; myName -> out.puttext; ':' -> out.put; 
                   id -> out.putint; ' lsc: ' -> out.puttext; lsc -> out.putint; out.newline;
                   'VFields: ' -> out.puttext; 
                   (for i: Vfields.range repeat vfields[i] -> out.putint; ' ' -> out.put for);
                   out.newline;
                   'Rfields: ' -> out.puttext;
                   (for i: Rfields.range repeat 
                        (if rfields[i][] <> none then 
                            rfields[i].myName -> out.puttext; ':' -> out.put; 
                            rfields[i].id -> out.putint 
                         else
                            '*' -> out.put
                        if);
                        ' ' -> out.put
                   for);
                   out.newline;
                   'VStack: ' -> out.puttext;
                   (for i: vtop repeat vstack[i] -> out.putint; ' ' -> out.put for);
                   out.newline;
                   'RStack: ' -> out.puttext;
                   (for i: rtop repeat 
                        (if rstack[i][] <> none then 
                            rstack[i].myname -> out.puttext; ':'->out.put;
                            rstack[i].id -> out.putint 
                         else
                            '*' -> out.put
                        if);
                        ' ' -> out.put 
                   for);
                   out.newline;
                   'VdtTable: ' -> out.puttext;
                   (for i: vdtTable.range repeat vdtTable[i] -> out.putint; ' ' -> out.put for);
                   out.newline;
                   'lscStack: ' -> out.puttext;
                   (for i: lscTop repeat lscStack[i] -> out.putint; ' ' -> out.put for);
                   '\n--------------' -> out.putline;
               if)
            #) 
       enter(vinxsize,inx)
       do vinxsize + 1 -> vfields.new;  (* vfields[inx]  : range
                                         * vfields[inx+1]: rep[1]
                                         * ...
                                         *)
          vinxsize -> vfields[inx];
       exit this(Template)[]
       #);
     literals: [4] @integer; (* preliminary *) litTop: @integer;
     addLiteral:
       (# value: @integer; (* the value - if text: the length of the text *)
          index,length,inx: @integer
       enter value
       do value -> length; litTop + 1 -> index;
          (if (litTop + length) >= literals.range then 
              literals.range + length -> literals.extend 
          if);
          litTop + 1 -> litTop;
          length -> literals[litTop];
          (for i: length repeat
               INNER addLiteral (* add text possible elements *);
               litTop + 1 -> litTop;
               value -> literals[litTop];
          for)
       exit index
       #);
          
     vdtTable: [4] @integer; (* virtual dispatch table + main parts for inner *)
     vdtAdd:
       (# inx,descInx: @integer; T: @text
       enter(inx,descInx)
       do (if inx > vdtTable.range then 
              (* we should probably calculate and set the range *)
              inx - vdtTable.range -> vdtTable.extend
          if);
          (if true then
              'vdtAdd: inx=' -> T.puttext; inx -> T.putint; 
              ' descInx='-> T.puttext; descInx -> T.putint; 
          if);
          descInx -> vdtTable[inx]
       exit T[]
       #);
     vdtTable_serialize:
       (# BI : ^ByteImage;
          labsStart,litStart,BCstart: @integer;
       enter BI[]
       do (30 + 12 + vdTtable.range * 4) -> labsStart -> BI.put4;
          labsStart + labs.range * 2 -> litStart -> BI.put4;
          litStart + litTop * 2 -> BCstart -> BI.put4;
          (for i: vdtTable.range repeat vdtTable[i] -> BI.put4 for);
          (for i: labs.range repeat labs[i] -> BI.put2 for);
          (for i: litTop repeat literals[i] -> BI.put2 for);
       #);
     bc: @ByteCode(# descId::(# do descInx -> inx #)#);
     
     labs: [0] @integer;
     
     OSDvisibility: @integer;
     
     
     setSuperBC: (# enter(topDescNo,superBC[]) #);
     topDescNo: @integer;
     superBC: ^ByteCode;
     serialize:
       (# BI : ^ByteImage
       enter BI[]
       do name[] -> BI.puttext; 
          descInx -> BI.put4; 
          topDescNo -> BI.put4;
          vSize -> BI.put4;
          rSize -> BI.put4;
          originOff -> BI.put2;
          BI[] -> BC.serialize(#do BI[]->vdTtable_serialize #)
       #);     
     dump: 
       (# 
       do out.newline;
          'Class ' -> out.puttext;
          name[] -> out.puttext; ' '-> out.put; 
          descInx -> out.putint; ' '-> out.put; 
          originOff -> out.putint; out.newline;
          bc.dump
       #);
     init: 
       (#
       enter(name[],descInx,originOff,out[])  
       do (descInx,out[]) -> bc.init;
       #);
     out: ^Stream 
  #);

OSDvisibility_open: (# exit 0 #);
OSDvisibility_SkipInternal: (# exit 1 #);
OSDvisibility_Disguised: (# exit 2 #);

ByteCode: 
  (# B: [8] @int8u; top: @integer;
     init: (# enter(descInx,out[]) #);
     descInx: @integer; (* of this objDesc - and not a possible subpattern af this ObjDesc *)
     descId:< (# inx: @integer do inner exit inx #);
     allocE,enterE,doE,exitE,procE: @ integer;

     out: ^Stream;
     emit:
       (# op: @int8u
       enter op
       do (if (top + 1 -> top) > B.range then
              B.range-> B.extend
          if);
          op -> B[top]
       #);
     emit2:
       (# op: @int16u
       enter op
       do (if (top + 2 -> top) > B.range then
              B.range-> B.extend
          if);
          op div 256 -> B[top - 1];
          op mod 256 -> B[top]
       #);
     
     pos: (# exit top + 1 #);
     breaks: [4] @integer; breakTop: @integer;
     setBreak:
       (# pos: @integer
       enter pos
       do (if false then
              'SetBreak: In: ' -> puttext; descInx -> putint; 
              ' at: ' -> puttext; pos -> putint; newline;
          if);
          (if (breakTop + 1 -> breakTop) > breaks.range then
              breaks.range -> breaks.extend
          if);     
          pos -> breaks[breakTop]
       #);
     isBreak: booleanValue
       (# pos: @integer
       enter pos
       do L:
            (for i: breakTop repeat
                 (if pos = breaks[i] then
                     true -> value;
                     leave L
       if)for)#);
     codes: @ByteCodes;
     patchLab: 
       (# lab,inx: @integer
       enter(lab,inx)
       do (*'Lab: ' -> puttext; lab -> putint; ' in ' -> puttext; inx -> putint; newline;*)
          lab div 256 -> B[inx - 1]; (* should be relative *)
          lab mod 256 -> B[inx] (* should be relative *)
       #);
     serialize:
       (# BI : ^ByteImage
       enter BI[]
       do procE -> BI.put2;
          allocE -> BI.put2;
          enterE -> BI.put2;
          doE -> BI.put2;
          exitE -> BI.put2;
          inner;

          top -> BI.put2;
          (for i: top repeat
               B[i] -> BI.put (* putRep?*)
          for)
       #);
     dump:     
       (# cde:
            (# T: ^text
            enter T[]
            do T[] -> out.puttext; inc;
            #);
          op1:
            (# asChar: @boolean
            enter asChar
            do (if asChar then B[inx] -> out.put else B[inx] -> out.putint if);
               inc;
            #);
          op2:
            (#
            do B[inx]*256 + B[inx+1] -> out.putint; 
               inc; inc
            #);          
          nl: (# do out.newline #);
          inc: (# do inx+1 -> inx #);
          inx: @integer
       do (if false then
              (for i: top repeat
                   B[i] -> putint; 
              for)
           else
              1 -> inx;
              Loop: 
                (# isChar: @boolean
                do (if inx = procE  then 'procE: '  -> out.putline if);
                   (if inx = allocE then 'allocE:'  -> out.putline if);
                   (if inx = enterE then 'enterE: ' -> out.putline if);
                   (if inx = doE then 'doE: ' -> out.putline if);
                   (if inx = exitE then 'exitE: ' -> out.putline if);
                   inx -> out.putint; ':' -> out.put;
                   (if B[inx]
                    // codes.pushc then
                       '\tpushc ' -> cde;
                       op1;
                       nl 
                    // codes.pushc2 then
                       '\tpushc2 ' -> cde;
                       op2; 
                       nl
                    // codes.pushNone then
                       '\tpushNone ' -> cde; nl
                    // codes.pushthis then
                       '\tpushthis ' -> cde; nl
                    // codes.rpush then
                       '\trpush ' -> cde;
                       op1;
                       nl
                    // codes.rpushg then
                       '\trpushg ' -> cde;
                       op1;
                       nl
                    // codes.push then
                       '\tpush ' -> cde;
                       op1;
                       nl
                    // codes.pushg then
                       '\tpushg ' -> cde;
                       op1;
                       nl
                    // codes.xpush then
                       '\txpush ' -> cde; op1; nl
                    // codes.xpushg then
                       '\txpushg ' -> cde; op1; nl
                    // codes.rstore  then
                       '\trstore ' -> cde;
                       op1;
                       nl
                    // codes.rstoreg  then
                       '\trstoreg ' -> cde;
                       op1;
                       nl
                    // codes.store  then
                       '\tstore ' -> cde;
                       op1;
                       nl
                    // codes.storeg  then
                       '\tstoreg ' -> cde;
                       op1;
                       nl
                    // codes.xstore then
                       '\txstore ' -> cde; op1; nl
                    // codes.xstoreg then
                       '\txstoreg ' -> cde; op1; nl
                    // codes.double then
                       '\tdouble ' -> cde; nl
                    // codes.rdouble then
                       '\trdouble ' -> cde; nl
                    // codes.rpop then
                       '\trpop ' -> cde; nl
                    // codes.alloc then
                       '\talloc ' -> cde; op2; ' ' -> out.put; op1; nl
                    // codes.allocIndexed then
                       '\tallocIndexed ' -> cde; op2; ' ' -> out.put; op1; nl
                    // codes.exeAlloc then
                       '\texeAlloc ' -> cde; op2; nl
                    // codes.newVrep then
                       '\tnewVrep ' -> cde; nl
                    // codes.send then
                       '\tsend ' -> cde; op1; nl
                    // codes.sendv then
                       '\tsendv ' -> cde; op1; nl
                    // codes.rtn then
                       '\trtn ' -> cde; true -> op1; nl
                    // codes.rtnExit then
                       '\trtnExit' -> cde; nl
                    // codes.rtnInner then
                       '\trtnInner' -> cde; nl
                    // codes.rtnC then
                       '\trtnC' -> cde; nl
                    // codes.mvStack then
                       '\tmvStack' -> cde; nl;
                    // codes.doExit then
                       '\tdoExit' -> cde; nl
                    // codes.susp then
                       '\tsusp ' -> cde; nl;
                    // codes.call then
                       '\tcall ' -> cde; true -> op1; nl;
                    // codes.innerx then
                       '\tinner ' -> cde; op1; nl
                    // codes.innerExit then
                       '\tinnerExit ' -> cde; op1; nl
                    // codes.doSuper then
                       '\tdoSuper ' -> cde; op2; nl
                    // codes.stop then
                       '\tstop ' -> cde; nl;
                    // codes.prim then
                       false -> isChar;
                       (if B[inx+1]
                        // 2 then
                           '\t%prim put ' -> cde 
                        // 3 then
                           '\t%prim pushIndex ' -> cde 
                        // 4 then
                           '\t%prim storeIndex ' -> cde 
                        // 5 then
                           '\t%prim range ' -> cde 
                        // 7 then
                           '\t%sendMsg ' -> cde
                        // 9 then
                           '\t%prim get ' -> cde
                        // 10 then
                           '\t%prim attach ' -> cde; op1; ' ' -> out.put; true -> isChar;
                        // 11 then
                           '\t%prim disable ' -> cde
                        // 12 then
                           '\t%prim enable ' -> cde
                       if);
                       isChar -> op1;
                       nl
                    // codes.jmp then
                       '\tjmp ' -> cde; op2; nl
                    // codes.jmpFalse then
                       '\tjmpFalse ' -> cde; op2; nl
                    // codes.jmpTrue then
                       '\tjmpTrue ' -> cde; op2; nl
                    // codes.jmpGT then
                       '\tjmpGT ' -> cde; op2; nl
                    // codes.pushText then
                       '\tpushText ' -> cde; op1; nl
                    // codes.eq then
                       '\teq ' -> cde; nl
                    // codes.lt then
                       '\tlt' -> cde; nl
                    // codes.le then
                       '\tle' -> cde; nl
                    // codes.gt then
                       '\tgt ' -> cde; nl
                    // codes.ge then
                       '\tge ' -> cde; nl
                    // codes.ne then
                       '\tne ' -> cde; nl
                    // codes.req then
                       '\treq ' -> cde; nl
                    // codes.plus then
                       '\t+'-> cde; nl
                    // codes.minus then
                       '\t-' -> cde; nl
                    // codes.orr then
                       '\torr' -> cde; nl
                    // codes.xorr then
                       '\txorr'-> cde; nl
                    // codes.mult then
                       '\t*'-> cde; nl
                    // codes.rdiv then
                       '\trdiv' -> cde; nl
                    // codes.idiv then
                       '\tidiv' -> cde; nl
                    // codes.modd then
                       '\tmodd' -> cde; nl
                    // codes.andd then
                       '\tandd' -> cde; nl
                    // codes.uminus then
                       '\tuminus'-> cde; nl
                    // codes.saveBETAworld then
                       '\tsaveBETAworld' -> cde; nl
                    // codes.mkStrucRef then
                       '\tmkStrucRef' -> cde; nl
                    // codes.mkVirtualStrucRef then
                       '\tmkVirtualStrucRef ' -> cde; op1; nl
                    // codes.allocFromStrucRefObj then
                       '\tallocFromStrucRefObj' -> cde; nl
                    // codes.rtnEvent then
                       '\trtnEvent ' -> cde; op1; nl
                    // codes.break then
                       '\tbreak ' -> cde; op1; op2; nl
                    else
                       '\t' -> out.put;
                       B[inx] -> out.putint;
                       inc;
                       nl
                   if);
                   (if inx <= top then restart Loop if)
                #)
          if);
          out.newline
       #)
  #);
RuntimeDescriptors:
  (# OD: [4] ^ObjDesc; top: @integer;
     setCurrent:
       (# name: ^text; descNo,originOff: @integer; out: ^stream
       enter(name[],descNo,originOff,out[])
       do &ObjDesc[] -> OD[descNo][] -> current[];
          (name[],descNo,originOff,out[]) -> current.init
       #);
     current: ^ObjDesc;
     markAllocE: (# do current.BC.top+1 -> current.BC.allocE #);
     markProcE : (# do current.BC.top+1 -> current.BC.procE #);
     markEnterE: (# do current.BC.top+1 -> current.BC.enterE #);
     markExitE: (# do current.BC.top+1 -> current.BC.exitE #);
     markDoE: (# do current.BC.top+1 -> current.BC.DoE #);
     serialize:
       (# BI: ^ByteImage; descs: @integer
       do &ByteImage[] -> BI[];
          (* allocate desc index table *)
          '-' -> BI.put; 
          'x' -> BI.put; 'B' -> BI.put; 'e' -> BI.put; 't' -> BI.put; 'a' -> BI.put;
          '-' -> BI.put; '-' -> BI.put;
          0  -> BI.put4; (* size of binary image *)
          textDescNo -> BI.put4;
          structureRefDescNo -> BI.put4;
          0 -> BI.put4; (* index of stringtable *)
          top -> BI.put4;
          (for i: top repeat 0 -> BI.put2 for);
          (for i: top repeat
               (if OD[i][] <> none then
                   i -> BI.setIndex;                   
                   BI[] -> OD[i].serialize
               if);               
          for);

          BI.copyStrings;
          '\nBefore marktop'->putline;
          BI.markTop;
          (*BI.dump;*)
          @@BI.B[1] -> descs ; (* MUST be last stm - no GC from here to 
                                * init_interpreter i betaVM
                                *)
       exit descs
       #);
     dump:
       (#
       do (for i: top repeat
               (if OD[i][] = none then  (* primitive descriptor has no code *)
                   (*'dump:none:i:' -> puttext; i -> putint; 
                   ' top:' -> puttext; top -> putint; newline;*) 
                else
                   OD[i].dump     
               if);
          for)
       #);
     textDescNo,structureRefDescNo: @integer
  #);
byteCodes: 
  (#
     pushThis: (# exit 1 #);
     pushc: (#exit 2 #);
     
     push: (#exit 3 #);
     rpush: (#exit 4 #);
     pushg: (# exit 5 #);
     rpushg: (# exit 6 #);
     xpush: (# exit 7 #);
     xpushg: (# exit 8 #);     
     
     store: (#exit 9 #);
     rstore: (#exit 10 #);
     storeg: (# exit 11 #);
     rstoreg: (# exit 12 #);     
     xstore: (# exit 13 #);
     xstoreg: (# exit 14#);
     
     double: (#exit 15 #);          
     rdouble: (# exit 16 #);
     
     rtn: (#exit 17 #);
     mvStack: (# exit 18 #);
     stop: (# exit 19 #);
     susp: (# exit 20 #);
     call: (# exit 21 #);
     alloc: (# exit 22 #);

     doExit: (# exit 24 #);
     rtnExit: (# exit 25 #);
     prim: (# exit 26 #);
     
     jmp: (# exit 27 #);
     jmpFalse: (# exit 28 #);
     jmpGT: (# exit 30 #);
     
     pushNone: (# exit 31 #);

     doSuper: (# exit 34 #);
     innerx: (# exit 35 #);
     rtnInner: (# exit 36 #);
     innerExit: (# exit 37 #);
     sendv: (# exit 38 #);
     send: (#exit 39 #);     
     newVrep: (# exit 40 #);
     jmpTrue: (# exit 41 #);
     pushText: (# exit 42 #);
     exeAlloc: (# exit 43 #);
     rtnc: (# exit 44 #);
     rpop: (# exit 45 #);
     
     eq: (#exit 50#);
     lt: (#exit 51#);
     le: (#exit 52#);
     gt: (#exit 53#);
     ge: (#exit 54#);
     ne: (#exit 55#);
     req: (#exit 56#);
     rne: (#exit 57#);
     
     plus: (#exit 61#);
     minus: (#exit 62#);
     
     orr: (#exit 63#);
     xorr: (#exit 64 #);
     nott: (# exit 65 #);
     
     mult: (#exit 66 #);
     rdiv: (#exit 67 #);
     idiv: (#exit 68 #);
     modd: (#exit 69#);
     andd: (#exit 70#);
     
     uminus: (# exit 71#);
     pushc2: (# exit 72#);
     allocIndexed: (# exit 73 #);
     saveBETAworld: (# exit 74 #);
     mkStrucRef: (# exit 75 #);
     mkVirtualStrucRef: (# exit 76 #);
     allocFromStrucRefObj: (# exit 77 #);
     rtnEvent: (# exit 78 #);
     break: (# exit 79 #)
  #);

     
