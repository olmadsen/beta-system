theBank: @
  (# with dk.au.cs.Accounts;
     with dk.au.cs.Persons;
     out: @DefaultScreen;
     People: @ 
       (# Joe,Lisa,Peter: ^Person;
          open: 
            (# 
            do &Person -> Joe[];
               &Person -> Lisa[];
               &Person -> Peter[];
               'Joe Smith' -> Joe.open;
               'Lisa Lind' -> Lisa.open;
               'Peter Pan' -> Peter.open
            #)
       #);
     aBank: @ 
       (# JoeA,LisaA,PeterA: ^Account;
          open: 
            (# 
            do &Account -> JoeA[];
               &Account -> LisaA[];
               &Account -> PeterA[];
               (people.Joe[],300) -> JoeA.open;
               (people.Lisa[],400) -> LisaA.open;
               (people.Peter[],500) -> PeterA.open
            #);
          transfer: 
            (# from,to: ^Account;
               amount: @ integer;
               N: ^text
            enter (from[],to[],amount)
            do '>' -> put;
               from.ownerName -> N[];
               N.print;
               ':' -> put;
               amount -> out.putint;
               '>' -> put;
               to.ownerName -> N[];
               N.print;
               10 -> put;
               (to[],amount) -> from.withdraw;
               (from[],amount) -> to.deposit
            #)
       #);
     go: 
       (# 
       do (aBank.peterA[],aBank.JoeA[],120) -> aBank.transfer;
          (aBank.peterA[],aBank.LisaA[],110) -> aBank.transfer
       #)
  do People.open;
     aBank.open;
     (*go*) 
     (aBank.peterA[],aBank.JoeA[],120) -> aBank.transfer;
     (aBank.peterA[],aBank.LisaA[],110) -> aBank.transfer
  #)
