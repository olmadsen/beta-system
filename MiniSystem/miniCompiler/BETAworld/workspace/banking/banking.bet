banking: @
  (# with dk.au.cs.Collections; 
     Person: 
       (# name: ^Text;
          init: 
            (# value: ^Text
            enter value[]
            do value[] -> name[]
            #)
       #);
     x,y: @ integer;
     constant: 
       (# value: @ integer
       do 13 -> value
       exit value
       #);
     simple1: (# x: @ integer do 11 -> x #);
     simple2: (# x,y: @ integer do 44 -> x; x -> y #);
     simple3: (#  do 43 -> x #);
     simple4: (#  do x -> y #);
     simple5: 
       (# tripple1: 
            (# age: @ integer;
               name: ^Text;
               male: @ integer
            do 25 -> age;
               'James' -> name[];
               1 -> male
            exit (age,name[],male)
            #);
          tripple2: 
            (# age: @ integer;
               name: ^Text;
               male: @ integer
            enter (age,name[],male)
            do age + 3 -> age
            exit (age,name[],male)
            #);
          tripple3: 
            (# age: @ integer;
               name: ^Text;
               male: @ integer
            enter (age,name[],male)
            do (if male = 1 then
                   age - 1 -> age
               if)
            #)
       do tripple1 -> tripple2 -> tripple3
       #);
     simple6: 
       (# a: @ integer;
          r:[3] @ integer;
          x: @ integer
       do 87 -> r[2]; r[2] -> x
       #);
     a:[9] @ integer;
     simple7: (# y: @ integer do 89 -> a[7]; a[7] -> y #);
     simple8: 
       (# x: @ integer; y: @ integer
       do 12 -> x; 13 -> y; (x,y) -> (y,x)
       #);
     simple9: 
       (# x: @ integer; y: @ integer; z: @ integer
       do 13 -> x; 17 -> y; x + y -> z
       #);
     Bank: @ 
       (# interest: @ integer;
          setInterest: 
            (# value: @ integer
            enter value
            do value -> interest
            #);
          addInterest: 
            (# to: ^Account; balance: @ integer
            enter to[]
            do to.getBalance + interest -> balance;
               balance -> to.setBalance
            exit balance
            #);
          transfer: 
            (# from: ^Account;
               to: ^Account;
               amount: @ integer
            enter (from[],to[],amount)
            do amount -> from.withdraw;
               amount -> to.deposit
            #);
          Account: 
            (# owner: ^Person;
               balance: @ integer;
               init: 
                 (# aPerson: ^Person
                 enter aPerson[]
                 do aPerson[] -> owner[]
                 #);
               deposit: 
                 (# amount: @ integer
                 enter amount
                 do balance + amount -> setBalance
                 #);
               withdraw: 
                 (# amount: @ integer
                 enter amount
                 do balance - amount -> setBalance
                 #);
               setBalance: 
                 (# value: @ integer
                 enter value
                 do value -> balance
                 #);
               getBalance: 
                 (# value: @ integer
                 do balance -> value
                 exit value
                 #)
            #)
       #);
     example1: 
       (# myAccount: @ bank.Account
       do 12 -> myAccount.deposit
       #);
     example2: 
       (# joe: ^Person;
          account1: ^bank.Account;
          jane: ^Person;
          account2: ^bank.Account
       do &Person[] -> joe[];
          'Joe Smith' -> joe.init;
          &bank.Account[] -> account1[];
          joe[] -> account1.init;
          100 -> account1.deposit;
          &Person[] -> jane[];
          'Jane Doe' -> jane.init;
          &bank.Account[] -> account2[];
          jane[] -> account2.init;
          300 -> account2.deposit;
          (account1[],account2[],50) -> bank.transfer
       #);
     example3: 
       (# createAccountFor: 
            (# name: ^Text;
               thePerson: ^Person;
               theAccount: ^bank.Account
            enter name[]
            do &Person[] -> thePerson[];
               name[] -> thePerson.init;
               &bank.Account[] -> theAccount[];
               thePerson[] -> theAccount.init
            exit theAccount[]
            #);
          theAccount: ^bank.Account
       do 'Jane Smith'
             -> createAccountFor
             -> theAccount[];
          13 -> theAccount.deposit
       #);
     example4: 
       (# createAccountFor: 
            (# name: ^Text;
               thePerson: ^Person;
               theAccount: ^bank.Account
            enter name[]
            do &Person[] -> thePerson[];
               name[] -> thePerson.init;
               &bank.Account[] -> theAccount[];
               thePerson[] -> theAccount.init
            exit theAccount[]
            #);
          theAccount: ^bank.Account;
          balance: @ integer
       do 4 -> bank.setInterest;
          'Jane Smith'
             -> createAccountFor
             -> theAccount[];
          100 -> theAccount.deposit;
          theAccount[] -> bank.addInterest -> balance
       #);
     simple: 
       (# Mingle: (#  do  #)
       do Mingle;
          simple1;
          simple2;
          simple3;
          simple4;
          simple5;
          simple6;
          simple7;
          simple8
       #);
     example: 
       (# 
       do example1; example2; example3; example4
       #);
     lists: 
       (# lst: ^List; name: ^Text
       do &List[] -> lst[];
          lst.init;
          'Joe' -> lst.append;
          'Jane' -> lst.append;
          'Jack' -> lst.append;
          'Dan' -> lst.append
       #);
     comp1: 
       (# function: 
            (# value: @ integer
            do 3 -> value
            exit value
            #);
          singleton: @ 
            (# value: @ integer
            do 5 -> value
            exit value
            #);
          producer: @ 
            (# value: @ integer
            do 7 -> value;
               producer.suspend;
               11 -> value
            exit value
            #);
          result: @ integer
       do function -> result;
          singleton -> result;
          producer -> result;
          producer -> result
       #);
     comp2: 
       (# producer: @ 
            (# value: @ integer;
               a: @ 
                 (# m1: 
                      (# 
                      do 3 -> value;
                         producer.suspend;
                         b.m2
                      #)
                 #);
               b: @ 
                 (# m2: 
                      (# 
                      do 5 -> value;
                         producer.suspend;
                         c.m3
                      #)
                 #);
               c: @ 
                 (# m3: 
                      (# 
                      do 7 -> value;
                         producer.suspend
                      #)
                 #)
            do a.m1
            exit value
            #);
          result: @ integer
       do producer -> result;
          producer -> result;
          producer -> result
       #);
     comp: (#  do comp1; comp2 #);
     
     error:
       (# x, y: @integer;
          thePerson: ^Person;
       do 10 -> x;
          x -> y;
          thePerson.init;
       #);
  do (if 1 then
         simple
     if);
     (if 1 then
         example
     if);
     (if 0 then
         lists
     if);
     (if 0 then
         comp
     if);
     (if 0 then
         error;
     if);
  #)
