ActorDemo: @ ActorSystem.System
  (# Factorial: Message (# V, fac: @integer enter(V,fac) #);
     Fibonacci: Message(# V,fib: @integer enter(V,fib) #);
     Stop: Message(##);
     
     A1: @ Actor
       (# fac: 
            (# V,res: @integer
            enter V 
            do (if V = 1 then V -> res else (V - 1 -> fac) * V -> res if)
            exit res
            #)
       do (for i: 10 repeat
              (1,i,i -> fac)  -> Factorial -> A3.send; 
          for);
          Stop -> A3.send
       #);
     A2: @ Actor
       (# fib:
            (# next: @integer
            do pred + last -> next;
               last -> pred; 
               next -> last
            exit next
            #);
          last,pred: @integer
       do (2,1, 1->pred) -> Fibonacci -> A3.send;
          (2,2, 1->last) -> Fibonacci -> A3.send;
          (for i: 10 repeat
               (2,2+i,fib) -> Fibonacci -> A3.send
          for);
          Stop -> A3.send
       #);
     A3: @ Actor
       (# stopC: @integer
       do waitMessage:
            cycle
            (#do
            select
            (# fac: ^Factorial; fib: ^Fibonacci
            do (if true
                // Factorial## -> receive then
                   M[] -> fac[]; 
                   console.display
                   (#
                   do 'Factoral('.print;
                      fac.V -> putint; 
                      ') = '.print;
                     fac.fac -> putint;
                      '\n'.print
                   #);
                // Fibonacci## -> receive then
                   M[] -> fib[]; 
                   console.display
                   (#
                   do 'Fibonacci('.print;
                      fib.V -> putint; 
                      ') = '.print;
                      fib.fib -> putint;
                      '\n'.print
                   #)
                // Stop## -> receive then 
                   (if (stopC + 1 -> stopC) = 2 then 
                       leave waitMessage 
                   if)
               if)
            #)#)
       #)     
  do 'ActorSystem demo\n'.print;
    10 -> A1.start; 11 -> A2.start; 12 -> A3.start
  #)

