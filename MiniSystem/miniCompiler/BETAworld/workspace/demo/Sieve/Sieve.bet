Sieve: @ ActorSystem.System
  (# max: (# exit 30 #);
     primeMax: (# exit 1000 #);
     primeNumber: Message(##);
     Sieve: Actor 
       { start:: (# enter succ[] do #);
          print:
            (# P: @integer
            enter P
            do console.display(# do 'Prime: '.print; P -> putint; newline #)
            #);
          display:
            (#
            do console.display
               (# S: ^Sieve
               do 'chain:'.print; id -> putint;
                  succ[] -> S[];
                  loop: 
                    (if S[] <> none then
                        ',' -> put;
                        S.id -> putint; 
                        S.succ[] -> S[];
                        restart loop
                    if);
                  newline
               #)
            #);
          P: @integer;
          succ: ^Sieve
       do inner
       };
     sieve0: @ Sieve
       (# 
       do display;
          2 -> P; 
          P -> print;
          3 -> P;
          Loop:
            (if P < primeMax then
                P -> PrimeNumber -> succ.send;
                p + 2 -> P;
                restart Loop
            if);
          0 -> PrimeNumber -> succ.send;
       #);
     bSieve: Sieve
       (# 
       do select
          (# PP: ^PrimeNumber; 
          do (if PrimeNumber## -> receive then
                 M[] -> PP[];
                 PP.id -> P;
                 P -> print
             if)
          #);
          loop:
            select
            (# PP: ^PrimeNumber; mp,mx: @integer
            do (if PrimeNumber## -> receive then
                   M[] -> PP[];
                   PP.id -> mx;
                   (if mx = 0 then
                        mx -> Primenumber -> succ.send
                    else
                       P -> mp;
                       L: (if mx > mp then mp + P -> mp; restart L if);
                       (if mx < mp then 
                           mx -> Primenumber -> succ.send
                       if);
                       restart loop
               if)if)
            #)
       #);
     sieve31: @ Sieve
       (#
       do loop:
            select
            (# PP: ^PrimeNumber; 
            do
               (if PrimeNumber## -> receive then
                   M[] -> PP[];
                   PP.id -> P;
                   P -> print;
                   (if P > 0 then
                       restart loop
                   if)
               if)
            #);
       #);
     pred,S: ^Sieve
  do 
     sieve0[] -> pred[];
     (for i: max repeat
          &bSieve[] -> S[];
          (i - 1,S[]) -> pred.start;
          S[] -> pred[]
     for);
     (30,sieve31[]) -> pred.start;
     (max + 1,none) -> sieve31.start;
  #)

     
