Sieve: @ ActorSystem.System
  (# max: (# exit 30 #);
     primeMax: (# exit 1000 #);
     primeNumber: Message (##);
     Sieve: Actor 
       (# start:: (# enter succ[] do '!' #);
          print:
            (# P: @integer
            enter P
            do console.display(# do 'Prime: '.print; P -> putint; newline #)
            #);
          display:
            (#
            do console.display
               (# S: ^Sieve
               do 'chain:'.print; id -> putint;
                  succ[] -> S[];
                  loop: 
                    (if S[] <> none then
                        ',' -> put;
                        S.id -> putint; 
                        S.succ[] -> S[];
                        restart loop
                    if);
                  newline
               #)
            #);
          P: @integer;
          succ: ^Sieve
       do inner
       #);
     sieve0: @ Sieve
       (# 
       do display;
          2 -> P; 
          P -> print;
          3 -> P;
          (if succ[] = none then 'Succ is none'.print if);
          Loop:
            (if P < primeMax then
                P -> PrimeNumber -> succ.send;
                p + 2 -> P;
                restart Loop
            if);
          0 -> PrimeNumber -> succ.send;
       #);
     bSieve: Sieve
       (# mp: @integer
       do 
          select
          (# PP: ^PrimeNumber; 
          do 
             (if PrimeNumber## -> receive then
                 M[] -> PP[];
                 PP.id -> P;
                 P -> print
             if)
          #);
          P -> mp;
          loop:
            select
            (# PP: ^PrimeNumber; mx: @integer
            do (if PrimeNumber## -> receive then
                   M[] -> PP[];
                   PP.id -> mx;
                   L: (if mx > mp then mp + P -> mp; restart L if);
                   (*  console.display
                    (#
                    do 'Got: '.print; id -> putint; '>'->put; P -> putint; ':'->put;
                    mx -> putint; ','->put; mp -> putint; newline
                    #);*)
                   (if true
                    // mx = mp then
                    // mx < mp then
                       mx -> Primenumber -> succ.send
                    // mx = 0 then
                       mx -> Primenumber -> succ.send;
                       leave loop
                   if)
               if);
               P -> mp
               restart loop
            #)
       #);
     sieve1001: @ Sieve
       (#
       do  '.'-> put;
          loop:
            select
            (# PP: ^PrimeNumber; 
            do
               (if PrimeNumber## -> receive then
                   M[] -> PP[];
                   PP.id -> P;
                   P -> print;
                   (if P > 0 then
                       restart loop
                   if)
               if)
            #)
       #);
     pred,S: ^Sieve
  do 
     sieve0[] -> pred[];
     (for i: max repeat
          &bSieve[] -> S[];
          (i - 1,S[]) -> pred.start;
          S[] -> pred[]
     for);
     (30,sieve1001[]) -> pred.start;
     (max + 1,none) -> sieve1001.start;
  #)

     
