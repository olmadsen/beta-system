Shop: @RendezvousSystem.rSystem
  (# customer: @ Process
       (# start:: (#  do mailBox.init; 301 -> shopperHandler.start #);
          mailBox: @ Port
            (#  deliver: entry
                 (# inv,amount,price: @ integer
                 enter (inv,amount,price)
                 do '\n:::Customer: goods received:' -> console.puttext;
                    inv -> putint;
                    ',' -> put;
                    amount -> putint;
                    ',' -> put;
                    price -> putint
                 #)               
            #)
          shopperHandler: @ Activity
            (# 
            do (* '\nShopper:a' -> console.puttext;*) 
               (for i: 3 repeat
                    (i,i * i) -> Shop.orderBox.order; 
                    (* '\nShopper:b' -> console.puttext;*) mailBox.accept
               for);
               (* '\nShopper:c' -> console.puttext;*) 
               (0,0) -> shop.orderBox.order
            #)
       do '\nCustomer here!' -> console.puttext
       #);
     Shop: @ Process
       (# start:: 
            (# 
            do orderBox.init;
               out.init;
               201 -> orderHandler.start;
               202 -> importHandler.start
            #);
          orderBox: @ Port
            (# nextInv,nextAmount: @ integer;
               accept:: (#  do exit(nextInv,nextAmount) #);
               order: entry
                 (# inv,amount: @ integer
                 enter (inv,amount)
                 do '\n:::Shop:orderX received:' -> console.puttext;
                    inv -> putint;
                    ',' -> put;
                    amount -> putint;
                    newline;
                    inv -> nextInv; amount -> nextAmount
                 #)
            #);
          out: @ port
            (# receive: entry
                 (# price: @ integer
                 enter (price)
                 do '\n:::Shop:received:price:' -> console.puttext;
                    price -> putint
                 #)
            #);
          store: @ 
            (# add: 
                 (# inv,amount: @ integer
                 enter (inv,amount)
                 do inv -> nextInv; amount -> nextAmount
                 #);
               inStock: booleanValue
                 (# inv,amount: @ integer
                 enter (inv,amount)
                 do true -> value
                 #);
               get: 
                 (# inv,amount: @ integer
                 enter (inv,amount)                      
                 do price + 1 -> price
                 exit (nextinv,nextamount,price)
                 #);
               nextInv,nextAmount,price: @ integer
            #);
          orderHandler: @ Activity
            (# 
            do loop:
                 cycle
                 (# inv,amount: @integer
                 do orderBox.accept -> (inv,amount)
                    (if inv = 0 then
                        leave loop
                    if);
                    wait(#do (inv,amount) -> store.inStock -> value #);
                    store.get -> Customer.mailbox.deliver
                 #)
            #);
          importHandler: @ Activity
            (# 
            do (for i: 2 repeat
                    (i,i + 2) -> supplier.P.request; out.accept
               for);
               (0,0) -> supplier.P.request
            #)
       do '\nShop here'.print
       #);
     Supplier: @ Process
       (# start:: (#  do P.init; 401 -> exportHandler.start #);
          P: @ Port
            (# request: entry
                 (# inv,amount: @ integer
                 enter (inv,amount)
                 do '\n:::Supplier:Goods:requested:' -> console.puttext;
                    inv -> putint;
                    ',' -> put;
                    amount -> putint;
                    inv -> store.inv;
                    amount -> store.amount
                 #)
            #);
          store: @ 
            (# inv,amount,price: @ integer;
               next: (#  do price + 2 -> price exit price #)
            #);
          exportHandler: @ Activity
            (# 
            do (*'\nexportHandlerH:a' -> console.puttext; *) Loop:
                 cycle
                 (# 
                 do P.accept;
                    (*'\nexportHandlerH:b' -> console.puttext; *) 
                    (if store.inv = 0 then
                        leave loop
                    if);
                    store.next -> Shop.out.receive;
                    (* '\nexportHandlerH:c' -> console.puttext; *) 
                    restart loop
                 #)
            #)
       do '\nSupplier here!' -> console.puttext
       #)
  do 101 -> customer.start; 102 -> Shop.start; 103 -> Supplier.start
  #)
