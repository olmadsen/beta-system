Shop: @RendezvousSystem.System
  (# customer: @ Process
       (# start:: 
            (#
            do 'mailBox' -> mailBox.init; 
               301 -> shopperHandler.start 
            #);
          mailBox: @ Port
            (#  deliver: entry
                 (# inv,amount,price: @ integer
                 enter (inv,amount,price)
                 do console.display
                    (#
                    do '\n:::Customer:goods:received:'.print;
                       inv -> putint;
                       ',' -> put;
                       amount -> putint;
                       ',' -> put;
                       price -> putint
                    #)
                 #)               
            #)
          shopperHandler: @ Activity
            (# terminate::
                 (# do console.display(#do '\nShopperHandler terminates'.print #)#)
            do console.display(#do '\nShopperHandler here'.print #);
               (for i: 3 repeat
                    (i,i * i) -> Shop.orderBox.order; 
                    mailBox.accept
               for);
               (0,0) -> shop.orderBox.order
            #);
          terminate::(#do console.display(#do '\nCustomer terminates'.print #)#);
       do console.display(#do '\nCustomer here!'.print #);
       #);
     Shop: @ Process
       (# start:: 
            (# 
            do 'orderBox' -> orderBox.init;
               'out' -> out.init;
               201 -> orderHandler.start;
               202 -> importHandler.start
            #);
          orderBox: @ Port
            (# nextInv,nextAmount: @ integer;
               accept:: 
                 (# nI,nA: @integer  do nextInv -> nI; nextAmount -> nA exit(ni,nA) #);
               order: entry
                 (# inv,amount: @ integer
                 enter (inv,amount)
                 do console.display
                    (#
                    do '\n:::Shop:order received:'.print;
                       inv -> putint;
                       ',' -> put;
                       amount -> putint;
                    #);
                    inv -> nextInv; amount -> nextAmount
                 #)
            #);
          out: @ port
            (# receive: entry
                 (# inv,amount,price: @ integer
                 enter(inv,amount,price)
                 do console.display
                    (#
                    do '\n:::Shop:received:'.print; 
                       inv -> putint; ','->put; amount -> putint; ' ' -> put;
                       price -> putint;
                    #);
                    (inv,amount) -> store.add
                 #)
            #);
          store: @ 
            (# add: 
                 (# inv,amount: @ integer
                 enter(inv,amount)
                 do amounts[inv] + amount -> amounts[inv]
                 #);
               inStock: booleanValue
                 (# inv,amount: @ integer
                 enter (inv,amount)
                 do (amounts[inv] > 0) -> value; 
                    console.display
                    (#
                       do '\n:::Store:inStock:'.print;
                       inv -> putint; '/' -> put; amount->putint; ';'->put;
                       (for i: 5 repeat
                            i -> putint; ':'->put; amounts[i] -> putint; ';'->put
                       for);
                       (if value then ':true'.print else ':false'.print if)
                    #)
                 #);
               get: 
                 (# inv,amount,price: @ integer
                 enter (inv,amount)                      
                 do amounts[inv] - amount -> amounts[inv]
                 exit(inv,amount,price)
                 #);
               amounts: [5] @ integer
            #);
          orderHandler: @ Activity
            (# terminate::
                 (# do console.display(#do '\nOrderHandler terminates'.print #)#);
            do console.display(#do '\nOrderHandler here'.print #);
               loop:
                 cycle
                 (# inv,amount: @integer
                 do orderBox.accept -> (inv,amount);
                    (if inv = 0 then
                        leave loop
                    if);
                    wait(#do (inv,amount) -> store.inStock -> value #); 
                    (inv,amount) -> store.get -> Customer.mailbox.deliver
                 #);
            #);
          importHandler: @ Activity
            (# terminate::
                 (# do console.display(#do '\nImportHandler terminates'.print #)#);
            do console.display(#do '\nImportHandler here'.print #);
               (for i: 4 repeat
                    (i,i + 2) -> supplier.P.request; 
                    out.accept; 
               for); 
               (0,0) -> supplier.P.request;
            #);
          terminate::(#do console.display(#do '\nShop terminates'.print #)#);
       do console.display(#do '\nShop here'.print #)
       #);
     Supplier: @ Process
       (# start:: (#  do 'P ' -> P.init; 401 -> exportHandler.start #);
          P: @ Port
            (# request: entry
                 (# inv,amount: @ integer
                 enter (inv,amount)
                 do console.display
                    (#
                    do '\n:::Supplier:Goods:requested:'.print
                       inv -> putint;
                       ',' -> put;
                       amount -> putint;
                    #);
                    inv -> store.inv;
                    amount -> store.amount;
                 #)
            #);
          store: @ 
            (# inv,amount,price: @ integer;
               next: (#  do price + 2 -> price exit(inv,amount,price) #)
            #);
          exportHandler: @ Activity
            (# terminate::
                 (# do console.display(#do '\nExportHandler terminates'.print #)#);
            do console.display(#do '\nExportHandler here'.print #);
               Loop:
                 cycle
                 (# 
                 do P.accept;
                    (if store.inv = 0 then
                        console.display(#do '\n:Supplier end'.print #);
                        leave loop
                    if);
                    console.display(#do '\n:Supplier: call Shop.out.receive'.print#)
                    store.next -> Shop.out.receive;
                    restart loop
                 #)
            #);
          terminate::(# do console.display(#do '\nSupplier terminates'.print #)#);
       do console.display(#do '\nSupplier here!'.print #);
       #)
  do 101 -> customer.start; 102 -> Shop.start; 103 -> Supplier.start
  #)
