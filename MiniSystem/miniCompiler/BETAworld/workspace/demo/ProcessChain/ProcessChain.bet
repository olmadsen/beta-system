ProcessChain: @ MessagePassingSystem.system
  (# MyMessage: Message
       (# id: @integer; T: ^text
       enter(id,T[])
       do 
       exit this(MyMessage)[]
       #);
     Producer: Process
       (# start:: (# enter next[] do #);
          M: ^MyMessage;
          next: ^Process
       do loop:
            (if 1 then
                getMessage -> M[];
                (if M[] <> none then
                    console.display
                    (# 
                    do 'Producer: '.print; id -> putint; 
                       ' To: '.print; next.id -> putint; ' ' -> put;
                       M.T.print; 
                    #);
                    M[] -> next.sendMessage;
                    (if M.id > 0 then restart loop if)
                 else
                    this(producer).suspend;
                    restart loop
                if);               

            if)
       #);
      cons: @ Process
       (# M: ^MyMessage; N: @integer
       do loop:
            (if 1 then 
                getMessage -> M[];
                (if M[] <> none then
                    console.display
                    (# 
                    do N + 1 -> N -> putint; ': Received: '.print; M.T.print;
                    #);
                    (if M.id > 0 then
                        restart loop
                    if)
                 else
                    cons.suspend;                
                    restart Loop
                if)
            if)
       #);
     first: @ Process
       (# T: ^text
       do (for i: 10 repeat
               (if i
                // 1 then 'Hello from Linnea\n' -> T[]
                // 2 then 'Hello from Jocke\n' -> T[]
                // 3 then 'Hello from Charlotte\n' -> T[]
                // 4 then 'Hello from Marianne\n' -> T[]
                // 5 then 'Hello from Susanne\n' -> T[]
                // 6 then 'Hello from Sofie\n' -> T[]
                // 7 then 'Hello from Christian\n' -> T[]
                // 8 then 'Hello from Johanne\n' -> T[]
                // 9 then 'Hello from Robin\n' -> T[]
                // 10 then 'Hello from Thea\n' -> T[]
               if);                   
               (1,T[]) -> myMessage -> P2.sendMessage;
          for);
          (0,'Stop\n') -> myMessage -> M[]; M[] -> P2.sendMessage;
       #);
     
     P1,P2: ^Producer;
     M: ^MyMessage;
  do 'Many \n'.print;
     100 ->  cons.start;
     &Producer[] -> P1[]; 
     (200,cons[]) -> P1.start;
     (for i: 10 repeat             (* P2.next = P1;*)
          &Producer[] -> P2[];     (* Pn.next = Pn-1;*)
          (100 + i,P1[]) -> P2.start   
          P2[] -> P1[]
     for);
     300 -> first.start;     
  #)


     
