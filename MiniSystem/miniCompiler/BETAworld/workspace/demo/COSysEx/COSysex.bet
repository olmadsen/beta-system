COSysEx: @ COSystem.system
  (# with Containers;
     List: (# a,b,c: @ integer #);
     
     Client: process 
       (# valuex: entry
            (# V: ^List
            enter V[]
            do console.display
               (#do 'Value: '.print;
                  V.a -> putint; ' '-> put;
                  V.b -> putint; ' '-> put;
                  V.c -> putint; ' '-> put;
                  newline;
               #)
            #)
       do console.display(#do 'Client:\n'.print #); inner 
       #);
     C1: @Client
       (# L: ^List
       do console.display(#do 'C1 \n'.print;#);
          &List[] -> L[];
          30 -> L.a; 20 -> L.b; 10 -> L.c;
          (L[],this(Client)[]) -> router.sortx;
          accept;
          C2.stop
       #);
     C2: @Client
       (# 
       do console.display(#do 'C2\n'.print;#);
          accept;
          router.stop;
          sorter.stop;
          printer.stop
       #);
     router: @ Process
       (# sortx: entry
            (# V: ^List; sender: ^Client
            enter(V[],sender[])
            do V[] -> V1[]; sender[] -> S[]
            #);  
          V1: ^List;
          S: ^Client
       do doAccept
          (#
          do (if S[] <> none then
                 (V1[],S[]) -> sorter.sort;
                 none -> S[]
             if)
          #)
       #);
     sorter: @ Process
       (# swap:
            (# a,b,x: @integer
            enter(a,b)
            do (if a > b then
                   a -> x;
                   b -> a;
                   x -> b
               if)
            exit(a,b)
            #);
          sort: entry
            (# V: ^List; sender: ^Client; X: @integer
            enter(V[],sender[])
            do console.display(#do 'sorter.sort:\n'.print #);
               (V.a,V.b) -> swap -> (V.a,V.b);
               (V.b,V.c) -> swap -> (V.b,V.c);
               (V.a,V.b) -> swap -> (V.a,V.b);
               V[] -> sender.valuex
            #);         
       do doAccept 
       #);
     printer: @ Process
       (#
       do doAccept;
       #)
     
  do console.display(#do 'COSysEx:\n'.print;#);
     1 -> C1.start;
     2 -> C2.start;
     3 -> router.start;
     4 -> sorter.start;
     5 -> printer.start
     
  #)
