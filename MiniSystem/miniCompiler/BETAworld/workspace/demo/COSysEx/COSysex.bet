COSysEx: @ COSystem.system
  (# with Containers;
     List: (# a,b,c: @ integer #);
     ImmText: Stream
       (# (*[immutable];*)
          _cons: 
            (# T: ^text 
            enter T[] 
            do 'ImmText:'.print; T.print; newline ;
               T[] -> S[]
            exit this(ImmText)[]
            #);
          S: ^text
       #);
     
     Client: process 
       (# valuex: entry
            (# V: ^List; S:^text
            enter V[]
            do console.display
               (#do 'Value: '.print;
                  V.a -> puti; ' '-> put;
                  V.b -> puti; ' '-> put;
                  V.c -> puti; ' '-> put;
                  newl;
               #);
               &text(100)[] -> S[];
               'Value: ' -> S.puttext;
               V.a -> S.putint; ' '-> S.putx;
               V.b -> S.putint; ' '-> S.putx;
               V.c -> S.putint; ' '-> S.putx;
               S.newline;
               &ImmText(S[])[] -> T[];               
            #);
          T: ^ImmText
       do console.display(#do 'Client:\n'.print #); inner 
       #);
     C1: @Client
       (# L: ^List
       do console.display(#do 'C1 \n'.print;#);
          (for i: 5 repeat
               &List[] -> L[];
               30+i -> L.a; 20+i -> L.b; 10+i -> L.c;
               (L[],this(Client)[]) -> router.sortx;
               accept;
          for);
          C2.stop
       #);
     C2: @Client
       (#
       do console.display(#do 'C2\n'.print;#);
          accept;
          router.stop;
          sorter.stop;
          printer.stop
       #);
     router: @ Process
       (# sortx: entry
            (# V: ^List; sender: ^Client
            enter(V[],sender[])
            do V[] -> V1[]; sender[] -> S[]
            #); 
           print:
            (# T : ^text
            enter T[]
            do T[] -> T1[]
            #);
          V1: ^List;
          S: ^Client;
          T1: ^text
       do doAccept
          (#
          do (if S[] <> none then
                 (V1[],S[]) -> sorter.sort;
                 none -> S[]
             if)
          #)
       #);
     sorter: @ Process
       (# swap:
            (# a,b,x: @integer
            enter(a,b)
            do (if a > b then
                   a -> x;
                   b -> a;
                   x -> b
               if)
            exit(a,b)
            #);
          sort: entry
            (# V: ^List; sender: ^Client; X: @integer
            enter(V[],sender[])
            do console.display(#do 'sorter.sort:\n'.print #);
               (V.a,V.b) -> swap -> (V.a,V.b);
               (V.b,V.c) -> swap -> (V.b,V.c);
               (V.a,V.b) -> swap -> (V.a,V.b);
               V[] -> sender.valuex
            #);         
       do doAccept 
       #);
     printer: @ Process
       (#
       do doAccept;
       #);
     T: ^ImmText
     
  do console.display
     (# x: @integer
     do 117 -> x; 'COSysEx:\n'.print;
        &ImmText('Hello')[] -> T[];
     #);
     1 -> C1.start;
     2 -> C2.start;
     3 -> router.start;
     4 -> sorter.start;
     5 -> printer.start
     
  #)
