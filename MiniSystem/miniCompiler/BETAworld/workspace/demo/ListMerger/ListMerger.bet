ListMerger: @
  (# List: 
       (# init: (#  do #);
          node: 
            (# elm: ^text;
               next: ^Node;
               display: 
                 (# 
                 do elm.print;
                    ' ' -> put;
                    (if next[] <> none[] then
                        next.display
                    if)
                 #)
            #);
          add: 
            (# S: ^text; N: ^Node
            enter S[]
            do &Node[] -> N[];
               S[] -> N.elm[];
               (if first[] = none[] then
                   N[] -> first[];
                   first[] -> last[]
                else
                   N[] -> last.next[];
                   N[] -> last[]
               if)
            #);
          traverse: @ 
            (# next: ^text;
               scan: 
                 (# current: ^node
                 enter current[]
                 do (if current[] <> none[] then
                        current.elm[] -> next[];
                        traverse.suspend;
                        current.next[] -> scan
                    if)
                 #)
            do first[] -> scan;
            exit next[]
            #);
          display: 
            (# 
            do (if first[] <> none[] then
                   first.display; 10 -> put
               if)
            #);
          first,last: ^Node
       #);
     init: 
       (# 
       do &List[] -> L1[];
          L1.init;
          &List[] -> L2[];
          L2.init;
          'Lars' -> L1.add;
          'Dave' -> L1.add;
          'Scarlett' -> L1.add;
          'Bak' -> L2.add;
          'Ungar' -> L2.add;
          'Johansson' -> L2.add
       #);
     L1,L2: ^List;
     none: ^object;
     first,last: ^text
  do 'Here is ListMerger'.print; 10 -> put;
     init;
     L1.display;
     L2.display;
     (for i: 3 repeat
          L1.traverse -> first[];
          L2.traverse -> last[];
          first.print; ' ' -> put; last.print; 10->put
     for)
  #)
