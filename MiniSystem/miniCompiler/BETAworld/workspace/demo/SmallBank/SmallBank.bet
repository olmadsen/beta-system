SmallBank: @
  (# People: @ 
       (# Person: 
            (# name: ^text;
               open: 
                 (# nm: ^text
                 enter nm[]
                 do nm[] -> name[]
                 #);
               print: (#  do name.print #);
               notify: 
                 (# amount: @ integer
                 enter amount
                 do name.print;
                    ': balance change: '.print;
                    amount -> putint;
                    10 -> put
                 #)
            #);
          Peter,Poul,Mary: @ Person;
          open: 
            (# 
            do 'Peter' -> Peter.open;
               'Mary' -> Mary.open
            #)
       #);
     Bank: @ 
       (# with People; 
          clerk: @ 
            (# transfer: 
                 (# from,to: ^Account;
                    amount: @ integer
                 enter (from[],amount,to[])
                 do 'Transfer: '.print;
                    amount -> putint;
                    10 -> put;
                    amount -> from.withdraw;
                    amount -> to.deposit
                 #)
            #);
          Account: 
            (# balance: @ integer;
               owner: ^Person;
               deposit: 
                 (# amount: @ integer
                 enter amount
                 do balance + amount -> balance;
                    amount -> owner.notify
                 exit balance
                 #);
               withdraw: 
                 (# amount: @ integer
                 enter amount
                 do balance - amount -> balance;
                    - amount -> owner.notify
                 #);
               open: 
                 (# ow: ^Person
                 enter ow[]
                 do ow[] -> owner[]
                 #)
            #);
          Peter_Account,Poul_account,Mary_account: @ Account;
          open: 
            (# 
            do Peter[] -> Peter_Account.open;
               350 -> Peter_Account.deposit;
               Poul[] -> Poul_Account.open;
               Mary[] -> Mary_Account.open
            #)
       #);
     SmallBusiness: @ 
       (# mkPayments: 
            (# 
            do (Bank.Peter_Account[]
                ,100
                ,Bank.Mary_account[])
                  -> Bank.clerk.transfer
            #)
       #)
  do People.open;
     Bank.open;
     SmallBusiness.mkPayments
  #)