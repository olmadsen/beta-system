BasicSystem: @
  (# with Containers; 
     %OSDvisibility=disguised %
     BasicSystem: 
       (# Lock:
            (# id: ^text; M: @integer;
               init: (# T: ^text enter T[] do T[] -> id[]; 0 -> M #);
               get:
                 (# V: @integer; theActive: ^BasicProcess
                 do Loop: 
                      (if 1 then
                          disable;
                          (if (1 -> M.cmpAndSwap) = 1 then 
                             (* (if (V + 1 -> V) > 50000 then
                                  0 -> V;
                                  '\n\n***** Timeout for Lock: '.print;
                                  id.print;
                                  '\n'.print
                              if);*)
                              (if false and not thisCore.main.inScheduler then
                                  '&'->put;
                                  thisCore.main.active[] -> theActive[];
                                  enable;
                                  theActive.suspend;
                                  '$'->put
                               else
                                  enable
                              if);
                              (* 500 -> sleep; *)
                              restart Loop 
                      if)if);
                 #);
               free: (# do 0 -> M; enable #)
            #);
          Core: 
            (# %core%
               main: ^Scheduler;
               attach: (# enter main[] do #);
            do loop:
                 (if main[] <> none then 
                     main                   
                  else 
                     100 -> sleep;
                     restart loop
                 if)
            #);
          noOfCores: (# exit 4 #);
          cores: @
            (# C: [4] ^Core;
               init:
                 (# S: ^Scheduler;
                 do (for i: noOfCores repeat 
                        &Core[] -> C[i][];
                        C[i][] -> fork;
                        &Scheduler[] -> S[];
                        S.init;
                        S[] -> C[i].attach;
                   for);
                 #)
            #);
          BasicProcess: 
            (# start:< 
                 (# 
                 enter id
                 do '\nStarting: '.print;
                    id -> putint;
                    P_status.ACTIVE -> status;
                    inner
                 #);
               display: 
                 (# msg: ^text
                 enter msg[]
                 do console.display
                    (# 
                    do 'BasicProces:'.print;
                       id -> putint;
                       msg.print
                    #)
                 #);
               terminate:< (# do inner #);
               id: @ integer;
               status: @ integer;
            do inner;
               P_status.TERMINATED -> status;
            #);
          P_status: @ 
            (# ACTIVE: (#  exit 1 #);
               WAITING: (#  exit 2 #);
               TERMINATED: (#  exit 3 #)
            #);
          Scheduler: 
            (# init: 
                 (#  
                 do 'Scheduler' -> idf[]; 
                    console.display(#do '\nStarting '.print; idf.print; newline #) 
                 #);
               active: ^BasicProcess;
               idf: ^text;
               display: 
                 (# msg: ^text
                 enter msg[]
                 do console.display
                    (# 
                    do '\nScheduler:'.print; idf.print; 'active:'.print;
                       (if active[] <> none then
                           active.id -> putint; ':status='.print;
                           (if active.status
                            // P_status.ACTIVE then
                               'ACTIVE'.print
                            // P_status.WAITING then
                               'WAITING'.print
                            // P_status.TERMINATED then
                               'TERMINATED'.print
                            else
                               '???'.print
                           if)
                        else
                           'none'.print
                       if);
                       '[' -> put; processId -> putint; ']' -> put;
                       msg.print; ':' -> put; SQS.display
                    #)
                 #);
               processId:< (# id: @ integer do inner exit id #);
               P: ^BasicProcess;
               inScheduler: @boolean
            do true -> inScheduler;
               Loop:
                 (if 1 then
                     SQS.removeNext -> active[];
                     (if active[] <> none then
                         P_status.ACTIVE -> active.status;
                         false -> inScheduler;
                         50 -> active.attach
                         true -> inScheduler;
                         (if active.status = P_status.ACTIVE then
                             active[] -> SQS.insert;                            
                     if)if);
                     (if SQS.isEmpty then 
                         (if SQS.hasWaiting then
                             100 -> sleep;
                             restart loop
                         if)
                      else
                         restart Loop
                 if)if);
               console.display
               (#
               do '\n*** terminated: '.print; idf.print; '\n'.print
               #)
            #);
          ProcessQueue: Queue
            (# display: 
                 (# P: ^BasicProcess
                 do console.display
                    (# 
                    do 'SQS:head:'.print;
                       (if head[] <> none then
                           head.elm[] -> P[];
                           P.id -> putint
                       if);
                       '[' -> put;
                       scan
                       (# 
                       do current[] -> P[];
                          P.id -> putint;
                          ' ' -> put
                       #);
                       '] last:'.print;
                       (if last[] <> none then
                           last.elm[] -> P[];
                           P.id -> putint
                       if);
                       newline
                    #)
                 #);
            #);
          SQS: @ 
            (# Q: @ ProcessQueue;
               L: @lock;
               V: @integer; 
               init: entry(# do 'SQS' -> L.init; #);
               entry: 
                 (# 
                 do L.get;
                    (if (V + 1 -> V) > 1 then 
                        console.display(#do '\nLock:entry: V:'.print; V -> putint; newline #)
                    if);
                    inner; 
                    V - 1 -> V; 
                    L.free 
                 #);
               insert: entry
                 (# P: ^BasicProcess
                 enter P[]
                 do P[] -> Q.insert
                 #);
               removeNext: entry
                 (# P: ^BasicProcess
                 do Q.removeNext -> P[]
                 exit P[]
                 #);
               remove: entry
                 (# P: ^BasicProcess
                 enter P[]
                 do P[] -> Q.remove
                 #);              
               isEmpty: entry
                 (# value: @boolean
                 do Q.isEmpty (*and (W = 0)*) -> value
                 exit value
                 #);
               display: entry(# do Q.display #);
               addWaiting: entry
                 (# 
                 do W + 1 -> W ; 
                    (*console.display(#do 'W+:'.print; W -> putint; newline #)*)
                 #);
               dcrWaiting: entry
                 (# 
                 do W - 1 -> W;
                    (*console.display(#do 'W-:'.print; W -> putint; newline #)*)
                 #);
               hasWaiting: entry(# B: @boolean do W > 0 -> B exit B #);
               W: @integer
            #);
          coreLock: @Lock;
          console: @ 
            (# init: (# do L.get; 'Console' -> L.init; L.free #);
               
               display: 
                 (# puti: (# v: @integer enter v do v -> putint #);
                    newl: (# do newline #);
                 do L.get;
                    (if (V + 1 -> V) > 1 then '*'->put;
                        ' V:'.print; V ->putint; newline
                    if);
                    inner; 
                    V - 1 -> V;
                    L.free 
                 #);               
               display_t: (#do (if 0 then L.get; inner; L.free if) #);               
               L: @lock; V: @integer
            #);        
       do 'BasicSystem\n'.print;
          console.init;
          SQS.init;
          inner;
          cores.init          
       #)
  #)
