UniCoreRendezvousSystem: @UniCoreBasicSystem
  (# %OSDvisibility=disguised %
     Rsystem: system
       (# Semaphore: 
            (# id: ^text;
               init: 
                 (# V: @ integer
                 enter (id[],V)
                 do V -> cnt
                 #);
               wait: 
                 (# P: ^Process
                 do disable;
                    (if sTest then
                        console.display
                        (# 
                        do '\nSem:wait:'.print;
                           id.print
                        #)
                    if);
                    cnt - 1 -> cnt;
                    (if cnt < 0 then
                        (if sTest then
                            console.display
                            (# 
                            do ':isBlocked:'.print
                            #)
                        if);
                        scheduler.active[] -> P[];
                        (if P[] = Xnone[] then
                            'Scheduler.active is none'.print;
                            newline
                        if);
                        (if P.activityScheduler.active[] = Xnone[] then
                            'Scheduler.active.activityScheduler.active is none'.print;
                            newline
                        if);
                        P_status.WAITING
                           -> P.activityScheduler.active.status;
                        P.activityScheduler[]
                           -> P.activityScheduler.active.sch[];
                        P.activityScheduler.incrWaiting;
                        P.activityScheduler.active[]
                           -> Q.insert;
                        enable;
                        '?' -> put;
                        P.activityScheduler.active.suspend
                     else
                        enable
                    if)
                 #);
               signal: 
                 (# P: ^BasicProcess; cP: ^Process
                 do disable;
                    (if sTest then
                        console.display
                        (# 
                        do '\nSem:signal:'.print;
                           id.print
                        #)
                    if);
                    cnt + 1 -> cnt;
                    (if cnt <=  0 then
                        (if sTest then
                            console.display
                            (# 
                            do ':waiting processes'.print
                            #)
                        if);
                        Q.removeNext -> P[];
                        P_status.RESUMED -> P.status;
                        P[] -> P.sch.insert;
                        P.sch.decrWaiting;
                        scheduler.active[] -> cP[];
                        (if sTest then
                            console.display
                            (# 
                            do ':activate:'.print;
                               P.id -> putint;
                               ':inQueue:'.print;
                               P.sch.processid
                                  -> putint;
                               ':processExecutingSignal:'.print;
                               cp.id -> putint;
                               newline;
                               ':whenActivated:'
                                  -> P.sch.display
                            #)
                        if);
                        enable;
                        cP.activityScheduler.active.suspend
                     else
                        enable
                    if)
                 #);
               display: 
                 (# 
                 do console.display
                    (# 
                    do 'Semaphore: '.print;
                       id.print;
                       Q.display
                    #)
                 #);
               cnt: @ integer;
               Q: @ ProcessQueue;
               sTest: (#  exit 0 #)
            #);
          Process: BasicProcess
            (# start::< 
                  (# 
                  do activityScheduler.init;
                     this(Process)[]
                        -> scheduler.insert;
                     inner
                  #);
               Activity: BasicProcess
                 (# start::< 
                       (# 
                       do this(Activity)[]
                             -> activityScheduler.insert;
                          inner
                       #);
                    wait: BooleanValue
                      (# P: ^Process
                      do testCondition:
                           (if true then
                               inner;
                               (if not value then
                                   (if test then
                                       ':activity:wait:'
                                          -> display
                                   if);
                                   activityScheduler.active.suspend;
                                   (if test then
                                       ':activity:wait:restart'
                                          -> display
                                   if);
                                   restart testCondition
                                else
                               if)
                           if)
                      #)
                 do inner
                 #);
               port: 
                 (# id: ^text;
                    m,mutex: @ semaphore;
                    init: 
                      (# T1,T2: @ text
                      enter id[]
                      do this(port).id[] -> T1.puttext;
                         ':m' -> T1.puttext;
                         (T1[],0) -> m.init;
                         this(port).id[] -> T2.puttext;
                         ':mutex' -> T2.puttext;
                         (T2[],0) -> mutex.init;
                         (if pTest then
                             ':init' -> display
                         if)
                      #);
                    display: 
                      (# msg: ^text
                      enter msg[]
                      do console.display
                         (# 
                         do '\nPort:'.print;
                            id.print;
                            msg.print;
                            m.display;
                            mutex.display
                         #)
                      #);
                    entry: 
                      (# 
                      do (if pTest then
                             ':entry:A' -> display
                         if);
                         m.wait;
                         (if pTest then
                             ':entry:after:wait:beforeInner'
                                -> display
                         if);
                         inner;
                         (if pTest then
                             ':entry:C' -> display
                         if);
                         mutex.signal;
                         (if pTest then
                             ':entry:D' -> display
                         if)
                      #);
                    accept:< 
                      (# 
                      do (if pTest then
                             ':accept:A' -> display
                         if);
                         m.signal;
                         (if pTest then
                             ':accept:B:' -> display
                         if);
                         mutex.wait;
                         inner
                      #);
                    pTest: @ integer
                 #);
               activityScheduler: @ gScheduler
                 (# init:: 
                      (# 
                      do 'activityScheduler' -> idf[]
                      #);
                    insert::< (#  do P[] -> SQS.insert #);
                    remove::< (#  do P[] -> SQS.remove #);
                    processId:: 
                      (# 
                      do this(Process).id -> id
                      #);
                    waitingActivities:: 
                      (# 
                      do (if atest then
                             ':activityScheduler:SQSisEmpty'
                                -> display;
                             SQS.display
                         if);
                         (if noOfWaiting > 0 then
                             (if atest then
                                 console.display
                                 (#
                                 do '\nWaitingactivities: '.print; idf.print; 
                                    ':'->put;
                                    noOfWaiting -> putint; '\n'.print
                                 #)
                             if);
                             true -> value
                          else
                             (if atest then
                                 ':No waiting Activites:Process terminates'
                                    -> display
                             if)
                         if);
                      #);
                    aTest: (#  exit 0 #)
                 do (if test then
                        newline;
                        newline;
                        'Process:'.print;
                        this(Process).id -> putint;
                        ':activityScheduler'
                           -> console.puttext
                    if);
                    active;
                    (if test then
                        ':activityScheduler:after:active.suspend'
                           -> display
                    if)
                 #)
            do inner;
               (if test then
                   'Rsystem:aSQS:'.print;
                   activityScheduler.SQS.display
               if);
               activityScheduler
            #)
       do 'Rendezvous system'.print; inner
       #)
  #)
