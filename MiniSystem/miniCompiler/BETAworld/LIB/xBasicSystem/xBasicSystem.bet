xBasicSystem: @
  (# with Containers; 
     %OSDvisibility=disguised %
     test: (#  exit 0 #);
     Lock:
       (# id: ^text; M: @integer;
          init: (# T: ^text enter T[] do T[] -> id[] #);
          get:
            (# V: @integer
            do Loop: 
                 (if 1 then
                     disable;
                     (if (1 -> M.cmpAndSwap) = 1 then 
                         enable;
                         (if (V + 1 -> V) > 1000 then
                             '\n\n***** Timeout for Lock: '.print;
                             id.print;
                             '\n'.print
                         if);
                         (* 500 -> sleep; *)
                         restart Loop 
                 if)if);
            #);
          free: (# do 0 -> M; enable #)
       #);
     System: 
       (#  Core: 
            (# %core%
               S: ^Scheduler;
               attach: (# enter S[] do #);
            do loop:
                 (if S[] <> none then 
                     S                   
                  else 
                     100->sleep;
                     restart loop
                 if)
            #);
          core1,core2: @Core;          
          init_core:
            (#
            do core1[] -> fork; 
               core2[] -> fork; 
            #);
          
          BasicProcess: 
            (# start:< 
                 (# 
                 enter id
                 do '\nStarting: '.print;
                    id -> putint;
                    P_status.ACTIVE -> status;
                    inner
                 #);
               display: 
                 (# msg: ^text
                 enter msg[]
                 do console.display
                    (# 
                    do 'BasicProces:'.print;
                       id -> putint;
                       msg.print
                    #)
                 #);
               id: @ integer;
               status: @ integer;
            do inner;
               disable;
               P_status.TERMINATED -> status;
               enable
            #);
          P_status: @ 
            (# ACTIVE: (#  exit 1 #);
               WAITING: (#  exit 2 #);
               RESUMED: (# exit 3 #);
               TERMINATED: (#  exit 4 #)
            #);
          Scheduler: 
            (# init: (#  do 'Scheduler' -> idf[]; idf.print #);
               active: ^BasicProcess;
               idf: ^text;
               display: 
                 (# msg: ^text
                 enter msg[]
                 do console.display
                    (# 
                    do '\nScheduler:'.print;
                       idf.print;
                       'active:'.print;
                       (if active[] <> none then
                           active.id -> putint;
                           ':status='.print;
                           (if active.status
                            // P_status.ACTIVE then
                               'ACTIVE'.print
                            // P_status.WAITING then
                               'WAITING'.print
                            // P_status.RESUMED then
                               'RESUME'.print
                            // P_status.TERMINATED then
                               'TERMINATED'.print
                            else
                               '???'.print
                           if)
                        else
                           'none'.print
                       if);
                       '[' -> put;
                       processId -> putint;
                       ']' -> put;
                       msg.print;
                       ':' -> put;
                       SQS.display
                    #)
                 #);
               processId:< (# id: @ integer do inner exit id #);
               P: ^BasicProcess;
               cTest: (#exit 0 #)
            do (if cTest then
                   ':Initial:' -> display
               if);
               Loop:
                 (if true then
                     (if cTest then ':scheduler:loop: ' -> display if);
                     disable;
                     SQS.removeNext -> active[];
                     (if active[] <> none then
                         (if cTest then ':next:' -> display if);
                         P_status.ACTIVE -> active.status;
                         enable;
                         50 -> active.attach
                         disable;
                         (if cTest then ':after suspend:status:' -> display if);
                         (if active.status = P_status.ACTIVE then
                             active[] -> SQS.insert;
                             (if cTest then
                                 ':scheduler:insert active' -> display
                             if)
                         if)
                      else
                         (if cTest then '\nNo processes in SQS' -> display if)
                     if);
                     (if SQS.isEmpty then
                         (if cTest then ':SQS.isEmpty:' -> display if);
                         enable
                      else
                         enable; restart Loop
                     if)
                 if);
               console.display
               (#
               do '\n*** terminated: '.print; idf.print; '\n'.print
               #)
            #);
          ProcessQueue: Queue
            (# display: 
                 (# P: ^BasicProcess
                 do console.display
                    (# 
                    do 'SQS:head:'.print;
                       (if head[] <> none then
                           head.elm[] -> P[];
                           P.id -> putint
                       if);
                       '[' -> put;
                       scan
                       (# 
                       do current[] -> P[];
                          P.id -> putint;
                          ' ' -> put
                       #);
                       '] last:'.print;
                       (if last[] <> none then
                           last.elm[] -> P[];
                           P.id -> putint
                       if);
                       newline
                    #)
                 #);
            #);
          SQS: @ 
            (# Q: @ ProcessQueue;
               L: @lock;
               init: entry(# do 'SQS' -> L.init; #);
               entry: (# do L.get; inner; L.free #);
               insert: entry
                 (# P: ^BasicProcess
                 enter P[]
                 do P[] -> Q.insert
                 #);
               removeNext: entry
                 (# P: ^BasicProcess
                 do Q.removeNext -> P[]
                 exit P[]
                 #);
               remove: entry
                 (# P: ^BasicProcess
                 enter P[]
                 do P[] -> Q.remove
                 #);              
               isEmpty: entry
                 (# P: ^BasicProcess; value: @boolean
                 enter P[]
                 do Q.isEmpty -> value
                 exit value
                 #);
               display: entry(# do Q.display #);
            #);
          coreLock: @Lock;
          console: @ stream
            (# init: (# do L.get; 'Console' -> L.init; L.free #);
               display: (#do L.get; inner; L.free #);               
               L: @lock
            #);
          test: (# exit 0 #)
          theScheduler: ^Scheduler;
          MultiCore: (# exit 1 #)
       do '!' -> put;
          (if MultiCore  then
              console.init;
              SQS.init;              
              init_core;              
              &Scheduler[] -> theScheduler[]; theScheduler.init;
              inner;
              theScheduler[] -> core1.attach;
              &Scheduler[] -> theScheduler[]; theScheduler.init;
              theScheduler[] -> core2.attach;
           else 
              &Scheduler[] -> theScheduler[]; 
              theScheduler.init;
              inner; 
              theScheduler
          if)
       #)
  #)
