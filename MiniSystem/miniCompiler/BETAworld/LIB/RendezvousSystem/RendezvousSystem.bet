RendezvousSystem: @BasicSystem
  (# Rsystem: system
       (# Semaphore: 
            (# id: ^text;
               init: 
                 (# V: @ integer enter(id[],V) do V -> cnt #);
               wait: 
                 (# P: ^Process
                 do disable;
                    (if sTest then
                        console.display(#do '\nSem:wait:'.print; id.print #)
                    if);
                    cnt - 1 -> cnt;
                    (if cnt < 0 then
                        (if sTest then
                            console.display(#do ':isBlocked:'.print #)
                        if);
                        (* update status *) 
                        scheduler.active[] -> P[];
                        (if P[] = none[] then
                            'Scheduler.active is none'.print;
                            newline
                        if);
                        (if P.aPscheduler.active[] = none[] then
                            'Scheduler.active.aPscheduler.active is none'.print;
                            newline
                        if);
                        P_status.WAITING
                           -> P.aPscheduler.active.status;
                        (*P.aPscheduler.active[] -> P.aPscheduler.remove;*)
                        P.aPscheduler[] -> P.aPscheduler.active.sch[];
                        P.aPscheduler.incrWaiting;
                        (* save P.aPscheduler - signal should put it back here *) 
                        P.aPscheduler.active[] -> Q.insert;
                        enable;
                        (* if external interrupt here, active.status = WAITING
                         * check if this works?
                         *) 
                        P.aPScheduler.active.suspend
                     else
                        enable
                    if)
                 #);
               signal: 
                 (# P: ^BasicProcess; cP: ^Process
                 do disable;
                    (if sTest then
                        console.display(#do '\nSem:signal:'.print; id.print #)
                    if);
                    cnt + 1 -> cnt;
                    (if cnt <=  0 then
                        (if sTest then
                            console.display(#do ':waiting processes'.print; #)
                        if);
                        Q.removeNext -> P[];
                        P_status.ACTIVE -> P.status;
                        P[] -> P.sch.insert;
                        P.sch.decrWaiting;                        
                        scheduler.active[] -> cP[];
                        (if sTest then
                            console.display
                            (# 
                            do ':activate:'.print; P.id -> putint;
                               ':inQueue:'.print; P.sch.processid ->putint;
                               ':processExecutingSignal:'.print;
                               cp.id -> putint;
                               newline;
                               ':whenActivated:' -> P.sch.display;
                               P.sch.SQS.display
                            #)
                        if);
                        enable;
                        cP.aPscheduler.active.suspend
                     else
                        enable
                    if)
                 #);
               display: (#  do 'Semaphore: '.print; Q.display #);
               cnt: @ integer;
               Q: @ ProcessQueue;
               sTest: (# exit 0 #);
            #);
          Process: BasicProcess
            (# start::< 
                  (# 
                  do aPscheduler.init;
                     this(Process)[] -> scheduler.insert;
                     inner
                  #);
               Activity: BasicProcess
                 (# start::< 
                       (# 
                       do this(Activity)[] -> aPscheduler.insert;
                          inner
                       #);
                    wait: BooleanValue
                      (# P: ^Process
                      do testCondition:
                           (if true then
                               inner;
                               (if not value then 
                                   (if test then ':activity:wait:' -> display if);
                                   (*disable;*)
                                   (* scheduler.active[] -> P[]; *)
                                   (* P is the active process
                                    * executing and owning this activity *)
                                   (*P.aPscheduler.active[] -> P.aPscheduler.remove;*)
                                   
                                   (*enable;*)
                                   (*P.*)aPscheduler.active.suspend;
                                   (if test then 
                                       ':activity:wait:restart' -> display
                                   if);
                                   restart testCondition
                                else
                                   '>' -> put
                           if)if)
                      #);
                 do inner
                 #);
               port: 
                 (# id: ^text
                    m,mutex: @ semaphore;
                    init: 
                      (# T1,T2: @text;
                      enter id[]
                      do ':init' -> display;
                         this(port).id[] -> T1.puttext; ':m' -> T1.puttext;
                         (T1[],0) -> m.init;
                         this(port).id[] -> T2.puttext; ':mutex' -> T2.puttext;
                         (T2[],0) -> mutex.init
                      #);
                    display: 
                      (# msg: ^text
                      enter msg[]
                      do console.display
                         (# do '\nPort:'.print; id.print; msg.print #)
                      #);
                    entry: 
                      (# 
                      do (if pTest then ':entry:A' -> display if);
                         m.wait;
                         (if pTest then ':entry:after:wait:beforeInner' -> display if);
                         inner;
                         (if pTest then ':entry:C' -> display if);
                         mutex.signal;
                         (if pTest then ':entry:D' -> display if);
                      #);
                    accept:< 
                      (# 
                      do (if pTest then ':accept:A' -> display if);
                         m.signal;
                         (if pTest then ':accept:B:' -> display if);
                         mutex.wait;
                         inner
                      #);
                    pTest: (# exit 0 #);
                 #);
               aPscheduler: @ gScheduler
                 (# init:: (#  do 'aPscheduler' -> idf[] #);
                    insert::< (#  do P[] -> SQS.insert #);
                    remove::< (#  do P[] -> SQS.remove #);
                    processId:: (#  do this(Process).id -> id #);
                    waitingActivities:: 
                      (# 
                      do (if atest then
                             ':aPscheduler:SQSisEmpty' -> display;
                             SQS.display;
                         if);
                         (if noOfWaiting > 0 then
                             (if atest then
                                 '\n:Waiting Activity"s' -> display
                             if);
                             true -> value;
                             enable;
                             this(Process).suspend;
                             (if aTest then
                                 ':aPscheduler:resumed:' -> display
                             if)
                          else
                             (if atest then
                                 ':No waiting Activity"s:Process terminates'
                                    -> display
                             if);
                             enable
                         if)
                      #);
                    aTest: (# exit 0 #);
                 do (if test then
                        newline;
                        newline;
                        'Process:'.print;
                        this(Process).id -> putint;
                        ':aPscheduler' -> console.puttext
                    if);
                    active;
                    (if test then
                        ':aPscheduler:after:active.suspend' -> display
                    if)
                 #)
            do inner;
               (if test then
                   'Rsystem:aSQS:'.print; aPscheduler.SQS.display
               if);
               aPscheduler
            #)
       do 'Rendezvous system'.print; inner
       #)
  #)
