Queue:
  (# %OSDvisibility=disguised %
     test: (#  exit 0 #);
     insert: 
       (# elm: ^Object; E: ^element
       enter elm[]
       do (if elm[] <> Xnone[] then
              elm[] -> Element -> E[];
              (if head[] = Xnone[] then
                  E[] -> head[]; head[] -> last[]
               else
                  head[] -> E.succ[];
                  E[] -> head.pred[];
                  E[] -> head[]
              if)
          if)
       #);
     remove: 
       (# elm: ^Object
       enter elm[]
       do (if test then
              'Queue:remove: '.print
          if);
          scan
          (# E: ^Element
          do (if elm[] = current[] then
                 (if currentElement.pred[] <> Xnone[] then
                     currentElement.pred.succ[] -> E[];
                     currentElement.succ[]
                        -> currentElement.pred.succ[];
                     (if currentElement.succ[] <> Xnone[] then
                         E[] -> currentElement.succ.pred[]
                      else
                         currentElement.pred[] -> last[]
                     if)
                  else
                     currentElement.succ[] -> head[];
                     (if head[] <> Xnone[] then
                         Xnone[] -> head.pred[]
                      else
                         Xnone[] -> last[]
                     if)
                 if)
             if)
          #)
       #);
     removeNext: 
       (# elm: ^Object
       do (if last[] <> Xnone[] then
              last.elm[] -> elm[];
              last.pred[] -> last[];
              (if last[] <> Xnone[] then
                  Xnone[] -> last.succ[]
               else
                  Xnone[] -> head[]
              if)
          if)
       exit elm[]
       #);
     isEmpty: booleanValue(#  do head[] = Xnone[] -> value #);
     scan: 
       (# current: ^Object; currentElement: ^element
       do head[] -> currentElement[];
          Loop:
            (if currentElement[] <> Xnone[] then
                currentElement.elm[] -> current[];
                inner;
                currentElement.succ[] -> currentElement[];
                restart Loop
            if)
       #);
     element: 
       (# elm: ^Object; pred,succ: ^element
       enter elm[]
       exit this(element)[]
       #);
     head,last: ^element;
     Xnone: ^Object
  #)
