Queue:
  (# %OSDvisibility=disguised %
     test: (#  exit 0 #);
     insert: 
       (# elm: ^Object; E: ^element
       enter elm[]
       do (if elm[] <> none then
              elm[] -> Element -> E[];
              (if head[] = none then
                  E[] -> head[]; head[] -> last[]
               else
                  head[] -> E.succ[];
                  E[] -> head.pred[];
                  E[] -> head[]
              if)
          if)
       #);
     remove: 
       (# elm: ^Object
       enter elm[]
       do (if test then
              'Queue:remove: '.print
          if);
          scan
          (# E: ^Element
          do (if elm[] = current[] then
                 (if currentElement.pred[] <> none then
                     (* currentElement.pred.succ[] -> E[];
                      * (if elm[] = E.elm[] then
                      *    ' OOPS! elm = e'.print;
                      * if);
                      *)
                     currentElement.succ[] -> currentElement.pred.succ[];
                     (if currentElement.succ[] <> none then
                         (*E[] -> currentElement.succ.pred[]*)
                         currentElement.pred[] -> currentElement.succ.pred[];
                      else
                         currentElement.pred[] -> last[]
                     if)
                  else
                     currentElement.succ[] -> head[];
                     (if head[] <> none then
                         none -> head.pred[]
                      else
                         none -> last[]
                     if)
                 if)
             if)
          #)
       #);
     removeNext: 
       (# elm: ^Object
       do (if last[] <> none then
              last.elm[] -> elm[];
              last.pred[] -> last[];
              (if last[] <> none then
                  none -> last.succ[]
               else
                  none -> head[]
              if)
          if)
       exit elm[]
       #);
     isEmpty: booleanValue(#  do head[] = none -> value #);
     scan: 
       (# current: ^Object; currentElement: ^element
       do head[] -> currentElement[];
          Loop:
            (if currentElement[] <> none then
                currentElement.elm[] -> current[];
                inner;
                currentElement.succ[] -> currentElement[];
                restart Loop
            if)
       #);
     element: 
       (# elm: ^Object; pred,succ: ^element
       enter elm[]
       exit this(element)[]
       #);
     head,last: ^element;
  #)
