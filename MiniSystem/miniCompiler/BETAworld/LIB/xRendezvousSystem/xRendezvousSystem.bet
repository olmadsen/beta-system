xRendezvousSystem: @xBasicSystem
  (# %OSDvisibility=disguised %
     Rsystem: system
       (# Semaphore: 
            (# id: ^text;
               init: 
                 (# V: @ integer
                 enter (id[],V)
                 do V -> cnt; id[] -> M.init;
                 #);
               wait: 
                 (# P: ^Process
                 do M.get; (*disable;*)
                    (if sTest then
                        console.display
                        (# 
                        do '\nSem:wait:'.print;
                           id.print
                        #)
                    if);
                    cnt - 1 -> cnt;
                    (if cnt < 0 then
                        (if sTest then
                            console.display(#do ':isBlocked:'.print #)
                        if);
                        (if MultiCore then
                            thisCore.S.active[] -> P[];
                         else
                            theScheduler.active[] -> P[];
                        if);
                        (if P[] = none then
                            'Scheduler.active is none\n'.print;
                        if);
                        (if P.myAS.active[] = none then
                            'Scheduler.active.myAS.active is none\n'.print;
                        if);
                        P_status.WAITING -> P.myAS.active.status;
                        P.myAS[] -> P.myAS.active.sch[];
                        P.myAS.incrWaiting;
                        P.myAS.active[] -> Q.insert;
                        M.free; (*enable;*)
                        '?' -> put;
                        P.myAS.active.suspend
                     else
                        M.free (*enable*)
                    if)
                 #);
               signal: 
                 (# A: ^(*BasicProcess*) Process.Activity; cP: ^Process
                 do M.get; (*disable;*)
                    (if sTest then
                        console.display
                        (#
                        do '\nSem:signal:'.print; id.print
                        #)
                    if);
                    cnt + 1 -> cnt;
                    (if cnt <=  0 then
                        (if sTest then
                            console.display(#do ':waiting processes'.print #)
                        if);
                        Q.removeNext -> A[];
                        (if A[] = none then 'A none\n'.print if);
                        P_status.RESUMED -> A.status;
                        (if A.sch[] = none then 'A.sch none\n'.print if);
                        A[] -> A.sch.insert;

                        A.sch.decrWaiting;
                        (if MultiCore then
                            thisCore.S.active[] -> cP[];
                         else
                            theScheduler.active[] -> cP[]; 
                        if);
                        (if sTest then
                            console.display
                            (# 
                            do ':activate:'.print;
                               A.id -> putint;
                               ':inQueue:'.print;
                               A.sch.processid -> putint;
                               ':processExecutingSignal:'.print;
                               cp.id -> putint;
                               '\n:whenActivated:' -> A.sch.display
                            #)
                        if);
                        M.free; (*enable;*)
                        cP.myAS.active.suspend
                     else
                        M.free; (*enable*)
                    if)
                 #);
               display: 
                 (# 
                 do console.display
                    (# 
                    do 'Semaphore: '.print;
                       id.print;
                       Q.display
                    #)
                 #);
               cnt: @ integer;
               M: @Lock;
               Q: @ProcessQueue;
               sTest: (#  exit 0 #)
            #);
          Process: BasicProcess
            (# start::< 
                  (# 
                  do myAS.init;
                     (if MultiCore then 
                         this(Process)[] -> SQS.insert; 
                      else
                         this(Process)[] -> SQS.insert;
                     if);
                     inner
                  #);
               Activity: BasicProcess
                 (# start::< 
                       (# 
                       do this(Activity)[] -> myAS.insert;
                          inner
                       #);
                    wait: BooleanValue
                      (# P: ^Process
                      do testCondition:
                           (if true then
                               inner;
                               (if not value then
                                   (if test then
                                       ':activity:wait:' -> display
                                   if);
                                   myAS.active.suspend;
                                   (if test then
                                       ':activity:wait:restart' -> display
                                   if);
                                   restart testCondition
                               if)
                           if)
                      #);
                    sch: ^ActivityScheduler
                 do inner
                 #);
               port: 
                 (# id: ^text;
                    m,mutex: @ semaphore;
                    init: 
                      (# T1,T2: @ text
                      enter id[]
                      do this(port).id[] -> T1.puttext;
                         ':m' -> T1.puttext;
                         (T1[],0) -> m.init;
                         this(port).id[] -> T2.puttext;
                         ':mutex' -> T2.puttext;
                         (T2[],0) -> mutex.init;
                         (if pTest then ':init' -> display if)
                      #);
                    display: 
                      (# msg: ^text
                      enter msg[]
                      do console.display
                         (# 
                         do '\nPort:'.print;
                            id.print;
                            msg.print;
                            m.display;
                            mutex.display
                         #)
                      #);
                    entry: 
                      (# 
                      do (if pTest then ':entry:A' -> display if);
                         m.wait;
                         (if pTest then ':entry:B' -> display if);
                         inner;
                         (if pTest then ':entry:C' -> display if);
                         mutex.signal;
                         (if pTest then ':entry:D' -> display if)
                      #);
                    accept:< 
                      (# 
                      do (if pTest then ':accept:A' -> display if);
                         m.signal;
                         (if pTest then ':accept:B:' -> display if);
                         mutex.wait;
                         inner
                      #);
                    pTest: (# exit 0 #)
                 #);
               activityScheduler: 
                 (# active: ^ Activity;
                    idf: ^text;
                    init:
                      (# 
                      do 'activityScheduler' -> idf[]
                      #);
                    insert: 
                      (# P: ^activity
                      enter P[] 
                      do P[] -> aSQS.insert 
                      #);
                    remove: 
                      (# P: ^Activity
                      enter P[] 
                      do P[] -> aSQS.remove 
                      #);
                    processId:
                      (# 
                      do this(Process).id -> id 
                      exit id
                      #);
                    waitingActivities: booleanValue
                      (# 
                      do (if atest then
                             ':myAS:aSQSisEmpty' -> display;
                             aSQS.display
                         if);
                         (if noOfWaiting > 0 then
                             (if atest then
                                 '\n:Waiting Activities' -> display
                             if);
                             true -> value
                          else
                             (if atest then
                                 ':No waiting Activites:Process terminates'
                                    -> display
                             if)
                         if)
                      #);
                    incrWaiting: 
                      (# 
                      do noOfWaiting + 1 -> noOfWaiting
                      #);
                    decrWaiting: 
                      (# 
                      do noOfWaiting - 1 -> noOfWaiting
                      #);
                    display: (# T: ^text enter T[] do T.print #);
                    noOfWaiting: @ integer;
                    ach: ^activityScheduler;
                    P: ^Process;
                    aTest: (# exit 0 #);
                    cTest: (# exit 0 #)
                 do (if cTest then ':Initial:' -> display if);
                    Loop:
                      (if true then
                          (if cTest then ':scheduler:loop: ' -> display if);
                          disable;
                          aSQS.removeNext -> active[];
                          (if active[] <> none then
                              (if cTest then ':next:' -> display if);
                              P_status.ACTIVE -> active.status;
                              enable;                              
                              (if test then
                                  '\n\nProcess:'.print;
                                  this(Process).id -> putint;
                                  ':activityScheduler' -> console.puttext
                              if);
                              active;
                              (if test then
                                  ':activityScheduler:after:active.suspend'
                                    -> display
                              if);
                              disable;
                              (if cTest then
                                  ':after suspend:status:' -> display
                              if);
                              (if active.status = P_status.ACTIVE then
                                  active[] -> aSQS.insert
                                  (if cTest then
                                      ':scheduler:insert active' -> display
                                  if)
                              if)
                           else
                              (if cTest then '\nNo processes ina aSQS' -> display if)
                          if);
                          (if aSQS.isEmpty then 
                              (if cTest then ':SQS.isEmpty:' -> display if);
                              (if waitingActivities then
                                  (if cTest then
                                      console.display
                                      (#
                                      do ':waiting activities'.print
                                      #)
                                  if);
                                  enable;
                                  coreLock.get;
                                  thisCore.S.active[] -> P[];
                                  coreLock.free;
                                  (*enable;*)
                                  (if MultiCore then
                                     P.suspend
                                   else
                                      theScheduler.active.suspend
                                  if);
                                  (if cTest then
                                      '\nactiviyScheduling resumed after waiting activities'
                                        -> display
                                  if);
                                  restart Loop
                              if);
                              enable
                           else
                              enable; restart Loop
                          if)
                      if);
                    (if cTest then ':terminated\n' -> display if)
                 #);
               myAS: @ActivityScheduler;
               aSQS: @ ProcessQueue;
            do inner;
               (if test then
                   'Rsystem:aaSQS:'.print;
                   aSQS.display
               if);
               myAS
            #)
       do 'Rendezvous system'.print; inner
       #)
  #)
