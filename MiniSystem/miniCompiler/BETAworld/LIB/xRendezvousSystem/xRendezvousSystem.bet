xRendezvousSystem: @xBasicSystem
  (# %OSDvisibility=disguised %
     Rsystem: system
       (# Semaphore: 
            (# id: ^text;
               init: 
                 (# V: @ integer
                 enter (id[],V)
                 do V -> cnt; id[] -> M.init;
                 #);
               wait: 
                 (# P: ^Process
                 do M.get; (*disable;*)
                    cnt - 1 -> cnt;
                    (if cnt < 0 then
                        (if MultiCore then
                            thisCore.S.active[] -> P[];
                         else
                            theScheduler.active[] -> P[];
                        if);
                        (if P[] = none then
                            'Scheduler.active is none\n'.print;
                        if);
                        (if P.myAS.active[] = none then
                            'Scheduler.active.myAS.active is none\n'.print;
                        if);
                        P_status.WAITING -> P.myAS.active.status;
                        P.myAS[] -> P.myAS.active.sch[];
                        P.myAS.incrWaiting;
                        P.myAS.active[] -> Q.insert;
                        M.free; (*enable;*)
                        P.myAS.active.suspend
                     else
                        M.free (*enable*)
                    if)
                 #);
               signal: 
                 (# A: ^Process.Activity; cP: ^Process
                 do M.get; (*disable;*)
                    cnt + 1 -> cnt;
                    (if cnt <=  0 then
                        Q.removeNext -> A[];
                        P_status.RESUMED -> A.status;
                        A[] -> A.sch.insert;
                        A.sch.decrWaiting;
                        (if MultiCore then
                            thisCore.S.active[] -> cP[];
                         else
                            theScheduler.active[] -> cP[]; 
                        if);
                        M.free; (*enable;*)
                        cP.myAS.active.suspend
                     else
                        M.free; (*enable*)
                    if)
                 #);
               display: 
                 (# 
                 do console.display
                    (# 
                    do 'Semaphore: '.print;
                       id.print;
                       Q.display
                    #)
                 #);
               cnt: @ integer;
               M: @Lock;
               Q: @ProcessQueue;
            #);
          Process: BasicProcess
            (# start::< 
                  (# 
                  do myAS.init;
                     (if MultiCore then 
                         this(Process)[] -> SQS.insert; 
                      else
                         this(Process)[] -> SQS.insert;
                     if);
                     inner
                  #);
               Activity: BasicProcess
                 (# start::< 
                       (# 
                       do this(Activity)[] -> myAS.insert;
                          inner
                       #);
                    wait: BooleanValue
                      (# P: ^Process
                      do testCondition:
                           (if true then
                               inner;
                               (if not value then
                                   myAS.active.suspend;
                                   restart testCondition
                               if)
                           if)
                      #);
                    sch: ^ActivityScheduler
                 do inner
                 #);
               port: 
                 (# id: ^text;
                    m,mutex: @ semaphore;
                    init: 
                      (# T1,T2: @ text
                      enter id[]
                      do this(port).id[] -> T1.puttext;
                         ':m' -> T1.puttext;
                         (T1[],0) -> m.init;
                         this(port).id[] -> T2.puttext;
                         ':mutex' -> T2.puttext;
                         (T2[],0) -> mutex.init;
                      #);
                    display: 
                      (# msg: ^text
                      enter msg[]
                      do console.display
                         (# 
                         do '\nPort:'.print;
                            id.print;
                            msg.print;
                            m.display;
                            mutex.display
                         #)
                      #);
                    entry: 
                      (# 
                      do 
                         m.wait;

                         inner;

                         mutex.signal;

                      #);
                    accept:< 
                      (# 
                      do 
                         m.signal;

                         mutex.wait;
                         inner
                      #);
                 #);
               activityScheduler: 
                 (# active: ^ Activity;
                    idf: ^text;
                    init:
                      (# 
                      do 'activityScheduler' -> idf[]
                      #);
                    insert: 
                      (# P: ^activity
                      enter P[] 
                      do P[] -> aSQS.insert 
                      #);
                    remove: 
                      (# P: ^Activity
                      enter P[] 
                      do P[] -> aSQS.remove 
                      #);
                    processId:
                      (# 
                      do this(Process).id -> id 
                      exit id
                      #);
                    waitingActivities: booleanValue
                      (# 
                      do (if noOfWaiting > 0 then
                             true -> value
                         if)
                      #);
                    incrWaiting: 
                      (# 
                      do noOfWaiting + 1 -> noOfWaiting
                      #);
                    decrWaiting: 
                      (# 
                      do noOfWaiting - 1 -> noOfWaiting
                      #);
                    display: (# T: ^text enter T[] do T.print #);
                    noOfWaiting: @ integer;
                    ach: ^activityScheduler;
                    P: ^Process;
                 do Loop:
                      (if true then
                          disable;
                          aSQS.removeNext -> active[];
                          (if active[] <> none then
                              P_status.ACTIVE -> active.status;
                              enable;                              ;
                              active;
                              disable;
                              (if active.status = P_status.ACTIVE then
                                  active[] -> aSQS.insert
                              if)
                          if);
                          (if aSQS.isEmpty then 
                              (if waitingActivities then
                                  (*enable;
                                  coreLock.get;*)
                                  thisCore.S.active[] -> P[];
                                  (*coreLock.free;*)
                                  enable;
                                  (if MultiCore then
                                     P.suspend
                                   else
                                      theScheduler.active.suspend
                                  if);
                                  restart Loop
                              if);
                              enable
                           else
                              enable; restart Loop
                          if)
                      if);
                 #);
               myAS: @ActivityScheduler;
               aSQS: @ ProcessQueue;
            do inner;
               myAS
            #)
       do 'Rendezvous system'.print; inner
       #)
  #)
