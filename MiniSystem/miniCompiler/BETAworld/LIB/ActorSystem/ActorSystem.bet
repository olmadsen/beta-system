ActorSystem: @ BasicSystem
  (# with Containers;
     System: BasicSystem
       (# Message:
            (# id: @integer;
               display: (# do '\nMessage here:\n'.print#)
            enter id
            exit this(Message)[]
            #);
          MessageQueue: Queue
            (#
            #);
          Actor: BasicProcess
            (# start:: (# do this(actor)[] -> SQS.insert #);
               send: 
                 (# M: ^Message 
                 enter M[] 
                 do L.get; 
                    M[] -> Q.insert; 
                    (if status = P_status.WAITING then '!'->put;
                        this(Actor)[] -> SQS.insert;
                        SQS.dcrWaiting
                    if)
                    L.free 
                 #);
               receive: 
                 (# M: ^Message
                 do L.get;
                    waitMessage:
                      (if Q.isEmpty then '?'->put;
                          SQS.addWaiting;
                          P_status.WAITING -> status;
                          L.free;
                          this(Actor).suspend;
                          L.get;
                          (*50 -> sleep; (* makes the thread sleep 
                                        * - we should have an xBeta sleep
                                        *)
                          restart waitMessage
                       else 
                          Q.removeNext -> M[];
                          L.free;
                          inner;
                          L.get;
                          restart waitMessage
                      if)
                 #);
                xreceive: 
                 (# msgs: [3]@integer; M: ^Message
                 enter(msgs[1],msgs[2],msgs[3])
                 do 
                    console.display
                    (#
                    do '\nxreceive: '.print;
                       (for i: 3 repeat
                            msgs[i] -> putint;  ' '-> put
                       for);
                       newline
                    #);
                    waitMessage:
                      (if 1 then
                          L.get;
                          (if Q.isEmpty then 
                              '?'->put;
                              SQS.addWaiting;
                              P_status.WAITING -> status;
                              L.free;
                              this(Actor).suspend;
                              restart waitMessage
                           else 
                              '\nScan: '.print;
                              Q.scan
                              (#do current[] -> M[]; M.id->putint; ' ' -> put#);
                              newline;
                              Loop:
                                (if 1 then
                                    Q.scan
                                    (#
                                    do current[] -> M[];
                                       (for i: 3 repeat
                                            (if M.id = msgs[i] then
                                                M[] -> Q.remove;
                                                leave Loop
                                       if)for);
                                    #);
                                    L.free;
                                    restart waitMessage
                                if);
                              L.free;
                              inner;
                          if)
                      if)
                 #);     
               select:
                 (# receive:
                      (# id: @integer; msg: ## Message; match: @boolean
                      enter msg##
                      do L.get; 
                         L:
                           Q.scan
                           (# 
                           do current[] -> M[]; 
                              (if msg## = M## then 
                                  (*
                                  console.display
                                  (#do
                                     '\nScan(: '.print;
                                     Q.scan
                                     (# M1: ^message
                                     do current[] -> M1[]; M1.id->putint; ' ' -> put#);
                                     newline;
                                  #);*)
                                  M[] -> Q.remove;
                                  (*console.display
                                  (# M1: ^Message
                                  do
                                     '\n)Scan: '.print;
                                     Q.scan
                                     (#do current[] -> M1[]; M1.id->putint; ' ' -> put#);
                                     newline;
                                  #);*)
                                  true -> match -> found;
                                  leave L
                              if)
                           #);
                         L.free
                      exit match
                      #);
                    found: @boolean;
                    M: ^Message
                 do false -> found;
                    waitMessage:
                      (if 1 then
                          L.get;
                          (if Q.isEmpty then 
                              '?'->put;
                              SQS.addWaiting;
                              P_status.WAITING -> status;
                              L.free;
                              this(Actor).suspend;
                              restart waitMessage
                           else 
                              '\nScan: '.print;
                              Q.scan
                              (#do current[] -> M[]; M.id->putint; ' ' -> put#);
                              L.free;
                              inner;
                              (if not found then 
                                  this(Actor).suspend;
                                  restart waitMessage
                              if);
                          if)
                      if)
                 #);               
               Q: @MessageQueue;
               L: @Lock
            do 'Lock'->L.init;
               P_status.ACTIVE -> status;
               inner;
               P_status.TERMINATED -> status  
            #)
       do console.display(#do '\nActor System:\n'.print; #);
          inner
       #)
  #)

