ActorSystem: @ BasicSystem
  (# with Containers;
     System: BasicSystem
       (# Message:
            (# id: @integer;
               display:< (# do 'Msg:'.print; id -> putint; ','->put; inner#)
            enter id
            exit this(Message)[]
            #);
          MessageQueue: Queue
            (# 
            #);
          Actor: BasicProcess
            (# start::< (# do this(actor)[] -> SQS.insert; inner #);
               send: 
                 (# M: ^Message 
                 enter M[] 
                 do L.get; 
                    M[] -> Q.insert;
                    (* 'Send:' -> display;*)
                    (if status = P_status.WAITING then(* '!'->put;*)
                        this(Actor)[] -> SQS.insert;
                        SQS.dcrWaiting
                    if)
                    L.free 
                 #);
               select:
                 (# receive:
                      (# id: @integer; msg: ## Message; match: @boolean
                      enter msg##
                      do L.get; 
                         L:
                           Q.scanFromLast
                           (# 
                           do current[] -> M[]; 
                              (if msg## = M## then 
                                  M[] -> Q.remove;
                                  true -> match -> found;
                                  leave L
                              if)
                           #);
                         L.free
                      exit match
                      #);
                    found: @boolean;
                    M: ^Message
                 do false -> found;
                    waitMessage:
                      (if 1 then
                          L.get;
                          (if Q.isEmpty then 
                              SQS.addWaiting;
                              P_status.WAITING -> status;
                              L.free;
                              this(Actor).suspend;
                              restart waitMessage
                           else 
                             (* 'Get:' -> display;*)
                              L.free;
                              inner;
                              (if not found then 
                                  this(Actor).suspend;
                                  restart waitMessage
                              if);
                          if)
                      if)
                 #);  
               display:
                 (# T: ^text
                 enter T[]
                 do console.display
                    (# M: ^Message
                    do T.print; newline
                       Q.scanFromLast(#do ' '->put; current[] -> M[]; M.display; newline #);
                       newline
                    #);
                 #);
               Q: @MessageQueue;
               L: @Lock
            do 'Lock'->L.init;
               inner;
            #)
       do console.display(#do '\nActor System:\n'.print; #);
          inner
       #)
  #)

