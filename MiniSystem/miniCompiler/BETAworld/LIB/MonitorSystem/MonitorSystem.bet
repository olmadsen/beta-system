MonitorSystem: @ BasicSystem
  (# with Containers; 
     System: BasicSystem
       (# Process: BasicProcess
            (# [interface[],globals[monitor]]
               start::< 
                 (# 
                 do this(Process)[] -> SQS.insert
                 #);
            do P_status.ACTIVE -> status;
               inner;
               P_status.TERMINATED -> status
            #);
          Monitor: 
            (# [globals[],interface[entry]]
               init: (# do mutex.init #);
               entry: 
                 (# [arguments[immutable],kind[method]]
                    theActive: ^Process
                 do mutex.wait;
                    (if (V + 1 -> V) > 1 then
                        ' E.V:'.print; V -> putint; newline
                    if);
                      inner;
                    V - 1 -> V;
                    mutex.signal
                 #);
               V:@integer;
               Condition: 
                 (# Q: @ semaphore;
                    wait: (#  do  #);
                    signal: (#  do  #)
                 #);
               Wait: 
                 (# cond: @ boolean
                 do Loop:                      
                      (# 
                      do inner;
                         (if not cond then
                             mutex.signal; 
                             (* do not acccess thisCore without disabling interrupt*)
                             P_status.ACTIVE
                                -> thisCore.main.active.status;
                             thisCore.main.active.suspend;
                             mutex.wait;
                             restart Loop
                         if)
                      #)
                 #);
               display: 
                 (# 
                 do 'Monitor:'.print; mutex.display
                 #);
               mutex: @ semaphore
            #);
          Semaphore: 
            (# init: (#  do 1 -> cnt #);
               wait: 
                 (# theActive: ^Process
                 do M.get; 
                    cnt - 1 -> cnt;
                    (if cnt < 0 then
                        P_status.WAITING
                          -> thisCore.main.active.status;
                        SQS.addWaiting;
                        thisCore.main.active[] -> Q.insert;
                        thisCore.main.active[] -> theActive[];
                        M.free; 
                        theActive.suspend
                     else
                        M.free
                    if)
                 #);
               signal: 
                 (# P: ^Process
                 do M.get; 
                    cnt + 1 -> cnt;
                    (if cnt <=  0 then
                        Q.removeNext -> P[];
                        P_status.ACTIVE -> P.status;
                        P[] -> SQS.insert;
                        SQS.dcrWaiting;
                        M.free; 
                     else
                        M.free 
                    if)
                 #);
               display: 
                 (# 
                 do 'Semaphore: '.print; Q.display
                 #);
               cnt: @ integer;
               M: @Lock;
               Q: @ ProcessQueue
            #);
       do inner;  
       #)
  #)
