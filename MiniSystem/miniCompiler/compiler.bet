ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE 'lexer';
INCLUDE 'parser';
INCLUDE 'checker';
INCLUDE 'generator';
INCLUDE '../VM/betaVM';
BODY 'parseStdBeta'
---program:descriptor---
SystemEnv
(# compile: @generator;
   FN: ^text;
   VM: @betaVM
     (# putCh:: (# do ch -> put #);
        getCh:: (# do get -> ch #);
        (*startEvent:: (#do '\n***Start:' -> RTT.puttext #);
        allocEvent::
            (#
            do '\n***Alloc:' -> RTT.puttext;
            #);
          doEvent::
            (#
            do '\n***Do:' ->  RTT.puttext;
            #);
          rtnEvent::
            (#
            do '\n***Rtn:' ->  RTT.puttext;
         #);*)
        errorEvent:: (# do msg[] -> putline; done #);
        done:: (# do stop #);
     #);
   setWindowEnv:: (# do myWindowEnv[] -> theWindowEnv[]#);
   myWIndowEnv: @ guienv;
   L: @LOG;
   betaLib: ^compile.Node;
   runMode: @integer
do 'New BETA compiler - SVN version: ' -> puttext;
   2 -> arguments -> FN[];
   (if true
    // ('-c' -> FN.equal) then
       Run_C_INT -> runMode;
       ' ' -> put;
       3 -> arguments -> FN[]
    // ('-C' -> FN.equal) then
       save_BC_Image -> runMode;
       3 -> arguments -> FN[]       
    else
       Run_BETA_INT -> runMode
   if);   
   ('/MiniSystem/miniCompiler/BETAworld/','.bet') -> compile.initDirectoryModules;
   FN[] -> compile.fixExtension_bet -> FN[] -> putline;
   ((FN[],'..log') -> mkAuxName, false) -> L.open;
   (L[],false,none,FN[]) -> compile;

   (if compile.hasErrors then
       stop
    else
       (runMode,false) -> VM.init; (* two inits? *)
       (compile.runtimeDescs,compile.mainDescNo,FN[],runMode) -> VM.execute.init;
       VM.execute[] -> fork;
   if);
#)
