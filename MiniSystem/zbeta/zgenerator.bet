ORIGIN '~beta/basiclib/betaenv';
INCLUDE 'zchecker';
INCLUDE '../miniCompiler/objectcode'
---lib:attributes---
generator: checker
  (# MkCom: (# t1,t2: ^text enter (t1[],t2[]) do t2[] -> T1.append -> cd.comment #);
     
    Module::
       (# gen::
            (#
            do ('Comp',newDesc.noOfDescs) -> cd.newComponent;
               SO.gen;
               this(Module)[] -> main[];
            #);
       #);
     StaticObject::
       (# gen::
            (# T: @text
            do (if not OG.IV.isBasic then 
                   'StaticObject is not Basic' -> cd.comment;
                   sig.doPt -> cd.comment;
                   (if origin[] <> none then 
                       cd.pushThis;
                       OG.descNo -> T.putint;
                       ('staticObject: alloc: ',T[]) -> cd.com2; 
                       OG.gen;
                       cd.rswap;
                       (sig.id[],this(StaticObject).off) -> cd.rstoreg
                    else
                       (* A module object *)
                       (if not OG.IS.isEmpty then
                           (sig.id[],none,OG[]) ->  ObjTmpStack.add
                       if)   
                   if)
               if)
            #);
          pushThis:: (# do cd.pushThis #);
          loadAdr:: 
            (* Called when Receiver 
             * R: @ Foo or R: @ Foo{...} -> push R = adr of object
             * X: @integer -> no action
             *)
            (# 
            do (if not OG.IV.isBasic then 
                   ('StaticObject:loadADr: ',dopt) -> cd.com2;
                   (sig.id[],this(StaticObject).off) -> cd.rpushg
               if)
            #);
          load::
            (* called when not receiver - i.e. value to be pushed *)
            (#
            do (sig.id[],off) -> cd.pushg
            #);
          store::
            (* called when var to be stored
             * does not seem to be used since store happens in Binary
             *)
            (#
            do 'StaticObject:store: '->putline; (sig.id[],off) -> cd.push
            #);
       #);
     ObjectGeneration::
       (# gen::
            (# 
            do (*('OG: ',OI.inv.compoundSymb[]) -> MkCom;*)
               (*IS.inv.ATd.doPT -> putline*)
               (if IS.isEmpty then
                   (* check this - called when R.foo *)
                   IV.gen
                else
                   (* called when R.foo{ } *)
                   IV.loadOrigin;
                   ('Singular',descno,true) -> cd.alloc;
                   ('Singular',none,this(ObjectTemplate)[]) -> ObjTmpStack.add
               if)
            #);
          genClass::
            (#
            do (N[],sig[],descNo) -> IS.genClass
            #)
       #);
     ObjectDesc::
       (#
          genClass::
            (#
            do (N[],sig[],descNo) -> IS.genClass
            #)
       #);
     items::
       (# genClass:
            (# N: ^text; sig: ^Signature; descNo: @integer; T: @text
            enter(N[],sig[],descNo)
            do     
               (N[], descNo, false, 1,1) -> cd.class;
               (* super *)
               ('origin',originOff) -> cd.rstore; 
               scanSons
               (# I: ^Item
               do current[] -> I[];
                  (if I.isDecl then 
                      current.dopt -> CD.comment; 
                      current.gen; 
                  if) 
               #);
               scanSons
               (# I: ^Item
               do current[] -> I[];
                  (if not I.isDecl then 
                      current.dopt -> CD.comment;
                      current.gen
                  if) 
               #);
               'D' -> cd.rtn;
               (vsize,rsize,1) -> CD.endClass     
            #);
       #);
     UnaryPattern::
       (# gen:: (# do (sig.id[],sig[],OD[]) -> objTmpStack.add #);
          loadAdr::
            (#
            do 'UnaryPattern:loadAdr: '->puttext; doPT -> putline;
               cd.pushThis;
            #);
          load::
            (#
            do ('Pattern:load: ',doPT) -> cd.com2;
               (sig.id[],OD.descno,true) -> cd.alloc;
               (*cd.rpop;*) (* depend on the return type *)
               (* and perhaps a difference
                *    R: @foo -- no rpop
                *    foo;    -- if foo on rstack it should be popped
                *)
            #);
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #);   
       #);
     BinaryPattern::
       (# gen:: (# do (sig.id[],sig[],OD[]) -> objTmpStack.add #);
          loadAdr::
            (#
            do 'BinaryPattern:loadAdr: '->puttext; doPT -> putline;
            #);
          load::
            (#
            do ('Pattern:load: ',doPT) -> cd.com2;
               (sig.id[],OD.descno,true) -> cd.alloc;
               cd.rpop; (* depend on the return type *)
            #);
          store::
            (#
            do 'Pattern:store: '->puttext; doPT -> putline;
            #); 
       #);
     BasicBinaryPattern::
       (# load:: 
            (# 
            do OD.IS.props.basicProp.primno -> cd.op 
            #);
       #);
     FunctionalPattern::
       (# gen:: (# do (sig.id[],sig[],OD[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          loadAdr:: (# #);
          load:: (# do (sig.id[],OD.descno,true) -> cd.alloc #);
       #);
     BasicFunctionalPattern::
       (# load:: (# do ('put',OD.IS.props.basicProp.primNo) -> cd.Callprim #);
       #);
     KeyWordPattern::
       (# gen:: (# do (sig.id[],sig[],OD[]) -> objTmpStack.add #);
          pushThis:: (# do cd.pushThis #);
          loadAdr:: (# #);
          load:: (# do (sig.id[],OD.descno,true) -> cd.alloc #);          
       #);
     BasicKeyWordPattern::
       (# load:: 
            (# exitLab: @labelHandler.label
            do (if OD.IS.props.basicProp.primNo
                // 100 then

                   exitLab.new;
                   1111 -> cd.pushConst;                   
                   exitlab -> cd.jmpFalse;
                   2222 -> cd.pushConst;
                   exitLab -> cd.defLab
               if)
            #)
       #);
            
     Invocation::
       (* E1.E2.E3
        *    E1.pushThis
        *    E1.loadAdr
        *    E2.loadAdr
        *    E3.execute
        * R.foo
        * R.foo{...}
        * X:= y + 12
        *)
       (# gen:: 
            (# first: @boolean
            do true -> first;
               scanSons
               (# E: ^Exp
               do current[] -> E[];
                  (if first then E.pushThis if);
                  (if not isLast then 
                      E.loadAdr 
                   else
                      E.execute
                  if);
                  false -> first
               #)
            #);
          loadOrigin:
            (# 
            do 'Invocation:loadOrigin' -> cd.comment
            #);
       #);
     Unary::
       (* May be 
        *  A simple variable X declared as X:@integer; 
        *  An object R declared as R: @Foo
        *  A pattern Foo declared as Foo: Bar{ ... }
        * ATd = operator.Decl
        *)
       (# pushThis:: (#do cd.pushThis #); (* Perhaps loadOrigin?*)
          loadAdr:: (# do ATd.loadAdr #);
          execute:: (# do ATd.load #);
       #);
     Binary::
       (# pushThis:: (#do rec.ATd.pushThis #);
          loadAdr:: (# do 'Binary:loadAdr:'->puttext; dopt -> putline; execute #);
          execute::
            (#
            do (if isAssign then 
                   argument.gen;
                   ('var',rec.ATd.off) -> cd.storeg
                else
                   (if rec.ATd.isBasic then 
                       rec.ATd.load
                   if);
                   argument.gen;
                   ATd.load
               if)
            #)
       #);
     Function::
       (# pushThis:: (#do ATd.pushThis #);
          loadAdr:: (# do execute #);
          execute::
            (# first: @boolean
            do ATd.loadAdr;
               true -> first;               
               scanSons
               (#
               do (if not first then
                      current.gen
                  if);
                  false -> first
               #);
               (if ATd[] = none then 
                   'none' -> putline
                else
                   ATd.load
               if)
            #)          
       #);
     KeyWord::
       (# pushThis:: (#do ATd.pushThis #);
          loadAdr:: (# do execute #);
          execute::
            (# isArg: @boolean
            do scanSons
               (#
               do (if isArg then
                      current.gen
                  if);
                  not isArg -> isArg
               #);
               ATd.load
            #);
       #);
     Const::
       (# execute:: (# do L.T.setpos; L.T.getInt -> cd.pushConst #);
       #);
     ObjTmpStack: @
       (# item: 
            (# N: ^text; sig: ^Signature; OT: ^ObjectTemplate;
            enter(N[],sig[],OT[])
            exit this(item)[]
            #);
          add:
            (# N: ^text; sig: ^Signature; OT: ^ObjectTemplate;
            enter(N[],sig[],OT[])
            do (if (top+1-> top) > OTL.range then OTL.range -> OTL.extend if);
                   (N[],sig[],OT[]) -> item -> OTL[top][]
            #);          
          next:
            (# inx: @integer
            do Loop:
                 (#
                 do inx +1 -> inx;
                    (if inx <= top then
                        (OTL[inx].N[],OTL[inx].sig[]) -> OTL[inx].OT.genClass;
                        restart Loop
                    if);
                 #)
            #);               
          OTL: [10] ^Item;top: @integer;
       #);
     labelHandler: @
       (# labCount: @integer;
          label:
            (# labNo: @integer;
               new: (# do  labCount + 1 -> labCount -> labNo #);
            enter labNo
            exit labNo
            #);
          init: (# do 0 -> labCount #);
       #);
     genMain:
       (# descno: @integer
       do ('main',newDesc,false,1,0) -> cd.class;
          cd.pushThis;
          (*  (if false then
           *               'main: topModule: ' -> puttext;
           *               markIncludes.topModule.name.lexemSy -> puttext;
           *           if);
           *           ('BETAworld',(markIncludes.topModule.desc).descno,true) -> cd.alloc;
           *           (for i: markIncludes.top repeat
           *                ('origin',markIncludes.orgOff[markIncludes.top - i + 1]) -> cd.rpushg;
           *           for);
           *
           * ('x','S') -> (markIncludes.main.desc).doCall;          
           *)
          ('BETAworld',1,true) -> cd.alloc;
          (*('x','S') -> (main.desc).doCall;  *)
          cd.stop; (* we should generate stop here and not in betaVM *)
          
          (*   (if markIncludes.main## <> ObjectModule## then
           *               '\n\n***** Only ObjectModules can be executed *****\n\n' -> putline
           *           if)
           *)
       #);
     genEventProcessor:
       (#
       do ('EventProcessor',newDesc,false,1,0) -> cd.class;
          cd.stop (* we should generate stop here and not in betaVM *)
       #);
     
     mainDescNo: (# exit newDesc.noOfDescs -1 (*- 2*) (* ad hoc *) #);
     cd: @ObjectCode;
     descs: ^RunTimeDescriptors;
     BC: ^File;
  enter BC[]
  do 'GENERATOR' -> putline;
     (if true then
         BC.openWrite;
         BC[] -> cd.init;
         'zbeta'-> cd.comment;
         labelHandler.init;
         main.gen;
         ObjTmpStack.next;
         genMain;
         genEventProcessor;
         cd.dump;
         cd.descs[] -> descs[]; (* why ?*)         
         BC.close;
     if)
  #)

