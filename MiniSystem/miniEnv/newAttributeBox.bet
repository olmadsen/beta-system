ORIGIN '~beta/guienv/utils/groupbox';
INCLUDE '~beta/guienv/utils/radiogroup';
include 'subwindow'
---GUIenvlib:attributes---

newAttributeBox: window
  (# open::<
       (#
       do 'New attribute' -> title;
          (30,30) -> position;
          (270,170) -> size;
          nameLab.open;
          nameFld.open;
          kindGroup.open;
          (if false then
              beforeAfterGroup.open;
          if);
          okButton.open;
          cancelButton.open;
          okButton[] -> defaultButton;
          nameFld[] -> target;
       #);
     type:: (# do windowTypes.dialog->value #);
     borderWidth: (# exit 0 #);
     topBorder: (# exit 0 #);
     nameLab: @staticText
       (# open::
            (#
            do 'Name:' -> label;
               (borderWidth+10, topBorder+10) -> position;
               fitToContents;
            #);
       #);
     nameFld: @editText
       (# open::
            (# 
            do (borderWidth+50, topBorder+5) -> position;
               FitToContents;
            #);
       #);
     kindGroup:  @radioGroup
       (# radio1: @radio
            (# open::
                 (#
                 do 'Pattern' -> label;
                 #);
               eventHandler::
                 (# onStateChanged:: (# do (if state then ' ' -> kind if)#)
                 #)
            #);
          radio2: @radio
            (# open::
                 (#
                 do '@ staticObject' -> label;
                 #);
             eventHandler::
                 (# onStateChanged:: (# do (if state then '@' -> kind if)#)
                 #)
            #);
          radio3: @radio
            (# open::
                 (#
                 do '/ object module' -> label;
                 #);
               eventHandler::
                 (# onStateChanged:: (# do (if state then '/' -> kind if)#)
                 #)
            #);
          radio4: @radio
            (# open::
                 (#
                 do '# pattern module' -> label;
                 #);
               eventHandler::
                 (# onStateChanged:: (# do (if state then '#' -> kind if)#)
                 #)
            #);
          open:: 
            (#
            do 'Attribute kind: ' -> title;
               radio1.open;
               radio2.open;
               radio3.open;
               radio4.open;
               radio1[] -> currentRadio;
               (borderWidth + 10, topBorder + 40) -> position;
               (120,110) -> size;
            #);
       #);
     
     beforeAfterGroup:  @radioGroup
       (# radio1: @radio
            (# open::
                 (#
                 do 'Before' -> label;
                 #);
               eventHandler::
                 (#
                    onStateChanged:: (# do (if state then true -> before if)#)
                 #)
            #);
          radio2: @radio
            (# open::
                 (#
                 do 'After' -> label;
                 #);
               eventHandler::
                 (# onStateChanged:: (# do (if state then false -> before if)#)
                 #)
            #);
          open::
            (#
            do 'Position: ' -> title;
               radio1.open;
               radio2.open;
               radio1[] -> currentRadio;
               (150, topBorder+40) -> position;
               (100, 80) -> size;
            #);
       #);
     
     okButton: @pushButton
       (# open::
            (# 
            do 'OK' -> label; 
               (50, 20) -> size; 
               (borderWidth+200,topBorder+130) -> position;
            #);
          eventHandler::
            (# onMouseUp:: 
                 (# 
                 do nameFld.contents -> name[];
                    (if false then
                        'Name: ' -> puttext; nameFld.contents -> putline;
                        'Kind: ' -> puttext; kind -> put; 
                        ' before: ' -> puttext; 
                        (if before then 
                            'true' -> putline 
                         else 
                            'false' -> putline 
                        if);
                    if);
                    this(newAttributeBox).close;
            #)#)               
       #);
     cancelButton: @pushButton
       (# open::
            (# 
            do 'Cancel' -> label;               
               (50, 20) -> size; 
               
               (borderWidth+140,topBorder+130) -> position;
            #);
          eventHandler::
            (# onMouseUp:: 
                 (# 
                 do true -> isCanceled;
                    this(newAttributeBox).close
                 #)
            #)             
       #); 
     name: ^text;
     kind: @char;
     before,isCanceled: @boolean;
  #);
MessageBox: Window
  (# init:
       (# T: ^text
       enter T[]
       do T[] -> M[]; open
       #);
     M: ^text;
     open:: 
       (# 
       do (300,100) -> size;
          (400,300) -> position;
          msg.open; 
          M[] -> msg.label 
       #);     
     msg: @StaticText
       (# open:: (#
                 do (0,0) -> position; (300,100) -> size
                 #)
       #)
  #)

(*---program:descriptor---
GUIenv
(# theWindow: @newAttributeBox
     (# eventHandler :: (# onAboutToClose:: (# do terminate #)#)#)
do theWindow.open
#)*)
