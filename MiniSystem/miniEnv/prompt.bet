ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/auxcontrols';
INCLUDE '~beta/guienv/controls';

-- guienvLib: attributes --

PromptDialog: Window
  (# onYes:< (# do INNER #);
     onNo:< (# do INNER #);
     onCancel:< (# do INNER #);
     
     open::<
       (#
       do (400, 125) -> size;
          label.open;
          yesBtn.open;
          noBtn.open;
          cancelBtn.open;
	  cancelBtn.hide;
          yesBtn[] -> defaultButton;
          hide;
          INNER ;
       #);
     
     ChangeLabel:
       (# txt: ^Text;
       enter txt[]
       do (# captionWidth, captionHeight: @integer;
             width, height: @integer;
          do txt[] -> label.label;
             label.size -> (captionWidth, captionHeight);
             size -> (width, height);
             (if captionWidth > (400 - 50) then
                 (captionWidth + 50, height) -> size;
              else
                 (400, height) -> size;
             if);
          #);
       #);
     label: @caption
       (# ts: @TextStyle;
          open::<
            (#
            do 'Arial' -> ts.name;
               12 -> ts.size;
               ts[] -> style;
               'Do you want to save the changes' -> label;
               (400, 30) -> size;
               (25, 25) -> position;
               INNER
            #)
       #);
     delta: (# exit 40 #);
     yesBtn: @ pushbutton
       (# open::<
            (#
            do 'Yes' -> label;
               (60, 20) -> size;
               (100 + delta, 90) -> position;
               INNER
            #);
          eventHandler::<
            (# onMouseUp::<
                 (#
                 do onYes;
                 #)
            #)
       #);
     noBtn: @ pushbutton
       (# open::<
            (#
            do 'No' -> label;
               (60, 20) -> size;
               (180 + delta, 90) -> position;
               INNER
            #);
          eventHandler::<
            (# onMouseUp::<
                 (#
                 do onNo;
                 #)
            #)
       #);
     cancelBtn: @ pushbutton
       (# open::<
            (#
            do 'Cancel' -> label;
               (60, 20) -> size;
               (260, 90) -> position;
               INNER
            #);
          eventHandler::<
            (# onMouseUp::<
                 (#
                 do onCancel;
                 #)
            #)
       #)
  #);
