ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/postwimp/graphicscanvas/graphicscanvas';
INCLUDE '~beta/postwimp/demo/chaos/figures';
INCLUDE '~beta/postwimp/demo/chaos/diagram';
-- program: descriptor --
GUIenv
(# loadImage:
     (# name: ^Text;
        image: ^RichPixmap;
       enter name[]
     do (# path: ^Text;
          do &RichPixmap[] -> image[];
           'c:\\beta\\r5.5\\postwimp\\images\\' -> path[];
           name[] -> path.append;
           path[] -> image.readAlpha;
           false -> image.transparent;
        #);
     exit image[]
     #);
   main: @Window
     (# 
        scroller: @GraphicsCanvas
          (# res: @Resource;
             theStyle: ^TextStyle;
             theColor: ^Color;
             icon1: ^RichPixmap;
             createImageFigure:
               (# image: ^RichPixmap;
                  item: ^ImageFigure;
               enter image[]
               do &ImageFigure[] -> item[];
                  item.init;
                  image[] -> item.content;
               exit item[]
               #);
             items: @Sequence
               (# Element:: ScrollItem;
               #);
             ScrollItem:
               (# label: ^Text;
                  image: ^RichPixmap;
               #);
             
             addItemByLabel:
               (# label: ^Text;
                  item: ^ScrollItem;
               enter label[]
               do &ScrollItem[] -> item[];
                  label[] -> item.label[];
                  icon1[] -> item.image[];
                  item[] -> items.append;
                  
               #);
             
             render:
               (#
               do body.clear;
                  items.scan
                  (# shape: ^ScrollItemFigure;
                  do &ScrollItemFigure[] -> shape[];
                     current[] -> shape.init;
                     shape[] -> body.add;
                  #);
                  body.pack;
                  update;
               #);
             open::
               (#
               do 'icons\\cog.png' -> loadImage -> icon1[];
                  res.init;
                  (480, 640) -> size;
                  body[] -> add;
               #);
             
             Construction:
               (# hSpace:
                    (# minimum: @integer;
                       space: ^SpaceFigure;
                    enter minimum
                    do (#
                       do &SpaceFigure[] -> space[];
                          space.init;
                          minimum -> space.minimumWidth;
                          false -> space.horizontallyFlexible;
                       #);
                    exit space[]
                    #);
                  flexHSpace:
                    (# minimum: @integer;
                       space: ^SpaceFigure;
                    enter minimum
                    do minimum -> hSpace -> space[];
                       true -> space.horizontallyFlexible;
                    exit space[]
                    #);
               #);
             body: @ColumnFigure;
             ScrollItemFigure: RowFigure
               (# item: ^ScrollItem;
                  init::
                    (# 
                    enter item[]
                    do (# icon: ^ImageFigure;
                          title: ^TextFigure;
                          construct: @Construction;
                       do 2 -> construct.hSpace -> add;
                          item.image[] -> createImageFigure -> icon[];
                          icon[] -> add;
                          2 -> construct.hSpace -> add;
                          &TextFigure[] -> title[];
                          title.init;
                          item.label[] -> title.content;
                          theStyle[] -> title.style;
                          theColor[] -> title.stroke;
                          title[] -> add;
                          2 -> construct.flexHSpace -> add;
                          18 -> baseMinimumHeight;
                          true -> horizontallyFlexible;
                       #);
                    #);
               #);
          #);
        open::
          (# input: @File;
             fixed: ^TextStyle;
          do &TextStyle[] -> fixed[];
             'Courier' -> fixed.name;
             8 -> fixed.size;
             textFaces.plain -> fixed.face;
             'Scroll' -> title;
             (480, 640) -> size;
             scroller.open;
             true -> scroller.bindRight;
             true -> scroller.bindBottom;
             fixed[] -> scroller.theStyle[];
             scroller.res.colors.darkerCoolBrown[] -> scroller.theColor[];
             'scrolltest.bet' -> input.name;
             input.openRead;
             while:
               (if not input.eos then
                   input.getline -> scroller.addItemByLabel;
                   restart while;
               if);
             input.close;
             scroller.render;
          #);
     #);
do main.open;
#)
