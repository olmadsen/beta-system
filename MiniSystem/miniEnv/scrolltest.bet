ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/postwimp/graphicscanvas/graphicscanvas';
INCLUDE '~beta/postwimp/demo/chaos/figures';
INCLUDE '~beta/postwimp/demo/chaos/diagram';
-- program: descriptor --
GUIenv
(# loadImage:
     (# name: ^Text;
        image: ^RichPixmap;
       enter name[]
     do (# path: ^Text;
          do &RichPixmap[] -> image[];
           'c:\\beta\\r5.5\\postwimp\\images\\' -> path[];
           name[] -> path.append;
           path[] -> image.readAlpha;
           false -> image.transparent;
        #);
     exit image[]
     #);
    makeColor:
     (# red, green, blue: @integer;
        theColor: ^Color;
     enter (red, green, blue)
     do &Color[] -> theColor[];
        (# r, g, b: @real;
        do red / 255.0 -> r;
           green / 255.0 -> g;
           blue / 255.0 -> b;
           r * 0xFFFF -> theColor.red;
           g * 0xFFFF -> theColor.green;
           b * 0xFFFF -> theColor.blue;
        #);
     exit theColor[]
     #);
   main: @Window
     (# 
        checkScrollValues:
          (# value: @integer;
          do scroller.getScrollMaximum -> theScrollbar.maxValue;
             theScrollbar.value -> value;
             (if value > theScrollbar.maxValue then
                 theScrollbar.maxValue -> theScrollbar.value;
                 theScrollbar.value -> scroller.setScroll;
                 
             if);
          #);
        theScrollbar: @Scrollbar
          (# vertical:: (# do true -> value; #);
             eventHandler::
               (#
                  onValueChanged::
                    (#
                    do value -> scroller.setScroll;
                    #);
               #);
          #);
        scroller: @FigureCanvas
          (# selected: ^ScrollItemFigure;
             
             selector: @Behaviour
               (# TargetType:: ScrollItemFigure;
                  onMouseDown::
                    (#
                    do target[] -> setSelection;
                    #);
                  
               #);
             setSelection:
               (# next: ^ScrollItemFigure;
                  previous: ^ScrollitemFigure;
               enter next[]
               do (if selected[] <> next[] then
                      selected[] -> previous[];
                      next[] -> selected[];
                      (if previous[] <> NONE then
                          previous.refresh;
                      if);
                      (if selected[] <> NONE then
                          selected.refresh;
                      if);
                      body.pack;
                      update;
                  if);
               #);
             setScroll:
               (# value: @integer;
               enter value
               do (# maximum: @integer;
                  do getScrollMaximum -> maximum;
                     (if value < 0 then
                         0 -> value;
                     if);
                     (if value > maximum then
                         maximum -> value;
                     if);
                     (if value <> theScroll then
                         value -> theScroll;
                         -theScroll -> body.y;
                         update;
                     if);
                  #);
               #);
             getScroll:
               (# value: @integer;
               do theScroll -> value;
               exit value
               #);
             getScrollMaximum:
               (# width, height: @integer;
                  value: @integer;
               do size -> (width, height);
                  body.height - height -> value;
                  (if value < 0 then
                      0 -> value;
                  if);
               exit value
               #);
             
             theStyle: ^TextStyle;
             theColor: ^Color;
             theSelectionColor: ^Color;
             theScroll: @integer;
             icon1: ^RichPixmap;
             createImageFigure:
               (# image: ^RichPixmap;
                  item: ^ImageFigure;
               enter image[]
               do &ImageFigure[] -> item[];
                  item.init;
                  image[] -> item.content;
               exit item[]
               #);
             createBox:
               (# box: ^RectFigure;
                  white: ^Color;
               enter white[]
               do &RectFigure[] -> box[];
                  box.init;
                  true -> box.verticallyFlexible;
                  true -> box.horizontallyFlexible;
                  white[] -> box.fill;
               exit box[]
               #);
             items: @Sequence
               (# Element:: ScrollItem;
               #);
             ScrollItem:
               (# label: ^Text;
                  image: ^RichPixmap;
               #);
             
             addItemByLabel:
               (# label: ^Text;
                  item: ^ScrollItem;
               enter label[]
               do &ScrollItem[] -> item[];
                  label[] -> item.label[];
                  icon1[] -> item.image[];
                  item[] -> items.append;
                  
               #);
             
             render:
               (#
               do body.clear;
                  items.scan
                  (# shape: ^ScrollItemFigure;
                  do &ScrollItemFigure[] -> shape[];
                     current[] -> shape.init;
                     selector[] -> shape.theBehaviour[];
                     shape[] -> body.add;
                  #);
                  body.pack;
                  update;
               #);
             open::
               (#
               do 'icons\\cog.png' -> loadImage -> icon1[];
                  (253, 185, 87) -> makeColor -> theSelectionColor[];
                  (480, 640) -> size;
                  body[] -> add;
               #);
             
             Construction:
               (# hSpace:
                    (# minimum: @integer;
                       space: ^SpaceFigure;
                    enter minimum
                    do (#
                       do &SpaceFigure[] -> space[];
                          space.init;
                          minimum -> space.minimumWidth;
                          false -> space.horizontallyFlexible;
                       #);
                    exit space[]
                    #);
                  flexHSpace:
                    (# minimum: @integer;
                       space: ^SpaceFigure;
                    enter minimum
                    do minimum -> hSpace -> space[];
                       true -> space.horizontallyFlexible;
                    exit space[]
                    #);
               #);
             body: @ColumnFigure;
             ScrollItemFigure: CenterFigure
               (# item: ^ScrollItem;
                  init::
                    (# 
                    enter item[]
                    do refresh;
                    #);
                  refresh:
                    (#
                    do (# icon: ^ImageFigure;
                          title: ^TextFigure;
                          construct: @Construction;
                          body: ^RowFigure;
                          
                       do clear;
                          (if THIS(ScrollItemFigure)[] = selected[] then
                              theSelectionColor[] -> createBox -> background[];
                              background[] -> add;
                          if);
                          &RowFigure[] -> body[];
                          body.init;
                          2 -> construct.hSpace -> body.add;
                          item.image[] -> createImageFigure -> icon[];
                          icon[] -> body.add;
                          2 -> construct.hSpace -> body.add;
                          &TextFigure[] -> title[];
                          title.init;
                          item.label[] -> title.content;
                          theStyle[] -> title.style;
                          theColor[] -> title.stroke;
                          title[] -> body.add;
                          2 -> construct.flexHSpace -> body.add;
                          18 -> body.baseMinimumHeight;
                          true -> body.horizontallyFlexible;
                          body[] -> add;
                          true -> horizontallyFlexible;
                       #);
                    #);
               #);
          #);
        open::
          (# input: @File;
             fixed: ^TextStyle;
             res: @scroller.Resource;
             orange: ^Color;
          do 
             &TextStyle[] -> fixed[];
             'Courier' -> fixed.name;
             8 -> fixed.size;
             textFaces.plain -> fixed.face;
             'Scroll' -> title;
             (480, 640) -> size;
             scroller.open;
             (480 - 16, 640) -> scroller.size;
             true -> scroller.bindRight;
             true -> scroller.bindBottom;
             fixed[] -> scroller.theStyle[];
             res.init;
             res.colors.darkerCoolBrown[] -> scroller.theColor[];
             theScrollbar.open;
             18 -> theScrollbar.scrollAmount;
             (480 - 16, 0) -> theScrollbar.position;
             (16, 640) -> theScrollbar.size;
             true -> theScrollbar.bindRight;
             true -> theScrollbar.bindBottom;
             false -> theScrollbar.bindLeft;
             'scrolltest.bet' -> input.name;
             input.openRead;
             while:
               (if not input.eos then
                   input.getline -> scroller.addItemByLabel;
                   restart while;
               if);
             input.close;
             scroller.render;
             checkScrollValues;
          #);
     #);
do main.open;
#)
