ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '~beta/MiniSystem/compiler_IF/directoryComponents';
INCLUDE '~beta/MiniSystem/qbeta/qgenerator';
INCLUDE '~beta/MiniSystem/VM/betaVM';
INCLUDE '~beta/web/webservice';
BODY '~beta/MiniSystem/qbeta/unithandlerBody';
BODY '~beta/MiniSystem/VM/objectcode_VM';
-- program: descriptor --
SystemEnv
(# setWindowEnv:: (# do gui[] -> theWindowEnv[] #);
   gui: @guienv;

   evaluateHandler: @RequestHandler
     (# init::
          (#
          do 'evaluate' -> path[];
          #);
        post::
          (#
          do &HttpResponse[] -> response[];
             request.body[] -> evaluate -> response.body[];
             'text/plain' -> response.contentType[];
             201 -> response.status;
             'Created' -> response.message[];
          #);
     #);
   runtimeService: @WebService;

   evaluate:
     (# compile: @generator;
        VM: @ betaVM
          (# putCH:: (# do ch -> output.put #);
             getCh:: (# do get -> ch #);
          #);
        LG: @log;
        fullPath,fileExt,BetaWorld,errors: ^Text;
        iMain: ^compile.IModule;
        runMode: @integer;
     
        source: ^Text;
        output: ^Text;
        sourceFile: @File;
     enter source[]
     do 'C:\\beta\\r5.5\\MiniSystem\\qbeta\\BETAworld\\workspace\\hello\\hello.qbeta' -> fullPath[];
        fullPath[] -> sourceFile.name;
        sourceFile.openWrite;
        source[] -> sourceFile.putText;
        sourceFile.close;
        '/MiniSystem/qbeta/BETAworld/' -> BetaWorld[];
        '.qbeta' -> fileExt[];
        (BetaWorld[],fileExt[],'.xbeta') -> compile.initDirectoryModules;
        ('qservice.log', false) -> LG.open;
     
        true -> compile.newFatComma;
        true -> compile.stringAsVal;
        true -> compile.isXbeta;
        true -> compile.altParser;
        run_Beta_INT -> runMode;
        (LG[], true, NONE, fullPath[]) -> compile -> (iMain[],errors[]);
        (if errors[] <> none then
            'errors:'-> putline; errors[] -> putline;
        if);

        &Text[] -> output[];
    
        (runMode,true) -> VM.init;
     
        (if not compile.onlyLex
            and not compile.hasSyntaxError
            and not compile.semanticErrors then
            true -> VM.noPause;
            (compile.runtimeDescs,compile.mainDescNo,fullPath[],runMode)
              -> VM.execute.init;
            VM.execute[] -> fork;
            VM.reactivate
        if);
     exit output[]
     #);

do 
   7913 -> runtimeService.init;
   evaluateHandler.init;
   evaluateHandler[] -> runtimeService.register;
   cycle
     (#
     do runtimeService.accept;
     #);
#)