ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/auxcontrols'
'~beta/guienv/controls'


-- windowLib: attributes --

center:
  (# screen: @rectangle;
     width, height: @integer;
     screenWidth, screenHeight: @integer;
     left, top: @integer;
  do size -> (width, height);
     screenRectangle -> screen;
     
     screen.size -> (screenWidth, screenHeight);
     
     screen.left + ((screenWidth - width) div 2) -> left;
     
     
     screen.top + (screenHeight div 5) -> top;
     (left, top) -> position;
  #);


-- guienvLib: attributes --

PlainDialog: Window
  (# 
     confirmLabel: (#  enter confirmBtn.label exit confirmBtn.label #);
     dialogTitle: (#  enter titleCaption.label exit titleCaption.label #);
     onConfirm:< (#  do INNER #);
     onCancel:< (#  do INNER #);
     open::< 
       (# 
       do
          (235,75)->size;
          titleCaption.open;
          separator1.open;
          confirmBtn.open;
          cancelBtn.open;
          INNER ;
          center;
          hide;
       #);
     titleCaption: @caption
       (#
          open::< 
            (# 
            do 'Label'->label; (33,13)->size; (15,10)->position; INNER
            #)
       #);
     separator1: @separator
       (#
          open::< 
            (# 
            do
               true->bindRight;
               2->style;
               (125,4)->size;
               (15,24)->position;
               INNER
            #)
       #);
     confirmBtn: @pushbutton
       (#
          open::< 
            (# 
            do
               'OK'->label;
               false->bindLeft;
               true->bindRight;
               (70,20)->size;
               (155,15)->position;
               THIS(pushButton)[]->defaultButton;
               INNER
            #);
          eventHandler::<  (# onMouseUp::<  (#  do onConfirm #) #)
       #);
     cancelBtn: @pushbutton
       (#
          open::< 
            (# 
            do
               false->bindLeft;
               true->bindRight;
               'Cancel'->label;
               (70,20)->size;
               (155,45)->position;
               INNER
            #);
          eventHandler::<  (# onMouseUp::<  (#  do onCancel #) #)
       #)
  #);



GoDialog: PlainDialog
  (# 
     open::< 
       (# count: @integer
       enter count
       do
          (225,75)->size;
          'Go'->title;
          'Go'->dialogTitle;
          countFld.open;
          count->countFld.integerContents;
          countFld[]->target;
          countFld.selectAll;
          caption2.open;
          INNER
       #);
     countFld: @dialogField
       (#
          filter::  (#  do ch->ascii.isDigit->allow #);
          open::< 
            (# 
            do
               ''->textContents;
               true->editable;
               false->allowReturn;
               true->border.visible;
               (85,21)->size;
               (50,40)->position;
               countFld[]->target;
               
            #)
       #);
     caption2: @caption
       (#
          open::< 
            (# 
            do 'Step:'->label; (30,13)->size; (15,44)->position; INNER
            #)
       #)
  #)  

