ORIGIN '~beta/guienv/guienv';
INCLUDE 'subwindow';
---windowlib:attributes---
attributeHeight: (# exit 16 #);
attributeWidth: (# exit 90 #);

runtimeWindow: subWindow
  (# open::<
       (# T: ^text
       enter T[]
       do T[] -> head.label;          
          18 + borderWidth -> hPos;
          setSize;          
          inner;
          fitToContents
       #);
     setSize: 
       (# 
       do (attributeWidth + 2 * borderWidth, hPos + borderWidth) -> size; 
          this(subWindow).setSize;
       #);
     
     attributeItem:< StaticText
       (# open::
            (# T: ^text; kind: ^text; v,h: @integer; 
               kindTxt: ^text; isRef: @boolean
            enter(T[],kind[],v,h,off,isRef)
            do T[] -> label;
               7 -> borderWidth;
               (attributeWidth,attributeHeight) -> size;
               (v,h) -> position;
               (this(runtimeWindow)[],kind[],v+attributeWidth-attributeHeight - 1,h + 1,isRef) 
                 -> (&accessButton[]).open;
               (* true -> bindRight;
                * true-> bindBottom; -- funny effect, changes size and position?
                *)
            #);
          accessButton:< pushButton
          (# eventHandler::<
               (# onMouseUp::<
                    (# 
                    do 'Go' -> putline;
                       inner
                    #);
               #);
             open::< 
               (# aTextStyle: ^textStyle;
                  kind: ^text; v,h: @integer
               enter(kind[],v,h,isRef)
               do &textStyle[] -> aTextStyle[]; 
                  textFaces.bold -> aTextStyle.face;
                  8 -> aTextStyle.size;
                  aTextStyle[] -> style;
                  kind[] -> label;
                  (attributeHeight,attributeheight - 2) -> size;
                  (v,h) -> position;
                  'green'-> CT.lookup -> backgroundColor; (* no effect? *)
                  INNER
               #)
          #);
          theAttributeItem: (# exit this(attributeItem)[] #);
          off: @integer;
          isRef: @boolean;
       #);
     addAttribute:
       (# name,kind,value: ^text; off: @integer; isRef: @boolean
       enter(name[],kind[],off,value[],isRef)
       do '   ' -> (name.copy -> name[]).append;
          value[] -> name.append;
          (this(runTimeWindow)[],name[], kind[],borderWidth,hPos,off,isRef) -> (&attributeItem[]).open;
          hPos + attributeHeight + 1 -> hPos;
          setSize;
       #);
     hPos: @integer;

  #)

     
