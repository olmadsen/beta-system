ORIGIN '~beta/guienv/guienv';
INCLUDE 'objectContent';
INCLUDE 'runtimeWindow';
INCLUDE '../miniCompiler/generator';
---windowlib:attributes---
xRuntimeWindow: runtimeWindow
  (# attributeItem::<
       (# accessButton::<
            (# eventHandler::<
                 (# onMouseUp::<
                      (# obj: ^objDesc.template;
                      do (if isRef then
                             'Get reference:' -> puttext; off -> putint; newline;
                             thisObj.rfields[off][] -> obj[];
                             (if obj[] <> none then
                                 (if obj.myDescInx = 0 then
                                     (compile[],objCont[],obj[]) 
                                       -> (&xRuntimeWindow[]).createVrep
                                  else                                     
                                     (compile[],objCont[],obj[]) 
                                       -> (&xRuntimeWindow[]).create
       if)if)if)#)#)#)#);
     compile: ^generator;
     thisObj: ^objDesc.template;   
     objCont: ^ObjectContent;
     create:
       (# name: ^text;
          descNo: @integer;
          OD: ^generator.objectDesc;
          org,obj: ^objDesc.template;
          orgN: ^text
       enter(compile[],objCont[],thisObj[])     
       do 'Create: ' -> puttext; descNo -> putint; ' ' ->put; newline;
          thisObj.myName -> name[];
          thisObj.myDescInx -> descNo;
          descNo -> compile.newDesc.getOD -> OD[];

          (none,name[]) -> open;
          
          thisObj.rfields[1][] -> org[]; (* get the right originOff *)
          (if org[] = none then
              'none' -> orgN[]
           else
              org.myName -> orgN[]
          if);
          ('Origin','^',1,orgN[],true) -> addAttribute;
          OD[] -> objCont.scanODattributes
          (# off: @integer; value: ^text;
             isRef: @boolean
          do currentND.off -> off;
             &text[] -> value[];
             false -> isRef;
             'Kind: ' -> puttext; currentDcl.IT.sKind -> puttext; newline;
             (if off > 0 then 
                 (if currentDcl.IT.isSimple then
                     off -> thisObj.get -> value.putint;
                     false -> isRef;
                  else
                     thisObj.rfields[off][] -> obj[];
                     (if obj[] = none then
                         'none' -> value[]
                      else
                         obj.myName -> value[]
                     if);
                     true -> isRef
                 if)            
              else (* pattern *)
                 '' -> value.puttext
             if);
             (currentND.lexemSy,currentDcl.it.sKind,off,value[],isRef) -> addAttribute
          #)
       exit this(xRuntimeWindow)[]
       #);
     createVrep:
       (# name: ^text;
          descNo: @integer;
          inx: ^text;
          mkVal: 
            (# off: @integer; val: @text  
            enter off 
            do thisObj.vfields[off] -> val.putint;
            exit val[]
            #)
       enter(compile[],objCont[],thisObj[])     
       do 'Create vRep:'->puttext;
          thisObj.myName -> name[];
          thisObj.myDescInx -> descNo;
          (none,name[]) -> open;
          ('index','=',1,1->mkVal,false) -> addAttribute;
          ('range','=',2,2->mkVal,false) -> addAttribute;
          (for i: thisObj.vfields[2] repeat
               &text[] -> inx[];
               i -> inx.putint; ':' -> inx.put;
               (inx[],'=',2 + i,2 + i -> mkVal,false) -> addAttribute
          for);
       #)


  #)
