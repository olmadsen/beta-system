ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/postwimp/graphicscanvas/graphicscanvas';
INCLUDE '~beta/postwimp/graphicscanvas/colors';
INCLUDE '~beta/postwimp/demo/chaos/figures';

-- WindowLib: Attributes --

loadImage:
  (# name: ^Text;
     image: ^RichPixmap;
  enter name[]
  do (# path: ^Text;
     do &RichPixmap[] -> image[];
        'c:\\beta\\r5.5\\postwimp\\images\\' -> path[];
        name[] -> path.append;
        path[] -> image.readAlpha;
        false -> image.transparent;
     #);
  exit image[]
  #);

makeColor:
  (# red, green, blue: @integer;
     theColor: ^Color;
  enter (red, green, blue)
  do &Color[] -> theColor[];
     (# r, g, b: @real;
     do red / 255.0 -> r;
        green / 255.0 -> g;
        blue / 255.0 -> b;
        r * 0xFFFF -> theColor.red;
        g * 0xFFFF -> theColor.green;
        b * 0xFFFF -> theColor.blue;
     #);
  exit theColor[]
  #);

TableView: Canvas
  (# style:
       (# setStyle:
            (# value: ^TextStyle;
            enter value[]
            do value[] -> interior.theStyle[];
               interior.refresh;
            #);
       enter setStyle
       exit interior.theStyle[]
       #);
     numberOfItems:
       (# value: @integer;
       do interior.items.size -> value;
       exit value
       #);
     clear:
       (#
       do interior.items.clear;
          interior.refresh;
       #);
     insert:
       (# index: @integer;
          rows: @integer;
       enter (index, rows)
       do (# lst: ^interior.TableViewItemSequence;
             anx: @integer;
          do &interior.TableViewItemSequence[] -> lst[];
             lst.init;
             interior.items.scan
             (#
             do (if inx = index then
                    (# item: ^interior.TableViewItem;
                    do (for rows repeat
                            &interior.TableViewItem[] -> item[];
                            ('', NONE) -> item.init;
                            item[] -> lst.append;
                       for);
                    #);
                if);
                current[] -> lst.append;
             #);
             (if index > lst.size then
                 (# item: ^interior.TableViewItem;
                 do (for rows repeat
                         &interior.TableViewItem[] -> item[];
                         ('', NONE) -> item.init;
                         item[] -> lst.append;
                    for);
                 #);
             if);
             lst[] -> interior.items[];
          #);
          refresh;
       #);
     append:
       (# rows: @integer;
       enter rows
       do (# item: ^interior.TableViewItem;
          do (for rows repeat
                  &interior.TableViewItem[] -> item[];
                  ('', NONE) -> item.init;
                  item[] -> interior.items.append;
             for);
          #);
          refresh;
       #);
     prepend:
       (# rows: @integer;
       enter rows
       do (# item: ^interior.TableViewItem;
          do (for rows repeat
                  &interior.TableViewItem[] -> item[];
                  ('', NONE) -> item.init;
                  item[] -> interior.items.prepend;
             for);
          #);
          refresh;
       #);
     delete:
       (# index: @integer;
          rows: @integer;
       enter (index, rows)
       do (# lst: ^interior.TableViewItemSequence;
             anx: @integer;
          do &interior.TableViewItemSequence[] -> lst[];
             lst.init;
             interior.items.scan
             (#
             do (if (inx >= index) and (inx < (index + rows)) then
                 else
                    current[] -> lst.append;
                if);
             #);
             lst[] -> interior.items[];
          #);
          refresh;
       #);
     
     itemForIndex:
       (# index: @integer;
          item: ^interior.TableViewItem;
       enter index
       do index -> interior.items.get -> item[];
       exit item[]
       #);
     indexForItem:
       (# item: ^interior.TableViewItem;
          index: @integer;
       enter item[]
       do search: interior.items.scan
            (#
            do index + 1 -> index;
               (if current[] = item[] then
                   leave search;
               if);
            #);
       exit index
       #);
     
     setText:
       (# index: @integer;
          value: ^Text;
       enter (index, value[])
       do (# item: ^interior.TableViewItem;
          do index -> itemForIndex -> item[];
             value[] -> item.setLabel;
             refresh;
          #);
       #);
     getText:
       (# index: @integer;
          value: ^Text;
       enter index
       do (# item: ^interior.TableViewItem;
          do index -> itemForIndex -> item[];
             item.getLabel -> value[];
          #);
       exit value[]
       #);
     setImage:
       (# index: @integer;
          image: ^RichPixmap;
       enter (index, image[])
       do (# item: ^interior.TableViewItem;
          do index -> itemForIndex -> item[];
             image[] -> item.setImage;
             refresh;
          #);
       #);
     getImage:
       (# index: @integer;
          image: ^RichPixmap;
       enter index
       do (# item: ^interior.TableViewItem;
          do index -> itemForIndex -> item[];
             item.getImage -> image[];
          #);
       exit image[]
       #);
     
     selection: @
       (# select:
            (# index: @integer;
               extend: @boolean;
            enter (index, extend)
            do (if (index > 0) and (index <= interior.items.size) then 
                   index -> itemForIndex -> interior.setSelection;
                else
                   NONE -> interior.setSelection;
               if);
            #);
          first:
            (# index: @integer;
            do (if interior.selected[] <> NONE then
                   interior.selected[] -> indexForItem -> index;
                else
                   0 -> index;
               if);
            exit index
            #);
       #);
     
     getItemRectangle:
       (# inx: @integer;
          r: @Rectangle;
       enter inx
       do (# item: ^interior.TableViewItem;
          do inx -> itemForIndex -> item[];
             (if item[] <> NONE then
                 item.x - r.left;
                 item.y -> r.top;
                 item.x + item.width -> r.right;
                 item.y + item.height -> r.bottom;
             if);
          #);
       exit r
       #);
     onMouseDown:<
       (# event: ^EventHandler.MouseEvent;
       enter event[]
       do INNER;
       #);
     onMouseUp:<
       (# event: ^EventHandler.MouseEvent;
       enter event[]
       do INNER;
       #);
     
     checkScrollValues:
       (# value: @integer;
       do interior.getScrollMaximum -> theScrollbar.maxValue;
          theScrollbar.value -> value;
          (if value > theScrollbar.maxValue then
              theScrollbar.maxValue -> theScrollbar.value;
              theScrollbar.value -> interior.setScroll;
          if);
       #);
     scrollIntoView:
       (# y0: @integer;
          y1: @integer;
          width, height: @integer;
       do (if interior.selected[] <> NONE then
              interior.selected.y -> y0;
              y0 + interior.selected.height -> y1;
              y0 - theScrollbar.value -> y0;
              y1 - theScrollbar.value -> y1;
              size -> (width, height);
              (if y0 < 0 then
                  y0 -> theScrollbar.value;
              if);              
              (if y1 > height then
                  y1 - (height - 4) -> theScrollbar.value;
              if);
          if);
       #);
     
     refresh:
       (#
       do interior.refresh;
          checkScrollValues;
       #);
     
     theScrollbar: @Scrollbar
       (# vertical::
            (# do true -> value; #);
          eventHandler::
            (# onValueChanged::
                 (#
                 do value -> interior.setScroll;
                 #);
            #);
       #);
     
     interior: @FigureCanvas
       (# selected: ^TableViewItem;
          clicker: @Tool
            (# onMouseDown::
                 (#
                 do none -> setSelection;
                 #);
            #);
          selector: @Behaviour
            (# TargetType:: TableViewItem;
               onMouseDown::
                 (#
                 do (if target[] <> selected[] then
                        target[] -> setSelection;
                    if);
                 #);
            #);
          setSelection:
            (# next: ^TableViewItem;
               previous: ^TableViewItem;
            enter next[]
            do (if selected[] <> next[] then
                   selected[] -> previous[];
                   next[] -> selected[];
                   (if previous[] <> NONE then
                       previous.refresh;
                   if);
                   (if selected[] <> NONE then
                       selected.refresh;
                   if);
                   body.pack;
                   update;
               if);
            #);
          setScroll:
            (# value: @integer;
            enter value
            do (# maximum: @integer;
               do getScrollMaximum -> maximum;
                  (if value < 0 then
                      0 -> value;
                  if);
                  (if value > maximum then
                      maximum -> value;
                  if);
                  (if value <> theScroll then
                      value -> theScroll;
                      -theScroll -> body.y;
                      update;
                  if);
               #);
            #);
          getScroll:
            (# value: @integer;
            do theScroll -> value;
            exit value
            #);
          getScrollMaximum:
            (# width, height: @integer;
               value: @integer;
            do size -> (width, height);
               body.height - height -> value;
               (if value < 0 then
                   0 -> value;
               if);
            exit value
            #);
          
          theStyle: ^TextStyle;
          theColor: ^Color;
          theSelectionColor: ^Color;
          theScroll: @integer;
          createImageFigure:
            (# image: ^RichPixmap;
               item: ^ImageFigure;
            enter image[]
            do &ImageFigure[] -> item[];
               item.init;
               image[] -> item.content;
            exit item[]
            #);
          createBox:
            (# box: ^RectFigure;
               white: ^Color;
            enter white[]
            do &RectFigure[] -> box[];
               box.init;
               true -> box.verticallyFlexible;
               true -> box.horizontallyFlexible;
               white[] -> box.fill;
            exit box[]
            #);
          
          TableViewItemSequence: Sequence
            (# Element:: TableViewItem;
            #);
          
          items: ^TableViewItemSequence;
                    
          refresh:
            (# space: ^SpaceFigure;
            do (if items[] <> NONE then
                   body.clear;
                   items.scan
                   (#
                   do selector[] -> current.theBehaviour[];
                      current[] -> body.add;
                   #);
                   &SpaceFigure[] -> space[];
                   space.init;
                   18 -> space.minimumHeight;
                   false -> space.verticallyFlexible;
                   space[] -> body.add;
                   body.pack;
                   update;
               if);
            #);
          open::
            (#
            do &TableViewItemSequence[] -> items[];
               (253, 185, 87) -> makeColor -> theSelectionColor[];
               (480, 640) -> size;
               body[] -> add;
               clicker[] -> clickTool[];
            #);
          
          Construction:
            (# hSpace:
                 (# minimum: @integer;
                    space: ^SpaceFigure;
                 enter minimum
                 do (#
                    do &SpaceFigure[] -> space[];
                       space.init;
                       minimum -> space.minimumWidth;
                       false -> space.horizontallyFlexible;
                    #);
                 exit space[]
                 #);
               flexHSpace:
                 (# minimum: @integer;
                    space: ^SpaceFigure;
                 enter minimum
                 do minimum -> hSpace -> space[];
                    true -> space.horizontallyFlexible;
                 exit space[]
                 #);
            #);
          body: @ColumnFigure;
          TableViewItem: CenterFigure
            (# label: ^Text;
               image: ^RichPixmap;
               init::
                 (#
                 enter (label[], image[])
                 do refresh;
                 #);
               
               setLabel:
                 (# value: ^Text;
                 enter value[]
                 do value[] -> label[];
                    refresh;
                 #);
               getLabel:
                 (#
                 exit label[]
                 #);
               setImage:
                 (# value: ^RichPixmap;
                 enter value[]
                 do value[] -> image[];
                    refresh;
                 #);
               getImage:
                 (#
                 exit image[]
                 #);
               refresh:
                 (#
                 do (# icon: ^ImageFigure;
                       title: ^TextFigure;
                       construct: @Construction;
                       body: ^RowFigure;
                       
                    do clear;
                       (if THIS(TableViewItem)[] = selected[] then
                           theSelectionColor[] -> createBox -> background[];
                           background[] -> add;
                       if);
                       &RowFigure[] -> body[];
                       body.init;
                       2 -> construct.hSpace -> body.add;
                       (if image[] <> NONE then
                           image[] -> createImageFigure -> icon[];
                           icon[] -> body.add;
                        else
                           12 -> construct.hSpace -> body.add;
                       if);
                       2 -> construct.hSpace -> body.add;
                       &TextFigure[] -> title[];
                       title.init;
                       label[] -> title.content;
                       theStyle[] -> title.style;
                       theColor[] -> title.stroke;
                       title[] -> body.add;
                       2 -> construct.flexHSpace -> body.add;
                       18 -> body.baseMinimumHeight;
                       true -> body.horizontallyFlexible;
                       body[] -> add;
                       true -> horizontallyFlexible;
                    #);
                 #);
            #);
          eventHandler::
            (# onFrameChanged::
                 (# width, height: @integer;
                 do THIS(FigureCanvas).size -> (width, height);
		    width -> body.baseMinimumWidth;
                    body.pack;
                    checkScrollValues;
                 #);
               onMouseDown::
                 (#
                 do THIS(MouseEvent)[] -> THIS(TableView).onMouseDown;
                 #);
               onMouseUp::
                 (#
                 do THIS(MouseEvent)[] -> THIS(TableView).onMouseUp;
                 #);
            #);
       #);
     open::<
       (# fixed: ^TextStyle;
          colors: @ColorTable;
       do 2 -> margin;
          true -> border.visible;
          borderStyles.shadowIn -> border.style;
          &TextStyle[] -> fixed[];
          'Courier' -> fixed.name;
          8 -> fixed.size;
          textFaces.plain -> fixed.face;
          (480, 640) -> size;
          interior.open;
          (margin, margin) -> interior.position;
          (480 - 16 - 2*margin, 640 - 2*margin) -> interior.size;
          true -> interior.bindRight;
          true -> interior.bindBottom;
          fixed[] -> interior.theStyle[];
          colors.init;
          colors.darkerCoolBrown[] -> interior.theColor[];
          theScrollbar.open;
          18 -> theScrollbar.scrollAmount;
          (480 - 16 - margin, margin) -> theScrollbar.position;
          (16, 640 - 2*margin) -> theScrollbar.size;
          true -> theScrollbar.bindRight;
          true -> theScrollbar.bindBottom;
          false -> theScrollbar.bindLeft;
          INNER;
       #);
     margin: @integer;
  #);
