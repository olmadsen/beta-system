ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/utils/framer';
INCLUDE '~beta/guienv/utils/colorTable';
---windowlib:attributes---
SubWindow: Framer
  (# open:: <
       (# lab: ^text; T: @text
       enter lab[]
       do (*(80,180) -> size;*)
          true -> border.visible;
          borderstyles.simple -> border.style;
          (12,12) -> position;
          7 -> borderwidth;
          '153 180 209 beta' -> T.putline; 
          '220 220 220 betaExeGrey' -> T.putline; 
          0 -> T.setpos; 
          T[] -> CT.load(# merge::trueObject #);          
          (*'beta' -> CT.lookup -> backgroundColor;*)
          ocBack.open;
          openClose.open;
          head.open;
          bottomRightCorner.open;
          lab[] -> head.label;
          INNER;
          setsize
       #);
     init:<
       (# T: ^text
       enter T[]
       do (theFather[],T[]) -> open;
          inner;
          setSize; (* clean-up: open, init, setsize, etc *)
       #);
     setSize:
       (#
       do openClose.setSize;
          head.setSize;
          bottomRightCorner.setPosition
       #);
     ocBack: @StaticText 
       (* to ensure at BETA colored background behind the open/close button *)
       (# open::
            (# 
            do (borderWidth,borderWidth) -> position;
               (16,18) -> size; (* too big for seq-objWs - or? - so fix it *)
               '' -> label;
               'beta' -> CT.lookup -> backgroundColor;
            #)
       #);
     openClose: @openCloseButton;
     openCloseButton:< pushButton
       (# open::<
            (#
            do '*' -> label;;
               setSize
            #);
          setSize: 
            (# 
            do (borderWidth+2,borderWidth+2) -> position; 
               (16,16) -> size; 
            #);
          eventHandler::<
            (# onMouseUp::<
                 (#
                 do inner
                 #)
       #)#);
     Head: @Heading;
     Heading:< StaticText
       (# eventHandler::<
            (# onMouseDown::<
                 (#
                 do (if buttonState = 1 then
                        trackMouse
                        (# mouseMove:: 
                             (#
                             do (h,v) -> this(SubWindow).move; 
                            true -> this(SubWindow).update
                             #)
                           
                        #)
                     else
                        INNER
            if)#)#);
          open::<
            (# h,w: @integer ; C: @color; 
            do this(subWindow).size -> size -> (w,h); (* why do we do this?
                                                       * See setSize below
                                                       *)
               (borderWidth+16,borderWidth) -> position;
               setSize;
               true -> border.visible;
               borderstyles.etchedOut -> border.style;
               false -> border.visible;
               (*39168,46080,53504) -> backgroundColor; -- does not work *)
               'beta' -> CT.lookup -> C -> backgroundcolor;
               (*  c.red -> putint; ' ' -> put; 
                *                c.green -> putint; ' ' -> put; 
                *                c.blue -> putint; newline;
                *)
               INNER
            #);
          setSize: 
            (# w,h: @integer 
            do this(subWindow).size -> (w,h);
               (w - 2 * borderWidth - 16,18) -> size 
            #);
          textWidth:
            (# T: ^text; width: @integer; 
            enter T[]
            do (if T[] = none then
                   'subWindow:TextWidth:T=none'->putline
                else
                   T[] -> (style).widthOfText -> width
               if)
            exit width
            #);
       #);
     bottomRightCorner: @Framer
       (# eventHandler::<
            (# onMouseEnter::<
                 (# 
                 do theCursor -> oldCursor[];
                    cursors.iBeam[] -> theCursor
                 #);
               onMouseLeave::<
                 (#
                 do oldCursor[] -> theCursor
                 #);
               oldCursor: ^cursor;
               onMouseDown::<
                 (# width,height: @integer
                 do this(subWindow).size -> (width,height);
                    trackMouse
                    (# mouseMove:: 
                         (#
                         do (width+h-> width,height+v->height) -> this(subWindow).size;
                            (*width -> *)head.setSize;
                            true -> this(SubWindow).update;
                            setPosition
                         #)
                    #);
                    inner
                 #);               
            #);
          open::<
            (# 
            do 'grey40' -> CT.lookup -> backGroundColor;
               this(subWindow).borderWidth -> borderWidth;
               (borderWidth,borderWidth) -> size;
               (*true -> bindRight;
                true -> bindBottom;*)
               setPosition;
            #);
          setPosition: 
            (# w,h: @integer
            do this(SubWindow).size -> (w,h);
               (w - borderwidth, h - borderWidth) -> position; 
            #)
       #);
     width:
       (# w,h: @integer
       do size -> (w,h)
       exit w
       #);
     height:
       (# w,h: @integer
       do size -> (w,h) 
       exit h
       #);
     left: 
       (# P: @point
       do position -> P;
       exit P.H (* I am confused: P.H is the horisontal line and thus top? *)
       #);
     top: (* and vice versa?? *)
       (# P: @point
       do position -> P;
       exit P.V (* And P.V should be left and not top? *)
       #);
     right: (# exit left + width #);
     bottom: (# exit top + height #);
     center: (# exit left + width div 2 #);
     middle: (# exit top + height div 2 #);
     
     theFather: ^canvas;
     CT: @colorTable;
  #)



