ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/controls';
INCLUDE '~beta/guienv/utils/framer';
INCLUDE '~beta/guienv/utils/colorTable';

INCLUDE '~beta/guienv/private/winnt/guienv_ntiprivate';
INCLUDE '~beta/guienv/private/datastructures/sequence';
INCLUDE '~beta/win32lib/wingdiconsts';
INCLUDE '~beta/win32lib/winuserconsts';
INCLUDE '~beta/win32lib/systemmetrics';
INCLUDE '~beta/guienv/utils/guienvadds';

---windowlib:attributes---
LayoutManager:
  (# x, y: @integer;
     width, height: @integer;
     updating: @boolean;
     
     children: @Sequence
       (# element:: SubWindow;
       #);
     init:
       (#
       do children.init;
       #);
     position:
       (#
       enter (x, y)
       #);
     size:
       (#
       enter (width, height)
       #);
     add:
       (# child: ^SubWindow;
       enter child[]
       do child[] -> children.append;
          THIS(LayoutManager)[] -> child.layout[];
       #);
     remove:
       (# child: ^SubWindow;
       enter child[]
       do child[] -> children.delete;
          NONE -> child.layout[];
       #);
     layout:
       (# sum: @integer;
          delta: @integer;
          changed: ^SubWindow;
          last: ^SubWindow;
          cx, cy: @integer;
       enter changed[]
       do true -> updating;
          children.scan
          (# w, h: @integer;
          do current.size -> (w, h);
             sum + w -> sum;
             (if changed[] <> current[] then
                 current[] -> last[];
             if);
          #);
          width - sum -> delta;
          x -> cx;
          y -> cy;
          children.scan
          (# w, h: @integer;
          do (cx, cy) -> current.position;
             current.size -> (w, h);
             (if current[] = last[] then
                 w + delta -> w;
             if);
             (w, height) -> current.size;
             cx + w -> cx;
          #);
          false -> updating;
       #);
  #);
SubWindow: Canvas
  (# layout: ^LayoutManager;
     theBorderWidth: @integer;
     open::<
       (# lab: ^text; T: @text;
          create::
            (#
            do WS_THICKFRAME -> windowItemStyle;
               SM_CXFRAME -> GetSystemMetrics -> theBorderWidth;
            #);
       enter lab[]
       do (12,12) -> position;
          '153 180 209 beta' -> T.putline; 
          '220 220 220 betaExeGrey' -> T.putline; 
          0 -> T.setpos; 
          T[] -> CT.load(# merge::trueObject #);          
          ocBack.open;
          openClose.open;
          head.open;
          lab[] -> head.label;
          INNER;
          setsize;
          true -> head.bindRight;
       #);
     init:<
       (# T: ^text
       enter T[]
       do (theFather[],T[]) -> open;
          inner;
          setSize; (* clean-up: open, init, setsize, etc *)
       #);
     setSize:
       (#
       do openClose.setSize;
          head.setSize;
       #);
     ocBack: @StaticText 
       (* to ensure at BETA colored background behind the open/close button *)
       (# open::
            (# 
            do (16,18) -> size; (* too big for seq-objWs - or? - so fix it *)
               '' -> label;
               'beta' -> CT.lookup -> backgroundColor;
            #)
       #);
     openClose: @openCloseButton;
     openCloseButton:< pushButton
       (# open::<
            (#
            do '*' -> label;;
               setSize
            #);
          setSize: 
            (# 
            do (16,16) -> size; 
            #);
          eventHandler::<
            (# onMouseUp::<
                 (#
                 do inner
                 #)
       #)#);
     Head: @Heading;
     Heading:< StaticText
       (# eventHandler::<
            (# onMouseDown::<
                 (#
                 do (if buttonState = 1 then
                        THIS(SubWindow).bringToFront;
                        trackMouse
                        (# mouseMove:: 
                             (#
                             do (h,v) -> this(SubWindow).move; 
                                true -> this(SubWindow).update
                             #);
                        #);
                     else
                        INNER
                    if);
                 #);
            #);
          open::<
            (# h,w: @integer ; C: @color; 
            do this(subWindow).size -> size -> (w,h); (* why do we do this?
                                                       * See setSize below
                                                       *)
               (16, 0) -> position;
               setSize;
               true -> border.visible;
               borderstyles.etchedOut -> border.style;
               false -> border.visible;
               (*39168,46080,53504) -> backgroundColor; -- does not work *)
               'beta' -> CT.lookup -> C -> backgroundcolor;
               (*  c.red -> putint; ' ' -> put; 
                *                c.green -> putint; ' ' -> put; 
                *                c.blue -> putint; newline;
                *)
               INNER
            #);
          setSize: 
            (# w,h: @integer 
            do this(subWindow).size -> (w,h);
               (w - 16,18) -> size 
            #);
          textWidth:
            (# T: ^text; width: @integer; 
            enter T[]
            do (if T[] = none then
                   'subWindow:TextWidth:T=none'->putline
                else
                   T[] -> (style).widthOfText -> width
               if)
            exit width
            #);
       #);
     width:
       (# w,h: @integer
       do size -> (w,h)
       exit w
       #);
     height:
       (# w,h: @integer
       do size -> (w,h) 
       exit h
       #);
     left: 
       (# P: @point
       do position -> P;
       exit P.H (* I am confused: P.H is the horisontal line and thus top? *)
       #);
     top: (* and vice versa?? *)
       (# P: @point
       do position -> P;
       exit P.V (* And P.V should be left and not top? *)
       #);
     right: (# exit left + width #);
     bottom: (# exit top + height #);
     center: (# exit left + width div 2 #);
     middle: (# exit top + height div 2 #);
     
     eventHandler::<
       (# onFrameChanged::<
            (#
            do (if layout[] <> NONE then
                   (if NOT layout.updating then 
                       THIS(SubWindow)[] -> layout.layout;
                   if);
               if);
            #);
       #);
     
     theFather: ^canvas;
     CT: @colorTable;
  #);



