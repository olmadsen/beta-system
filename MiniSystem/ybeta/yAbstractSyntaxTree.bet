ORIGIN '~beta/basiclib/betaenv';
INCLUDE '../xbeta/Tree';
---lib:attributes---
AbstractSyntaxTree: Tree
  (#  Module:< node
       (# SO: ^StaticObject;
          PT:: 
            (# 
            do (0,length) -> block
               (#
               do (0,break) -> SO.PT 
               #)
            #);
          length:: (# do SO.length -> value #);
          PP:: (# do 'Module' -> lx.append #);
       enter SO[] -> append
       #);
     Items:< node 
       (# PT:: (# do scanSons(# do (pos,break) -> current.PT #)#);
          PP:: (# do 'Items' -> lx.append #);
          isEmpty: @boolean
       #);
     Item: node 
       (# sig: ^Signature
       do inner 
       #);
     Decl: Item
       (# PT::< (# do inner #);
          sig: ^Signature
       #);
     Signature:< node
       (* In xbeta, Signature have subpatterns UnarySignature, etc 
        * Consider including arguments in the Signature
        *)
       (# PP::(# do 'Signature'-> lx.append #);
          S: ^Lexem; (* other arguments, and fatComma are jus appended *)
          id: ^text;
          setId:
            (# 
            do ''-> id[];
               scanSons
               (# B: @boolean; L: ^Lexem
               do (if not B then 
                      current[] -> L[];
                      L.T[] -> id.append;
                  if);
                  not B -> B
               #)
            #)          
       enter id[] -> Lexem -> S[] -> append
       #);          
     StaticObject:< Decl
       (# OG: ^ObjectGeneration;
          (*isSimple:: (# do OG.isSimple -> value #);*)
          PT:: 
            (# 
            do ' ' -> lx.put; '@' -> lx.put; ' ' -> lx.put; 
               (pos + 2,break) -> OG.PT 
            #);
          length:: (# do 3 + OG.length -> value #);
          PP:: (# do 'StaticObject'-> lx.append #)
       enter(sig[] -> append,OG[] -> append)
       #);
     UnaryPattern:< Decl
       (# PP:: (# do 'UnaryPtn:' -> lx.append; sig.id[] -> lx.append #);
          OD: ^ObjectDesc
       enter (sig[]->append,OD[]->append)
       #);
     BinaryPattern:< Decl
       (# PP:: (#do 'BinaryPtn:'-> lx.append; sig.id[]-> lx.append #);
          OD: ^ObjectDesc
       enter(sig[]->append,OD[]->append)
       #);
     FunctionalPattern:< Decl
       (# PP:: (# do 'FunctionalPtn:' -> lx.append; sig.id[] -> lx.append #);
          OD: ^ObjectDesc
       enter(sig[] -> append,OD[]->append)
       #);
     KeywordPattern:< Decl
       (# PP:: 
            (# 
            do 'KeywordPattern:' -> lx.append;  
               sig.id[] -> lx.append
            #);
          OD: ^ObjectDesc
       enter(sig[] -> append, OD[]->append)
       #);
     ObjectTemplate: node
       (# IS: ^Items 
       #);
     ObjectGeneration:< ObjectTemplate
       (# PT:: (# do (if IS[] <> none then (pos,break) -> IS.PT if)#);
          PP:: (# do 'ObjectGeneration' -> lx.append #);
          E: ^Exp
       enter(E[] -> append,IS[] -> append)
       #);
     PatternId:< Node
       (# PP:: (#do 'PatternId'->lx.append #);
          rec: ^Receiver
       #);
     ObjectDesc:< ObjectTemplate
       (# PP:: (# do 'ObjectDesc' -> lx.append #);
          superPI: ^PatternId;
       enter(superPI[] -> append,IS[] -> append)
       #);

     Exp: item 
       (# rec: ^Receiver
       enter rec[] -> append
       #);
     Receiver:< node
       (# PP:: (# do 'Receiver' -> lx.append #);
          isBinary: @Boolean
       #);
     Unary:< Exp
       (# PP:: (# do 'Unary' -> lx.append #);
          M: ^Lexem
       enter M[] -> append
       #);
     Binary:< Exp
       (# PP:: (# do 'Binary' -> lx.append #);
          operator: ^Lexem;
          argument: ^Exp
       enter(operator[]->append, argument[]->append)
       #);
     Function:< Exp
       (# PP:: (# do 'Function' -> lx.append #);
          F: ^Lexem
       enter(lexem->append)
       #);
     KeyWord:< Exp
       (# PP:: (# do 'KeyWord' -> lx.append #)
       #);
     lexem:< Node
       (# PP:: (# do T[] -> lx.append #);
          T: ^text
       enter T[]
       #)     
  do
     inner
  #)
