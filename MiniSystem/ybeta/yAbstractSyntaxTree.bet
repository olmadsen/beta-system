ORIGIN '~beta/basiclib/betaenv';
INCLUDE '../xbeta/Tree';
---lib:attributes---
AbstractSyntaxTree: Tree
  (#  Module:< node
       (# SO: ^StaticObject;
          PT:: 
            (# 
            do (0,length) -> block
               (#
               do (0,break) -> SO.PT 
               #)
            #);
          length:: (# do SO.length -> value #);
          PP:: (# do 'Module' -> lx.append #);
       enter SO[] -> append
       #);
     Items:< node 
       (# PT:: 
            (# 
            do (if props[] <> none then 
                   props.PT;
                   (pos,break) -> mkbreak
               if);
               scanSons
               (# 
               do (pos,break) -> current.PT; 
                  (if not isLast then
                      (pos,break) -> mkbreak
                  if)
               #)
            #);
          PP:: (# do 'Items' -> lx.append #);
          isEmpty: @boolean;
          props: ^Properties
       #);
     Item: node 
       (# sig: ^Signature
       do inner 
       #);
     Properties:< node
       (# PT::
            (#
            do '['->lx.put;
               scanSons
               (#
               do current.pt; (if not isLast then ','->lx.put if) 
               #);
               ']'->lx.put;
            #);
       do
       #);
     Property:< node
       (# PT:: 
            (# 
            do P.pt; (if arg[] <> none then arg.PT if) 
            #);
          P: ^Exp;
          arg: ^Properties
       enter append
       #);
     Decl: Item
       (# PT::< (# do inner #);
          sig: ^Signature
       #);
     Signature:< node
       (* In xbeta, Signature have subpatterns UnarySignature, etc 
        * Consider including arguments in the Signature
        *)
       (# PT:: 
            (# 
            do scanSons
               (#do (pos,break) -> current.PT; 
                  (if not isLast then ' ' -> lx.put if)
               #);
               (if val[] <> none then val.PT if)
            #);
          PP::(# do 'Signature'-> lx.append #);
          S: ^Lexem; (* Arguments, and fatCommas are just appended *)
          val: ^Value;
          id: ^text;
          setId:
            (# 
            do ''-> id[];
               scanSons
               (# B: @boolean; L: ^Lexem
               do (if not B then 
                      current[] -> L[];
                      L.T[] -> id.append;
                  if);
                  not B -> B
               #)
            #)          
       enter id[] -> Lexem -> S[] -> append
       #);     
     Value: node
       (* A list of Decl's *)
       (# PT::
            (#
            do (if son[] <> none then 
                   ' -> ' -> lx.append;
                   scanSons
                   (# 
                   do current.PT; 
                      (if not isLast then 
                          ','-> lx.put;
                          ' '-> lx.put
                      if) 
                   #)
               if)
            #);
          PP:: (# do 'Value' -> lx.append #)
       #);
     StaticObject:< Decl
       (# OG: ^ObjectGeneration;
          (*isSimple:: (# do OG.isSimple -> value #);*)
          PT:: 
            (# 
            do (pos,break) -> sig.PT;
               ':' -> lx.put;
               ' ' -> lx.put; '@' -> lx.put; ' ' -> lx.put; 
               (pos,break) -> OG.PT 
            #);
          length:: (# do 3 + OG.length -> value #);
          PP:: (# do 'StaticObject'-> lx.append #)
       enter(sig[] -> append,OG[] -> append)
       #);
     DynamicObject:< Decl
       (# PI: ^PatternId;
          PT:: 
            (# 
            do (pos,break) -> sig.PT;
               ':' -> lx.put;
               ' ' -> lx.put; '^' -> lx.put; ' ' -> lx.put; 
               (pos,break) -> PI.PT 
            #);
          length:: (# do 3 + PI.length -> value #);
          PP:: (# do 'StaticObject'-> lx.append #)
       enter(sig[] -> append,PI[] -> append)
       #);     
     Pattern: Decl
       (# PT:: 
            (# 
            do (pos,break) -> sig.PT; ':' -> lx.put; ' ' -> lx.put;
               (*(pos+4,true) -> mkBreak;*)
               (pos+4,true) -> OD.PT
            #);
          OD: ^ObjectDesc
       #);
     UnaryPattern:< Pattern
       (# PP:: (# do 'UnaryPtn:' -> lx.append; sig.id[] -> lx.append #);         
       enter (sig[]->append,OD[]->append)
       #);
     BinaryPattern:< Pattern
       (# PP:: (#do 'BinaryPtn:'-> lx.append; sig.id[]-> lx.append #);
       enter(sig[]->append,OD[]->append)
       #);
     FunctionalPattern:< Pattern
       (# PP:: (# do 'FunctionalPtn:' -> lx.append; sig.id[] -> lx.append #);
       enter(sig[] -> append,OD[]->append)
       #);
     KeywordPattern:< Pattern
       (# PP:: 
            (# 
            do 'KeywordPattern:' -> lx.append;  
               sig.id[] -> lx.append
            #);
       enter(sig[] -> append, OD[]->append)
       #);
     ObjectTemplate: node
       (# IS: ^Items 
       #);
     ObjectGeneration:< ObjectTemplate
       (# PT:: 
            (# 
            do E.PT;
               (if not IS.isEmpty then 
                   (pos+4,true) -> mkBreak;
                   (pos+4,true) -> IS.PT;
               if)
            #);
          PP:: (# do 'ObjectGeneration' -> lx.append #);
          E: ^Exp
       enter(E[] -> append,IS[] -> append)
       #);
     PatternId:< Node
       (# PT:: (# do rec.PT; (if id[] <> none then id[] -> lx.append if) #);
          PP:: (#do 'PatternId'->lx.append #);
          rec: ^Receiver;
          id: ^text
       enter rec[] -> append
       #);
     ObjectDesc:< ObjectTemplate
       (# PP:: (# do 'ObjectDesc' -> lx.append #);
          PT:: 
            (#
            do superPI.PT;
               (pos,true) -> mkBreak;
               (pos,break) -> IS.PT 
            #);
          superPI: ^PatternId;
       enter(superPI[] -> append,IS[] -> append)
       #);

     Exp: item 
       (# rec: ^Receiver
       enter rec[] -> append
       #);
     Receiver:< node
       (# PP:: (# do 'Receiver' -> lx.append #);
          PT:: 
            (# do scanSons
               (#do current.PT; 
                  (if not isBinary then '.' -> lx.put 
                   else
                      (if isLast then ' ' -> lx.put if)
                  if)
                  #)
               #);
          isBinary: @Boolean
       #);
     Unary:< Exp
       (# PT:: (# do rec.PT; M.PT #);
          PP:: (# do 'Unary' -> lx.append #);
          M: ^Lexem
       enter M[] -> append
       #);
     Binary:< Exp
       (# PP:: (# do 'Binary' -> lx.append #);
          PT::
            (#
            do (*(pos,break) -> mkBreak;*)
               rec.PT; 
               operator.PT; ' '-> lx.put;
               argument.PT
            #);
          operator: ^Lexem;
          argument: ^Exp
       enter(operator[]->append, argument[]->append)
       #);
     Function:< Exp
       (# PP:: (# do 'Function' -> lx.append #);
          PT:: (# do scanSons(#do current.PT; (if not isLast then ' ' -> lx.put if)#)#);
          F: ^Lexem
       enter(lexem->append)
       #);
     KeyWord:< Exp
       (# PP:: (# do 'KeyWord' -> lx.append #);
          PT:: 
            (# first: @boolean
            do (* first element in sons is receiver - may be empty *)
               true -> first;
               scanSons
               (#do current.PT;
                  (if not isLast and not first then ' ' -> lx.put if);
                  false -> first
               #)
            #)
       #);
     
     Name:< Exp
       (# PT:: (# do N.T[] -> lx.append #);
          PP:: (# do N.T[] -> lx.append #);
          N: ^Lexem
       enter Lexem -> N[] -> append
       #);
     Textt:< Exp
       (# PT::< (# do '\'' -> lx.put;  T.T[] -> lx.append; '\'' -> lx.put #);
          PP:: (# do T.T[] -> lx.append #);
          T: ^Lexem
       enter Lexem -> T[] -> append
       #);
     Const:< Exp
       (# PT:: (# do C.T[] -> lx.append #);
          PP:: (# do C.T[] -> lx.append #);
          C: ^Lexem  
       enter Lexem -> C[] -> append
       #);
     Lexem:< node
       (# PT:: (# do T[] -> lx.append #);
          PP:: (# do T[] -> lx.append #);          
          T: ^text
       enter T[]
       #)     
  do
     inner
  #)
