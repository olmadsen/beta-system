ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/file';
INCLUDE '~beta/MiniSystem/qbeta/qprint';
---lib:attributes---

(********************* the sub-pattern used for printing RTF **********************)
qHTMLprint : qPrint
(#

  begBlock ::
    (# brdrClr, bckgClr :@ Text;
       begDiv1 : (# do  '<div style="width:80ch;'  ->  out.puttext  #);
       begDiv  : (# do  '<div style="width:auto;'  ->  out.puttext  #);
       indentDiv : 
         (# do 
         (if indent < 0 then 3 -> indent if); (* hack: if multiple complete statements on one line *)
         'padding:0ch;'  ->  out.puttext;
         'margin:0ch '   ->  out.puttext;
         indent          ->  out.putint;
         'ch;'           ->  out.puttext;
         #);
       borderDiv : (# do 'border:1px solid ' -> out.puttext; borderTbl[blkLvl][] -> out.puttext; ';' -> out.put #);
       bckgrnDiv : (# do 'background-color:' -> out.puttext; colorTbl[blkLvl][] ->  out.puttext; ';' -> out.put #);
       endDiv    : (# do  '">'  ->  out.putline;  #);
    do
       (if blkLvl = 1 then begDiv1 else begDiv if);
       indentDiv;
       borderDiv;
       bckgrnDiv;
       endDiv;
    #);
  
  endBlock   ::
    (# do
    '</div>' -> out.putline;
    #); 
  

  begBackGround :: (#  do (* nothing *)  #);
  endBackGround :: (#  do (* nothing *)  #);


  begUnderline ::  (# do '<span style="text-decoration: underline;">'  ->   out.puttext #);
  endUnderline ::  (# do '</span>' ->  out.puttext #);

  begBold ::  (# do '<b>' ->   out.puttext #);
  endBold ::  (# do '</b>' ->  out.puttext #);

  begItalics ::  (# do '<i>' ->   out.puttext #);
  endItalics ::  (# do '</i>' ->  out.puttext #);

  begSmallCaps ::  (# do  '<span id="scaps">'  ->  out.puttext  #);
  endSmallCaps ::  (# do  '</span>'            ->  out.puttext  #);

  begColor ::  
    (# do '<span style="color:' -> out.puttext;
          colorTbl[clr][] -> out.puttext;
          ';">' -> out.puttext; 
    #);
  endColor ::  (# do  '</span>' ->  out.puttext; #);

  (* reduced text size *)
  begCmt ::  (# do '<span style="font-size: 0.875em;">' ->  out.puttext;  #);    
  endCmt ::  (# do '</span>' ->  out.puttext;  #);    

  constant   ::  (# do ':=' ->   out.puttext #);
  variable   ::  (# do ':?' ->   out.puttext #);
  pattern    ::  (# do ':'  ->   out.puttext #);
  virtual    ::  (# do ':<' ->   out.puttext #);
  further    ::  (# do '::<' ->  out.puttext #);
  final      ::  (# do '::' ->   out.puttext #);
  assignment ::  (# do ':-' ->   out.puttext #);
  special    :: (# do specialColor    -> begColor; t[] -> stripPercent -> Txt; endColor; #); 
  reserved   :: (# do reservedColor   -> begColor; t[] -> stripPercent -> Txt; endColor; #);    
  preDefined :: (# do predefinedColor -> begColor; t[] -> stripPercent -> Txt; endColor; #); 
  keyword    :: (# do keywordColor    -> begColor; t[] -> stripPercent -> Txt; endColor; #); 
  fatcomma   :: (# do fatcommaColor   -> begColor; t[] -> stripPercent -> Txt; endColor; #); 
    
  

  Spaces :: (# do  (for i: count repeat '&nbsp;' -> out.puttext for)  #);
  
  Chr ::
  (# do (if true
         // C ='<' then (* '<' MUST be escaped in HTML *)
            '&lt;'  -> out.puttext;
         // C ='&' then (* '&' MUST be escaped in HTML *)
            '&amp;' -> out.puttext;
         // C ='>' then (* '>' should be escaped in HTML *)
            '&gt;'  -> out.puttext;
         // C ='"' then (* '"' should be escaped in HTML *)
            '&quot;' -> out.puttext;
         // C =' ' then (* ' ' should be escaped in HTML *)
            '&nbsp;' -> out.puttext;
        else
            C -> out.put;
        if);
  #);    

  UniChr ::  (#  i :@ integer  enter  i
             do '&#' -> out.puttext;  i -> out.putint;   ';' -> out.put;  #);
     
  setTabStops ::  (# do  (* do nothing *)  #);
   
  (* output a tab and a space at the very end of this line *)
  tabToEOL ::  (# do  (* do nothing *)  #);

  tab ::  (# do  (* do nothing *)  #);

  lineShift ::  (# do  '<br>'-> out.puttext  #);

  readableLine:: (# do (* out.newline *) #);   (* a no-op, only purpose is to make the output more readable *)

  landscape ::  (# do  (* do nothing *)  #);


  colorTbl  : [25] ^Text;
  borderTbl : [10] ^Text;
     
init ::
  (#
     initColors : (# b, c, stride :@ integer;
                  enter (c,stride)
                  do 
                     (* Set up the tables for border colors and background colors. *)
                     (for i : 10 repeat
                          c - stride -> b;
                          &Text[] -> borderTbl[i][];
                          'rgb(' -> borderTbl[i].append;
                          b -> borderTbl[i].putint; ',' -> borderTbl[i].put;
                          b -> borderTbl[i].putint; ',' -> borderTbl[i].put;
                          b -> borderTbl[i].putint; ')' -> borderTbl[i].put;
                          b - stride -> b;
                          &Text[] -> colorTbl[i][];
                          'rgb(' -> colorTbl[i].append;
                          c -> colorTbl[i].putint; ',' -> colorTbl[i].put;
                          c -> colorTbl[i].putint; ',' -> colorTbl[i].put;
                          c -> colorTbl[i].putint; ')' -> colorTbl[i].put;
                          c - stride -> c;
                     for);
                     (* Set up other colors. *)
                     &Text[] -> colorTbl[11][]; 'rgb(0,50,0)'     -> colorTbl[11].append;
                     &Text[] -> colorTbl[12][]; 'rgb(0,0,130)'    -> colorTbl[12].append;
                     &Text[] -> colorTbl[13][]; 'rgb(165,42,42)'  -> colorTbl[13].append;
                     &Text[] -> colorTbl[14][]; 'rgb(65,105,225)' -> colorTbl[14].append;
                     &Text[] -> colorTbl[15][]; 'rgb(120,0,128)'  -> colorTbl[15].append;
                     &Text[] -> colorTbl[16][]; 'rgb(70,130,180)' -> colorTbl[16].append;
                     &Text[] -> colorTbl[17][]; 'rgb(0,139,139)'  -> colorTbl[17].append;
                     &Text[] -> colorTbl[18][]; 'rgb(50,80,80)'   -> colorTbl[18].append;
                     &Text[] -> colorTbl[19][]; 'rgb(210,105,30)' -> colorTbl[19].append;
                     &Text[] -> colorTbl[20][]; 'rgb(255,127,80)' -> colorTbl[20].append;
                     &Text[] -> colorTbl[21][]; 'rgb(255,140,0)'  -> colorTbl[21].append;
                     &Text[] -> colorTbl[22][]; 'rgb(0,0,205)'    -> colorTbl[22].append;
                  #);
  do
     (***   Set up the HTML Header   ***)
     '<!DOCTYPE html>' -> out.putline; 
     '<html>' -> out.putline; 
     '<head>' -> out.putline;
     '<title>Source code formatted as HTML.</title>'  ->  out.putline;
     '<meta name="viewport" content="width=85ch, "min-width=25ch, initial-scale=1.0">'  ->  out.putline;
     '<style>' -> out.putline;
     (* Set Gogle's NoTo as default font, with font size 12. *)
     'body { font-family: "NoTo Mono", "Lucida Console", Monospace; font-size: 120%; }' -> out.putline;
     (* Define foreground and background color for each block level. *)
     (245,12)  -> initColors;
     '#scaps {  font-variant: small-caps;  }' -> out.putline;
     '</style>' -> out.putline;
     '</head>' -> out.putline;
     '<body>' -> out.putline;
     
     (* set up document header and footer *)
     '<header> ' -> out.putline;
         lineShift;
         8 -> Spaces;
         'File:' -> out.puttext;
         3 -> Spaces;
         '<span style="color:blue;font-size:150%">' -> out.puttext;
         inFarg[] -> out.putline;
         '</span>' -> out.puttext;
         lineShift;
         'In directory:' -> out.puttext;
         3 -> Spaces;
         '<span style="color:green;">' -> out.puttext;
         inFdir[] -> out.putline;
         '</span>' -> out.puttext;
         lineShift;
         '</header>' -> out.putline;
     '<footer>' -> out.putline; 
         2 -> Spaces;
         'Printed on:' -> out.puttext;
         3 -> Spaces;
         '<span style="color:brown;">' -> out.puttext;
         timeNow -> out.putline;
         '</span>' -> out.puttext;
         lineShift;
         2 -> Spaces;
         'Last saved:' -> out.puttext;
         3 -> Spaces;
         '<span style="color:brown;">' -> out.puttext;
         timeMod -> out.putline;
         '</span>' -> out.puttext;
         lineShift;
         lineShift;
         lineShift;
         '</footer>' -> out.putline;

     (1,1) -> begBlock;

  #); (* end init *)

  finis ::
  (# do
     endBlock;
     '</body>' -> out.putline;
     '</html>' -> out.putline;
  #);
  


#);