ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/basiclib/textUtils';
--- program: descriptor ---
(#
   StateMachine: 
     (# State: 
          (# encl: ^StateType;
             entry:< (# DO this(State)[] -> cs[]; INNER; #);
             display:< (# do 'State='-> puttext; inner #)
          #);
        StateType:< State;
        cs: ^StateType;
     #);
   Switch: StateMachine 
     (# StateType::< SwitchState;
        onEvent: (# DO cs.on #);
        offEvent: (# DO cs.off #);
        SwitchState: State 
          (# on:< (# DO INNER #);
             off:< (# DO INNER #);
          #);
        OffState: SwitchState 
          (# on::< (# DO OnS.entry; INNER; #);
             entry::< (# DO this(OffState)[]-> cs[]; display  #);
             display:: (# do 'OffState: ' -> puttext; #)
          #);
        OnState: SwitchState 
          (# off::< (# DO OffS.entry; INNER; #);
             entry::< (# DO this(OnState)[]-> cs[]; display #);
             display:: (# do 'OnState: ' -> puttext; #)
          #);
        OffS: ^StateType;
        OnS: ^StateType;
        init:
          (#
          DO &OnState[] -> OnS[];
             &OffState[] -> OffS[];
             OffS[] -> cs[];
          #);
     #);
   MediaSwitch: Switch 
     (# StateType::< MediaSwitchState;
        MediaSwitchState: SwitchState
          (# mode:< (# DO INNER; #);
          #);
        modeEvent: (# DO cs.mode; #);
        MSOffState: MediaSwitchState 
          (# on::< (# DO OnS.entry; INNER; #);
             entry::< (# DO this(MSOffState)[]-> cs[]; display #);
             display:: (# do 'MSOffState: ' -> puttext; #)
          #);
        MSOnState: MediaSwitchState 
          (# off::< (# DO OffS.entry; INNER; #);
             mode:< (# DO INNER; #);
             entry:: <(# DO CD.entry; INNER; display #);
             display:: (# do 'MSOnState: ' -> puttext; #)
          #);
        CDState: MediaSwitchState 
          (# entry::< (# DO this(CDState)[]-> cs[]; display #);
             on::< (# DO encl.on; #);
             off::< (# DO encl.off; #);
             mode::< (# DO TV.entry; #);
             display:: (# do 'CDState: ' -> puttext; #)
          #);
        TVState: MediaSwitchState 
          (# entry::< (# DO this(TVState)[]-> cs[]; display #);
             on::< (# DO encl.on; #);
             off::< (# DO encl.off; #);
             mode::< (# DO CD.entry; #);
             display:: (# do 'TVState: ' -> puttext; #)
          #);
        CD:^CDState;
        TV:^TVState;
        init: 
          (#
          DO &MSOnState[] -> OnS[];
             &MSOffState[] -> OffS[];
             OffS[] -> cs[];
             &CDState[] -> CD[];
             &TVState[] -> TV[];
             
             OnS[] -> CD.encl[];
             OnS[] -> TV.encl[];
          #)
     #);
   myMS: @ MediaSwitch;
DO myMS.init;
   'Turn on\t\t: ' -> puttext;
   myMS.onEvent; 
   '\nTurn off\t: ' -> puttext;
   myMS.offEvent; 
   '\nTurn on\t\t: ' -> puttext;
   myMS.onEvent; 
   '\nChange mode 1\t: ' -> puttext;
   myMS.modeEvent; 
   '\nChange mode 2\t: ' -> puttext;
   myMS.modeEvent; 
   '\nTurn off\t: ' -> puttext;
   myMS.offEvent; 
   '\nEnd\t: ' -> puttext
#)
