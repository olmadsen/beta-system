ORIGIN '~beta/basiclib/systemenv';
INCLUDE '~beta/process/processmanager';
INCLUDE '~beta/process/streamgenerator';
---systemlib:attributes---
Service:
  (#	
     application: @process;
     inSocket: ^StreamSocket;
     Generator: @socketGenerator;
     aText: ^Text;
     init:<
       (# aplName: ^text
       enter aplName[]
       do	
          'Launching \'Java\'' -> putline;
          'C:\\Windows\\system32\\java' ->	application.init;
          '-jar '-> application.argument.putArg;
          '.jar' ->aplName.append -> application.argument.putArg;
          aplName[] -> application.argument.putArg;
          application.start; 
     
          'Communicate via socket using number 5123' -> putline;
          5123 -> Generator.bind; 
          
          'Establishing connection: ' -> putTEXT;
          waitForever -> Generator.getStreamConnection -> inSocket[];
          'DONE'-> screen.putline
       #);
     test:
       (#
       do
          (for i: 10 repeat 
               (# T: @text
               do 'Mesage: ' -> T; i -> T.putint; T.newline;
                  T[] -> inSocket.putText;
               #)
          for);
          inSocket.newline;
          inSocket.newline;
          (* inSocket.flush is done automatically by inSocket.newline *)
       #);
     mkCommand:
       (# no: @integer;  cmd: @text;
       enter no
       do no -> cmd.putint; ' ' -> cmd.put;
          inner;
          cmd.newline;
          cmd[] -> send
       #);
          
     send:
       (# cmd: ^text
       enter cmd[]
       do cmd[] -> inSocket.putText;
       #);
     close:<
       (#
       do
          'Closing down' -> putline;
          application.awaitStopped; (* client first: avoids address-in-use error *)
          inSocket.close;
          Generator.close;
       #)
  #)

