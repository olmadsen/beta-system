ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '~beta/process/processmanager';
INCLUDE '~beta/process/streamgenerator';
---systemlib:attributes---
Service:
  (#	
     application: @process;
     inSocket: ^StreamSocket;
     Generator: @socketGenerator;
      
     aText: ^Text;
     init:<
       (# aplName: ^text; socketNo: @integer
       enter(aplName[],socketno)
       do	
          'Launching \'Java\'' -> putline;
          'C:\\Windows\\system32\\java' ->	application.init;
          '-jar '-> application.argument.putArg;
          '.jar' ->aplName.append -> application.argument.putArg;
          aplName[] -> application.argument.putArg;
          application.start; 

          'Communicate via socket using number: ' -> puttext; socketNo -> putint; newline;
          socketNo -> Generator.bind;
          'Establishing connection: ' -> putTEXT;
          waitForever -> Generator.getStreamConnection -> inSocket[];
          'DONE'-> screen.putline;
       #);
     openSocket:
       (# socketNo: @integer
       enter socketNo
       do socketNo -> Generator.bind;                    
          'Establishing connection: ' -> putTEXT; socketNo -> putint; newline;
          waitForever -> Generator.getStreamConnection -> inSocket[];
          socketNo -> putint; '>>DONE'-> screen.putline;
       #);
     test:
       (#
       do
          (for i: 10 repeat 
               (# T: @text
               do 'Mesage: ' -> T; i -> T.putint; T.newline;
                  T[] -> inSocket.putText;
               #)
          for);
          inSocket.newline;
          inSocket.newline;
          (* inSocket.flush is done automatically by inSocket.newline *)
       #);
     mkCommand:
       (# no: @integer;  cmd: @text;
       enter no
       do no -> cmd.putint; ' ' -> cmd.put;
          inner;
          ' '  -> cmd.put; '!' -> cmd.put; (* sometimes a zero is wirtten before nl
                                            * this may confuse JavaInt *)
          cmd.newline;
          'BETA:mkCmd: "' -> puttext; cmd[] -> puttext; '"' -> put; newline;
          cmd[] -> send
       #);
          
     send:
       (# cmd: ^text
       enter cmd[]
       do 
          'BETA:sending: "'->puttext; cmd[] -> puttext; '"' -> put; newline;
          cmd.scanAll(# do ch -> putint; ','->put #); newline;
          (for i: 100000 repeat for);
          sends + 1 -> sends;
          cmd[] -> inSocket.putText;
       #);
     sends: @integer;
     receive:
       (# ans: ^text
       do (* getLine may imply a suspend of theActive System - through a Semaphore *)
          inSocket.getLine -> ans[];
          sends - 1 -> sends
       exit ans[]
       #);
     close:<
       (# noApp: @boolean
       enter noApp
       do
          'Closing down' -> putline;
          (if not noApp then
              '0\n' -> send;
              application.awaitStopped; (* client first: avoids address-in-use error *)
          if);
          inSocket.close;
          (if not noApp then Generator.close if);
       #)
  #)

