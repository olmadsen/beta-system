ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '~beta/process/processmanager';
INCLUDE '~beta/process/streamgenerator';
---systemlib:attributes---
Service:
  (# trace: @
       (# TT: (# exit false #);
          msg: 
            (# T: ^text
            enter T[]
            do (if TT then T[] -> putline if);
            #);
          val: (# V: @integer enter V do (if TT then V -> putint if)#);
          nl: (# do (if TT then newline if)#);
       exit TT
       #);
     
     application: @process;
     inSocket: ^StreamSocket;
     Generator: @socketGenerator;
      
     aText: ^Text;
     init:<
       (# aplName: ^text; socketNo: @integer
       enter(aplName[],socketno)
       do 	
          'Launching \'Java\'' -> trace.msg;
          'C:\\Windows\\system32\\java' -> application.init;
          '-jar '-> application.argument.putArg;
          '.jar' ->aplName.append -> application.argument.putArg;
          aplName[] -> application.argument.putArg;
          application.start; 

          'Communicate via socket using number: ' -> trace.msg; socketNo -> trace.val; trace.nl;
          socketNo -> Generator.bind;
          'Establishing connection: ' -> trace.msg;
          waitForever -> Generator.getStreamConnection -> inSocket[];
          'DONE'-> trace.msg
       #);
     openSocket:
       (# socketNo: @integer
       enter socketNo
       do socketNo -> Generator.bind;                    
          'Establishing connection: ' -> trace.msg; socketNo -> trace.val; trace.nl;
          waitForever -> Generator.getStreamConnection -> inSocket[];
          socketNo -> trace.val; '>>DONE'-> trace.msg
       #);
     test:
       (#
       do
          (for i: 10 repeat 
               (# T: @text
               do 'Mesage: ' -> T; i -> T.putint; T.newline;
                  T[] -> inSocket.putText;
               #)
          for);
          inSocket.newline;
          inSocket.newline;
          (* inSocket.flush is done automatically by inSocket.newline *)
       #);
     mkCommand:
       (# no: @integer;  cmd: @text;
       enter no
       do no -> cmd.putint; ' ' -> cmd.put;
          inner;
          cmd.newline;
         (* 'BETA:mkCommand: "' -> puttext; cmd[] -> puttext; '"' -> put; newline;*)
          cmd[] -> send
       #);
          
     send:
       (# cmd: ^text
       enter cmd[]
       do 'BETA:sending: "'->trace.msg; cmd[] -> trace.msg; '"' -> trace.msg; trace.nl;
          sends + 1 -> sends;
          cmd[] -> inSocket.putText;
       #);
     sends: @integer;
     receive:
       (# ans: ^text
       do (* getLine may imply a suspend of theActive System - through a Semaphore *)
          inSocket.getLine -> ans[];
          (if trace.TT then 
              'BETA:receive: ' -> puttext; ans[] -> putline
          if);
          sends - 1 -> sends
       exit ans[]
       #);
     close:<
       (# noApp: @boolean
       enter noApp
       do
          'Closing down' ->trace.msg;
          (if not noApp then
              '0\n' -> send;
              application.awaitStopped; (* client first: avoids address-in-use error *)
          if);
          inSocket.close;
          (if not noApp then Generator.close if);
       #)
  #)

