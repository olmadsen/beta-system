ORIGIN '~beta/basiclib/systemenv';
INCLUDE '~beta/process/processmanager';
INCLUDE '~beta/process/streamgenerator';
---systemlib:attributes---
Service:
  (#	
     application: @process;
     inSocket: ^StreamSocket;
     Generator: @socketGenerator;
     aText: ^Text;
     init:<
       (# aplName: ^text; socketNo: @integer
       enter(aplName[],socketno)
       do	
          'Launching \'Java\'' -> putline;
          'C:\\Windows\\system32\\java' ->	application.init;
          '-jar '-> application.argument.putArg;
          '.jar' ->aplName.append -> application.argument.putArg;
          aplName[] -> application.argument.putArg;
          application.start; 

          'Communicate via socket using number: ' -> puttext; socketNo -> putint; newline;
          socketNo -> Generator.bind; 
          
          'Establishing connection: ' -> putTEXT;
          waitForever -> Generator.getStreamConnection -> inSocket[];
          'DONE'-> screen.putline;
       #);
     openSocket:
       (# socketNo: @integer
       enter socketNo
       do socketNo -> Generator.bind;                    
          'Establishing connection: ' -> putTEXT; socketNo -> putint; newline;
          waitForever -> Generator.getStreamConnection -> inSocket[];
          socketNo -> putint; '>>DONE'-> screen.putline;
       #);
     test:
       (#
       do
          (for i: 10 repeat 
               (# T: @text
               do 'Mesage: ' -> T; i -> T.putint; T.newline;
                  T[] -> inSocket.putText;
               #)
          for);
          inSocket.newline;
          inSocket.newline;
          (* inSocket.flush is done automatically by inSocket.newline *)
       #);
     mkCommand:
       (# no: @integer;  cmd: @text;
       enter no
       do no -> cmd.putint; ' ' -> cmd.put;
          inner;
          cmd.newline;
          'BETA:mkCmd: ' -> puttext; cmd[] -> puttext;
          cmd[] -> send
       #);
          
     send:
       (# cmd: ^text
       enter cmd[]
       do cmd[] -> inSocket.putText(# blocking::(# do true -> continue #)#);
          'BETA:sending: '->puttext; cmd[] -> puttext
       #);
     receive:
       (# ans: ^text
       do inSocket.getLine -> ans[] -> putline;
       exit ans[]
       #);
     close:<
       (#
       do
          'Closing down' -> putline;
          '0\nl' -> send;
          application.awaitStopped; (* client first: avoids address-in-use error *)
          inSocket.close;
          Generator.close;
       #)
  #)

