(* File timeClient.bet
 * ===================
 * 
 * Example implementation of a timeClient shell. The textString received
 * from the timeServer is simply displayed in a motif label widget.
 * 
 * This timeClient takes the name of the ensemble on which a timeServer
 * is expected to reside as a command line parameter. It then gets hold
 * of a reference to that ensemble using its local ensemble (myEnsemble), 
 * and checks whether a timeServer is actually present at that ensemble. 
 * If so, it registers with that server. Default serverEnsemble is the 
 * local ensemble. *)

ORIGIN '~beta/distribution/v1.0/xshell';
(* 
 * $RCSfile: timeClient.bet,v $ $Revision: 1.1.1.1 $ $Date: 1994-08-22 08:37:07 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- include '~beta/Xt/v1.8/motifenv'
--- include '~beta/Xt/v1.8/motif/label'
--- include 'timeLib'

--- program:descriptor ---

shellEnv
(# shellType::< timeClient
     (# updateClockImpl::< (# do timeNow[] -> mymotif.clock.labelString #);
        
        server: ^timeServer;
        serverEnsemble: ^ensemble;
        sename: ^Text;
        
     do 
        (if NoOfArguments < 2 //true then
            myEnsemble[] -> serverEnsemble[];
            myEnsemble.hostName[] -> sename[];
         else
            2 -> arguments -> sename[];
            (ensemble##, sename[]) -> myEnsemble.ns.get 
            (# notFound::< 
                 (#
                 do sename[] -> screen.putText;
                    ' ensemble not found' -> screen.putLine;
                    kill;
                 #)
            #) -> serverEnsemble[];
        if);
        
        (timeServer##, 'timeServer') -> serverEnsemble.ns.get 
        (# notFound::< 
             (#
             do 'No timeServer present on ' -> screen.putText;
                sename[] -> screen.putLine;
                kill;
             #)
        #) -> server[];
        
        errorHandler
        (# timeOutValue::< (# do 5 -> sec #)
        do theShell[] -> server.registerClient;
        #)
     #);
   
   setWindowEnv::< (# do mymotif[] -> theWindowEnv[] #);
   
   mymotif: @motifEnv
     (# clock: @label (# init::< (# do true -> recomputeSize #)#);
     do clock.init;
        true -> toplevel.allowShellResize;
     #);
#)
