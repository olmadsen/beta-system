ORIGIN '~beta/distribution/v1.6/guienvshell';
INCLUDE '~beta/guienv/v1.6/controls';
INCLUDE 'timelib';

(* File timeClient.bet
 * ===================
 * 
 * Example implementation of a timeClient shell. The textString received
 * from the timeServer is simply displayed in a guienv label widget.
 * 
 * This timeClient takes the name of the ensemble on which a timeServer
 * is expected to reside as a command line parameter. It then gets hold
 * of a reference to that ensemble using its local ensemble (myEnsemble), 
 * and checks whether a timeServer is actually present at that ensemble. 
 * If so, it registers with that server. Default serverEnsemble is the 
 * local ensemble. *)

--- program:descriptor ---

shellEnv
(# shellType:: timeClient
     (# updateClockImpl:: (# do timeNow[] -> myguienv.setClock #);
        
        server: ^timeServer;
        serverEnsemble: ^ensemble;
        sename: ^Text;
        
        quit: (# do theShell[] -> server.withDrawClient #);
        
     do 
        (if NoOfArguments < 2 then
            myEnsemble[] -> serverEnsemble[];
            myEnsemble.hostName[] -> sename[];
         else
            2 -> arguments -> sename[];
            (ensemble##, sename[]) -> myEnsemble.ns.get 
            (# notFound:: 
                 (#
                 do sename[] -> screen.putText;
                    ' ensemble not found' -> screen.putLine;
                    kill;
                 #)
            #) -> serverEnsemble[];
        if);
        
        (timeServer##, 'timeServer') -> serverEnsemble.ns.get 
        (# notFound:: 
             (#
             do 'No timeServer present on ' -> screen.putText;
                sename[] -> screen.putLine;
                kill;
             #)
        #) -> server[];
        
        errorHandler
        (# timeOutValue:: (# do 5 -> sec #)
        do theShell[] -> server.registerClient;
        #)
     #);
   
   setWindowEnv:: (# do myguienv[] -> theWindowEnv[] #);
   
   myguienv: @guienv
     (# txtWindow: @window
          (# txtField: @staticText
               (# open:: 
                    (# 
                    do (0, 0)->position;
                       (300, 50)->size;
                    #);
               #);
             open::
               (# 
               do (300, 50)->size;
                  'timeClient'->title;
                  txtField.open
               #);
             eventhandler::
               (# onAboutToClose::
                    (# 
                    do theShell.quit;
                       terminate 
                    #) 
               #);
             
          #);
        setClock:
          (# t: ^Text;
          enter t[]
          do t[]->txtWindow.txtField.label;
          #);
     do txtWindow.open
     #)
#)
