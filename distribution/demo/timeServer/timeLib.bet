(* File timeLib.bet
 * ================
 * 
 * $RCSfile: timeLib.bet,v $ $Revision: 1.1.1.1 $ $Date: 1994-08-22 08:37:07 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *
 * This fragment contains the definition of the shellTypes
 * 
 *    timeServer & timeClient
 *    =======================
 * 
 * The timeServer maintains a list of clients whose updateClock
 * operation is called once each second with a textstring describing
 * the current wall-clock time.
 * 
 * A new timeServer registers itself with its local ensemble (myEnsemble)
 * using the name 'timeServer'. 
 * 
 * New timeClients may thus lookup a timeServer and register themselves
 * using the timeServer operation 'registerClient'. Having done this, the
 * timeServer will call the updateClock operation of the timeClient once
 * each second. When a timeClient is done using the server, it may call
 * the 'withdrawClient' operation of the server to make the server forget
 * about it.
 * 
 * Example implementations of timeServer and timeClient may be found in the
 * files timeServer.bet and timeClient.bet. *)

ORIGIN '~beta/distribution/v1.0/basicshell'

--- shellEnvLib:attributes ---

(* TIMESERVER
 * ========== *)

timeServer: shell
  (# 
     (* Interface specification:
      * ======================== *)
     
     registerClient: entry
       (# theClient: ^timeClient;
       enter theClient[]
       do theClient[] -> registerClientImpl;
       #);
        
     withdrawClient: entry
       (# theClient: ^timeClient;
       enter theClient[]
       do theClient[] -> withdrawClientImpl;
       #);
     
     
     (* Virtual implementations:
      * ======================== *)
     
     registerClientImpl:<
       (# theClient: ^timeClient;
       enter theClient[]
       do INNER
       #);
        
     withdrawClientImpl:<
       (# theClient: ^timeClient;
       enter theClient[]
       do INNER
       #);
  do
     INNER;
     (THIS(timeServer)[], 'timeServer') -> myEnsemble.ns.put
     (# overWrite::< (# do true -> value #)#);
  #);


(* TIMECLIENT
 * ========== *)

timeClient: shell
  (# 
     (* Interface specification:
      * ======================== *)
     
     updateClock: entry
       (# timeNow: ^Text
       enter timeNow[]
       do timeNow[] -> updateClockImpl; none -> timeNow[];
       #);
     
     (* Virtual implementation:
      * ======================= *)
     
      updateClockImpl:<
	(# timeNow: ^Text
	enter timeNow[]
	do INNER
	#);
     
  do INNER
  #);
