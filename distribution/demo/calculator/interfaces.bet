(* File calculator/interfaces.bet
 * ============================== 
 * 
 * This fragment describes the interfaces between a simple
 * calculating server and its clients.
 * 
 * The programs calcServer, calcClient and XcalcClient are 
 * example implementations of these interfaces. *)

(* As we don't want to choose here whether programs implementing
 * or using these interfaces should be X programs or not, we must 
 * have origin in basicshell: *)

ORIGIN '~beta/distribution/v1.3/basicshell'

--- shellEnvLib:attributes ---

(* CALCULATOR
 * ==========
 * 
 * A calculator performs various calculations on numbers.
 * 
 * double multiplies the value entered with 2.
 * 
 * exp calculates value times value.
 * 
 * factorial calculates value! calling foreach for each
 * step in the calculation. *)

calculator: remoteable
  (# 
     (* Interface used by clients:
      * ========================== *)
     
     double: entry
       (# value: @Integer;
       enter value
       do value -> doubleImpl -> value; 
       exit value
       #);
     
     exp: entry
       (# value: @Integer;
       enter value
       do value -> expImpl -> value
       exit value
       #);
     
     factorial: entry
       (# N, value: @Integer;
          foreach: ^valueCallback;
       enter (N,foreach[])
       do (N,foreach[]) -> factorialImpl -> value
       exit value
       #);
   
     (* Implementation virtuals:
      * ========================
      * 
      * These virtuals should be furtherbound by the program 
      * or library implementing the calculator functionality. *)
   
     doubleImpl:<
       (# value: @Integer;
       enter value
       do INNER
       exit value
       #);
   
     expImpl:<
       (# value: @Integer
       enter value
       do INNER
       exit value
       #);
     
     factorialImpl:<
       (# N, value: @Integer;
          foreach: ^valueCallback;
       enter (N,foreach[])
       do INNER
       exit value
       #);
  #);

(* VALUECALLBACK
 * =============
 * 
 * used in the factorial method of calculator. *)

valueCallback: remoteAble
  (# callback: entry
       (# value: @Integer
       enter value
       do value -> callbackImpl
       #);
     
     callbackImpl:<
       (# value: @Integer;
       enter value
       do INNER
       #);
  #);

(* CALCSERVER
 * ==========
 * 
 * The shell containing some calculators.
 * 
 * newCalc is called by clients to get a reference to a 
 * new calculator.
 * 
 * calcDone is called by the client when it is done using
 * the calculator. *)

calcServer: shell
  (#
     newCalc: entry
       (# theCalc: ^calculator;
       do newCalcImpl -> theCalc[]
       exit theCalc[] 
       #);
     
     calcDone: entry
       (# theCalc: ^calculator;
       enter theCalc[]
       do theCalc[] -> calcDoneImpl
       #);
     
     newCalcImpl:< 
       (# theCalc: ^calculator;
       do INNER
       exit theCalc[]
       #);
     
     calcDoneImpl:<
       (# theCalc: ^calculator;
       enter theCalc[]
       do INNER
       #);
  do 
     (THIS(calcServer)[],'calcServer') -> myEnsemble.ns.put
     (# overWrite:: (# do true -> value #)#);
  #);
