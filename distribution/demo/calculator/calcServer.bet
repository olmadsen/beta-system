(* File calcServer.bet
 * ===================
 * 
 * This a simple implementation of a calcServer.
 * The interfaces between the server and the clients are described in the
 * file interfaces.bet. *)

ORIGIN '~beta/distribution/v1.6/shell';
INCLUDE 'interfaces';
INCLUDE '~beta/basiclib/v1.6/timehandler';

--- program:descriptor ---
shellEnv 
(# 
   shellType:: calcServer 
     (# newCalcImpl:: 
          (# 
          do &serverCalc[] -> theCalc[];
             onTimeOutId -> timer.unregister;
             (onTimeOut[],5*60) -> timer.register -> onTimeOutId;
          #);
        calcDoneImpl::
          (# 
          do theCalc[] -> withdraw;
          #);
     #);
   
   servercalc: calculator
     (# doubleImpl:: 
          (# 
          do value*2 -> value;
          #);
        expImpl:: 
          (# 
          do value*value -> value;
          #);
        factorialImpl:: 
          (# 
          do
             1 -> value;
             (for i: N repeat
                  value*i -> value;
                  value -> foreach.callback;
             for)
          #);
     #);
   
   timer: @timeHandler;
   onTimeOutId: @Integer;
   
   onTimeOut: @
     (* This object is called to kill the server if no new calculators
      * have been created for 5 minutes. The reason is that we don't want 
      * this simple example server to stay around forever. 
      * Alternatively some client could kill it. *)
     (# 
     do theShell.kill;
     #);
   
do timer.init;
   (onTimeOut[],5*60) -> timer.register -> onTimeOutId;
#)
