(* File calcClient.bet
 * ===================
 * 
 * Simple example client of the calcServer. *)

(* As this is not an X program, we must use shell: (not xshell) *)

ORIGIN '~beta/distribution/v1.6/shell';
INCLUDE 'interfaces';
INCLUDE '~beta/basiclib/v1.6/formatio';
INCLUDE '~beta/sysutils/v1.6/envstring';

--- program:descriptor ---
shellEnv
(#
   (* Tell ensemble.createShell where to look for the calcServer executable: *)
   
   defaultAppsDir:: 
     (# 
     do '$(BETALIB)' -> expandenvvar -> dir[];
        '/demo/distribution/calculator/$/' -> dir.puttext; 
     #);
   
   globalErrorHandler:: (# timeOutValue:: (# do 10 -> sec #)#);
   
   server: ^calcServer;
   serverEnsembleName: ^Text;
   serverEnsemble: ^ensemble;
   
   useServer:
     (# calc: ^calculator;
        
        shortTest: 
          (# s1,s2: @shortint; i:@Integer;
          enter (s1,s2)
          do 'shorttest('->putText;s1->putInt;','->put;s2->putInt;
             ')=('->putText;
             (s1,s2)->calc.shorts->(s1,i);
             s1->putInt;','->put;i->putInt;')'->put;newLine;
          #);
     do
        server.newCalc -> calc[];
        
        (for param:100 repeat
             '2*%d = %d\n' -> putFormat
             (# 
             do param -> d;
                param -> calc.double -> d;
             #);
        for);
        
        newline; newline;
        
        (for param:100 repeat
             '%d*%d = %d\n' -> putFormat
             (# 
             do param -> d; param -> d; param -> calc.exp -> d; 
             #);
        for);
        
        (# foreach: @valueCallback
             (# callbackImpl:: 
                  (# 
                  do value -> screen.putInt; screen.newline 
                  #)
             #);
           fac8: @Integer;
           
        do 
           'Calculating 8!' -> putLine;
           (8,foreach[]) -> calc.factorial -> fac8;
           '8! = %d\n' -> putFormat
           (# 
           do fac8 -> d;
           #);
        #);
        
        (for param:10 repeat
             'sin(%f)*cos(%f) = %15.15f\n' -> putFormat
             (# 
             do 0.1*param -> f; 0.1*param -> f; 0.1*param -> calc.sincos -> f; 
             #);
        for);
        
        (32767,10000)->shortTest;
        (32768,10000)->shortTest;
        (32769,10000)->shortTest;

        calc[] -> server.calcDone;
        
     #);
   
   t: ^Text;
do 
   (* Get a reference to the ensemble on which a calcServer is
    * running or should run: *)
   
   'Enter calcServer hostname: ' -> screen.putText;
   getLine -> serverEnsembleName[];
   
   getHost: (ensemble##, serverEnsembleName[]) 
     -> theShell.myEnsemble.ns.get
   (# notFound::
        (# 
        do serverEnsembleName[] -> screen.putText;
           ' not found. Try again: ' -> screen.putText;
           keyboard.getLine -> serverEnsembleName[];
           restart getHost
        #)
   #) -> serverEnsemble[];
   
   (* Check whether a calcServer is already present on the 
    * serverEnsemble: *)
   
   (calcServer##, 'calcServer') -> serverEnsemble.ns.get
   (# notFound::
        (# 
        do 
           'No calcServer on ' -> screen.putText;
           serverEnsembleName[] -> screen.putLine;
           'Starting a new one ...' -> screen.putText;
           (calcServer##,'calcServer') 
             -> serverEnsemble.createShell
             -> obj[];
           'done' -> screen.putLine;
        #)
   #) -> server[];
   
   useServer;
   
   theShell.kill;
#)
