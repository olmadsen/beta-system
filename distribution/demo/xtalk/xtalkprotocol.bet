ORIGIN '~beta/distribution/v1.1/basicshell';

--- shellEnvLib: attributes ---

(* The xtalkProtocol represents a partner in a xtalk session
 * ========================================================= *)

xtalkProtocol: remoteable
  (# 
     (* Entrys called by partner in an xtalk session:
      * ============================================= *)
     
     call: entry
       (# p: ^xtalkProtocol; callerName: ^Text; 
          answer: @Integer;
       enter (p[],callerName[])
       do (p[],callerName[]) -> callImpl -> answer; 
          none -> callerName[]
       exit answer
       #);
     
     reply: entry
       (# p: ^xtalkProtocol;
          answer: @Integer;
       enter p[]
       do p[] -> replyImpl -> answer
       exit answer
       #);
     
     event: entry
       (# firstPos, lastPos: @Integer; new: ^Text
       enter (firstPos, lastPos, new[])
       do (firstPos, lastPos, new[]) -> eventImpl; 
          none -> new[];
       #);
     
     hangup: entry 
       (# p: ^xtalkProtocol
       enter p[]
       do p[] -> hangupImpl;
       #);
     
     (* Virtuals implementing the entrys above:
      * ======================================= *)
     
     callImpl:<
       (# p: ^xtalkProtocol; callerName: ^Text; reply: @Integer;
       enter (p[],callerName[])
       do INNER; 
       exit reply
       #);
     
     replyImpl:<
       (# p: ^xtalkProtocol;
          answer: @Integer;
       enter p[]
       do INNER
       exit answer
       #);
     
     eventImpl:<
       (# firstPos, lastPos: @Integer; new: ^Text
       enter (firstPos, lastPos, new[])
       do INNER
       #);
     
     hangupImpl:<
       (# p: ^xtalkProtocol
       enter p[]
       do INNER
       #);
  #);
     
XT_BUSY:     (# exit -1 #);
XT_OK:       (# exit -2 #);
XT_TIMEDOUT: (# exit -3 #);
