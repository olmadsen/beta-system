ORIGIN '~beta/Xt/v1.8/motifenv';
(* 
 * $RCSfile: partnerDialog.bet,v $ $Revision: 1.1.1.1 $ $Date: 1994-08-22 08:37:08 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- include '~beta/Xt/v1.8/motif/form'
--- include '~beta/Xt/v1.8/motif/rowcolumn'
--- include '~beta/Xt/v1.8/motif/pushbuttongadget'
--- include '~beta/Xt/v1.8/motif/texts'

--- motifEnvLib:attributes ---

partnerDialog: FormDialog
  (# Group: RowColumn
       (# Field: 
            (# message, defaultValue: ^text;
               gettext: (# exit value.value #);
               getint: 
                 (# t: ^text 
                 do value.value -> t[]; 0 -> t.setPos;
                 exit t.getInt
                 #);
               msg: @LabelGadget
                 (# init::< (# do message[] -> labelString #)#);
               value: @TextField
                 (# init::<
                      (# 
                      do defaultValue[] -> value;
                         true -> pendingDelete;
                         15 -> columns;
                         AddTabGroup;
                      #);
                    focusCallback::<
                      (* Select all on entry *)
                      (# do (0, lastPosition, 0) -> selection.set #);
                 #);
               init:<
                 (# 
                 enter (message[],defaultValue[])
                 do rows+1 -> rows;
                    msg.init;
                    value.init;
                 #);
            #);
          rows: @integer;
          init::<
            (# 
            do INNER;
               XmHORIZONTAL -> orientation;
               XmPACK_COLUMN -> packing;
               rows -> numColumns;
               true -> isAligned;
               XmALIGNMENT_BEGINNING -> entryAlignment;
            #);
       #);
     partnerAddress: @Group
       (# ensemble, partnername: @Field;
          init::<
            (# 
            do ('Call','') -> partnername.init;
               ('On host','') -> ensemble.init;
            #)
       #);
     OK: @PushButtonGadget (# activateCallback::< (# do onOk #)#);
     onOk:< Object;
     
     Cancel: @PushButtonGadget (# activateCallback::< (# do onCancel #)#);
     onCancel:< Object;
     
     ensembleName: (# exit partnerAddress.ensemble.gettext #);
     partnerName: (# exit partnerAddress.partnername.gettext #);
     
     init::<
       (# 
       do partnerAddress.init;
          OK.init;
          Cancel.init;
          OK -> defaultButton;
          0 -> shadowThickness;
          true -> autoUnmanage;
          (* Layout constraints on the children of THIS(FormDialog) *)
          XmATTACH_FORM   -> partnerAddress.leftAttachment;
          10              -> partnerAddress.leftOffset;
          XmATTACH_FORM   -> OK.leftAttachment;
          10              -> OK.leftOffset;
          XmATTACH_WIDGET -> OK.topAttachment;
          10              -> OK.topOffset;
          partnerAddress  -> OK.topWidget;
          XmATTACH_OPPOSITE_WIDGET -> Cancel.topAttachment;
          OK              -> Cancel.topWidget;
          XmATTACH_WIDGET -> Cancel.leftAttachment;
          OK              -> Cancel.leftWidget;
          10              -> Cancel.leftOffset;
          XmDIALOG_FULL_APPLICATION_MODAL -> dialogStyle;
          XmRESIZE_NONE -> resizePolicy;
          'Partner to call' -> dialogTitle;
          true -> noResize;
       #);
     initialized: @Boolean;
     popup: 
       (# 
       do 
          (if initialized //false then
              init; true -> initialized;
          if);
          manageChild;
       #);
  #);
