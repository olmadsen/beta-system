(* File calcClient.bet
 * ===================
 * 
 * Simple example client of the calcServer. *)

(* As this is not an X program, we must use shell: (not xshell) *)

ORIGIN '~beta/distribution/shell';
(* 
 * $RCSfile: calcClient.bet,v $ $Revision: 1.5 $ $Date: 1998-04-23 12:57:52 $
 *
 * COPYRIGHT
 *       Copyright (C) Aarhus University
 *       All rights reserved.
 *)
INCLUDE 'interfaces';
INCLUDE '~beta/basiclib/formatio';

--- program:descriptor ---
shellEnv
(#
   (* Tell ensemble.createShell where to look for the calcServer executable: *)
   
   defaultAppsDir::< 
     (# 
     do '/usr/local/lib/beta/distribution/v1.1/test/distrdir/' -> dir[] 
     #);
   
   distributionDir::<
     (# 
     do '/usr/local/lib/beta/distribution/v1.1/test/distrdir/sys/' -> dir[]
     #);
   
   globalErrorHandler::< (# timeOutValue::< (# do 10 -> sec #)#);
   
   server: ^calcServer;
   serverEnsembleName: ^Text;
   serverEnsemble: ^ensemble;
   
   useServer:
     (# calc: ^calculator;
     do
        server.newCalc -> calc[];
        
        (for param:100 repeat
             '2*%d = %d\n' -> putFormat
             (# 
             do param -> d;
                param -> calc.double -> d;
             #);
        for);
        
        newline; newline;
        
        (for param:100 repeat
             '%d*%d = %d\n' -> putFormat
             (# 
             do param -> d; param -> d; param -> calc.exp -> d; 
             #);
        for);
        
        (# foreach: @valueCallback
             (# callbackImpl::< 
                  (# 
                  do value -> screen.putInt; screen.newline 
                  #)
             #);
           fac8: @Integer;
           
        do 
           'Calculating 8!' -> putLine;
           (8,foreach[]) -> calc.factorial -> fac8;
           '8! = %d\n' -> putFormat
           (# 
           do fac8 -> d;
           #);
        #);
        
        calc[] -> server.calcDone;
        
     #);
   
   t: ^Text;
do 
   (* Get a reference to the ensemble on which a calcServer is
    * running or should run: *)
   
   'Enter calcServer hostname: ' -> screen.putText;
   keyboard.getLine -> serverEnsembleName[];
   
   getHost: (ensemble##, serverEnsembleName[]) 
     -> theShell.myEnsemble.ns.get
   (# notFound::<
        (# 
        do serverEnsembleName[] -> screen.putText;
           ' not found. Try again: ' -> screen.putText;
           keyboard.getLine -> serverEnsembleName[];
           restart getHost
        #)
   #) -> serverEnsemble[];
   
   (* Check whether a calcServer is already present on the 
    * serverEnsemble: *)
   
   (calcServer##, 'calcServer') -> serverEnsemble.ns.get
   (# notFound::<
        (# 
        do 
           'No calcServer on ' -> screen.putText;
           serverEnsembleName[] -> screen.putLine;
           'Starting a new one ...' -> screen.putText;
           (calcServer##,'calcServer') 
             -> serverEnsemble.createShell
             -> obj[];
           'done' -> screen.putLine;
        #)
   #) -> server[];
   
   useServer;
   
   theShell.kill;
#)
