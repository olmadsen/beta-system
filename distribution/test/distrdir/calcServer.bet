(* File calcServer.bet
 * ===================
 * 
 * This a simple implementation of a calcServer.
 * The interfaces between the server and the clients are described in the
 * file interfaces.bet. *)

ORIGIN '~beta/distribution/shell';
(* 
 * $RCSfile: calcServer.bet,v $ $Revision: 1.5 $ $Date: 1998-04-23 12:57:52 $
 *
 * COPYRIGHT
 *       Copyright (C) Aarhus University
 *       All rights reserved.
 *)
INCLUDE 'interfaces';
INCLUDE '~beta/basiclib/timehandler';
INCLUDE '~beta/distribution/remoteRefAsText';

--- program:descriptor ---
shellEnv 
(# setListAddrHack: external
     (# ptr: @Integer
     enter ptr
     #);
   
   defaultScreenName::<
     (# 
     do '/users/beta/distribution/v1.1/demo/calculator/calcServerOut'
          -> name[]
     #);
   
   shellType::< calcServer 
     (# newCalcImpl::< 
          (# 
          do &serverCalc[] -> theCalc[] -> calcList.append;
             onTimeOutId -> timer.unregister;
             (onTimeOut[],5*60) -> timer.register -> onTimeOutId;
          #);
        calcDoneImpl::<
          (# 
          do theCalc[] -> calcList.at -> calcList.delete;
             theCalc[] -> withdraw;
          #);
     #);
   
   calcList: @list (# element::< servercalc #);
   
   servercalc: calculator
     (# doubleImpl::< 
          (# 
          do value*2 -> value;
          #);
        expImpl::< 
          (# 
          do value*value -> value;
          #);
        factorialImpl::< 
          (# 
          do
             1 -> value;
             (for i: N repeat
                  value*i -> value;
                  value -> foreach.callback;
             for)
          #);
     #);
   
   timer: @timeHandler;
   onTimeOutId: @Integer;
   
   onTimeOut: @
     (* This object is called to kill the server if no new calculators
      * have been created for 5 minutes. The reason is that we don't want 
      * this simple example server to stay around forever. 
      * Alternatively some client could kill it. *)
     (# 
     do theShell.kill;
     #);
   
do timer.init; calcList.init;
   (onTimeOut[],5*60) -> timer.register -> onTimeOutId;
#)
