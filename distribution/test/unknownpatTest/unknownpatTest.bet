ORIGIN '~beta/distribution/v1.3/shell';
INCLUDE '~beta/distribution/v1.3/demo/calculator/interfaces';
INCLUDE '~beta/distribution/v1.3/remoteRefAsText';
INCLUDE '~beta/sysutils/v1.6/objinterface';

--- lib:attributes ---
dtext: Text (# #);

--- program:descriptor ---
shellEnv
(#
   (* Tell ensemble.createShell where to look for the calcServer executable: *)
   
   defaultAppsDir::< 
     (# 
     do '/usr/local/lib/beta/distribution/v1.1/demo/calculator/' -> dir[] 
     #);
   
   globalErrorHandler::< (# timeOutValue::< (# do 10 -> sec #)#);
   
   server: ^calcServer;
   serverEnsembleName: ^Text;
   serverEnsemble: ^ensemble;
   
   useServer:
     (# c: ^calculator;
     do
        server.newCalc -> c[];
        
        'Before 10000 double' -> putLine;
        (for i:10000 repeat
             1 -> c.double;
        for);
        'After 10000 double' -> putLine;
        
        (for i:100 repeat
             '2*' -> screen.putText;
             i -> screen.putInt;
             ' = ' -> screen.putText;
             i -> c.double -> screen.putInt; 
             screen.newline;
        for);
        
        screen.newline;
        screen.newline;
        
        (for i:100 repeat
             i -> screen.putInt;
             '*' -> screen.putText;
             i -> screen.putInt;
             ' = ' -> screen.putText;
             i -> c.exp -> screen.putInt; 
             screen.newline;
        for);
        
        (# foreach: @valueCallback
             (# callbackImpl::< 
                  (# 
                  do value -> screen.putInt; screen.newline 
                  #)
             #);
           fac8: @Integer;
           
        do 
           'Calculating 8!' -> screen.putLine;
           (8,foreach[]) -> c.factorial -> fac8;
           '8! = ' -> screen.putText;
           fac8 -> screen.putInt;
           screen.newline;
        #);
        
        c[] -> server.calcDone;
        
     #);
   
   shellType::<
     (# uniqueName: @Text;
     #);
   
   BeforeSer::<
     (# 
     do (o[],screen[]) -> printObject; newline;
     #);
   
   t: ^Text;
do 
   
   'Enter server address as text? (y/n) ' -> screen.putText;
   keyboard.getLine -> t[];
   (if 'y' -> t.equal //true then
       'Enter server address as text: ' -> screen.putText;
       keyboard.getLine -> t[];
       t[] -> refFromText -> server[];
    else
   
       (* Get a reference to the ensemble on which a calcServer is
        * running or should run: *)
       
       'Enter calcServer hostname: ' -> screen.putText;
       keyboard.getLine -> serverEnsembleName[];
   
       getHost: (ensemble##, serverEnsembleName[]) 
         -> theShell.myEnsemble.ns.get
       (# notFound::<
            (# 
            do serverEnsembleName[] -> screen.putText;
               ' not found. Try again: ' -> screen.putText;
               keyboard.getLine -> serverEnsembleName[];
               restart getHost
            #)
       #) -> serverEnsemble[];
       
       (* Check whether a calcServer is already present on the 
        * serverEnsemble: *)
       
       'calcServer' -> theShell.uniqueName;
       true -> traceSer;
       (calcServer##, theShell.uniqueName[]) -> serverEnsemble.ns.get
       (# notFound::<
            (# 
            do 
               'No calcServer on ' -> screen.putText;
               serverEnsembleName[] -> screen.putLine;
               'Starting a new one ...' -> screen.putText;
               (calcServer##,'calcServer') 
                 -> serverEnsemble.createShell 
                 -> obj[];
               'done' -> screen.putLine;
            #)
       #) -> server[];
       false -> traceSer;
   if);
   
   useServer;
   
   '\nServer reference as text: ' -> screen.putText;
   server[] -> refAsText -> screen.putLine;
   
   theShell.kill;
#)
