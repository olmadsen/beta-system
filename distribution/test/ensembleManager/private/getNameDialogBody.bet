ORIGIN  '../getNameDialog';

INCLUDE '~beta/uienv/new/figureitems';
INCLUDE '~beta/uienv/new/uienvactions';

--- GetNameDialogAttributes:Descriptor ---
(#
   iCancel: @pushButton
     (#	eventHandler::<
          (# onMouseup::<  
               (# do	TRUE -> cancelled;
                  this (GetNameDialog).close; 
               #);
          #);
        
        open::<
          (# 
          do (130,70) -> position; 
             (50,20) -> size; 
             'Cancel' -> label;
          #);
     #);
   iOk: @pushButton
     (#	eventHandler::<
          (# onMouseup::<  
               (# do	FALSE -> cancelled;
                  iName.contents -> Name[];
                  this (GetNameDialog).close;
               #);
          #);
        
        open::<
          (# 
          do (10,70)-> position; 
             (30,20) -> size; 
             'OK' -> label;
          #);
     #);
   
   iStatTextButton: @staticText
     (# 
        open::<
          (# 
          do (10,8) ->position; 
             (250,20) -> size; 
             '' -> label;
          #);
     #);
   
   iName: @editText
     (# 
        open::<
          (# 
          do (10,27) -> position; 
             (175,29) -> size;
          #);
     #);  
   
   (* iNameBorder: @rect; *)
   
   iNameKeyDown: keyDownAction
     (#	theKDEvent: ^theEventHandler.KeyDown;
     do	theEvent[] -> theKDEvent[];
	(* Do not insert cr or enter characters! *)
	(* ((theKDEvent.ch=ascii.cr) or (theKDEvent.ch=ascii.etx)) -> skip;  *)
     #);
   
#)
--- GetNameDialogRunDialog:Descriptor ---
(# (* inserts a defaultvalue into the contents of iName *)
   st: ^text;
do DialogLabel[] -> private.iStatTextButton.label;
   &text[] -> st[]; Name -> st; st[] -> private.iName.contents;
   (* private.iName.all -> private.iName.selection;  *)
   showModal;
#)  

--- GetNameDialogOpen:DescriptorForm ---
(#	
do hide;
   (195,100) -> size;
   (470,370) -> position;
   &Text[] -> Name[];
   private.iCancel.open;
   private.iOK.open; 
   private.iOk[] -> DefaultButton;
   private.iStatTextButton.open; (* private.iStatTextButton.disable; *)
   private.iName.open; 
   Private.iName[]-> target;
   &private.iNameKeyDown[] -> private.iName.prependAction;
#)  
