ORIGIN 'ensembleManager';

INCLUDE '~beta/basiclib/v1.4/formatio';
INCLUDE '~beta/uienv/v1.1.8/uienv';
INCLUDE '~beta/uienv/v1.1.8/scrolllists';
INCLUDE 'getNameDialog';
INCLUDE '~beta/uienv/v1.1.8/figureitems';


--- windowEnv:descriptor ---
uienv
(# theWindow: @window
     (# 
        ensembles: @list
          (# element::< ensembleWindow;
          #);
        
        active: ^ensembleWindow;
        
        framed: canvas
          (# frame: @rect
               (# open::<
                    (# 
                    do (1,1) -> pen.size; patterns.white[] -> pen.stipple;
                       father.size -> size;
                       true -> bindBottom -> bindRight -> bindTop -> bindLeft;
                    #);
               #);
             contentsType:< Canvas 
               (# open::< 
                    (# fw,fh: @Integer;
                    do 
                       true -> bindBottom -> bindRight -> bindTop -> bindLeft; 
                       (2,2) -> position;
                       father.size -> (fw,fh);
                       (fw-4,fh-4) -> size;
                       INNER;
                    #);
               #);
             contents: @contentsType;
             open::<
               (# 
               do INNER;
                  frame.open;
                  contents.open;
               #);
          #);
        
        ensembleWindow: framed
          (# es: ^Ensemble;
             contentsType::< 
               (# scr: @TextScrollList
                    (# open::<
                         (# 
                         do true -> bindBottom -> bindRight 
                              -> bindTop -> bindLeft;
                            father.size -> size;
                            es.ns.scanNames
                            (# i: @Integer;
                            do i+1 -> i; 1 -> append;
                               (i,current[]) -> setText;
                            #);
                         #);
                       eventHandler::<
                         (# onMouseDown::<
                              (#
                              do THIS(ensembleWindow)[] -> active[];
                              #)
                         #);
                    #);
                  open::<
                    (# 
                    do scr.open;
                    #)
               #);
             
             setup:
               (# enter es[]
               do 
                  THIS(ensembleWindow)[] -> ensembles.append;
                  open;
               #);
             
             open::<
               (# fw,fh: @Integer;
               do father.size -> (fw,fh);
                  (fw,150) -> size;
                  true -> bindRight -> bindLeft;
                  (0,150*(ensembles.size - 1)) -> position;
               #);
             
             slideup:
               (# x,y: @Integer;
               do position -> (x,y);
                  (x,y-150) -> position;
               #);
             
             remove:
               (# cur: ^ensembles.theCellType;
               do 
                  ensembles.head -> cur[];
                  find:
                    (# 
                    do (if THIS(ensembleWindow)[] //cur.elm[] then
                           close;
                           slide:
                             (# 
                             do cur.succ[] -> cur[];
                                (if cur[] //NONE then else
                                    cur.elm.slideup;
                                    restart slide;
                                if);
                             #);
                           cur[] -> ensembles.delete;
                        else
                           cur.succ[] -> cur[];
                           (if cur[] //NONE then else
                               restart find;
                           if);
                       if);
                    #);
               #);
          #);
        
        OpenEnsemble:
          (# prompt: @GetNameDialog;
             ensembleName: ^Text;
             es: ^ensemble;
          do prompt.open;
             theShell.myEnsemble.hostName[] -> prompt.name[];
             'Name of ensemble to open:' -> prompt.DialogLabel;
             prompt.runDialog;
             getIt:
               (if prompt.cancelled //false then
                   (ensemble##, prompt.name[]->ensembleName[]) -> 
                   theShell.myEnsemble.ns.get
                   (# notFound::<
                        (# 
                        do 'Unknown network host: %s\n' -> putFormat
                           (# 
                           do ensembleName[] -> s;
                              leave getIt;
                           #);
                        #);
                   #) -> es[];
                   
                   (if es.ping //true then
                       (# new: ^ensembleWindow;
                       do &ensembleWindow[] -> new[];
                          es[] -> new.setup;
                       #);
                    else
                       '%s ensemble not responding.\n' -> putFormat
                       (# 
                       do ensembleName[] -> s;
                       #);
                       leave getIt;
                   if);
                   
                   
                   
               if);
          #);
        
        CloseActive:
          (# 
          do (if active[] //NONE then else
                 active.remove
             if);
             NONE -> active[];
          #);
        
        KillActive:
          (# 
          #);
        
        menuBarType::<
          (# fileMenu: @Menu
               (# openItem: @menuItem
                    (# open::<
                         (# 
                         do 'Open' -> name;
                            'O' -> key;
                         #);
                       eventHandler::<
                         (# onSelect::<
                              (# 
                              do OpenEnsemble;
                              #)
                         #);
                    #);
                  closeItem: @menuItem
                    (# open::<
                         (# 
                         do 'Close' -> name;
                            'C' -> key;
                         #);
                       eventHandler::<
                         (# onSelect::<
                              (# 
                              do CloseActive;
                              #)
                         #);
                    #);
                  killItem: @menuItem
                    (# open::<
                         (# 
                         do 'Close' -> name;
                            'C' -> key;
                         #);
                       eventHandler::<
                         (# onSelect::<
                              (# 
                              do KillActive;
                                 CloseActive;
                              #)
                         #);
                    #);
                  quitItem: @menuItem
                    (# open::<
                         (# 
                         do 'Quit' -> name;
                            'Q' -> key;
                         #);
                       eventHandler::<
                         (#  onSelect::<
                              (# 
                              do Terminate;
                              #);
                         #);
                    #);
                  open::<
                    (# 
                    do 'File' -> name;
                       openItem.open;
                       openItem[] -> append;
                       closeItem.open;
                       closeItem[] -> append;
                       killItem.open;
                       killItem[] -> append;
                       quitItem.open;
                       quitItem[] -> append;
                    #);
               #);
             open::<
               (# 
               do fileMenu.open;
                  fileMenu[] -> append;
               #);
          #);
        
        open::<
          (# 
          do (100,300) -> size;
          #);
     #)
   
do theWindow.open;
#)

--- ensembleManagerBody:descriptor ---
(# 
do 
#)
