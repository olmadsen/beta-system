ORIGIN '../remoteRefAsText';
(* 
 * $RCSfile: remoteRefAsTextBody.bet,v $ $Revision: 1.1.1.1 $ $Date: 1994-08-22 08:37:11 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
INCLUDE 'shellBody'

--- shellEnvRefAsText:descriptor ---
(# 
do
   (if r.ri[] //NONE then
       (* This is a local object that has not yet been given an OID. 
        * First assign an OID. *)
       r[] -> senvPriv.OIDhandler.assignOID;
   if);
   
   &Text[] -> t[];
   r.ri.OID.a -> t.putInt; '.' -> t.put; 
   r.ri.OID.b -> t.putInt;'.' -> t.put;
   r.ri.shellOID.a -> t.putInt; '.' -> t.put; 
   r.ri.shellOID.b -> t.putInt; '.' -> t.put; 
   r.ri.shellAdr.asText -> t.append;
#)     
  
--- shellEnvRefFromText:descriptor ---
(# OID: @OIDtype; s: ^Shell; 
   og: ^ObjectGetter;
do
   (* Create a fake proxy for the shell containing the object requested. *)
   &Shell[]->s[]; &remoteInfo[]->s.ri[];
   
   true -> s.isProxy;
   
   t.reset; 
   t.getInt -> OID.a; t.get; 
   t.getInt -> OID.b; t.get; 
   
   t.getInt -> s.ri.OID.a; t.get;
   t.getInt -> s.ri.OID.b; t.get;
   s.ri.OID -> s.ri.shellOID;
   
   (t.pos+1,t.lgth) -> t.sub
     -> portableCommAddressFromText
     -> s.ri.shellAdr[];
   
   s[] -> senvpriv.typeAllocator.initFromObj -> s.ri.netType[];
   
   s.getObjectGetter -> og[];
   
   (if og[] //NONE then else OID -> og.getObject -> r[] if);
   
#)


--- shellPrivateEntries:attributes ---

getObjectGetter: entry
  (# og: ^objectGetter;
  do senvpriv.theObjectGetter[] -> og[];
  exit og[]
  #);
