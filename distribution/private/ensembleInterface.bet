ORIGIN '../basicshell';

--- shellEnvLib:attributes ---

ES_ok:       (# exit 0 #);
ES_exists:   (# exit 1 #);
ES_notfound: (# exit 2 #);

ensembleInterface:
  (# 
     (* saveReference
      * =============
      *     
      * The (name,objref) pair is saved. If force is true then an existing
      * reference with the same name will be overwritten. Otherwise an 
      * existing ref with the same name will result in the ES_exists result
      * being returned.
      * Return codes: ES_ok or ES_exists. *)
     
     saveReferenceImpl:<
       (# name: ^Text;
          r: ^remoteable;
          force: @Boolean;
          res: @Integer;
       enter (name[],r[],force)
       do INNER;
       exit res
       #);
     
     (* getReference
      * ============
      * 
      * Returns the objref saved under name. 
      * Return codes: ES_ok or ES_notfound. *)
     
     getReferenceImpl:<
       (# name: ^Text; r: ^remoteable; 
          res: @Integer;
       enter name[]
       do INNER;
       exit (r[],res)
       #);
     
     
     
     (* removeReference
      * ===============
      * 
      * Undoes the effect of saveReference.
      * Return codes: ES_ok or ES_notfound. *)
     
     removeReferenceImpl:< 
       (# name: ^Text;
          res: @Integer;
       enter name[]
       do INNER;
       exit res
       #);
     
     
     
     (* newOIDs
      * =======
      * 
      * Allocates requestes OID's for use by the calling shell. 
      * Returns OIDa and the number of the first OIDb allocated. *)
     
     newOIDsImpl:<
       (# requested: @Integer;
          OIDa, nextFree: @Integer;
       enter requested
       do INNER;
       exit (OIDa,nextFree)
       #);
     
     
     
     getNamesImpl:<
       (# names:[0]^Text;
       do INNER
       exit names
       #)
  #);
