ORIGIN 'socketStream';
(* 
 * $RCSfile: socketStreamBody.bet,v $ $Revision: 1.1.1.1 $ $Date: 1994-08-22 08:37:16 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- include 'shellSockets'

--- ssPutIntBody:dopart ---
do 
   (4, 0) -> (toWrite, totalWrote);
   
   loop:
     (# 
     do
         (sockfd, @@i + totalWrote, toWrite) -> nbWriten -> wrote;
         (if true
         //(wrote = -1) then
             shutDownStream; conBroken; 
          //(wrote < toWrite) then
             wrote + totalWrote -> totalWrote;
             toWrite - wrote -> toWrite;
             (* Wait until it is again possible to send a positive number
              * of bytes on the socket or an exceptional state is pending
              * on the socket. *)
             (if ios.writeBlock //true then
                 timeOut
             if);
             restart loop;
         if)
     #)


--- ssGetIntBody:dopart ---
do
   (4, 0) -> (toRead, totalRead);
   
   loop:
     (if 1//1 then
         (sockfd, @@i + totalRead, toRead) -> nbReadn -> read;
         (if true
          //(read = -1) then
             shutDownStream; conBroken;
          //(read = -2) then
             (if waited //false then
                 (* The socket may not have been connected yet. *)
                 (if ios.readBlock //true then timeOut if);
                 true -> waited;
                 restart loop;
              else
                 shutDownStream; conBroken;
             if)
          //(read < toRead) then
             read + totalRead -> totalRead;
             toRead - read -> toRead;
             (if ios.readBlock //true then timeOut if);
             restart loop;
         if)
     if)


--- ssPutRepBody:dopart ---
do
   (if rs[] //NONE then
       0 -> putInt
       (# conBroken:: (# do THIS(PutRep).conBroken #);
          timeOut::   (# do THIS(PutRep).timeOut #);
       #);
    else
       (rs.size*4, 0) -> (toWrite, totalWrote);
       loop:
         (if 1//1 then
            (sockfd, @@rs.r[1] + totalWrote, toWrite) 
              -> nbWriten 
              -> wrote;
            (if true
             //(wrote = -1) then
                shutDownStream; conBroken;
             //(wrote < toWrite) then
                wrote + totalWrote -> totalWrote;
                toWrite - wrote -> toWrite;
                (if ios.writeBlock //true then timeOut if);
                restart loop;
            if)
         if)
   if);



--- ssGetRepBody:dopart ---
do
   get: 
     (if 1//1 then
         getInt 
         (# conBroken:: (# do THIS(GetRep).conBroken #);
            timeOut::   (# do THIS(GetRep).timeOut #);
         #) -> size;
         
         (if size //0 then
             NONE -> rs[];
          else
             (if rs[] //NONE then
                 &RepetitionObject[] -> rs[];
             if);
             size -> rs.size;
             
             (if size > rs.r.range //true then
                 size - rs.r.range -> rs.r.extend;
             if);
             
             ((size - 1) * 4, 0) -> (toRead, totalRead);
             
             loop:
               (if (sockfd, @@rs.r[2] + totalRead, toRead) -> nbReadn -> read
                //-1 then
                   shutDownStream; conBroken;
                //-2 then
                   (if waited //false then
                       (* The socket may not have been connected yet. *)
                       (if ios.readBlock //true then timeOut if);
                       true -> waited;
                       restart loop;
                    else
                       shutDownStream; conBroken;
                   if)
                else
                   (if (read < toRead) //true then
                       read + totalRead -> totalRead;
                       toRead - read -> toRead;
                       (if ios.readBlock //true then timeOut if);
                       restart loop;
                   if)
               if)
         if)
     if)

--- ssCloseBody:dopart ---
do sockfd -> close

--- ssShutDownBody:dopart ---
do (sockfd, 2) -> shutDown
