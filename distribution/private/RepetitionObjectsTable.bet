ORIGIN '~beta/basiclib/v1.4/betaenv';
(* 
 * $RCSfile: RepetitionObjectsTable.bet,v $ $Revision: 1.2 $ $Date: 1996-02-19 12:26:23 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-94
 *       All rights reserved.
 *)
--- include '~beta/objectserver/v2.3/RepetitionObject'

--- lib:attributes ---
RepetitionObjectsTable:
  (# table: [2]^RepetitionObject;
     last: @Integer;
     append: @
       (# ro: ^RepetitionObject;
       enter ro[]
       do
          (if (last+1 -> last) > table.range //true then
              table.range -> table.extend;
          if);
          ro[] -> table[last][];
       exit last
       #);
     scan:
       (# current: ^RepetitionObject;
       do (for i:last repeat
               table[i][] -> current[]; INNER;
          for);
       #);
     get: @
       (# inx: @Integer;
       enter inx
       exit table[inx][]
       #);
     reInit: @ 
       (# 
       do (if last //0 then else
              2 -> table.new;
              0 -> last;
          if);
       #);
  #);
