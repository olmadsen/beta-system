ORIGIN 'shellBody';
(* 
 * $RCSfile: shellEnvPrivateBody.bet,v $ 
 * $Revision: 1.9 $ 
 * $Date: 1997-08-04 12:00:42 $
 *
 * COPYRIGHT
 *       Copyright Mjolner Informatics, 1992-96
 *       All rights reserved.
 *)

BUILD nti '$$/outputRedirection.obj' 'external/outputRedirection.c' 'betacc $0 $1'
      default '$$/outputRedirection.o'   'external/outputRedirection.c' '$CC -D$$ -c -o $0 $1';

BUILD  nti '$$/deamonStart.obj' 'external/deamonStart.c' 'betacc $0 $1'
       default '$$/deamonStart.o'   'external/deamonStart.c' '$CC -D$$ -c -o $0 $1';

--- shellEnvFirstInitBody:descriptor ---
(# redirectOutput: external
     (# name: [1]@Char
     enter name
     #);
   deamonStart: external
     (# ignsigcld: @Integer;
        didIt: @integer;
     enter ignsigcld
     exit didIt
     #);
   ri: ^remoteInfo;
   t: ^Text;
   tcp_addr: ^tcpPortAddress;
   inadr: @integer;
do
   senvpriv.shellCreator.init;
   (if true
    //senvpriv.shellCreator.remotelyInstantiated
    //isEnsemble then
       (if (1->deamonStart)
        // 0 then
           (* deamonStart prevented *)
        else
           (if senvpriv.shellCreator.screenName -> t[] //NONE then
               '' -> redirectOutput;
            else
               t -> redirectOutput;
           if);
       if);
   if);
   
   senvpriv.OIDhandler.init; 
   senvpriv.remoteExecutor.init;
   senvpriv.remoteObjects.init; 
   senvpriv.protoTable.init;
   senvpriv.typeAllocator.init;
   senvpriv.ptOffsets.init;
   (isEnsemble,ensemblePort)->senvpriv.RPC.init; 

   (* This works as long as localAddress is TCP/IP based:
    * Get internet address of this host *)
   senvPriv.RPC.localAddress.getTcpPort->tcp_addr[];
   tcp_addr.inetAddr->inadr;
   
   (senvPriv.RPC.localHostName[], 
    senvPriv.RPC.localEnsembleAdrAsText[], 
    (inadr,0))
     -> senvPriv.remoteObjects.exclCreateEnsemble 
     -> ri[];
   ri.o[] -> theShell.myEnsemble[];
#)

--- shellEnvSecondInitBody:descriptor ---
(#
do
   (if senvpriv.shellCreator.remotelyInstantiated //true then
       
       (* This shell was instantiated from another shell and it 
        * should therefore report back to the instantiator. *)
       
       (* Assign OID to theShell: *)
       senvpriv.OIDhandler.shellOID;
       
       (* Register with instantiator. *)
       senvpriv.shellCreator.report;
   if);
   
   theShell.shellPrivate.start;
#)
