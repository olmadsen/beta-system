ORIGIN '~beta/basiclib/v1.6/basicsystemenv';
INCLUDE '~beta/editor/v5.2/ymersiflib'
        '~beta/guienv/v1.6/guienvsystemenv'
        '~beta/sysutils/v1.6/pathhandler'
        'userbeta';
BODY '~beta/editor/v5.2/private/compiler';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-97
 *   All rights reserved.
 *)
-- program: Descriptor --
systemenv
  (#
     setWindowEnv::  (#  do fb[]->theWindowEnv[] #);
     fb: @fragmentBrowser
       (#
          ymerBrowserWindow:: 
            (#
               nonterminalAllowed:: 
                 (# 
                 do
                    (if nont.nonterminalSymbol = edenv.betagram.impOpt then
                        true->value
                    if)
                 #);
               browserTitle:: 
                 (# 
                 do
                    'Sif: Mjolner BETA Source Browser and Editor'
                      ->title[] (*.prepend*)
                 #);
               menubarType:: 
                 (#
                    groupFileMenuType:: 
                      (#
                         open:: 
                           (# 
                           do
                              'New...'->iNew.new;
                              'New BETA Program...'->iNewBetaProgram.new;
                              'New BETA Library...'->iNewBetaLibrary.new;
                              newSeparator;
                              'Close'->iClose.new;
                              'Save'->iSave.new;
                              'Save As...'->iSaveAs.new;
                              'Save All...'->iSaveAll.new;
                              'Save Abstract...'->iSaveAbstract.new;
                              newSeparator;
                              'Recover...'->iRecover.new;
                              'Revert'->iRevert.new;
                              newSeparator;
                              'Group Write Protection'->iSetIsReadOnly.new;
                              'Global Write Protection'
                                ->iSetGlobalWriteProtection.new
                           #)
                      #);
                    groupPerformMenuType:: 
                      (#
                         open:: 
                           (# 
                           do
                              'Check Current'->iCheck.new;
                              'Recheck'->iReCheck.new;
                              'Compile Current'->iCompile.new;
                              ('Recompile','g')->iRecompile.newKey;
                              'Execute Current'->iExecute.new;
                              'Reexecute'->iReExecute.new;
                              newSeparator;
                              'Debug'->iDebug.new;
                              newSeparator;
                              'Semantic Errors...'->iSemanticErrors.new;
                              newSeparator;
                              'Show Diagram'->iShowDiagram.new;
                              INNER
                           #)
                      #);
                    userMenu: @simplemenu
                      (#
                         iUserCommand: @item
                           (#
                              onStatus:: 
                                (#  do browser.cfe[] <> none ->value #);
                              onSelect:: 
                                (# 
                                do
                                   (none ,'Hello user','User dialog')
                                     ->alertUser;
                                   'user command: '->puttext;
                                   edenv.uep.global+1->edenv.uep.global->putInt;
                                   newLine
                                #)
                           #);
                         open::  (#  do 'User command'->iUserCommand.new #)
                      #);
                    open:: 
                      (# 
                      do
                         SLOTs.new;
                         SLOTs[]->append;
                         'Fragment'->GroupEdit.new;
                         'Tools'->GroupPerform.new;
                         'User'->userMenu.new;
                         userMenu[]->append;
                         INNER
                      #)
                 #);
               workspace:: 
                 (#
                    menubarType:: 
                      (#
                         userMenu: @simplemenu
                           (#
                              iUserCommand: @item
                                (#
                                   onStatus::  (#  do true->value #);
                                   onSelect:: 
                                     (# 
                                     do
                                        (none ,'Hello user','User dialog')
                                          ->alertUser;
                                        'user command: '->puttext;
                                        edenv.uep.global+1->edenv.uep.global
                                          ->putInt;
                                        newLine
                                     #)
                                #);
                              open:: 
                                (#  do 'User command'->iUserCommand.new #)
                           #);
                         open:: 
                           (# 
                           do 'User'->userMenu.new; userMenu[]->append; INNER
                           #)
                      #)
                 #);
               codeViewWindow:: 
                 (#
                    menubarType:: 
                      (#
                         userMenu: @simplemenu
                           (#
                              iUserCommand: @item
                                (#
                                   onStatus:: 
                                     (# 
                                     do browser.cfe[] <> none ->value
                                     #);
                                   onSelect:: 
                                     (# 
                                     do
                                        (none ,'Hello user','User dialog')
                                          ->alertUser;
                                        'user command: '->puttext;
                                        edenv.uep.global+1->edenv.uep.global
                                          ->putInt;
                                        newLine
                                     #)
                                #);
                              open:: 
                                (#  do 'User command'->iUserCommand.new #)
                           #);
                         open:: 
                           (# 
                           do 'User'->userMenu.new; userMenu[]->append; INNER
                           #)
                      #)
                 #);
               open:: 
                 (# 
                 do
                    (if hiddenSif then
                        'calling hide in open'->putLine; hide
                     else
                        'hiddenSif is false'->putLine
                    if);
                    initCompiler
                 #)
            #);
          isBETAsif:: trueObject;
          useExternalTextEditor:: 
            (#  do true (*enable always useExtTextEd*) ->value #);
          init:: 
            (# 
            do
               ('Sif: Known Bugs and Inconveniences',
                '~beta/editor/v5.2/doc/sif-faq.html')->helpDocument;
               
            #);
          arg,fgname,ffname: ^text;
          
       do
          newLine;
          'Sif - Mjolner BETA Browser and Editor version '->putText;
          editorVersion->putText;
          ' '->put;
          machineType[]->putLine;
          '      integrated with BETA Compiler version '->putText;
          compilerVersion->putLine;
          (if noOfarguments > 1 then
              scanArguments:
              (for i: noOfArguments-1 repeat
                i+1->arguments->arg[];
                (if true
                 // '-test'->arg.equal then
                    true->doTest
                 // '-mpsTrace'->arg.equal then
                    ymerbrowser.appendDone; leave scanArguments
                 else
                    arg[]->analyzeArg->(fgname[],ffname[]);
                    (if fgname.length > 0 then
                        (if ffname.length > 0 then
                            fgname[]->ymerBrowser.appendProject;
                            (fgname[],ffname[])
                              ->ymerBrowser.browser.selectFragmentform;
                            (if hiddenSif then
                                (if ymerBrowser.browser.cfe[] <> none then
                                    true
                                      ->
                                        ymerBrowser.browser.cfe.edenv.
                                          setGlobalWriteProtection;
                                    (ymerBrowser.browser.cfe.frag.root[],
                                     ymerBrowser.browser.cfe.frag.root[],true)
                                      ->
                                        ymerBrowser.browser.cfe.
                                          selectNodeInFormEditor
                                if)
                            if)
                         else
                            fgname[]->ymerbrowser.appendProject
                        if)
                    if)
                if)
              for);
              (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
              
          if);
          (if doTest then
              (ymerbrowser.theMenubar).test.open;
              'Test'->(ymerbrowser.theMenubar).test.name;
              (ymerbrowser.theMenubar).test[]->(ymerbrowser.theMenubar).append
          if)
       #);
     arg: ^text
  do (*fb*)
     (if noOfarguments > 1 then
         (for i: noOfArguments-1 repeat
           i+1->arguments->arg[];
           (if true
            // '-test'->arg.equal then
               true->fb.doTest
            // '-hide'->arg.equal then
               true->fb.hiddenSif
           if)
         for);
         
     if)
  #)  

