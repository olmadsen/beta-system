ORIGIN '~beta/basiclib/v1.4/betaenv';
INCLUDE '~beta/mps/v4.9.1/astlevel';
-- lib: Attributes --
fragmentGroupScanner:
  (#
     g: ^mps.fragmentGroup;
     mps: ^astInterface;
     debugBrowser: @boolean;
     (*groupEditorListSize: @integer;*)
     noOfProps,noOfFrags: @integer;
     scanPropsAndFragsForText:
       (#
          myAddText:< (# t: ^text;  enter t[] do INNER #);
          no: @integer;
          help,prophelp: ^text;
          latestName: @text;
          
       do
          &text[]->prophelp[];
          (if debugBrowser then
              (if g.prop.propList.Size
               // 0 then 'The property list is emty'->putLine; 
               else
                  g.prop.scanProp
                    (#
                       doProp::< 
                         (# 
                         do
                            prop[]->putText;
                            ' '->put;
                            scanParameters
                              (#
                                 doString::< 
                                   (# 
                                   do
                                      ''''->put;
                                      s[]->putText;
                                      ''''->put;
                                      ' '->put;
                                      
                                   #);
                                 doName::< 
                                   (#  do n[]->putText; ' '->put;  #);
                                 doConst::< 
                                   (#  do c->putInt; ' '->put;  #);
                                 
                              #);
                            newLine;
                            
                         #);
                       
                    #);
                  
              if);
              
          if);
          g.prop.scanProp
            (#
               doProp::< 
                 (# 
                 do (* Ignore some properties. *)
                    (if debugBrowser then prop[]->putLine if);
                    (if true
                     // ('donecheck'->prop.equalNCS)
                     // ('ABSTRACTED'->prop.equal) then (* nothing *)
                        
                     else
                        scanParameters
                          (#
                             doConst::< 
                               (# 
                               do
                                  (if debugBrowser then
                                      'prop(const): '->putText;
                                      '<'->put;
                                      prop[]->putText;
                                      '> '->putText;
                                      c->putInt;
                                      newLine;
                                      
                                  if);
                                  c->prophelp.putInt;
                                  ' '->prophelp.put;
                                  (if true
                                   // ('ON'->prop.equalNCS)
                                   // ('OFF'->prop.equalNCS) then
                                      no+1->no;
                                      (if debugBrowser then
                                          prop[]->putText; no->putInt; newLine; 
                                      if);
                                      prop.copy->prophelp[];
                                      ' '->prophelp.put;
                                      c->prophelp.putInt;
                                      prophelp[]->myAddText;
                                      prophelp.clear;
                                      
                                  if)
                               #);
                             doName::< 
                               (# 
                               do
                                  (if debugBrowser then
                                      'prop(name): '->putText;
                                      '<'->put;
                                      prop[]->putText;
                                      '> '->putText;
                                      n[]->putLine;
                                      
                                  if);
                                  n[]->prophelp.putText;
                                  ' '->prophelp.put;
                                  n->latestName;
                                  
                               #);
                             doString::< 
                               (# 
                               do
                                  (if debugBrowser then
                                      'prop(string): '->putText;
                                      '<'->put;
                                      prop[]->putText;
                                      '> '->putText;
                                      ''''->put;
                                      s[]->putText;
                                      ''''->put;
                                      newLine;
                                      
                                  if);
                                  (if true
                                   // ('ORIGIN'->prop.equalNCS)
                                   // ('BODY'->prop.equalNCS)
                                   // ('MDBODY'->prop.equalNCS)
                                   // ('OBJFILE'->prop.equalNCS)
                                   // ('BETARUN'->prop.equalNCS)
                                   // ('MAKE'->prop.equalNCS)
                                   // ('LIBFILE'->prop.equalNCS) then
                                      no+1->no;
                                      (if debugBrowser then
                                          prop[]->putText; no->putInt; newLine; 
                                      if);
                                      prop.copy->prophelp[];
                                      ' '->prophelp.put;
                                      (if ('MDBODY'->prop.equalNCS) then
                                          latestName[]->prophelp.putText;
                                          ' '->prophelp.put;
                                          
                                      if);
                                      ''''->prophelp.put;
                                      s[]->prophelp.putText;
                                      ''' '->prophelp.putText;
                                      prophelp[]->myAddText;
                                      prophelp.clear;
                                      
                                  if)
                               #);
                             
                          #);
                        
                    if);
                    
                 #)
            #);
          no->noOfProps;
          (*(if (noOfProps > groupEditorListSize)
           * then
           *   'No of  properties is greater than '->help[];
           *   groupEditorListSize->help.putInt;
           *   ', contact Mjolner Informatics'->help.putText;
           *  help[]-> {*UI.alert*} putLine;
           *else
           *(if (g.fragmentList.Size+noOfProps > groupEditorListSize)
           *then
           *   &text[]->help[];
           *   'Too many properties and fragments!'->help.putLine;
           *   'No of properties: '->help.putText;
           *   noOfProps->help.putInt;
           *   help.newLine;
           *       'No of fragments: '->help.putText;
           *       g.fragmentList.Size->help.putInt;
           *       help.newLine;
           *       'Contact Mjolner Informatics'->help.putText;
           *       help[]-> {*UI.alert*} putLine;
           *   if);
           *if);*)
          g.fragmentList.scan
            (# t: ^text; help: ^text; 
            do
               no+1->no;
               (*(if (no <=groupEditorListSize)
                * then*)
               (if current.type
                // mps.linkType then
                   'INCLUDE '''->help[];
                   current.name[]->help.putText;
                   ''''->help.put;
                   help[]->myAddText;
                   
                // mps.formType then
                     (# ff: ^mps.fragmentForm
                     do
                        current.open->ff[];
                        (ff.name).copy->help[];
                        ': '->help.append;
                        ;
                        ff.category->ff.Grammar.symbolToName->t[];
                        (if t[]
                         // none then
                            'ff.category: '->putText;
                            ff.category->putInt;
                            newLine;
                            'ff.root.symbol: '->putText;
                            ff.root.symbol->putInt;
                            newLine;
                            
                         else
                            (if true
                             // ('DescriptorForm'->t.equal)
                             // ('ObjectDescriptor'->t.equal) then
                                'Descriptor'->help.putText; 
                             // ('AttributesForm'->t.equal)
                             // ('AttributeDecl'->t.equal) then
                                'Attributes'->help.putText; 
                             else
                                (if t.empty then
                                    'Unexpanded'->help.putText; 
                                 else
                                    t[]->help.putText; 
                                if);
                                
                            if);
                            
                        if);
                        help[]->myAddText;
                        
                     #)
               if);
               (*if);*)
               
            #);
          no->noOfFrags;
          
       #);
     scanPropsAndFrags:
       (#
          doProperty:< (# prop,s: @text enter (prop,s) do INNER #);
          doFragmentLink:<
            (# fle: ^g.fragmentListElement enter fle[] do INNER #);
          doFragmentForm:<
            (# fle: ^g.fragmentListElement enter fle[] do INNER #);
          doFragmentGroup:<
            (# fle: ^g.fragmentListElement enter fle[] do INNER #);
          g: ^mps.fragmentGroup;
          currentItemNo: @integer;
          no: @integer;
          help: ^text;
          
       enter (g[],currentItemNo)
       do
          (if debugBrowser then
              'noOfProps, noOfFrags, currentItemNo:'->putText;
              noOfProps->putInt;
              ' '->put;
              noOfFrags->putInt;
              ' '->put;
              currentItemNo->putInt;
              newLine;
              
          if);
          (if (currentItemNo <= noOfProps) then
              g.prop.scanProp
                (#
                   help: @text;
                   doProp::< 
                     (# 
                     do (* Ignore some properties. *)
                        (if true
                         // ('donecheck'->prop.equalNCS)
                         // ('ABSTRACTED'->prop.equal) then (* nothing *)
                            
                         else
                            scanParameters
                              (#
                                 doString::< 
                                   (# 
                                   do
                                      (if true
                                       // ('ORIGIN'->prop.equalNCS)
                                       // ('BODY'->prop.equalNCS)
                                       // ('MDBODY'->prop.equalNCS)
                                       // ('OBJFILE'->prop.equalNCS)
                                       // ('BETARUN'->prop.equalNCS)
                                       // ('MAKE'->prop.equalNCS)
                                       // ('LIBFILE'->prop.equalNCS) then
                                          no+1->no;
                                          (if no
                                           // currentItemNo then
                                              (prop,s)->doProperty; 
                                          if)
                                      if)
                                   #);
                                 
                              #);
                            
                        if);
                        
                     #)
                #);
              
           else
              noOfProps->no;
              g.fragmentList.scan
                (# t: ^text; help: @text; 
                do
                   (if current.type
                    // mps.linkType then
                       no+1->no;
                       (if no
                        // currentItemNo then current[]->doFragmentLink
                       if)
                    // mps.formType then
                         (# ff: ^mps.fragmentForm
                         do
                            no+1->no;
                            (if no
                             // currentItemNo then current[]->doFragmentForm
                            if);
                            
                         #)
                    // mps.groupType then
                         (# fg: ^mps.fragmentGroup
                         do
                            no+1->no;
                            (if no
                             // currentItemNo then current[]->doFragmentGroup
                            if);
                            
                         #)
                   if);
                   
                #);
              
          if);
          
       #)
  enter (mps[],g[])
  #)  

