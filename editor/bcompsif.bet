ORIGIN '~beta/basiclib/v1.6/betaenv';
(* 
 * Used on NTI when SDK<>bor: Links with binary compiler
 *)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-97
 *   All rights reserved.
 *)
INCLUDE 'ymersiflib'
        'private/seticon';
BODY 'private/bcompiler'
     'private/nouser';
-- program: Descriptor --
(#
   fb: @fragmentBrowser
     (#
        ymerBrowserWindow::< 
          (#
             browserTitle:: 
               (# 
               do
                  'Sif: Mjolner BETA Source Browser and Editor'
                    ->title[] (*.prepend*)
               #);
             menubarType:: 
               (#
                  groupFileMenuType:: 
                    (#
                       open:: 
                         (# 
                         do
                            'New...'->iNew.new;
                            'New BETA Program...'->iNewBetaProgram.new;
                            'New BETA Library...'->iNewBetaLibrary.new;
                            newSeparator;
                            'Close'->iClose.new;
                            'Save'->iSave.new;
                            'Save As...'->iSaveAs.new;
                            'Save All...'->iSaveAll.new;
                            'Save Abstract...'->iSaveAbstract.new;
                            newSeparator;
                            'Recover'->iRecover.new;
                            'Revert'->iRevert.new;
                            newSeparator;
                            'Group Write Protection'->iSetIsReadOnly.new;
                            'Global Write Protection'
                              ->iSetGlobalWriteProtection.new
                         #)
                    #);
                  groupPerformMenuType:: 
                    (#
                       open::< 
                         (# 
                         do
                            'Check Current'->iCheck.new;
                            'Recheck'->iReCheck.new;
                            'Compile Current'->iCompile.new;
                            ('Recompile','g')->iRecompile.newKey;
                            'Execute Current'->iExecute.new;
                            'Reexecute'->iReExecute.new;
                            newSeparator;
                            'Debug'->iDebug.new;
                            newSeparator;
                            'Semantic Errors...'->iSemanticErrors.new;
                            INNER
                         #)
                    #);
                  open::< 
                    (# 
                    do
                       SLOTs.new;
                       SLOTs[]->append;
                       'Fragment'->GroupEdit.new;
                       'Tools'->GroupPerform.new;
                       (if doTest then 'Test'->test.new if);
                       INNER
                    #)
               #);
             open::  (#  do SetSifIcon; initCompiler #)
          #);
        isBETAsif::<  (#  do true->value #);
        init::< 
          (# 
          do
             ('Sif: Known Bugs and Inconveniences','~beta/editor/v5.2/doc/BUGS')
               ->helpDocument;
             
          #);
        arg: ^text;
        
     do
        'Sif - Mjolner BETA Browser and Editor version '->putText;
        editorVersion->putLine;
        '      integrated with the BETA Compiler version '->putText;
        compilerVersion->putLine;
        (if noOfarguments > 1 then
            (for i: noOfArguments-1 repeat
              i+1->arguments->arg[];
              (if true
               // '-test'->arg.equal then
                  true->doTest
               // '-e'->arg.equal then
                  
               else
                  arg[]->ymerbrowser.appendProject
              if)
            for);
            (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
            
        if)
     #);
   arg: ^text
do
   (if noOfarguments > 1 then
       (for i: noOfArguments-1 repeat
         i+1->arguments->arg[];
         (if true
          // '-test'->arg.equal then
             true->fb.doTest
          // '-e'->arg.equal then
             true->fb.useExtTextEd
         if)
       for);
       
   if);
   fb
#)  

