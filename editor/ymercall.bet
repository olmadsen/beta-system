ORIGIN '~beta/mps/astlevel';

(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-2001
 *   All rights reserved.
 *)
-- lib: Attributes --
ymerCall:
  (#
     theBrowser: ^object;
     theBrowserId: @integer;
     refreshGroup:<
       (* Calls updatebrowser *)
       (# cb: ^object; fg: ^astInterface.fragmentGroup
       enter (cb[],fg[])
       do INNER
       #);
     getFragmentGroup:<
       (* Calls MPS.getFragmentGroup *)
       (# name: ^text; fg: ^astInterface.fragmentGroup
       enter name[]
       do INNER
       exit fg[]
       #);
     fragmentGroupList: list (# element:: text #);
     getCurrentProject:<
       (* Uses ymerbrowserInt.currentProject *)
       (# name: ^text; fgList: ^fragmentGroupList
       do INNER
       exit (name[],fgList[])
       #);
     getCurrentGroupProject:<
       (* Uses ymerbrowserInt.currentProject *)
       (# searchArea: @integer; name: ^text; fgList: ^fragmentGroupList
       enter searchArea
       do INNER
       exit (name[],fgList[])
       #);
     getCurrentProjectStatus:< (* Uses ymerbrowserInt.currentProject *)
       (# name: ^text do INNER exit name[] #);
     getCurrentFragmentGroup:<
       (* Uses cge in ymerbrowserInt *)
       (# fg: ^astInterface.fragmentGroup do INNER exit fg[] #);
     getCurrentFragmentForm:<
       (* Uses cfe in ymerbrowserInt *)
       (# ff: ^astinterface.fragmentform do INNER exit ff[] #);
     getCurrentNode:<
       (* Uses cfe in ymerbrowserInt *)
       (# node: ^astinterface.ast do INNER exit node[] #);
     findAndSelect:<
       (* Uses
        * ymerbrowserInt.selectFragmentForm
        * ymerbrowserInt.cfe.setNode
        *)
       (# node: ^astInterface.ast enter node[] do INNER #);
     selectFragmentForm:<
        (* Uses ymerbrowserInt.selectFragmentForm *)
       (# ff: ^astInterface.fragmentForm enter ff[] do INNER #);
     selectFragmentGroup:<
       (* Uses ymerBrowserInt.selectFragmentGroup *)
       (# fgName: ^text enter fgName[] do INNER #);
     check:<
       (* Uses compiler.check in SifBrowserWindowExt *)
       (# fg: ^astInterface.fragmentGroup; status: @boolean
       enter fg[]
       do INNER
       exit status
       #);
     saveAsAndCheck:<
       (* Uses
        * check
        * selectFragmentGroup
        * ymerBrowserInt.cge.saveAs
        *)
       (# fg: ^astInterface.fragmentGroup; status: @boolean
       enter fg[]
       do INNER
       exit status
       #);
     betaenvVersion:< 
       (* Uses sifapplication.betaenvversion *)
       (# t: ^text do INNER exit t[] #);
     machineType:< 
       (* Uses global betaenv attribute *)
       (# t: ^text do INNER exit t[] #);
     infoView:< 
       (* Uses ymerbrowserInt.infoview *)
       (# t: ^text enter t[] do INNER #);
     infoViewClear:< 
       (* Uses ymerbrowserInt.infoview *)
       object;
     infoViewDone:<
       (* Uses ymerbrowserInt.infoview *)
       object;
     onAboutToCloseGroup:<
       (* Does nothing *)
       (#
          fg: ^astInterface.fragmentGroup;
          OKtoClose,askSave: @boolean
          (* if OKtoClose the groupEditor is closed.
           * if askSave the user is prompted whether to save the group
           * else it is just saved (if necessary)
           *)
       enter fg[]
       do true->OKtoClose->askSave; INNER
       exit (OKtoClose,askSave)
       #);
     onGroupSaved:<
       (* Updates the sourcebrowserview *)
       (# fg: ^astInterface.fragmentGroup;  enter fg[] do INNER #);
     onGroupTouched:<
       (* Updates the sourcebrowserview *)
       (# fg: ^astInterface.fragmentGroup;  enter fg[] do INNER #);
     onGroupDetouched:<
       (* Updates the sourcebrowserview *)
       (# fg: ^astInterface.fragmentGroup;  enter fg[] do INNER #);
     externalTextEdit:<
       (* Uses sifapplication.externalTextEdit *)
       (# textBefore,textAfter: ^text
       enter textBefore[]
       do INNER
       exit textAfter[]
       #);
     frejaInclude:< 
       (* Uses sifapplication.frejaInclude *)
       (# t: ^text do INNER exit t[] #);
     setFrejaMark:<
       (* Implemented by sifApplication *)
       (# frejamark: @boolean; FG: ^astInterface.fragmentGroup
       enter (frejamark,FG[])
       do INNER
       #);
     getFrejaMark:<
       (* Implemented by sifApplication *)
       (# frejamark: @boolean; FG: ^astInterface.fragmentGroup
       enter FG[]
       do INNER
       exit frejamark
       #);
     isEditor:<
       (* Specified by sifApplication *)
       booleanValue;
     callSearch:< 
       (* Implemented by sifApplication *)
       object;
     callReplace:<
       (* Implemented by sifApplication *)
       object;
     showSearchHits:<
       (* Implemented by sifApplication *)
       (#
          fg: ^astInterface.fragmentGroup;
          ff: ^astInterface.fragmentForm;
          node: ^astInterface.ast;
          t: ^text;
          caseSensitive,wholeWord,showAllHits,onlyApplications: @boolean;
          searchArea: @integer;
          theNameDcl: ^astInterface.ast
       enter
       (fg[],ff[],node[],t[],caseSensitive,wholeWord,searchArea,showAllHits,
        onlyApplications,theNameDcl[])
       do INNER
       #);
     replaceAll:<
       (* Implemented by sifApplication *)
       (#
          fg: ^astInterface.fragmentGroup;
          ff: ^astInterface.fragmentForm;
          node: ^astInterface.ast;
          t,newt: ^text;
          caseSensitive,wholeWord,replaceDeclaration,onlyApplications:
            @boolean;
          searchArea: @integer;
          theNameDcl: ^astInterface.ast
       enter
       (fg[],ff[],node[],t[],newt[],caseSensitive,wholeWord,replaceDeclaration,
        searchArea,onlyApplications,theNameDcl[])
       do INNER
       #);
     isActive:<
       (* Updates sourcebrowser *)
       (# value: @boolean enter value do INNER #);
     openSubeditor:<
       (* Implemented by sifApplication *)
       (# editorRoot,node: ^astInterface.ast
       enter (editorRoot[],node[])
       do INNER
       #);
     askShowDiagram:< booleanValue
       (* Implemented by sifApplication *)
       (# node: ^astInterface.ast enter node[] do INNER #);
     showDiagram:< 
       (* Implemented by sifApplication to call freja *)
       (# node: ^astInterface.ast enter node[] do INNER #);
     askEditUserinterface:< booleanValue
       (* Implemented by sifApplication to call frigg *)
       (# node: ^astInterface.ast enter node[] do INNER #);
     editUserinterface:<
       (* Implemented by sifApplication to call frigg *)
       (# frag: ^astInterface.fragmentForm; node: ^astInterface.ast
       enter (frag[],node[])
       do INNER
       #);
     OKtoTextedit:< (* ask userinterfacebuilder *) booleanValue
     (* Implemented by sifApplication to call frigg *)
     (# node: ^astInterface.ast enter node[] do INNER #);
     createImplementationFragment:<
       (* Implemented by sifApplication *)
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     selectImplementationFragment:<
       (* Implemented by sifApplication *)
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     quit:<
       (* Calls sourcebrowser *)
       object;
     allowDebug:< booleanValue
       (* ??? *)
       (# fg: ^astInterface.fragmentGroup; name: ^text
       enter fg[]
       do INNER
       exit name[]
       #);
     doDebug:<
       (* Implemented by SifApplication to call debugger *)
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     stopDebugging:< object;
     newCodeViewWindow:<
       (* ???? *)
       (# action1: ^object; callback: ^object; 
       enter callback[]
       do INNER
       exit action1[]
       #);
     showSemanticErrors:<
       (* Implemented by sifApplication *)
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #)
  #)  

