ORIGIN '~beta/mps/astlevel';
INCLUDE 'codeeditor'
        '~beta/sourcebrowser/projects';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-2000
 *   All rights reserved.
 *)
-- lib: Attributes --
sifInterface:
  (#
     isEditor:< BooleanObject;
     onBrowserSelectionChanged:< Object;
     openSubEditor:<
       (#
          parent: ^GUIenv.Window.Canvas;
          fe: ^codeeditor;
          editorast: ^astinterface.ast;
          
       enter (parent[],editorast[])
       do INNER
       exit fe[]
       #);
     check:< (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     hasProgramFG:< BooleanValue;
     compilerVersion:< (# t: ^text do INNER exit t[] #);
     compile:< BooleanValue
       (# fg: ^astInterface.fragmentGroup;  enter fg[] do INNER #);
     isExecutable:< BooleanValue
       (# fg: ^astInterface.fragmentGroup;  enter fg[] do INNER #);
     execute:<
       (# fg: ^astInterface.fragmentGroup;  enter fg[] do INNER #);
     debug:< booleanValue
       (# fg: ^astInterface.fragmentGroup;  enter fg[] do INNER #);
     debugProgramFGPossible:< BooleanValue;
     selectEditor:<
       (# fe: ^codeeditor enter fe[] do INNER #);
     openForm:<
       (# (* returns none if the form could not be opened in an editor *)
          name: ^text; node: ^astInterface.ast; fe: ^codeeditor
       enter (name[],node[])
       do INNER
       exit fe[]
       #);
     showDiagram:<
       (# node: ^astInterface.ast; newName: ^text
       enter node[]
       do INNER
       #);
     getEditorForForm:<
       (# (* exits none if no editor found *)
          ff: ^astInterface.fragmentForm;
          node: ^astInterface.ast;
          fe: ^codeeditor
       enter (ff[],node[])
       do INNER
       exit fe[]
       #);
     newBETAProgram:< booleanValue
       (# (* returns false if no editor created *) name: ^text
       enter name[]
       do INNER
       #);
     newBETALibrary:< booleanValue
       (# (* returns false if no editor created *) name: ^text
       enter name[]
       do INNER
       #);
     saveAndQuit:< (#  do INNER #);
     save:< (#  do INNER #);
     autoSave:< (#  do INNER #);
     checkAutoSaveFile:<
       (# fg: ^astInterface.fragmentGroup; didRecover: @boolean
       enter fg[]
       do INNER
       exit didRecover
       #);
     simplecheck:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     toggleDebug:< (#  do INNER #);
     addProp:<
       (#
          FG: ^astInterface.fragmentGroup;
          propName,propString: ^text;
          deleteIfExists: @boolean
       enter (FG[],propName[],propString[],deleteIfExists)
       do INNER
       #);
     deleteProp:< booleanValue
       (# (* returns true if property found *)
          FG: ^astInterface.fragmentGroup; propName,propString: ^text
       enter (FG[],propName[],propString[])
       do INNER
       #);
     setFrejaMark:<
       (#
          frejamark: @boolean;
          FG: ^astInterface.fragmentGroup;
          propvalue: @integer
       enter (frejamark,FG[])
       do INNER
       #);
     getFrejaMark:<
       (#
          FG: ^astInterface.fragmentGroup;
          GetProp:
            (#
               FG: ^astInterface.fragmentGroup;
               P: @Text;
               res: @integer;
               Txt: ^Text;
               
            enter (P,FG[])
            do
               &Text[]->Txt[];
               L: FG.prop.scanProp
                 (#
                    doProp:: 
                      (# 
                      do
                         (if (P[]->Prop.equal) then
                             scanParameters
                               (#
                                  doConst::  (#  do C->res; leave L #);
                                  doString::  (#  do S[]->Txt[]; leave L #)
                               #);
                             
                         if);
                         
                      #);
                    
                 #);
               INNER (* ugly *)
            exit res
            #)
       enter FG[]
       exit (1 = (('frejamark',FG[])->getProp))
       #)
  #)  

