ORIGIN '~beta/guienv/fields';
INCLUDE '~beta/guienv/utils/textfieldadds';
INCLUDE '~beta/guienv/keys';
INCLUDE 'private/codeRegexps';
BODY 'private/codePattern';

--windowLib: attributes--

codeTextField  : textField
  (# 
     
     init:<
       (# 
       do
          &indentRegexps[]->aux[];
          aux.init;
          INNER;
       #);
     
     
     aux: ^indentRegexps; 
     controlmodified: @boolean;          
     trace: (# exit false #);
     
     myScan: scantext (# exit ch #);     

     beginning_of_line:
       (# pos: @integer;   
       <<SLOT beginning: doPart>>
       exit pos
       #);
     
     next_line:
       (# pos: @integer;
       <<SLOT next: doPart>>
       exit pos
       #);
     
     previous_line:
       (# pos: @integer;
       <<SLOT previous: doPart>>
       exit pos
       #);
     
     end_of_line:
       (# pos: @integer;
       <<SLOT end: doPart>>
       exit pos
       #);
     
     current_line:
       (# t : ^text;
       <<SLOT current: doPart>>
       exit t[]
       #);
     
     last_line:
       (# result: @boolean;
       <<SLOT last: doPart>>
       exit result
       #); 
     
     first_line:  
       (# result: @boolean;
       <<SLOT first: doPart>>
       exit result
       #); 
     
     indent_by_int:
       (# indent: @integer;
       enter indent
       <<SLOT indent_by_int: doPart>> 
       #); 
     
     
     indent_line:<
       (# t: ^text;
          indent_value,pos,pos2: @integer;
       do 
          disableUpdate;
          selection->(pos,pos2);
          current_line->aux.eat_indent->(indent_value,t[]);
          
          (* We must calculate the start position *)
          beginning_of_line->pos2;
          (if (pos-pos2)>indent_value then pos-indent_value->pos2; if); 
          INNER;
          (if indent_value>0 then indent_value + pos2 -> pos2; if);
          (pos2,pos2)->selection;
          enableUpdate;
       #); 
     
     getBlockIndent:
       (# 
          blockNumber, blockIndent,count: @integer;
          mem1,mem2: @integer;
          t : ^text; 
          b,finished,firstBlock: @boolean;
          
          getBlockNumber:<
            (# do INNER
            exit blockNumber
            #);
          
       enter t[] 
       do
          selection->(mem1,mem2);
          toploop: cycle
            (# do 
               loop: cycle
                 (# do
                    (t[],getBlockNumber)->aux.RemoveBlock->(b,firstBlock);
                    (if not(b) then leave loop;
                     else (if firstBlock then 
                              (if count=0 then
                                  t.length->blockIndent;
                                  true->finished;
                                  leave loop;
                              if);
                              count-1->count;
                           else
                              count+1->count;                               
                          if); 
                    if); 
                 #); 
               (if finished then 
                   INNER getBlockIndent;
                   leave toploop;
                else 
                   (if first_line then
                       (* error: missing blockBegin *)
                       0->blockIndent;
                       leave toploop;
                   if);
                   previous_line;                    
                   current_line->t[];
               if);
            #); (* toploop *) 
          (mem1,mem2)->selection;
       exit blockIndent
       #); 
     
     
     eventhandler::<
       (# 
          enterPushed: @boolean;
          changingText: @boolean;
          
          onKeyDown::<
            (# spk: @SpecialKeys;
            do 
               (if trace then 'onKeyDown'->putline if);
               (if ch=13 then true->enterPushed; if);
               (if key= (spk.HOME) then 
                   beginning_of_line;
               if);
               (if key=(spk.END) then 
                   end_of_line; 
               if);
               controlkey -> controlmodified; 
               INNER
            #);
          
          onTextChanged::<
            (# 
            do
               (if trace and enterPushed then 'enterPushed'->putline; if);
               (if enterPushed and NOT(changingText) then  
                   true->changingText;
                   previous_line;
                   indent_line;
                   next_line;
                   beginning_of_line; 
                   indent_line;
                   false->enterPushed->changingText;
               if); 
               INNER
            #);
          
          onBeforeChange::<
            (# T,t1: ^text;
               ch1: @char;
               indent: @integer;
            do 
               (if trace then 'onBeforeChange' -> putline; if); 
               (if (trace and controlmodified) then 
                   'Control' -> putline; 
               if);
               theText -> T[];
               (if (t[]<>NONE) AND (T.lgth=1) then
                   T.T[1] -> ch1;
                   (if trace then 
                       'her 3'->screen.putline;
                       'ch1: '->screen.puttext; 
                       ch1->screen.putint; 
                       screen.newline;
                   if);
                   (if ch1
                    // ascii.ht then 
                       (if trace then 'Tab' -> screen.putline; if);
                       indent_line;
                       false -> allow;
                    // 'a'
                    // 'A' then 
                       (if controlmodified then 
                           beginning_of_line; 
                           false->allow;
                       if);
                    // 'e'
                    // 'E' then
                       (if controlmodified then 
                           end_of_line; 
                           false->allow; 
                       if);
                    // 'p'
                    // 'P' then
                       (if controlmodified then 
                           previous_line; 
                           false->allow;
                       if);
                    // 'n'
                    // 'N' then
                       (if controlmodified then 
                           next_line; 
                           false->allow;
                       if);
                    // 10 then
                       true->enterPushed;
                    else INNER
                   if);
                else INNER
               if); 
            #);
       #);
  #);
