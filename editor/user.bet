ORIGIN '~beta/editor/codeeditor';
INCLUDE '~beta/mps/grammarinit'
        'mylangcfl'
        'usernotifications';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-2000
 *   All rights reserved.
 *)
-- userOnOpenCodeeditor: Descriptor --
(# 
do
   edenv.uep.mylangCFL[]->up.mylangCFL[];
   up.userPopUPmenu.open;
   'attach my observer to ff subject'->putLine;
   
   &up.ffObserverType[] -> up.ffObserver[] -> frag.attachObserver;

   'attach my observer to my subject'->putLine;
   
   &up.userObserverType[] -> up.userObserver[] -> frag.attachObserver;
#)  

-- userPrivate: Descriptor --
(#
   mylangCFL: ^astInterface.mylang;
   userPopUpMenu: @mysimplemenu
     (#
        iUserCommand: @item
          (#
             onStatus:: (#  do true->value #);
             onSelect::
               (# 
               do
                  'user command'->putLine;
                  frag.notifyUserEvent
               #)
          #);
        open:: (#  do iUserCommand.new #)
     #);
   ffObserverType: frag.sifFragmentFormObserver
     (#
        onRefresh::< (#  do 'user onRefresh'->putLine #);
        onFocusChanged::< (#  do 'user onFocusChanged'->putLine #);
        onAstReplaced::< (#  do 'user onAstReplaced'->putLine #);
        onAstReplacedSequence::<
          (#  do 'user onAstReplacedSequence'->putLine #);
        onListElementInserted::<
          (#  do 'user onListElmentInserted'->putLine #);
        onListElementsDeleted::<
          (#  do 'user onListElementDeleted'->putLine #);
        onListElementsReplaced::<
          (#  do 'user onListElementsReplaced'->putLine #)
     #);
   ffObserver: ^ffObserverType;
   userObserverType: frag.userFragmentFormObserver
     (# onUserEvent::< (#  do 'user onUserEvent'->putLine #) #);
   userObserver: ^userObserverType;
   ifInWhile:
     (#
        fatherNode: ^astInterface.expanded;
        anAST: ^astInterface.ast;
        sonNo: @integer;
        aWhile: ^mylangCFL.while;
        theStatementList: ^mylangCFL.statementList;
        anIff: ^mylangCFL.iff;
        unexp: ^astinterface.unexpanded
     do
        'ifInWhile'->putLine;
        cs.node.father->fatherNode[];
        cs.node.sonNo->sonNo;
        (mylangCFL.while,frag[])->mylangCFL.newAST->aWhile[];
        (sonNo,aWhile[])->fatherNode.put;
        cs.node[]->anIff[];
        (mylangCFL.statementList,frag[])->mylangCFL.newAST->theStatementList[];
        (2,theStatementList[])->aWhile.put;
        (1,anIff[])->theStatementList.insert;
        aWhile.getCondition->unexp[];
        10 (* exp *) ->unexp.nonterminalSymbol;
        (anIff[],aWhile[])->frag.notifyAstReplaced;
        ((aWhile[],1,0,0))->frag.notifyFocusChanged;
        ((aWhile[],1,0,0))->localOnFocusChanged
     #)
#)  

-- userOnOpenGroupeditor: Descriptor --
(# 
do &ugp.fgObserverType[] -> ugp.fgObserver[] -> fg.attachObserver;
   &ugp.userObserverType[] -> ugp.userObserver[] -> fg.attachObserver;
#)  

-- userGroupEditorPrivate: Descriptor --
(#
   fgObserverType: fg.sifFragmentGroupObserver
     (#
        onNameChanged::< (#  do 'user onNameChanged'->putLine #);
        onFragmentInserted::< (#  do 'user onFragmentInserted'->putLine #);
        onFragmentDeleted::< (#  do 'user onFragmentDeleted'->putLine #);
        onPropertiesChanged::<
          (#  do 'user onPropertiesChanged'->putLine #);
        onGroupSaved::< (#  do 'user onGroupSaved'->putLine #);
        onGroupAutoSaved::< (#  do 'user onGroupAutoSaved'->putLine #);
        onGroupChecked::< (#  do 'user onGroupChecked'->putLine #);
        onGroupLocked::< (#  do 'user onGroupLoced'->putLine #);
        onGroupUnlocked::< (#  do 'user onGroupUnLocked'->putLine #);
        onBeforeGroupClose::< (#  do 'user onBeforeGroupClose'->putLine #);
        onGroupClosed::< (#  do 'user onGroupClosed'->putLine #);
        onRefresh::< (#  do 'user onRefresh'->putLine #)
     #);
   fgObserver: ^fgObserverType;
   userObserverType: fg.userFragmentGroupObserver
     (# onUserGroupEvent::< (#  do 'user onUserGroupEvent'->putLine #)
     #);
   userObserver: ^userObserverType
#)  

-- userEditorenvPrivate: Descriptor --
(#
   global: @integer;
   mylangCFL: ^astInterface.mylang;
   userGrammarInit:
     (# 
     do
        &mps.mylang[]->mylangCFL[];
        ('~beta/grammars/mylang/mylang','mylang',screen[])
          ->mylangCFL.grammarInit
            (#
               MPSerror:: (#  do (failure,msg[])->stop #);
               noParserAvailable:: (#  do (failure,msg[])->stop #)
            #)
     #)
do 
#)  

-- usernotificationOnDoubleClick: Descriptor --
(#  do 'doubleClick'->putLine #)  

-- usernotificationOnPopUpButton: Descriptor --
(# 
do
   (if not cs.isUnexpanded then
       true->doneInInner;
       (1,pos,theSifTextEditor.contents[])->up.userPopupMenu.popUp
   if)
#)  

