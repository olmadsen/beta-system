ORIGIN '~beta/editor/v5.0.1/codeeditor';
INCLUDE 'mylangcfl';
-- userInit: Descriptor --
(#  do edenv.uep.mylangCFL[]->up.mylangCFL[]; up.userPopUPmenu.open #)  

-- userPrivate: Descriptor --
(#
   mylangCFL: ^astInterface.mylang;
   userPopUpMenu: @mysimplemenu
     (#
        iUserCommand: @item
          (#
             onStatus::  (#  do true->value #);
             onSelect::  (#  do 'user command'->putLine #)
          #);
        open::  (#  do iUserCommand.new #)
     #);
   ifInWhile:
     (#
        fatherNode: ^astInterface.expanded;
        anAST: ^astInterface.ast;
        sonNo: @integer;
        aWhile: ^mylangCFL.while;
        theStatementList: ^mylangCFL.statementList;
        anIff: ^mylangCFL.iff;
        unexp: ^astinterface.unexpanded
     do
        'ifInWhile'->putLine;
        cs.node.father->fatherNode[];
        cs.node.sonNo->sonNo;
        (mylangCFL.while,frag[])->mylangCFL.newAST->aWhile[];
        (sonNo,aWhile[])->fatherNode.put;
        cs.node[]->anIff[];
        (mylangCFL.statementList,frag[])->mylangCFL.newAST->theStatementList[];
        (2,theStatementList[])->aWhile.put;
        (1,anIff[])->theStatementList.insert;
        aWhile.getCondition->unexp[];
        10 (* exp *) ->unexp.nonterminalSymbol;
        (true,anIff[],aWhile[])->astReplacedEvent;
        (true,(aWhile[],1,0,0))->focusChangedEvent
     #)
#)  

-- userEditorenvPrivate: Descriptor --
(#
   global: @integer;
   mylangCFL: ^astInterface.mylang;
   userGrammarInit:
     (# grammarWithPath,help: ^text; gg,ggg: ^astInterface.fragmentGroup; bobsfile: @file
     do
        &mps.mylang[]->mylangCFL[];
        mylangCFL.init;
        '~beta/grammars/mylang/v1.2/mylang'->grammarWithPath[];
        (* to open mylang-meta.ast: *)
        ('-meta'->grammarWithPath.copyAppend,screen[])->mps.top.open->gg[];
        (if gg[]
         // none then
            'Could not open: '->screen.putText;
            '-meta'->grammarWithPath.copyAppend->screen.putLine;
            (failure,'')->stop;
            
         else
            ('mylang',screen[])->gg.open->mylangCFL.grammarAst[];
            (if mylangCFL.grammarAst[]
             // none then
                'No grammarAst for '->screen.putText;
                grammarWithPath[]->screen.putLine;
                (failure,'')->stop;
                
             else
                grammarWithPath[]->mps.expandToFullPath->help[];
                '-parser'->help.append;
                mps.parserFileExtension->help.append;
                help[]->bobsFile.name;
                (if bobsFile.entry.exists then
                    bobsFile.name->mylangCFL.parser.initialize; 
                 else
                    'No parser available: '->screen.putText; bobsFile.name->screen.putLine; 
                if)
            if);
            
        if)
     #)
do 
#)  

-- usernotificationfragmentChanged: Descriptor --
(#  do  #)  

-- usernotificationAstReplaced: Descriptor --
(#  do 'astReplacedEvent'->putLine #)  

-- usernotificationAstReplacedSequence: Descriptor --
(#  do 'astReplacedSequencdEvent'->putLine #)  

-- usernotificationListElementInserted: Descriptor --
(#  do 'listElementInsertedEvent'->putLine #)  

-- usernotificationListElementsDeleted: Descriptor --
(#  do 'listElementDeletedEvent'->putLine #)  

-- usernotificationListElementsReplaced: Descriptor --
(#  do 'listElementsReplacedEvent'->putLine #)  

-- usernotificationRefresh: Descriptor --
(#  do 'refresh'->putLine #)  

-- usernotificationFocusChanged: Descriptor --
(# 
do
   'focusChangedEvent'->putLine;
   cs.node[]->astObserver;
   (if (up[] <> none ) and (up.mylangCFL[] <> none ) then
       (if cs.node.symbol = up.mylangCFL.iff then 'It is an if statement'->putLine if)
   if)
#)  

-- usernotificationOnDoubleClick: Descriptor --
(#  do 'doubleClick'->putLine #)  

-- usernotificationOnPopUpButton: Descriptor --
(# 
do
   (if not cs.isUnexpanded then
       true->doneInInner; (1,pos,theSifTextEditor.contents[])->up.userPopupMenu.popUp
   if)
#)  

