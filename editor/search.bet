ORIGIN '~beta/sourcebrowser/ymer';
INCLUDE '~beta/guienv/utils/pane'
        '~beta/guienv/utils/listview';
-- browserlib: Attributes --
searchBrowser: ymerLocalWindow
  (#
     checkNode:<
       (# node: ^astInterface.ast; value: @boolean; t: ^text
       enter node[]
       do INNER
       exit (value,t[])
       #);
     setTitle:<
       (# noHits: @boolean; t: ^text
       enter noHits
       do INNER ; t[]->title
       exit t[]
       #);
     setNodesListLabel:< (# t: ^text do INNER exit t[] #);
     showSourceLine:< booleanValue;
     clear: contents.clear (#  #);
     fg: ^mps.AST.fragmentGroup;
     showAllHits: @boolean;
     fgName: ^text;
     (* isWarning:
      *        {* findes i mps/v5.0.ess.ess.ess - blot her for at
      *         * kunne filtrere warnings fra allerede i
      *         * v5.0.1 - skal fjernes senere
      *         *}
      *        (# no: @integer; boo: @boolean
      *        enter no
      *        do
      *           (if no
      *            //24//25//86//95//97//99//106//109
      *            //26 {*not used *} then
      *               true->boo
      *           if)
      *        exit boo
      *        #);
      *)
     includeWarnings: @boolean;
     noNodes: @boolean;
     aspect:< integerValue (#  do 6->value; INNER #);
     markCurrentNode: (#  do contents.nodesList.markCurrent #);
     unmarkCurrentNode: (#  do contents.nodesList.unmarkCurrent #);
     contents: @contentsType;
     contentsType:< pane
       (#
          clear: (#  do FFlist.clear; nodesList.clear #);
          dobind:<
            (# 
            do
               TRUE->bindTop->bindLeft->bindRight->bindBottom;
               FFlist.dobind;
               nodesList.dobind;
               INNER dobind
            #);
          onNewfragmentForm:<
            (# ff: ^mps.AST.fragmentForm
            enter ff[]
            do INNER onNewfragmentForm
            #);
          onSelectFragmentForm:<
            (# ff: ^mps.AST.fragmentForm
            enter ff[]
            do INNER onSelectFragmentForm
            #);
          onSelectNode:<
            (# node: ^mps.AST.ast;  enter node[] do INNER onSelectNode #);
          FFlist: @labelled
            (#
               clear: contents.clear (#  #);
               dobind: (#  do contents.dobind #);
               contentsType:: listView
                 (#
                    clear:
                      (# 
                      do
                         listitems.scanReverse
                           (#  do current.delete #);
                         listitems.clear;
                         
                      #);
                    current: ^listitems.theCellType;
                    dobind:
                      (# 
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom
                      #);
                    eventhandler:: 
                      (#
                         onKeyDown:: 
                           (# 
                           do
                              (if ch
                               // ascii.cr // ascii.nl then
                                  (if current[] = none then
                                      listitems.head->current[]; 
                                   else
                                      current.succ[]->current[];
                                      (if current[] = none then
                                          listitems.head->current[]
                                      if);
                                      
                                  if);
                                  (if current[] <> none then
                                      current.elm.select;
                                      selection.scrollIntoView
                                  if)
                              if)
                           #)
                      #);
                    ffItem: listItem
                      (#
                         ff: ^mps.AST.fragmentForm;
                         firstTime: @boolean;
                         nodes: @list (# element:: node #);
                         onSelect:: 
                           (# fi: ^listitems.theCellType
                           do
                              THIS(ffItem)[]->listitems.at->fi[];
                              (if fi[] <> current[] then
                                  fi[]->current[];
                                  THIS(ffItem)[]->nodesList.contents.setView;
                                  (if firstTime then
                                      false->firstTime; ff[]->onNewfragmentForm
                                   else
                                      ff[]->onSelectFragmentForm
                                  if)
                              if)
                           #);
                         
                      #);
                    node: (# node: ^mps.AST.ast; theText: ^text;  #);
                    registerNodes:
                      (#
                         exp: ^mps.AST.expanded;
                         n: ^node;
                         theffItem: ^ffList.contents.ffItem;
                         OK: @boolean;
                         t: ^text;
                         
                      enter theffItem[]
                      do
                         (if theffItem.ff.root.kind = mps.AST.kinds.interior
                          then
                             theffItem.ff.root[]->exp[];
                             exp.suffixWalk
                               (# 
                               do
                                  current[]->checkNode->(OK,t[]);
                                  (if OK then
                                      &node[]->n[];
                                      current[]->n.node[];
                                      (if t[] <> none then
                                          t.copy->n.theText[]
                                      if);
                                      n[]->theffItem.nodes.append
                                  if)
                               #)
                         if);
                         (if n[] = none then
                             true->noNodes
                          else
                             (listitems.head).elm.select
                         if)
                      #);
                    registerFragmentForms:
                      (#
                         exp: ^mps.AST.expanded;
                         fi: ^ffItem;
                         fg: ^mps.AST.fragmentGroup;
                         ff: ^mps.AST.fragmentForm;
                         OK: @boolean
                      enter fg[]
                      do
                         true->noNodes;
                         none ->nodesList.contents.current[];
                         (if fg[] <> none then
                             fg.fragmentList.scan
                               (# 
                               do
                                  (if current.type
                                   // mps.AST.formType then
                                        (# 
                                        do
                                           current.open->ff[];
                                           (if ff[] <> none then
                                               (if ff.root.kind =
                                               mps.AST.kinds.interior then
                                                   ff.root[]->exp[];
                                                   l: exp.suffixWalk
                                                     (# t,dummy: ^text
                                                     do
                                                        current[]->checkNode
                                                          ->(OK,dummy[]);
                                                        (if OK then
                                                            &ffItem[]->fi[];
                                                            fi.init;
                                                            true->fi.firstTime;
                                                            ff[]->fi.ff[];
                                                            fgName.copy->t[];
                                                            '-'->t.append;
                                                            ff.name->t.append;
                                                            t[]->fi.name;
                                                            fi[]->registerNodes;
                                                            leave l
                                                        if)
                                                     #)
                                               if)
                                            else
                                               'registerFragmentForms: could not open fragmentform'
                                                 ->putLine
                                           if)
                                        #)
                                  if)
                               #);
                             (if fi[] = none then
                                 ''->nodesList.label; true->noNodes; 
                              else
                                 setNodesListLabel->nodesList.label;
                                 false->noNodes;
                                 (listitems.head).elm.select
                             if)
                         if)
                      #);
                    open::  (#  do 'Fragment Forms'->label #);
                    
                 #);
               open:: 
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (w,(h div aspect)-1)->size;
                    
                 #);
               
            #);
          nodesList: @labelled
            (#
               clear: contents.clear (#  #);
               dobind: (#  do contents.dobind #);
               markCurrent:
                 (# t: ^text
                 do
                    (if contents.current[] <> none then
                        contents.current.elm.name->t[];
                        (if (1->t.inxget) <> '*' then
                            '*'->t.prepend; t[]->contents.current.elm.name
                        if)
                    if)
                 #);
               unmarkCurrent:
                 (# t: ^text
                 do
                    (if contents.current[] <> none then
                        contents.current.elm.name->t[];
                        (if (1->t.inxget) = '*' then
                            (1,1)->t.delete; t[]->contents.current.elm.name
                        if)
                    if)
                 #);
               contentsType:: listView
                 (#
                    clear:
                      (# 
                      do
                         listitems.scanReverse
                           (#  do current.delete #);
                         listitems.clear;
                         
                      #);
                    current: ^listitems.theCellType;
                    dobind:
                      (# 
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom
                      #);
                    eventhandler:: 
                      (#
                         onKeyDown:: 
                           (# 
                           do
                              (if ch
                               // ascii.cr // ascii.nl then
                                  (if current[] = none then
                                      listitems.head->current[]; 
                                   else
                                      current.succ[]->current[];
                                      (if current[] = none then
                                          listitems.head->current[]
                                      if);
                                      
                                  if);
                                  (if current[] <> none then
                                      current.elm.select;
                                      selection.scrollIntoView;
                                      
                                  if)
                              if)
                           #)
                      #);
                    l1,c1,l2,c2: @integer;
                    nodeItem: listItem
                      (#
                         node: ^mps.AST.ast;
                         itemTextUpdated: @boolean;
                         theNode: ^fflist.contents.node;
                         onSelect:: 
                           (# currentPosition: @integer
                           do
                              THIS(nodeItem)[]->listitems.at->current[];
                              position->currentposition;
                              (if not itemTextUpdated then
                                  (if browser.cfe[] <> none then
                                      (node.frag.root[],node[],false)
                                        ->browser.selectNode;
                                      node[]->cfe.openContractionsUntilRoot;
                                      cfe.doReprettyprint;
                                      (node[],1,0,0)->cfe.selectArea
                                        ->(l1,c1,l2,c2)
                                        ->cfe.astView.out.selectArea;
                                      3 (* search hit *) ->cfe.setTextStyle;
                                      (if not
                                      ((l1 = 1) and (c1 = 1) and (l2 = 1) and
                                       (c2 = 1)) then
                                          l1->cfe.theSifTexteditor.getLine
                                            ->name;
                                          true->itemTextUpdated
                                      if)
                                  if)
                              if);
                              node[]->onSelectNode;
                              (currentposition,false)->selection.select
                           #);
                         
                      #);
                    setView:
                      (#
                         exp: ^mps.AST.expanded;
                         ei: ^nodeItem;
                         theffItem: ^ffList.contents.ffItem;
                         OK: @boolean;
                         t: ^text;
                         
                      enter theffItem[]
                      do
                         clear;
                         theffItem.nodes.scan
                           (# 
                           do
                              &nodeItem[]->ei[];
                              ei.init;
                              current.node[]->ei.node[];
                              current[]->ei.theNode[];
                              (if current.theText[] <> none then
                                  current.theText.copy->ei.name
                              if)
                           #);
                         (if ei[] = none then
                             true->noNodes
                          else
                             (listitems.head).elm.select
                         if)
                      #);
                    open::  (#  do setNodesListLabel->label #);
                    
                 #);
               open:: 
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (w,(h div aspect)-1)->size;
                    
                 #)
            #);
          open::< 
            (#
               setDefaults:: 
                 (#  do true->verticalStacking; 50->minsize; 2->panewidth #)
            do
               FFlist.open;
               nodesList.open;
               FFlist[]->appendMember;
               nodesList[]->appendMember;
               INNER open;
               appendsDone;
               dobind;
               
            #)
       #);
     clearBrowser: (#  do clear; contents.FFlist.clear #);
     registerFragmentGroup:
       (# fgSet: @set (# element:: mps.ast.fragmentgroup #)
       enter fg[]
       do
          clearBrowser;
          (if fg[] <> none then
              fg.name->fgName[];
              fg[]->contents.FFlist.contents.registerFragmentForms;
              noNodes->setTitle
           else
              (if browser.currentProject[] <> none then
                  browser.currentProject.groups.scan
                    (# fg: ^astinterface.fragmentGroup
                    do
                       current.getfg->fg[];
                       (if fg[] <> none then
                           (if not (fg[]->fgSet.has) then
                               fg[]->fgSet.insert;
                               fg.name->fgName[];
                               fg[]
                                 ->
                                   contents.FFlist.contents.
                                     registerFragmentForms
                           if)
                       if)
                    #);
                  'Search hits in current project'->title
               else
                  'No current project'->title
              if)
          if);
          
       #);
     type::  (#  do (*windowTypes.palette->value*)  #);
     open::< 
       (# w,h: @integer; 
       do hide; INNER open; contents.open; fg[]->registerFragmentGroup; show; 
       #)
  #);
searchViewer: searchBrowser
  (#
     aspect::  (#  do 2->value #);
     contentsType::< 
       (#
          onNewfragmentForm::< 
            (# l1,c1,l2,c2: @integer
            do
               (if showAllHits then
                   ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
                   (if browser.cfe[] <> none then
                       contents.nodesList.contents.listitems.scan
                         (# i: ^contents.nodesList.contents.nodeItem
                         do
                            current[]->i[];
                            i.node[]->cfe.openContractionsUntilRoot
                         #);
                       cfe.doReprettyprint;
                       contents.nodesList.contents.listitems.scan
                         (# i: ^contents.nodesList.contents.nodeItem
                         do
                            current[]->i[];
                            (i.node.frag.root[],i.node[],false)
                              ->browser.selectNode;
                            (if not i.itemTextUpdated then
                                (i.node[],1,0,0)->cfe.selectArea->(l1,c1,l2,c2)
                                  ->cfe.astView.out.selectArea;
                                3 (* search hit *) ->cfe.setTextStyle;
                                (if not
                                ((l1 = 1) and (c1 = 1) and (l2 = 1) and
                                 (c2 = 1)) then
                                    l1->cfe.theSifTexteditor.getLine->i.name;
                                    true->i.itemTextUpdated
                                if)
                            if)
                         #);
                       (if contents.nodesList.contents.listItems.size > 0 then
                           (contents.nodesList.contents.listitems.head).elm.
                             select
                       if)
                   if)
               if);
               INNER
            #);
          onSelectFragmentForm::< 
            (# 
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               INNER
            #);
          onSelectNode::< 
            (# 
            do
               (node.frag.root[],node[],false)->browser.selectNode;
               3 (* search hit *) ->cfe.setTextStyle
            #)
       #);
     menubarType::< 
       (#
          fileMenu: @menu
            (#
               reloadItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do fg[]->registerFragmentGroup #)
                      #);
                    open::  (#  do 'r'->key; 'Reload'->name #)
                 #);
               quitItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do THIS(searchBrowser).close #)
                      #);
                    open::  (#  do 'w'->key; 'Close'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'File'->name;
                    reloadItem.open;
                    reloadItem[]->append;
                    quitItem.open;
                    quitItem[]->append
                 #)
            #);
          open::<  (#  do fileMenu.open; fileMenu[]->append #);
          
       #);
     open::<  (#  do (500,300)->size; INNER open;  #);
     
  #);
mySemanticViewer: searchBrowser
  (#
     aspect::  (#  do 2->value #);
     contentsType::< 
       (#
          onNewfragmentForm::< 
            (# l1,c1,l2,c2: @integer; msg: ^text
            do
               ((ff.father).fullname,ff.name)->selectFragmentForm;
               (if cfe[] <> none then
                   contents.nodesList.contents.listitems.scan
                     (# i: ^contents.nodesList.contents.nodeItem
                     do current[]->i[]; i.node[]->cfe.openContractionsUntilRoot
                     #);
                   cfe.doReprettyprint;
                   contents.nodesList.contents.listitems.scan
                     (# i: ^contents.nodesList.contents.nodeItem
                     do
                        current[]->i[];
                        (if not i.itemTextUpdated then
                            (i.node[],1,0,0)->cfe.selectArea->(l1,c1,l2,c2)
                              ->cfe.astView.out.selectArea;
                            2 (* semantic error *) ->cfe.setTextStyle;
                            (if not
                            ((l1 = 1) and (c1 = 1) and (l2 = 1) and (c2 = 1))
                             then
                                i.name->msg[];
                                ': '->msg.append;
                                l1->cfe.theSifTexteditor.getLine->msg.append;
                                msg[]->i.name;
                                msg[]->i.theNode.theText[];
                                true->i.itemTextUpdated
                            if)
                        if)
                     #);
                   (if contents.nodesList.contents.listItems.size > 0 then
                       (contents.nodesList.contents.listitems.head).elm.select
                   if)
               if);
               INNER
            #);
          onSelectFragmentForm::< 
            (# 
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               INNER
            #);
          onSelectNode::< 
            (# 
            do
               (node.frag.root[],node[],false)->browser.selectNode;
               2 (* semantic error *) ->cfe.setTextStyle
            #)
       #);
     menubarType:: 
       (#
          fileMenu: @menu
            (#
               reloadItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do fg[]->registerFragmentGroup #)
                      #);
                    open::  (#  do 'r'->key; 'Reload'->name #)
                 #);
               quitItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do THIS(mySemanticViewer).close #)
                      #);
                    open::  (#  do 'w'->key; 'Close'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'File'->name;
                    reloadItem.open;
                    reloadItem[]->append;
                    quitItem.open;
                    quitItem[]->append
                 #)
            #);
          markMenu: @menu
            (#
               markItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do markCurrentNode #) #);
                    open::  (#  do 'm'->key; 'Mark as corrected'->name #)
                 #);
               unmarkItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do unmarkCurrentNode #) #);
                    open::  (#  do 'u'->key; 'Unmark'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'Mark'->name;
                    markItem.open;
                    markItem[]->append;
                    unmarkItem.open;
                    unmarkItem[]->append
                 #)
            #);
          warningsMenu: @menu
            (#
               includeItem: @menuItem
                 (#
                    eventhandler:: 
                      (#
                         onSelect:: 
                           (# 
                           do
                              not checked->checked->includeWarnings;
                              fg[]->registerFragmentGroup
                           #)
                      #);
                    open::  (#  do 'i'->key; 'Include'->name #);
                    
                 #);
               open:: 
                 (# 
                 do 'Warnings'->name; includeItem.open; includeItem[]->append; 
                 #);
               
            #);
          open:: 
            (# 
            do
               fileMenu.open;
               fileMenu[]->append;
               warningsMenu.open;
               warningsMenu[]->append;
               markMenu.open;
               markMenu[]->append
            #);
          
       #);
     open::<  (#  do (500,300)->size; INNER open;  #);
     
  #)  

