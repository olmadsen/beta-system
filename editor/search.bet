ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/pane'
        '~beta/betaast/semanticerrortext'
        '~beta/guienv/utils/listview'
        '~beta/sourcebrowser/ymerInterface'
        '~beta/sourcebrowser/codemoveable'
        '~beta/guienv/utils/private/promptsbody'
        '~beta/guienv/utils/auxcontrols';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-2000
 *   All rights reserved.
 *)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-2000
 *   All rights reserved.
 *)
-- guienvlib: Attributes --
searchBrowser: Window
  (#
     browser: ^ymerBrowserInterface;
     mps: ^ymermps;
     checkNode:<
       (#
          node: ^astInterface.ast; value: @boolean; t: ^text; type: @integer
       enter node[]
       do INNER
       exit (value,t[],type)
       #);
     setTitle:<
       (# noHits: @boolean; t: ^text
       enter noHits
       do INNER ; t[]->title
       exit t[]
       #);
     setNodesListLabel:< (# t: ^text do INNER exit t[] #);
     showSourceLine:< booleanValue;
     checkAll,doNotCheckAll,cancelCommand: @boolean;
     isSemanticErrorViewer:< booleanValue;
     checkGroupStatus:<
       (# fg: ^astInterface.fragmentGroup; value: @boolean
       enter fg[]
       do true->value; INNER
       exit value
       #);
     fgExcludeSet: @set (# element:: browser.mps.ast.fragmentgroup #);
     noOfHits: @integer;
     noOfFiles: (#  exit fgSet.size #);
     fgSet: @set (# element:: browser.mps.ast.fragmentgroup #);
     clear: contents.clear (#  #);
     fg: ^astInterface.fragmentGroup;
     ff: ^astInterface.fragmentForm;
     node: ^astInterface.ast;
     searchArea: @integer;
     showAllHits: @boolean;
     fgName: ^text;
     (* isWarning:
      *        {* findes i mps/v5.0.ess.ess.ess - blot her for at
      *         * kunne filtrere warnings fra allerede i
      *         * v5.0.1 - skal fjernes senere
      *         *}
      *        (# no: @integer; boo: @boolean
      *        enter no
      *        do
      *           (if no
      *            //24//25//86//95//97//99//106//109
      *            //26 {*not used *} then
      *               true->boo
      *           if)
      *        exit boo
      *        #);
      *)
     includeWarnings: @boolean;
     noNodes: @boolean;
     searchDone: @boolean;
     aspect:< integerValue (#  do 6->value; INNER #);
     markCurrentNode: (#  do contents.nodesList.markCurrent #);
     unmarkCurrentNode: (#  do contents.nodesList.unmarkCurrent #);
     contents: @contentsType;
     contentsType:< pane
       (#
          clear:
            (# 
            do FFlist.clear; nodesList.clear; 0->noOfHits; fgSet.clear
            #);
          dobind:<
            (# 
            do
               TRUE->bindTop->bindLeft->bindRight->bindBottom;
               FFlist.dobind;
               nodesList.dobind;
               INNER dobind
            #);
          onNewfragmentForm:<
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do INNER onNewfragmentForm
            #);
          onSelectFragmentForm:<
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do INNER onSelectFragmentForm
            #);
          onSelectNode:<
            (# node: ^astInterface.ast; 
            enter node[]
            do INNER onSelectNode
            #);
          FFlist: @labelled
            (#
               clear: contents.clear (#  #);
               dobind: (#  do contents.dobind #);
               contentsType:: listView
                 (#
                    printList:
                      (# 
                      do
                         newLine;
                         'Search hits: '->putText;
                         noOfHits->putInt;
                         (if noOfHits = 1 then
                             ' hit in '->putText
                          else
                             ' hits in '->putText
                         if);
                         noOfFiles->putInt;
                         (if noOfFiles = 1 then
                             ' file'->putLine
                          else
                             ' files'->putLine
                         if);
                         listItems.scan
                           (# ffi: ^ffItem
                           do
                              current[]->ffi[];
                              '-----------------------------------------------------------------------------------------------------'
                                ->putLine;
                              ffi.name->putLine;
                              ffi.nodes.scan
                                (# aNode: ^node
                                do current[]->aNode[]; aNode.theText[]->putLine
                                #)
                           #)
                      #);
                    selectFirst:
                      (# firstFFitem: ^ffItem
                      do
                         (if current[] <> none then
                             current.elm[]->firstFFitem[];
                             none ->current[];
                             firstFFitem.onSelect
                         if)
                      #);
                    clear:
                      (# 
                      do
                         listitems.scanReverse
                           (#  do current.delete #);
                         listitems.clear;
                         
                      #);
                    current: ^listitems.theCellType;
                    dobind:
                      (# 
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom
                      #);
                    eventhandler:: 
                      (#
                         onKeyDown:: 
                           (# 
                           do
                              (if ch
                               // ascii.cr // ascii.nl then
                                  (if current[] = none then
                                      listitems.head->current[]; 
                                   else
                                      current.succ[]->current[];
                                      (if current[] = none then
                                          listitems.head->current[]
                                      if);
                                      
                                  if);
                                  (if current[] <> none then
                                      current.elm.select;
                                      selection.scrollIntoView
                                  if)
                              if)
                           #)
                      #);
                    ffItem: listItem
                      (#
                         astRoot: ^astInterface.ast;
                         firstTime: @boolean;
                         nodes: @list (# element:: node #);
                         onSelect:: 
                           (# fi: ^listitems.theCellType
                           do
                              THIS(ffItem)[]->listitems.at->fi[];
                              (if fi[] <> current[] then
                                  fi[]->current[];
                                  THIS(ffItem)[]->nodesList.contents.setView;
                                  (if firstTime then
                                      false->firstTime;
                                      (if searchDone then
                                          astRoot.frag[]->onNewfragmentForm
                                       else
                                          true->firstTime
                                      if)
                                   else
                                      astRoot.frag[]->onSelectFragmentForm
                                  if)
                              if)
                           #);
                         
                      #);
                    node:
                      (#
                         node: ^astInterface.ast;
                         theText: ^text;
                         type: @integer;
                         
                      #);
                    registerNodes:
                      (#
                         exp: ^astInterface.expanded;
                         n: ^node;
                         theffItem: ^ffList.contents.ffItem;
                         OK: @boolean;
                         t: ^text;
                         type: @integer;
                         
                      enter theffItem[]
                      do
                         (if theffItem.astRoot.kind = mps.AST.kinds.interior
                          then
                             theffItem.astRoot[]->exp[];
                             exp.suffixWalk
                               (# 
                               do
                                  current[]->checkNode->(OK,t[],type);
                                  (if OK then
                                      &node[]->n[];
                                      current[]->n.node[];
                                      (if t[] <> none then
                                          t.copy->n.theText[]
                                      if);
                                      type->n.type;
                                      n[]->theffItem.nodes.append;
                                      noOfHits+1->noOfHits
                                  if)
                               #)
                         if);
                         (if n[] = none then
                             true->noNodes
                          else
                             (listitems.head).elm.select
                         if)
                      #);
                    registerFragmentForms:
                      (#
                         exp: ^astInterface.expanded;
                         fi: ^ffItem;
                         searchfg: ^astInterface.fragmentGroup;
                         ff,searchff: ^astInterface.fragmentForm;
                         searchNode,searchRoot: ^astInterface.ast;
                         OK: @boolean
                      enter (searchfg[],searchff[],searchNode[])
                      do
                         true->noNodes;
                         none ->nodesList.contents.current[];
                         (if searchfg[] <> none then
                             searchfg.fragmentList.scan
                               (# 
                               do
                                  (if current.type
                                   // mps.AST.formType then
                                      thisFragment:
                                        (# 
                                        do
                                           current.open->ff[];
                                           (if ff[] <> none then
                                               (if searchff[] = none then
                                                   ff.root[]->searchRoot[]
                                                else
                                                   (if
                                                   searchff.name
                                                     ->(ff.name).equal then
                                                       (if searchNode[] = none
                                                        then
                                                           ff.root[]
                                                             ->searchRoot[]
                                                        else
                                                           searchNode[]
                                                             ->searchRoot[]
                                                       if)
                                                    else
                                                       leave thisFragment
                                                   if)
                                               if);
                                               (if (searchRoot[] <> none ) and
                                               (searchRoot.kind =
                                                mps.AST.kinds.interior) then
                                                   searchRoot[]->exp[];
                                                   l: exp.suffixWalk
                                                     (#
                                                        t,dummy: ^text;
                                                        dummy2: @integer
                                                     do
                                                        current[]->checkNode
                                                          ->(OK,dummy[],dummy2);
                                                        (if OK then
                                                            &ffItem[]->fi[];
                                                            fi.init;
                                                            true->fi.firstTime;
                                                            searchRoot[]
                                                              ->fi.astRoot[];
                                                            fgName.copy->t[];
                                                            '-'->t.append;
                                                            ff.name->t.append;
                                                            t[]->fi.name;
                                                            searchFg[]
                                                              ->fgSet.insert;
                                                            fi[]->registerNodes;
                                                            leave l
                                                        if)
                                                     #)
                                               if)
                                            else
                                               'registerFragmentForms: could not open fragmentform'
                                                 ->putLine
                                           if)
                                        #)
                                  if)
                               #);
                             (if fi[] = none then
                                 ''->nodesList.label; true->noNodes; 
                              else
                                 setNodesListLabel->nodesList.label;
                                 false->noNodes;
                                 (listitems.head).elm.select
                             if)
                         if)
                      #);
                    open::  (#  do 'Fragment Forms'->label #);
                    
                 #);
               open:: 
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (w,(h div aspect)-1)->size;
                    
                 #);
               
            #);
          nodesList: @labelled
            (#
               clear: contents.clear (#  #);
               dobind: (#  do contents.dobind #);
               markCurrent:
                 (# t: ^text
                 do
                    (if contents.current[] <> none then
                        contents.current.elm.name->t[];
                        (if (1->t.inxget) <> '*' then
                            '*'->t.prepend; t[]->contents.current.elm.name
                        if)
                    if)
                 #);
               unmarkCurrent:
                 (# t: ^text
                 do
                    (if contents.current[] <> none then
                        contents.current.elm.name->t[];
                        (if (1->t.inxget) = '*' then
                            (1,1)->t.delete; t[]->contents.current.elm.name
                        if)
                    if)
                 #);
               contentsType:: listView
                 (#
                    selectNext:
                      (# 
                      do
                         (if current[] = none then
                             listitems.head->current[]; 
                          else
                             current.succ[]->current[];
                             (if current[] = none then
                                 listitems.head->current[]
                             if);
                             
                         if);
                         (if current[] <> none then
                             current.elm.select; selection.scrollIntoView; 
                         if)
                      #);
                    clear:
                      (# 
                      do
                         listitems.scanReverse
                           (#  do current.delete #);
                         listitems.clear;
                         
                      #);
                    current: ^listitems.theCellType;
                    dobind:
                      (# 
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom
                      #);
                    eventhandler:: 
                      (#
                         onKeyDown:: 
                           (# 
                           do
                              (if ch
                               // ascii.cr // ascii.nl then
                                  (if current[] = none then
                                      listitems.head->current[]; 
                                   else
                                      current.succ[]->current[];
                                      (if current[] = none then
                                          listitems.head->current[]
                                      if);
                                      
                                  if);
                                  (if current[] <> none then
                                      current.elm.select;
                                      selection.scrollIntoView;
                                      
                                  if)
                              if)
                           #)
                      #);
                    l1,c1,l2,c2: @integer;
                    nodeItem: listItem
                      (#
                         node: ^astInterface.ast;
                         itemTextUpdated: @boolean;
                         theNode: ^fflist.contents.node;
                         type: @integer;
                         onSelect:: 
                           (# currentPosition: @integer; msg: ^text
                           do
                              (if searchDone then
                                  THIS(nodeItem)[]->listitems.at->current[];
                                  position->currentposition;
                                  (if not itemTextUpdated then
                                      (node.frag.root[],node[],false)
                                        ->browser.selectNode;
                                      (if browser.cfe[] <> none then
                                          node[]
                                            ->
                                              browser.cfe.
                                                openContractionsUntilRoot;
                                          browser.cfe.doReprettyprint;
                                          (node[],1,0,0)->browser.cfe.selectArea
                                            ->(l1,c1,l2,c2)
                                            ->
                                              browser.cfe.astView.out.
                                                selectArea;
                                          THIS(nodeItem).type
                                            ->browser.cfe.setTextStyle;
                                          (if not
                                          ((l1 = 1) and (c1 = 1) and (l2 = 1)
                                           and (c2 = 1)) then
                                              l1
                                                ->
                                                  browser.cfe.theSifTexteditor.
                                                    getLine->msg[]->name;
                                              msg.copy
                                                ->
                                                  THIS(nodeItem).theNode.
                                                    theText[];
                                              true->itemTextUpdated
                                          if)
                                      if)
                                  if);
                                  node[]->onSelectNode;
                                  (currentposition,false)->selection.select
                              if)
                           #);
                         
                      #);
                    setView:
                      (#
                         exp: ^astInterface.expanded;
                         ei: ^nodeItem;
                         theffItem: ^ffList.contents.ffItem;
                         OK: @boolean;
                         t: ^text;
                         
                      enter theffItem[]
                      do
                         clear;
                         theffItem.nodes.scan
                           (# 
                           do
                              &nodeItem[]->ei[];
                              ei.init;
                              current.node[]->ei.node[];
                              current.type->ei.type;
                              current[]->ei.theNode[];
                              (if current.theText[] <> none then
                                  current.theText.copy->ei.name
                              if)
                           #);
                         (if ei[] = none then
                             true->noNodes
                          else
                             (listitems.head).elm.select
                         if)
                      #);
                    open::  (#  do setNodesListLabel->label #);
                    
                 #);
               open:: 
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (if isSemanticErrorViewer then
                        (w,2*(h div aspect)-1)->size
                     else
                        (w,2*(h div aspect)-1)->size
                    if);
                    
                 #)
            #);
          errorMessageWindow: @labelled
            (#
               clear: (#  do ''->contents.writeText #);
               contentsType:: staticText
                 (#
                    eventHandler::< 
                      (#
                         onLabelChanged::< 
                           (# 
                           do (labelWidth,labelHeight*linesinMessage)->size; 
                           #)
                      #);
                    linesInMessage: @integer;
                    writeText:
                      (# msg: ^text; msg2: @text
                      enter msg[]
                      do
                         1->linesInMessage;
                         msg2.reset;
                         msg.scanAll
                           (# 
                           do
                              (if ch
                               // ascii.newline then
                                  linesInMessage+1->linesInMessage; ch->msg2.put
                               // ascii.ht then (* ignore *)
                                  (if includeWarnings then
                                      '          '->msg2.putText
                                  if)
                               else
                                  ch->msg2.put
                              if)
                           #);
                         msg2[]->label
                      #);
                    open::  (#  do 'Semantic error'->label #);
                    
                 #);
               open:: 
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (w,(h div aspect)-5)->size;
                    'Error message'->label;
                    
                 #);
               
            #);
          open::< 
            (#
               setDefaults:: 
                 (#  do true->verticalStacking; 50->minsize; 2->panewidth #)
            do
               FFlist.open;
               nodesList.open;
               FFlist[]->appendMember;
               nodesList[]->appendMember;
               (if isSemanticErrorViewer then
                   errorMessageWindow.open;
                   errorMessageWindow[]->contents.appendMember
               if);
               INNER open;
               appendsDone;
               dobind;
               
            #)
       #);
     clearBrowser:
       (# 
       do
          false->checkAll->doNotCheckAll->cancelCommand;
          clear;
          contents.FFlist.clear;
          (if isSemanticErrorViewer then contents.errorMessageWindow.clear if)
       #);
     registerFragmentGroup:
       (#
          fgSet: @set (# element:: mps.ast.fragmentgroup #);
          OK: @boolean;
          help: ^text;
          gList: ^projectInfo.groupList
       enter (fg[],ff[],node[])
       do
          thisCommand:
            (# 
            do
               false->searchDone;
               clearBrowser;
               (if fg[] <> none then
                   fg.name->fgName[];
                   (fg[],ff[],node[])
                     ->contents.FFlist.contents.registerFragmentForms;
                   noNodes->setTitle
                else
                   (if browser.currentProject[] <> none then
                       (if searchArea
                        // 4 (* project *) then
                           browser.currentProject.scanGroups;
                           browser.currentProject.groups[]->gList[]
                        // 5 (* domain *) then
                           (if browser.currentGroup[] <> none then
                               browser.currentGroup.getDomain->gList[]
                           if)
                        // 6 (* extent *) then
                           (if browser.currentGroup[] <> none then
                               browser.currentGroup.getExtent->gList[]
                           if)
                       if);
                       (if gList[] <> none then
                           gList.scan
                             (# fg: ^astinterface.fragmentGroup
                             do
                                current.getfg->fg[];
                                (if fg[] <> none then
                                    (if not (fg[]->fgSet.has) and not
                                    (fg[]->fgExcludeSet.has) then
                                        fg[]->checkGroupStatus->OK;
                                        (if cancelCommand then
                                            leave thisCommand
                                        if);
                                        (if OK then
                                            fg[]->fgSet.insert;
                                            fg.name->fgName[];
                                            (fg[],ff[],node[])
                                              ->
                                                contents.FFlist.contents.
                                                  registerFragmentForms
                                         else
                                            fg[]->fgExcludeSet.insert
                                        if)
                                    if)
                                if)
                             #);
                           'Search hits in current project - '->help[];
                           noOfHits->help.putInt;
                           (if noOfHits = 1 then
                               ' hit in '->help.putText
                            else
                               ' hits in '->help.putText
                           if);
                           noOfFiles->help.putInt;
                           (if noOfFiles = 1 then
                               ' file'->help.append
                            else
                               ' files'->help.append
                           if);
                           help[]->title
                        else
                           'No group list'->title
                       if)
                    else
                       'No current project'->title
                   if)
               if);
               true->searchDone;
               contents.ffList.contents.selectFirst;
               
            #)
       #);
     printList: (#  do contents.ffList.contents.printList #);
     type::  (#  do (*windowTypes.palette->value*)  #);
     open::< 
       (# w,h: @integer; 
       do
          (300,200)->size;
          true->opened;
          hide;
          INNER open;
          contents.open;
          (fg[],ff[],node[])->registerFragmentGroup;
          show
       #);
     close::  (#  do false->opened;  #);
     opened: @boolean;
     init: (#  enter browser[] do browser.mps[]->mps[] #)
  #);
searchViewer: searchBrowser
  (#
     aspect::  (#  do 3->value #);
     contentsType::< 
       (#
          onNewfragmentForm::< 
            (# l1,c1,l2,c2: @integer
            do
               (if showAllHits then
                   ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
                   (if browser.cfe[] <> none then
                       contents.nodesList.contents.listitems.scan
                         (# i: ^contents.nodesList.contents.nodeItem
                         do
                            current[]->i[];
                            i.node[]->browser.cfe.openContractionsUntilRoot
                         #);
                       browser.cfe.doReprettyprint;
                       contents.nodesList.contents.listitems.scan
                         (#
                            i: ^contents.nodesList.contents.nodeItem;
                            msg: ^text
                         do
                            current[]->i[];
                            (i.node.frag.root[],i.node[],false)
                              ->browser.selectNode;
                            (if not i.itemTextUpdated then
                                (i.node[],1,0,0)->browser.cfe.selectArea
                                  ->(l1,c1,l2,c2)
                                  ->browser.cfe.astView.out.selectArea;
                                i.type->browser.cfe.setTextStyle;
                                (if not
                                ((l1 = 1) and (c1 = 1) and (l2 = 1) and
                                 (c2 = 1)) then
                                    l1->browser.cfe.theSifTexteditor.getLine
                                      ->msg[]->i.name;
                                    msg.copy->i.theNode.theText[];
                                    true->i.itemTextUpdated
                                if)
                            if)
                         #);
                       (if contents.nodesList.contents.listItems.size > 0 then
                           (contents.nodesList.contents.listitems.head).elm.
                             select
                       if)
                   if)
               if);
               INNER
            #);
          onSelectFragmentForm::< 
            (# 
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               INNER
            #);
          onSelectNode::< 
            (# 
            do
               (node.frag.root[],node[],false)->browser.selectNode;
               (if (browser.cfe[] <> none ) and
               ((node[],node.frag.root[])->browser.cfe.nodeExists) then
                   3 (* search hit *) ->browser.cfe.setTextStyle
               if)
            #)
       #);
     menubarType::< 
       (# (* NOTE: there is a menu quite similar to this one in 
           * ../sourcebrowser/private/semanticEditor.bet *)
          fileMenu: @menu
            (#
               reloadItem: @menuItem
                 (#
                    eventHandler:: 
                      (#
                         onSelect:: 
                           (# 
                           do (fg[],ff[],node[])->registerFragmentGroup
                           #)
                      #);
                    open::  (#  do 'l'->key; 'Reload'->name #)
                 #);
               printItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do printList #) #);
                    open::  (#  do 'Print list in log window'->name #)
                 #);
               quitItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do THIS(searchBrowser).close #)
                      #);
                    open::  (#  do 'w'->key; 'Close'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'File'->name;
                    reloadItem.open;
                    reloadItem[]->append;
                    printItem.open;
                    printItem[]->append;
                    quitItem.open;
                    quitItem[]->append
                 #)
            #);
          open::<  (#  do fileMenu.open; fileMenu[]->append #);
          
       #);
     open::<  (#  do (410,300)->size; INNER open;  #)
  #);
mySemanticViewer: searchBrowser
  (#
     aspect::<  (#  do 4->value; INNER #);
     isSemanticErrorViewer:: trueObject;
     contentsType::< 
       (#
          onNewfragmentForm::< 
            (# l1,c1,l2,c2: @integer; msg: ^text
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               (if browser.cfe[] <> none then
                   contents.nodesList.contents.listitems.scan
                     (# i: ^contents.nodesList.contents.nodeItem
                     do
                        current[]->i[];
                        i.node[]->browser.cfe.openContractionsUntilRoot
                     #);
                   browser.cfe.doReprettyprint;
                   contents.nodesList.contents.listitems.scan
                     (# i: ^contents.nodesList.contents.nodeItem
                     do
                        current[]->i[];
                        (if not i.itemTextUpdated then
                            (i.node[],1,0,0)->browser.cfe.selectArea
                              ->(l1,c1,l2,c2)
                              ->browser.cfe.astView.out.selectArea;
                            i.type->browser.cfe.setTextStyle;
                            (if not
                            ((l1 = 1) and (c1 = 1) and (l2 = 1) and (c2 = 1))
                             then
                                i.name->msg[];
                                (if false then ': '->msg.append if);
                                l1->browser.cfe.theSifTexteditor.getLine
                                  ->msg.append;
                                msg[]->i.name;
                                msg.copy->i.theNode.theText[];
                                true->i.itemTextUpdated
                            if)
                        if)
                     #);
                   (if contents.nodesList.contents.listItems.size > 0 then
                       (contents.nodesList.contents.listitems.head).elm.select
                   if)
               if);
               INNER
            #);
          onSelectFragmentForm::< 
            (# 
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               INNER
            #);
          onSelectNode::< 
            (# t: ^text
            do
               (if (browser.cfe[] = none ) then
                   (node.frag.root[],node[],false)->browser.selectNode
               if);
               (if (browser.cfe[] <> none ) and
               ((node[],node.frag.root[])->browser.cfe.nodeExists) then
                   (if (node.semanticError->mps.AST.isWarning) then
                       (if includeWarnings then
                           'Warning: '->t[];
                           node.semanticError->mps.ast.semanticErrorText
                             ->t.append
                        else
                           ''->t[]
                       if);
                       t[]->errorMessageWindow.contents.writeText;
                       (node.frag.root[],node[],false)->browser.selectNode;
                       4 (* warning *) ->browser.cfe.setTextStyle
                    else
                       (if includeWarnings then
                           'Error:   '->t[]
                        else
                           &text[]->t[]
                       if);
                       node.semanticError->mps.AST.semanticErrorText->t.append;
                       t[]->errorMessageWindow.contents.writeText;
                       (node.frag.root[],node[],false)->browser.selectNode;
                       2 (* semantic error *) ->browser.cfe.setTextStyle
                   if)
               if)
            #)
       #);
     menubarType:: 
       (#
          fileMenu: @menu
            (#
               reloadItem: @menuItem
                 (#
                    eventHandler:: 
                      (#
                         onSelect:: 
                           (# 
                           do (fg[],ff[],node[])->registerFragmentGroup
                           #)
                      #);
                    open::  (#  do 'l'->key; 'Reload'->name #)
                 #);
               quitItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do THIS(mySemanticViewer).close #)
                      #);
                    open::  (#  do 'w'->key; 'Close'->name #)
                 #);
               nextItem: @menuItem
                 (#
                    eventHandler:: 
                      (#
                         onStatus:: 
                           (# 
                           do THIS(mySemanticViewer).hasErrors->value
                           #);
                         onSelect:: 
                           (#  do THIS(mySemanticViewer).selectnext #)
                      #);
                    open::  (#  do 'e'->key; 'Next Error'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'File'->name;
                    reloadItem.open;
                    reloadItem[]->append;
                    nextItem.open;
                    nextItem[]->append;
                    quitItem.open;
                    quitItem[]->append
                 #)
            #);
          markMenu: @menu
            (#
               markItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do markCurrentNode #) #);
                    open::  (#  do 'm'->key; 'Mark as corrected'->name #)
                 #);
               unmarkItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do unmarkCurrentNode #) #);
                    open::  (#  do 'u'->key; 'Unmark'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'Mark'->name;
                    markItem.open;
                    markItem[]->append;
                    unmarkItem.open;
                    unmarkItem[]->append
                 #)
            #);
          warningsMenu: @menu
            (#
               includeItem: @menuItem
                 (#
                    eventhandler:: 
                      (#
                         onSelect:: 
                           (# 
                           do
                              not checked->checked->includeWarnings;
                              (fg[],ff[],node[])->registerFragmentGroup
                           #)
                      #);
                    open::  (#  do ('i',true,true,false)->specialkey; 'Include'->name #);
                    
                 #);
               open:: 
                 (# 
                 do 'Warnings'->name; includeItem.open; includeItem[]->append; 
                 #);
               
            #);
          open:: 
            (# 
            do
               fileMenu.open;
               fileMenu[]->append;
               warningsMenu.open;
               warningsMenu[]->append;
               markMenu.open;
               markMenu[]->append
            #);
          
       #);
     hasErrors: BooleanValue (#  do (noOfhits > 0)->value #);
     selectnext:
       (# 
       do (if noOfHits > 0 then contents.nodeslist.contents.selectnext if)
       #);
     open::<  (#  do (410,300)->size; INNER open;  #)
  #);
semanticEditor: mySemanticViewer
  (#
     aspect::<  (#  do 6->value #);
     contentsType::< 
       (#
          onNewFragmentForm::<  (#  do ff[]->asteditor.newFragment #);
          dobind::<  (#  do ASTeditor.dobind; INNER dobind #);
          ASTeditor: @labelled
            (#
               dobind: (#  do contents.dobind #);
               ge: ^guienv.window.editorenv.groupEditor;
               fe: ^codeEditor;
               newFragment:
                 (# ff: ^mps.AST.fragmentForm; 
                 enter ff[]
                 do
                    (ff[],ff.root[],none ,contents[],ge[])
                      ->browser.edenv.findOrCreateFormEditor->fe[];
                    
                 #);
               contentsType::< codemoveableEditor
                 (#
                    isolated:: trueObject;
                    dobind:
                      (# 
                      do true->bindRight->bindBottom->bindLeft->bindTop
                      #);
                    selectNode:: 
                      (# 
                      do (theEditorRoot[],node[],separate)->browser.selectNode
                      #);
                    charWidthType:: 
                      (#  do ASTeditor.private.theLabel.style->ts[] #);
                    getEncloser::  (#  do ffEncloser[]->theEncloser[] #);
                    ffEncloser: @codemoveableEditorEncloser
                      (#
                         getAST:: 
                           (# 
                           do (*gram*) THIS(searchBrowser).mps.AST[]->AST[]
                           #);
                         getBETACFL:: 
                           (# 
                           do (*GRAM *)
                              THIS(searchBrowser).mps.BETACFL[]->BETACFL[]
                           #);
                         
                      #);
                    
                 #);
               open:: 
                 (#
                    sz: @point;
                    openContents:: 
                      (# t: ^text
                      do
                         (ASTeditor[],sz)->contents.open;
                         (* ASTeditor.contents[]->browser.onCodeViewOpen; *)
                         'No fragments selected'->label;
                         true->doneInInner
                      #);
                    w,h: @integer
                 do
                    THIS(semanticEditor).size->(w,h);
                    (w,(h div aspect)*4-1)->sz->size;
                    (browser.Id,fg[],false)
                      ->browser.edenv.findOrCreateGroupEditor->ge[];
                    
                 #);
               
            #);
          open::< 
            (# 
            do ASTeditor.open; ASTeditor[]->appendMember; INNER open; 
            #);
          
       #);
     insetDisplay:
       (# p: @point; r: @rectangle
       enter p
       do (((0,0),p),screenRectangle)->r.intersection; (0,50)->r.inset; 
       exit r.size
       #);
     open::<  (#  do (600,800)->insetDisplay->size; INNER open;  #);
     
  #);
(* Taken from sourcebrowser/private/semanticeditor, but does not work *)
  

