ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/pane'
        '~beta/betaast/semanticerrortext'
        '~beta/guienv/utils/listview'
        '~beta/sourcebrowser/ymerInterface'
        '~beta/sourcebrowser/codemoveable'
        '~beta/guienv/utils/private/promptsbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-2000
 *   All rights reserved.
 *)
-- guienvlib: Attributes --
promptForBooleanAll: promptForBoolean
  (#
     ok::<  (#  do true->THIS(prompt).private.popdown; INNER #);
     notok::< (* Called when the 'No' button is pressed *) 
       (#  do true->THIS(prompt).private.popdown; INNER #);
     okLabel::<  (#  do 'Yes'->value[]; INNER #);
     notokLabel::<  (#  do 'No'->value[]; INNER #);
     cancelLabel::<  (#  do 'Cancel'->value[]; INNER #);
     okAll:< Object (* Called when the OK button is clicked *) ;
     open::< 
       (# 
       do THIS(prompt).contents->myprivate.okAllButton.open; INNER ; 
       #);
     myprivate: @
       (#
          okAllButton: @private.promptButton
            (#
               eventHandler:: 
                 (#
                    onMouseUp:: 
                      (# 
                      do
                         (if not THIS(prompt).private.done then
                             true->THIS(prompt).private.done;
                             true->THIS(prompt).private.popdown;
                             ok;
                             (if THIS(prompt).private.popdown then
                                 THIS(Prompt).hide
                              else
                                 false->THIS(prompt).private.done
                             if)
                         if)
                      #)
                 #);
               open:: 
                 (# 
                 do
                    okLabel->label;
                    1->(okLabel).inxGet->ascii.upcase->upcase->ascii.lowcase
                      ->lowcase;
                    (100,80)->position;
                    
                 #);
               
            #)
       #);
     
  #);
searchBrowser: Window
  (#
     browser: ^ymerBrowserInterface;
     mps: ^ymermps;
     checkNode:<
       (# node: ^astInterface.ast; value: @boolean; t: ^text
       enter node[]
       do INNER
       exit (value,t[])
       #);
     setTitle:<
       (# noHits: @boolean; t: ^text
       enter noHits
       do INNER ; t[]->title
       exit t[]
       #);
     setNodesListLabel:< (# t: ^text do INNER exit t[] #);
     showSourceLine:< booleanValue;
     checkGroupStatus:<
       (# fg: ^astInterface.fragmentGroup; value: @boolean
       enter fg[]
       do true->value; INNER
       exit value
       #);
     fgExcludeSet: @set (# element:: browser.mps.ast.fragmentgroup #);
     clear: contents.clear (#  #);
     fg: ^astInterface.fragmentGroup;
     ff: ^astInterface.fragmentForm;
     node: ^astInterface.ast;
     showAllHits: @boolean;
     fgName: ^text;
     (* isWarning:
      *        {* findes i mps/v5.0.ess.ess.ess - blot her for at
      *         * kunne filtrere warnings fra allerede i
      *         * v5.0.1 - skal fjernes senere
      *         *}
      *        (# no: @integer; boo: @boolean
      *        enter no
      *        do
      *           (if no
      *            //24//25//86//95//97//99//106//109
      *            //26 {*not used *} then
      *               true->boo
      *           if)
      *        exit boo
      *        #);
      *)
     includeWarnings: @boolean;
     noNodes: @boolean;
     aspect:< integerValue (#  do 6->value; INNER #);
     markCurrentNode: (#  do contents.nodesList.markCurrent #);
     unmarkCurrentNode: (#  do contents.nodesList.unmarkCurrent #);
     contents: @contentsType;
     contentsType:< pane
       (#
          clear:
            (#  do FFlist.clear; nodesList.clear; 0->noOfHits->noOfFiles #);
          dobind:<
            (# 
            do
               TRUE->bindTop->bindLeft->bindRight->bindBottom;
               FFlist.dobind;
               nodesList.dobind;
               INNER dobind
            #);
          onNewfragmentForm:<
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do INNER onNewfragmentForm
            #);
          onSelectFragmentForm:<
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do INNER onSelectFragmentForm
            #);
          onSelectNode:<
            (# node: ^astInterface.ast; 
            enter node[]
            do INNER onSelectNode
            #);
          noOfHits,noOfFiles: @integer;
          FFlist: @labelled
            (#
               clear: contents.clear (#  #);
               dobind: (#  do contents.dobind #);
               contentsType:: listView
                 (#
                    clear:
                      (# 
                      do
                         listitems.scanReverse
                           (#  do current.delete #);
                         listitems.clear;
                         
                      #);
                    current: ^listitems.theCellType;
                    dobind:
                      (# 
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom
                      #);
                    eventhandler:: 
                      (#
                         onKeyDown:: 
                           (# 
                           do
                              (if ch
                               // ascii.cr // ascii.nl then
                                  (if current[] = none then
                                      listitems.head->current[]; 
                                   else
                                      current.succ[]->current[];
                                      (if current[] = none then
                                          listitems.head->current[]
                                      if);
                                      
                                  if);
                                  (if current[] <> none then
                                      current.elm.select;
                                      selection.scrollIntoView
                                  if)
                              if)
                           #)
                      #);
                    ffItem: listItem
                      (#
                         astRoot: ^astInterface.ast;
                         firstTime: @boolean;
                         nodes: @list (# element:: node #);
                         onSelect:: 
                           (# fi: ^listitems.theCellType
                           do
                              THIS(ffItem)[]->listitems.at->fi[];
                              (if fi[] <> current[] then
                                  fi[]->current[];
                                  THIS(ffItem)[]->nodesList.contents.setView;
                                  (if firstTime then
                                      false->firstTime;
                                      astRoot.frag[]->onNewfragmentForm
                                   else
                                      astRoot.frag[]->onSelectFragmentForm
                                  if)
                              if)
                           #);
                         
                      #);
                    node:
                      (# node: ^astInterface.ast; theText: ^text;  #);
                    registerNodes:
                      (#
                         exp: ^astInterface.expanded;
                         n: ^node;
                         theffItem: ^ffList.contents.ffItem;
                         OK: @boolean;
                         t: ^text;
                         
                      enter theffItem[]
                      do
                         (if theffItem.astRoot.kind = mps.AST.kinds.interior
                          then
                             theffItem.astRoot[]->exp[];
                             exp.suffixWalk
                               (# 
                               do
                                  current[]->checkNode->(OK,t[]);
                                  (if OK then
                                      &node[]->n[];
                                      current[]->n.node[];
                                      (if t[] <> none then
                                          t.copy->n.theText[]
                                      if);
                                      n[]->theffItem.nodes.append;
                                      noOfHits+1->noOfHits
                                  if)
                               #)
                         if);
                         (if n[] = none then
                             true->noNodes
                          else
                             (listitems.head).elm.select
                         if)
                      #);
                    registerFragmentForms:
                      (#
                         exp: ^astInterface.expanded;
                         fi: ^ffItem;
                         searchfg: ^astInterface.fragmentGroup;
                         ff,searchff: ^astInterface.fragmentForm;
                         searchNode,searchRoot: ^astInterface.ast;
                         OK: @boolean
                      enter (searchfg[],searchff[],searchNode[])
                      do
                         true->noNodes;
                         none ->nodesList.contents.current[];
                         (if searchfg[] <> none then
                             searchfg.fragmentList.scan
                               (# 
                               do
                                  (if current.type
                                   // mps.AST.formType then
                                      thisFragment:
                                        (# 
                                        do
                                           current.open->ff[];
                                           (if ff[] <> none then
                                               (if searchff[] = none then
                                                   ff.root[]->searchRoot[]
                                                else
                                                   (if
                                                   searchff.name
                                                     ->(ff.name).equal then
                                                       (if searchNode[] = none
                                                        then
                                                           ff.root[]
                                                             ->searchRoot[]
                                                        else
                                                           searchNode[]
                                                             ->searchRoot[]
                                                       if)
                                                    else
                                                       leave thisFragment
                                                   if)
                                               if);
                                               (if
                                               (searchRoot.kind =
                                                mps.AST.kinds.interior) then
                                                   searchRoot[]->exp[];
                                                   l: exp.suffixWalk
                                                     (# t,dummy: ^text
                                                     do
                                                        current[]->checkNode
                                                          ->(OK,dummy[]);
                                                        (if OK then
                                                            &ffItem[]->fi[];
                                                            fi.init;
                                                            true->fi.firstTime;
                                                            searchRoot[]
                                                              ->fi.astRoot[];
                                                            fgName.copy->t[];
                                                            '-'->t.append;
                                                            ff.name->t.append;
                                                            t[]->fi.name;
                                                            fi[]->registerNodes;
                                                            leave l
                                                        if)
                                                     #)
                                               if)
                                            else
                                               'registerFragmentForms: could not open fragmentform'
                                                 ->putLine
                                           if)
                                        #)
                                  if)
                               #);
                             (if fi[] = none then
                                 ''->nodesList.label; true->noNodes; 
                              else
                                 setNodesListLabel->nodesList.label;
                                 false->noNodes;
                                 (listitems.head).elm.select
                             if)
                         if)
                      #);
                    open::  (#  do 'Fragment Forms'->label #);
                    
                 #);
               open:: 
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (w,(h div aspect)-1)->size;
                    
                 #);
               
            #);
          nodesList: @labelled
            (#
               clear: contents.clear (#  #);
               dobind: (#  do contents.dobind #);
               markCurrent:
                 (# t: ^text
                 do
                    (if contents.current[] <> none then
                        contents.current.elm.name->t[];
                        (if (1->t.inxget) <> '*' then
                            '*'->t.prepend; t[]->contents.current.elm.name
                        if)
                    if)
                 #);
               unmarkCurrent:
                 (# t: ^text
                 do
                    (if contents.current[] <> none then
                        contents.current.elm.name->t[];
                        (if (1->t.inxget) = '*' then
                            (1,1)->t.delete; t[]->contents.current.elm.name
                        if)
                    if)
                 #);
               contentsType:: listView
                 (#
                    selectNext:
                      (# 
                      do
                         (if current[] = none then
                             listitems.head->current[]; 
                          else
                             current.succ[]->current[];
                             (if current[] = none then
                                 listitems.head->current[]
                             if);
                             
                         if);
                         (if current[] <> none then
                             current.elm.select; selection.scrollIntoView; 
                         if)
                      #);
                    clear:
                      (# 
                      do
                         listitems.scanReverse
                           (#  do current.delete #);
                         listitems.clear;
                         
                      #);
                    current: ^listitems.theCellType;
                    dobind:
                      (# 
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom
                      #);
                    eventhandler:: 
                      (#
                         onKeyDown:: 
                           (# 
                           do
                              (if ch
                               // ascii.cr // ascii.nl then
                                  (if current[] = none then
                                      listitems.head->current[]; 
                                   else
                                      current.succ[]->current[];
                                      (if current[] = none then
                                          listitems.head->current[]
                                      if);
                                      
                                  if);
                                  (if current[] <> none then
                                      current.elm.select;
                                      selection.scrollIntoView;
                                      
                                  if)
                              if)
                           #)
                      #);
                    l1,c1,l2,c2: @integer;
                    nodeItem: listItem
                      (#
                         node: ^astInterface.ast;
                         itemTextUpdated: @boolean;
                         theNode: ^fflist.contents.node;
                         onSelect:: 
                           (# currentPosition: @integer
                           do
                              THIS(nodeItem)[]->listitems.at->current[];
                              position->currentposition;
                              (if not itemTextUpdated then
                                  (if browser.cfe[] <> none then
                                      (node.frag.root[],node[],false)
                                        ->browser.selectNode;
                                      node[]
                                        ->browser.cfe.openContractionsUntilRoot;
                                      browser.cfe.doReprettyprint;
                                      (node[],1,0,0)->browser.cfe.selectArea
                                        ->(l1,c1,l2,c2)
                                        ->browser.cfe.astView.out.selectArea;
                                      3 (* search hit *)
                                        ->browser.cfe.setTextStyle;
                                      (if not
                                      ((l1 = 1) and (c1 = 1) and (l2 = 1) and
                                       (c2 = 1)) then
                                          l1
                                            ->
                                              browser.cfe.theSifTexteditor.
                                                getLine->name;
                                          true->itemTextUpdated
                                      if)
                                  if)
                              if);
                              node[]->onSelectNode;
                              (currentposition,false)->selection.select
                           #);
                         
                      #);
                    setView:
                      (#
                         exp: ^astInterface.expanded;
                         ei: ^nodeItem;
                         theffItem: ^ffList.contents.ffItem;
                         OK: @boolean;
                         t: ^text;
                         
                      enter theffItem[]
                      do
                         clear;
                         theffItem.nodes.scan
                           (# 
                           do
                              &nodeItem[]->ei[];
                              ei.init;
                              current.node[]->ei.node[];
                              current[]->ei.theNode[];
                              (if current.theText[] <> none then
                                  current.theText.copy->ei.name
                              if)
                           #);
                         (if ei[] = none then
                             true->noNodes
                          else
                             (listitems.head).elm.select
                         if)
                      #);
                    open::  (#  do setNodesListLabel->label #);
                    
                 #);
               open:: 
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (w,(h div aspect)-1)->size;
                    
                 #)
            #);
          open::< 
            (#
               setDefaults:: 
                 (#  do true->verticalStacking; 50->minsize; 2->panewidth #)
            do
               FFlist.open;
               nodesList.open;
               FFlist[]->appendMember;
               nodesList[]->appendMember;
               INNER open;
               appendsDone;
               dobind;
               
            #)
       #);
     clearBrowser: (#  do clear; contents.FFlist.clear #);
     registerFragmentGroup:
       (#
          fgSet: @set (# element:: mps.ast.fragmentgroup #);
          OK: @boolean;
          help: ^text
       enter (fg[],ff[],node[])
       do
          clearBrowser;
          (if fg[] <> none then
              fg.name->fgName[];
              (fg[],ff[],node[])
                ->contents.FFlist.contents.registerFragmentForms;
              noNodes->setTitle
           else
              (if browser.currentProject[] <> none then
                  browser.currentProject.groups.scan
                    (# fg: ^astinterface.fragmentGroup
                    do
                       current.getfg->fg[];
                       (if fg[] <> none then
                           (if not (fg[]->fgSet.has) and not
                           (fg[]->fgExcludeSet.has) then
                               fg[]->checkGroupStatus->OK;
                               (if OK then
                                   fg[]->fgSet.insert;
                                   fg.name->fgName[];
                                   (fg[],ff[],node[])
                                     ->
                                       contents.FFlist.contents.
                                         registerFragmentForms
                                else
                                   fg[]->fgExcludeSet.insert
                               if)
                           if)
                       if)
                    #);
                  'Search hits in current project - '->help[];
                  contents.noOfHits->help.putInt;
                  ' hits'->help.append;
                  help[]->title
               else
                  'No current project'->title
              if)
          if);
          
       #);
     type::  (#  do (*windowTypes.palette->value*)  #);
     open::< 
       (# w,h: @integer; 
       do
          true->opened;
          hide;
          INNER open;
          contents.open;
          (fg[],ff[],node[])->registerFragmentGroup;
          show
       #);
     close::  (#  do false->opened;  #);
     opened: @boolean;
     init: (#  enter browser[] do browser.mps[]->mps[] #)
  #);
searchViewer: searchBrowser
  (#
     aspect::  (#  do 2->value #);
     contentsType::< 
       (#
          onNewfragmentForm::< 
            (# l1,c1,l2,c2: @integer
            do
               (if showAllHits then
                   ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
                   (if browser.cfe[] <> none then
                       contents.nodesList.contents.listitems.scan
                         (# i: ^contents.nodesList.contents.nodeItem
                         do
                            current[]->i[];
                            i.node[]->browser.cfe.openContractionsUntilRoot
                         #);
                       browser.cfe.doReprettyprint;
                       contents.nodesList.contents.listitems.scan
                         (# i: ^contents.nodesList.contents.nodeItem
                         do
                            current[]->i[];
                            (i.node.frag.root[],i.node[],false)
                              ->browser.selectNode;
                            (if not i.itemTextUpdated then
                                (i.node[],1,0,0)->browser.cfe.selectArea
                                  ->(l1,c1,l2,c2)
                                  ->browser.cfe.astView.out.selectArea;
                                3 (* search hit *) ->browser.cfe.setTextStyle;
                                (if not
                                ((l1 = 1) and (c1 = 1) and (l2 = 1) and
                                 (c2 = 1)) then
                                    l1->browser.cfe.theSifTexteditor.getLine
                                      ->i.name;
                                    true->i.itemTextUpdated
                                if)
                            if)
                         #);
                       (if contents.nodesList.contents.listItems.size > 0 then
                           (contents.nodesList.contents.listitems.head).elm.
                             select
                       if)
                   if)
               if);
               INNER
            #);
          onSelectFragmentForm::< 
            (# 
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               INNER
            #);
          onSelectNode::< 
            (# 
            do
               (node.frag.root[],node[],false)->browser.selectNode;
               3 (* search hit *) ->browser.cfe.setTextStyle
            #)
       #);
     menubarType::< 
       (#
          fileMenu: @menu
            (#
               reloadItem: @menuItem
                 (#
                    eventHandler:: 
                      (#
                         onSelect:: 
                           (# 
                           do (fg[],ff[],node[])->registerFragmentGroup
                           #)
                      #);
                    open::  (#  do 'r'->key; 'Reload'->name #)
                 #);
               quitItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do THIS(searchBrowser).close #)
                      #);
                    open::  (#  do 'w'->key; 'Close'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'File'->name;
                    reloadItem.open;
                    reloadItem[]->append;
                    quitItem.open;
                    quitItem[]->append
                 #)
            #);
          open::<  (#  do fileMenu.open; fileMenu[]->append #);
          
       #);
     open::<  (#  do (500,300)->size; INNER open;  #)
  #);
mySemanticViewer: searchBrowser
  (#
     aspect::<  (#  do 2->value; INNER #);
     contentsType::< 
       (#
          onNewfragmentForm::< 
            (# l1,c1,l2,c2: @integer; msg: ^text
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               (if browser.cfe[] <> none then
                   contents.nodesList.contents.listitems.scan
                     (# i: ^contents.nodesList.contents.nodeItem
                     do
                        current[]->i[];
                        i.node[]->browser.cfe.openContractionsUntilRoot
                     #);
                   browser.cfe.doReprettyprint;
                   contents.nodesList.contents.listitems.scan
                     (# i: ^contents.nodesList.contents.nodeItem
                     do
                        current[]->i[];
                        (if not i.itemTextUpdated then
                            (i.node[],1,0,0)->browser.cfe.selectArea
                              ->(l1,c1,l2,c2)
                              ->browser.cfe.astView.out.selectArea;
                            2 (* semantic error *) ->browser.cfe.setTextStyle;
                            (if not
                            ((l1 = 1) and (c1 = 1) and (l2 = 1) and (c2 = 1))
                             then
                                i.name->msg[];
                                ': '->msg.append;
                                l1->browser.cfe.theSifTexteditor.getLine
                                  ->msg.append;
                                msg[]->i.name;
                                msg[]->i.theNode.theText[];
                                true->i.itemTextUpdated
                            if)
                        if)
                     #);
                   (if contents.nodesList.contents.listItems.size > 0 then
                       (contents.nodesList.contents.listitems.head).elm.select
                   if)
               if);
               INNER
            #);
          onSelectFragmentForm::< 
            (# 
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               INNER
            #);
          onSelectNode::< 
            (# 
            do
               (node.frag.root[],node[],false)->browser.selectNode;
               2 (* semantic error *) ->browser.cfe.setTextStyle
            #)
       #);
     menubarType:: 
       (#
          fileMenu: @menu
            (#
               reloadItem: @menuItem
                 (#
                    eventHandler:: 
                      (#
                         onSelect:: 
                           (# 
                           do (fg[],ff[],node[])->registerFragmentGroup
                           #)
                      #);
                    open::  (#  do 'r'->key; 'Reload'->name #)
                 #);
               quitItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do THIS(mySemanticViewer).close #)
                      #);
                    open::  (#  do 'w'->key; 'Close'->name #)
                 #);
               nextItem: @menuItem
                 (#
                    eventHandler:: 
                      (#
                         onStatus:: 
                           (# 
                           do THIS(mySemanticViewer).hasErrors->value
                           #);
                         onSelect:: 
                           (#  do THIS(mySemanticViewer).selectnext #)
                      #);
                    open::  (#  do 'e'->key; 'Next Error'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'File'->name;
                    reloadItem.open;
                    reloadItem[]->append;
                    nextItem.open;
                    nextItem[]->append;
                    quitItem.open;
                    quitItem[]->append
                 #)
            #);
          markMenu: @menu
            (#
               markItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do markCurrentNode #) #);
                    open::  (#  do 'm'->key; 'Mark as corrected'->name #)
                 #);
               unmarkItem: @menuItem
                 (#
                    eventHandler:: 
                      (# onSelect::  (#  do unmarkCurrentNode #) #);
                    open::  (#  do 'u'->key; 'Unmark'->name #)
                 #);
               open:: 
                 (# 
                 do
                    'Mark'->name;
                    markItem.open;
                    markItem[]->append;
                    unmarkItem.open;
                    unmarkItem[]->append
                 #)
            #);
          warningsMenu: @menu
            (#
               includeItem: @menuItem
                 (#
                    eventhandler:: 
                      (#
                         onSelect:: 
                           (# 
                           do
                              not checked->checked->includeWarnings;
                              (fg[],ff[],node[])->registerFragmentGroup
                           #)
                      #);
                    open::  (#  do 'i'->key; 'Include'->name #);
                    
                 #);
               open:: 
                 (# 
                 do 'Warnings'->name; includeItem.open; includeItem[]->append; 
                 #);
               
            #);
          open:: 
            (# 
            do
               fileMenu.open;
               fileMenu[]->append;
               warningsMenu.open;
               warningsMenu[]->append;
               markMenu.open;
               markMenu[]->append
            #);
          
       #);
     hasErrors: BooleanValue (#  do (contents.noOfhits > 0)->value #);
     selectnext:
       (# 
       do
          (if contents.noOfHits > 0 then
              contents.nodeslist.contents.selectnext
          if)
       #);
     open::<  (#  do (500,300)->size; INNER open;  #)
  #);
semanticEditor: mySemanticViewer
  (#
     aspect::<  (#  do 6->value #);
     contentsType::< 
       (#
          onNewFragmentForm::<  (#  do ff[]->asteditor.newFragment #);
          dobind::<  (#  do ASTeditor.dobind; INNER dobind #);
          ASTeditor: @labelled
            (#
               dobind: (#  do contents.dobind #);
               ge: ^guienv.window.editorenv.groupEditor;
               fe: ^codeEditor;
               newFragment:
                 (# ff: ^mps.AST.fragmentForm; 
                 enter ff[]
                 do
                    (ff[],ff.root[],none ,contents[],ge[])
                      ->browser.edenv.findOrCreateFormEditor->fe[];
                    
                 #);
               contentsType::< codemoveableEditor
                 (#
                    isolated:: trueObject;
                    dobind:
                      (# 
                      do true->bindRight->bindBottom->bindLeft->bindTop
                      #);
                    selectNode:: 
                      (# 
                      do (theEditorRoot[],node[],separate)->browser.selectNode
                      #);
                    charWidthType:: 
                      (#  do ASTeditor.private.theLabel.style->ts[] #);
                    getEncloser::  (#  do ffEncloser[]->theEncloser[] #);
                    ffEncloser: @codemoveableEditorEncloser
                      (#
                         getAST:: 
                           (# 
                           do (*gram*) THIS(searchBrowser).mps.AST[]->AST[]
                           #);
                         getBETACFL:: 
                           (# 
                           do (*GRAM *)
                              THIS(searchBrowser).mps.BETACFL[]->BETACFL[]
                           #);
                         
                      #);
                    
                 #);
               open:: 
                 (#
                    sz: @point;
                    openContents:: 
                      (# t: ^text
                      do
                         (ASTeditor[],sz)->contents.open;
                         (* ASTeditor.contents[]->browser.onCodeViewOpen; *)
                         'No fragments selected'->label;
                         true->doneInInner
                      #);
                    w,h: @integer
                 do
                    THIS(semanticEditor).size->(w,h);
                    (w,(h div aspect)*4-1)->sz->size;
                    (browser.Id,fg[],false)
                      ->browser.edenv.findOrCreateGroupEditor->ge[];
                    
                 #);
               
            #);
          open::< 
            (# 
            do ASTeditor.open; ASTeditor[]->appendMember; INNER open; 
            #);
          
       #);
     insetDisplay:
       (# p: @point; r: @rectangle
       enter p
       do (((0,0),p),screenRectangle)->r.intersection; (0,50)->r.inset; 
       exit r.size
       #);
     open::<  (#  do (600,800)->insetDisplay->size; INNER open;  #);
     
  #);
(* Taken from sourcebrowser/private/semanticeditor, but does not work *)
  

