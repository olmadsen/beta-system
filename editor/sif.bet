(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-97
 *   All rights reserved.
 *)
ORIGIN '~beta/basiclib/v1.6/basicsystemenv';
INCLUDE 'ymersiflib'
        '~beta/guienv/v1.6/guienvsystemenv'
        '~beta/sysutils/v1.6/pathhandler';
BODY 'private/compiler'
     'private/nouser';
MDBODY nti 'private/sif_ntibody'
       mac 'private/sif_macbody'
       ppc 'private/sif_macbody'
       ppcmac 'private/sif_macbody'
       default 'private/sif_unixbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-97
 *   All rights reserved.
 *)
-- program: Descriptor --
systemenv
  (#
     setWindowEnv::  (#  do fb[]->theWindowEnv[] #);
     fb: @fragmentBrowser
       (#
          ymerBrowserWindow:: 
            (#
               browserTitle:: 
                 (# 
                 do
                    'Sif: Mjolner BETA Source Browser and Editor'
                      ->title[] (*.prepend*)
                 #);
               menubarType:: 
                 (#
                    groupFileMenuType:: 
                      (#
                         open:: 
                           (# 
                           do
                              'New...'->iNew.new;
                              'New BETA Program...'->iNewBetaProgram.new;
                              'New BETA Library...'->iNewBetaLibrary.new;
                              newSeparator;
                              'Close'->iClose.new;
                              'Save'->iSave.new;
                              'Save As...'->iSaveAs.new;
                              'Save All...'->iSaveAll.new;
                              'Save Abstract...'->iSaveAbstract.new;
                              newSeparator;
                              'Recover...'->iRecover.new;
                              'Revert'->iRevert.new;
                              newSeparator;
                              'Group Write Protection'->iSetIsReadOnly.new;
                              'Global Write Protection'
                                ->iSetGlobalWriteProtection.new
                           #)
                      #);
                    groupPerformMenuType:: 
                      (#
                         open:: 
                           (# 
                           do
                              'Check Current'->iCheck.new;
                              'Recheck'->iReCheck.new;
                              'Compile Current'->iCompile.new;
                              ('Recompile','g')->iRecompile.newKey;
                              'Execute Current'->iExecute.new;
                              'Reexecute'->iReExecute.new;
                              newSeparator;
                              'Debug'->iDebug.new;
                              newSeparator;
                              'Semantic Errors...'->iSemanticErrors.new;
                              newSeparator;
                              'Show Diagram'->iShowDiagram.new;
                              INNER
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         SLOTs.new;
                         SLOTs[]->append;
                         'Fragment'->GroupEdit.new;
                         'Tools'->GroupPerform.new;
                         INNER
                      #)
                 #);
               open::  (#  do initCompiler #);
               compilerPrivate:
                 @<<SLOT compilerPrivate:Descriptor>>;
               initCompiler:
                 (# version: ^text
                 do <<SLOT initCompilerBody:Descriptor>>
                 exit version[]
                 #);
               check:: 
                 (# 
                 <<SLOT compilerCheck:DoPart>>
                 #);
               compile::  (#  <<SLOT compilerCompile:DoPart>> #);
               externalTextedit::
                 (# 
                 do
                    <<SLOT externalTextEdit:Descriptor>>
                 #)
            #);
          compilerVersion::
            (# 
            <<SLOT sifCompilerVersion:DoPart>>
            #);
          isBETAsif::  (#  do true->value #);
          useExternalTextEditor:: 
            (#  do true (*enable always useExtTextEd*) ->value #);
          init:: 
            (# 
            do
               ('Sif: Known Bugs and Inconveniences',
                '~beta/editor/v5.2/doc/sif-faq.html')->helpDocument;
               
            #);
          ark,fgname,ffname: ^text;
          f: @file;
          
       do
          newLine;
          'Sif - Mjolner BETA Browser and Editor version '->putText;
          editorVersion->putText;
          ' '->put;
          machineType[]->putLine;
          '      integrated with BETA Compiler version '->putText;
         compilerVersion->putLine;
          (if noOfarguments > 1 then
              scanArguments:
              (for i: noOfArguments-1 repeat
                i+1->arguments->ark[];
                (if true
                 // '-test'->ark.equal then
                    true->doTest
                 // '-mpsTrace'->ark.equal then
                    ymerbrowser.appendDone; leave scanArguments
                 else
                    ark[]->analyzeArg->(fgname[],ffname[]);
                    (if fgname.length > 0 then
                        (if ffname.length > 0 then
                            fgname[]->f.name;
                            (if f.entry.exists then
                                (if ffname.length > 0 then
                                    fgname[]->ymerBrowser.appendProject;
                                    (fgname[],ffname[])
                                      ->ymerBrowser.browser.selectFragmentform
                                 else
                                    fgname[]->ymerbrowser.appendProject
                                if)
                            if)
                         else
                            fgname[]->ymerbrowser.appendProject
                        if)
                    if)
                if)
              for);
              (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
              
          if);
          (if doTest then
              (ymerbrowser.theMenubar).test.open;
              'Test'->(ymerbrowser.theMenubar).test.name;
              (ymerbrowser.theMenubar).test[]->(ymerbrowser.theMenubar).append
          if)
       #);
     ark: ^text
  do (*fb*)
     (if noOfarguments > 1 then
         (for i: noOfArguments-1 repeat
           i+1->arguments->ark[];
           (if true // '-test'->ark.equal then true->fb.doTest if)
         for);
         
     if)
  #)  

