(*  '~beta/basiclib/v1.5/betaenv';*)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
ORIGIN '~beta/basiclib/v1.5/basicsystemenv';
INCLUDE '~beta/sourcebrowser/v1.1/toolsMenu'
        'ymersiflib'
        '~beta/guienv/v1.4/guienvsystemenv';
BODY 'private/compiler'
     'private/nouser';
MDBODY nti 'private/sif_ntibody'
       mac 'private/sif_macbody'
       ppc 'private/sif_macbody'
       ppcmac 'private/sif_macbody'
       default 'private/sif_unixbody';
-- program: Descriptor --
systemenv
  (#
     setWindowEnv::  (#  do fb[]->theWindowEnv[] #);
     fb: @fragmentBrowser
       (#
          ymerBrowserWindow::< 
            (#
               browserTitle:: 
                 (# 
                 do
                    'Sif: Mjolner BETA Source Browser and Editor'
                      ->title[] (*.prepend*)
                 #);
               externalTextEdit:: 
                 (#  do <<SLOT externalTextEdit:Descriptor>>;  #);
               menubarType:: 
                 (#
                    groupFileMenuType:: 
                      (#
                         open:: 
                           (# 
                           do
                              'New...'
                                ->iNew.new;
                              'New BETA Program...'->iNewBetaProgram.new;
                              'New BETA Library...'->iNewBetaLibrary.new;
                              newSeparator;
                              'Close'->iClose.new;
                              'Save'->iSave.new;
                              'Save As...'->iSaveAs.new;
                              'Save All...'->iSaveAll.new;
                              'Save Abstract...'->iSaveAbstract.new;
                              newSeparator;
                              'Recover'->iRecover.new;
                              'Revert'->iRevert.new;
                              newSeparator;
                              'Group Write Protection'->iSetIsReadOnly.new;
                              'Global Write Protection'
                                ->iSetGlobalWriteProtection.new
                           #)
                      #);
                    groupPerformMenuType:: 
                      (#
                         open::< 
                           (# 
                           do
                              'Check Current'->iCheck.new;
                              'Recheck'->iReCheck.new;
                              'Compile Current'->iCompile.new;
                              ('Recompile','g')->iRecompile.newKey;
                              'Execute Current'->iExecute.new;
                              'Reexecute'->iReExecute.new;
                              newSeparator;
                              'Debug'->iDebug.new;
                              newSeparator;
                              'Semantic Errors...'->iSemanticErrors.new;
                              newSeparator;
                              'Show Diagram'->iShowDiagram.new;
                              INNER
                           #)
                      #);
                    open::< 
                      (# 
                      do
                         SLOTs.new;
                         SLOTs[]->append;
                         'Fragment'->GroupEdit.new;
                         'Tools'->GroupPerform.new;
                         INNER
                      #)
                 #);
               open::  (#  do initCompiler #)
            #);
          isBETAsif::<  (#  do true->value #);
          useExternalTextEditor::< 
            (#  do true (*enable always useExtTextEd*) ->value #);
          init::< 
            (# 
            do
               ('Sif: Known Bugs and Inconveniences',
                '~beta/editor/v5.1/doc/BUGS')->helpDocument;
               
            #);
          arg: ^text;
          
       do
          newLine;
          'Sif - Mjolner BETA Browser and Editor version '->putText;
          editorVersion->putText;
          ' '->put;
          machineType[]->putLine;
          '      integrated with BETA Compiler version '->putText;
          compilerVersion->putLine;
          (if noOfarguments > 1 then
              scanArguments:
              (for i: noOfArguments-1 repeat
                i+1->arguments->arg[];
                (if true
                 // '-test'->arg.equal then
                    true->doTest
                 // '-e'->arg.equal then
                    
                 // '-mpsTrace'->arg.equal then
                    ymerbrowser.appendDone; leave scanArguments
                 else
                    arg[]->ymerbrowser.appendProject
                if)
              for);
              (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
              
          if);
          (if doTest then
              (ymerbrowser.theMenubar).test.open;
              'Test'->(ymerbrowser.theMenubar).test.name;
              (ymerbrowser.theMenubar).test[]->(ymerbrowser.theMenubar).append
          if)
       #);
     arg: ^text
  do (*fb*)
     (if noOfarguments > 1 then
         (for i: noOfArguments-1 repeat
           i+1->arguments->arg[];
           (if true
            // '-test'->arg.equal then
               true->fb.doTest
            // '-e'->arg.equal then
               true->fb.useExtTextEd
           if)
         for);
         
     if);
     true->fb.useExtTextEd (* do it anyway *)
  #)  

