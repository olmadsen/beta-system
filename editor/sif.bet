(*  '~beta/basiclib/v1.4/betaenv';*)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
ORIGIN '~beta/basiclib/v1.4/basicsystemenv';
INCLUDE '~beta/sourcebrowser/v1.0.2/toolsMenu'
        'ymersiflib'
        '~beta/guienv/v1.3.1/guienvsystemenv';
BODY 'private/compiler'
     'private/nouser';
-- program: Descriptor --
systemenv
  (#
     setWindowEnv::  (#  do fb[]->theWindowEnv[] #);
     fb: @fragmentBrowser
       (#
          ymerBrowserWindow::< 
            (#
               browserTitle:: 
                 (# 
                 do
                    'Sif: Mjolner BETA Source Browser and Editor'
                      ->title[] (*.prepend*)
                 #);
               menubarType::< 
                 (#
                    open::< 
                      (# 
                      do
                         SLOTs.new;
                         SLOTs[]->append;
                         'Fragment'->GroupEdit.new;
                         'Tools'->GroupPerform.new;
                         INNER
                      #)
                 #);
               open::  (#  do initCompiler #)
            #);
          isBETAsif::<  (#  do true->value #);
          init::< 
            (# 
            do
               ('Sif: Known Bugs and Inconveniences','~beta/editor/v5.0.2/doc/TODO')->helpDocument; 
            #);
          
       do
          'Sif - Mjolner BETA Browser and Editor version '->putText;
          editorVersion->putText;
          ' '->put;
          machineType[]->putLine;
          '      integrated with BETA Compiler version '->putText;
          compilerVersion->putLine;
          '      betaenvVersion: '->putText;
          betaenvVersion->putLine;
          '      frejaInclude: '->putText;
          frejaInclude->putLine;
          (if noOfarguments > 1 then
              (for i: noOfArguments-1 repeat
                i+1->arguments->ymerbrowser.appendProject
              for);
              (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
              
          if)
       #)
  do (*fb*) 
  #)  

