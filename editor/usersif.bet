ORIGIN '~beta/editor/v5.1/ymersiflib';
INCLUDE 'mylangcfl'
        'user';
BODY '~beta/editor/v5.1/private/nocompiler';
-- program: Descriptor --
fragmentBrowser
  (#
     ymerBrowserWindow::<
       (#
          browserTitle:: (#  do 'User sif: '->title.prepend #);
          menubarType::<
            (#
               groupFileMenuType:: 
                 (#
                    open:: 
                      (# 
                      do
                         'New...'->iNew.new;
                         'New BETA Program...'->iNewBetaProgram.new;
                         'New BETA Library...'->iNewBetaLibrary.new;
                         newSeparator;
                         'Close'->iClose.new;
                         'Save'->iSave.new;
                         'Save As...'->iSaveAs.new;
                         'Save All...'->iSaveAll.new;
                         'Save Abstract...'->iSaveAbstract.new;
                         newSeparator;
                         'Recover'->iRecover.new;
                         'Revert'->iRevert.new;
                         newSeparator;
                         'Group Write Protection'->iSetIsReadOnly.new;
                         'Global Write Protection'
                           ->iSetGlobalWriteProtection.new
                      #)
                 #);
               userMenu: @simplemenu
                 (#
                    iUserCommand: @item
                      (#
                         onStatus:: (#  do cfe[] <> none ->value #);
                         onSelect::
                           (# 
                           do
                              (none ,'Hello user','User dialog')->alertUser;
                              'user command: '->puttext;
                              edenv.uep.global+1->edenv.uep.global->putInt;
                              newLine
                           #)
                      #);
                    iIfInWhile: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (cfe[] <> none ) and (cfe.up.mylangCFL[] <> none )
                              and (cfe.cs.node.symbol = cfe.up.mylangCFL.iff)
                                ->value
                           #);
                         onSelect:: (#  do cfe.up.ifInWhile #)
                      #);
                    open::
                      (# 
                      do
                         'User command'->iUserCommand.new;
                         'If in While'->iIfInWhile.new
                      #)
                 #);
               open::<
                 (#  do 'User'->userMenu.new; userMenu[]->append; INNER #)
            #);
          open:: (#  do 'Browser window opened'->putLine #)
       #);
     userGrammarInit::<
       (# 
       do 'userGrammarInit'->putLine; ymerBrowser.edenv.uep.userGrammarInit
       #);
     init::< (#  do  #)
  do
     (if noOfarguments > 1 then
         (for i: noOfArguments-1 repeat
           i+1->arguments->ymerbrowser.appendProject
         for);
         (if noOfArguments-1 > 0 then ymerbrowser.appendDone if);
         
     if)
  #)  

