ORIGIN '../ymersiflib';
LIB_ITEM 'editorymersiflib'
(*
 * COPYRIGHT
 *	 Copyright (C) Aarhus University
 *	 All rights reserved.
 *)
-- insertBodyAndOrigin: DoPart --
do
     (#
        msg: ^text;
        prop: ^propertyList;
        bodyName,originName: ^text;
        (*ph: @pathhandler*)
        
     do (*ph.init;*)
        (originfg[],bodyfg[])->calculateBodyAndOriginName
          ->(bodyName[],originName[]);
        edenv.groupeditorList.scan
          (# 
          do
             (if true
              // (current.fg[] = originfg[]) then
              (*(bodyfg.fullName,originfg.fullName)->ph.localPath->bodyName[];*)
                 ('BODY',bodyName[])
                   ->current.addProp
                     (# propExists::  (#  do true->delete #) #)
              // (current.fg[] = bodyfg[]) then
                 'Implementation fragment file: '->msg[];
                 current[]->edenv.implementationGroupEditor[];
                 bodyfg.fullName->msg.append;
                 msg[]->writeinfoView;
                 (if bodyfg.prop[] <> none then
                     'ORIGIN'->bodyfg.prop.deleteprop
                 if);
                 (*(originfg.fullName,bodyfg.fullName)->ph.localPath
                  ->originName[];*)
                 ('ORIGIN',originName[])
                   ->current.addProp
                     (# propExists::  (#  do true->delete #) #)
             if)
          #)
     #)  

-- createImplementationFragment: DoPart --
do
     (# bodyfg: ^astInterface.fragmentGroup
     do
        (ymerbrowserInt.ymerbrowser[],none ,false)->edenv.newBetaBody->bodyfg[];
        (if bodyfg[] <> none then
            bodyfg.fullName->ymerbrowserInt.addproject;
            (fg[],bodyfg[])->insertBodyAndOrigin
        if)
     #)  

-- selectImplementationFragment: DoPart --
do
     (# bodyfg: ^astInterface.fragmentGroup
     do
        'Select implementation file'
          ->ymerBrowserInt.projectMenuLoad;
        (* ->(ymerbrowser.theMenubar).projectMenu.iLoad.theEventHandler.onSelect; *)
        (if ymerBrowserInt.cge.fg[] <> fg[] then
            ymerBrowserInt.cge.fg[]->bodyfg[];
            (fg[],bodyfg[])->insertBodyAndOrigin
        if)
     #)  

-- calculateBodyAndOriginName: DoPart --
do
(* typical:
 * /users/ess/test/foo
 * /users/ess/test/private/foobody
 * just to test:
 * /users/ess/test/private/public/foo
 * /users/ess/test/private/foobody
 *)
     (#
        CommonDirectoryChar: charValue (#  do '/'->value #);
        lastCommon,lastDelimiterPos,olevels,blevels: @integer;
        dotdot: ^text
     do
        originfg.fullName->originName[];
        bodyfg.fullName->bodyName[];
        findCommonPrefix:
        (for i: (originName.length,bodyName.length)->min repeat
          (if originName.t[i] -> isDirectoryChar then i->lastDelimiterPos if);
          (if originName.t[i] = bodyName.t[i] then
              i->lastCommon
           else
              leave findCommonPrefix
          if)
        for);
        (if lastDelimiterPos > 0 then lastDelimiterPos->lastCommon if);
        (lastCommon+1,bodyName.length)->bodyName.sub->bodyName[];
        (lastCommon+1,originName.length)->originName.sub->originName[];
        '..'->dotdot[];
        directoryChar->dotdot.put;
        directoryChar->bodyName.findAll (#  do olevels+1->olevels #);
        directoryChar->originName.findAll (#  do blevels+1->blevels #);
        (for i: olevels repeat dotdot[]->originName.prepend for);
        (for i: blevels repeat dotdot[]->bodyName.prepend for);
        (if directoryChar = '\\' (* PC, convert to common directoryChar *) then
            originName.scanAll
              (# inx: @integer
              do
                 inx+1->inx;
                 (if ch = directoryChar then
                     (commonDirectoryChar,inx)->originName.inxPut
                 if)
              #);
            bodyName.scanAll
              (# inx: @integer
              do
                 inx+1->inx;
                 (if ch = directoryChar then
                     (commonDirectoryChar,inx)->bodyName.inxPut
                 if)
              #)
        if)
     #)  

