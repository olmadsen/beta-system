ORIGIN '../ymersiflib';
INCLUDE '~beta/process/v1.5/processmanager'
        '~beta/sysutils/v1.5/time';
-- externalTextEdit: Descriptor --
(#
   editor: ^text;
   editorprocess: ^process;
   filename: ^text;
   tempfile: ^file;
   unique_id: @
     (# id: ^text; username,systime: ^text
     do
        ''->systime[];
        systemtime->systime.putint;
        (if username[] = none then '$(USER)'->expandEnvVar->username[] if);
        ''->id[];
        username[]->id.append;
        systime[]->id.append
     exit id[]
     #);
   
do
   (if editor[] = none then '$(EDITOR)'->expandEnvVar->editor[] if);
   &process[]->editorprocess[];
   editor[]->editorprocess.init;
   '/tmp'->filename[];
   '/ReSifText'->filename.append;
   unique_id->filename.append;
   '.bet'->filename.append;
   &file[]->tempfile[];
   filename[]->tempfile.name;
   tempfile.openwrite;
   textbefore[]->tempfile.puttext;
   tempfile.close;
   filename[]->editorprocess.argument.append;
   L:
     (# errorOccurred: @boolean
     do
        false->errorOccurred;
        editorprocess.start
          (#
             error::< 
               (# 
               do true->errorOccurred; 'error'->screen.putLine; true->continue
               #);
             twoCurrent::< 
               (# 
               do
                  true->errorOccurred;
                  'twoCurrent'->screen.putLine;
                  true->continue
               #)
          #);
        (if errorOccurred then leave L if);
        editorprocess.awaitstopped
     #);
   &text[]->textafter[];
   tempfile.openread;
   (0,frombeginning)->tempfile.setpos;
   loop:
   (if (not tempfile.eos) then
       tempfile.get->textafter.put; restart loop; 
   if);
   tempfile.close;
   tempfile.delete;
   ' done'->putLine;
   
#)  

