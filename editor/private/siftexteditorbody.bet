ORIGIN '../codeviewer';
INCLUDE '~beta/guienv/v1.6/utils/navigationkeys';
INCLUDE '~beta/basiclib/v1.6/textUtils';
MDBODY nti 'siftexteditor_ntibody'
       mac 'siftexteditor_macbody'
       ppc 'siftexteditor_macbody'
       ppcmac 'siftexteditor_macbody'
       default 'siftexteditor_X11body';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-97
 *   All rights reserved.
 *)
-- siftexteditorSelectArea: DoPart --
do
   false->contents.automaticScrolling;
   (if SifViewer.switch[2] then
       'TE.selectArea'->screen.putLine;
       line1->screen.putint;
       ' '->screen.put;
       pos1->screen.putint;
       ' '->screen.put;
       line2->screen.putint;
       ' '->screen.put;
       pos2->screen.putint;
       ' '->screen.put;
       screen.newLine;
       
   if);
   contents[]->THIS(window).target;
   ((line1,pos1)->lineCharToPos,(line2,pos2)->lineCharToPos)
     ->contents.selection;
   (if SifViewer.switch[2] then
       'selection: '->screen.putText;
       contents.selection.start->screen.putint;
       ' '->screen.put;
       contents.selection.end->screen.putint;
       ' '->screen.put;
       screen.newLine;
       
   if);
   true->contents.automaticScrolling;
     

-- sdeWtexteditordeleteline: Descriptor --
(# pos1,pos2: @integer; 
do
   (if SifViewer.switch[1] then
       'TE.deleteline ***********************'->screen.putLine; showContents
   if);
   (lineno,1)->lineCharToPos->pos1;
   (lineno+1,1)->lineCharToPos->pos2;
   (if SifViewer.switch[2] then
       'lineno: '->putText;
       lineno->putint;
       ' pos1: '->putText;
       pos1->putint;
       ' pos2: '->putText;
       pos2->putint;
       newLine;
       
   if);
   (pos1,pos2)->contents.selection;
   contents.delete (*cut*) ;
   showContents;
   (pos1,pos1)->contents.selection;
   showContents
#)  

-- sdeWtexteditordeletelines: Descriptor --
(# pos1,pos2: @integer; 
do
   (if SifViewer.switch[1] then
       'TE.deletelines ***********************'->screen.putLine; showContents; 
   if);
   (lineno,1)->lineCharToPos->pos1;
   (lineno+number,1)->lineCharToPos->pos2;
   (pos1,pos2)->contents.selection;
   contents.delete (*cut*) ;
   showContents
#)  

-- sdeWtexteditorinsertline: Descriptor --
(# pos1,pos2: @integer; 
do
   (if SifViewer.switch[1] then
       'TE.insertline ***********************'->screen.putLine; 
   if);
   (lineno,1)->lineCharToPos->pos1;
   (if SifViewer.switch[2] then
       'lineno: '->putline;
       lineno->putint;
       ' pos1: '->putline;
       pos1->putint;
       newLine;
       
   if);
   (pos1,pos1)->contents.selection;
   ascii.newline->tt.put;
   tt[]->contents.insert;
   
#)  

-- sdeWtexteditorgetline: Descriptor --
(# pos1,pos2: @integer; 
do
   (if SifViewer.switch[1] then
       'TE.getline ***********************'->screen.putLine; showContents; 
   if);
   (lineno,1)->lineCharToPos->pos1;
   (lineno+1,1)->lineCharToPos->pos2;
   (if SifViewer.switch[2] then
       'lineno: '->putText;
       lineno->putint;
       ' pos1: '->putText;
       pos1->putint;
       ' pos2: '->putText;
       pos2->putint;
       newLine;
       
   if);
   (pos1,pos2-1)->contents.selection;
   contents.selection.contents->tt[];
   (if SifViewer.switch[2] then tt[]->putline; showContents;  if)
#)  

-- sdeWtexteditorinsertText: Descriptor --
(# pos1,pos2: @integer; 
do
   (if SifViewer.switch[1] then
       'TE.insertText ***********************'->screen.putLine; 
   if);
   (if SifViewer.switch[2] then
       showContents;
       'insert: ['->screen.putText;
       tt[]->screen.putText;
       ']'->screen.put;
       screen.newLine;
       showContents
   if);
   tt[]->contents.insert;
   showContents;
   
#)  

-- sdeWtexteditorsetText: Descriptor --
(# pos1,pos2: @integer;  do 'TE.setText'->screen.putLine;  #)  

-- sdeWtexteditorgettext: Descriptor --
(# pos1,pos2: @integer;  do 'TE.gettext'->screen.putLine;  #)  

-- sdeWtexteditordelete: Descriptor --
(# pos1,pos2: @integer;  do 'TE.delete'->screen.putLine;  #)  

-- sdeWtexteditorbefore: Descriptor --
(# 
do
   false->contents.automaticScrolling;
   contents.disableUpdate;
   true->prettyprinting;
   (if SifViewer.switch[1] then 'TE.before'->screen.putLine;  if);
   
#)  

-- sdeWtexteditorafter: Descriptor --
(# 
do
   contents.enableUpdate;
   true->contents.automaticScrolling;
   false->prettyprinting;
   <<SLOT siftexteditorUpdate:Descriptor>>;
   (if SifViewer.switch[1] then
       'TE.after'->screen.putLine; 
   if)
#)  

-- sdeWtexteditorrefresh: Descriptor --
(# pos1,pos2: @integer;  do 'TE.refresh'->screen.putLine;  #)  

-- sdeWtexteditorLastposition: DoPart --
do contents.length->pos  

-- sdeWtexteditorPosToLineChar: DoPart --
do
   pos
     ->contents.posToRowCol
       (#
          indexError:: 
            (# 
            do
               'pos: '->msg.append; pos->msg.putInt; msg.newLine; true->continue
            #)
       #)->(line,char);
   (* guienv bug?  char-1->char  *)
     

-- sdeWtexteditorLineCharToPos: DoPart --
do
   (line,char)
     ->contents.rowColToPos
       (#
          indexError:: 
            (# 
            do
               'row: '->msg.append;
               row->msg.putInt;
               msg.newLine;
               'col: '->msg.append;
               col->msg.putInt;
               msg.newLine;
               true->continue
            #)
       #)->pos;
     

-- sifTexteditorOnOpen: DoPart --
do sifViewer.onOpen  

-- sifTexteditorOnDoubleClick: DoPart --
do
   (if not textediting then
       (if not modifier then
           (start,end,false)->SifViewer.selectionToFocus
       if);
       modifier->SifViewer.handleDoubleClick
    else
       (start,end)->sifViewer.checkTexteditingFinished
   if);
     

-- sifTexteditorOnClick: DoPart --
do
   (if not textediting then
       (start,end,false)->SifViewer.selectionToFocus
    else
       (start,end)->SifViewer.checkTexteditingFinished
   if);
     

-- sifTexteditorEnterTextEditing: DoPart --
do
     (# pos1,pos2: @integer
     do
        contents.setTexteditStyle;
        (if ch = ' ' then
            contents.selection->(pos1,pos2); (pos1,pos1)->contents.selection
        if);
        ch->sifViewer.doTextEdit;
        
     #);
     

-- sifTexteditorExitTextEditing: DoPart --
do SifViewer.doRevertTextedit  

-- sifTexteditorOnBeforeChange: DoPart --
do
   (if sifViewer[] <> none then
       (if sifViewer.switch[2] then
           'onbefore change isReadOnly: '->putText;
           (if isReadOnly then 'true'->putLine else 'false'->putLine if);
           
       if);
       (if isReadOnly then
           prettyprinting->allow
        else
           (if prettyprinting then
               true->allow
            else
               (if keyAllowed then textediting->allow else false->allow if)
           if)
       if);
       (if sifViewer.switch[2] then
           'onBeforeChange: '->putText;
           allow->putBoolean;
           newLine;
           'theText: '->putText;
           theText->putText;
           '.'->put;
           newLine;
           
       if);
       INNER onBeforeChange
   if)  

-- siftexteditorKeyDownTimerAction: DoPart --
do
   stop;
   (if sifViewer[] <> none then
       (if SifViewer.switch[1] then
           'keyDownTimerAction'->putLine;
           'theKey: '->putText;
           theKey->putInt;
           newLine;
           
       if);
       (if not textediting then
           (if theKey
            // leftArrow then
               sifViewer.onLeftArrowKey
            // rightArrow then
               sifViewer.onRightArrowKey
            // upArrow then
               sifViewer.onUpArrowKey
            // downArrow then
               sifViewer.onDownArrowKey
            // ascii.cr then
               sifViewer.onReturnKey
            // ascii.del then
               sifViewer.onDeleteKey
            // ascii.bs then
               sifViewer.onDeleteKey
            // ascii.esc then
               
            else
               
           if);
           
       if)
   if);
     

-- sifTexteditorOnKeyDown: DoPart --
do
   (if sifViewer[] <> none then
       (if SifViewer.switch[2] then
           'onKeyDown: '->putText;
           ch->putInt;
           '<'->put;
           ch->put;
           '>'->put;
           newLine;
           'textediting: '->putText;
           (if textediting then 1->putInt else 0->putInt if);
           newline;
           
       if);
       (if ch
        // leftArrow then
           (selection.start,selection.start)->selection;
           ch->theKey;
           keyDownTimer.start
        // rightArrow then
           (selection.end,selection.end)->selection;
           ch->theKey;
           keyDownTimer.start
        // upArrow then
           (selection.start,selection.start)->selection;
           ch->theKey;
           keyDownTimer.start
        // downArrow then
           (selection.end,selection.end)->selection;
           ch->theKey;
           keyDownTimer.start
        // ascii.ht then
           localposition->sifviewer.onPopUpButton
       if);
       (if isReadOnly then
           INNER onKeyDown
        else
           ch->sifViewer.keyAllowed->keyAllowed;
           (if keyAllowed then
               (if not textEditing then
                   (if (ch >= ' ') and (ch <= '~') then
                       ch->enterTextEditing
                    else
                       (if ch
                        // ascii.cr // ascii.del // ascii.bs then
                           ch->theKey; keyDownTimer.start
                        else
                           INNER onKeyDown
                       if)
                   if)
                else
                   (if ch = ascii.esc then exitTextEditing if); 
               if)
           if);
           
       if)
   if);
     

-- siftexteditorMouseDownTimerAction: DoPart --
do   

-- sifTexteditorOnMouseDown: DoPart --
do
   (if sifViewer[] <> none then
       (if sifViewer.switch[2] then
           'on mouse down isReadOnly: '->putText;
           (if isReadOnly then 'true'->putLine else 'false'->putLine if);
           
       if);
       (if buttonState // 3 then localposition->sifviewer.onPopUpButton if);
       INNER onMouseDown
   if)  

-- siftexteditorMouseUpTimerAction: DoPart --
do
   stop;
   (if sifViewer[] <> none then
       (if SifViewer.switch[1] then
           '-------------------'->putLine; 'onMouseUp'->putLine; 
       if);
       (if SifViewer.switch[2] then
           'selection: '->putText;
           selection.start->putInt;
           ' '->put;
           selection.end->putInt;
           newLine;
           'oldselection: '->putText;
           oldselection.h->putInt;
           ' '->put;
           oldselection.v->putInt;
           newLine;
           
       if);
       (if (siftexteditorPrivate.me[] <> none ) then
           (if siftexteditorPrivate.me.doubleClick then
               (if SifViewer.switch[1] then
                   'doubleClick'->putLine;
                   (if siftexteditorPrivate.me.shiftKey then
                       'shiftKey'->putLine
                   if);
                   (if siftexteditorPrivate.me.controlKey then
                       'controlKey'->putLine
                   if);
                   (if siftexteditorPrivate.me.metaKey then
                       'metaKey'->putLine
                   if);
                   
               if);
               siftexteditorPrivate.me.shiftKey->theShiftKey;
               (oldSelection.h,oldSelection.v,theShiftKey)->onDoubleClick
            else
               (if selection.start <> selection.end then
                   (if SifViewer.switch[1] then
                       'draggingSelection'->putLine; 
                   if);
                   (if siftexteditorPrivate.me.ControlKey or
                   siftexteditorPrivate.me.ShiftKey or
                   siftexteditorPrivate.me.MetaKey then
                       (if oldSelection.v < selection.end then
                           (oldSelection.h,selection.end)->onClick
                        else
                           (selection.start,oldSelection.v)->onClick
                       if)
                    else
                       selection->oldSelection;
                       true->draggingSelection;
                       (if sifViewer[] <> none then
                           (if SifViewer.switch[1] then
                               'onIdle'->putLine; 
                           if);
                           (if draggingSelection then
                               oldSelection->onClick; false->draggingSelection
                           if)
                       if)
                   if)
                else
                   (if siftexteditorPrivate.me.ControlKey or
                   siftexteditorPrivate.me.ShiftKey or
                   siftexteditorPrivate.me.MetaKey then
                       
                    else
                       selection->oldSelection->onClick
                   if)
               if)
           if);
           (*INNER onMouseUP;*)
           
        else
           'siftexteditorPrivate.me[] is none!!'->putLine
       if);
       (if SifViewer.switch[1] then
           '-------------------'->putLine; 'onMouseUp end'->putLine; 
       if)
   if);
   (*pause; selection->oldSelection->onClick  *)
   (*none-> siftexteditorPrivate.me[];*)
     

-- sifTexteditorOnMouseUp: DoPart --
do
   doubleClick->siftexteditorPrivate.me.doubleClick;
   shiftKey->siftexteditorPrivate.me.shiftKey;
   metaKey->siftexteditorPrivate.me.metaKey;
   ControlKey->siftexteditorPrivate.me.ControlKey;
   mouseUpTimer.start;
     

-- siftexteditorNewfragment: DoPart --
do
     (# theSifViewer: ^codeviewer
     do
        THIS(guienv)[]->UI[];
        (if theEditorRoot[] = none then
            'NewFragment: theEditorRoot is none!!'->putLine
         else
            (if
            'beta'->(theEditorRoot.frag.grammar.grammarIdentification).equal
             then
                theGrammar[]->betaGrammar[]
             else
                (if
                'property'
                  ->(theEditorRoot.frag.grammar.grammarIdentification).equal
                 then
                    true->isApropertyEditor
                if)
            if);
            INNER newFragment;
            (if mps[] = none then
                
             else
                (if theEditorRoot.father <> none then true->isASubeditor if);
                (*              'isAproperty editor: '->putText; 
                 (if isApropertyEditor then 
                 'true'->putLine else 'false'->putLine
                 if);
                 'isAsubeditor: '->putText; 
                 (if isAsubEditor then 
                 'true'->putLine else 'false'->putLine
                 if);
                 *)
                (if SifViewer[] = none then
                    &codeviewerType[]->SifViewer[]->ed[];
                    theEditorRoot.frag[]->THIS(sifTextEditor).frag[];
                    theEditorRoot[]->THIS(sifTexteditor).editorRoot[];
                    initialSelection[]->THIS(sifTexteditor).initialSelection[];
                    (mps[],theGrammar[],UI[],THIS(SifTextEditor)[],
                     theEditorRoot.frag[],theEditorRoot[],initialSelection[])
                      ->SifViewer.init;
                    (if not isApropertyEditor and not isAsubEditor then
                        SifViewer[]->SifViewer.setupFormEditor
                    if);
                    
                 else
                    (if theEditorRoot.frag[] = frag[] then
                        SifViewer.doReprettyprint;
                        SifViewer.cs->SifViewer.setFocus
                     else
                        (theEditorRoot.frag[],not isAsubeditor)
                          ->SifViewer.checkFormEditorExistence->theSifViewer[];
                        (if theSifViewer[] = none then
                            &codeviewerType[]->SifViewer[]->ed[];
                            theEditorRoot.frag[]->THIS(sifTextEditor).frag[];
                            theEditorRoot[]->THIS(sifTexteditor).editorRoot[];
                            initialSelection[]
                              ->THIS(sifTextEditor).initialSelection[];
                            (mps[],theGrammar[],UI[],THIS(SifTextEditor)[],
                             theEditorRoot.frag[],theEditorRoot[],
                             initialSelection[])->SifViewer.init;
                            (if not isApropertyEditor and not isAsubEditor then
                                SifViewer[]->SifViewer.setupFormEditor
                            if);
                            
                         else
                            theSifViewer[]->SifViewer[];
                            theEditorRoot.frag[]->THIS(sifTextEditor).frag[];
                            theEditorRoot[]->THIS(sifTexteditor).editorRoot[];
                            initialSelection[]
                              ->THIS(sifTextEditor).initialSelection[];
                            SifViewer.doReprettyprint;
                            SifViewer.cs->SifViewer.setFocus
                        if)
                    if)
                if);
                
            if);
            
        if)
     #);
     

-- siftexteditorDeleteArea: DoPart --
do
   (if SifViewer.switch[1] then
       'TE.deleteArea'->screen.putLine; showContents; 
   if);
   ((line1,pos1)->lineCharToPos,((line2,pos2)->lineCharToPos)+1 (* why?? *) )
     ->contents.selection;
   (if SifViewer.switch[2] then
       'line1: '->screen.putText;
       line1->screen.putint;
       ' line2: '->screen.putText;
       line2->screen.putint;
       ' pos1: '->screen.putText;
       pos1->screen.putint;
       ' pos2: '->screen.putText;
       pos2->screen.putint;
       screen.newLine;
       'selection.start: '->screen.putline;
       contents.selection.start->screen.putint;
       ' selection.end: '->screen.putline;
       contents.selection.end->screen.putint;
       screen.newLine;
       
   if);
   contents.delete (*cut*) ;
   showContents  

-- siftexteditorOnFrameChanged: Descriptor --
(# oz,nz: @Point
do
   (if not textediting then
       (if SifViewer[] <> none then
           (if sifViewer.astView[] <> none then
               (if sifViewer.adaptivePrettyprinting then
                   oldFrame.size->oz;
                   newFrame.size->nz;
                   (if nz.h <> oz.h then
                       newFrame.size->charWidth->SifViewer.astView.pp.width;
                       (if sifviewer.switch[13] then
                           'frameChanged: pp width '->putText;
                           sifViewer.astView.pp.width->putInt;
                           newLine
                       if);
                       (editorRoot[],- 1)->SifViewer.astView.pp.present;
                       (if SifViewer.cs.node[] <> none then
                       (*SifViewer.cs->SifViewer.astToSelection; *)
                           sifViewer.cs->sifViewer.setFocus
                       if)
                   if)
                else
                   80->SifViewer.astView.pp.width
               if)
           if)
       if)
   if)
#)  

-- siftexteditorOnEnableTarget: DoPart --
do
     (# s,t: @integer
     do
        (if (sifViewer[] <> none ) and (not textediting) then
        (*sifViewer.cs->sifViewer.astToSelection;*)
        (*THIS(siftexteditor).contents.selection->(s,t);
         (s,t)->THIS(siftexteditor).contents.selection;
         is now disabled to avoid automatic scrollintoview *)
        (* all this just to get the selection back when reentering the window *)
        (* THIS(siftexteditor).contents.selection.scrollIntoView*)
            
        if)
     #)  

-- siftexteditorOnDisableTarget: DoPart --
do   

-- siftexteditorOpen: DoPart --
do INNER Open; defaultSize->size; <<SLOT siftexteditorSetBindings:Descriptor>>  

-- siftexteditorDefaultSize: DoPart --
do (500,150)->value; INNER  

-- siftexteditorClose: DoPart --
do INNER Close; (if SifViewer[] <> none then SifViewer.close if)  

-- siftexteditorInitialContractionLevel: DoPart --
do 2->value; INNER initialContractionLevel  

-- siftexteditorPrivate: Descriptor --
(#
   me: @ (# doubleClick,shiftKey,metaKey,controlKey: @boolean #);
   textColor: @integer
#)  

-- siftexteditorScrollIntoView: DoPart --
do contents.selection.scrollIntoView  

-- siftexteditorCenterSelection: DoPart --
do   

-- siftexteditorSelectionLength: DoPart --
do contents.selection.end-contents.selection.start->length  
