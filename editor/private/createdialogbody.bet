ORIGIN '../createdialog';
INCLUDE '~beta/guienv/v1.3.1/fields'
        '~beta/guienv/v1.3.1/stddialogs'
        '~beta/guienv/v1.3.1/utils/guienvadds'
        '~beta/sourcebrowser/v1.0/private/ymerBody';
(* er dettte noedvendigt?? *)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- createPrivate: Descriptor --
(#
   fatherpos,fathersize,mysize: @point;
   rescale:
     (# newsize: @point; hpct,vpct: @real; h,v: @integer
     do
        size->newsize;
        newsize.h / mysize.h->hpct;
        newsize.v / mysize.v->vpct;
        newsize->mysize;
        theLanguageText.position->(h,v);
        (h*hpct,v*vpct)->theLanguageText.position;
        theLanguageText.size->(h,v);
        (h*hpct,v)->theLanguageText.size;
        theGroupName.position->(h,v);
        (h*hpct,v*vpct)->theGroupName.position;
        theGroupName.size->(h,v);
        (h*hpct,v)->theGroupName.size;
        theGroupNameText.position->(h,v);
        (h*hpct,v*vpct)->theGroupNameText.position;
        theGroupNameText.size->(h,v);
        (h*hpct,v)->theGroupNameText.size;
        theFormName.position->(h,v);
        (h*hpct,v*vpct)->theFormName.position;
        theFormName.size->(h,v);
        (h*hpct,v)->theFormName.size;
        theFormNameText.position->(h,v);
        (h*hpct,v*vpct)->theFormNameText.position;
        theFormNameText.size->(h,v);
        (h*hpct,v)->theFormNameText.size;
        createButton.position->(h,v);
        (h*hpct,v*vpct)->createButton.position;
        createButton.size->(h,v);
        (h*hpct,v)->createButton.size;
        cancelButton.position->(h,v);
        (h*hpct,v*vpct)->cancelButton.position;
        cancelButton.size->(h,v);
        (h*hpct,v)->cancelButton.size;
        
     #);
   dialogopened: @boolean;
   DialogText: textField
     (#
        eventhandler::< 
          (#
             onBeforeChange:: 
               (# txt: ^text
               do
                  (if length = 1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl // ascii.cr then
                          THIS(createDialog).private.createButton.
                            theEventhandler.onMouseup;
                          false->allow;
                          
                      if);
                      
                  if)
               #);
             onKeyDown:: 
               (# 
               do
                  (if ch = ascii.esc then
                      THIS(createDialog).private.cancelButton.theEventhandler.
                        onMouseup;
                      
                  if);
                  
               #);
             
          #);
        
     #);
   extension: ^text;
   theLanguageMenu: menu
     (#
        open:: 
          (# 
          do
             'Languages'->name;
             ymerPrivate.grammarTable.scan
               (#
                  gramItem: menuItem
                    (#
                       grammarName,grammarExtension,grammarPath: ^text;
                       eventhandler:: 
                         (#
                            onSelect:: 
                              (#  do grammarExtension[]->extension[] #)
                         #);
                       open::  (#  do grammarName[]->name #)
                    #);
                  gmi: ^gramItem
               do
                  &gramItem[]->gmi[];
                  currentName[]->gmi.grammarName[];
                  currentExtension[]->gmi.grammarExtension[];
                  currentPath[]->gmi.grammarPath[];
                  gmi.open;
                  gmi[]->append;
                  
               #);
             
          #)
     #);
   theLanguageText: @optionButton
     (#
        contents:
          (#  exit (currentItem->theMenu.getMenuItemByNumber).name #);
        theMenu: @theLanguageMenu;
        open:: 
          (# 
          do
             'Language:'->label;
             theMenu.open;
             theMenu[]->popupMenu;
             preferredSize->size;
             ((20,10),(20+320,10+30))->frame;
             
          #);
        
     #);
   theGroupName: @staticText
     (#
        open:: 
          (#  do ((20,50),(20+130,50+30))->frame; 'Group Name:'->label #);
        eventhandler::  (# onFatherFrameChanged::  (#  do rescale #) #)
     #);
   theGroupNameText: @DialogText
     (#
        open::  (#  do ((20+140,50),(20+320,50+30))->frame #);
        eventhandler:: 
          (#
             onMouseUp:: 
               (# t: @styledtext
               do
                  (if doubleClick and shiftKey then
                      THIS(createDialog)[]
                        ->fileCreationDialog
                          (# f: @file
                          do
                             'Select group file name'->title[];
                             'Group filename'->label[];
                             contents->f.name;
                             f.entry.path.head->path[];
                             (if path.empty then
                                 none ->path[]; '*'->filter[]
                              else
                                 '/*'->(path.copy).append->filter[]; 
                             if);
                             extension[]->filter.append
                          #)->t.puttext;
                      t[]->contents
                  if)
               #)
          #)
     #);
   theFormName: @staticText
     (#
        open:: 
          (#  do ((20,90),(20+130,90+30))->frame; 'Form Name:'->label #)
     #);
   theFormNameText: @DialogText
     (# open::  (#  do ((20+140,90),(20+320,90+30))->frame #);  #);
   createButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do
                  theLanguageText.contents->grammar[];
                  theGroupNameText.contents->groupName[];
                  theFormNameText.contents->formName[];
                  create;
                  THIS(createDialog).hide
               #)
          #);
        open:: 
          (#  do ((20,130),(20+150,130+30))->frame; 'Create'->label;  #);
        
     #);
   cancelButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do
                  theGroupNameText.contents->grammar[];
                  theGroupNameText.contents->groupName[];
                  theFormNameText.contents->formName[];
                  cancel;
                  THIS(createDialog).hide
               #)
          #);
        open:: 
          (#  do ((190,130),(190+150,130+30))->frame; 'Cancel'->label;  #);
        
     #);
   
#)  

-- createPopup: DoPart --
do
   (if not private.dialogopened then open;  if);
   titleText[]->title;
   (if (groupText[] <> none ) or not groupText.empty then
       private.theGroupNameText.all->private.theGroupNameText.selection;
       private.theGroupNameText.delete;
       groupText[]->private.theGroupNameText.insert;
       
   if);
   private.theGroupNameText.all->private.theGroupNameText.selection;
   (if (formText[] <> none ) or not formText.empty then
       private.theFormNameText.all->private.theFormNameText.selection;
       private.theFormNameText.delete;
       formText[]->private.theFormNameText.insert;
       
   if);
   private.theFormNameText.all->private.theFormNameText.selection;
   INNER ;
   (if father[] <> none then
       size->private.mysize;
       father.size->private.fathersize;
       father.position->private.fatherpos;
       (private.fatherpos.v+(private.fathersize.v-private.mysize.v) div 2,
        private.fatherpos.h+(private.fathersize.h-private.mysize.h) div 2)
         ->position;
       
    else
       mouse.globalPosition->position; 
   if);
   show;
     

-- createOpen: DoPart --
do
   (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       private.theLanguageText.open;
       private.theGroupName.open;
       private.theGroupNameText.open;
       private.theFormName.open;
       private.theFormNameText.open;
       private.createButton.open;
       private.cancelButton.open;
       (360,170)->private.mysize->size;
       INNER ;
       
   if);
     

