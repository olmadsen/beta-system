ORIGIN '../ymersiflib';
INCLUDE '../moreprompts';
LIB_ITEM 'editorymersiflib';
(*
 * COPYRIGHT
 *	 Copyright Mjolner Informatics, 1995-2000
 *	 All rights reserved.
 *)
-- ymersiflibSearchCheckNode: DoPart --
do
     (#
        lexText: ^astInterface.lexemText;
        exp: ^astInterface.expanded;
        help,currentText: ^text;
        applicationOf,semanticErrorsOrNotChecked: @boolean
     do
        (if not searchFor.onlyApplications then
            (if node.kind
             // mps.ast.kinds.nameDecl // mps.ast.kinds.nameAppl
             // mps.ast.kinds.string then
                (if
                (searchFor.t[],node[],searchFor.caseSensitive,
                 searchFor.wholeWord)->edenv.inLexem then
                    true->value;
                    node[]->lexText[];
                    '...'->t[];
                    lexText.getText->t.append;
                    '...'->t.append;
                    3 (* search hit *) ->type
                if)
            if)
         else
            (if searchFor.theNameDcl[] <> none then
                (if (node.kind = mps.ast.kinds.nameAppl) or
                (node.kind = mps.ast.kinds.nameDecl) then
                    node[]->lexText[];
                    lexText.getText->currentText[];
                    (if currentText[]->searchFor.t.equalNCS then
                        (node[],searchFor.theNameDcl[])->edenv.isApplicationOf
                          ->(applicationOf,semanticErrorsOrNotChecked);
                        (if applicationOf then
                            true->value;
                            node[]->lexText[];
                            '...'->t[];
                            lexText.getText->t.append;
                            '...'->t.append;
                            3 (* search hit *) ->type
                        if)
                    if)
                if)
            if)
        if)
     #)  

-- onBrowserSelectionChanged: DoPart --
do
   (if edenv.searchDialogOpened then true->callSearch if);
   (if edenv.replaceDialogOpened then true->callReplace if);
   (if (ymerBrowserInt.cge[] <> none ) and (ymerBrowserInt.cge.fg[]->isProgram)
    then
       ymerBrowserInt.cge.fg[]->lastProgramFG[];
       ymerBrowserInt.cge.fgTitle->lastProgramName[]
   if)  

-- ymersiflibSearchShowSearchHits: DoPart --
do
   doCommand
     (# 
     do
        'Browse hits...'->ymerbrowserInt.infoView.setText;
        ymerbrowserInt.infoView.msgPush;
        true->ymerbrowserInt.infoView.silent;
        t[]->searchFor.t[];
        caseSensitive->searchFor.caseSensitive;
        wholeWord->searchFor.wholeword;
        onlyApplications->searchFor.onlyApplications;
        theNameDcl[]->searchFor.theNameDcl[];
        (if searchArea
         // searchArea_cs then
            node[]->searchNode[]; ff[]->searchff[]; fg[]->searchfg[]
         // searchArea_ff then
            none ->searchNode[]; ff[]->searchff[]; fg[]->searchfg[]
         // searchArea_fg then
            none ->searchNode[]; none ->searchff[]; fg[]->searchfg[]
         // searchArea_project // searchArea_domain // searchArea_extent then
            none ->searchNode[]; none ->searchff[]; none ->searchfg[]
        if);
        (if theSearchViewer[] = none then
            &searchViewer[]->thesearchViewer[];
            ymerBrowserInt[]->thesearchViewer.init;
            showAllHits->theSearchViewer.showAllHits;
            searchArea->theSearchViewer.searchArea;
            thesearchViewer.open
         else
            (if not thesearchViewer.opened then
                &searchViewer[]->thesearchViewer[];
                ymerBrowserInt[]->thesearchViewer.init;
                showAllHits->theSearchViewer.showAllHits;
                searchArea->theSearchViewer.searchArea;
                thesearchViewer.open;
                (searchfg[],searchff[],searchNode[])
                  ->thesearchViewer.registerFragmentGroup
             else
                showAllHits->theSearchViewer.showAllHits;
                searchArea->theSearchViewer.searchArea;
                (searchfg[],searchff[],searchNode[])
                  ->thesearchViewer.registerFragmentGroup;
                thesearchViewer.show;
                thesearchViewer.bringToFront
            if)
        if);
        false->ymerbrowserInt.infoView.silent;
        ymerbrowserInt.infoView.msgPop
     #)  

-- ymersiflibSearchReplaceAll: DoPart --
do
   thisCommand: doCommand
     (#
        noOfReplacements,noOfFiles: @integer;
        didReplace: @boolean;
        changedGroups: @set (# element:: mps.ast.fragmentGroup #);
        changedForms: @set (# element:: mps.ast.fragmentForm #);
        registerReplacement:
          (#
             node: ^mps.ast.ast;
             ge: ^edenv.groupEditor;
             ce: ^codeeditor
          enter node[]
          do
             (if true
             (* 2000-07-26: Now both variants are working.
              * There was a bug in sourceBrowser.selectFragmentform:
              * it called  edenv.groupEditorList.createGroupEditor instead of
              * findOrCreateGroupEditor, and thereby creating an extra groupeditor
              *) then
                 (ymerBrowserInt.id,node.frag.father)
                   ->edenv.groupEditorList.findGroupEditor->ge[]
              else
                 (ymerBrowserInt.id,node.frag.father,false)
                   ->edenv.groupEditorList.findOrCreateGroupEditor->ge[]
             if);
             (if ge[] <> none then
                 node.frag[]->ge.formEditorList.findEditor->ce[];
                 (if ce[] = none then
                     ((node.frag.father).fullname,node.frag.name)
                       ->ymerbrowserInt.selectFragmentForm;
                     node.frag[]->ge.formEditorList.findEditor->ce[]
                 if);
                 (if ce[] <> none then
                     true->didReplace; noOfReplacements+1->noOfReplacements
                  else
                     'RegisterReplacement: ce is none!'->putLine
                 if)
              else
                 ((node.frag.father).fullname,node.frag.name)
                   ->ymerbrowserInt.selectFragmentForm;
                 (node.frag[],node[])->edenv.findEditorForForm->ce[];
                 (if ce[] <> none then
                     true->didReplace; noOfReplacements+1->noOfReplacements
                  else
                     'RegisterReplacement: ce is none! (2)'->putLine
                 if)
             if);
             (if not (node.frag.father->changedGroups.has) then
                 node.frag.father->changedGroups.insert;
                 node.frag[]->changedForms.insert
              else
                 (if not (node.frag[]->changedForms.has) then
                     node.frag[]->changedForms.insert
                 if)
             if)
          exit ce[]
          #);
        writeStatus:
          (# msg: @text
          do
             (if didReplace then
                 noOfReplacements->msg.putInt;
                 (if noOfReplacements = 1 then
                     ' replacement in '->msg.append
                  else
                     ' replacements in '->msg.append
                 if);
                 changedGroups.size->msg.putInt;
                 (if changedGroups.size = 1 then
                     ' file '->msg.append
                  else
                     ' files '->msg.append
                 if);
                 'and '->msg.append;
                 changedForms.size->msg.putInt;
                 (if changedGroups.size = 1 then
                     ' fragment form'->msg.append
                  else
                     ' fragment forms'->msg.append
                 if)
              else
                 t->msg; ' not found'->msg.append
             if);
             msg[]->writeInfoView
          #);
        ce: ^codeeditor;
        scanFragmentGroups:
          (#
             currentNode:<
               (# node: ^astInterface.ast enter node[] do INNER #);
             OK: @boolean;
             checkAll,doNotCheckAll,cancelCommand: @boolean;
             checkGroupStatus:
               (# fg: ^astInterface.fragmentGroup; value: @boolean
               enter fg[]
               do
                    (# msg: ^text
                    do
                       (if not ((mps.ast[],fg[])->getDoneCheckProperty) then
                           (if true
                            // checkAll then
                               fg[]->check; true->value
                            // doNotCheckAll then
                               false->value
                            else
                               false->value;
                               (fg.name).copy->msg[];
                               '\nmust be (re)checked in order to (re)build the semantic links\n\nCheck?'
                                 ->msg.append;
                               ('Check?',msg[])
                                 ->gui.promptForBooleanAll
                                   (#
                                      ok:: 
                                        (#  do fg[]->check; true->value #);
                                      cancel:: 
                                        (#  do true->cancelCommand #);
                                      okAll:: 
                                        (# 
                                        do
                                           fg[]->check;
                                           true->value;
                                           true->checkAll
                                        #);
                                      notOkAll:: 
                                        (#  do true->doNotCheckAll #)
                                   #)
                           if)
                        else
                           true->value
                       if)
                    #)
               exit value
               #);
             scanFragmentForms:
               (#
                  exp: ^astInterface.expanded;
                  searchfg: ^astInterface.fragmentGroup;
                  ff,searchff: ^astInterface.fragmentForm;
                  searchNode,searchRoot: ^astInterface.ast;
                  OK: @boolean;
                  ge: ^edenv.groupEditor;
                  ce: ^codeeditor
               enter (searchfg[],searchff[],searchNode[])
               do
                  (if searchfg[] <> none then
                      searchfg.fragmentList.scan
                        (# 
                        do
                           (if current.type
                            // mps.AST.formType then
                               thisFragment:
                                 (# 
                                 do
                                    current.open->ff[];
                                    (if ff[] <> none then
                                        (if searchff[] = none then
                                            ff.root[]->searchRoot[]
                                         else
                                            (if searchff.name->(ff.name).equal
                                             then
                                                (if searchNode[] = none then
                                                    ff.root[]->searchRoot[]
                                                 else
                                                    searchNode[]->searchRoot[]
                                                if)
                                             else
                                                leave thisFragment
                                            if)
                                        if);
                                        (if
                                        (searchRoot.kind =
                                         mps.AST.kinds.interior) then
                                            searchRoot[]->exp[];
                                            l: exp.suffixWalk
                                              (# t,dummy: ^text
                                              do current[]->currentNode
                                              #)
                                        if)
                                     else
                                        'scanFragmentForms: could not open fragmentform'
                                          ->putLine
                                    if)
                                 #)
                           if)
                        #)
                  if)
               #);
             gList: ^projectInfo.groupList
          enter (fg[],ff[],node[],searchArea)
          do
             (if fg[] <> none then
                 (fg[],ff[],node[])->scanFragmentForms
              else
                 (if ymerBrowserInt.currentProject[] <> none then
                     (if searchArea
                      // 4 (* project *) then
                         ymerBrowserInt.currentProject.scanGroups;
                         ymerBrowserInt.currentProject.groups[]->gList[]
                      // 5 (* domain *) then
                         (if ymerBrowserInt.currentGroup[] <> none then
                             ymerBrowserInt.currentGroup.getDomain->gList[]
                         if)
                      // 6 (* extent *) then
                         (if ymerBrowserInt.currentGroup[] <> none then
                             ymerBrowserInt.currentGroup.getExtent->gList[]
                         if)
                     if);
                     (if gList[] <> none then
                         ymerBrowserInt.currentProject.groups.scan
                           (# fg: ^astinterface.fragmentGroup
                           do
                              current.getfg->fg[];
                              (if fg[] <> none then
                                  (if not (fg[]->fgSearchSet.has) and not
                                  (fg[]->fgSearchExcludeSet.has) then
                                      (if searchFor.onlyApplications then
                                          fg[]->checkGroupStatus->OK;
                                          (if cancelCommand then
                                              edenv.setStructureCursor;
                                              leave thisCommand
                                          if)
                                       else
                                          true->OK
                                      if);
                                      (if OK then
                                          fg[]->fgSearchSet.insert;
                                          (fg[],ff[],node[])->scanFragmentForms
                                       else
                                          fg[]->fgSearchExcludeSet.insert
                                      if)
                                   else
                                      (if searchFor.onlyApplications then
                                          fg[]->checkGroupStatus->OK;
                                          (if cancelCommand then
                                              edenv.setStructureCursor;
                                              leave thisCommand
                                          if)
                                       else
                                          true->OK
                                      if);
                                      (if OK then
                                          (fg[],ff[],node[])->scanFragmentForms
                                       else
                                          fg[]->fgSearchSet.delete;
                                          fg[]->fgSearchExcludeSet.insert
                                      if)
                                  if)
                              if)
                           #)
                     if)
                 if)
             if);
             
          #)
     do (* writeInfoViewClear;*)
        'Replace all...'->writeInfoView;
        true->ymerbrowserInt.infoView.silent;
        t[]->searchFor.t[];
        caseSensitive->searchFor.caseSensitive;
        wholeWord->searchFor.wholeword;
        onlyApplications->searchFor.onlyApplications;
        theNameDcl[]->searchFor.theNameDcl[];
        (if searchArea
         // searchArea_cs then
            node[]->searchNode[]; ff[]->searchff[]; fg[]->searchfg[]
         // searchArea_ff then
            none ->searchNode[]; ff[]->searchff[]; fg[]->searchfg[]
         // searchArea_fg then
            none ->searchNode[]; none ->searchff[]; fg[]->searchfg[]
         // searchArea_project // searchArea_domain // searchArea_extent then
            none ->searchNode[]; none ->searchff[]; none ->searchfg[]
        if);
        (searchfg[],searchff[],searchNode[],searchArea)
          ->scanFragmentGroups
            (#
               currentNode:: 
                 (#
                    lexText: ^astInterface.lexemText;
                    exp: ^astInterface.expanded;
                    currentText: ^text;
                    applicationOf,semanticErrorsOrNotChecked: @boolean
                 do
                    (if searchFor.onlyApplications then
                        (if searchFor.theNameDcl[] <> none then
                            (if (node.kind = mps.ast.kinds.nameAppl) or
                            (node.kind = mps.ast.kinds.nameDecl) then
                                node[]->lexText[];
                                lexText.getText->currentText[];
                                (if currentText[]->searchFor.t.equalNCS then
                                    (node[],searchFor.theNameDcl[])
                                      ->edenv.isApplicationOf
                                      ->
                                        (applicationOf,
                                         semanticErrorsOrNotChecked);
                                    (if applicationOf then
                                        node[]->registerReplacement->ce[];
                                        (if ce[] <> none then
                                            (node[],t[],newt[])->ce.editLexem
                                         else
                                            'ReplaceAll: ce is none! (3)'
                                              ->putLine
                                        if)
                                    if)
                                if)
                            if)
                        if)
                     else
                        (if node.kind
                         // mps.ast.kinds.nameDecl // mps.ast.kinds.nameAppl
                         // mps.ast.kinds.string then
                            (if
                            (searchFor.t[],node[],searchFor.caseSensitive,
                             searchFor.wholeWord)->edenv.inLexem then
                                node[]->registerReplacement->ce[];
                                (if ce[] <> none then
                                    (node[],t[],newt[])->ce.editLexem
                                 else
                                    'ReplaceAll: ce is none! (2)'->putLine
                                if)
                            if)
                        if)
                    if)
                 #)
            #);
        (if replaceDeclaration then
            (if searchFor.onlyApplications and (searchFor.theNameDcl[] <> none )
             then
                searchFor.theNameDcl[]->registerReplacement->ce[];
                (if ce[] <> none then
                    (searchFor.theNameDcl[],t[],newt[])->ce.editLexem
                 else
                    'ReplaceAll: ce is none! (4)'->putLine
                if)
            if)
        if);
        false->ymerbrowserInt.infoView.silent;
        writeStatus;
        
     #)  

-- ymersiflibCheckGroupStatus: DoPart --
do
     (# msg: ^text
     do
        (if searchFor.onlyApplications then
            (if not ((mps.ast[],fg[])->getDoneCheckProperty) then
                (if true
                 // checkAll then
                    fg[]->check; true->value
                 // doNotCheckAll then
                    false->value
                 else
                    false->value;
                    (fg.name).copy->msg[];
                    '\nmust be (re)checked in order to (re)build the semantic links\n\nCheck?'
                      ->msg.append;
                    ('Check?',msg[])
                      ->gui.promptForBooleanAll
                        (#
                           ok::  (#  do fg[]->check; true->value #);
                           cancel::  (#  do true->cancelCommand #);
                           okAll:: 
                             (# 
                             do fg[]->check; true->value; true->checkAll
                             #);
                           notOkAll::  (#  do true->doNotCheckAll #)
                        #)
                if)
            if)
        if)
     #)  

-- ymersiflibSearchCallSearch: DoPart --
do
     (# originalGE: ^edenv.groupEditor; originalFE: ^codeeditor
     do
        ymerBrowserInt.cge[]->originalGE[];
        ymerBrowserInt.cfe[]->originalFE[];
        (ymerBrowserInt.cge[],ymerBrowserInt.cfe[],none ,none ,none ,
         keepSearchCriteria)
          ->edenv.searchTextDialog
            (#
               found:: 
                 (# 
                 do
                    (if edenv.trace[12] then 'found'->putLine if);
                    foundNode[]->findandselect
                 #);
               notfound:: 
                 (# help: ^text
                 do
                    searchText[]->help[];
                    ' not found'->help.append;
                    (if originalGE[] <> none then
                        ' in '->help.append;
                        originalGE.fgTitle->help.append;
                        (if originalFE[] <> none then
                            '-'->help.append; originalFE.frag.name->help.append
                        if)
                    if);
                    (if edenv.trace[12] then help[]->putLine if);
                    help[]->writeInfoView
                 #)
            #)
     #)  

-- ymersiflibSearchCallReplace: DoPart --
do
     (# originalGE: ^edenv.groupEditor; originalFE: ^codeeditor
     do
        (ymerBrowserInt.cge[],ymerBrowserInt.cfe[],none ,none ,none ,
         keepSearchCriteria)
          ->edenv.replaceTextDialog
            (#
               found:: 
                 (# 
                 do
                    (if edenv.trace[12] then 'found'->putLine if);
                    foundNode[]->findandselect
                 #);
               notfound:: 
                 (# help: ^text
                 do
                    searchText[]->help[];
                    ' not found'->help.append;
                    (if originalGE[] <> none then
                        ' in '->help.append;
                        originalGE.fgTitle->help.append;
                        (if originalFE[] <> none then
                            '-'->help.append; originalFE.frag.name->help.append
                        if)
                    if);
                    (if edenv.trace[12] then help[]->putLine if);
                    help[]->writeInfoView
                 #)
            #)
     #)  

