Origin '~beta/editor/codeTextField.bet';

--beginning: doPart--
do
   (# endpos,x,y: @integer;
   do selection -> (pos,endpos);
      pos -> posToPt -> (x,y);
      pos-(x/6)+2 -> pos; 
      (pos,pos) -> selection;
   #);
   
   
--next: doPart--
do
   (# endpos,x,y: @integer;
   do selection -> (pos,endpos); 
      pos -> posToPt -> (x,y); 
      (x,y+13) -> ptToPos -> pos;
      (pos,pos) -> selection;
   #);
   
--previous: doPart--
do
   (# endpos,x,y: @integer;
   do selection -> (pos,endpos);
      pos->posToPt->(x,y);
      (x,y-13)->ptToPos->pos;
      (pos,pos) -> selection;
   #);
   
--end: doPart--
do
   (# endpos,x,y: @integer;
   do 
      (if last_line then length->pos
       else
          next_line; 
          beginning_of_line; 
          selection -> (pos,endpos);
          pos-1 -> pos;                           
      if);
      (pos,pos) -> selection;   
   #);
   
--current: doPart--
do
   (# begin,end,i,pos,endpos : @integer;
      ch: @char;
      tt: ^text;
   do 
      selection->(pos,endpos);
      beginning_of_line->begin->i;
      end_of_line->end;
      &text[]->t[];
      loop: cycle
        (# do 
           (i,i+1)->myScan->ch;
           (if (i>=end) OR (ch=ascii.nl) then leave loop if);
           ch->t.put; 
           i+1->i;
        #);
      (pos,endpos)->selection;
   #);
   
   
--last: doPart-- 
do
   (# ppos, pos, endpos : @integer;
      x,y,x_next,y_next : @integer;
   do
      
      selection ->(ppos, endpos);
      ppos -> posToPt ->(x,y);
      next_line;
      selection ->(pos,endpos);
      pos -> posToPt ->(x_next,y_next);
      (ppos,ppos) ->selection;
      (y=y_next)->result;
   #); 
   
--first: doPart--
do
   (# ppos, pos, endpos : @integer;
   do
      selection ->(pos, endpos);
      (pos,pos) ->selection;
      pos->ppos;
      previous_line;
      selection->(pos,endpos); 
      (if pos<>ppos then (ppos,ppos)->selection; if);
      (pos=ppos)->result;
   #); 
   
   
--indent_by_int: doPart--
do       
   (# 
      current_indent: @integer;
      t,t1: ^text;
   do 
      current_line->aux.eat_indent->(current_indent, t[]);
      (if (current_indent>indent) AND (indent>=0) then
          (beginning_of_line,beginning_of_line+current_indent-indent)->selection;
          delete;
      if);
      (if current_indent<indent then
          ''->t[];
          (for i:indent-current_indent repeat ' '->t.prepend; for); 
          t[]->insert;    
      if); 
   #); 
   
   
