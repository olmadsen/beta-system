Origin '~beta/editor/codeTextField.bet';

--beginning: doPart--
do
   (# endpos,row,col: @integer;
   do 
      selection->(pos,endpos);
      pos->posToRowCol->(row,col);
      (row,1)->rowColToPos->pos;
      (pos,pos)->selection;
   #);
   
   
--next: doPart--
do
   (# endpos,row,col: @integer;
      tmp: ^text;
   do 
      selection->(pos,endpos);
      pos->posToRowCol->(row,col);
      (if not(last_line) then
          (row+1,1)->rowColToPos->pos;
          (pos,pos)->selection;
          current_line->tmp[];
          (if tmp.length>=col then 
              pos+col-1->pos
           else
              pos+tmp.length->pos;
          if);
       else
          end_of_line->pos;
      if);
      (pos,pos)->selection;
   #);
   
--previous: doPart--
do
   (# endpos,row,col: @integer;
      tmp: ^text;
   do 
      selection->(pos,endpos);
      pos->posToRowCol->(row,col);
      (if not(row=1) then 
          (row-1,1)->rowColToPos->pos;
          (pos,pos)->selection;
          current_line->tmp[];
          (if tmp.length>=col then
              pos+col-1->pos;
           else
              pos+tmp.length->pos;
          if);
       else
          0->pos;
      if);
      (pos,pos)->selection;      
   #);
   
--end: doPart--
do
   (# endpos,row,col: @integer;
   do 
      selection->(pos,endpos);
      pos->posToRowCol->(row,col);
      (if not(last_line) then
          (row+1,0)->rowColToPos->pos;
          pos-1-pos;
       else
          length->pos;
      if);
      (pos,pos)->selection; 
   #);
   
--current: doPart--
do
   (# pos,endpos : @integer;
   do 
      selection->(pos,endpos);
      (beginning_of_line,end_of_line)
        ->get_selection->t[];
      (if regexp.nonCaseSensitive then
          t[]->regexp.lowercase->t[];
      if);
      (pos,endpos)->selection;
   #);
   
   
--get_selection: dopart--
do
   (# i: @integer;
      ch: @char;
   do
      begin->i;
      &text[]->t[];
      loop: cycle
        (# do 
           (i,i+1)->myScan->ch;
           (if (i>=end) OR (ch=ascii.nl) then leave loop if);
           ch->t.put; 
           i+1->i;
        #);
   #);
   
--last: doPart-- 
do
   (# pos, endpos,row,col,lastrow,lastcol : @integer;
   do            
      selection->(pos,endpos);
      pos->posToRowCol->(row,col);
      length->posToRowCol->(lastrow,lastcol);
      (lastrow=row)->result;
   #); 
   
--first: doPart--
do
   (# pos, endpos,row,col : @integer;
   do      
      selection->(pos,endpos);
      pos->posToRowCol->(row,col);
      (row=1)->result;
   #); 
   
   
--indent_by_int: doPart--
do 
   (# 
      current_indent: @integer;
      t,t1: ^text;
   do 
      current_line->regexp.eat_indent->(current_indent, t[]);
      (if (current_indent>indent) AND (indent>=0) then
          (beginning_of_line,beginning_of_line+current_indent-indent)->selection;
          delete;
      if);
      (if current_indent<indent then
          ''->t[];
          (for i:indent-current_indent repeat ' '->t.prepend; for); 
          t[]->insert;    
      if); 
   #); 
   
   
--depth_of_selection: doPart--
do
   (# t: ^text;
      i: @integer;
      ch: @char;
   do
      begin->i; 
      loop: cycle
        (# do 
           (i,i+1)->myScan->ch;
           (if ch=ascii.nl then depth+1->depth if);
           (if (i>=end) then leave loop if);
           i+1->i;
        #);
      (if begin>end then -1->depth if);
   #);
