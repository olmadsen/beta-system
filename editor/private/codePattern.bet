ORIGIN '~beta/editor/codeTextField';
-- beginning: DoPart --
do
     (# endpos,row,col,begin,end: @integer; 
     do (*'beginning_of_line'->putline;*)
        selection->(pos,endpos);
        pos->posToRowCol->(row,col);
        (row,1)->rowColToPos->pos;
        texteditingExtent->(begin,end);
        (if begin > pos then begin->pos if);
        (pos,pos)->selection
     #);
     

-- cheap_previous_line: DoPart --
do
     (# pos,endpos,row,col: @integer
     do
        (if not (first_line) then
            selection->(pos,endpos);
            pos->posToRowCol->(row,col);
            (row-1,1)->rowColToPos->pos;
            (pos,pos)->selection
        if)
     #)  

-- cheap_next_line: DoPart --
do
     (# pos,endpos,row,col: @integer
     do
        (if not (last_line) then
            selection->(pos,endpos);
            pos->posToRowCol->(row,col);
            (row+1,1)->rowColToPos->pos;
            (pos,pos)->selection;
            
        if)
     #)  

-- next: DoPart --
do
     (# endpos,row,col,begin,end: @integer; tmp: ^text; 
     do (*  'next_line'->putline; *)
        selection->(pos,endpos);
        pos->posToRowCol->(row,col);
        texteditingExtent->(begin,end);
        (if not (last_line) then
            (row+1,1)->rowColToPos->pos;
            (pos,pos)->selection;
            current_line->tmp[];
            (if last_line and (tmp.length+pos > end) then
                (end,end)->selection; 
             else
                (if tmp.length >= col then
                    pos+col-1->pos
                 else
                    pos+tmp.length->pos
                if);
                (pos,pos)->selection
            if)
         else
            (end,end)->selection; 
        if)
     #);
     

-- previous: DoPart --
do
     (# endpos,row,col,begin,end: @integer; tmp: ^text; 
     do (*'previous_line'->putline;*)
        selection->(pos,endpos);
        pos->posToRowCol->(row,col);
        texteditingExtent->(begin,end);
        (if not (first_line) then
            (row-1,1)->rowColToPos->pos;
            (pos,pos)->selection;
            current_line->tmp[];
            (if tmp.length >= col then
                pos+col-1->pos; 
             else
                pos+tmp.length->pos
            if);
            (if pos < begin then begin->pos if);
            
         else
            begin->pos; 
        if);
        (pos,pos)->selection
     #);
     

-- end: DoPart --
do
     (# endpos,row,col,begin,end: @integer; 
     do (*  'end_of_line'->putline; *)
        selection->(pos,endpos);
        pos->posToRowCol->(row,col);
        texteditingExtent->(begin,end);
        (if not (last_line) then
            (row+1,0)->rowColToPos->pos; pos-1-pos; 
         else
            end->pos; 
        if);
        (pos,pos)->selection
     #);
     

-- current: DoPart --
do
     (# pos,endpos: @integer; 
     do
        selection->(pos,endpos);
        (beginning_of_line,end_of_line)->selection;
        selection.contents->t[];
        (if regexp.nonCaseSensitive then t[]->regexp.lowercase->t[] if);
        (pos,endpos)->selection
     #);
   (* -- get_selection: DoPart --
    do
    (# i: @integer; ch: @char; 
    do
    begin->i;
    &text[]->t[];
    loop: cycle
    (# 
    do
    (i,i+1)->myScan->ch;
    (if (i >= end) or (ch = ascii.nl) then leave loop if);
    ch->t.put;
    i+1->i;
    
    #);
    
    #);
    
    *)
     

-- last: DoPart --
do
     (# pos,endpos,row,col,lastrow,lastcol,begin,end: @integer; 
     do
        selection->(pos,endpos);
        pos->posToRowCol->(row,col);
        texteditingExtent->(begin,end);
        end->posToRowCol->(lastrow,lastcol);
        (lastrow = row)->result;
        
     #);
     

-- first: DoPart --
do
     (# pos,endpos,row,col,begin,end,firstrow,firstcol: @integer; 
     do
        selection->(pos,endpos);
        pos->posToRowCol->(row,col);
        texteditingExtent->(begin,end);
        begin->posToRowCol->(firstrow,firstcol);
        (row = firstrow)->result;
        
     #);
     

-- indent_by_int: DoPart --
do
     (# current_indent: @integer; t,t1: ^text; 
     do
        current_line->regexp.eat_indent->(current_indent,t[]);
        (if (current_indent > indent) and (indent >= 0) then
            (beginning_of_line,beginning_of_line+current_indent-indent)
              ->selection;
            delete;
            
        if);
        (if (current_indent < indent) then
            beginning_of_line; indent-current_indent->get_whitespace->insert; 
        if);
        
     #);
     

-- depth_of_selection: DoPart --
do
     (# t: ^text; i: @integer; ch: @char; 
     do
        begin->i;
        (if begin > end then
            - 1->depth
         else
            1->depth;
            loop: cycle
              (# 
              do
                 (i,i+1)->myScan->ch;
                 (if (i >= end) then leave loop if);
                 (if ch = ascii.nl then depth+1->depth if);
                 i+1->i;
                 
              #);
            
        if);
        
     #);
     

