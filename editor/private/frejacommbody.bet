ORIGIN '../codeeditor';
INCLUDE 'editorenvbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-97
 *   All rights reserved.
 *)
-- frejaCommNewFragment: Descriptor --
(# 
do
   (if notifyFreja then
       (frag[],editorRoot[])->edenv.frejaInterface.newFragment
   if);
   
#)  

-- frejaCommAstReplaced: Descriptor --
(# (* oldAst,newAst: ^astInterface.ast;
    * enter (oldAst[],newAst[])
    *) command: ^text
do
   (if notifyFreja then
       (if oldAst[]->visibleInFreja then
           (oldAst[],newAst[])->edenv.frejaInterface.astReplaced
       if)
   if);
   
#)  

-- frejaCommAstReplacedSequence: Descriptor --
(# 
do
   (if notifyFreja then
       (if astReplacedList.size > 0 then
           astReplacedList.scan
             (# 
             do
                (if current.oldAst[]->visibleInFreja then
                    (current.oldAst[],current.newAst[])
                      ->edenv.frejaInterface.astReplaced
                if)
             #)
        else
           'no events in astReplacedList'->screen.putLine
       if)
   if)
#)  

-- frejaCommListElementInserted: Descriptor --
(# 
do
   (if notifyFreja then
       (if position->listNode.get->visibleInFreja then
           (listNode[],position)->edenv.frejaInterface.listElementInserted
       if)
   if)
#)  

-- frejaCommListElementsDeleted: Descriptor --
(#
(* oldElements: ^partList;
 * listNode: ^astInterface.expanded;
 * position,length: @integer
 * enter (listNode[],position,length,oldElements[])
 *) command: ^text
do
   (if notifyFreja then
       (if oldElements.elm[length][]->visibleInFreja then
           (listNode[],position,length,oldElements.elm)
             ->edenv.frejaInterface.listElementsDeleted
       if)
   if);
   
#)  

-- frejaCommListElementsReplaced: Descriptor --
(# 
do
   (if notifyFreja then
       (if (length = 0) and (newLength = 1) then
           'converting listElementsReplacedEvent to listElemenInsertedEvent'
             ->screen.putline;
           (listNode[],position)->edenv.frejaInterface.listElementInserted
        else
           (if oldElements.elm[length][]->visibleInFreja then
               (listNode[],position,length,oldElements.elm,newLength)
                 ->edenv.frejaInterface.listElementsReplaced
           if)
       if)
   if)
#)  

-- frejaCommFocusChanged: Descriptor --
(# 
do
   (if notifyFreja then
       (if edenv.trace[1] then
           '*********************************************************************'
             ->screen.putLine;
           'Show Freja selection: '->putText;
           newCS.node.index->putInt;
           newline;
           
       if);
       (newCS.node[],newCS.length)->edenv.frejaInterface.focusChanged
   if)
#)  

-- frejacommPropertiesChanged: Descriptor --
(# 
do (if isAfrejaEditor then fg[]->frejaInterface.propertiesChanged if); 
#)  

-- frejacommFragmentChanged: Descriptor --
(# 
do (if isAfrejaEditor then ff[]->frejaInterface.fragmentChanged if); 
#)  

-- frejacommFragmentInserted: Descriptor --
(# 
do (if isAfrejaEditor then ff[]->frejaInterface.fragmentInserted if); 
#)  

-- frejacommFragmentDeleted: Descriptor --
(# 
do (if isAfrejaEditor then ff[]->frejaInterface.fragmentDeleted if); 
#)  

-- visibleInFreja: Descriptor --
(# synCatNo: @integer; 
do
   true->value;
   loop:
     (# 
     do
        node[]->getSyncatNo->syncatNo;
        (if synCatNo
         // betaGram.imperatives // betaGram.enterPart // betaGram.enterPartOpt
         // betaGram.exitPart // betaGram.exitPartOpt // betaGram.doPart
         // betaGram.doPartOpt then
            false->value; 
         else
            (if node.father = none then
            (* imperatives, enterpart or exitpart was not found *)
                
             else
                node.father->node[]; restart loop
            if)
        if)
     #)
#)  

