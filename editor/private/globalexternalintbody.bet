ORIGIN '../ymersiflib';
INCLUDE 'editorenvbody';
LIB_ITEM 'editorymersiflib';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-2000
 *   All rights reserved.
 *)
-- externalInterfaceOpenForm: DoPart --
do
     (# ff: ^edenv.mps.fragmentForm; anAST: ^edenv.mps.ast; help: ^text
     do
        (if edenv.trace[11] then
            'Sif ******** Global external interface: openForm'->screen.putline;
            'Sif ******** <<name: '->screen.putText;
            name[]->screen.putText;
            ' index: '->screen.putText;
            (if node[] <> none then
                node.index->screen.putInt
             else
                0->screen.putint
            if);
            '>>'->screen.putLine;
            
        if);
        (if node[] <> none then
            (name[],node.index)->edenv.openForm->(ff[],anAST[])
         else
            (name[],0)->edenv.openForm->(ff[],anAST[])
        if);
        (if ff[] <> none then
            true->edenv.openingFrejaEditor;
            ((ff.father).fullName,ff.name)->ymerbrowserInt.selectFragmentForm;
            (ff[],anAST[])->edenv.findEditorForForm->fe[];
            false->edenv.openingFrejaEditor;
            (if fe[] <> none then
                true->fe.isAFrejaEditor->fe.thisGroupEditor.isAfrejaEditor; 
            if)
        if)
     #)  

-- externalInterfaceGetEditorForForm: DoPart --
do
     (# anAST: ^edenv.mps.ast; help: ^text
     do
        (if edenv.trace[11] then
            'Sif ******** Global external interface: getEditorForForm'
              ->screen.putline;
            'Sif ******** <<ff: '->screen.putText;
            ff.fullname->screen.putText;
            ' index: '->screen.putText;
            (if node[] <> none then
                node.index->screen.putInt
             else
                0->screen.putint
            if);
            '>>'->screen.putLine;
            
        if);
        true->edenv.openingFrejaEditor;
        (if node[] <> none then
            (ff[],node[])->edenv.findEditorForForm->fe[]
         else
            (ff[],ff.root[])->edenv.findEditorForForm->fe[]
        if);
        (if fe[] = none then
            ((ff.father).fullName,ff.name)->ymerBrowserInt.selectFragmentForm;
            (ff[],anAST[])->edenv.findEditorForForm->fe[]
        if);
        false->edenv.openingFrejaEditor;
        (if fe[] <> none then
            true->fe.isAFrejaEditor->fe.thisGroupEditor.isAfrejaEditor; 
        if)
     #)  

-- externalInterfaceNewBETAProgram: DoPart --
do
     (#
        fg: ^edenv.mps.fragmentGroup;
        ff: ^edenv.mps.fragmentForm;
        fe: ^ymerbrowser.codeeditor;
        ge: ^edenv.groupeditor;
        id: @integer;
        help: ^text
     do
        (if edenv.trace[11] then
            'Sif ******** Global external interface: newBETAProgram'
              ->screen.putline;
            'Sif ******** <<name: '->screen.putText;
            name[]->screen.putText;
            '>>'->screen.putLine;
            
        if);
        true->value;
        (ymerCallback.theBrowser[],name[],true)->edenv.newBETAProgram->fg[];
        (if fg[] <> none then
            fg.fullName->ymerbrowserInt.addproject;
            (if ymerbrowserInt.cge[] <> none then
                true->ymerBrowserInt.cge.isAfrejaEditor;
                ymerBrowserInt.cge.fg.fragmentlist.scan
                  (# 
                  do
                     (if current.type = mps.ast.formType then
                         current.f[]->ff[]
                     if)
                  #);
                (if ff[] = none then
                    'ff is none!'->putLine
                 else
                (* (ge.fg.fullName,ff.name)->ymerBrowserInt.selectFragmentForm; *)
                    ff[]->ymerBrowserInt.cge.formEditorList.findEditor->fe[];
                    (if fe[] <> none then
                        true->fe.isAfrejaEditor;
                        (fe.frag[],fe.editorRoot[])
                          ->edenv.frejaExternalInterface.newFragment;
                        (fe.editorRoot[],- 1)->fe.astViEw.pp.present;
                        fe.id->id
                     else
                        false->value
                    if)
                if)
             else
                'ExternalInterface: No group in browser?!'->putLine
            if)
        if);
        
     #)  

-- externalInterfaceNewBETALibrary: DoPart --
do
     (#
        fg: ^edenv.mps.fragmentGroup;
        ff: ^edenv.mps.fragmentForm;
        fe: ^ymerbrowser.codeeditor;
        ge: ^edenv.groupeditor;
        id: @integer;
        help: ^text
     do
        (if edenv.trace[11] then
            'Sif ******** Global external interface: newBETAProgram'
              ->screen.putline;
            'Sif ******** <<name: '->screen.putText;
            name[]->screen.putText;
            '>>'->screen.putLine;
            
        if);
        true->value;
        (ymerCallback.theBrowser[],name[],true)->edenv.newBETALibrary->fg[];
        (if fg[] <> none then
            fg.fullName->ymerbrowserInt.addproject;
            (if ymerBrowserInt.cge[] <> none then
                true->ymerBrowserInt.cge.isAfrejaEditor;
                ymerBrowserInt.cge.fg.fragmentlist.scan
                  (# 
                  do
                     (if current.type = mps.ast.formType then
                         current.f[]->ff[]
                     if)
                  #);
                (if ff[] = none then
                    'ff is none!'->putLine
                 else
                (* (ge.fg.fullName,ff.name)->browser.selectFragmentForm; *)
                    ff[]->ymerBrowserInt.cge.formEditorList.findEditor->fe[];
                    (if fe[] <> none then
                        true->fe.isAfrejaEditor;
                        (fe.frag[],fe.editorRoot[])
                          ->edenv.frejaExternalInterface.newFragment;
                        (fe.editorRoot[],- 1)->fe.astViEw.pp.present;
                        fe.id->id
                     else
                        false->value
                    if)
                if)
             else
                'ExternalInterface: No group in browser?!'->putLine
            if)
        if);
        
     #)  

-- externalInterfaceSaveAndQuit: DoPart --
do
   edenv.groupeditorList.scan
     (# textfile: ^text
     do
        (if (current[] <> none ) then
            (if current.isAfrejaEditor then
                (true,false,true)->current.save
            if)
        if)
     #)  

-- externalInterfaceSave: DoPart --
do
   edenv.groupeditorList.scan
     (# textfile: ^text
     do
        (if (current[] <> none ) then
            (if current.isAfrejaEditor then
                (true,false,true)->current.save
            if)
        if)
     #)  

-- externalInterfaceAutoSave: DoPart --
do
   edenv.groupeditorList.scan
     (# textfile: ^text
     do
        (if current.isAfrejaEditor then
            (if current.touched > 0 then
                ('#',current.fg[])->edenv.editorenvprivate.extendedSaveBackup; 
            if)
        if)
     #)  

-- externalInterfaceCheckAutoSaveFile: DoPart --
do fg[]->edenv.editorenvPrivate.checkAutoSaveFile->didRecover  

-- externalInterfaceCheck: DoPart --
do fg[]->THIS(sifBrowserWindowExt).check  

-- externalInterfaceToggleDebug: DoPart --
do not edenv.trace[1]->edenv.trace[1]  

-- externalInterfaceAddProp: DoPart --
do
   edenv.groupeditorList.scan
     (# 
     do
        (if (current.fg[] = fg[]) then
            (propName[],propString[])
              ->current.addProp
                (# propExists::  (#  do deleteIfExists->delete #) #)
        if)
     #)  

-- externalInterfaceDeleteProp: DoPart --
do
   true->value;
   edenv.groupeditorList.scan
     (# 
     do
        (if (current.fg[] = fg[]) then
            (propName[],propString[])
              ->current.deleteProp
                (# propNotFound::  (#  do false->value #) #)
        if)
     #)  

-- externalInterfaceSelectEditor: DoPart --
do
   (if formEditorExists then
       (if fe.frag[] <> ymerbrowserInt.cfe.frag[] then
           ((fe.frag.father).fullName,fe.frag.name)
             ->ymerbrowserInt.selectFragmentForm
       if)
    else
       ((fe.frag.father).fullName,fe.frag.name)
         ->ymerbrowserInt.selectFragmentForm
   if)  

-- externalInterfaceShowDiagram: DoPart --
do
     (#
        isVisibleInFreja: @boolean;
        findVisibleFrejaNode:
        (* returns (value, theNode[])
         * if node[] is already visible in freja (true, none is returned) 
         * if node[] is not visible in freja (false, none) is returned.
         * otherwise it will search in the whole fragmentform for a node: theNode 
         * that is visible in freja
         * if found (true,theNode[]) is returned
         *) booleanValue
          (#
             node,theNode,fatherNode: ^astInterface.ast;
             anExp: ^astInterface.expanded
          enter node[]
          do
             thisOp:
               (# 
               do
                  node[]->theNode[];
                  (if (theNode[] <> none ) and
                  (theNode.frag.root.kind = mps.ast.kinds.interior) then
                      (if theNode.frag.root.symbol = mps.beta.DoPart then
                      (* is not visible in freja *)
                          false->value; none ->theNode[]; leave thisOp
                      if)
                  if);
                  searchPathToRoot:
                  (if theNode[] <> none then
                      (if theNode.kind = mps.ast.kinds.interior then
                          (if theNode.symbol
                           // mps.beta.SimpleDecl // mps.beta.PatternDecl
                           // mps.beta.VirtualDecl // mps.beta.BindingDecl
                           // mps.beta.FinalDecl
                           //
                           mps.beta.RepetitionDecl
                           (*// mps. beta.VariablePatternDecl*) then
                           (* is already visible in freja *)
                              true->value; none ->theNode[]; leave thisOp
                           else
                              theNode.father->theNode[];
                              restart searchPathToRoot
                          if)
                       else
                          theNode.father->theNode[]; restart searchPathToRoot
                      if)
                  if);
                  (if node[] <> none then
                      node.frag.root[]->anExp[];
                      anExp.suffixwalk
                        (# 
                        do
                           current[]->theNode[];
                           (if theNode.kind = mps.ast.kinds.interior then
                               (if theNode.symbol
                                // mps.beta.SimpleDecl // mps.beta.PatternDecl
                                // mps.beta.VirtualDecl // mps.beta.BindingDecl
                                // mps.beta.FinalDecl
                                //
                                mps.beta.RepetitionDecl
                                (*// mps. beta.VariablePatternDecl*) then
                                (* found *)
                                   true->value; leave thisOp
                               if)
                           if)
                        #);
                      none ->theNode[]
                  if)
               #)
          exit theNode[]
          #)
     do
        (if ymerbrowserInt.cge[] <> none then (* dsa hack *)
            (if (ymerbrowserInt.cfe[] <> none ) then
                (if ymerbrowserInt.cfe.cs[] <> none then
                    doCommand
                      (# 
                      do
                         writeInfoViewClear;
                         ymerbrowserInt.cfe.cs.node[]->findVisibleFrejaNode
                           ->(isVisibleInFreja,node[]);
                         (if isVisibleInFreja then
                             (if node[] <> none then
                                 (node[],1,0,0)->ymerbrowserInt.cfe.setFocus
                             if);
                             (ymerbrowserInt.cfe.frag[],
                              ymerbrowserInt.cfe.cs.node[])
                               ->edenv.frejaExternalInterface.openForm
                          else
                             'Current selection has no UML representation'
                               ->writeInfoView;
                             edenv.frejaExternalInterface.newDiagram
                         if)
                      #)
                 else
                    'No current selection'->writeInfoView
                if)
             else
                edenv.frejaExternalInterface.newDiagram
            if)
         else
            edenv.frejaExternalInterface.newDiagram
        if)
     #)  

