ORIGIN '../ymersiflib';
INCLUDE 'editorenvbody';
-- externprivate: Descriptor --
(#  #)  

-- onGlobalExternCommandBody: Descriptor --
(#
   op: ^text;
   fg: ^editorEnv.mps.fragmentGroup;
   ff: ^editorEnv.mps.fragmentForm;
   normalFeedback: @boolean;
   OK: @integer;
   intTrue,intFalse: @integer;
   f: @file
do
   1->intTrue;
   0->intFalse;
   (if edenv.trace[1] then 'Sif ******** <<Global op: '->screen.putText;  if);
   command.reset;
   command.getAtom->op[];
   (if edenv.trace[1] then op[]->screen.putText; '>>'->screen.putLine;  if);
   intTrue->OK;
   true->normalFeedback;
   op[]->status[];
   (if true
    // ('off'->op.equal) then
       'off'->putLine
    // ('on'->op.equal) then
       'on'->putLine
    // ('openGroup'->op.equal) then
         (# name: ^text; fg: ^editorEnv.mps.fragmentGroup
         do
            command.getAtom->name[];
            (if edenv.trace[1] then
                'Sif ******** <<name: '->screen.putText;
                name[]->screen.putText;
                '>>'->screen.putLine;
                
            if);
            '(name[],true)->openGroup->fg[]'->putLine;
            (if fg[] <> none then 1->OK else 0->OK if);
            
         #)
    // ('openForm'->op.equal) then
         (#
            name: ^text;
            index: @integer;
            ff: ^editorEnv.mps.fragmentForm;
            node: ^editorEnv.mps.ast;
            oldNotifyFreja: @boolean;
            fe: ^codeeditor;
            id: @integer
         do
            command.getAtom->name[];
            command.getInt->index;
            (if edenv.trace[1] then
                'Sif ******** <<name: '->screen.putText;
                name[]->screen.putText;
                ' index: '->screen.putText;
                index->screen.putInt;
                '>>'->screen.putLine;
                
            if);
            (name[],index)->edenv.openForm->(ff[],node[]);
            (if ff[] <> none then
                (ff[],node[])->edenv.findEditorForForm->fe[];
                (if fe[] = none then
                    'not found use selectFragmentForm'->putLine;
                    ((ff.father).fullName,ff.name)->browser.selectFragmentForm;
                    (ff[],node[])->edenv.findEditorForForm->fe[];
                    
                if);
                (if fe[] = none then
                    'Editor not found!'->putLine
                 else
                    true->fe.isAFrejaEditor->fe.thisGroupEditor.isAfrejaEditor;
                    (* no not anymore ('~',fe.frag.father)
                     ->edenv.editorenvprivate.extendedSaveBackup;
                     'makeFrejaTilde'->externCommand;*)
                    fe.id->id
                if);
                (if id
                 // 0 then intFalse->OK; 'id is 0'->putLine; 
                 else
                    false->normalFeedback;
                    ' '->status.put;
                    intTrue->status.putInt;
                    ' '->status.put;
                    id->status.putInt;
                    ' '->status.put;
                    name[]->status.putText;
                    
                if)
             else
                name[]->putText; ' could not be opened'->putLine
            if);
            
         #)
    // ('newBETAProgram'->op.equal) then
         (#
            name: ^text;
            fg: ^editorEnv.mps.fragmentGroup;
            ff: ^editorEnv.mps.fragmentForm;
            fe: ^codeeditor;
            ge: ^editorEnv.groupeditor;
            id: @integer
         #)
    // ('newBETALib'->op.equal) then
         (#
            name: ^text;
            fg: ^editorEnv.mps.fragmentGroup;
            ff: ^editorEnv.mps.fragmentForm;
            fe: ^codeeditor;
            ge: ^editorEnv.groupeditor;
            id: @integer
         #)
    // ('quit'->op.equal) then
       false->normalFeedback;
       ''->status;
       true->edenv.doQuit;
       edenv.groupEditorList.scan
         (# textfile: ^text
         do
            (if current.isAfrejaEditor then
                current.resetGroupTouch;
                false->current.needToUpdateTextFile;
                current.fg[]->edenv.editorenvPrivate.deleteAutoSaveFile;
                current.closeGroup;
                
             else
                'Is not a freja editor'->putLine; 
            if)
         #);
       
    // ('closeComm'->op.equal) then
       'closeComm'->putLine
    // ('save'->op.equal) // ('saveAndQuit')->op.equal then
       edenv.groupeditorList.scan
         (# textfile: ^text
         do
            (if (current[] <> none ) then
                (if current.isAfrejaEditor then
                    (true,false)
                      ->
                        current.save
                        (* no not anymore
                         ('~',current.fg[])
                         ->edenv.editorenvprivate.extendedSaveBackup;
                         current.fg[]
                         ->edenv.editorenvprivate.makeNewTextfile;
                         current.fg[]
                         ->
                         edenv.editorenvprivate.deleteAutoSaveFile
                         ;
                         'rm '->screen.putText;
                         current.fg.diskFileName->screen.putText;
                         '# '->screen.putLine
                         *)
                 else
                (*'Is not a freja editor'->putLine*)
                    
                if)
            if)
         #)
    // ('autoSave'->op.equal) then
       edenv.groupeditorList.scan
         (# textfile: ^text
         do
            (if current.isAfrejaEditor then
                (if current.touched > 0 then
                    ('#',current.fg[])
                      ->edenv.editorenvprivate.extendedSaveBackup;
                    
                if)
             else
                'Is not a freja editor'->putLine; 
            if)
         #)
    // 'toggleDebug'->op.equal then
       not edenv.trace[1]->edenv.trace[1]
    else
       'Unknown global command: '->screen.putText;
       op[]->screen.putLine;
       intFalse->OK;
       
   if);
   (if normalFeedback then ' '->status.put; OK->status.putInt;  if);
   
#)  

-- globalExternCommandBody: Descriptor --
(# 
do
   '#0'->command.prepend;
   (if edenv.trace[1] then
       'Sif ******** <<sending: '->screen.putText;
       command[]->screen.putText;
       '>>'->screen.putLine;
       
   if);
   command[]->externalInterface.putLine;
   
#)  

-- commFrejaOnInput: DoPart --
do (*true->edenv.trace[1];*) t->handleFrejaCommand  

-- commFrejaPutline: DoPart --
do
   'commFreja.putLine: '->screen.putLine;
   t[]->screen.putLine;
   t[]->edenv.frejaInterface.onInput  

-- commFrejaHandleFrejaCommand: Descriptor --
(# id: @integer; fe: ^codeeditor; help,command,status: ^text; ch: @char
do
   (if edenv.trace[1] then
       'Sif ******** handleFrejaCommand '->screen.putText;
       t[]->screen.putLine;
       'Sif ******** <<command: '->screen.putText;
       t[]->screen.putText;
       '>>'->screen.putLine;
       
   if);
   t.reset;
   (if (1->t.inxGet)
    // '#' then
       t.get->ch;
       t.getInt->id;
       (if edenv.trace[1] then
           'ch: '->putText;
           ch->put;
           newLine;
           'id: '->putText;
           id->putInt;
           newLine;
           
       if);
       (if id
        // 0 then
           (t.pos+1,t.length)->t.sub->command[];
           (if edenv.trace[1] then
               'command: '->putText; command[]->putLine; 
           if);
           command[]->onExternCommand->status[];
           (if not status.empty then
               '!0'->help[];
               help[]->status.prepend;
               (if edenv.trace[1] then
                   'Sif ********  status: '->screen.putText;
                   status[]->screen.putLine;
                   
               if);
               status[]->externalInterface.putLine;
               
           if)
        else
           id->edenv.findFormEditorId->fe[];
           (if fe[]
            // none then
               'editor #'->screen.putText;
               id->screen.putInt;
               ' not found'->screen.putLine;
               ('!',1)->t.inxPut;
               t[]->externalInterface.putLine;
               
            else
               (if formEditorExists then
                   (if fe.frag[] <> cfe.frag[] then
                       ((fe.frag.father).fullName,fe.frag.name)
                         ->browser.selectFragmentForm
                   if)
                else
                   ((fe.frag.father).fullName,fe.frag.name)
                     ->browser.selectFragmentForm
               if);
               (t.pos+1 (* because of getInt *) ,t.length)->t.sub->command[];
               command[]->fe.onExternCommand->status[];
               (if not status.empty then
                   '!'->help[];
                   id->help.putInt;
                   help[]->status.prepend;
                   (if edenv.trace[1] then
                       'Sif ********  status: '->screen.putText;
                       status[]->screen.putLine;
                       
                   if);
                   status[]->externalInterface.putLine;
                   
               if);
               
           if);
           
       if);
       
    else
       (if (1->t.inxGet)
        // '!' then
           (if edenv.trace[1] then
               'Sif ******** <<Status from Freja (callback): '->screen.putText;
               t[]->screen.putText;
               '>>'->screen.putLine;
               
           if);
           t.get->ch;
           t.getInt->id;
           (if id
            // 0 then 
            else
               id->edenv.findFormEditorId->fe[];
               (if fe[]
                // none then
                   'editor #'->screen.putText;
                   id->screen.putInt;
                   ' not found'->screen.putLine;
                   ('!',1)->t.inxPut;
                   t[]->externalInterface.putLine;
                   
                else
                   (if formEditorExists then
                       (if fe.frag[] <> cfe.frag[] then
                           ((fe.frag.father).fullName,fe.frag.name)
                             ->browser.selectFragmentForm
                       if)
                    else
                       ((fe.frag.father).fullName,fe.frag.name)
                         ->browser.selectFragmentForm
                   if);
                   2->t.setpos;
                   t.Getatom->command[];
                   (if edenv.trace[1] then
                       '<'->screen.put;
                       command[]->screen.putText;
                       '>'->screen.put;
                       screen.newLine;
                       
                   if);
                   (if 'openForm'->command.equal then
                       true->fe.notifyFreja; 
                   if);
                   
               if);
               
           if)
        else
           'Not a command: '->screen.putText;
           t[]->screen.putLine;
           '!0Not a command: '->t.copyPrepend->externalInterface.putLine;
           
       if);
       
   if)
#)  

-- commFrejaHandleSavedGroups: Descriptor --
(# ge: ^editorEnv.groupEditor; command: ^text; doSend: @boolean
do
   loop: savedGroups.scan
     (# 
     do
        (ymerCallBack.theBrowser[],current[])
          ->edenv.groupEditorList.findGroupEditor->ge[];
        (if ge[] <> none then
            (if ge.isAfrejaEditor then
                ge.formEditorList.scan
                  (# 
                  do
                     (if current.notifyFreja then true->doSend; leave loop if)
                  #)
            if)
        if)
     #);
   savedGroups.clear;
   (if doSend then false->edenv.frejaInterface.groupChecked if)
#)  

