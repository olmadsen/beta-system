ORIGIN 'compiler';
INCLUDE '~beta/unixlib/v1.5/unixfile';
-- MDinitCompiler: Descriptor --
(#  do  #)  

-- machineDependentSwitches: Descriptor --
(#
do
#)

-- compilerJobFileDelete: DoPart --
do
     (#
        f: @file;
        jobFileName,path,name: ^text;
        splitPath:
          (# e: @diskEntry; 
          enter e.path
          exit (e.path.head,e.path.name)
          #);
        codedir: @diskEntry
     do
        fgname[]->splitPath->(path[],name[]);
        path[]->jobFileName[];
        '/'->jobFileName.append;
        machineType[]->jobFileName.append;
        jobfileName[]->codedir.path;
        '/'->jobFileName.append;
        name[]->jobFileName.append;
        '..job'->jobFileName.append->f.name;
        (if f.entry.exists then
            (if f.entry.writeable and codedir.writeable then
                'Deleting '->putText; f.name->putLine; f.delete
             else
                f.name->putText; ' could not be deleted!'->putLine
            if)
        if)
     #)  

-- compilerJobFilePermission: DoPart --
do
     (#
        f: @file;
        jobFileName,path,name: ^text;
        ue: @unixentry
          (#
             permissionDesc:: 
               (#
                  changePermError:: 
                    (# 
                    do
                       'No permission to change permisions'->putLine;
                       true->continue
                    #)
               #)
          #);
        splitPath:
          (# e: @diskEntry;  enter e.path exit (e.path.head,e.path.name) #)
     do
        fgname[]->splitPath->(path[],name[]);
        path[]->jobFileName[];
        '/'->jobFileName.append;
        machineType[]->jobFileName.append;
        '/'->jobFileName.append;
        name[]->jobFileName.append;
        '..job'->jobFileName.append->f.name;
        (if f.entry.exists then
            f.name->ue.path; ('a','w')->ue.permission.add
        if)
     #)  

