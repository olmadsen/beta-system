ORIGIN '../moreprompts';
INCLUDE '~beta/guienv/controls'
        '~beta/guienv/fields'
        '~beta/guienv/utils/buttonadds';
-- morePromptsPrivate: Descriptor --
(#
   yesNoAllPrompt: @window
     (#
        open:: 
          (# 
          do
             (405,115)->size;
             ;
             yesButton.open;
             noButton.open;
             cancelButton.open;
             yesToAllButton.open;
             noToAllButton.open;
             messageTxt.open;
             showModal
          #);
        eventHandler::< 
          (#
             onKeyDown::< 
               (# 
               do
                  (if ch
                   // ascii.cr // ascii.nl then
                      yesButton.theEventHandler.onMouseUp
                   // ascii.esc then
                      cancelButton.theEventHandler.onMouseUp
                  if)
               #)
          #);
        popDown: (#  do hide #);
        messageTxt: @staticText
          (#
             eventHandler::< 
               (#
                  onLabelChanged::< 
                    (# 
                    do (labelWidth,labelHeight*linesinMessage)->size; 
                    #)
               #);
             open::< 
               (# 
               do
                  (380,61)->size;
                  (10,15)->position;
                  message[]->writeText;
                  INNER
               #);
             linesInMessage: @integer;
             writeText:
               (# msg: ^text
               enter msg[]
               do
                  1->linesInMessage;
                  msg.scanAll
                    (# 
                    do
                       (if ch = ascii.newline then
                           linesInMessage+1->linesInMessage; 
                       if)
                    #);
                  msg[]->label
               #)
          #);
        yesButton: @pushbutton
          (#
             open::< 
               (# 
               do okLabel->label; (63,20)->size; (10,80)->position; INNER
               #);
             eventHandler::<  (# onMouseUp::<  (#  do popdown; ok #) #)
          #);
        noButton: @pushbutton
          (#
             open::< 
               (# 
               do notOKlabel->label; (63,20)->size; (90,80)->position; INNER
               #);
             eventHandler::< 
               (# onMouseUp::<  (#  do popdown; notOK #) #)
          #);
        cancelButton: @pushbutton
          (#
             open::< 
               (# 
               do
                  cancelLabel->label; (63,20)->size; (170,80)->position; INNER
               #);
             eventHandler::< 
               (# onMouseUp::<  (#  do popdown; cancel #) #)
          #);
        yesToAllButton: @pushbutton
          (#
             open::< 
               (# 
               do okAllLabel->label; (63,20)->size; (250,80)->position; INNER
               #);
             eventHandler::< 
               (# onMouseUp::<  (#  do popdown; okAll #) #)
          #);
        noToAllButton: @pushbutton
          (#
             open::< 
               (# 
               do
                  notOkAllLabel->label;
                  (63,20)->size;
                  (330,80)->position;
                  INNER
               #);
             eventHandler::< 
               (# onMouseUp::<  (#  do popdown; notOkAll #) #)
          #)
     #)
#)  

-- morePromptsOpen: DoPart --
do
   (if message[] = none then ''->message[] if);
   myPrivate.yesNoAllPrompt.open;
   title[]->myprivate.yesNoAllPrompt.title;
   INNER ;
     

