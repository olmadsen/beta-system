ORIGIN '../searchreplace';
INCLUDE '~beta/guienv/v1.6/fields';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-97
 *   All rights reserved.
 *)
-- SRsearchDialogPrivate: Descriptor --
(#
   fatherpos,fathersize,mysize: @point;
   rescale:
     (# newsize: @point; hpct,vpct: @real; h,v: @integer
     do
        size->newsize;
        newsize.h / mysize.h->hpct;
        newsize.v / mysize.v->vpct;
        newsize->mysize;
        cancelButton.position->(h,v);
        (h*hpct,v*vpct)->cancelButton.position;
        cancelButton.size->(h,v);
        (h*hpct,v)->cancelButton.size;
        nextButton.position->(h,v);
        (h*hpct,v*vpct)->nextButton.position;
        nextButton.size->(h,v);
        (h*hpct,v)->nextButton.size;
        previousButton.position->(h,v);
        (h*hpct,v*vpct)->previousButton.position;
        previousButton.size->(h,v);
        (h*hpct,v)->previousButton.size;
        browseHitsButton.position->(h,v);
        (h*hpct,v*vpct)->browseHitsButton.position;
        browseHitsButton.size->(h,v);
        (h*hpct,v)->browseHitsButton.size;
        theMessage.position->(h,v);
        (h*hpct,v*vpct)->theMessage.position;
        theMessage.size->(h,v);
        (h*hpct,v)->theMessage.size;
        theText.position->(h,v);
        (h*hpct,v*vpct)->theText.position;
        theText.size->(h,v);
        (h*hpct,v)->theText.size;
        caseSensitiveButton.position->(h,v);
        (h*hpct,v*vpct)->caseSensitiveButton.position;
        caseSensitiveButton.size->(h,v);
        (h*hpct,v)->caseSensitiveButton.size;
        wholeWordButton.position->(h,v);
        (h*hpct,v*vpct)->wholeWordButton.position;
        wholeWordButton.size->(h,v);
        (h*hpct,v)->wholeWordButton.size;
        searchAreaButton.position->(h,v);
        (h*hpct,v*vpct)->searchAreaButton.position;
        searchAreaButton.size->(h,v);
        (h*hpct,v)->searchAreaButton.size;
        
     #);
   dialogopened: @boolean;
   DialogText: textField
     (#
        eventhandler:: 
          (#
             onBeforeChange:: 
               (# txt: ^text
               do
                  (if length = 1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl // ascii.cr then
                          THIS(searchDialog).private.nextButton.theEventhandler.
                            onMouseup;
                          false->allow;
                          
                      if);
                      
                  if)
               #);
             onKeyDown:: 
               (# 
               do
                  (if ch = ascii.esc then
                      THIS(searchDialog).private.cancelButton.theEventhandler.
                        onMouseup;
                      
                  if)
               #);
             
          #);
        
     #);
   theMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,10),(20+320,10+30))->frame #);
        
     #);
   theText: @DialogText
     (# open::  (#  do ((20,40),(20+320,40+30))->frame #);  #);
   nextButton: @pushButton
     (#
        eventHandler:: 
          (# onMouseUp::  (#  do theText.contents->usertext[]; next #)
          #);
        open::  (#  do ((20,80),(20+100,80+30))->frame; 'Next'->label;  #);
        
     #);
   previousButton: @pushButton
     (#
        eventHandler:: 
          (# onMouseUp::  (#  do theText.contents->usertext[]; previous #)
          #);
        open:: 
          (# 
          do ((130,80),(130+110,80+30))->frame; 'Previous'->label; disable; 
          #);
        
     #);
   browseHitsButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp::  (#  do theText.contents->usertext[]; browseHits #)
          #);
        open:: 
          (# 
          do ((130,80),(130+110,80+30))->frame; 'Browse hits'->label; 
          #);
        
     #);
   cancelButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do theText.contents->usertext[]; cancel; THIS(searchDialog).hide
               #)
          #);
        open:: 
          (#  do ((250,80),(240+100,80+30))->frame; 'Close'->label;  #);
        
     #);
   caseSensitiveButton: @radioButton
     (#
        open:: 
          (# 
          do ((20,120),(20+100,120+30))->frame; 'Match case'->label; 
          #);
        
     #);
   wholeWordButton: @radioButton
     (#
        open:: 
          (# 
          do ((130,120),(130+110,120+30))->frame; 'Match word'->label; 
          #);
        
     #);
   searchAreaButton: (*$ 225*) @optionButton
     (#
        areaNo: @integer;
        areamenu: @menu
          (#
             itemtype: menuitem
               (#
                  no: @integer;
                  eventHandler:: 
                    (# onSelect::  (#  do no->areaNo;  #) #)
               #);
             addarea:
               (# t: ^text; item: ^itemType; no: @integer; 
               enter (t[],no)
               do
                  &itemType[]->item[];
                  item.open;
                  t[]->item.name;
                  no->item.no;
                  item[]->append;
                  
               #);
             open:: 
               (# 
               do
                  ('Form',1)->addarea;
                  ('Group',2)->addarea;
                  ('Project',3)->addarea;
                  
               #);
             
          #);
        open:: 
          (# 
          do
             ((20,150),(20+200,150+30))->frame;
             areamenu.open;
             areamenu[]->popUpMenu;
             2->areaNo->currentItem;
             'Search in:'->label;
             
          #)
     #);
   
#)  

-- SRsearchDialogPopup: DoPart --
do
   (if not private.dialogopened then open;  if);
   titleText[]->title;
   msgText[]->private.theMessage.label;
   private.theText.all->private.theText.selection;
   private.theText.delete;
   (if defaultText[] = none then ''->defaultText[] if);
   defaultText[]->private.theText.insert;
   private.theText.all->private.theText.selection;
   INNER ;
   (if father[] <> none then
       father.size->private.fathersize;
       size->private.mysize;
       father.position->private.fatherpos;
       (private.fatherpos.v+(private.fathersize.v-private.mysize.v) div 2,
        private.fatherpos.h+(private.fathersize.h-private.mysize.h) div 2)
         ->position;
       
    else
       mouse.globalPosition->position; 
   if);
   show;
     

-- SRsearchDialogOpen: DoPart --
do
   (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       THIS(searchDialog).contents->private.theMessage.open;
       THIS(searchDialog).contents->private.theText.open;
       THIS(searchDialog).contents->private.nextButton.open;
       THIS(searchDialog).contents->private.previousButton.open;
       THIS(searchDialog).contents->private.browseHitsButton.open;
       THIS(searchDialog).contents->private.cancelButton.open;
       THIS(searchDialog).contents->private.caseSensitiveButton.open;
       THIS(searchDialog).contents->private.wholeWordButton.open;
       THIS(searchDialog).contents->private.searchAreaButton.open;
       (360,190)->private.mysize->size;
       private.theText[]->target;
       INNER ;
       
   if);
     

-- SRreplaceDialogPrivate: Descriptor --
(#
   fatherpos,fathersize,mysize: @point;
   rescale:
     (# newsize: @point; hpct,vpct: @real; h,v: @integer
     do
        size->newsize;
        newsize.h / mysize.h->hpct;
        newsize.v / mysize.v->vpct;
        newsize->mysize;
        cancelButton.position->(h,v);
        (h*hpct,v*vpct)->cancelButton.position;
        cancelButton.size->(h,v);
        (h*hpct,v)->cancelButton.size;
        nextButton.position->(h,v);
        (h*hpct,v*vpct)->nextButton.position;
        nextButton.size->(h,v);
        (h*hpct,v)->nextButton.size;
        previousButton.position->(h,v);
        (h*hpct,v*vpct)->previousButton.position;
        previousButton.size->(h,v);
        (h*hpct,v)->replaceButton.size;
        replaceButton.position->(h,v);
        (h*hpct,v*vpct)->replaceButton.position;
        replaceButton.size->(h,v);
        (h*hpct,v)->replaceButton.size;
        theSearchMessage.position->(h,v);
        (h*hpct,v*vpct)->theSearchMessage.position;
        theSearchMessage.size->(h,v);
        (h*hpct,v)->theSearchMessage.size;
        theReplaceMessage.position->(h,v);
        (h*hpct,v*vpct)->theReplaceMessage.position;
        theReplaceMessage.size->(h,v);
        (h*hpct,v)->theReplaceMessage.size;
        theSearchtxt.position->(h,v);
        (h*hpct,v*vpct)->theSearchtxt.position;
        theSearchtxt.size->(h,v);
        (h*hpct,v)->theSearchtxt.size;
        theReplacetxt.position->(h,v);
        (h*hpct,v*vpct)->theReplacetxt.position;
        theReplacetxt.size->(h,v);
        (h*hpct,v)->theReplacetxt.size;
        caseSensitiveButton.position->(h,v);
        (h*hpct,v*vpct)->caseSensitiveButton.position;
        caseSensitiveButton.size->(h,v);
        (h*hpct,v)->caseSensitiveButton.size;
        wholeWordButton.position->(h,v);
        (h*hpct,v*vpct)->wholeWordButton.position;
        wholeWordButton.size->(h,v);
        (h*hpct,v)->wholeWordButton.size;
        searchAreaButton.position->(h,v);
        (h*hpct,v*vpct)->searchAreaButton.position;
        searchAreaButton.size->(h,v);
        (h*hpct,v)->searchAreaButton.size;
        
     #);
   dialogopened: @boolean;
   DialogText: textField
     (#
        eventhandler:: 
          (#
             onBeforeChange:: 
               (# txt: ^text
               do
                  (if length = 1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl // ascii.cr then
                          THIS(replaceDialog).private.nextButton.theEventhandler
                          .onMouseup;
                          false->allow;
                          
                      if);
                      
                  if)
               #);
             onKeyDown:: 
               (# 
               do
                  (if ch = ascii.esc then
                      THIS(replaceDialog).private.cancelButton.theEventhandler.
                        onMouseup;
                      
                  if)
               #);
             
          #);
        
     #);
   theSearchMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,10),(20+320,10+30))->frame #);
        
     #);
   theSearchTxt: @DialogText
     (# open::  (#  do ((20,40),(20+430,40+30))->frame #);  #);
   theReplaceMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,70),(20+320,70+30))->frame #);
        
     #);
   theReplaceTxt: @DialogText
     (# open::  (#  do ((20,100),(20+430,100+30))->frame #);  #);
   replaceButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (#  do theReplaceTxt.contents->thereplaceText[]; replace #)
          #);
        open:: 
          (#  do ((20,140),(20+100,140+30))->frame; 'Replace'->label;  #);
        
     #);
   nextButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (#  do theSearchTxt.contents->thesearchText[]; next #)
          #);
        open:: 
          (#  do ((130,140),(130+100,140+30))->frame; 'Next'->label;  #);
        
     #);
   previousButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (#  do theSearchTxt.contents->thesearchText[]; previous #)
          #);
        open:: 
          (# 
          do ((240,140),(240+100,140+30))->frame; 'Previous'->label; 
          #);
        
     #);
   cancelButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do
                  theSearchTxt.contents->thesearchText[];
                  cancel;
                  THIS(replaceDialog).hide
               #)
          #);
        open:: 
          (#  do ((350,140),(350+100,140+30))->frame; 'Close'->label;  #);
        
     #);
   caseSensitiveButton: @radioButton
     (#
        open:: 
          (# 
          do ((20,180),(20+100,180+30))->frame; 'Match case'->label; 
          #);
        
     #);
   wholeWordButton: @radioButton
     (#
        open:: 
          (# 
          do ((130,180),(130+110,180+30))->frame; 'Match word'->label; 
          #);
        
     #);
   searchAreaButton: (*$ 225*) @optionButton
     (#
        areaNo: @integer;
        areamenu: @menu
          (#
             itemtype: menuitem
               (#
                  no: @integer;
                  eventHandler:: 
                    (# onSelect::  (#  do no->areaNo;  #) #)
               #);
             addarea:
               (# t: ^text; item: ^itemType; no: @integer; 
               enter (t[],no)
               do
                  &itemType[]->item[];
                  item.open;
                  t[]->item.name;
                  no->item.no;
                  item[]->append;
                  
               #);
             open:: 
               (# 
               do
                  ('Form',1)->addarea;
                  ('Group',2)->addarea;
                  ('Project',3)->addarea;
                  
               #);
             
          #);
        open:: 
          (# 
          do
             ((20,210),(20+200,210+30))->frame;
             areamenu.open;
             areamenu[]->popUpMenu;
             2->areaNo->currentItem;
             'Search in:'->label;
             
          #)
     #);
   
#)  

-- SRreplaceDialogPopup: DoPart --
do
   (if not private.dialogopened then open;  if);
   titleText[]->title;
   msgText[]->private.theSearchMessage.label;
   msgText2[]->private.theReplaceMessage.label;
   private.theSearchTxt.all->private.theSearchTxt.selection;
   private.theSearchTxt.delete;
   (if defaultText[] = none then ''->defaultText[] if);
   defaultText[]->private.theSearchTxt.insert;
   private.theSearchTxt.all->private.theSearchTxt.selection;
   INNER ;
   (if father[] <> none then
       father.size->private.fathersize;
       size->private.mysize;
       father.position->private.fatherpos;
       (private.fatherpos.v+(private.fathersize.v-private.mysize.v) div 2,
        private.fatherpos.h+(private.fathersize.h-private.mysize.h) div 2)
         ->position;
       
    else
       mouse.globalPosition->position; 
   if);
   show;
     

-- SRreplaceDialogOpen: DoPart --
do
   (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       THIS(replaceDialog).contents->private.theSearchMessage.open;
       THIS(replaceDialog).contents->private.theSearchTxt.open;
       THIS(replaceDialog).contents->private.theReplaceMessage.open;
       THIS(replaceDialog).contents->private.theReplaceTxt.open;
       THIS(replaceDialog).contents->private.replaceButton.open;
       THIS(replaceDialog).contents->private.nextButton.open;
       THIS(replaceDialog).contents->private.previousButton.open;
       THIS(replaceDialog).contents->private.cancelButton.open;
       THIS(replaceDialog).contents->private.caseSensitiveButton.open;
       THIS(replaceDialog).contents->private.wholeWordButton.open;
       THIS(replaceDialog).contents->private.searchAreaButton.open;
       (460,250)->private.mysize->size;
       private.theSearchTxt[]->target;
       INNER ;
       
   if);
     

-- SRsearchDialogMatchCase: DoPart --
do private.caseSensitiveButton.state->value  

-- SRsearchDialogMatchWord: DoPart --
do private.wholeWordButton.state->value  

-- SRsearchDialogSearchArea: DoPart --
do private.searchAreaButton.areaNo->value  

-- SRreplaceDialogMatchCase: DoPart --
do private.caseSensitiveButton.state->value  

-- SRreplaceDialogMatchWord: DoPart --
do private.wholeWordButton.state->value  

-- SRreplaceDialogSearchArea: DoPart --
do private.searchAreaButton.areaNo->value  

