ORIGIN '../searchreplace';
INCLUDE '~beta/guienv/fields';
LIB_ITEM 'editorsearchreplace';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-2000
 *   All rights reserved.
 *)
-- SRsearchDialogPrivate: Descriptor --
(#
   fatherpos,fathersize,mysize: @point;
   rescale:
     (# newsize: @point; hpct,vpct: @real; h,v: @integer
     do
        size->newsize;
        newsize.h / mysize.h->hpct;
        (if mysize.v <> 0 then
            newsize.v / mysize.v->vpct
         else
            leave rescale
        if);
        newsize->mysize;
        cancelButton.position->(h,v);
        (h*hpct,v*vpct)->cancelButton.position;
        cancelButton.size->(h,v);
        (h*hpct,v)->cancelButton.size;
        nextButton.position->(h,v);
        (h*hpct,v*vpct)->nextButton.position;
        nextButton.size->(h,v);
        (h*hpct,v)->nextButton.size;
        previousButton.position->(h,v);
        (h*hpct,v*vpct)->previousButton.position;
        previousButton.size->(h,v);
        (h*hpct,v)->previousButton.size;
        browseHitsButton.position->(h,v);
        (h*hpct,v*vpct)->browseHitsButton.position;
        browseHitsButton.size->(h,v);
        (h*hpct,v)->browseHitsButton.size;
        theMessage.position->(h,v);
        (h*hpct,v*vpct)->theMessage.position;
        theMessage.size->(h,v);
        (h*hpct,v)->theMessage.size;
        theText.position->(h,v);
        (h*hpct,v*vpct)->theText.position;
        theText.size->(h,v);
        (h*hpct,v)->theText.size;
        caseSensitiveCheckBox.position->(h,v);
        (h*hpct,v*vpct)->caseSensitiveCheckBox.position;
        caseSensitiveCheckBox.size->(h,v);
        (h*hpct,v)->caseSensitiveCheckBox.size;
        wholeWordCheckBox.position->(h,v);
        (h*hpct,v*vpct)->wholeWordCheckBox.position;
        wholeWordCheckBox.size->(h,v);
        (h*hpct,v)->wholeWordCheckBox.size;
        showAllHitsCheckBox.position->(h,v);
        (h*hpct,v*vpct)->showAllHitsCheckBox.position;
        showAllHitsCheckBox.size->(h,v);
        (h*hpct,v)->showAllHitsCheckBox.size;
        searchAreaOptionButton.position->(h,v);
        (h*hpct,v*vpct)->searchAreaOptionButton.position;
        searchAreaOptionButton.size->(h,v);
        (h*hpct,v)->searchAreaOptionButton.size;
        
     #);
   dialogopened: @boolean;
   DialogText: textField
     (#
        eventhandler:: 
          (#
             onBeforeChange:: 
               (# txt: ^text
               do
                  (if length = 1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl // ascii.cr then
                          THIS(searchDialog).private.nextButton.theEventhandler.
                            onMouseup;
                          false->allow
                      if);
                      
                  if)
               #);
             onKeyDown:: 
               (# 
               do
                  (if ch = ascii.esc then
                      THIS(searchDialog).private.cancelButton.theEventhandler.
                        onMouseup
                   else
                      theText.contents->usertext[]; onUserTextChanged
                  if)
               #);
             
          #);
        
     #);
   theMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,10),(20+320,10+30))->frame #);
        
     #);
   theText: @DialogText
     (# open::  (#  do ((20,40),(20+320,40+30))->frame #);  #);
   nextButton: @pushButton
     (#
        eventHandler:: 
          (# onMouseUp::  (#  do theText.contents->usertext[]; next #)
          #);
        open::  (#  do ((20,80),(20+100,80+30))->frame; 'Next'->label;  #);
        
     #);
   previousButton: @pushButton
     (#
        eventHandler:: 
          (# onMouseUp::  (#  do theText.contents->usertext[]; previous #)
          #);
        open:: 
          (# 
          do ((130,80),(130+110,80+30))->frame; 'Previous'->label; disable; 
          #);
        
     #);
   browseHitsButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp::  (#  do theText.contents->usertext[]; browseHits #)
          #);
        open:: 
          (# 
          do ((130,80),(130+110,80+30))->frame; 'Browse hits'->label; 
          #);
        
     #);
   cancelButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do theText.contents->usertext[]; cancel; THIS(searchDialog).hide
               #)
          #);
        open:: 
          (#  do ((250,80),(240+100,80+30))->frame; 'Close'->label;  #);
        
     #);
   caseSensitiveCheckBox: @checkBox
     (#
        open:: 
          (# 
          do ((20,120),(20+100,120+30))->frame; 'Match case'->label; 
          #);
        
     #);
   wholeWordCheckBox: @checkBox
     (#
        open:: 
          (# 
          do ((130,120),(130+110,120+30))->frame; 'Match word'->label; 
          #);
        
     #);
   showAllHitsCheckBox: @checkBox
     (#
        open:: 
          (# 
          do ((240,120),(240+110,120+30))->frame; 'Show all hits'->label; 
          #);
        
     #);
   onlyApplicationsCheckBox: @checkBox
     (#
        open:: 
          (# 
          do ((350,120),(350+110,120+30))->frame; 'Only Applications'->label; 
          #);
        
     #);
   searchAreaOptionButton: @optionButton
     (#
        areaNo: @integer;
        areamenu: @menu
          (#
             itemtype: menuitem
               (#
                  no: @integer;
                  enable: @boolean;
                  eventHandler:: 
                    (#
                       onStatus::  (#  do true->value #);
                       onSelect:: 
                         (# 
                         do
                            no->areaNo;
                            'areamenu, areano: '->putText;
                            areano->putInt;
                            newLine;
                            
                         #)
                    #)
               #);
             addarea:
               (# t: ^text; item: ^itemType; no: @integer; 
               enter (t[],no)
               do
                  &itemType[]->item[];
                  item.open;
                  t[]->item.name;
                  no->item.no;
                  item[]->append;
                  
               #);
             menuStatus:
             (* is not because is does not work on windows, 
              * under X11 is works but the text is not dimmed 
              *)
               (# 
               do
                  scan
                    (# cur: ^itemType
                    do
                       current[]->cur[];
                       (if cur.no
                        // searchArea_project then
                           enableProjectScope->cur.enable
                        // searchArea_fg then
                           enableFileScope->cur.enable
                        // searchArea_ff then
                           enableFragmentFormScope->cur.enable
                        // searchArea_cs then
                           enableFragmentFormScope->cur.enable
                       if)
                    #)
               #);
             calculate:
               (# 
               do
                  (if enableFragmentFormScope then
                      ('Current selection',searchArea_cs)->addarea;
                      ('Current fragment form',searchArea_ff)->addarea
                  if);
                  (if enableFileScope then
                      ('Current file',searchArea_fg)->addarea
                  if);
                  (if enableProjectScope then
                      ('Current project',searchArea_project)->addarea
                  if)
               #);
             setCurrent:
               (# keepAreaNo: @boolean
               do
                  (if areaNo > 0 then
                      loop: scan
                        (# cur: ^itemType
                        do
                           current[]->cur[];
                           (if cur.no = areaNo then
                               true->keepAreaNo; leave loop
                           if)
                        #)
                  if);
                  (if not keepAreaNo then
                      (if enableFileScope then
                          searchArea_fg->areaNo->currentItem
                       else
                          (if enableProjectScope then
                              searchArea_project->areaNo->currentItem
                          if)
                      if)
                  if)
               #);
             recalculate: (#  do clear; calculate; setCurrent #);
             open::  (#  do calculate #);
             
          #);
        open:: 
          (# 
          do
             (500,37)->size;
             (20,150)->position;
             ((20,150),(20+200,150+30))->frame;
             areamenu.open;
             areamenu[]->popUpMenu;
             areamenu.setCurrent;
             'Search in:'->label;
             
          #)
     #);
   
#)  

-- SRsearchDialogReset: DoPart --
do
   messText[]->private.theMessage.label;
   private.caseSensitiveCheckBox.enable;
   private.wholeWordCheckBox.enable;
   false->onlyApplications;
     

-- SRsearchDialogPopup: DoPart --
do
   (if not private.dialogopened then
       open;
       (if father[] <> none then
           father.size->private.fathersize;
           size->private.mysize;
           father.position->private.fatherpos;
           (private.fatherpos.v+(private.fathersize.v-private.mysize.v) div 2,
            private.fatherpos.h+(private.fathersize.h-private.mysize.h) div 2)
             ->position
        else
           mouse.globalPosition->position
       if)
    else
       private.searchAreaOptionButton.areaMenu.recalculate
   if);
   titleText[]->title;
   msgText[]->private.theMessage.label;
   (if defaultText[] = none then
       ''->defaultText[];
       private.theText.all->private.theText.selection;
       private.theText.delete;
       defaultText[]->private.theText.insert;
       private.theText.all->private.theText.selection
    else
       (if defaultText.length <> 0 then
           private.theText.all->private.theText.selection;
           private.theText.delete;
           defaultText[]->private.theText.insert;
           private.theText.all->private.theText.selection
        else
       (* keep existing text *)
           
       if)
   if);
   (if onlyApplications then
       private.caseSensitiveCheckBox.disable; private.wholeWordCheckBox.disable
    else
       private.caseSensitiveCheckBox.enable; private.wholeWordCheckBox.enable
   if);
   INNER ;
   show;
     

-- SRsearchDialogOpen: DoPart --
do
   (480,205)->size;
   (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       THIS(searchDialog).contents->private.theMessage.open;
       THIS(searchDialog).contents->private.theText.open;
       THIS(searchDialog).contents->private.nextButton.open;
       THIS(searchDialog).contents->private.previousButton.open;
       THIS(searchDialog).contents->private.browseHitsButton.open;
       THIS(searchDialog).contents->private.cancelButton.open;
       THIS(searchDialog).contents->private.caseSensitiveCheckBox.open;
       THIS(searchDialog).contents->private.wholeWordCheckBox.open;
       THIS(searchDialog).contents->private.showAllHitsCheckBox.open;
       THIS(searchDialog).contents->private.searchAreaOptionButton.open;
       (if onlyApplications then
           private.caseSensitiveCheckBox.disable;
           private.wholeWordCheckBox.disable
        else
           private.caseSensitiveCheckBox.enable;
           private.wholeWordCheckBox.enable
       if);
       (360,190)->private.mysize->size;
       private.theText[]->target;
       INNER
    else
       'search dialog already opened'->putLine
   if);
     

-- SRsearchDialogClose: DoPart --
do onClose  

-- SRreplaceDialogPrivate: Descriptor --
(#
   fatherpos,fathersize,mysize: @point;
   rescale:
     (# newsize: @point; hpct,vpct: @real; h,v: @integer
     do
        size->newsize;
        newsize.h / mysize.h->hpct;
        (if mysize.v <> 0 then
            newsize.v / mysize.v->vpct
         else
            leave rescale
        if);
        newsize->mysize;
        cancelButton.position->(h,v);
        (h*hpct,v*vpct)->cancelButton.position;
        cancelButton.size->(h,v);
        (h*hpct,v)->cancelButton.size;
        nextButton.position->(h,v);
        (h*hpct,v*vpct)->nextButton.position;
        nextButton.size->(h,v);
        (h*hpct,v)->nextButton.size;
        replaceAllButton.position->(h,v);
        (h*hpct,v*vpct)->replaceAllButton.position;
        replaceAllButton.size->(h,v);
        (h*hpct,v)->replaceButton.size;
        replaceButton.position->(h,v);
        (h*hpct,v*vpct)->replaceButton.position;
        replaceButton.size->(h,v);
        (h*hpct,v)->replaceButton.size;
        theSearchMessage.position->(h,v);
        (h*hpct,v*vpct)->theSearchMessage.position;
        theSearchMessage.size->(h,v);
        (h*hpct,v)->theSearchMessage.size;
        theReplaceMessage.position->(h,v);
        (h*hpct,v*vpct)->theReplaceMessage.position;
        theReplaceMessage.size->(h,v);
        (h*hpct,v)->theReplaceMessage.size;
        theSearchtxt.position->(h,v);
        (h*hpct,v*vpct)->theSearchtxt.position;
        theSearchtxt.size->(h,v);
        (h*hpct,v)->theSearchtxt.size;
        theReplacetxt.position->(h,v);
        (h*hpct,v*vpct)->theReplacetxt.position;
        theReplacetxt.size->(h,v);
        (h*hpct,v)->theReplacetxt.size;
        caseSensitiveCheckBox.position->(h,v);
        (h*hpct,v*vpct)->caseSensitiveCheckBox.position;
        caseSensitiveCheckBox.size->(h,v);
        (h*hpct,v)->caseSensitiveCheckBox.size;
        wholeWordCheckBox.position->(h,v);
        (h*hpct,v*vpct)->wholeWordCheckBox.position;
        wholeWordCheckBox.size->(h,v);
        (h*hpct,v)->wholeWordCheckBox.size;
        replaceDeclarationCheckBox.position->(h,v);
        (h*hpct,v*vpct)->replaceDeclarationCheckBox.position;
        replaceDeclarationCheckBox.size->(h,v);
        (h*hpct,v)->replaceDeclarationCheckBox.size;
        searchAreaOptionButton.position->(h,v);
        (h*hpct,v*vpct)->searchAreaOptionButton.position;
        searchAreaOptionButton.size->(h,v);
        (h*hpct,v)->searchAreaOptionButton.size;
        
     #);
   dialogopened: @boolean;
   DialogText: textField
     (#
        eventhandler::< 
          (#
             onBeforeChange::< 
               (# txt: ^text
               do
                  (if length = 1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl // ascii.cr then
                          THIS(replaceDialog).private.nextButton.theEventhandler
                          .onMouseup;
                          false->allow;
                          
                       // ascii.ht then
                          false->allow
                      if);
                      
                  if)
               #)
          #)
     #);
   theSearchMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,20),(20+320,20+30))->frame #);
        
     #);
   theSearchTxt: @DialogText
     (#
        eventhandler:: 
          (#
             onKeyDown:: 
               (# 
               do
                  (if ch = ascii.esc then
                      THIS(replaceDialog).private.cancelButton.theEventhandler.
                        onMouseup
                   else
                      theSearchTxt.contents->theUserSearchText[];
                      onUserTextChanged
                  if);
                  (if ch = ascii.ht then theReplaceTxt[]->target;  if)
               #);
             
          #);
        open::  (#  do ((20,40),(20+430,40+30))->frame #);
        
     #);
   theReplaceMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,80),(20+320,80+30))->frame #);
        
     #);
   theReplaceTxt: @DialogText
     (#
        eventhandler:: 
          (#
             onKeyDown:: 
               (# 
               do
                  (if ch = ascii.esc then
                      THIS(replaceDialog).private.cancelButton.theEventhandler.
                        onMouseup
                  if);
                  (if ch = ascii.ht then theSearchTxt[]->target;  if)
               #)
          #);
        open::  (#  do ((20,100),(20+430,100+30))->frame #);
        
     #);
   replaceButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do
                  theSearchTxt.contents->theUserSearchText[];
                  theReplaceTxt.contents->theUserReplaceText[];
                  replace
               #)
          #);
        open:: 
          (#  do ((20,140),(20+100,140+30))->frame; 'Replace'->label;  #);
        
     #);
   nextButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (#  do theSearchTxt.contents->theUserSearchText[]; next #)
          #);
        open:: 
          (#  do ((130,140),(130+100,140+30))->frame; 'Next'->label;  #);
        
     #);
   replaceAllButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do
                  theSearchTxt.contents->theUserSearchText[];
                  theReplaceTxt.contents->theUserReplaceText[];
                  replaceAll
               #)
          #);
        open:: 
          (# 
          do ((240,140),(240+100,140+30))->frame; 'Replace all'->label; 
          #);
        
     #);
   cancelButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do
                  theSearchTxt.contents->theUserSearchText[];
                  cancel;
                  THIS(replaceDialog).hide
               #)
          #);
        open:: 
          (#  do ((350,140),(350+100,140+30))->frame; 'Close'->label;  #);
        
     #);
   caseSensitiveCheckBox: @checkBox
     (#
        open:: 
          (# 
          do ((20,180),(20+100,180+30))->frame; 'Match case'->label; 
          #);
        
     #);
   wholeWordCheckBox: @checkBox
     (#
        open:: 
          (# 
          do ((130,180),(130+110,180+30))->frame; 'Match word'->label; 
          #);
        
     #);
   replaceDeclarationCheckbox: @checkBox
     (#
        open:: 
          (# 
          do
             ((240,180),(240+120,180+30))->frame; 'Replace declaration'->label; 
          #);
        
     #);
   searchAreaOptionButton: (*$ 225*) @optionButton
     (#
        areaNo: @integer;
        areamenu: @menu
          (#
             itemtype: menuitem
               (#
                  no: @integer;
                  eventHandler:: 
                    (# onSelect::  (#  do no->areaNo;  #) #)
               #);
             addarea:
               (# t: ^text; item: ^itemType; no: @integer; 
               enter (t[],no)
               do
                  &itemType[]->item[];
                  item.open;
                  t[]->item.name;
                  no->item.no;
                  item[]->append;
                  
               #);
             calculate:
               (# 
               do
                  (if enableFragmentFormScope then
                      ('Current selection',searchArea_cs)->addarea;
                      ('Current fragment form',searchArea_ff)->addarea
                  if);
                  (if enableFileScope then
                      ('Current file',searchArea_fg)->addarea
                  if);
                  (if enableProjectScope then
                      ('Current project',searchArea_project)->addarea
                  if)
               #);
             setCurrent:
               (# keepAreaNo: @boolean
               do
                  (if areaNo > 0 then
                      loop: scan
                        (# cur: ^itemType
                        do
                           current[]->cur[];
                           (if cur.no = areaNo then
                               true->keepAreaNo; leave loop
                           if)
                        #)
                  if);
                  (if not keepAreaNo then
                      (if enableFragmentFormScope then
                          searchArea_ff->areaNo->currentItem
                       else
                          (if enableProjectScope then
                              searchArea_project->areaNo->currentItem
                          if)
                      if)
                  if)
               #);
             recalculate: (#  do clear; calculate; setCurrent #);
             open::  (#  do calculate #);
             
          #);
        open:: 
          (# 
          do
             ((20,210),(20+200,210+30))->frame;
             areamenu.open;
             areamenu[]->popUpMenu;
             areamenu.setCurrent;
             'Search in:'->label;
             
          #)
     #);
   
#)  

-- SRsearchDialogOnClose: DoPart --
do false->private.dialogopened; INNER  

-- SRreplaceDialogOnClose: DoPart --
do false->private.dialogopened; INNER  

-- SRreplaceDialogReset: DoPart --
do
   messText[]->private.theSearchMessage.label;
   private.caseSensitiveCheckBox.enable;
   private.wholeWordCheckBox.enable;
   private.replaceDeclarationCheckBox.disable;
   false->onlyApplications;
     

-- SRreplaceDialogPopup: DoPart --
do
   (if not private.dialogopened then
       open;
       (if father[] <> none then
           father.size->private.fathersize;
           size->private.mysize;
           father.position->private.fatherpos;
           (private.fatherpos.v+(private.fathersize.v-private.mysize.v) div 2,
            private.fatherpos.h+(private.fathersize.h-private.mysize.h) div 2)
             ->position
        else
           mouse.globalPosition->position
       if)
    else
       private.searchAreaOptionButton.areaMenu.recalculate
   if);
   titleText[]->title;
   msgText[]->private.theSearchMessage.label;
   msgText2[]->private.theReplaceMessage.label;
   (if defaultText[] = none then
       ''->defaultText[];
       private.theSearchTxt.all->private.theSearchTxt.selection;
       private.theSearchTxt.delete;
       defaultText[]->private.theSearchTxt.insert;
       private.theSearchTxt.all->private.theSearchTxt.selection
    else
       (if defaultText.length <> 0 then
           private.theSearchTxt.all->private.theSearchTxt.selection;
           private.theSearchTxt.delete;
           defaultText[]->private.theSearchTxt.insert;
           private.theSearchTxt.all->private.theSearchTxt.selection
        else
       (* keep existing text *)
           'keep existing text'->putLine
       if)
   if);
   private.theReplaceTxt.all->private.theReplaceTxt.selection;
   private.theReplaceTxt.clear;
   (if onlyApplications then
       private.caseSensitiveCheckBox.disable;
       private.wholeWordCheckBox.disable;
       private.replaceDeclarationCheckBox.enable
    else
       private.caseSensitiveCheckBox.enable;
       private.wholeWordCheckBox.enable;
       private.replaceDeclarationCheckBox.disable
   if);
   INNER ;
   show;
     

-- SRreplaceDialogOpen: DoPart --
do
   (435,400)->size;
   (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       THIS(replaceDialog).contents->private.theSearchMessage.open;
       THIS(replaceDialog).contents->private.theSearchTxt.open;
       THIS(replaceDialog).contents->private.theReplaceMessage.open;
       THIS(replaceDialog).contents->private.theReplaceTxt.open;
       THIS(replaceDialog).contents->private.replaceButton.open;
       THIS(replaceDialog).contents->private.nextButton.open;
       THIS(replaceDialog).contents->private.replaceAllButton.open;
       THIS(replaceDialog).contents->private.cancelButton.open;
       THIS(replaceDialog).contents->private.caseSensitiveCheckBox.open;
       THIS(replaceDialog).contents->private.wholeWordCheckBox.open;
       THIS(replaceDialog).contents->private.replaceDeclarationCheckBox.open;
       THIS(replaceDialog).contents->private.searchAreaOptionButton.open;
       false->private.replaceDeclarationCheckBox.state;
       (if onlyApplications then
           private.caseSensitiveCheckBox.disable;
           private.wholeWordCheckBox.disable;
           private.replaceDeclarationCheckBox.enable
        else
           private.caseSensitiveCheckBox.enable;
           private.wholeWordCheckBox.enable;
           private.replaceDeclarationCheckBox.disable
       if);
       (460,250)->private.mysize->size;
       private.theSearchTxt[]->target;
       INNER ;
       
   if);
     

-- SRreplaceDialogClose: DoPart --
do onClose  

-- SRsearchDialogMatchCase: DoPart --
do private.caseSensitiveCheckBox.state->value  

-- SRsearchDialogMatchWord: DoPart --
do private.wholeWordCheckBox.state->value  

-- SRsearchDialogShowAllHits: DoPart --
do private.showAllHitsCheckBox.state->value  

-- SRsearchDialogSearchArea: DoPart --
do private.searchAreaOptionButton.areaNo->value  

-- SRreplaceDialogMatchCase: DoPart --
do private.caseSensitiveCheckBox.state->value  

-- SRreplaceDialogMatchWord: DoPart --
do private.wholeWordCheckBox.state->value  

-- SRreplaceDialogReplaceDeclaration: DoPart --
do private.replaceDeclarationCheckBox.state->value  

-- SRreplaceDialogSearchArea: DoPart --
do private.searchAreaOptionButton.areaNo->value  

