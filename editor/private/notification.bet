ORIGIN '../codeeditor';
INCLUDE '~beta/pretty/v5.1/pplib';
(*
 * COPYRIGHT
 * Copyright Mjolner Informatics, 1986-96
 * All rights reserved.
 *)
-- notificationPrivate: Descriptor --
(#
   updateList:
     (# listNode: ^astInterface.expanded; containsLastElement: @boolean
     enter (listNode[],containsLastElement)
     do (* (editorRoot[] ,- 1)->astView.pp.present;*)
        (if containsLastElement then
            (if listNode[]->editorRoot.equal then
                doReprettyprint
             else
                (if listNode.father <> none then
                    (listNode.father,listNode.father)->astView.pp.update
                 else
                    doReprettyprint
                if)
            if)
         else
            (listNode[],listNode[])->astView.pp.update
        if)
     #)
#)  

-- autoSaveLimitBody: Descriptor --
(#  do 1->ASlimit;  #)  

-- fragmentChangedEventBody: Descriptor --
(# 
do
   noOfAstChanges+1->noOfAstChanges;
   fragmentTouched;
   (if byMe then
       (if (noOfAstChanges >= ASLimit) then
           (if not isApropertyEditor then
               (if thisGroupEditor[] <> none then
                   (if not thisGroupEditor.isAfrejaEditor then
                       autoSave; 0->noOfAstChanges; 
                   if)
                else
                   autoSave; 0->noOfAstChanges
               if)
           if)
       if)
   if)
#)  

-- resetFragmentTouchBody: Descriptor --
(#  do 0->touched;  #)  

-- fragmentTouchedBody: Descriptor --
(# 
do
   touched+1->touched;
   (if switch[7] then
       (frag.father).name->putText;
       '-'->put;
       frag.name->putText;
       ' fragment touched '->putText;
       touched->putint;
       ' times'->putLine;
       
   if);
   (if touched = 1 then 'TOUCHED'->frag.prop.addProp if);
   (if thisGroupEditor[] <> none then thisGroupEditor.groupTouched;  if)
#)  

-- fragmentDetouchedBody: Descriptor --
(# 
do
   touched-1->touched;
   (if switch[7] then
       (frag.father).name->putText;
       '-'->put;
       frag.name->putText;
       ' fragment touched '->putText;
       touched->putint;
       ' times'->putLine;
       
   if);
   (if thisGroupEditor[] <> none then thisGroupEditor.groupDetouched;  if);
   
#)  

-- notificationNewFragment: Descriptor --
(#  do (editorRoot[],- 1)->astViEw.pp.present;  #)  

-- notificationAstReplaced: Descriptor --
(# 
do
   (if switch[7] then
       '***** astReplacedEvent'->screen.putLine;
       '***** '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putLine
   if);
   INNER astReplacedEvent;
   (if byMe then
       (if switch[7] then 'astReplacedEvent byMe'->putLine if);
       (if oldAST[]->editorRoot.equal then
           (if switch[7] then 'oldAST[] = editorRoot[]'->putLine if);
           newAST[]->editorRoot[];
           (if theSifTexteditor[] <> none then
               newAST[]->theSifTExteditor.editorRoot[]
           if)
       if);
       (oldAst[],newAst[])->astView.pp.update;
       (THIS(codeeditor)[],oldAst[],newAst[])->ffh.anASTreplacedEvent
    else
       (if theSifTextEditor[] <> none (* the editor is visible *) then
           (if switch[7] then 'astReplacedEvent not byMe'->putLine if);
           (if oldAST[]->editorRoot.equal then
               (if switch[7] then 'oldAST[] = editorRoot[]'->putLine if);
               newAST[]->editorRoot[];
               (if theSifTexteditor[] <> none then
                   newAST[]->theSifTExteditor.editorRoot[]
               if)
           if);
           (if (newAst[],editorRoot[])->isNodeInTree then
           (* newAST is in the subtree of this editor *)
               (if switch[7] then
                   'newAST is in the subtree of this editor'->putLine
               if);
               (oldAst[],newAst[])->astView.pp.update;
               
            else
               (if (editorRoot[],oldAST[])->isNodeInTree then
               (* the subtree in this editor is in oldAST, which means that *)
               (* this editor is obsolete and must be closed *)
                   (if switch[7] then
                       'This editor is obsolete and must be closed '->putLine
                   if);
                   (THIS(codeeditor).theSifTextEditor.theWindow).close;
                   
               if)
           if)
       if)
   if)
#)  

-- notificationAstReplacedSequence: Descriptor --
(# 
do
   (if switch[7] then
       '***** astReplacedSequencdEvent'->screen.putLine;
       '***** '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putLine
   if);
   INNER astReplacedSequenceEvent;
   (if byMe then
       (if switch[7] then 'astReplacedSequenceEvent byMe'->putLine if);
       (rootOfSequence[],rootOfSequence[])->astView.pp.update;
       (THIS(codeeditor)[],rootOfSequence[])->ffh.ASTreplacedSequenceEvent
    else
       (if theSifTextEditor[] <> none (* the editor is visible *) then
           (if (rootOfSequence[],editorRoot[])->isNodeInTree then
               (rootOfSequence[],rootOfSequence[])->astView.pp.update; 
           if)
       if)
   if)
#)  

-- notificationListElementInserted: Descriptor --
(# 
do
   (if switch[7] then
       '***** listElementInsertedEvent'->screen.putLine;
       '***** '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putLine
   if);
   INNER listElementInsertedEvent;
   (if byME then (*(listNode[],position,0,1)->astView.pp.updateList;*)
       (if switch[7] then 'listElementInsertedEvent byMe'->putLine if);
       (listNode[],false)->notificationPrivate.updateList;
       (THIS(codeeditor)[],listNode[],position)->ffh.listElementInsertedEvent
    else
       (if theSifTextEditor[] <> none (* the editor is visible *) then
           (if (listNode[],editorRoot[])->isNodeInTree then
               (listNode[],false)->notificationPrivate.updateList
           if)
       if)
   if)
#)  

-- notificationListElementsDeleted: Descriptor --
(# 
do
   (if switch[7] then
       '***** listElementDeletedEvent'->screen.putLine;
       '***** '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putLine
   if);
   INNER listElementsDeletedEvent;
   (if byMe then (*(listNode[],position,length,0)->astView.pp.updateList;*)
       (if switch[7] then 'listElementsDeletedEvent byMe'->putLine if);
       (listNode[],false)->notificationPrivate.updateList;
       (THIS(codeeditor)[],listNode[],position,length,oldElements[])
         ->ffh.listElementsDeletedEvent
    else
       (if theSifTextEditor[] <> none (* the editor is visible *) then
           (if (listNode[],editorRoot[])->isNodeInTree then
               (listNode[],false)->notificationPrivate.updateList
            else
               (for i: length repeat
                 (if (editorRoot[],oldElements.elm[i][])->isNodeInTree then
                     (THIS(codeeditor).theSifTextEditor.theWindow).close; 
                 if)
               for)
           if)
       if)
   if)
#)  

-- notificationListElementsReplaced: Descriptor --
(# containsLastElement: @boolean
do
   (if switch[7] then
       '***** listElementsReplacedEvent'->screen.putLIne;
       '***** '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putLine
   if);
   INNER listElementsReplacedEvent;
   (if position+length = listNode.noOfSons then true->containsLastElement if);
   (if byMe then
   (*(listNode[],position,length,newLength)->astView.pp.updateList;*)
       (if switch[7] then 'listElementsReplacedEvent byMe'->putLine if);
       (listNode[],containsLastElement)->notificationPrivate.updateList;
       (THIS(codeeditor)[],listNode[],position,length,oldElements[],newLength)
         ->ffh.listElementsReplacedEvent
    else
       (if theSifTextEditor[] <> none (* the editor is visible *) then
           (if (listNode[],editorRoot[])->isNodeInTree then
               (listNode[],containsLastElement)->notificationPrivate.updateList
            else
               (for i: length repeat
                 (if (editorRoot[],oldElements.elm[i][])->isNodeInTree then
                     (THIS(codeeditor).theSifTextEditor.theWindow).close; 
                 if)
               for)
           if)
       if)
   if)
#)  

-- notificationRefresh: Descriptor --
(# 
do
   (if byMe then
       (node[],node[])->astView.pp.update;
       (THIS(codeeditor)[],node[])->ffh.refreshEvent
    else
       (if isAfrejaEditor then
           (if theSifTexteditor[] <> none then
               (if (node[],editorRoot[])->isNodeInTree then
                   (node[],node[])->astView.pp.update
               if)
           if)
       if);
       
   if)
#)  

-- notificationFocusChanged: Descriptor --
(# 
do
   (if switch[7] then '***** focusChangedEvent'->screen.putLine if);
   updateExpandMenu;
   updateEditMenu;
   (if byMe then
       (if cs.isUnexpanded then
           edenv.setRightButtonCursor
        else
           edenv.setStructureCursor
       if);
       (THIS(codeeditor)[],newCS)->ffh.focusChangedEvent
   if);
   INNER focusChangedEvent
#)  

