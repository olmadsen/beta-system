ORIGIN '../codeeditor';
LIB_ITEM 'editorcodeeditor';
(*
 * COPYRIGHT
 *	 Copyright (C) Aarhus University
 *	 All rights reserved.
 *)
-- ffObserverPrivate: Descriptor --
(#
   updateList:
     (# listNode: ^astInterface.expanded; containsLastElement: @boolean
     enter (listNode[],containsLastElement)
     do (* (editorRoot[] ,- 1)->astView.pp.present;*)
        (if containsLastElement then
            (if listNode[]->editorRoot.equal then
                doReprettyprint
             else
                (if listNode.father <> none then
                    (listNode.father,listNode.father)->astView.pp.update
                 else
                    doReprettyprint
                if)
            if)
         else
            (listNode[],listNode[])->astView.pp.update
        if)
     #);
   checkCS:
     (# 
     do
        (if not ((cs.node[],editorRoot[])->isNodeInTree) then
        (* cs.node does not exist anymore  *)
            (editorRoot[],1,0,0)->cs
        if)
     #)
#)  

-- ffObserverOnRefresh: DoPart --
do
   (if isAfrejaEditor then
       (if theSifTexteditor[] <> none then
           (if (node[],editorRoot[])->isNodeInTree then
               (node[],node[])->astView.pp.update
           if)
       if)
   if)  

-- ffObserverOnFocusChanged: DoPart --
do
   (if switch[7] then
       '***** onFocusChanged    '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putText;
       ': '->putText;
       ffObserver.no->putInt;
       newLine
   if);
   INNER onFocusChanged  

-- ffObserverOnAstReplaced: DoPart --
do
   (if switch[7] then
       '***** onAstReplaced     '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putText;
       ': '->putText;
       ffObserver.no->putInt;
       newLine
   if);
   (if theSifTextEditor[] <> none (* the editor is visible *) then
       (if oldAST[]->editorRoot.equal then
           (if switch[7] then 'oldAST[] = editorRoot[]'->putLine if);
           newAST[]->editorRoot[];
           (if theSifTexteditor[] <> none then
               newAST[]->theSifTExteditor.editorRoot[]
           if)
       if);
       (if (newAst[],editorRoot[])->isNodeInTree then
       (* newAST is in the subtree of this editor *)
           (if switch[7] then
               'newAST is in the subtree of this editor'->putLine
           if);
           (oldAst[],newAst[])->astView.pp.update;
           
        else
           (if (editorRoot[],oldAST[])->isNodeInTree then
           (* the subtree in this editor is in oldAST, which means that *)
           (* this editor is obsolete and must be closed *)
               (if switch[7] then
                   'This editor is obsolete and must be closed '->putLine
               if);
               (THIS(codeeditor).theSifTextEditor.theWindow).close;
               
           if)
       if);
       private.checkCS
   if);
   INNER onAstReplaced  

-- ffObserverOnAstReplacedSequence: DoPart --
do
   (if switch[7] then
       '***** onAstReplacedSequence     '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putText;
       ': '->putText;
       ffObserver.no->putInt;
       newLine
   if);
   (if true (* byMe *) then
       (node[],node[])->astView.pp.update
    else
       (if theSifTextEditor[] <> none (* the editor is visible *) then
           (if (node[],editorRoot[])->isNodeInTree then
               (node[],node[])->astView.pp.update; 
           if)
       if)
   if);
   private.checkCS;
   INNER onAstReplacedSequence  

-- ffObserverOnListElementInserted: DoPart --
do
   (if switch[7] then
       '***** onListElementInserted     '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putText;
       ': '->putText;
       ffObserver.no->putInt;
       newLine
   if);
   (if theSifTextEditor[] <> none (* the editor is visible *) then
       (if (node[],editorRoot[])->isNodeInTree then
           (node[],false)->private.updateList
       if);
       private.checkCS
   if);
   INNER onListElementInserted  

-- ffObserverOnListElementsDeleted: DoPart --
do
   (if switch[7] then
       '***** onListElementsDeleted     '->putText;
       (frag.father).name->putText;
       '-'->put;
       frag.name->putText;
       ': '->putText;
       ffObserver.no->putInt;
       newLine
   if);
   (if theSifTextEditor[] <> none (* the editor is visible *) then
       (if (node[],editorRoot[])->isNodeInTree then
           (node[],false)->private.updateList; private.checkCS
        else
           (for i: length repeat
             (if (editorRoot[],oldElements.elm[i][])->isNodeInTree then
                 (THIS(codeeditor).theSifTextEditor.theWindow).close; 
             if)
           for)
       if)
   if);
   INNER onListElementsDeleted  

-- ffObserverOnListElementsReplaced: DoPart --
do
     (# containsLastElement: @boolean
     do
        (if switch[7] then
            '***** onListElementsReplaced     '->putText;
            (frag.father).name->putText;
            '-'->put;
            frag.name->putText;
            ': '->putText;
            ffObserver.no->putInt;
            newLine
        if);
        (if position+length = node.noOfSons then
            true->containsLastElement
        if);
        (if theSifTextEditor[] <> none (* the editor is visible *) then
            (if (node[],editorRoot[])->isNodeInTree then
                (node[],containsLastElement)->private.updateList;
                private.checkCS
             else
                (for i: length repeat
                  (if (editorRoot[],oldElements.elm[i][])->isNodeInTree then
                      (THIS(codeeditor).theSifTextEditor.theWindow).close; 
                  if)
                for)
            if)
        if);
        INNER onListElementsReplaced
     #)  

-- ffObserverOnTouched: DoPart --
do fragmentTouched  

-- ffObserverOnDetouched: DoPart --
do fragmentDetouched  

-- ffObserverOnEnterTextediting: DoPart --
do (*'onEnterTextediting'->putLine*)   

-- ffObserverOnExitTextediting: DoPart --
do (*'onExitTextediting'->putLine*)   

