ORIGIN '../codeeditor';
-- fragmentGroupHandlerSubscribe: DoPart --
do
     (# fgh: ^fragmentGroupHandler
     do
        (if trace[7] then 'subscribe fragmentGroupHandler'->putLine if);
        search: fghandler.scan
          (# 
          do
             (if current.fg[] = ge.fg[] then
                 current[]->fgh[]; leave search
             if);
             
          #);
        (if fgh[] = none then
            &fragmentGroupHandler[]->fgh[];
            ge.fg[]->fgh.fg[];
            fgh[]->fghandler.append
        if);
        fgh[]->ge.fgh[];
        ge[]->fgh.GroupEditors.append;
        
     #)  

-- fragmentGroupHandlerUnSubscribe: DoPart --
do
     (# fgh: ^fragmentGroupHandler
     do
        (if trace[7] then 'unsubscribe fragmentGroupHandler'->putLine if);
        search: fghandler.scan
          (# 
          do
             (if current.fg[] = ge.fg[] then
                 current[]->fgh[]; leave search
             if);
             
          #);
        (if fgh[] = none then
            'No handler was installed for: '->putText; ge.fg.name->putLine; 
         else
            ge[]->fgh.GroupEditors.at->fgh.GroupEditors.delete;
            (if fgh.GroupEditors.size = 0 then
                (if trace[7] then
                    'Deleting handler for: '->putText;
                    ge.fg.name->putLine;
                    'No more subscribers'->putLine;
                    
                if);
                fgh[]->fghandler.at->fghandler.delete
            if)
        if);
        
     #)  

-- fragmentGroupHandlerGroupChanged: DoPart --
do INNER GroupChangedEvent  

-- fragmentGroupHandlerPropertiesChanged: DoPart --
do
   (if trace[7] then 'fragmentGroupHandlerPropertiesChanged'->putLine if);
   GroupEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (if trace[7] then
                'calling current.propertiesChanged'->putLine
            if);
            false->current.propertiesChanged
        if)
     #)  

-- fragmentGroupHandlerFragmentChanged: DoPart --
do
   (if trace[7] then 'fragmentGroupHandlerFragmentChanged'->putLine if);
   GroupEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (if trace[7] then 'calling current.FragmentChanged'->putLine if);
            (false,ff[])->current.FragmentChanged
        if)
     #)  

-- fragmentGroupHandlerFragmentInserted: DoPart --
do
   (if trace[7] then 'fragmentGroupHandlerFragmentInserted'->putLine if);
   GroupEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (if trace[7] then 'calling current.FragmentInserted'->putLine if);
            (false,ff[])->current.FragmentInserted
        if)
     #)  

-- fragmentGroupHandlerFragmentDeleted: DoPart --
do
   (if trace[7] then 'fragmentGroupHandlerFragmentDeleted'->putLine if);
   GroupEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (if trace[7] then 'calling current.FragmentDeleted'->putLine if);
            (false,ff[])->current.FragmentDeleted
        if)
     #)  

-- fragmentGroupHandlerGroupSaved: DoPart --
do
   (if trace[7] then 'fragmentGroupHandlerGroupSaved'->putLine if);
   GroupEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (if trace[7] then 'calling current.GroupSaved'->putLine if);
            false->current.GroupSaved
        if)
     #)  

