ORIGIN '../ymersiflib';
-- performMenuIsProgram: DoPart --
do
   loop: fg.fragmentlist.scan
     (# 
     do
        (if (current.type = mps.ast.formType) and
        ('program'->current.name.equalNCS) then
            true->value; leave loop; 
        if)
     #)  

-- performMenuAction: DoPart --
do
   stop;
   (if not jobFileRunning then 'Linking finished'->writeInfoView; stop if)  

-- performMenuCheckOnStatus: DoPart --
do
   allowPerform and not edenv.textediting->value;
   (if value then
       'Check '''->newName[];
       browser.cge.fgTitle->newName.append;
       ''''->newName.append;
       newName[]->name;
       (if lastCheckedName[] <> none then
           (if browser.cge.fgTitle->lastCheckedName.equal then
               'Check'->name; false->value
           if)
       if)
   if)  

-- performMenuCheckOnSelect: DoPart --
do
   (if not ('untitled'->(browser.cge.fgTitle).equal) then
       browser.cge.fg[]->lastCheckedFg[];
       browser.cge.fgTitle->lastCheckedName[];
       browser.cge.fg[]->check
    else
       'Rename ''untitled'' before checking'->writeInfoView;
       browser.cge.saveAs->newName[];
       (if newName[] <> none then
           browser.currentGroup.close; newName[]->browser.selectFragmentGroup
       if)
   if)  

-- performMenuReCheckOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastCheckedFg[] <> none )->value;
       (if value then
           (if lastCheckedName[] <> none then
               'Check '''->newName[];
               lastCheckedName[]->newName.append;
               ''''->newName.append;
               newName[]->name
           if)
       if)
    else
       false->value
   if)  

-- performMenuReCheckOnSelect: DoPart --
do lastCheckedFg[]->check  

-- performMenuCompileOnStatus: DoPart --
do
   allowPerform and not edenv.textediting->value;
   (if value then
       'Compile '''->newName[];
       browser.cge.fgTitle->newName.append;
       ''''->newName.append;
       newName[]->name;
       (if lastCompiledName[] <> none then
           (if browser.cge.fgTitle->lastCompiledName.equal then
               'Compile'->name; false->value
           if)
       if)
   if)  

-- performMenuCompileOnSelect: DoPart --
do
   (if not ('untitled'->(browser.cge.fgTitle).equal) then
       browser.cge.fg[]->lastCompiledFg[];
       browser.cge.fgTitle->lastCompiledName[];
       (if browser.cge.fg[]->isRunning then browser.cge.fg[]->kill if);
       (if browser.cge.fg[]->compile then
           (if browser.cge.fg[]->isProgram then
               'Linking...'->writeInfoView; 1000->jobFileTimer.start
            else
               'Compilation finished'->writeInfoView
           if)
       if)
    else
       'Rename ''untitled'' before compiling'->writeInfoView;
       browser.cge.saveAs->newName[];
       (if newName[] <> none then
           browser.currentGroup.close; newName[]->browser.selectFragmentGroup
       if)
   if)  

-- performMenuRecompileOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastCompiledFg[] <> none )->value;
       (if value then
           (if lastCompiledName[] <> none then
               'Compile '''->newName[];
               lastCompiledName[]->newName.append;
               ''''->newName.append;
               newName[]->name
           if)
       if)
    else
       false->value
   if)  

-- performMenuRecompileOnSelect: DoPart --
do
   (if lastCompiledFg[]->isRunning then lastCompiledFg[]->kill if);
   (if lastCompiledFg[]->compile then
       'Linking...'->writeInfoView; 1000->jobFileTimer.start
   if)  

-- performMenuExecutableExists: DoPart --
do
   (if fg[] <> none then
       fg.fullName->executable[];
       (if true
        // 'nti_ms'->(machine_type).equal // 'nti_gnu'->(machine_type).equal
        then
           '.exe'->executable.append
       if);
       executable[]->f.name;
       (if f.entry.exists and f.entry.writeable then true->value; INNER if)
   if)  

-- performMenuExecute: DoPart --
do
   'Starting execution of '->msg[];
   executable[]->msg.append;
   '...'->msg.append;
   msg[]->writeInfoView;
   executable[]->executeProgram;
   ' done'->msg.append;
   msg[]->writeInfoView  

-- performMenuIsRunning: DoPart --
do (executable[]->isProgramRunning)->value; INNER  

-- performMenuKill: DoPart --
do executable[]->killProgram  

-- performMenuDebuggerExists: DoPart --
do
   '$(BETALIB)'->expandEnvVar->betalib[];
   betalib[]->debugger[];
   (if true
    // 'nti_ms'->(machine_type).equal // 'nti_gnu'->(machine_type).equal then
       '/bin/'->debugger.append;
       machine_type->debugger.append;
       '/valhalla'->debugger.append;
       '.exe'->debugger.append
    else
       '/bin/valhalla'->debugger.append
   if);
   debugger[]->f.name;
   (if f.entry.exists then true->value; INNER if)  

(*-- performMenuDebug: DoPart --
do
   fg[]
     ->executableExists
       (# 
       do
          'Starting debugger on '->msg[];
          executable[]->msg.append;
          msg[]->writeInfoView;
          (debugger[],executable[])->debugProgram;
          ' done'->msg.append;
          msg[]->writeInfoView
       #)  
 *)
-- performMenuExecuteOnStatus: DoPart --
do
   (if allowPerform then
       browser.cge.fg[]->executableExists->value;
       (if value then
           'Execute '''->newName[];
           browser.cge.fgTitle->newName.append;
           ''''->newName.append;
           newName[]->name;
           (if lastExecutedName[] <> none then
               (if browser.cge.fgTitle->lastExecutedName.equal then
                   'Execute'->name; false->value
               if)
           if)
       if)
    else
       false->value
   if)  

-- performMenuExecuteOnSelect: DoPart --
do
   (if browser.cge.fg[]->executableExists then
       (if browser.cge.fg[]->isRunning then browser.cge.fg[]->kill if);
       browser.cge.fg[]->execute;
       browser.cge.fg[]->lastExecutedFg[];
       browser.cge.fgTitle->lastExecutedName[]
   if)  

-- performMenuReExecuteOnStatus: DoPart --
do
   (if allowPerform then
       (lastExecutedFg[] <> none )->value;
       (if value then
           (if lastExecutedName[] <> none then
               'Execute '''->newName[];
               lastExecutedName[]->newName.append;
               ''''->newName.append;
               newName[]->name
           if)
       if)
    else
       false->value
   if)  

-- performMenuReExecuteOnSelect: DoPart --
do
     (# doExecute: (#  do lastExecutedFg[]->execute #)
     do
        (if not (lastExecutedFg[]->isRunning) then
            doExecute
         else
            'Kill '->msg[];
            lastExecutedName[]->msg.putText;
            '?'->msg.put;
            (none ,'Kill program',msg[])
              ->promptForBoolean
                (#
                   ok::<  (#  do lastExecutedFg[]->kill; doExecute #);
                   notOK::<  (#  do doExecute #);
                   cancel::<  (#  do  #)
                #)
        if)
     #)  

-- performMenuCompileAndExecuteOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       false->value;
       loop: browser.cge.fg.fragmentlist.scan
         (# 
         do
            (if (current.type = mps.ast.formType) and
            ('program'->current.name.equalNCS) then
                true->value; leave loop; 
            if)
         #);
       'Compile and execute '''->newName[];
       browser.cge.fgTitle->newName.append;
       ''''->newName.append;
       newName[]->name;
       (if value then
           (if lastCompiledName[] <> none then
               (if browser.cge.fgTitle->lastCompiledName.equal then
                   'Compile and execute'->name; false->value
               if)
           if)
       if)
    else
       false->value
   if)  

-- performMenuCompileAndExecuteOnSelect: DoPart --
do
   (if not ('untitled'->(browser.cge.fgTitle).equal) then
       browser.cge.fg[]->lastCompiledFg[];
       browser.cge.fgTitle->lastCompiledName[];
       (if browser.cge.fg[]->isRunning then browser.cge.fg[]->kill if);
       (if browser.cge.fg[]->compile then
           'Linking...'->writeInfoView;
           waitOnJobFile;
           'Linking finished'->writeInfoView;
           (if browser.cge.fg[]->executableExists then
               browser.cge.fg[]->execute;
               browser.cge.fg[]->lastExecutedFg[];
               browser.cge.fgTitle->lastExecutedName[]
           if)
       if)
    else
       'Rename ''untitled'' before compiling'->writeInfoView;
       browser.cge.saveAs->newName[];
       (if newName[] <> none then
           browser.currentGroup.close; newName[]->browser.selectFragmentGroup
       if)
   if)  

-- performMenuRecompileAndExecuteOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastCompiledFg[] <> none )->value;
       (if value then
           (if lastCompiledName[] <> none then
               'Compile and execute '''->newName[];
               lastCompiledName[]->newName.append;
               ''''->newName.append;
               newName[]->name
            else
               false->value
           if)
       if)
    else
       false->value
   if)  

-- performMenuRecompileAndExecuteOnSelect: DoPart --
do
     (#
        doCompile:
          (# 
          do
             (if lastCompiledFg[]->compile then
                 'Linking...'->writeInfoView;
                 waitOnJobFile;
                 'Linking finished'->writeInfoView;
                 (if lastCompiledFg[]->executableExists then
                     lastCompiledFg[]->execute;
                     lastCompiledFg[]->lastExecutedFg[];
                     lastCompiledName.copy->lastExecutedName[]
                 if)
             if)
          #);
        msg: ^text
     do
        (if not (lastCompiledFg[]->isRunning) then
            doCompile
         else
            'Kill '->msg[];
            lastCompiledName[]->msg.putText;
            '?'->msg.put;
            (none ,'Kill program',msg[])
              ->promptForBoolean
                (#
                   ok::<  (#  do lastCompiledFg[]->kill; doCompile #);
                   notOK::<  (#  do doCompile #);
                   cancel::<  (#  do  #)
                #)
        if)
     #)  

-- performMenuKillOnStatus: DoPart --
do
   (if allowPerform then
       browser.cge.fg[]->executableExists->value;
       (if value and (browser.cge.fg[]->isRunning) then
           'Kill '''->newName[];
           browser.cge.fgTitle->newName.append;
           ''''->newName.append;
           newName[]->name
        else
           false->value
       if)
    else
       false->value
   if)  

-- performMenuKillOnSelect: DoPart --
do (if browser.cge.fg[]->isRunning then browser.cge.fg[]->kill if)  

-- performMenuSemanticErrorsOnStatus: DoPart --
do allowPerform->value  

-- performMenuSemanticErrorsOnSelect: DoPart --
do browser.cge.fg[]->showSemanticErrors  

-- performMenuDebugOnStatus: DoPart --
do
   (if allowPerform then
       ((browser.cge.fg[]->executableExists) (* and debuggerExists *))->value;
       (if value then
           'Debug '''->newName[];
           browser.cge.fgTitle->newName.append;
           ''''->newName.append;
           newName[]->name
       if)
    else
       false->value
   if)  

-- performMenuDebugOnSelect: DoPart --
do (if browser.cge.fg[]->executableExists then browser.cge.fg[]->debug if)  

