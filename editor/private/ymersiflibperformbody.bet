ORIGIN '../ymersiflib';
(*-- compilerMenuOpen: Descriptor --
(#  do 'Compile/Run'->GroupPerform.new #)  
 *)
-- compilerPerformMenu: Descriptor --
(# 
do
   ('Check Current',gui.specKeys.F6,false,false,false)->iCheck.newSpecKey;
   ('Recheck','h')->iReCheck.newKey;
   ('Compile Current',gui.specKeys.F5,false,false,false)->iCompile.newSpecKey;
   ('Recompile',gui.specKeys.F5,false,false,true)->iRecompile.newSpecKey;
   ('Run Current',gui.specKeys.F5,false,true,false)->iExecute.newSpecKey;
   'Rerun'->iReExecute.new;
   ('Compile and Run',gui.specKeys.F5,true,false,false)->iCompileAndExecute.newSpecKey;
   ('Recompile and Run','g')->iRecompileAndExecute.newKey;
   'Quit Current'->iKill.new;
   newSeparator;
   'Semantic Errors...'->iSemanticErrors.new;
   ('Next Semantic Error','e')->iNextSemanticError.newKey;
   newSeparator;
   ('Debug',gui.specKeys.F7,false,false,false)->iDebug.newSpecKey;
   ('Redebug',gui.specKeys.F7,true,false,false)->iReDebug.newSpecKey;
   
#)  

-- performMenuIsProgram: DoPart --
do
   (if (fg[] <> none ) and (fg.fragmentList[] <> none ) then
       loop: fg.fragmentlist.scan
         (# 
         do
            (if (current.type = mps.ast.formType) and
            ('program'->current.name.equalNCS) then
                true->value; leave loop; 
            if)
         #)
    else
       'isProgram: fg[] or fg.fragmentList[] is none!!'->putLine
   if)  

-- performMenuAction: DoPart --
do
     (# txt: ^text
     do
        (if not jobFileRunning then
            'Object program on file: '->txt[];
            lastCompiledFg.name->txt.append;
            txt[]->writeInfoView;
            '-'->putLine;
            waitOnJobFile;
            stop
        if)
     #)  

-- performMenuCheckOnStatus: DoPart --
do
   allowPerform and not edenv.textediting->value;
   (if value then
       'Check \''->newName[];
       ymerBrowserInt.cge.fgTitle->newName.append;
       '\''->newName.append;
       newName[]->name;
       (if lastCheckedName[] <> none then
           (if ymerBrowserInt.cge.fgTitle->lastCheckedName.equal then
               'Check'->name; (* false->value *)
           if)
       if)
   if)  

-- performMenuSaveAsAndCheck: DoPart --
do
     (# OK: @boolean; newName: ^text
     do
            true->OK;
            ymerBrowserInt.cge.saveAs->newName[];
            (if newName[] <> none then
                ymerBrowserInt.currentGroup.close;
                newName[]->ymerbrowserInt.selectFragmentGroup
             else
                false->OK
            if);
        (if OK and (ymerBrowserInt.cge[] <> none ) then
            ymerBrowserInt.cge.fg[]->lastCheckedFg[];
            ymerBrowserInt.cge.fgTitle->lastCheckedName[];
            ymerBrowserInt.cge.fg[]->check->status
        if)
     #)  

-- performMenuCheckOnSelect: DoPart --
do
     (# OK: @boolean
     do
        true->OK;
        loop:
        (if ymerBrowserInt.cge.isReadOnly then
            'Is readonly, make copy before checking'->writeInfoView;
            ymerBrowserInt.cge.saveAs->newName[];
            (if newName[] <> none then
                ymerBrowserInt.currentGroup.close;
                newName[]->ymerbrowserInt.selectFragmentGroup
             else
                false->OK
            if)
        if);
        (if OK and (ymerBrowserInt.cge[] <> none ) then
            ymerBrowserInt.cge.fg[]->lastCheckedFg[];
            ymerBrowserInt.cge.fgTitle->lastCheckedName[];
            ymerBrowserInt.cge.fg[]->check
        if)
     #)  

-- performMenuReCheckOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastCheckedFg[] <> none )->value;
       (if value then
           (if lastCheckedName[] <> none then
               'Check \''->newName[];
               lastCheckedName[]->newName.append;
               '\''->newName.append;
               newName[]->name
           if)
       if)
    else
       false->value
   if)  

-- performMenuReCheckOnSelect: DoPart --
do lastCheckedFg[]->check  

-- performMenuCompileOnStatus: DoPart --
do
   allowPerform and not edenv.textediting->value;
   (if value then
       'Compile \''->newName[];
       ymerBrowserInt.cge.fgTitle->newName.append;
       '\''->newName.append;
       newName[]->name;
       (if lastCompiledName[] <> none then
           (if ymerBrowserInt.cge.fgTitle->lastCompiledName.equal then
               'Compile'->name; false->value
           if)
       if)
   if)  

-- performMenuCompileOnSelect: DoPart --
do
     (# OK: @boolean
     do
        true->OK;
        loop:
        (if ymerBrowserInt.cge.isReadOnly then
            'Is readonly, make copy before compiling'->writeInfoView;
            ymerBrowserInt.cge.saveAs->newName[];
            (if newName[] <> none then
                ymerBrowserInt.currentGroup.close;
                newName[]->ymerbrowserInt.selectFragmentGroup
             else
                false->OK
            if)
        if);
        (if OK and (ymerBrowserInt.cge[] <> none ) then
            ymerBrowserInt.cge.fg[]->lastCompiledFg[];
            (ymerBrowserInt.cge.fgTitle).copy->lastCompiledName[];
            (if ymerBrowserInt.cge.fg[]->isRunning then
                ymerBrowserInt.cge.fg[]->kill
            if);
            (if ymerBrowserInt.cge.fg[]->compile then
                (if ymerBrowserInt.cge.fg[]->isProgram then
                    'Linking...'->writeInfoView; 1000->jobFileTimer.start
                 else
                    'Compilation finished'->writeInfoView
                if)
            if)
        if)
     #)  

-- performMenuRecompileOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastCompiledFg[] <> none )->value;
       (if value then
           (if lastCompiledName[] <> none then
               'Compile \''->newName[];
               lastCompiledName[]->newName.append;
               '\''->newName.append;
               newName[]->name
           if)
       if)
    else
       false->value
   if)  

-- performMenuRecompileOnSelect: DoPart --
do
   (if lastCompiledFg[]->isRunning then lastCompiledFg[]->kill if);
   (if lastCompiledFg[]->compile then
       'Linking...'->writeInfoView; 1000->jobFileTimer.start
   if)  

-- performMenuExecutableExists: DoPart --
do
   (if fg[] <> none then
       fg.fullName->executable[];
       (if true
        // 'nti_ms'->(machine_type).equal // 'nti_gnu'->(machine_type).equal
        then
           '.exe'->executable.append
       if);
       executable[]->f.name;
       (if f.entry.exists and f.entry.writeable then true->value; INNER if)
   if)  

-- performMenuExecute: DoPart --
do
   'Starting '->msg[];
   executable[]->msg.append;
   '...'->msg.append;
   msg[]->putText;
   msg[]->writeInfoView;
   executable[]->executeProgram;
   ' done'->putLine;
   ' done'->msg.append;
   msg[]->writeInfoView  

-- performMenuIsRunning: DoPart --
do (executable[]->isProgramRunning)->value; INNER  

-- performMenuKill: DoPart --
do executable[]->killProgram  

-- performMenuExecuteOnStatus: DoPart --
do
   (if allowPerform then
       ymerBrowserInt.cge.fg[]->executableExists->value;
       (if value then
           'Run \''->newName[];
           ymerBrowserInt.cge.fgTitle->newName.append;
           '\''->newName.append;
           newName[]->name;
           (if lastExecutedName[] <> none then
               (if ymerBrowserInt.cge.fgTitle->lastExecutedName.equal then
                   'Run'->name; false->value
               if)
           if)
       if)
    else
       false->value
   if)  

-- performMenuExecuteOnSelect: DoPart --
do
   (if ymerBrowserInt.cge.fg[]->executableExists then
       (if ymerBrowserInt.cge.fg[]->isRunning then
           ymerBrowserInt.cge.fg[]->kill
       if);
       ymerBrowserInt.cge.fg[]->execute;
       ymerBrowserInt.cge.fg[]->lastExecutedFg[];
       ymerBrowserInt.cge.fgTitle->lastExecutedName[]
   if)  

-- performMenuReExecuteOnStatus: DoPart --
do
   (if allowPerform then
       (lastExecutedFg[] <> none )->value;
       (if value then
           (if lastExecutedName[] <> none then
               'Run \''->newName[];
               lastExecutedName[]->newName.append;
               '\''->newName.append;
               newName[]->name
           if)
       if)
    else
       false->value
   if)  

-- performMenuReExecuteOnSelect: DoPart --
do
     (# doExecute: (#  do lastExecutedFg[]->execute #)
     do
        (if not (lastExecutedFg[]->isRunning) then
            doExecute
         else
            lastExecutedName.copy->msg[];
            ' is still running, quit?'->msg.append;
            (none ,'Quit program',msg[])
              ->gui.promptForBoolean
                (#
                   ok::<  (#  do lastExecutedFg[]->kill; doExecute #);
                   notOK::<  (#  do doExecute #);
                   cancel::<  (#  do  #)
                #)
        if)
     #)  

-- performMenuCompileAndExecuteOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       'Compile and Run \''->newName[];
       ymerBrowserInt.cge.fgTitle->newName.append;
       '\''->newName.append;
       newName[]->name;
       (if ymerBrowserInt.cge.fg[]->isProgram->value then
           (if lastCompiledName[] <> none then
               (if ymerBrowserInt.cge.fgTitle->lastCompiledName.equal then
                   'Compile and Run'->name; false->value
               if)
           if)
       if)
    else
       false->value
   if)  

-- performMenuCompileAndExecuteOnSelect: DoPart --
do
     (# OK: @boolean
     do
        true->OK;
        loop:
        (if ymerBrowserInt.cge.isReadOnly then
            'Is readonly, make copy before compiling'->writeInfoView;
            ymerBrowserInt.cge.saveAs->newName[];
            (if newName[] <> none then
                ymerBrowserInt.currentGroup.close;
                newName[]->ymerBrowserInt.selectFragmentGroup
             else
                false->OK
            if)
        if);
        (if OK and (ymerBrowserInt.cge[] <> none ) then
            ymerBrowserInt.cge.fg[]->lastCompiledFg[];
            (ymerBrowserInt.cge.fgTitle).copy->lastCompiledName[];
            (if ymerBrowserInt.cge.fg[]->isRunning then
                ymerBrowserInt.cge.fg[]->kill
            if);
            (if ymerBrowserInt.cge.fg[]->compile then
                'Linking...'->writeInfoView;
                waitOnJobFile;
                'Linking finished'->writeInfoView;
                (if ymerBrowserInt.cge.fg[]->executableExists then
                    ymerBrowserInt.cge.fg[]->execute;
                    ymerBrowserInt.cge.fg[]->lastExecutedFg[];
                    ymerBrowserInt.cge.fgTitle->lastExecutedName[]
                if)
            if)
        if)
     #)  

-- performMenuRecompileAndExecuteOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastCompiledFg[] <> none )->value;
       (if value then
           (if lastCompiledName[] <> none then
               'Compile and Run \''->newName[];
               lastCompiledName[]->newName.append;
               '\''->newName.append;
               newName[]->name
            else
               false->value
           if)
       if)
    else
       false->value
   if)  

-- performMenuRecompileAndExecuteOnSelect: DoPart --
do
     (#
        doCompile:
          (# 
          do
             (if lastCompiledFg[]->compile then
                 'Linking...'->writeInfoView;
                 waitOnJobFile;
                 'Linking finished'->writeInfoView;
                 (if lastCompiledFg[]->executableExists then
                     lastCompiledFg[]->execute;
                     lastCompiledFg[]->lastExecutedFg[];
                     lastCompiledName.copy->lastExecutedName[]
                 if)
             if)
          #);
        msg: ^text
     do
        (if not (lastCompiledFg[]->isRunning) then
            doCompile
         else
            lastCompiledName.copy->msg[];
            ' is still running, quit?'->msg.append;
            (none ,'Quit program',msg[])
              ->gui.promptForBoolean
                (#
                   ok::< 
                     (# 
                     do lastCompiledFg[]->kill; hide; (*close; *) doCompile
                     #);
                   notOK::<  (#  do hide; (*  close; *) doCompile #);
                   cancel::<  (#  do  #)
                #)
        if)
     #)  

-- performMenuKillOnStatus: DoPart --
do
   (if allowPerform then
       ymerBrowserInt.cge.fg[]->executableExists->value;
       (if value and (ymerBrowserInt.cge.fg[]->isRunning) then
           'Quit \''->newName[];
           ymerBrowserInt.cge.fgTitle->newName.append;
           '\''->newName.append;
           newName[]->name
        else
           false->value
       if)
    else
       false->value
   if)  

-- performMenuKillOnSelect: DoPart --
do
   (if ymerBrowserInt.cge.fg[]->isRunning then
       ymerBrowserInt.cge.fg[]->kill
   if)  

-- performMenuSemanticErrorsOnStatus: DoPart --
do allowPerform->value  

-- performMenuSemanticErrorsOnSelect: DoPart --
do ymerBrowserInt.cge.fg[]->showSemanticErrors  

-- debugPossible: DoPart --
do
   'Debug'->newName[];
   (if allowPerform then
       (ymerBrowserInt.cge.fg[]->executableExists)->value;
       (if value then
           'Debug \''->newName[];
           ymerBrowserInt.cge.fgTitle->newName.append;
           '\''->newName.append
       if)
    else
       false->value
   if);
   (# t:^text;
   do machine_type->t[];
      (if 'hpux9pa'->t.equalncs then
          false->value
      if)
   #)

-- performMenuReDebugOnStatus: DoPart --
do
   (if allowPerform then
       (lastDebuggedFg[] <> none )->value;
       (if value then
           (if (lastDebuggedfg.name) <> none then
               'Debug \''->newName[];
               lastDebuggedfg.name->newName.append;
               '\''->newName.append;
               newName[]->name;
               
           if)
       if)
    else
       false->value
   if)  

