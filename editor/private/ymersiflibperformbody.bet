ORIGIN '../ymersiflib';
LIB_ITEM 'editorymersiflib';
(*
 * COPYRIGHT
 *	 Copyright Mjolner Informatics, 1995-2000
 *	 All rights reserved.
 *)
-- compilerPerformMenu: Descriptor --
(# 
do
   ('Check Current',gui.specKeys.F4,false,false,false)->iCheck.newSpecKey;
   ('Check Program',gui.specKeys.F4,false,true,false)->iReCheck.newSpecKey;
   ('Compile Current',gui.specKeys.F7,false,false,false)->iCompile.newSpecKey;
   ('Recompile',gui.specKeys.F7,false,true,false)->iRecompile.newSpecKey;
   ('Compile and Run Program',gui.specKeys.F7,true,false,false)
     ->iRecompileAndExecute.newSpecKey;
   ('Run Program',gui.specKeys.F8,false,false,false)->iReExecute.newSpecKey;
   'Quit Current'->iKill.new;
   newSeparator;
   'Semantic Errors and Warnings...'->iSemanticErrors.new;
   'Next Semantic Error'->iNextSemanticError.new;
   newSeparator;
   ('Debug Program',gui.specKeys.F9,false,false,false)->iReDebug.newSpecKey;
   ('Debug Executable...',gui.specKeys.F9,false,true,false)->iDebugAs.newSpecKey
#)  

-- performMenuIsProgram: DoPart --
do
   (if (fg[] <> none ) and (fg.fragmentList[] <> none ) then
       loop: fg.fragmentlist.scan
         (# 
         do
            (if (current.type = mps.ast.formType) and
            ('program'->current.name.equalNCS) then
                true->value; leave loop; 
            if)
         #)
    else
       'isProgram: fg[] or fg.fragmentList[] is none!!'->putLine
   if)  

-- performMenuAction: DoPart --
do
     (# txt: ^text
     do
        (if not jobFileRunning then
            'Object program on file: '->txt[];
            lastProgramFG.name->txt.append;
            txt[]->writeInfoView;
            '-'->putLine;
            waitOnJobFile;
            stop
        if)
     #)  

-- performMenuCheckOnStatus: DoPart --
do
   allowPerform and not edenv.textediting->value;
   (if value then
       'Check Current ('->newName[];
       ymerBrowserInt.cge.fgTitle->newName.append;
       ')'->newName.append;
       newName[]->name;
       (* (if lastCheckedName[] <> none then
        (if ymerBrowserInt.cge.fgTitle->lastCheckedName.equal then
        'Check Current'->name
        if)
        if)*)
       
   if)  

-- performMenuSaveAsAndCheck: DoPart --
do
     (# OK: @boolean; newName: ^text
     do
        true->OK;
        ymerBrowserInt.cge.saveAs->newName[];
        (if newName[] <> none then
            newName[]->ymerbrowserInt.selectFragmentGroup
         else
            false->OK
        if);
        (if OK and (ymerBrowserInt.cge[] <> none ) then
            ymerBrowserInt.cge.fg[]->lastProgramFG[];
            ymerBrowserInt.cge.fgTitle->lastProgramName[];
            ymerBrowserInt.cge.fg[]->check->status
        if)
     #)  

-- performMenuCheckOnSelect: DoPart --
do
     (# OK: @boolean
     do
        true->OK;
        loop:
        (if ymerBrowserInt.cge.isReadOnly then
            'Is readonly, make copy before checking'->writeInfoView;
            ymerBrowserInt.cge.saveAs->newName[];
            (if newName[] <> none then
                newName[]->ymerbrowserInt.selectFragmentGroup
             else
                false->OK
            if)
        if);
        (if OK and (ymerBrowserInt.cge[] <> none ) then
            (if ymerBrowserInt.cge.fg[]->isProgram then
                ymerBrowserInt.cge.fg[]->lastProgramFG[];
                ymerBrowserInt.cge.fgTitle->lastProgramName[]
            if);
            ymerBrowserInt.cge.fg[]->check
        if)
     #)  

-- performMenuReCheckOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastProgramFG[] <> none )->value;
       (if value then
           (if lastProgramName[] <> none then
               'Check Program ('->newName[];
               lastProgramName[]->newName.append;
               ')'->newName.append;
               newName[]->name
           if)
        else
           (if ymerBrowserInt.cge.fg[]->isProgram then
               'Check Program ('->newName[];
               ymerBrowserInt.cge.fgTitle->newName.append;
               ')'->newName.append;
               newName[]->name;
               true->value
           if)
       if)
    else
       false->value
   if)  

-- performMenuReCheckOnSelect: DoPart --
do lastProgramFG[]->check  

-- performMenuCompileOnStatus: DoPart --
do
   allowPerform and not edenv.textediting->value;
   (if value then
       'Compile Current ('->newName[];
       ymerBrowserInt.cge.fgTitle->newName.append;
       ')'->newName.append;
       newName[]->name
   if)  

-- performMenuCompileOnSelect: DoPart --
do
     (# OK: @boolean
     do
        true->OK;
        loop:
        (if ymerBrowserInt.cge.isReadOnly then
            'Is readonly, make copy before compiling'->writeInfoView;
            ymerBrowserInt.cge.saveAs->newName[];
            (if newName[] <> none then
                newName[]->ymerbrowserInt.selectFragmentGroup
             else
                false->OK
            if)
        if);
        (if OK and (ymerBrowserInt.cge[] <> none ) then
            (if ymerBrowserInt.cge.fg[]->isRunning then
                ymerBrowserInt.cge.fg[]->kill
            if);
            (if ymerBrowserInt.cge.fg[]->compile then
                (if ymerBrowserInt.cge.fg[]->isProgram then
                    ymerBrowserInt.cge.fg[]->lastProgramFG[];
                    (ymerBrowserInt.cge.fgTitle).copy->lastProgramName[];
                    'Linking...'->writeInfoView;
                    1000->jobFileTimer.start
                 else
                    'Compilation finished'->writeInfoView
                if)
            if)
        if)
     #)  

-- performMenuRecompileOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastProgramFG[] <> none )->value;
       (if value then
           (if lastProgramName[] <> none then
               'Compile Program ('->newName[];
               lastProgramName[]->newName.append;
               ')'->newName.append;
               newName[]->name
           if)
        else
           (if ymerBrowserInt.cge.fg[]->isProgram then
               'Compile Program ('->newName[];
               ymerBrowserInt.cge.fgTitle->newName.append;
               ')'->newName.append;
               newName[]->name;
               true->value
           if)
       if)
    else
       false->value
   if)  

-- performMenuRecompileOnSelect: DoPart --
do
   (if lastProgramFG[]->isRunning then lastProgramFG[]->kill if);
   (if lastProgramFG[]->compile then
       'Linking...'->writeInfoView; 1000->jobFileTimer.start
   if)  

-- performMenuExecutableExists: DoPart --
do
   (if fg[] <> none then
       fg.fullName->executable[];
       (if true
        // 'nti_ms'->(machine_type).equal // 'nti_gnu'->(machine_type).equal
        then
           '.exe'->executable.append
       if);
       executable[]->f.name;
       (if f.entry.exists and f.entry.writeable then true->value; INNER if)
   if)  

-- performMenuExecute: DoPart --
do
   'Starting '->msg[];
   executable[]->msg.append;
   '...'->msg.append;
   msg[]->putText;
   msg[]->writeInfoView;
   executable[]->executeProgram;
   ' done'->putLine;
   ' done'->msg.append;
   msg[]->writeInfoView  

-- performMenuIsRunning: DoPart --
do (executable[]->isProgramRunning)->value; INNER  

-- performMenuKill: DoPart --
do executable[]->killProgram  

-- performMenuExecuteOnStatus: DoPart --
do
   (if allowPerform then
       ymerBrowserInt.cge.fg[]->executableExists->value;
       (if value then
           'Run ('->newName[];
           ymerBrowserInt.cge.fgTitle->newName.append;
           ')'->newName.append;
           newName[]->name
        else
           (if (lastProgramFG[] <> none ) and
           (lastProgramFG[]->executableExists) then
               'Run ('->newName[];
               lastProgramName[]->newName.append;
               ')'->newName.append;
               newName[]->name
           if)
       if)
    else
       false->value
   if)  

-- performMenuExecuteOnSelect: DoPart --
do
   (if ymerBrowserInt.cge.fg[]->executableExists then
       (if ymerBrowserInt.cge.fg[]->isRunning then
           ymerBrowserInt.cge.fg[]->kill
       if);
       ymerBrowserInt.cge.fg[]->execute;
       ymerBrowserInt.cge.fg[]->lastProgramFG[];
       ymerBrowserInt.cge.fgTitle->lastProgramName[]
   if)  

-- performMenuReExecuteOnStatus: DoPart --
do
   (if allowPerform then
       (lastProgramFG[]->executableExists)->value;
       (if value then
           (if lastProgramName[] <> none then
               'Run Program ('->newName[];
               lastProgramName[]->newName.append;
               ')'->newName.append;
               newName[]->name
           if)
        else
           (if (ymerBrowserInt.cge.fg[]->isProgram) and
           (ymerBrowserInt.cge.fg[]->executableExists) then
               'Run Program ('->newName[];
               ymerBrowserInt.cge.fgTitle->newName.append;
               ')'->newName.append;
               newName[]->name;
               true->value
           if)
       if)
    else
       false->value
   if)  

-- performMenuReExecuteOnSelect: DoPart --
do
   (if lastProgramFG[] = none then
       ymerBrowserInt.cge.fg[]->lastProgramFG[];
       ymerBrowserInt.cge.fgtitle->lastProgramName[]
   if);
     (# doExecute: (#  do lastProgramFG[]->execute #)
     do
        (if not (lastProgramFG[]->isRunning) then
            doExecute
         else
            lastProgramName.copy->msg[];
            ' is still running, quit?'->msg.append;
            (none ,'Quit program',msg[])
              ->gui.promptForBoolean
                (#
                   ok::<  (#  do lastProgramFG[]->kill; doExecute #);
                   notOK::<  (#  do doExecute #);
                   cancel::<  (#  do  #)
                #)
        if)
     #)  

-- performMenuCompileAndExecuteOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       'Compile and Run \''->newName[];
       ymerBrowserInt.cge.fgTitle->newName.append;
       '\''->newName.append;
       newName[]->name;
       (if ymerBrowserInt.cge.fg[]->isProgram->value then
           (if lastProgramName[] <> none then
               (if ymerBrowserInt.cge.fgTitle->lastProgramName.equal then
                   'Compile and Run'->name; false->value
               if)
           if)
       if)
    else
       false->value
   if)  

-- performMenuCompileAndExecuteOnSelect: DoPart --
do
     (# OK: @boolean
     do
        true->OK;
        loop:
        (if ymerBrowserInt.cge.isReadOnly then
            'Is readonly, make copy before compiling'->writeInfoView;
            ymerBrowserInt.cge.saveAs->newName[];
            (if newName[] <> none then
                newName[]->ymerBrowserInt.selectFragmentGroup
             else
                false->OK
            if)
        if);
        (if OK and (ymerBrowserInt.cge[] <> none ) then
            ymerBrowserInt.cge.fg[]->lastProgramFG[];
            (ymerBrowserInt.cge.fgTitle).copy->lastProgramName[];
            (if ymerBrowserInt.cge.fg[]->isRunning then
                ymerBrowserInt.cge.fg[]->kill
            if);
            (if ymerBrowserInt.cge.fg[]->compile then
                'Linking...'->writeInfoView;
                waitOnJobFile;
                'Linking finished'->writeInfoView;
                (if ymerBrowserInt.cge.fg[]->executableExists then
                    ymerBrowserInt.cge.fg[]->execute;
                    ymerBrowserInt.cge.fg[]->lastProgramFG[];
                    ymerBrowserInt.cge.fgTitle->lastProgramName[]
                if)
            if)
        if)
     #)  

-- performMenuRecompileAndExecuteOnStatus: DoPart --
do
   (if allowPerform and not edenv.textediting then
       (lastProgramFG[] <> none )->value;
       (if value then
           (if lastProgramName[] <> none then
               'Compile and Run ('->newName[];
               lastProgramName[]->newName.append;
               ')'->newName.append;
               newName[]->name
            else
               false->value
           if)
        else
           (if ymerBrowserInt.cge.fg[]->isProgram then
               'Compile and Run ('->newName[];
               ymerBrowserInt.cge.fgTitle->newName.append;
               ')'->newName.append;
               newName[]->name;
               true->value
           if)
       if)
    else
       false->value
   if)  

-- performMenuRecompileAndExecuteOnSelect: DoPart --
do
     (#
        doCompile:
          (# 
          do
             (if lastProgramFG[]->compile then
                 'Linking...'->writeInfoView;
                 waitOnJobFile;
                 'Linking finished'->writeInfoView;
                 (if lastProgramFG[]->executableExists then
                     lastProgramFG[]->execute
                 if)
             if)
          #);
        msg: ^text
     do
        (if not (lastProgramFG[]->isRunning) then
            doCompile
         else
            lastProgramName.copy->msg[];
            ' is still running, quit?'->msg.append;
            (none ,'Quit program',msg[])
              ->gui.promptForBoolean
                (#
                   ok::< 
                     (# 
                     do lastProgramFG[]->kill; hide; (*close; *) doCompile
                     #);
                   notOK::<  (#  do hide; (*  close; *) doCompile #);
                   cancel::<  (#  do  #)
                #)
        if)
     #)  

-- performMenuKillOnStatus: DoPart --
do
   (if allowPerform then
       ymerBrowserInt.cge.fg[]->executableExists->value;
       (if value and (ymerBrowserInt.cge.fg[]->isRunning) then
           'Quit \''->newName[];
           ymerBrowserInt.cge.fgTitle->newName.append;
           '\''->newName.append;
           newName[]->name
        else
           false->value
       if)
    else
       false->value
   if)  

-- performMenuKillOnSelect: DoPart --
do
   (if ymerBrowserInt.cge.fg[]->isRunning then
       ymerBrowserInt.cge.fg[]->kill
   if)  

-- performMenuSemanticErrorsOnStatus: DoPart --
do allowPerform->value  

-- performMenuSemanticErrorsOnSelect: DoPart --
do ymerBrowserInt.cge.fg[]->showSemanticErrors  

-- debugPossible: DoPart --
do
   (if allowPerform then
       (fg[]->executableExists)->value; 
    else
       false->value
   if);
     (# t: ^text; 
     do machine_type->t[]; (if 'hpux9pa'->t.equalncs then false->value if)
     #)  

-- performMenuReDebugOnStatus: DoPart --
do
   (if allowPerform then
       ymerBrowserInt.cge.fg[]->debugPossible->value;
       (if value then
           'Debug Program ('->newName[];
           ymerBrowserInt.cge.fgTitle->newName.append;
           ')'->newName.append;
           newName[]->name;
           true->value
        else
           (lastProgramFG[] <> none )->value;
           (if value then lastProgramFG[]->debugPossible->value if);
           (if value then
               'Debug Program ('->newName[];
               lastProgramName[]->newName.append;
               ')'->newName.append;
               newName[]->name
           if)
       if)
    else
       false->value
   if)  

