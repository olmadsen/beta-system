ORIGIN 'editorenvbody';
INCLUDE  '~beta/win32lib/v1.0/ntiTypes'
MAKE default 'external/filedirmisc.make';
OBJFILE default '$/filedirmisc.obj';

INCLUDE '~beta/sysutils/v1.4.2/cstring';
INCLUDE '~beta/win32lib/v1.0/errorhandling';


-- setupCursors: Descriptor --
(#  do none ->rightButtonCursor[]; none ->textCursor[];  #)  

-- myRename: DoPart --
do   

-- lib: attributes --

FORMAT_MESSAGE_ALLOCATE_BUFFER: (# exit 16x00000100 #);
FORMAT_MESSAGE_FROM_SYSTEM: (# exit 16x00001000 #);

FormatMessage: external
  (# dwFlags: @integer;	     (* source and processing options *)
     lpSource: @integer;     (* pointer to  message source *)
     dwMessageId: @integer;  (* requested message identifier *)
     dwLanguageId: @integer; (* language identifier for requested message *)
     lpBuffer: @integer;     (* pointer to message buffer *)
     nSize: @integer;	     (* maximum size of message buffer *)
     Arguments: @integer;    (* address of array of message inserts *)
     result: @integer;
  enter (dwFlags,lpSource,dwMessageId,dwLanguageId,lpBuffer,nSize,Arguments)
  do 'FormatMessageA' -> callStd;
  exit result
  #);


MOVEFILE_REPLACE_EXISTING: (# exit 16x00000001 #);
MOVEFILE_COPY_ALLOWED: (# exit 16x00000002 #);
MOVEFILE_DELAY_UNTIL_REBOOT: (# exit 16x00000004 #);

myMAKELANGID: external
  (# a,b,c: @integer;
  enter (a,b)
  do 'myMAKELANGID' -> callC;
  exit c
  #);

LANG_NEUTRAL: (# exit 16x00 #);
SUBLANG_NEUTRAL: (# exit 16x00 #);
SUBLANG_DEFAULT: (# exit 16x01 #);


(* WINBASEAPI BOOL WINAPI MoveFileExA
 * (LPCSTR lpExistingFileName,
 * LPCSTR lpNewFileName,
 * DWORD dwFlags)
 *)
MoveFileEx: external
  (# lpExistingFileName: [1] @char;
     lpNewFileName: [1] @char;
     dwFlags: @integer;
     result: @integer;
  enter (lpExistingFileName,lpNewFileName,dwFlags)
  do 'MoveFileExA' -> callStd;
  exit result
  #);

renameWin32File_Dir:
  (# oldName: ^text;
     newName: ^text;
     result: @integer;
     flag: @integer;
     errorMsg: ^text;
     errorMsgCstr: ^cString;
     error:< exception
       (# do errorMsg[] -> msg.append; inner;  #);
     dwFlags: @integer;
     formatRes: @integer;
  enter (oldName[],newName[])
  do (oldName,newName,MOVEFILE_COPY_ALLOWED) -> MoveFileEx -> result; 
     (if result=0 then (* Rename failed. *)
         FORMAT_MESSAGE_FROM_SYSTEM -> dwFlags;
         &cString[] -> errorMsgCstr[];
         256 -> errorMsgCstr.init;
         (dwFlags,
         0,
         GetLastError,
         (LANG_NEUTRAL, SUBLANG_DEFAULT)->myMAKELANGID, 
         errorMsgCstr,
         256,
         0 ) -> FormatMessage-> formatRes;
         (if formatRes=0 then
             'FormatMessage failed. Error= ' -> screen.putText;
             GetLastError -> screen.putInt; screen.newline;
         if);
             
         errorMsgCstr.get -> errorMsg[];
         errorMsg.reset;
         errorMsgCstr.free;
         error;
     if);
  #)
