ORIGIN '../codeeditor';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- subeditorOpenSubeditor: DoPart --
do
     (#
        theSubwindow: ^subwindow;
        fe: ^codeeditor;
        subWindow: window
          (#
             editor: @sifTextEditor (# codeViewerType::< codeeditor #);
             sed: ^codeeditor;
             attach:
               (# fe: ^codeEditor
               enter fe[]
               do
                  fe[]->Edit.attach;
                  fe[]->view.attach;
                  (*                  (fe[],edenv[])->slots.attach*)
                  
               #);
             File: @mySimpleMenu
               (#
                  iClose: @item
                    (# onSelect::  (#  do theSubwindow.close #) #);
                  open::  (#  do ('Close','w')->iClose.newKey #)
               #);
             Edit: @codeEditorEditMenu;
             View: @codeEditorViewMenu;
             menubarType:: 
               (#
                  open:: 
                    (# 
                    do
                       File.new;
                       File[]->append;
                       Edit.new;
                       Edit[]->append;
                       View.new;
                       View[]->append;
                       
                    #)
               #);
             open:: 
               (#
                  ff: ^astInterface.fragmentForm;
                  theEditorRoot,node: ^astInterface.ast;
                  help: ^text
               enter (theEditorRoot[],node[])
               do
                  (if theEditorRoot[] <> none then
                      ((theEditorRoot.frag.father).name).copy
                        ->edenv.fragmentDefaultName->help[];
                      help.length->help.pos;
                      '-'->help.put;
                      theEditorRoot.frag.name->help.putText;
                      help[]->title;
                      THIS(subWindow).contents->editor.open;
                      (mps[],theGrammar[],theEditorRoot[],node[])
                        ->editor.newfragment->sed[];
                      (if sed[] <> none then
                          fe[]->sed.fatherEditor[];
                          fe.isReadOnly->sed.isReadOnly
                            ->sed.theSifTexteditor.isReadOnly;
                          sed[]->fe.subeditors.append;
                          sed[]->theSubWindow.attach;
                          
                       else
                          'no subeditor created!'->putLine
                      if);
                      editor.size->size;
                      true->editor.bindBottom->editor.bindRight
                  if);
                  
               #);
             
          #);
        synCatNo: @mps.nonterminalSymbol;
        
     do
        (if theEditorRoot[] = none then cs.node[]->theEditorRoot[] if);
        (if node[] = none then cs.node[]->node[] if);
        theEditorRoot[]->getSynCatNo->synCatNo;
        (if synCatNo.isLexem then (* cannot prettyprint a lexem *)
            theEditorRoot.father->theEditorRoot[]
        if);
        THIS(codeeditor)[]->fe[];
        &subwindow[]->theSubwindow[];
        (theEditorRoot[],node[])->theSubwindow.open
     #)  

-- subeditorCreateSubeditor: DoPart --
do
     (# fe: ^codeeditor; synCatNo: @mps.nonterminalSymbol; 
     do
        (if theEditorRoot[] = none then cs.node[]->theEditorRoot[] if);
        (if node[] = none then cs.node[]->node[] if);
        theEditorRoot[]->getSynCatNo->synCatNo;
        (if synCatNo.isLexem then (* cannot prettyprint a lexem *)
            theEditorRoot.father->theEditorRoot[]
        if);
        THIS(codeeditor)[]->fe[];
        (if theSifTextEditor[] <> none then
            &theSifTextEditor.codeviewerType[]->sed[];
            sed[]->theSifTexteditor.SifViewer[];
            theSifTextEditor[]->sed.theSifTextEditor[];
            theEditorRoot.frag[]->theSifTexteditor.frag[];
            theEditorRoot[]->theSifTexteditor.editorRoot[];
            (mps[],theEditorRoot.frag.grammar[],UI[],theSifTextEditor[],
             theEditorRoot.Frag[],theEditorRoot[],node[])->sed.init;
            fe[]->sed.fatherEditor[];
            fe.isReadOnly->sed.isReadOnly->sed.theSifTexteditor.isReadOnly;
            sed[]->subeditors.append;
            
         else
            'CreateSubEditor: theSifTexteditor is none!'->putLine
        if)
     #)  

