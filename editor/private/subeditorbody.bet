ORIGIN '../codeeditor';
-- subeditorOpenSubeditor: DoPart --
do
     (#
        theSubwindow: ^subwindow;
        fe: ^codeeditor;
        subWindow: window
          (#
             editor: @sifTextEditor (# codeViewerType::< subEditor #);
             open:: 
               (#
                  ff: ^mps.fragmentForm;
                  node: ^mps.ast;
                  help: ^text;
                  sed: ^subeditor
               enter node[]
               do
                  (if node[] <> none then
                      ((node.frag.father).name).copy->help[];
                      help.length->help.pos;
                      '-'->help.put;
                      node.frag.name->help.putText;
                      help[]->title;
                      help[]->putLine;
                      editor.open;
                      (mps[],theGrammar[],node[],node[])->editor.newfragment
                        ->sed[];
                      (if sed[] <> none then
                          fe[]->sed.fatherEditor[]; 
                          sed[]->fe.subeditors.append;
                       else
                          'no subeditor created!'->putLine
                      if);
                      editor.size->size;
                      true->editor.bindBottom->editor.bindRight
                  if);
                  
               #);
             
          #);
        
     do
        (if node[] = none then cs.node[]->node[] if);
        THIS(codeeditor)[]->fe[];
        (if subeditors.size > 0 then
            'sub editors are open'->putLine
         else
        (* prettyprint property list
         * and parse corresponding to the 
         * property grammar
         *)
            (if node[] <> none then
                &subwindow[]->theSubwindow[]; node[]->theSubwindow.open
             else
                'open subeditor: node is none'->putLine
            if)
        if)
     #)  

-- subeditorAstReplaced: DoPart --
do (oldAst[],newAst[])->fatherEditor.astView.pp.update  

-- subeditorAstReplacedSequence: DoPart --
do (rootOfSequence[],rootOfSequence[])->fatherEditor.astView.pp.update  

-- subeditorListElementInserted: DoPart --
do
   (fatherNode[],position,0,1)->fatherEditor.astView.pp.updateList;
   (if switch[7] then '(# fatherEditor.doReprettyPrint'->putLine;  if);
   (fatherEditor.root[],- 1)->fatherEditor.astView.pp.present  

-- subeditorListElementsDeleted: DoPart --
do
   (listNode[],position,length,0)->fatherEditor.astView.pp.updateList;
   (if switch[7] then '(# fatherEditor.doReprettyPrint'->putLine;  if);
   (fatherEditor.root[],- 1)->fatherEditor.astView.pp.present  

-- subeditorListElementsReplaced: DoPart --
do
   (listNode[],position,length,newLength)->fatherEditor.astView.pp.updateList;
   (if switch[7] then '(# fatherEditor.doReprettyPrint'->putLine;  if);
   (fatherEditor.root[],- 1)->fatherEditor.astView.pp.present  

