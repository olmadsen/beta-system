ORIGIN '../codeeditor';
-- editorenvHistoryPrivate: Descriptor --
(#
   test:
     (# 
     do
        (if trace[8] then
            'History: '->putLIne;
            h.scan
              (# 
              do
                 (if (current[]->h.at) = currentCell[] then
                     '-->'->putText; 
                  else
                     '   '->putText
                 if);
                 (current.frag.father).name->putText;
                 '-'->put;
                 current.frag.name->putText;
                 '-'->put;
                 current.index->putInt;
                 newLine
              #)
        if)
     #)
#)  

-- editorenvHistoryCurrent: DoPart --
do (if currentCell[] <> none then currentCell.elm[]->node[] if)  

-- editorenvHistoryAdd: DoPart --
do
   (if doUpdate then
       (# garbage: ^historylist
     do
        (if node[] <> none then
            (if h.size > 1 then
                currentCell[]->h.splitAfter->(h[],garbage[]); 
            if);
            node[]->h.append;
            h.last->currentCell[];
            private.test
        if)
       #) 
   if)

-- editorenvHistoryRemove: DoPart --
do
   (if doUpdate then
       (if node[] <> none then
           search: h.scan
             (# theC: ^h.theCellType
             do
                (if current[]->node.equal then
                    current[]->h.at->theC[];
                    (if theC.pred[] <> none then
                        theC.pred[]->currentCell[]; theC[]->h.delete
                     else
                        (if theC.succ[] <> none then
                            theC.succ[]->currentCell[]; theC[]->h.delete
                         else
                            currentCell[]->h.delete; none ->currentCell[]
                        if)
                    if);
                    leave search
                if)
             #);
           
       if);
       private.test
   if)

-- editorenvHistoryBackPossible: DoPart --
do
   (if currentCell[] <> none then
       (if (currentCell.elm[]->node.equal) then
           (currentCell.Pred[] <> none )->value
        else
           true->value
       if)
   if)  

-- editorEnvHistoryBack: DoPart --
do
   (if currentCell[] <> none then
;'+1+'->putLine;
       (if currentNode[] <> none then
           ;'+2+'->putLine;
           'currentNode: '->putText;
           (currentNode.frag.father).name->putText; '-'->put;
           currentNode.frag.name->putText;'-'->put;
           currentNode.index->putInt;newLine;
           'currentCell.elm: '->putText;
           (currentCell.elm.frag.father).name->putText; '-'->put;
           currentCell.elm.frag.name->putText;'-'->put;
           currentCell.elm.index->putInt;newLine;
           
           (if not (currentCell.elm[]->currentNode.equal) then
;'+3+'->putLine;
               currentCell.elm[]->newNode[]
            else
;'+4+'->putLine;
               (if currentCell.pred[] <> none then
;'+5+'->putLine;
                   currentCell.pred[]->currentCell[];
                   currentCell.elm[]->newNode[]
               if)
           if)
        else
;'+6+'->putLine;
           (if not (currentCell.elm[]->currentNode.equal) then
;'+7+'->putLine;
               currentCell.elm[]->newNode[]
            else
;'+8+'->putLine;
               (if currentCell.pred[] <> none then
;'+9+'->putLine;
                   currentCell.pred[]->currentCell[];
                   currentCell.elm[]->newNode[]
               if)
           if)
       if)
   if);
   private.test  

-- editorenvHistoryForwardPossible: DoPart --
do
   (if currentCell[] <> none then
       (if (currentCell.elm[]->node.equal) then
           (currentCell.succ[] <> none )->value
        else
           true->value
       if)
   if)  

-- editorenvHistoryForward: DoPart --
do
   (if currentCell[] <> none then
       (if currentNode[] <> none then
           (if not (currentCell.elm[]->currentNode.equal) then
               currentCell.elm[]->newNode[]
            else
               (if currentCell.succ[] <> none then
                   currentCell.succ[]->currentCell[];
                   currentCell.elm[]->newNode[]
               if)
           if)
        else
           (if not (currentCell.elm[]->currentNode.equal) then
               currentCell.elm[]->newNode[]
            else
               (if currentCell.succ[] <> none then
                   currentCell.succ[]->currentCell[];
                   currentCell.elm[]->newNode[]
               if)
           if)
       if)
   if);
   private.test  

-- editorenvHistoryUpdateHistory: DoPart --
do node[]->edenv.history.add  

-- editorenvHistoryProtect: Descriptor --
(# status: @boolean
do doUpdate->status; false->doUpdate;
   INNER protect;
   status->doUpdate
#)
