ORIGIN '../searchdialog';
INCLUDE '~beta/guienv/v1.6/fields';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- searchDialogPrivate: Descriptor --
(#
   fatherpos,fathersize,mysize: @point;
   rescale:
     (# newsize: @point; hpct,vpct: @real; h,v: @integer
     do
        size->newsize;
        newsize.h / mysize.h->hpct;
        newsize.v / mysize.v->vpct;
        newsize->mysize;
        cancelButton.position->(h,v);
        (h*hpct,v*vpct)->cancelButton.position;
        cancelButton.size->(h,v);
        (h*hpct,v)->cancelButton.size;
        nextButton.position->(h,v);
        (h*hpct,v*vpct)->nextButton.position;
        nextButton.size->(h,v);
        (h*hpct,v)->nextButton.size;
        previousButton.position->(h,v);
        (h*hpct,v*vpct)->previousButton.position;
        previousButton.size->(h,v);
        (h*hpct,v)->previousButton.size;
        theMessage.position->(h,v);
        (h*hpct,v*vpct)->theMessage.position;
        theMessage.size->(h,v);
        (h*hpct,v)->theMessage.size;
        theText.position->(h,v);
        (h*hpct,v*vpct)->theText.position;
        theText.size->(h,v);
        (h*hpct,v)->theText.size;
        
     #);
   dialogopened: @boolean;
   DialogText: textField
     (#
        eventhandler::
          (#
             onBeforeChange::
               (# txt: ^text
               do
                  (if length = 1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl // ascii.cr then
                          THIS(search).private.nextButton.theEventhandler.
                            onMouseup;
                          false->allow;
                          
                      if);
                      
                  if)
               #);
             onKeyDown::
               (# 
               do
                  (if ch = ascii.esc then
                      THIS(search).private.cancelButton.theEventhandler.
                        onMouseup;
                      
                  if)
               #);
             
          #);
        
     #);
   theMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,10),(20+320,10+30))->frame #);
        
     #);
   theText: @DialogText
     (# open:: (#  do ((20,40),(20+320,40+30))->frame #);  #);
   nextButton: @pushButton
     (#
        eventHandler::
          (# onMouseUp:: (#  do theText.contents->usertext[]; next #)
          #);
        open:: (#  do ((20,80),(20+100,80+30))->frame; 'Next'->label;  #);
        
     #);
   previousButton: @pushButton
     (#
        eventHandler::
          (# onMouseUp:: (#  do theText.contents->usertext[]; previous #)
          #);
        open::
          (#  do ((130,80),(130+110,80+30))->frame; 'Previous'->label;  #);
        
     #);
   cancelButton: @pushButton
     (#
        eventHandler::
          (#
             onMouseUp::
               (# 
               do theText.contents->usertext[]; cancel; THIS(search).hide
               #)
          #);
        open::
          (#  do ((250,80),(240+100,80+30))->frame; 'Close'->label;  #);
        
     #);
   
#)  

-- searchDialogPopup: DoPart --
do
   (if not private.dialogopened then open;  if);
   titleText[]->title;
   msgText[]->private.theMessage.label;
   private.theText.all->private.theText.selection;
   private.theText.delete;
   (if defaultText[] = none then ''->defaultText[] if);
   defaultText[]->private.theText.insert;
   private.theText.all->private.theText.selection;
   INNER ;
   (if father[] <> none then
       father.size->private.fathersize;
       size->private.mysize;
       father.position->private.fatherpos;
       (private.fatherpos.v+(private.fathersize.v-private.mysize.v) div 2,
        private.fatherpos.h+(private.fathersize.h-private.mysize.h) div 2)
         ->position;
       
    else
       mouse.globalPosition->position; 
   if);
   show;
     

-- searchDialogOpen: DoPart --
do
   (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       THIS(search).contents->private.theMessage.open;
       THIS(search).contents->private.theText.open;
       THIS(search).contents->private.nextButton.open;
       THIS(search).contents->private.previousButton.open;
       THIS(search).contents->private.cancelButton.open;
       (360,130)->private.mysize->size;
       INNER ;
       
   if);
     

-- replaceDialogPrivate: Descriptor --
(#
   fatherpos,fathersize,mysize: @point;
   rescale:
     (# newsize: @point; hpct,vpct: @real; h,v: @integer
     do
        size->newsize;
        newsize.h / mysize.h->hpct;
        newsize.v / mysize.v->vpct;
        newsize->mysize;
        cancelButton.position->(h,v);
        (h*hpct,v*vpct)->cancelButton.position;
        cancelButton.size->(h,v);
        (h*hpct,v)->cancelButton.size;
        nextButton.position->(h,v);
        (h*hpct,v*vpct)->nextButton.position;
        nextButton.size->(h,v);
        (h*hpct,v)->nextButton.size;
        previousButton.position->(h,v);
        (h*hpct,v*vpct)->previousButton.position;
        previousButton.size->(h,v);
        (h*hpct,v)->replaceButton.size;
        replaceButton.position->(h,v);
        (h*hpct,v*vpct)->replaceButton.position;
        replaceButton.size->(h,v);
        (h*hpct,v)->replaceButton.size;
        theSearchMessage.position->(h,v);
        (h*hpct,v*vpct)->theSearchMessage.position;
        theSearchMessage.size->(h,v);
        (h*hpct,v)->theSearchMessage.size;
        theReplaceMessage.position->(h,v);
        (h*hpct,v*vpct)->theReplaceMessage.position;
        theReplaceMessage.size->(h,v);
        (h*hpct,v)->theReplaceMessage.size;
        theSearchText.position->(h,v);
        (h*hpct,v*vpct)->theSearchText.position;
        theSearchText.size->(h,v);
        (h*hpct,v)->theSearchText.size;
        theReplaceText.position->(h,v);
        (h*hpct,v*vpct)->theReplaceText.position;
        theReplaceText.size->(h,v);
        (h*hpct,v)->theReplaceText.size;
        
     #);
   dialogopened: @boolean;
   DialogText: textField
     (#
        eventhandler:: 
          (#
             onBeforeChange:: 
               (# txt: ^text
               do
                  (if length = 1 then
                      theText->txt[];
                      (if 1->txt.inxGet
                       // ascii.nl // ascii.cr then
                          THIS(replaceWindow).private.nextButton.theEventhandler
                          .onMouseup;
                          false->allow;
                          
                      if);
                      
                  if)
               #);
             onKeyDown:: 
               (# 
               do
                  (if ch = ascii.esc then
                      THIS(replaceWindow).private.cancelButton.theEventhandler.
                        onMouseup;
                      
                  if)
               #);
             
          #);
        
     #);
   theSearchMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,10),(20+320,10+30))->frame #);
        
     #);
   theSearchText: @DialogText
     (# open::  (#  do ((20,40),(20+430,40+30))->frame #);  #);
   theReplaceMessage: @staticText
     (#
        eventhandler::< 
          (# onFatherFrameChanged::  (#  do rescale #) #);
        open::  (#  do ((20,70),(20+320,70+30))->frame #);
        
     #);
   theReplaceText: @DialogText
     (# open::  (#  do ((20,100),(20+430,100+30))->frame #);  #);
   replaceButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (#  do theReplaceText.contents->replaceText[]; replace #)
          #);
        open:: 
          (#  do ((20,140),(20+100,140+30))->frame; 'Replace'->label;  #);
        
     #);
   nextButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (#  do theSearchText.contents->searchText[]; next #)
          #);
        open:: 
          (#  do ((130,140),(130+100,140+30))->frame; 'Next'->label;  #);
        
     #);
   previousButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (#  do theSearchText.contents->searchText[]; previous #)
          #);
        open:: 
          (# 
          do ((240,140),(240+100,140+30))->frame; 'Previous'->label; 
          #);
        
     #);
   cancelButton: @pushButton
     (#
        eventHandler:: 
          (#
             onMouseUp:: 
               (# 
               do
                  theSearchText.contents->searchText[];
                  cancel;
                  THIS(replaceWindow).hide
               #)
          #);
        open:: 
          (#  do ((350,140),(350+100,140+30))->frame; 'Close'->label;  #);
        
     #);
   
#)  

-- replaceDialogPopup: DoPart --
do
   (if not private.dialogopened then open;  if);
   titleText[]->title;
   msgText[]->private.theSearchMessage.label;
   msgText2[]->private.theReplaceMessage.label;
   private.theSearchText.all->private.theSearchText.selection;
   private.theSearchText.delete;
   (if defaultText[] = none then ''->defaultText[] if);
   defaultText[]->private.theSearchText.insert;
   private.theSearchText.all->private.theSearchText.selection;
   INNER ;
   (if father[] <> none then
       father.size->private.fathersize;
       size->private.mysize;
       father.position->private.fatherpos;
       (private.fatherpos.v+(private.fathersize.v-private.mysize.v) div 2,
        private.fatherpos.h+(private.fathersize.h-private.mysize.h) div 2)
         ->position;
       
    else
       mouse.globalPosition->position; 
   if);
   show;
     

-- replaceDialogOpen: DoPart --
do
   (if not private.dialogopened then
       true->private.dialogopened;
       hide;
       THIS(replaceWindow).contents->private.theSearchMessage.open;
       THIS(replaceWindow).contents->private.theSearchText.open;
       THIS(replaceWindow).contents->private.theReplaceMessage.open;
       THIS(replaceWindow).contents->private.theReplaceText.open;
       THIS(replaceWindow).contents->private.replaceButton.open;
       THIS(replaceWindow).contents->private.nextButton.open;
       THIS(replaceWindow).contents->private.previousButton.open;
       THIS(replaceWindow).contents->private.cancelButton.open;
       (460,190)->private.mysize->size;
       INNER ;
       
   if);
     

