ORIGIN '../codeeditor';
-- fragmentFormHandlerSubscribe: DoPart --
do
     (# ffh: ^fragmentFormHandler
     do
        (if trace[7] then 'subscribe fragmentFormHandler'->putLine if);
        search: ffHandler.scan
          (# 
          do
             (if current.ff[] = fe.frag[] then
                 current[]->ffh[]; leave search
             if);
             
          #);
        (if ffh[] = none then
            &fragmentFormHandler[]->ffh[];
            fe.frag[]->ffh.ff[];
            ffh[]->ffHandler.append
        if);
        ffh[]->fe.ffh[];
        fe[]->ffh.formEditors.append;
        
     #)  

-- fragmentFormHandlerUnSubscribe: DoPart --
do
     (# ffh: ^fragmentFormHandler
     do
        (if trace[7] then 'unsubscribe fragmentFormHandler'->putLine if);
        search: ffHandler.scan
          (# 
          do
             (if current.ff[] = fe.frag[] then
                 current[]->ffh[]; leave search
             if);
             
          #);
        (if ffh[] = none then
            'No handler was installed for: '->putText;
            (fe.frag.father).name->putText;
            '-'->put;
            fe.frag.name->putLine;
            
         else
            fe[]->ffh.formEditors.at->ffh.formEditors.delete;
            (if ffh.formEditors.size = 0 then
                (if trace[7] then
                    'Deleting handler for: '->putText;
                    (fe.frag.father).name->putText;
                    '-'->put;
                    fe.frag.name->putLine;
                    'No more subscribers'->putLine
                if);
                ffh[]->ffHandler.at->ffHandler.delete
            if)
        if);
        
     #)  

-- fragmentFormHandlerfragmentChanged: DoPart --
do INNER  

-- fragmentFormHandlerAstReplaced: DoPart --
do
   (if trace[7] then 'fragmentFormHandlerAstReplace'->putLine if);
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (if trace[7] then
                'calling current.anASTReplacedEvent'->putLine
            if);
            (false,oldAST[],newAST[])->current.anASTReplacedEvent
        if)
     #);
   (if effh[] <> none then (oldAST[],newAST[])->effh.anASTReplacedEvent if)  

-- fragmentFormHandlerAstReplacedSequence: DoPart --
do
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (false,rootOfSequence[])->current.astReplacedSequenceEvent
        if)
     #);
   (if effh[] <> none then
       (rootOfSequence[])->effh.astReplacedSequenceEvent
   if)  

-- fragmentFormHandlerListElementInserted: DoPart --
do
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (false,listNode[],position)->current.aListElementInsertedEvent
        if)
     #);
   (if effh[] <> none then
       (listNode[],position)->effh.aListElementInsertedEvent
   if)  

-- fragmentFormHandlerListElementsDeleted: DoPart --
do
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (false,listNode[],position,length,oldElements[])
              ->current.aListElementsDeletedEvent
        if)
     #);
   (if effh[] <> none then
       (listNode[],position,length,oldElements[])
         ->effh.aListElementsDeletedEvent
   if)  

-- fragmentFormHandlerListElementsReplaced: DoPart --
do
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (false,listNode[],position,length,oldElements[],newLength)
              ->current.aListElementsReplacedEvent
        if)
     #);
   (if effh[] <> none then
       (listNode[],position,length,oldElements[],newLength)
         ->effh.aListElementsReplacedEvent
   if)  

-- externalfragmentFormHandlerSubscribe: DoPart --
do
     (# ffh: ^fragmentFormHandler
     do
        (if trace[7] then
            'subscribe externalfragmentFormHandler'->putLine
        if);
        search: ffHandler.scan
          (# 
          do
             (if current.ff[] = ff[] then current[]->ffh[]; leave search if); 
          #);
        (if ffh[] <> none then
            &externalFragmentFormHandler[]->effh[];
            ffh[]->effh.ffh[];
            effh[]->ffh.effh[]
         else
            'External fragment form handler subscribe: No form eiditor for this fragment form'
              ->putLine
        if);
        
     #)  

-- externalfragmentFormHandlerUnSubscribe: DoPart --
do (if trace[7] then 'unsubscribe externalfragmentFormHandler'->putLine if);   

-- externalfragmentFormHandlerfragmentChanged: DoPart --
do INNER  

-- externalfragmentFormHandlerAstReplaced: DoPart --
do
   (if trace[7] then 'externalfragmentFormHandlerAstReplace'->putLine if);
   (if ffh[] <> none then
       (if trace[7] then 'calling current.anASTReplacedEvent'->putLine if);
       (none ,oldAST[],newAST[])->ffh.anASTReplacedEvent
   if)  

-- externalfragmentFormHandlerAstReplacedSequence: DoPart --
do
   (if ffh[] <> none then
       (none ,rootOfSequence[])->ffh.astReplacedSequenceEvent
   if)  

-- externalfragmentFormHandlerListElementInserted: DoPart --
do
   (if ffh[] <> none then
       (none ,listNode[],position)->ffh.aListElementInsertedEvent
   if)  

-- externalfragmentFormHandlerListElementsDeleted: DoPart --
do
   (if ffh[] <> none then
       (none ,listNode[],position,length,oldElements[])
         ->ffh.aListElementsDeletedEvent
   if)  

-- externalfragmentFormHandlerListElementsReplaced: DoPart --
do
   (if ffh[] <> none then
       (none ,listNode[],position,length,oldElements[],newLength)
         ->ffh.aListElementsReplacedEvent
   if)  

