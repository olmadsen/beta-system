ORIGIN '../codeeditor';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- fragmentFormHandlerSubscribe: DoPart --
do
     (# ffh: ^fragmentFormHandler
     do
        (if trace[7] then 'subscribe fragmentFormHandler'->putLine if);
        search: ffHandler.scan
          (# 
          do
             (if current.ff[] = fe.frag[] then
                 current[]->ffh[]; leave search
             if);
             
          #);
        (if ffh[] = none then
            &fragmentFormHandler[]->ffh[];
            fe.frag[]->ffh.ff[];
            ffh[]->ffHandler.append
        if);
        ffh[]->fe.ffh[];
        fe[]->ffh.formEditors.append;
        
     #)  

-- fragmentFormHandlerUnSubscribe: DoPart --
do
     (# ffh: ^fragmentFormHandler
     do
        (if trace[7] then 'unsubscribe fragmentFormHandler'->putLine if);
        search: ffHandler.scan
          (# 
          do
             (if current.ff[] = fe.frag[] then
                 current[]->ffh[]; leave search
             if);
             
          #);
        (if ffh[] = none then
            'No handler was installed for: '->putText;
            (fe.frag.father).name->putText;
            '-'->put;
            fe.frag.name->putLine;
            
         else
            fe[]->ffh.formEditors.at->ffh.formEditors.delete;
            (if ffh.formEditors.size = 0 then
                (if trace[7] then
                    'Deleting handler for: '->putText;
                    (fe.frag.father).name->putText;
                    '-'->put;
                    fe.frag.name->putLine;
                    'No more subscribers'->putLine
                if);
                ffh[]->ffHandler.at->ffHandler.delete
            if)
        if);
        
     #)  

-- fragmentFormHandlerfragmentChanged: DoPart --
do INNER  

-- fragmentFormHandlerAstReplaced: DoPart --
do
   (if trace[7] then 'fragmentFormHandlerAstReplace'->putLine if);
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (if trace[7] then
                'calling current.anASTReplacedEvent'->putLine
            if);
            (false,oldAST[],newAST[])->current.anASTReplacedEvent
        if)
     #);
   (if (effh[] <> none ) and (byEditor[] <> none ) then
       (oldAST[],newAST[])->effh.ASTReplacedEvent
   if)  

-- fragmentFormHandlerAstReplacedSequence: DoPart --
do
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (false,rootOfSequence[])->current.astReplacedSequenceEvent
        if)
     #);
   (if (effh[] <> none ) and (byEditor[] <> none ) then
       (rootOfSequence[])->effh.astReplacedSequenceEvent
   if)  

-- fragmentFormHandlerListElementInserted: DoPart --
do
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (false,listNode[],position)->current.aListElementInsertedEvent
        if)
     #);
   (if (effh[] <> none ) and (byEditor[] <> none ) then
       (listNode[],position)->effh.ListElementInsertedEvent
   if)  

-- fragmentFormHandlerListElementsDeleted: DoPart --
do
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (false,listNode[],position,length,oldElements[])
              ->current.aListElementsDeletedEvent
        if)
     #);
   (if (effh[] <> none ) and (byEditor[] <> none ) then
       (listNode[],position,length,oldElements[])->effh.ListElementsDeletedEvent
   if)  

-- fragmentFormHandlerListElementsReplaced: DoPart --
do
   formEditors.scan
     (# 
     do
        (if current[] <> byEditor[] then
            (false,listNode[],position,length,oldElements[],newLength)
              ->current.aListElementsReplacedEvent
        if)
     #);
   (if (effh[] <> none ) and (byEditor[] <> none ) then
       (listNode[],position,length,oldElements[],newLength)
         ->effh.ListElementsReplacedEvent
   if)  

-- externalfragmentFormHandlerSubscribe: DoPart --
do
     (# ffh: ^fragmentFormHandler
     do
        (if trace[7] then
            'subscribe externalfragmentFormHandler'->putLine
        if);
        search: ffHandler.scan
          (# 
          do
             (if current.ff[] = remoteffh.ff[] then
                 current[]->ffh[]; leave search
             if);
             
          #);
        (if ffh[] = none then
            &fragmentFormHandler[]->ffh[];
            remoteffh.ff[]->ffh.ff[];
            ffh[]->ffHandler.append
        if);
        ffh[]->remoteffh.ffh[];
        remoteffh[]->ffh.effh[];
        &externalFragmentFormHandler
          (#
             AstReplacedEvent:: 
               (# 
               do
                  (if trace[7] then
                      'externalfragmentFormHandlerAstReplace'->putLine
                  if);
                  (if trace[7] then
                      'calling current.anASTReplacedEvent'->putLine
                  if);
                  (none ,oldAST[],newAST[])->ffh.anASTReplacedEvent
               #);
             AstReplacedSequenceEvent:: 
               (# 
               do (none ,rootOfSequence[])->ffh.astReplacedSequenceEvent
               #);
             ListElementInsertedEvent:: 
               (# 
               do (none ,listNode[],position)->ffh.aListElementInsertedEvent
               #);
             ListElementsDeletedEvent:: 
               (# 
               do
                  (none ,listNode[],position,length,oldElements[])
                    ->ffh.aListElementsDeletedEvent
               #);
             ListElementsReplacedEvent:: 
               (# 
               do
                  (none ,listNode[],position,length,oldElements[],newLength)
                    ->ffh.aListElementsReplacedEvent
               #)
          #)[]->localffh[];
        remoteffh.ff[]->localffh.ff[];
        ffh[]->localffh.ffh[];
        
     #)  

-- externalfragmentFormHandlerUnSubscribe: DoPart --
do (if trace[7] then 'unsubscribe externalfragmentFormHandler'->putLine if);   

