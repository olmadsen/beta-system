origin '../codeeditor'
-- fragmentFormHandlerSubscribe:DoPart--
do (# ffh: ^fragmentFormHandler
   do 'subscribe'->putLine; 
      search: ffHandler.scan 
     (# do (if current.ff[]=fe.frag[] then current[]->ffh[]; leave search if);#);
      (if ffh[]=none then
          &fragmentFormHandler[]->ffh[];
          fe.frag[]->ffh.ff[];
          ffh[]->ffHandler.append
      if);
      ffh[]->fe.ffh[];
      fe[]->ffh.formEditors.append;
   #)
      
-- fragmentFormHandlerUnSubscribe:DoPart--
do (# ffh: ^fragmentFormHandler
   do 'unsubscribe'->putLine; 
      search: ffHandler.scan 
     (# do (if current.ff[]=fe.frag[] then current[]->ffh[]; leave search if);#);
      (if ffh[]=none then
          'No handler was installed for: '->putText;
          (fe.frag.father).name->putText; '-'->put; fe.frag.name->putLine;
       else
          fe[]->ffh.formEditors.at->ffh.formEditors.delete;
          (if ffh.formEditors.size=0 then
              'Deleting handler for: '->putText;
              (fe.frag.father).name->putText; '-'->put; fe.frag.name->putLine;
              'No more subscribers'->putLine;
              ffh[]->ffHandler.at->ffHandler.delete
          if)
      if);
   #)
   
-- fragmentFormHandlerfragmentChanged:DoPart--
do INNER
     
-- fragmentFormHandlerAstReplaced:DoPart--
do 'fragmentFormHandlerAstReplace'->putLine; 
   formEditors.scan 
   (# 
   do (if current[]<>byEditor[] then
          'calling current.anASTReplacedEvent'->putLine; 
          (false,oldAST[],newAST[])->current.anASTReplacedEvent
      if)
   #)
     
-- fragmentFormHandlerAstReplacedSequence:DoPart--
do formEditors.scan 
   (# 
   do  (if current[]<>byEditor[] then
           (false,rootOfSequence[])->current.astReplacedSequenceEvent
       if)
   #)
     
-- fragmentFormHandlerListElementInserted:DoPart--
do formEditors.scan 
   (# 
   do   (if current[]<>byEditor[] then
            (false,fatherNode[],position)->current.aListElementInsertedEvent
        if)
   #)
     
-- fragmentFormHandlerListElementsDeleted:DoPart--
do formEditors.scan 
   (# 
   do  (if current[]<>byEditor[] then
           (false,listNode[],position,length,oldElements[])->current.aListElementsDeletedEvent
       if)
   #)
     
-- fragmentFormHandlerListElementsReplaced:DoPart--
do formEditors.scan 
   (# 
   do  (if current[]<>byEditor[] then
           (false,listNode[],position,length,oldElement[],newLength)->current.aListElementsReplacedEvent
       if)
   #)
     
