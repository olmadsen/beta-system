ORIGIN '~beta/basiclib/v1.4/betaenv';
INCLUDE '~beta/mps/v4.9.1/astlevel'
        '~beta/betaast/v4.9.1/betacfl'
        '~beta/mps/v4.9.1/private/astparser'
        '~beta/guienv/v1.2/guienv'
        'codeviewer';
-- program: Descriptor --
(#
   UI: ^guienv;
   myUI: guienv
     (#
        main: @window
          (#
             editor: @sifTextEditor (#  #);
             open:: 
               (#
                  fg: ^mps.fragmentGroup;
                  ff: ^mps.fragmentForm;
                  node: ^mps.ast;
                  
               do
                  mps;
                  betaGrammarInit;
                  ('codeviewer',screen[])->mps.top.open->fg[];
                  (if fg[] <> none then
                      fg.fragmentList.scan
                        (# 
                        do
                           (if current.type = mps.formType then
                               current.f[]->ff[]
                           if)
                        #);
                      
                  if);
                  ff.root[]->node[];
                  editor.open;
                  (mps[],betaGrammar[],ff[],node[])->editor.newfragment;
                  editor.size->size;
                  true->editor.bindBottom->editor.bindRight;
                  
               #);
             
          #);
        
     do main.open
     #);
   mps: @astInterface;
   betaGrammar: ^mps.treelevel;
   betaGrammarInit:
     (#
        theMetaGrammar: ^mps.treelevel;
        grammarWithPath,help: ^text;
        gg: ^mps.fragmentGroup;
        bobsfile: @file
     do
        &mps.metaGrammar[]->theMetaGrammar[]->mps.grammarTable.meta[];
        theMetaGrammar.init;
        '~beta/grammars/metagram/v4.4/metagrammar'->grammarWithPath[];
        (grammarWithPath[],screen[])->mps.top.open->gg[];
        (if gg[]
         // none then
            'Could not open: '->screen.putText;
            grammarWithPath[]->putLine;
            (failure,'')->stop;
            
        if);
        ('meta',screen[])->gg.open->gg[];
        (if gg[]
         // none then
            'Could not open: '->screen.putText;
            '-meta'->grammarWithPath.copyAppend->screen.putLine;
            (failure,'')->stop;
            
         else
            ('metagrammar',screen[])->gg.open->theMetaGrammar.grammarAst[];
            (if theMetaGrammar.grammarAst[]
             // none then
                'No grammarAst for '->screen.putText;
                grammarWithPath[]->screen.putLine;
                (failure,'')->stop;
                
             else
                '-parser'->help[];
                mps.parserFileExtension->help.append;
                help[]->grammarWithPath.copyAppend->mps.expandToFullPath
                  ->bobsFile.name;
                (if bobsFile.entry.exists
                 // true then
                    bobsFile.name->theMetaGrammar.parser.initialize; 
                 else
                    'No parser available: '->screen.putText;
                    bobsFile.name->screen.putLine;
                    
                if)
            if);
            
        if);
        &mps.beta[]->betaGrammar[];
        betaGrammar.init;
        '~beta/grammars/beta/v2.4/beta'->grammarWithPath[];
        (grammarWithPath[],screen[])->mps.top.open->gg[];
        (if gg[]
         // none then
            'Could not open: '->screen.putText;
            grammarWithPath[]->putLine;
            (failure,'')->stop;
            
        if);
        ('meta',screen[])->gg.open->gg[];
        (if gg[]
         // none then
            'Could not open: '->screen.putText;
            '-meta'->grammarWithPath.copyAppend->screen.putLine;
            (failure,'')->stop;
            
         else
            ('beta',screen[])->gg.open->betaGrammar.grammarAst[];
            (if betaGrammar.grammarAst[]
             // none then
                'No grammarAst for '->screen.putText;
                grammarWithPath[]->screen.putLine;
                (failure,'')->stop;
                
             else
                '-parser'->help[];
                mps.parserFileExtension->help.append;
                help[]->grammarWithPath.copyAppend->mps.expandToFullPath
                  ->bobsFile.name;
                (if bobsFile.entry.exists
                 // true then
                    bobsFile.name->betaGrammar.parser.initialize;
                    ('objectdescriptor','descriptor')
                      ->betaGrammar.parser.privatePart.b.defineNonTAlias;
                    ('attributedecl','attributes')
                      ->betaGrammar.parser.privatePart.b.defineNonTAlias;
                    
                 else
                    'No parser available: '->screen.putText;
                    bobsFile.name->screen.putLine;
                    
                if);
                
            if);
            
        if);
        
     #);
   
do &myUI[]->UI[]; UI
#)  

