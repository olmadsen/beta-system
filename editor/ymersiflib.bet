ORIGIN '~beta/sourcebrowser/v1.2/ymer';
INCLUDE '~beta/guienv/v1.6/utils/simplemenu';
INCLUDE '~beta/sourcebrowser/v1.2/private/parseEditor';
INCLUDE '~beta/sourcebrowser/v1.2/private/semanticEditor';
INCLUDE '~beta/dependency/v1.3/dependency';
        'ymercall'
'createdialog'
'frejasifcomm';
MDBODY nti 'private/sif_ntibody'
       mac 'private/sif_macbody'
       ppc 'private/sif_macbody'
       ppcmac 'private/sif_macbody'
       default 'private/sif_unixbody';
BODY 'private/globalexternalintbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- lib: Attributes --
fragmentBrowser: ymerApplication
  (#
     ymerBrowser: @ymerBrowserWindow;
     ymerBrowserWindow::<
       (#
          browserTitle::< (#  do ' and Editor'->title.append; INNER #);
          theSifTextEditor: ^sifTextEditor;
          cbg: (* current browser group *) ^browser.projects.group;
          cff:
          (* current fragment form, not used anymore only in oldfromeditorExists *)
          ^astInterface.fragmentform;
          ymerCallback: @ymerCall
            (#
               refreshGroup:: (#  do (cb[],fg[],none )->updateBrowser #);
               findAndSelect::
                 (#  do node[]->THIS(ymerBrowserWindow).findAndSelect #);
               selectFragmentForm::
                 (# 
                 do ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                 #);
               getFragmentGroup::
                 (# 
                 do name[]->mps.fragmentGroupTable.getFragmentGroup->fg[]
                 #);
               getCurrentProject::
                 (# fg: ^astInterface.fragmentGroup
                 do
                    &fragmentGroupList[]->fgList[];
                    (if (browser.currentProject[] <> none ) then
                        browser.currentProject.groups.scan
                          (# 
                          do
                             current.getfg->fg[];
                             (if fg[] <> none then fg.name->fgList.append if)
                          #)
                    if)
                 #);
               getCurrentFragmentGroup::
                 (# 
                 do
                    (if browser.cge[] <> none then browser.cge.fg[]->fg[] if)
                 #);
               getCurrentFragmentForm::
                 (# 
                 do
                    (if browser.cfe[] <> none then
                        browser.cfe.frag[]->ff[]
                    if)
                 #);
               check::
                 (#  do fg[]->THIS(ymerBrowserWindow).check->status #);
               betaenvVersion::
                 (#  do THIS(fragmentBrowser).betaenvVersion->t[] #);
               machineType::
                 (#  do THIS(fragmentBrowser).machineType[]->t[] #);
               infoView:: (#  do t[]->writeInfoView #);
               infoViewDone:: (#  do writeInfoViewDone #);
               onAboutToCloseGroup::
                 (# 
                 do
                    fg[]->THIS(fragmentBrowser).onAboutToCloseGroup
                      ->(OKtoClose,askSave)
                 #);
               onGroupClosed::
                 (#  do fg[]->THIS(fragmentBrowser).onGroupClosed #);
               onGroupSaved::
                 (# t: ^text
                 do
                    fg[]->THIS(fragmentBrowser).onGroupSaved;
                    (if (browser.cge[] <> none ) and (browser.cge.fg[] = fg[])
                     then
                        browser.locationView.label->t[];
                        (' ',10)->t.inxPut;
                        t[]->browser.locationView.label
                    if)
                 #);
               onGroupNotSaved::
                 (#  do fg[]->THIS(fragmentBrowser).onGroupNotSaved #);
               onGroupTouched::
                 (# t: ^text
                 do
                    (if (browser.cge[] <> none ) and (browser.cge.fg[] = fg[])
                     then
                    (*  browser.locationView.label->t[];
                     ('*',10)->t.inxPut;
                     t[]->browser.locationView.label;*)
                        false->browser.locationView.checked;
                        true->browser.locationView.changed
                    if)
                 #);
               onGroupDetouched::
                 (# t: ^text
                 do
                    (if (browser.cge[] <> none ) and (browser.cge.fg[] = fg[])
                     then
                        (if browser.cge.touched = 0 then
                        (* browser.locationView.label->t[];
                         (' ',10)->t.inxPut;
                         t[]->browser.locationView.label;*)
                            false->browser.locationView.changed
                        if)
                    if)
                 #);
               externalTextEdit::
                 (# 
                 do
                    textBefore[]->THIS(ymerBrowserWindow).externalTextEdit
                      ->textAfter[]
                 #);
               frejaInclude::
                 (#  do THIS(fragmentBrowser).frejaInclude->t[] #);
               setFrejaMark::
                 (#  do (frejaMark,fg[])->externalInterface.setFrejaMark #);
               getFrejaMark::
                 (#  do fg[]->externalInterface.getFrejaMark #);
               isEditor:: (#  do true->value #);
               showSearchHits::
                 (# 
                 do
                    (fg[],t[],caseSensitive,wholeWord,searchArea)
                      ->THIS(ymerBrowserWindow).showSearchHits
                 #);
               isActive:: (# do value->THIS(fragmentBrowser).isActive #)
            #);
          doCommand:
            (# 
            do edenv.setWaitCursor; INNER doCommand; edenv.setStructureCursor
            #);
          checkOngoingTextEditing:
            (# value: @boolean
            do
               (if browser.cfe[] <> none then
                   browser.cfe.texteditMode->value
               if)
            exit value
            #);
          formEditorExists:
            (# value: @boolean
            do
               (if browser.cfe[] <> none then
                   (if not browser.cfe.texteditMode then true->value;  if)
               if)
            exit value
            #);
          groupEditorExists:
            (# value: @boolean
            do (browser.cge[] <> none )->value
            exit value
            #);
          allowCommand:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and not
                   browser.cge.isReadOnly and not browser.cge.isAfrejaEditor
                    then
                       true->value
                   if)
               if)
            exit value
            #);
          allowPerform:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and not
                   browser.cge.isReadOnly then
                       true->value
                   if)
               if)
            exit value
            #);
          allowOnForm:
            (# value: @boolean
            do
               (if groupEditorExists and not browser.propertySelected then
                   (if not checkOngoingTextediting and not
                   browser.cge.isReadOnly then
                       true->value
                   if)
               if)
            exit value
            #);
          updateBrowser:
            (#
               fg: ^astInterface.fragmentGroup;
               ff: ^astInterface.fragmentForm;
               cb: ^object;
               browser: ^sourceBrowser;
               cbg: (* current browser group *) ^browser.projects.group
            enter (cb[],fg[],ff[])
            do
               (if (cb[] <> none ) and (cb## <= sourceBrowser##) then
                   cb[]->browser[];
                   browser.currentgroup[]->cbg[];
                   (if (cbg[] <> none ) and (cbg.fg[] = fg[]) then
                       (browser.currentProject[],none )->browser.setGroup;
                       (browser.currentProject[],cbg[])->browser.setGroup;
                       (if ff[] <> none then
                           (browser.currentProject[],cbg[],ff[])
                             ->browser.setFragmentForm
                       if)
                   if)
               if)
            #);
          findAndSelect:
            (# node: ^astInterface.ast
            enter node[]
            do
               (if node[] <> none then
                   (if browser.cfe[] <> none then
                       (if node.frag[] <> browser.cfe.frag[] then
                           ((node.frag.father).fullName,node.frag.name)
                             ->browser.selectFragmentForm;
                           (node[],1,0,0)->browser.cfe.setFocus
                        else
                           (node[],1,0,0)->browser.cfe.setFocus
                       if)
                    else
                       ((node.frag.father).fullName,node.frag.name)
                         ->browser.selectFragmentForm;
                       (node[],1,0,0)->browser.cfe.setFocus
                   if)
                else
                   'findAndSelect: node is none!'->putLine
               if)
            #);
          findForm:
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do
               (if ff[] <> none then
                   ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                else
                   'findForm: ff is none!'->putLine
               if)
            #);
          findGroup:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            do
               (if fg[] <> none then
                   fg.fullName->browser.selectFragmentGroup
                else
                   'findGroup: fg is none!'->putLine
               if)
            #);
          externalInterface: (* Global commands *) @
            (#
               externalInterfaceException: exception
                 (# m: ^text
                 enter m[]
                 <<SLOT externalInterfaceException:DoPart>>
                 #);
               selectEditor:
                 (#
                    fe:
                      ^ymerBrowserWindow.codeeditor
                 enter fe[]
                 <<SLOT externalInterfaceSelectEditor:DoPart>>
                 #);
               openForm:
                 (#
                    name: ^text;
                    node:
                      ^edenv.mps.
                         ast;
                    fe: ^ymerBrowserWindow.codeEditor;
                    NoEditorForForm:< externalInterfaceException;
                    couldNotOpenFragmentform:< externalInterfaceException
                 enter (name[],node[])
                 <<SLOT externalInterfaceOpenForm:DoPart>>
                 exit fe[]
                 #);
               getEditorForForm:
                 (#
                    ff: ^edenv.mps.fragmentForm;
                    node: ^edenv.mps.ast;
                    fe: ^ymerBrowserWindow.codeEditor;
                    NoEditorForForm:< externalInterfaceException
                 enter (ff[],node[])
                 <<SLOT externalInterfaceGetEditorForForm:DoPart>>
                 exit fe[]
                 #);
               newBETAProgram:
                 (# name: ^text; NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETAProgram:DoPart>>
                 #);
               newBETALibrary:
                 (#
                    name: ^text;
                    NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETALibrary:DoPart>>
                 #);
               saveAndQuit:
                 (# 
                 <<SLOT externalInterfaceSaveAndQuit:DoPart>>
                 #);
               save: (#  <<SLOT externalInterfaceSave:DoPart>> #);
               autoSave:
                 (# 
                 <<SLOT externalInterfaceAutoSave:DoPart>>
                 #);
               check:
                 (# fg: ^edenv.mps.fragmentGroup
                 enter fg[]
                 <<SLOT externalInterfaceCheck:DoPart>>
                 #);
               toggleDebug:
                 (# 
                 <<SLOT externalInterfaceToggleDebug:DoPart>>
                 #);
               addProp:
                 (#
                    FG: ^astInterface.fragmentGroup;
                    propName,propString: ^text;
                    ifExists:<
                      (# delete: @boolean
                      do true->delete; INNER
                      exit delete
                      #)
                 enter (FG[],propName[],propString[])
                 <<SLOT externalInterfaceAddProp:DoPart>>
                 #);
               deleteProp:
                 (#
                    FG: ^astInterface.fragmentGroup;
                    propName,propString: ^text;
                    notFound:< object
                 enter (FG[],propName[],propString[])
                 <<SLOT externalInterfaceDeleteProp:DoPart>>
                 #);
               setFrejaMark:
                 (#
                    frejamark: @boolean;
                    FG: ^astInterface.fragmentGroup;
                    propvalue: @integer;
                    
                 enter (frejamark,FG[])
                 do
                    (if frejamark then 1->propValue else 0->propValue if);
                    'frejamark'
                      ->FG.prop.addprop (#  do propValue->addConst #);
                    
                 #);
               getFrejaMark:
                 (#
                    FG: ^astInterface.fragmentGroup;
                    GetProp:
                      (#
                         FG: ^astInterface.fragmentGroup;
                         P: @Text;
                         res: @integer;
                         Txt: ^Text;
                         
                      enter (P,FG[])
                      do
                         &Text[]->Txt[];
                         L: FG.prop.scanProp
                           (#
                              doProp::<
                                (# 
                                do
                                   (if (P[]->Prop.equal) then
                                       scanParameters
                                         (#
                                            doConst::<
                                              (#  do C->res; leave L #);
                                            doString::<
                                              (#  do S[]->Txt[]; leave L #)
                                         #);
                                       
                                   if);
                                   
                                #);
                              
                           #);
                         INNER (* ugly *)
                      exit res
                      #);
                    
                 enter FG[]
                 exit (1 = (('frejamark',FG[])->getProp))
                 #);
               
            #);
          externalTextEdit:<
            (# textBefore,textAfter: ^text; doneInInner: @boolean
            enter textBefore[]
            do
               INNER ;
               (if not doneInInner then
                   <<SLOT externalTextEdit:Descriptor>>
               if)
            exit textAfter[]
            #);
          lastCompiledFg,lastCheckedFg:
            ^astInterface.fragmentGroup;
          compilerPrivate:
            @<<SLOT compilerPrivate:Descriptor>>;
          activateCompiler:
            (#
               fragmentFile: ^text;
               generateCode,generateDebugInfo,OK: @boolean
            enter (fragmentFile[],generateCode,generateDebugInfo)
            do <<SLOT activateCompilerBody:Descriptor>>
            exit OK
            #);
          initCompiler:
            (# version: ^text
            do
               <<SLOT initCompilerBody:Descriptor>>
            exit version[]
            #);
          formsWithSemanticErrors: @list
            (# element::< mps.ast.fragmentForm #);
          savedGroups: @set (# element::< mps.ast.fragmentGroup;  #);
          callCompiler: doCommand
            (# exp: ^astInterface.expanded; node: ^astInterface.ast
            do
               (if theSemanticErrorViewer[] <> none then
                   (if theSemanticErrorViewer.opened then
                       none ->theSemanticErrorViewer.registerFragmentGroup
                   if)
               if);
               ''->writeInfoView;
               formsWithSemanticErrors.clear;
               'Calling BETA compiler'->putLine;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               INNER ;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               (if formsWithSemanticErrors.size > 0 then
                   l: formsWithSemanticErrors.scan
                     (# 
                     do
                        none ->node[];
                        'Semantic Error(s)'->writeInfoView;
                        (if current.root.kind = mps.ast.kinds.interior then
                            current.root[]->exp[];
                            loop: exp.suffixWalk
                              (# 
                              do
                                 (if current.hasSemanticError then
                                 (*current.semanticError->semanticErrorText->t[];*)
                                     current[]->node[]; leave loop
                                 if)
                              #);
                            (if node[] <> none then node[]->findAndSelect if)
                        if);
                        current.father->showSemanticErrors;
                        leave l
                     #)
               if);
               
            #);
          fileChangedOutside: booleanValue
            (#
               fg: ^astInterface.fragmentGroup;
               diskfile,textfile: @file;
               t: @text
            enter fg[]
            do
               fg.diskfilename->diskfile.name;
               fg.textfilename->textfile.name;
               (if textfile.entry.exists and
               (textfile.entry.modTime > fg.modTime) then
               (* 'Source file: '->t.puttext;
                * fg.textfilename->t.putline;
                * 'changed since last access - reload?'->t.puttext;
                * (none ,'Notification',t[])
                *  ->promptForBoolean
                *      (# ok:: 
                *      (# do true->allowReload #);
                *    notok:: 
                *      (# do false->allowReload #);
                *    cancel:: 
                *       (# do false->allowReload #) 
                *  #)
                *)
                   t.clear;
                   'Source file: '->t.puttext;
                   fg.textfilename->t.putline;
                   'has been changed since last access!'->t.putline;
                   t.newLine;
                   'The file must have been changed by some other application.'
                     ->t.putline;
                   'Quit immediately (and restart) to avoid further problems'
                     ->t.puttext;
                   (none ,t[],'Warning')->alertUser;
                   true->value
                else
                   (if diskfile.entry.exists then
                   (* 'Group file: '->t.puttext;
                    * fg.diskfilename->t.putline;
                    * 'changed since last access - reload?'->t.puttext;
                    * (none ,'Notification',t[])
                    *   ->promptForBoolean
                    * (# ok:: (# do true->allowReload #);
                    *    notok:: (# do false->allowReload #);
                    *    cancel:: (# do false->allowReload #)
                    * #)
                    *)
                       (if (diskfile.entry.modTime > fg.modTime) then
                           t.clear;
                           'Group file: '->t.puttext;
                           fg.diskfilename->t.putline;
                           'has been changed since last access!'->t.putline;
                           t.newLine;
                           'The file must have been changed by some other application.'
                             ->t.putline;
                           'Quit immediately (and restart) to avoid further problems'
                             ->t.puttext;
                           (none ,t[],'Warning')->alertUser;
                           true->value
                       if)
                    else
                       checkIfOpen: edenv.groupEditorlist.scan
                         (# 
                         do
                            (if current.fg[] = fg[] then
                                current.groupTouched; 
                            if)
                         #);
                       t.clear;
                       'Group file: '->t.puttext;
                       fg.diskfilename->t.putline;
                       'has been deleted!'->t.putline;
                       t.newLine;
                       'Use the Save command to reestablish it.'->t.putline;
                       'Quit immediately (and restart) to avoid further problems'
                         ->t.puttext;
                       (none ,t[],'Warning')->alertUser;
                       true->value
                   if)
               if)
            #);
          nonterminalAllowed:< booleanValue
            (# nont: ^astinterface.unexpanded enter nont[] do INNER #);
          DGchangedOutside:
            (#
               changedOutside,otherError,nontExist: @boolean;
               fg,currentFG: ^astInterface.fragmentGroup;
               nont: ^astInterface.unexpanded;
               isOpen,keepDoRealOpen: @boolean;
               textFile: ^text;
               f: @file
            enter fg[]
            do
               scanningDG:
               fg.name->mps.AST.expandToFullPath
                 ->mps.DG.scanExtent
                   (#
                      warn: ^text;
                      Report:
                        (# msg: ^text
                        enter msg[]
                        do
                           (none ,msg[],'Dependency graph error')->alertUser;
                           true->otherError;
                           leave scanningDG
                        #);
                      startingParsingNotification:: (#  do  #);
                      reloadingNotification:: (#  do false->doReload #);
                      DoubleFormException:: (#  do true->continue #);
                      transAccessException::
                        (# 
                        do
                           'No read access to: '->warn[];
                           FN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      circularDependencyException::
                        (# 
                        do
                           'Circular dependency: '->warn[];
                           fg.name->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      unknownPropertyException:: (#  do true->continue #);
                      emptyFragmentException:: (#  do true->continue #);
                      parseException::
                        (# 
                        do
                           fullFN[]->openParseEditor;
                           true->continue;
                           true->otherError;
                           leave scanningDG
                        #);
                      transCreateDirException::
                        (# 
                        do
                           'Unable to create directory: '->warn[];
                           FN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      MPSexception::
                        (# 
                        do
                           'MPS overflow - '->warn[];
                           T[]->warn.puttext;
                           ' too large)'->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      notExistingException::
                        (# 
                        do
                           'Not a fragment file: '->warn[];
                           fullFN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      noSpaceException::
                        (# 
                        do
                           'No space on disk'->warn[];
                           true->continue;
                           warn[]->Report
                        #);
                      fragmentException::
                      (* Note that the group is closed here.
                       * Sif and other tools may NOT want to do this!
                       *)
                        (# keepMsg: ^text
                        do (*CloseGroup;*)
                           msg.copy->keepMsg[];
                           msg.clear;
                           (if errNo
                            // 1 then
                               'Two or more slots have the name: '->msg.puttext;
                               slot.name->msg.puttext;
                               
                            // 2 then
                               'No free slot found for: '->msg.Puttext;
                               FF.name->msg.puttext;
                               
                            // 3 then
                               'Category of fragment: '->msg.puttext;
                               FF.name->msg.puttext;
                               ' does not correspond to category of slot'
                                 ->msg.puttext;
                               
                            // 4 then
                               'The slot: '->msg.Puttext;
                               FF.name->msg.puttext;
                               ' Is already bound'->msg.Puttext
                            // 5 then
                               'Category of: '->msg.Puttext;
                               FF.name->msg.Puttext;
                               ' must be either: '->msg.Putline;
                               'Descriptor (ObjectDescriptor, DescriptorForm) or '
                                 ->msg.puttext;
                               'Attributes (AttributeDecl, AttributesForm)'
                                 ->msg.putline;
                               
                            // 6 then
                               'Category of the following slot is not supported!\n\n\t'
                                 ->msg.putText;
                               slot.name->msg.putLine;
                               '\nCategory must be: descriptor, attributes or doPart'
                                 ->msg.putLine
                            // 7 then
                               'Warning: mainPart-slots are NOT fully supported!'
                                 ->msg.putLine
                           if);
                           (if slot[] <> none then
                               msg[]->Report
                            else
                               (if ff[] <> none then
                                   msg[]->Report
                                else
                                   (if fg[] <> none then
                                       msg[]->Report
                                    else
                                       'fragmentException: '->putText;
                                       errNo->putInt;
                                       ': '->putLine;
                                       keepMsg[]->putLine;
                                       keepMsg[]->report
                                   if)
                               if)
                           if)
                        #);
                      propertyException::
                        (# keepMsg: ^text
                        do
                           msg.copy->keepMsg[];
                           msg.clear;
                           (if n
                            // 1 then
                               'Two or more origins are specified:\n\t'
                                 ->msg.puttext;
                                 (# g: ^astInterface.fragment; 
                                 do FG.origin->g[]; g.name->msg.puttext; 
                                 #);
                               '\n\t'->msg.puttext;
                               t[]->msg.putline
                            // 2 then
                               'The reserved property name "'->msg.puttext;
                               t.makeUC;
                               t[]->msg.puttext;
                               '"\n\tappears in the property list for '
                                 ->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               
                            // 3 then
                               'Illegal "'->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               '" property: "'->msg.puttext;
                               t.makeUC;
                               t[]->msg.puttext;
                               '"\n'->msg.puttext
                            // 4 then
                               'Empty filename in property "'->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               '"'->msg.put;
                               msg.newline;
                               
                            // 5 then
                               'Unrecognised property: '->Msg.Puttext;
                               p[]->Msg.Putline;
                               true->warning;
                               true->continue
                            // 6 then
                               'Property "'->msg.puttext;
                               p[]->msg.puttext;
                               '" has no value for "'->msg.putText;
                               t[]->msg.putText;
                               '"\n'->msg.putLine;
                               true->warning
                           if);
                           (if not warning then
                               '\n\n\tA ";" may be missing before '
                                 ->msg.puttext;
                               t[]->msg.putline;
                               (if fg[] <> none then
                                   'in '->msg.append;
                                   fg.name->msg.append;
                                   msg[]->Report
                                else
                                   'propertyException: '->putText;
                                   n->putInt;
                                   ': '->putLine;
                                   keepMsg[]->putLine;
                                   keepMsg[]->report
                               if);
                               
                           if)
                        #);
                      
                   do
                      current[]->currentFG[];
                      checkIfOpen: edenv.groupEditorlist.scan
                        (# 
                        do
                           (if current.fg[] = currentFG[] then
                               current.nonterminalsExist->(nontExist,nont[]);
                               (if nontExist then
                                   (if nont[]->nonterminalAllowed then
                                   (* continue *)
                                       
                                    else
                                       nont[]->findAndSelect;
                                       (none ,
                                        'This fragment group cannot be checked\nbefore all nonterminals have been removed',
                                        'Compiler message')->alertUser;
                                       true->otherError;
                                       leave scanningDG
                                   if);
                                   
                                else
                                   (true,false,true)->current.save
                               if);
                               
                           if)
                        #);
                      (if currentFG[]->fileChangedOutside then
                          true->changedOutSide;
                          leave scanningDG;
                          false->isOpen;
                          groupEditorListScan: edenv.groupEditorlist.scan
                            (# 
                            do
                               (if current.fg[] = currentFG[] then
                                   true->isOpen; leave groupEditorListScan
                               if)
                            #);
                          newLine;
                          currentFG.name->putText;
                          (if isOpen then
                              ' is open'->putLine; leave scanningDG
                           else
                              ' is not Open'->putLine;
                              mps.ast.doRealOpen->keepDoRealOpen;
                              true->mps.ast.doRealOpen;
                              fg.textFileName->textFile[]->f.name;
                              (if f.entry.exists then
                                  'xyzzyx'->textFile.append
                              if);
                              keepDoRealOpen->mps.ast.doRealOpen;
                              false->changedOutSide;
                              
                          if)
                      if);
                      
                   #)
            exit (changedOutside,otherError)
            #);
          check:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               changedOutSide,otherError: @boolean
            enter fg[]
            do
               'Calling checker...'->writeInfoView;
               thisOp: doCommand
                 (# 
                 do
                    fg[]->DGchangedOutSide->(changedOutSide,otherError);
                    (if not changedOutSide and not otherError then
                        callCompiler
                          (# 
                          do (fg.name,false,false)->activateCompiler->status; 
                          #);
                        (if edenv.frejaInterface[] <> none then
                            not status->edenv.frejaInterface.groupChecked
                        if);
                        (fg[],status)->onGroupChecked
                    if)
                 #)
            exit status
            #);
          compile:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               changedOutSide,otherError,nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               'Calling compiler...'->writeInfoView;
               thisOp: doCommand
                 (# 
                 do
                    fg[]->DGchangedOutSide->(changedOutSide,otherError);
                    (if not changedOutSide and not otherError then
                        callCompiler
                          (# 
                          do (fg.name,true,true)->activateCompiler->status; 
                          #);
                        (fg[],status)->onGroupCompiled
                    if)
                 #)
            exit status
            #);
          writeInfoView:
            (#
               t: ^text;
               myMsgPop:
                 (# 
                 do
                    (if browser.infoView.msgList.head <> none then
                        (browser.infoView.msgList.head).elm[]
                          ->browser.infoView.done;
                        browser.infoView.msgList.head
                          ->browser.infoView.msgList.delete
                    if)
                 #)
            enter t[]
            do
               (if t.length > 0 then
                   t[]->browser.infoView.msg; browser.infoView.msgPush
                else
                   ''->browser.infoView.msg
               if)
            #);
          writeInfoViewDone:
            (#
               myMsgPop:
                 (# 
                 do
                    (if browser.infoView.msgList.head <> none then
                        (browser.infoView.msgList.head).elm[]
                          ->browser.infoView.done;
                        browser.infoView.msgList.head
                          ->browser.infoView.msgList.delete
                    if)
                 #)
            do myMsgPop
            #);
          openParseEditor:
            (#
               pe: @parseEditor
                 (# open:: (#  do (600,800)->size; fn[]->loadFiles #)
                 #);
               fn: ^text
            enter fn[]
            do pe.open
            #);
          theSemanticErrorViewer: ^browser.semanticViewer;
          semanticErrorViewer: browser.semanticViewer
            (#
               open::
                 (# 
                 do
                    theGroupWithSemanticErrors[]->fg[];
                    theGroupWithSemanticErrors.name->fgName[];
                    
                 #)
            #);
          theGroupWithSemanticErrors: ^astInterface.fragmentGroup;
          showSemanticErrors:
            (# 
            enter theGroupWithSemanticErrors[]
            do
               (if theSemanticErrorViewer[] = none then
                   &semanticErrorViewer[]->theSemanticErrorViewer[];
                   theSemanticErrorViewer.open
                else
                   (if not theSemanticErrorViewer.opened then
                       &semanticErrorViewer[]->theSemanticErrorViewer[];
                       theSemanticErrorViewer.open
                    else
                       theGroupWithSemanticErrors[]
                         ->theSemanticErrorViewer.registerFragmentGroup;
                       theSemanticErrorViewer.show;
                       theSemanticErrorViewer.bringToFront
                   if)
               if)
            #);
          searchFor: @ (# t: ^text; caseSensitive,wholeWord: @boolean #);
          searchfg: ^astInterface.fragmentGroup;
          theSearchViewer: ^browser.searchViewer;
          searchViewer: browser.searchViewer
            (#
               checkNode::
                 (# lexText: ^astInterface.lexemText; help: ^text
                 do
                    (if
                    (searchFor.t[],node[],searchFor.caseSensitive,
                     searchFor.wholeWord)->edenv.inLexem then
                        true->value;
                        node[]->lexText[];
                        '...'->t[];
                        lexText.getText->t.append;
                        '...'->t.append
                    if)
                 #);
               setTitle::
                 (# 
                 do
                    (if noHits then
                        'No search hits in: '->t[]
                     else
                        &text[]->t[]; 'Search hits in: '->t.append
                    if);
                    fgName[]->t.append
                 #);
               setNodesListLabel:: (#  do 'Nodes'->t[] #);
               showSourceLine:: trueObject;
               open::
                 (# 
                 do
                    searchfg[]->fg[];
                    (if fg[] <> none then fg.name->fgName[] if);
                    
                 #)
            #);
          showSearchHits:
            (#
               fg: ^astInterface.fragmentGroup;
               t: ^text;
               caseSensitive,wholeWord: @boolean;
               searchArea: @integer
            enter (fg[],t[],caseSensitive,wholeWord,searchArea)
            do
               t[]->searchFor.t[];
               caseSensitive->searchFor.caseSensitive;
               wholeWord->searchFor.wholeword;
               (if searchArea = 3 (* search in current project *) then
                   none ->searchfg[]
                else
                   fg[]->searchfg[]
               if);
               (if theSearchViewer[] = none then
                   &searchViewer[]->thesearchViewer[]; thesearchViewer.open
                else
                   (if not thesearchViewer.opened then
                       &searchViewer[]->thesearchViewer[];
                       thesearchViewer.open;
                       searchfg[]->thesearchViewer.registerFragmentGroup
                    else
                       searchfg[]->thesearchViewer.registerFragmentGroup;
                       thesearchViewer.show;
                       thesearchViewer.bringToFront
                   if)
               if)
            #);
          recoverGroup: doCommand
            (#
               ge: ^editorEnv.groupEditor;
               g: ^astInterface.fragmentGroup;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->g.restoreBackup;
                   ge.groupTouched;
                   (false,true,true)->ge.save;
                   ge.resetGroupTouch;
                   browser.currentGroup.close;
                   true->edenv.onGoingRecovery;
                   groupName[]->browser.selectFragmentGroup;
                   (if browser.cge[] = none then
                       'no groupEditor, should not happen'->putLine
                    else
                       browser.cge.groupTouched;
                       false->browser.cge.autoSaveFileExists
                   if);
                   false->edenv.onGoingRecovery;
                   
               if);
               
            #);
          revertGroup: doCommand
            (#
               g: ^astInterface.fragmentGroup;
               ge: ^editorEnv.groupEditor;
               f: @file;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->((g.diskFileName).copy).Append->f.name;
                   (if f.entry.exists then f.delete if);
                   ge.resetGroupTouch;
                   edenv.showEditors;
                   browser.currentGroup.close;
                   edenv.showEditors;
                   groupName[]->browser.selectFragmentGroup;
                   edenv.showEditors;
                   
               if);
               
            #);
          Edit: @codeEditorEditMenu
            (#
               formEditorExists:: (#  do browser.cfe[]->cfe[] #);
               writeStatus:: (#  do t[]->writeInfoView #);
               open::
                 (# 
                 do
                 (* (if browser.cfe[] <> none then
                  THIS(codeEditorEditMenu)[]->browser.cfe.theEditMenu[]
                  if)*) 
                 #)
            #);
          View: @codeEditorViewMenu
            (#
               formEditorExists:: (#  do browser.cfe[]->cfe[] #);
               open::
                 (# 
                 do
                 (* (if browser.cfe[] <> none then
                  THIS(codeEditorViewMenu)[]->browser.cfe.theViewMenu[]
                  if)*) 
                 #)
            #);
          SLOTs: @codeEditorSLOTsMenu
            (#
               formEditorExists:: (#  do browser.cfe[]->cfe[] #);
               open::
                 (# 
                 do
                 (* (if browser.cfe[] <> none then
                  THIS(codeEditorSLOTsMenu)[]->browser.cfe.theSLOTsMenu[]
                  if)*) 
                 #)
            #);
          popupMenu: @codeEditorPopUpMenu
            (#
               formEditorExists:: (#  do browser.cfe[]->cfe[] #);
               open::
                 (# 
                 do
                 (* (if browser.cfe[] <> none then
                  THIS(codeEditorPopUpMenu)[]->browser.cfe.thePopUpMenu[]
                  if)*) 
                 #)
            #);
          onCodeViewOpen::
            (# 
            do (* this operation is called before the sifviewer is created *) 
            #);
          workspace::<
            (#
               menubarType::<
                 (#
                    editMenu: @codeEditorEditMenu
                      (#
                         formEditorExists::
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter useExternalTextEditor
                           do
                              open;
                              THIS(editMenu)[]->THIS(menubarType).append;
                              (*THIS(codeEditorEditMenu)[]
                               ->THIS(workspace).cfe.theEditMenu[]*)
                              
                           #)
                      #);
                    viewMenu: @codeEditorViewMenu
                      (#
                         formEditorExists::
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do
                              open;
                              THIS(viewMenu)[]->THIS(menubarType).append;
                              (* THIS(codeEditorViewMenu)[]
                               ->THIS(workspace).cfe.theViewMenu[]*)
                              
                           #)
                      #);
                    popUpMenu: @codeEditorPopUpMenu
                      (#
                         formEditorExists::
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do
                              open;
                              THIS(codeEditorPopUpMenu)[]
                                ->THIS(menubarType).append;
                              (* THIS(codeEditorPopUpMenu)[]
                               ->THIS(workspace).cfe.thePopUpMenu[]*)
                              
                           #)
                      #);
                    open::<
                      (# 
                      do
                         fileMenu.setup;
                         useExternalTexteditor->editMenu.setup;
                         isBETAsif->viewMenu.setup;
                         importMenu.setup;
                         scrollMenu.setup;
                         closeMenu.setup;
                         INNER ;
                         
                      #)
                 #)
            #);
          codeViewWindow::<
            (#
               menubarType::<
                 (#
                    editMenu: @codeEditorEditMenu
                      (#
                         formEditorExists:: (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter useExternalTextEditor
                           do
                              open;
                              THIS(editMenu)[]->THIS(menubarType).append;
                              (*THIS(codeEditorEditMenu)[]->fe.theEditMenu[]*)
                              
                           #)
                      #);
                    viewMenu: @codeEditorViewMenu
                      (#
                         formEditorExists:: (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do
                              open;
                              THIS(viewMenu)[]->THIS(menubarType).append;
                              (*THIS(codeEditorViewMenu)[]->fe.theViewMenu[]*)
                              
                           #)
                      #);
                    popUpMenu: @codeEditorPopUpMenu
                      (#
                         formEditorExists:: (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do
                              open;
                              THIS(codeEditorPopUpMenu)[]
                                ->THIS(menubarType).append;
                              (*  THIS(codeEditorPopUpMenu)[]->fe.thePopUpMenu[]*)
                              
                           #)
                      #);
                    open::<
                      (# 
                      do
                         fileMenu.setup;
                         useExternalTexteditor->editMenu.setup;
                         isBETAsif->viewMenu.setup;
                         buffersMenu.setup;
                         INNER
                      #)
                 #)
            #);
          menubarType::<
            (#
               GroupFile: @groupFileMenuType;
               groupFileMenuType:< simplemenu
                 (#
                    iNew: @item
                      (#
                         onStatus:: (#  do true->value #);
                         onSelect::
                           (#
                              fg: ^astInterface.fragmentGroup;
                              created: @createDialog
                                (#
                                   create::
                                     (# 
                                     do
                                        (browser[],grammar[],groupName[],
                                         formName[])->edenv.new->fg[];
                                        (if fg[] <> none then
                                            fg.fullName->addproject;
                                            (*(browser[],fg[],none )->updateBrowser*)
                                            
                                        if);
                                        
                                     #);
                                   cancel:: (#  do  #);
                                   
                                #)
                           do
                              created.open;
                              (none ,'Create new group','untitled','untitled')
                                ->created.popup
                           #)
                      #);
                    iNewBetaProgram: @item
                      (#
                         onStatus::
                           (#  do not checkOngoingTextediting->value #);
                         onSelect::
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaProgram
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iNewBetaLibrary: @item
                      (#
                         onStatus::
                           (#  do not checkOngoingTextEditing->value #);
                         onSelect::
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaLibrary
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iClose: @item
                      (#
                         onStatus::
                           (# 
                           do
                              groupEditorExists and not checkOngoingTextediting
                              and not browser.cge.isAfrejaEditor->value;
                              
                           #);
                         onSelect::
                           (# g: ^astInterface.fragmentGroup
                           do
                              browser.cge.fg[]->g[];
                              (*g.close;
                               'NB! g.close'->putLine;*)
                              browser.currentGroup.close
                           #)
                      #);
                    iSave: @item
                      (#
                         onStatus:: (#  do allowCommand->value #);
                         onSelect::
                           (#  do (true,false,true)->browser.cge.save #)
                      #);
                    iSaveAs: @item
                      (#
                         onStatus::
                           (# 
                           do
                              groupEditorExists and not
                              browser.cge.isAfrejaEditor->value
                           #);
                         onSelect::
                           (# newName: ^text
                           do
                              browser.cge.saveAs->newName[];
                              (if newName[] <> none then
                                  browser.currentGroup.close;
                                  newName[]->browser.selectFragmentGroup
                              if)
                           #)
                      #);
                    iSaveAll: @item
                      (#
                         askSave:
                           (#
                              ge: ^editorenv.groupEditor;
                              help: ^text;
                              cancelled,done: @boolean
                           enter ge[]
                           do
                              'Save changes to '->help[];
                              ge.fg.name->help.append;
                              '?'->help.append;
                              (THIS(ymerBrowserWindow)[],'Save group',help[])
                                -> (*UI.*) promptForBoolean
                                  (#
                                     done: @boolean;
                                     ok::<
                                       (# 
                                       do
                                          (if not done then
                                              hide;
                                              true->done;
                                              (true,false,true)->ge.save
                                          if)
                                       #);
                                     notOK::<
                                       (# 
                                       do
                                          (if not done then
                                              hide;
                                              true->done;
                                              ge.fg[]->onGroupNotSaved
                                          if)
                                       #);
                                     cancel::<
                                       (# 
                                       do
                                          (if not done then
                                              hide; true->done; true->cancelled
                                          if)
                                       #);
                                     
                                  #)
                           exit cancelled
                           #);
                         onStatus:: (#  do true->value #);
                         onSelect::
                           (# 
                           do
                           (*edenv.saveAll; temporarily placed here to be able 
                            to specify the fatherwindow for the dialog
                            in order to avoid "moving" dialogs *)
                              l: edenv.groupEditorList.scan
                                (# cancelled: @boolean
                                do
                                   (if (current.touched > 0) and not
                                   current.isAfrejaEditor then
                                       current[]->askSave->cancelled
                                    else
                                       doCommand
                                         (# 
                                         do
                                            (if current.needToUpdateTextFile and
                                            not current.isAFrejaEditor then
                                                current.fg[]
                                                  ->edenv.makeNewTextFile
                                            if)
                                         #)
                                   if);
                                   (if cancelled then leave l if)
                                #)
                           #)
                      #);
                    iSaveAbstract: @item
                      (#
                         onStatus:: (#  do groupEditorExists->value #);
                         onSelect:: (#  do browser.cge.saveabstract #)
                      #);
                    iRecover: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowCommand then
                                  browser.cge.autoSaveFileExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              (THIS(ymerBrowserWindow)[],'Recover group',
                               'This command will also save the recovered fragment group\nDo you want to recover?')
                                ->promptForBoolean
                                  (#
                                     ok::<
                                       (# 
                                       do browser.cge[]->recoverGroup
                                       #);
                                     
                                  #)
                           #)
                      #);
                    iRevert: @item
                      (#
                         f: @file;
                         onStatus::
                           (# 
                           do
                              (if allowCommand then
                              (* '~'->((browser.cge.fg.diskFileName).copy).Append->f.name;
                               f.entry.exists->value*)
                                  browser.cge.touched > 0->value
                               else
                                  false->value
                              if);
                              (* temporarily disabled *)
                              false->value
                           #);
                         onSelect:: (#  do browser.cge[]->revertGroup #)
                      #);
                    iSetIsReadOnly: @item
                      (#
                         onStatus::
                           (# 
                           do
                              groupeditorExists and not checkOngoingTextediting
                                ->value;
                              (if groupeditorExists then
                                  browser.cge.isReadOnly->checked;
                                  (if browser.cge.isReadOnly then
                                      'Write protected'->writeInfoView
                                   else
                                      ''->writeInfoView
                                  if)
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              (if browser.cge.isReadOnly then
                                  (if browser.cge.groupFileWriteable then
                                      false->browser.cge.setIsReadOnly;
                                      ''->writeInfoView
                                  if)
                               else
                                  true->browser.cge.setIsReadOnly;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    iSetGlobalWriteProtection: @item
                      (#
                         onStatus::
                           (# 
                           do
                              edenv.globalWriteProtection->checked;
                              (if edenv.globalWriteProtection then
                                  'Write protected'->writeInfoView
                               else
                                  ''->writeInfoView
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              (if edenv.globalWriteProtection then
                                  false->edenv.setGlobalWriteProtection;
                                  ''->writeInfoView
                               else
                                  true->edenv.setGlobalWriteProtection;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    
                 #);
               GroupEdit: @simplemenu
                 (#
                    iUndo: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowPerform then
                                  browser.cge.askUndoPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: (#  do browser.cge.undo;  #)
                      #);
                    iCut: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# brotherff: ^astInterface.fragmentForm
                           do
                              browser.cfe.frag[]->browser.cge.cutForm
                                ->brotherff[];
                              browser.cfe.frag[]->browser.currentGroup.close;
                              (if brotherff[] <> none then
                                  ((brotherff.father).fullName,brotherff.name)
                                    ->browser.selectFragmentForm
                              if);
                              
                           #)
                      #);
                    iCopy: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# 
                           do browser.cfe.frag[]->browser.cge.copyForm
                           #)
                      #);
                    iPaste: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  (if formEditorExists then
                                      browser.cfe.frag[]
                                        ->browser.cge.askPastePermission->value
                                   else
                                      false->value
                                  if)
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              browser.cfe.frag[]->browser.cge.pasteForm
                                ->findForm;
                              
                           #)
                      #);
                    iAppend: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowPerform then
                                  browser.cge.askPastePermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do none ->browser.cge.pasteForm->ff[]->findForm; 
                           #)
                      #);
                    iInsertDoPartFragmentForm: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],31)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iInsertDescriptorFragmentForm: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],2)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iInsertAttributesFragmentForm: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],3)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iEditProperties: @item
                      (#
                         onStatus:: (#  do allowPerform->value #);
                         onSelect:: (#  do browser.cge.editProperties;  #)
                      #);
                    iEditFormName: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# 
                           do browser.cfe.frag[]->browser.cge.editFormName
                           #)
                      #);
                    iSearch: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (edenv[] <> none ) and not edenv.textediting
                                ->value
                           #);
                         onSelect::
                           (# 
                           do
                              (browser.cge[],browser.cfe[],none ,none ,none )
                                ->edenv.searchTextDialog
                                  (#
                                     found::
                                       (# 
                                       do
                                          (if edenv.trace[12] then
                                              'found'->putLine
                                          if);
                                          foundNode[]->findandselect
                                       #);
                                     notfound::
                                       (# help: ^text
                                       do
                                          ascii.bel->put;
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          (if edenv.trace[12] then
                                              help[]->putLine
                                          if);
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    iReplace: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (edenv[] <> none ) and not edenv.textediting
                                ->value
                           #);
                         onSelect::
                           (# 
                           do
                              (browser.cge[],browser.cfe[],none ,none ,none )
                                ->edenv.replaceTextDialog
                                  (#
                                     found::
                                       (# 
                                       do
                                          (if edenv.trace[12] then
                                              'found'->putLine
                                          if);
                                          foundNode[]->findandselect
                                       #);
                                     notfound::
                                       (# help: ^text
                                       do
                                          ascii.bel->put;
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          (if edenv.trace[12] then
                                              help[]->putLine
                                          if);
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    open::
                      (# 
                      do
                         'Undo'->iUndo.new;
                         newSeparator;
                         'Cut'->iCut.new;
                         'Copy'->iCopy.new;
                         'Paste Before'->iPaste.new;
                         'Paste At End'->iAppend.new;
                         newSeparator;
                         'New DoPart Fragment Form...'
                           ->iInsertDoPartFragmentForm.new;
                         'New Descriptor Fragment form...'
                           ->iInsertDescriptorFragmentForm.new;
                         'New Attributes Fragment Form...'
                           ->iInsertAttributesFragmentForm.new;
                         newSeparator;
                         ('Edit Properties...','m')->iEditProperties.newKey;
                         'Edit Name...'->iEditFormName.new;
                         newSeparator;
                         ('Search...','4')->iSearch.newKey;
                         ('Replace...','5')->iReplace.newKey
                      #)
                 #);
               GroupPerform: @groupPerformMenuType;
               groupPerformMenuType:< simplemenu
                 (#
                    iCheck: @item
                      (#
                         onStatus::
                           (# 
                           do allowPerform and not edenv.textediting->value
                           #);
                         onSelect::
                           (# 
                           do
                              browser.cge.fg[]->lastCheckedFg[];
                              browser.cge.fg[]->check
                           #)
                      #);
                    iReCheck: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowPerform and not edenv.textediting then
                                  (lastCheckedFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: (#  do lastCheckedFg[]->check #)
                      #);
                    iCompile: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowPerform and not edenv.textediting then
                                  false->value;
                                  loop: browser.cge.fg.fragmentlist.scan
                                    (# 
                                    do
                                       (if (current.type = mps.ast.formType) and
                                       ('program'->current.name.equalNCS) then
                                           true->value; leave loop; 
                                       if)
                                    #)
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              browser.cge.fg[]->lastCompiledFg[];
                              browser.cge.fg[]->compile
                           #)
                      #);
                    iRecompile: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowPerform and not edenv.textediting then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: (#  do lastCompiledFg[]->compile #)
                      #);
                    iExecute: @item
                      (#
                         onStatus::
                           (#  do false-> (*allowCommand*) value #);
                         onSelect:: (#  do  #)
                      #);
                    iReExecute: @item
                      (#
                         onStatus::
                           (#  do false-> (*allowCommand*) value #);
                         onSelect:: (#  do  #)
                      #);
                    iCompileAndExecute: @item
                      (#
                         onStatus::
                           (#  do false-> (*allowCommand*) value #);
                         onSelect::
                           (# 
                           do
                              browser.cge.fg[]->lastCompiledFg[];
                              browser.cge.fg[]->compile
                           #)
                      #);
                    iRecompileAndExecute: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if false (*allowCommand*) then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: (#  do lastCompiledFg[]->compile #)
                      #);
                    iDebug: @item
                      (#
                         onStatus::
                           (#  do false (*groupEditorExists*) ->value #);
                         onSelect:: (#  do  #)
                      #);
                    iSemanticErrors: @item
                      (#
                         onStatus:: (#  do allowPerform->value #);
                         onSelect::
                           (#  do browser.cge.fg[]->showSemanticErrors #)
                      #);
                    iShowDiagram: @item
                      (#
                         onStatus::
                           (# 
                           do
                              false->value;
                              (if formEditorExists and not
                              browser.cfe.textEditMode then
                                  (edenv.frejaInterface[] <> none )->value
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              doCommand
                                (# 
                                do
                                   (browser.cfe.frag[],browser.cfe.cs.node[])
                                     ->edenv.frejaInterface.openForm
                                #)
                           #)
                      #);
                    
                 #);
               Test: @TestMenuType;
               testMenuType:< simplemenu
                 (#
                    iuseExternalTextEditor: @item
                      (#
                         onStatus::
                           (# 
                           do true->value; edenv.useExternalTextEditor->checked
                           #);
                         onSelect::
                           (# 
                           do
                              not edenv.useExternalTextEditor
                                ->edenv.useExternalTextEditor
                           #)
                      #);
                    iEventTrace: @item
                      (#
                         onStatus::
                           (#  do true->value; edenv.trace[7]->checked #);
                         onSelect::
                           (# 
                           do
                              not edenv.switch[7]->edenv.switch[7];
                              not edenv.trace[7]->edenv.trace[7]
                           #)
                      #);
                    iTraceStart: @item
                      (#
                         onStatus::
                           (#  do true->value; edenv.trace[2]->checked #);
                         onSelect::
                           (#  do not edenv.trace[2]->edenv.trace[2] #)
                      #);
                    iUndoTrace: @item
                      (#
                         onStatus::
                           (#  do true->value; edenv.trace[13]->checked #);
                         onSelect::
                           (#  do not edenv.trace[13]->edenv.trace[13] #)
                      #);
                    iTraceGetNextComment: @item
                      (#
                         isSet: @boolean;
                         onStatus:: (#  do true->value; isSet->checked #);
                         onSelect::
                           (# 
                           do
                              (mps.ast.trace.getNextComment,true)
                                ->mps.ast.trace.set;
                              true->isSet
                           #)
                      #);
                    iTraceSearch: @item
                      (#
                         onStatus::
                           (#  do true->value; edenv.trace[12]->checked #);
                         onSelect::
                           (#  do not edenv.trace[12]->edenv.trace[12] #)
                      #);
                    iReplace: @item
                      (#
                         onStatus:: (#  do true->value #);
                         onSelect::
                           (# 
                           do
                              (browser.cge[],browser.cfe[],none ,none ,none )
                                ->edenv.replaceTextDialog
                                  (#
                                     found::
                                       (# 
                                       do
                                          (if edenv.trace[12] then
                                              'found'->putLine
                                          if);
                                          foundNode[]->findandselect
                                       #);
                                     notfound::
                                       (# help: ^text
                                       do
                                          ascii.bel->put;
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          (if edenv.trace[12] then
                                              help[]->putLine
                                          if);
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    iTest: @item
                      (#
                         onStatus::
                           (#  do true (*groupEditorExists*) ->value #);
                         onSelect::
                           (# 
                           do
                              (if browser.cge[] <> none then
                                  (browser.cge.fg[],'INCLUDE','foolib')
                                    ->externalInterface.addprop
                              if)
                           #)
                      #);
                    iTest2: @item
                      (#
                         onStatus::
                           (#  do true (*groupEditorExists*) ->value #);
                         onSelect::
                           (# 
                           do
                              (if browser.cge[] <> none then
                                  (browser.cge.fg[],'INCLUDE','foolib')
                                    ->externalInterface.deleteprop
                              if)
                           #)
                      #);
                    iFreja: @item
                      (#
                         onStatus::
                           (#  do true (*groupEditorExists*) ->value #);
                         onSelect::
                           (# 
                           do
                              (if browser.cge[] <> none then
                                  (if browser.cfe[] <> none then
                                      true->browser.cfe.isAfrejaEditor;
                                      &(*edenv.*)frejaExternalInterface
                                        (# askBeforeTextediting:: trueObject
                                        #)[]->browser.edenv.frejaInterface[]
                                  if)
                              if)
                           #)
                      #);
                    open::<
                      (# 
                      do
                         'Use emacs'->iUseExternalTextEditor.new;
                         newSeparator;
                         'Trace start'->iTraceStart.new;
                         'Event trace'->iEventTrace.new;
                         'Undotrace'->iUndoTrace.new;
                         'Trace getnextComments'->iTraceGetNextComment.new;
                         newSeparator;
                         ('Replace','5')->iReplace.newKey;
                         'Trace search'->iTraceSearch.new;
                         newSeparator;
                         'Add prop'->iTest.new;
                         'Delete prop'->iTest2.new;
                         newSeparator;
                         'Freja mode'->iFreja.new;
                         INNER
                      #);
                    
                 #);
               open::<
                 (# 
                 do
                    fileMenu.setup;
                    projectMenu.setup;
                    'Group'->GroupFile.new;
                    useExternalTexteditor->Edit.useExternalTextEditor;
                    Edit.new;
                    Edit[]->append;
                    isBETAsif->View.BETAsif;
                    View.new;
                    View[]->append;
                    INNER ;
                    historyMenu.setup;
                    windowsMenu.setup
                 #)
            #)
       #);
     inithere: @boolean;
     isBETAsif:< booleanValue;
     useExternalTextEditor:< booleanValue;
     editorVersion: (#  exit '5.1(2)' #);
     compilerVersion:
       (# version: ^text
       <<SLOT sifCompilerVersion:DoPart>>
       exit version[]
       #);
     betaenvVersion:<
       (# t: ^text
       do
INCLUDE '~beta/basiclib/v1.6/betaenv'->t[];
          INNER
       exit t[]
       #);
     frejaInclude:<
       (# t: ^text
       do
INCLUDE '~beta/freja/v3.1/associations';
            ->t[];
          INNER
       exit t[]
       #);
     onAboutToCloseGroup:<
       (#
          fg: ^astInterface.fragmentGroup;
          OKtoClose,askSave: @boolean
          (* if OKtoClose the groupEditor is closed.
           * if askSave the user is prompted whether to save the group
           * else it is just saved (if necessary)
           *)
       enter fg[]
       do true->OKtoClose->askSave; INNER
       exit (OKtoClose,askSave)
       #);
     onGroupClosed:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupSaved:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupNotSaved:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupCompiled:<
       (#
          fg: ^astInterface.fragmentGroup;
          OK: @boolean (* OK means no errors *)
       enter (fg[],OK)
       do INNER
       #);
     onGroupChecked:<
       (#
          fg: ^astInterface.fragmentGroup;
          OK: @boolean (* OK means no errors *)
       enter (fg[],OK)
       do INNER
       #);
     init::<
       (# 
       do
          ymerBrowser.open;
          ymerBrowser.loadSettings;
          ymerBrowser.browser[]->ymerBrowser.ymerCallBack.theBrowser[];
          ymerBrowser.ymerCallback[]->ymerBrowser.edenv.ymerCallback[];
          INNER
       exit (mps.ast[],mps.betaCFL[])
       #);
     doTest,useExtTextEd,hiddenSif: @boolean;
     analyzeArg:
       (#
          arg,fgname,ffname: ^text;
          dashPos,dashPos2,slashPos: @integer;
          isAlphaNumeric: booleanValue
            (# t,s: ^text
            enter t[]
            do
               true->value;
               t.copy->s[];
               s.makeLC;
               loop: s.scanAll
                 (# 
                 do
                    (if not
                    (((ch >= 'a') and (ch <= 'z')) or
                     ((ch >= '0') and (ch <= '9'))) then
                        false->value; leave loop
                    if)
                 #)
            #)
       enter arg[]
       do
          0->dashPos->slashPos;
          ''->fgName[]->ffName[];
          '-'->arg.findAll (#  do inx->dashPos #);
          (if dashPos > 0 then
              (1,dashPos-1)->arg.sub->fgname[];
              '-'->fgname.findAll (#  do inx->dashPos2 #);
              (if dashPos2 > 0 then
                  ''->fgname[]
               else
                  (dashPos+1,arg.length)->arg.sub->ffName[];
                  (if not (ffname[]->isAlphaNumeric) then ''->ffname[] if)
              if)
           else
              arg[]->fgname[]
          if)
       exit (fgname[],ffname[])
       #);
     
  do init; INNER fragmentBrowser
  #)  

-- browserlib: Attributes --
searchBrowser: ymerLocalWindow
  (#
     checkNode:<
       (# node: ^astInterface.ast; value: @boolean; t: ^text
       enter node[]
       do INNER
       exit (value,t[])
       #);
     setTitle:<
       (# noHits: @boolean; t: ^text
       enter noHits
       do INNER ; t[]->title
       exit t[]
       #);
     setNodesListLabel:< (# t: ^text do INNER exit t[] #);
     showSourceLine:< booleanValue;
     clear: contents.clear (#  #);
     fg: ^mps.AST.fragmentGroup;
     fgName: ^text;
     (* isWarning: 
      *        {* findes i mps/v5.0.ess.ess.ess - blot her for at
      *         * kunne filtrere warnings fra allerede i
      *         * v5.0.1 - skal fjernes senere
      *         *}
      *        (# no: @integer; boo: @boolean
      *        enter no
      *        do
      *           (if no
      *            //24//25//86//95//97//99//106//109
      *            //26 {*not used *} then
      *               true->boo
      *           if)
      *        exit boo
      *        #);
      *)
     includeWarnings: @boolean;
     noErrors: @boolean;
     aspect:< integerValue (#  do 6->value; INNER #);
     markCurrentError: (#  do contents.errorsList.markCurrent #);
     unmarkCurrentError: (#  do contents.errorsList.unmarkCurrent #);
     contents: @contentsType;
     contentsType:< pane
       (#
          clear: (#  do FFlist.clear; errorsList.clear #);
          dobind:<
            (# 
            do
               TRUE->bindTop->bindLeft->bindRight->bindBottom;
               FFlist.dobind;
               errorsList.dobind;
               INNER dobind
            #);
          onNewFragmentGroup:<
            (# ff: ^mps.AST.fragmentForm; name: ^text; 
            enter (ff[],name[])
            do INNER onNewFragmentGroup
            #);
          onNewSemanticError:<
            (# node: ^mps.AST.ast; 
            enter node[]
            do INNER onNewSemanticError
            #);
          FFlist: @labelled
            (#
               clear: contents.clear (#  #);
               dobind: (#  do contents.dobind #);
               contentsType:: listView
                 (#
                    clear:
                      (# 
                      do
                         listitems.scanReverse
                           (#  do current.delete #);
                         listitems.clear;
                         
                      #);
                    current: ^listitems.theCellType;
                    dobind:
                      (# 
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom
                      #);
                    eventhandler::
                      (#
                         onKeyDown::
                           (# 
                           do
                              (if ch
                               // ascii.cr // ascii.nl then
                                  (if current[] = none then
                                      listitems.head->current[]; 
                                   else
                                      current.succ[]->current[];
                                      (if current[] = none then
                                          listitems.head->current[]
                                      if);
                                      
                                  if);
                                  (if current[] <> none then
                                      current.elm.select;
                                      selection.scrollIntoView
                                  if)
                              if)
                           #)
                      #);
                    fgItem: listItem
                      (#
                         ff: ^mps.AST.fragmentForm;
                         onSelect::
                           (# fi: ^listitems.theCellType
                           do
                              THIS(fgItem)[]->listitems.at->fi[];
                              (if fi[] <> current[] then
                                  fi[]->current[];
                                  (ff[],name)->onNewFragmentGroup;
                                  ff[]->errorsList.contents.registerNodes
                              if)
                           #);
                         
                      #);
                    registerFragmentForms:
                      (#
                         exp: ^mps.AST.expanded;
                         fi: ^fgItem;
                         fg: ^mps.AST.fragmentGroup;
                         ff: ^mps.AST.fragmentForm;
                         OK: @boolean
                      enter fg[]
                      do
                         true->noErrors;
                         none ->errorsList.contents.current[];
                         (if fg[] <> none then
                             fg.fragmentList.scan
                               (# 
                               do
                                  (if current.type
                                   // mps.AST.formType then
                                        (# 
                                        do
                                           current.open->ff[];
                                           (if ff[] <> none then
                                               (if ff.root.kind =
                                               mps.AST.kinds.interior then
                                                   ff.root[]->exp[];
                                                   l: exp.suffixWalk
                                                     (# t,dummy: ^text
                                                     do
                                                        current[]->checkNode
                                                          ->(OK,dummy[]);
                                                        (if OK then
                                                            &fgItem[]->fi[];
                                                            fi.init;
                                                            ff[]->fi.ff[];
                                                            fgName.copy->t[];
                                                            '-'->t.append;
                                                            ff.name->t.append;
                                                            t[]->fi.name;
                                                            leave l
                                                        if)
                                                     #)
                                               if)
                                            else
                                               'registerFragmentForms: could not open fragmentform'
                                                 ->putLine
                                           if)
                                        #)
                                  if)
                               #);
                             (if fi[] = none then
                                 ''->errorsList.label; true->noErrors; 
                              else
                                 setNodesListLabel->errorsList.label;
                                 false->noErrors;
                                 (listitems.head).elm.select
                             if)
                         if)
                      #);
                    open:: (#  do 'Fragment Forms'->label #);
                    
                 #);
               open::
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (w,(h div aspect)-1)->size;
                    
                 #);
               
            #);
          errorsList: @labelled
            (#
               clear: contents.clear (#  #);
               dobind: (#  do contents.dobind #);
               markCurrent:
                 (# t: ^text
                 do
                    (if contents.current[] <> none then
                        contents.current.elm.name->t[];
                        (if (1->t.inxget) <> '*' then
                            '*'->t.prepend; t[]->contents.current.elm.name
                        if)
                    if)
                 #);
               unmarkCurrent:
                 (# t: ^text
                 do
                    (if contents.current[] <> none then
                        contents.current.elm.name->t[];
                        (if (1->t.inxget) = '*' then
                            (1,1)->t.delete; t[]->contents.current.elm.name
                        if)
                    if)
                 #);
               contentsType:: listView
                 (#
                    clear:
                      (# 
                      do
                         listitems.scanReverse
                           (#  do current.delete #);
                         listitems.clear;
                         
                      #);
                    current: ^listitems.theCellType;
                    dobind:
                      (# 
                      do TRUE->bindTop->bindLeft->bindRight->bindBottom
                      #);
                    eventhandler::
                      (#
                         onKeyDown::
                           (# 
                           do
                              (if ch
                               // ascii.cr // ascii.nl then
                                  (if current[] = none then
                                      listitems.head->current[]; 
                                   else
                                      current.succ[]->current[];
                                      (if current[] = none then
                                          listitems.head->current[]
                                      if);
                                      
                                  if);
                                  (if current[] <> none then
                                      current.elm.select;
                                      selection.scrollIntoView;
                                      
                                  if)
                              if)
                           #)
                      #);
                    errorItem: listItem
                      (#
                         node: ^mps.AST.ast;
                         itemTextUpdated: @boolean;
                         onSelect::
                           (# currentPosition: @integer
                           do
                              THIS(errorItem)[]->listitems.at->current[];
                              position->currentposition;
                              node[]->onNewSemanticError;
                              (currentposition,false)->selection.select
                           #);
                         
                      #);
                    registerNodes:
                      (#
                         exp: ^mps.AST.expanded;
                         ei: ^errorItem;
                         ff: ^mps.AST.fragmentForm;
                         OK: @boolean;
                         t: ^text;
                         
                      enter ff[]
                      do
                         clear;
                         (if ff.root.kind = mps.AST.kinds.interior then
                             ff.root[]->exp[];
                             exp.suffixWalk
                               (# 
                               do
                                  current[]->checkNode->(OK,t[]);
                                  (if OK then
                                      &errorItem[]->ei[];
                                      ei.init;
                                      current[]->ei.node[];
                                      (if t[] <> none then
                                          t.copy->ei.name
                                      if)
                                  if)
                               #)
                         if);
                         (if ei[] = none then
                             true->noErrors
                          else
                             (listitems.head).elm.select
                         if)
                      #);
                    open:: (#  do setNodesListLabel->label #);
                    
                 #);
               open::
                 (# w,h: @integer; 
                 do
                    THIS(searchBrowser).size->(w,h);
                    (w,(h div aspect)-1)->size;
                    
                 #)
            #);
          open::<
            (#
               setDefaults::
                 (#  do true->verticalStacking; 50->minsize; 2->panewidth #)
            do
               FFlist.open;
               errorsList.open;
               FFlist[]->appendMember;
               errorsList[]->appendMember;
               INNER open;
               appendsDone;
               dobind;
               
            #)
       #);
     menubarType::<
       (#
          fileMenu: @menu
            (#
               reloadItem: @menuItem
                 (#
                    eventHandler::
                      (# onSelect:: (#  do fg[]->registerFragmentGroup #)
                      #);
                    open:: (#  do 'r'->key; 'Reload'->name #)
                 #);
               quitItem: @menuItem
                 (#
                    eventHandler::
                      (# onSelect:: (#  do THIS(searchBrowser).close #)
                      #);
                    open:: (#  do 'w'->key; 'Close'->name #)
                 #);
               open::
                 (# 
                 do
                    'File'->name;
                    reloadItem.open;
                    reloadItem[]->append;
                    quitItem.open;
                    quitItem[]->append
                 #)
            #);
          open::< (#  do fileMenu.open; fileMenu[]->append #);
          
       #);
     registerFragmentGroup:
       (# fgSet: @set (# element:: mps.ast.fragmentgroup #)
       enter fg[]
       do
          clear;
          contents.FFlist.clear;
          (if fg[] <> none then
              fg.name->fgName[];
              fg[]->contents.FFlist.contents.registerFragmentForms;
              noErrors->setTitle
           else
              (if browser.currentProject[] <> none then
                  browser.currentProject.groups.scan
                    (# fg: ^astinterface.fragmentGroup
                    do
                       current.getfg->fg[];
                       (if fg[] <> none then
                           (if not (fg[]->fgSet.has) then
                               fg[]->fgSet.insert;
                               fg.name->fgName[];
                               fg[]
                                 ->
                                   contents.FFlist.contents.
                                     registerFragmentForms
                           if)
                       if)
                    #);
                  'Search hits in current project'->title
               else
                  'No current project'->title
              if)
          if);
          
       #);
     open::<
       (# w,h: @integer; 
       do hide; INNER open; contents.open; fg[]->registerFragmentGroup; show; 
       #)
  #);
searchViewer: searchBrowser
  (#
     aspect:: (#  do 2->value #);
     contentsType::<
       (#
          onNewFragmentGroup::<
            (# 
            do
               ((ff.father).fullname,ff.name)->browser.selectFragmentForm;
               INNER
            #);
          onNewSemanticError::<
            (# l1,c1,l2,c2: @integer
            do
               (node.frag.root[],node[],false)->browser.selectNode;
               (if showSourceLine then
                   (if browser.cfe[] <> none then
                       contents.errorslist.contents.listitems.scan
                         (# i: ^contents.errorslist.contents.errorItem
                         do
                            current[]->i[];
                            (if not i.itemTextUpdated then
                                (i.node[],1,0,0)->cfe.selectArea->(l1,c1,l2,c2);
                                (if not
                                ((l1 = 1) and (c1 = 1) and (l2 = 1) and
                                 (c2 = 1)) then
                                    l1->cfe.theSifTexteditor.getLine->i.name;
                                    true->i.itemTextUpdated
                                if)
                            if)
                         #)
                   if)
               if);
               (node.frag.root[],node[],false)->browser.selectNode
            #)
       #);
     open::< (#  do (500,300)->size; INNER open;  #);
     
  #)  

