ORIGIN '~beta/sourcebrowser/v1.1/ymer';
INCLUDE '~beta/guienv/v1.4/utils/simplemenu'
        '~beta/sourcebrowser/v1.1/private/parseEditor'
        '~beta/sourcebrowser/v1.1/private/semanticEditor'
        '~beta/dependency/v1.2/dependency'
        'ymercall'
        'createdialog';
BODY 'private/globalfrejacommbody'
     'private/globalexternalinterfacebody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- lib: Attributes --
fragmentBrowser: ymerApplication
  (#
     ymerBrowser: @ymerBrowserWindow;
     ymerBrowserWindow::< 
       (#
          browserTitle::<  (#  do ' and Editor'->title.append; INNER #);
          theSifTextEditor: ^sifTextEditor;
          cbg: (* current browser group *) ^browser.projects.group;
          cff:
          (* current fragment form, not used anymore only in oldfromeditorExists *)
          ^astInterface.fragmentform;
          ymerCallback: @ymerCall
            (#
               refreshGroup::  (#  do (cb[],fg[],none )->updateBrowser #);
               findAndSelect:: 
                 (#  do node[]->THIS(ymerBrowserWindow).findAndSelect #);
               getFragmentGroup:: 
                 (# 
                 do name[]->mps.fragmentGroupTable.getFragmentGroup->fg[]
                 #);
               check:: 
                 (#  do fg[]->THIS(ymerBrowserWindow).check->status #);
               betaenvVersion:: 
                 (#  do THIS(fragmentBrowser).betaenvVersion->t[] #);
               frejaInclude:: 
                 (#  do THIS(fragmentBrowser).frejaInclude->t[] #);
               machineType:: 
                 (#  do THIS(fragmentBrowser).machineType[]->t[] #);
               infoView::  (#  do t[]->writeInfoView #);
               infoViewDone::  (#  do writeInfoViewDone #);
               onAboutToCloseGroup:: 
                 (# 
                 do
                    fg[]->THIS(fragmentBrowser).onAboutToCloseGroup
                      ->(OKtoClose,askSave)
                 #);
               onGroupClosed:: 
                 (#  do fg[]->THIS(fragmentBrowser).onGroupClosed #);
               onGroupSaved:: 
                 (# t: ^text
                 do
                    fg[]->THIS(fragmentBrowser).onGroupSaved;
                    (if (browser.cge[] <> none ) and (browser.cge.fg[] = fg[])
                     then
                        browser.locationView.label->t[];
                        (' ',10)->t.inxPut;
                        t[]->browser.locationView.label
                    if)
                 #);
               onGroupNotSaved:: 
                 (#  do fg[]->THIS(fragmentBrowser).onGroupNotSaved #);
               onGroupTouched:: 
                 (# t: ^text
                 do
                    (if (browser.cge[] <> none ) and (browser.cge.fg[] = fg[])
                     then
                        browser.locationView.label->t[];
                        ('*',10)->t.inxPut;
                        t[]->browser.locationView.label
                    if)
                 #);
               externalTextEdit:: 
                 (# 
                 do
                    textBefore[]->THIS(ymerBrowserWindow).externalTextEdit
                      ->textAfter[]
                 #);
               setFrejaMark:: 
                 (#  do (frejaMark,fg[])->externalInterface.setFrejaMark #);
               getFrejaMark:: 
                 (#  do fg[]->externalInterface.getFrejaMark #)
            #);
          doCommand:
            (# 
            do edenv.setWaitCursor; INNER doCommand; edenv.setStructureCursor
            #);
          checkOngoingTextEditing:
            (# value: @boolean
            do
               (if browser.cfe[] <> none then
                   browser.cfe.texteditMode->value
               if)
            exit value
            #);
          formEditorExists:
            (# value: @boolean
            do
               (if browser.cfe[] <> none then
                   (if not browser.cfe.texteditMode then true->value;  if)
               if)
            exit value
            #);
          groupEditorExists:
            (# value: @boolean
            do (browser.cge[] <> none )->value
            exit value
            #);
          allowCommand:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and not
                   browser.cge.isReadOnly and not browser.cge.isAfrejaEditor
                    then
                       true->value
                   if)
               if)
            exit value
            #);
          allowPerform:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and not
                   browser.cge.isReadOnly then
                       true->value
                   if)
               if)
            exit value
            #);
          allowOnForm:
            (# value: @boolean
            do
               (if groupEditorExists and not browser.propertySelected then
                   (if not checkOngoingTextediting and not
                   browser.cge.isReadOnly then
                       true->value
                   if)
               if)
            exit value
            #);
          updateBrowser:
            (#
               fg: ^astInterface.fragmentGroup;
               ff: ^astInterface.fragmentForm;
               cb: ^object;
               browser: ^sourceBrowser;
               cbg: (* current browser group *) ^browser.projects.group
            enter (cb[],fg[],ff[])
            do
               (if (cb[] <> none ) and (cb## <= sourceBrowser##) then
                   cb[]->browser[];
                   browser.currentgroup[]->cbg[];
                   (if (cbg[] <> none ) and (cbg.fg[] = fg[]) then
                       (browser.currentProject[],none )->browser.setGroup;
                       (browser.currentProject[],cbg[])->browser.setGroup;
                       (if ff[] <> none then
                           (browser.currentProject[],cbg[],ff[])
                             ->browser.setFragmentForm
                       if)
                   if)
               if)
            #);
          findAndSelect:
            (# node: ^astInterface.ast
            enter node[]
            do
               (if node[] <> none then
                   (if browser.cfe[] <> none then
                       (if node.frag[] <> browser.cfe.frag[] then
                           ((node.frag.father).fullName,node.frag.name)
                             ->browser.selectFragmentForm;
                           (node[],1,0,0)->browser.cfe.setFocus
                        else
                           (node[],1,0,0)->browser.cfe.setFocus
                       if)
                    else
                       ((node.frag.father).fullName,node.frag.name)
                         ->browser.selectFragmentForm;
                       (node[],1,0,0)->browser.cfe.setFocus
                   if)
                else
                   'findAndSelect: node is none!'->putLine
               if)
            #);
          findForm:
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do
               (if ff[] <> none then
                   ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                else
                   'findForm: ff is none!'->putLine
               if)
            #);
          findGroup:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            do
               (if fg[] <> none then
                   fg.fullName->browser.selectFragmentGroup
                else
                   'findGroup: fg is none!'->putLine
               if)
            #);
          externComm: @<<SLOT externprivate:Descriptor>>;
          onExternCommand:
            (# command,status: ^text; 
            enter command[]
            do
               <<SLOT onGlobalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externCommand:
            (# command,status: ^text; 
            enter command[]
            do <<SLOT globalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externalInterface:
          (* Global commands *) @
            (#
               externalInterfaceException: exception
                 (# m: ^text
                 enter m[]
                 <<SLOT externalInterfaceException:DoPart>>
                 #);
               selectEditor:
                 (#
                    fe:
                      ^ymerBrowserWindow.codeeditor
                 enter fe[]
                 <<SLOT externalInterfaceSelectEditor:DoPart>>
                 #);
               openForm:
                 (#
                    name: ^text;
                    node:
                      ^edenv.mps.
                         ast;
                    fe: ^ymerBrowserWindow.codeEditor;
                    NoEditorForForm:< externalInterfaceException;
                    couldNotOpenFragmentform:< externalInterfaceException
                 enter (name[],node[])
                 <<SLOT externalInterfaceOpenForm:DoPart>>
                 exit fe[]
                 #);
               getEditorForForm:
                 (#
                    ff: ^edenv.mps.fragmentForm;
                    node: ^edenv.mps.ast;
                    fe: ^ymerBrowserWindow.codeEditor;
                    NoEditorForForm:< externalInterfaceException
                 enter (ff[],node[])
                 <<SLOT externalInterfaceGetEditorForForm:DoPart>>
                 exit fe[]
                 #);
               newBETAProgram:
                 (# name: ^text; NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETAProgram:DoPart>>
                 #);
               newBETALibrary:
                 (#
                    name: ^text;
                    NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETALibrary:DoPart>>
                 #);
               saveAndQuit:
                 (# 
                 <<SLOT externalInterfaceSaveAndQuit:DoPart>>
                 #);
               save: (#  <<SLOT externalInterfaceSave:DoPart>> #);
               autoSave:
                 (# 
                 <<SLOT externalInterfaceAutoSave:DoPart>>
                 #);
               check:
                 (# fg: ^edenv.mps.fragmentGroup
                 enter fg[]
                 <<SLOT externalInterfaceCheck:DoPart>>
                 #);
               toggleDebug:
                 (# 
                 <<SLOT externalInterfaceToggleDebug:DoPart>>
                 #);
               onInput: @
                 (# t: ^text enter t[] <<SLOT commFrejaOnInput:DoPart>> #);
               putline: @
                 (# t: ^text
                 enter t[]
                 <<SLOT commFrejaPutline:DoPart>>
                 #);
               setFrejaMark:
                 (#
                    frejamark: @boolean;
                    FG: ^astInterface.fragmentGroup;
                    propvalue: @integer;
                    
                 enter (frejamark,FG[])
                 do
                    (if frejamark then 1->propValue else 0->propValue if);
                    'frejamark'
                      ->FG.prop.addprop (#  do propValue->addConst #);
                    
                 #);
               getFrejaMark:
                 (#
                    FG: ^astInterface.fragmentGroup;
                    GetProp:
                      (#
                         FG: ^astInterface.fragmentGroup;
                         P: @Text;
                         res: @integer;
                         Txt: ^Text;
                         
                      enter (P,FG[])
                      do
                         &Text[]->Txt[];
                         L: FG.prop.scanProp
                           (#
                              doProp::< 
                                (# 
                                do
                                   (if (P[]->Prop.equal) then
                                       scanParameters
                                         (#
                                            doConst::< 
                                              (#  do C->res; leave L #);
                                            doString::< 
                                              (#  do S[]->Txt[]; leave L #)
                                         #);
                                       
                                   if);
                                   
                                #);
                              
                           #);
                         INNER (* ugly *)
                      exit res
                      #);
                    
                 enter FG[]
                 exit (1 = (('frejamark',FG[])->getProp))
                 #);
               
            #);
          handleFrejaCommand:
            (# t: @text
            enter t
            do <<SLOT commFrejaHandleFrejaCommand:Descriptor>>; 
            #);
          externalTextEdit:<
            (#
               textBefore,
                 textAfter: ^text
            enter textBefore[]
            do INNER
            exit textAfter[]
            #);
          lastCompiledFg: ^astInterface.fragmentGroup;
          compilerPrivate: @<<SLOT compilerPrivate:Descriptor>>;
          activateCompiler:
            (#
               fragmentFile: ^text;
               generateCode,
                 generateDebugInfo,OK: @boolean
            enter (fragmentFile[],generateCode,generateDebugInfo)
            do <<SLOT activateCompilerBody:Descriptor>>
            exit OK
            #);
          initCompiler:
            (# version: ^text
            do
               <<SLOT initCompilerBody:Descriptor>>
            exit version[]
            #);
          formsWithSemanticErrors: @list
            (# element::< mps.ast.fragmentForm #);
          savedGroups: @set (# element::< mps.ast.fragmentGroup;  #);
          callCompiler: doCommand
            (# exp: ^astInterface.expanded; node: ^astInterface.ast
            do
               (if theSemanticErrorViewer[] <> none then
                   (if theSemanticErrorViewer.opened then
                       none ->theSemanticErrorViewer.registerFragmentGroup
                   if)
               if);
               ''->writeInfoView;
               formsWithSemanticErrors.clear;
               'Calling BETA compiler'->putLine;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               INNER ;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               (if formsWithSemanticErrors.size > 0 then
                   l: formsWithSemanticErrors.scan
                     (# 
                     do
                        none ->node[];
                        'Semantic Error(s)'->writeInfoView;
                        (if current.root.kind = mps.ast.kinds.interior then
                            current.root[]->exp[];
                            loop: exp.suffixWalk
                              (# 
                              do
                                 (if current.hasSemanticError then
                                 (*current.semanticError->semanticErrorText->t[];*)
                                     current[]->node[]; leave loop
                                 if)
                              #);
                            (if node[] <> none then node[]->findAndSelect if)
                        if);
                        current.father->showSemanticErrors;
                        leave l
                     #)
               if);
               
            #);
          fileChangedOutside: booleanValue
            (#
               fg: ^astInterface.fragmentGroup;
               diskfile,textfile: @file;
               t: @text
            enter fg[]
            do
               fg.diskfilename->diskfile.name;
               fg.textfilename->textfile.name;
               (if textfile.entry.exists and
               (textfile.entry.modTime > fg.modTime) then
               (* 'Source file: '->t.puttext;
                * fg.textfilename->t.putline;
                * 'changed since last access - reload?'->t.puttext;
                * (none ,'Notification',t[])
                *  ->promptForBoolean
                *      (# ok:: 
                *      (# do true->allowReload #);
                *    notok:: 
                *      (# do false->allowReload #);
                *    cancel:: 
                *       (# do false->allowReload #) 
                *  #)
                *)
                   t.clear;
                   'Source file: '->t.puttext;
                   fg.textfilename->t.putline;
                   'has been changed since last access!'->t.putline;
                   t.newLine;
                   'The file must have been changed by some other application.'
                     ->t.putline;
                   'Quit immediately (and restart) to avoid further problems'
                     ->t.puttext;
                   (none ,t[],'Warning')->alertUser;
                   true->value
                else
                   (if diskfile.entry.exists and
                   (diskfile.entry.modTime > fg.modTime) then
                   (* 'Group file: '->t.puttext;
                    * fg.diskfilename->t.putline;
                    * 'changed since last access - reload?'->t.puttext;
                    * (none ,'Notification',t[])
                    *   ->promptForBoolean
                    * (# ok:: (# do true->allowReload #);
                    *    notok:: (# do false->allowReload #);
                    *    cancel:: (# do false->allowReload #)
                    * #)
                    *)
                       t.clear;
                       'Group file: '->t.puttext;
                       fg.diskfilename->t.putline;
                       'has been changed since last access!'->t.putline;
                       t.newLine;
                       'The file must have been changed by some other application.'
                         ->t.putline;
                       'Quit immediately (and restart) to avoid further problems'
                         ->t.puttext;
                       (none ,t[],'Warning')->alertUser;
                       true->value
                   if)
               if)
            #);
          DGchangedOutside:
            (#
               changedOutside,otherError: @boolean;
               fg,currentFG: ^astInterface.fragmentGroup;
               isOpen,keepDoRealOpen: @boolean;
               textFile: ^text;
               f: @file
            enter fg[]
            do
               scanningDG:
               fg.name->mps.AST.expandToFullPath
                 ->mps.DG.scanExtent
                   (#
                      warn: ^text;
                      Report:
                        (# msg: ^text
                        enter msg[]
                        do
                           (none ,msg[],'Dependency graph error')->alertUser;
                           true->otherError;
                           leave scanningDG
                        #);
                      DoubleFormException::  (#  do true->continue #);
                      transAccessException:: 
                        (# 
                        do
                           'No read access to: '->warn[];
                           FN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      circularDependencyException:: 
                        (# 
                        do
                           'Circular dependency: '->warn[];
                           fg.name->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      unknownPropertyException::  (#  do true->continue #);
                      emptyFragmentException::  (#  do true->continue #);
                      parseException:: 
                        (# 
                        do
                           fullFN[]->openParseEditor;
                           true->continue;
                           true->otherError;
                           leave scanningDG
                        #);
                      transCreateDirException:: 
                        (# 
                        do
                           'Unable to create directory: '->warn[];
                           FN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      MPSexception:: 
                        (# 
                        do
                           'MPS overflow - '->warn[];
                           T[]->warn.puttext;
                           ' too large)'->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      notExistingException:: 
                        (# 
                        do
                           'Not a fragment file: '->warn[];
                           fullFN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      noSpaceException:: 
                        (# 
                        do
                           'No space on disk'->warn[];
                           true->continue;
                           warn[]->Report
                        #);
                      fragmentException::
                      (* Note that the group is closed here.
                       * Sif and other tools may NOT want to do this!
                       *) 
                        (# keepMsg: ^text
                        do (*CloseGroup;*)
                           msg.copy->keepMsg[];
                           msg.clear;
                           (if errNo
                            // 1 then
                               'Two or more slots have the name: '->msg.puttext;
                               slot.name->msg.puttext;
                               
                            // 2 then
                               'No free slot found for: '->msg.Puttext;
                               FF.name->msg.puttext;
                               
                            // 3 then
                               'Category of fragment: '->msg.puttext;
                               FF.name->msg.puttext;
                               ' does not correspond to category of slot'
                                 ->msg.puttext;
                               
                            // 4 then
                               'The slot: '->msg.Puttext;
                               FF.name->msg.puttext;
                               ' Is already bound'->msg.Puttext
                            // 5 then
                               'Category of: '->msg.Puttext;
                               FF.name->msg.Puttext;
                               ' must be either: '->msg.Putline;
                               'Descriptor (ObjectDescriptor, DescriptorForm) or '
                                 ->msg.puttext;
                               'Attributes (AttributeDecl, AttributesForm)'
                                 ->msg.putline;
                               
                            // 6 then
                               'Category of the following slot is not supported!\n\n\t'
                                 ->msg.putText;
                               slot.name->msg.putLine;
                               '\nCategory must be: descriptor, attributes or doPart'
                                 ->msg.putLine
                            // 7 then
                               'Warning: mainPart-slots are NOT fully supported!'
                                 ->msg.putLine
                           if);
                           (if slot[] <> none then
                               msg[]->Report
                            else
                               (if ff[] <> none then
                                   msg[]->Report
                                else
                                   (if fg[] <> none then
                                       msg[]->Report
                                    else
                                       'fragmentException: '->putText;
                                       errNo->putInt;
                                       ': '->putLine;
                                       keepMsg[]->putLine;
                                       keepMsg[]->report
                                   if)
                               if)
                           if)
                        #);
                      propertyException:: 
                        (# keepMsg: ^text
                        do
                           msg.copy->keepMsg[];
                           msg.clear;
                           (if n
                            // 1 then
                               'Two or more origins are specified:\n\t'
                                 ->msg.puttext;
                                 (# g: ^astInterface.fragment; 
                                 do FG.origin->g[]; g.name->msg.puttext; 
                                 #);
                               '\n\t'->msg.puttext;
                               t[]->msg.putline
                            // 2 then
                               'The reserved property name "'->msg.puttext;
                               t.makeUC;
                               t[]->msg.puttext;
                               '"\n\tappears in the property list for '
                                 ->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               
                            // 3 then
                               'Illegal "'->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               '" property: "'->msg.puttext;
                               t.makeUC;
                               t[]->msg.puttext;
                               '"\n'->msg.puttext
                            // 4 then
                               'Empty filename in property "'->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               '"'->msg.put;
                               msg.newline;
                               
                            // 5 then
                               'Unrecognised property: '->Msg.Puttext;
                               p[]->Msg.Putline;
                               true->warning
                            // 6 then
                               'Property "'->msg.puttext;
                               p[]->msg.puttext;
                               '" has no value for "'->msg.putText;
                               t[]->msg.putText;
                               '"\n'->msg.putLine;
                               true->warning
                           if);
                           (if not warning then
                               '\n\n\tA ";" may be missing before '
                                 ->msg.puttext;
                               t[]->msg.putline;
                               
                           if);
                           (if fg[] <> none then
                               msg[]->Report
                            else
                               'propertyException: '->putText;
                               n->putInt;
                               ': '->putLine;
                               keepMsg[]->putLine;
                               keepMsg[]->report
                           if)
                        #);
                      
                   do
                   (* 'dpscan: '->putText;
                    current.name->putText;*)
                      current[]->currentFG[];
                      checkIfOpen: edenv.groupEditorlist.scan
                        (# 
                        do
                           (if current.fg[] = currentFG[] then
                               (true,false)->current.save; 
                           if)
                        #);
                      (if currentFG[]->fileChangedOutside then
                          true->changedOutSide;
                          leave scanningDG;
                          false->isOpen;
                          groupEditorListScan: edenv.groupEditorlist.scan
                            (# 
                            do
                               (if current.fg[] = currentFG[] then
                                   true->isOpen; leave groupEditorListScan
                               if)
                            #);
                          newLine;
                          currentFG.name->putText;
                          (if isOpen then
                              ' is open'->putLine; leave scanningDG
                           else
                              ' is not Open'->putLine;
                              mps.ast.doRealOpen->keepDoRealOpen;
                              true->mps.ast.doRealOpen;
                              fg.textFileName->textFile[]->f.name;
                              (if f.entry.exists then
                                  'xyzzyx'->textFile.append
                              if);
                              keepDoRealOpen->mps.ast.doRealOpen;
                              false->changedOutSide;
                              
                          if)
                      if);
                      
                   #)
            exit (changedOutside,otherError)
            #);
          check:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               changedOutSide,otherError,nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               'Calling compiler...'->writeInfoView;
               thisOp: doCommand
                 (# 
                 do
                    fg[]->DGchangedOutSide->(changedOutSide,otherError);
                    (if not changedOutSide and not otherError then
                        edenv.nonterminalsExist->(nontExist,nont[]);
                        (if nontExist then
                            nont[]->findAndSelect;
                            (none ,
                             'This fragment group cannot be checked\nbefore all nonterminals have been removed',
                             'Compiler message')->alertUser;
                            
                         else
                            callCompiler
                              (# 
                              do
                                 (fg.name,false,false)->activateCompiler
                                   ->status;
                                 
                              #);
                            (if edenv.frejaInterface[] <> none then
                                not status->edenv.frejaInterface.groupChecked
                            if)
                        if);
                        (fg[],status)->onGroupChecked
                    if)
                 #)
            exit status
            #);
          compile:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               changedOutSide,otherError,nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               'Calling compiler...'->writeInfoView;
               thisOp: doCommand
                 (# 
                 do
                    fg[]->DGchangedOutSide->(changedOutSide,otherError);
                    (if not changedOutSide and not otherError then
                        edenv.nonterminalsExist->(nontExist,nont[]);
                        (if nontExist then
                            nont[]->findAndSelect;
                            (none ,
                             'This fragment group cannot be compiled\nbefore all nonterminals have been removed',
                             'Compiler message')->alertUser
                         else
                            callCompiler
                              (# 
                              do
                                 (fg.name,true,true)->activateCompiler->status; 
                              #);
                            (* not used anymore? (formsWithSemanticErrors.size = 0)->status;*)
                            
                        if);
                        (fg[],status)->onGroupCompiled
                    if)
                 #)
            exit status
            #);
          writeInfoView:
            (#
               t: ^text;
               myMsgPop:
                 (# 
                 do
                    (if browser.infoView.msgList.head <> none then
                        (browser.infoView.msgList.head).elm[]
                          ->browser.infoView.done;
                        browser.infoView.msgList.head
                          ->browser.infoView.msgList.delete
                    if)
                 #)
            enter t[]
            do
               (if t.length > 0 then
                   t[]->browser.infoView.msg; browser.infoView.msgPush
                else
                   ''->browser.infoView.msg
               if)
            #);
          writeInfoViewDone:
            (#
               myMsgPop:
                 (# 
                 do
                    (if browser.infoView.msgList.head <> none then
                        (browser.infoView.msgList.head).elm[]
                          ->browser.infoView.done;
                        browser.infoView.msgList.head
                          ->browser.infoView.msgList.delete
                    if)
                 #)
            do myMsgPop
            #);
          openParseEditor:
            (#
               pe: @parseEditor
                 (# open::  (#  do (600,800)->size; fn[]->loadFiles #)
                 #);
               fn: ^text
            enter fn[]
            do pe.open
            #);
          theSemanticErrorViewer: ^browser.semanticViewer;
          semanticErrorViewer: browser.semanticViewer
            (#
               open:: 
                 (# 
                 do
                    theGroupWithSemanticErrors[]->fg[];
                    theGroupWithSemanticErrors.name->fgName[];
                    
                 #)
            #);
          theGroupWithSemanticErrors: ^astInterface.fragmentGroup;
          showSemanticErrors:
            (# 
            enter theGroupWithSemanticErrors[]
            do
               (if theSemanticErrorViewer[] = none then
                   &semanticErrorViewer[]->theSemanticErrorViewer[];
                   theSemanticErrorViewer.open
                else
                   (if not theSemanticErrorViewer.opened then
                       &semanticErrorViewer[]->theSemanticErrorViewer[];
                       theSemanticErrorViewer.open
                    else
                       theGroupWithSemanticErrors[]
                         ->theSemanticErrorViewer.registerFragmentGroup;
                       theSemanticErrorViewer.show;
                       theSemanticErrorViewer.bringToFront
                   if)
               if)
            #);
          recoverGroup: doCommand
            (#
               ge: ^editorEnv.groupEditor;
               g: ^astInterface.fragmentGroup;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->g.restoreBackup;
                   ge.groupTouched;
                   (false,true)->ge.save;
                   ge.resetGroupTouch;
                   browser.currentGroup.close;
                   true->edenv.onGoingRecovery;
                   groupName[]->browser.selectFragmentGroup;
                   (if browser.cge[] = none then
                       'no groupEditor, should not happen'->putLine
                    else
                       browser.cge.groupTouched;
                       false->browser.cge.autoSaveFileExists
                   if);
                   false->edenv.onGoingRecovery;
                   
               if);
               
            #);
          revertGroup: doCommand
            (#
               g: ^astInterface.fragmentGroup;
               ge: ^editorEnv.groupEditor;
               f: @file;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->((g.diskFileName).copy).Append->f.name;
                   (if f.entry.exists then f.delete if);
                   ge.resetGroupTouch;
                   edenv.showEditors;
                   browser.currentGroup.close;
                   edenv.showEditors;
                   groupName[]->browser.selectFragmentGroup;
                   edenv.showEditors;
                   
               if);
               
            #);
          Edit: @codeEditorEditMenu
            (#
               formEditorExists::  (#  do browser.cfe[]->cfe[] #);
               writeStatus::  (#  do t[]->writeInfoView #)
            #);
          View: @codeEditorViewMenu
            (# formEditorExists::  (#  do browser.cfe[]->cfe[] #) #);
          SLOTs: @codeEditorSLOTsMenu
            (# formEditorExists::  (#  do browser.cfe[]->cfe[] #) #);
          workspace:: 
            (#
               menubarType:: 
                 (#
                    editMenu: @codeEditorEditMenu
                      (#
                         formEditorExists:: 
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter useExternalTextEditor
                           do open; THIS(editMenu)[]->THIS(menubarType).append
                           #)
                      #);
                    viewMenu: @codeEditorViewMenu
                      (#
                         formEditorExists:: 
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do open; THIS(viewMenu)[]->THIS(menubarType).append
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         fileMenu.setup;
                         useExternalTexteditor->editMenu.setup;
                         isBETAsif->viewMenu.setup;
                         importMenu.setup;
                         scrollMenu.setup;
                         closeMenu.setup;
                         
                      #)
                 #)
            #);
          codeViewWindow:: 
            (#
               menubarType:: 
                 (#
                    editMenu: @codeEditorEditMenu
                      (#
                         formEditorExists::  (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter useExternalTextEditor
                           do open; THIS(editMenu)[]->THIS(menubarType).append
                           #)
                      #);
                    viewMenu: @codeEditorViewMenu
                      (#
                         formEditorExists::  (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do open; THIS(viewMenu)[]->THIS(menubarType).append
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         fileMenu.setup;
                         useExternalTexteditor->editMenu.setup;
                         isBETAsif->viewMenu.setup;
                         buffersMenu.setup
                      #)
                 #)
            #);
          menubarType::< 
            (#
               GroupFile: @groupFileMenuType;
               groupFileMenuType:< simplemenu
                 (#
                    iNew: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (#
                              fg: ^astInterface.fragmentGroup;
                              created: @createDialog
                                (#
                                   create:: 
                                     (# 
                                     do
                                        (browser[],grammar[],groupName[],
                                         formName[])->edenv.new->fg[];
                                        (if fg[] <> none then
                                            fg.fullName->addproject;
                                            (*(browser[],fg[],none )->updateBrowser*)
                                            
                                        if);
                                        
                                     #);
                                   cancel::  (#  do  #);
                                   
                                #)
                           do
                              created.open;
                              (none ,'Create new group','untitled','untitled')
                                ->created.popup
                           #)
                      #);
                    iNewBetaProgram: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextediting->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaProgram
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iNewBetaLibrary: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextEditing->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaLibrary
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iClose: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupEditorExists and not checkOngoingTextediting
                              and not browser.cge.isAfrejaEditor->value;
                              
                           #);
                         onSelect:: 
                           (# g: ^astInterface.fragmentGroup
                           do
                              browser.cge.fg[]->g[];
                              (*g.close;
                               'NB! g.close'->putLine;*)
                              browser.currentGroup.close
                           #)
                      #);
                    iSave: @item
                      (#
                         onStatus::  (#  do allowCommand->value #);
                         onSelect:: 
                           (#  do (true,false)->browser.cge.save #)
                      #);
                    iSaveAs: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupEditorExists and not
                              browser.cge.isAfrejaEditor->value
                           #);
                         onSelect:: 
                           (# newName: ^text
                           do
                              browser.cge.saveAs->newName[];
                              (if newName[] <> none then
                                  browser.currentGroup.close;
                                  newName[]->browser.selectFragmentGroup
                              if)
                           #)
                      #);
                    iSaveAll: @item
                      (#
                         askSave:
                           (#
                              ge: ^editorenv.groupEditor;
                              help: ^text;
                              cancelled,done: @boolean
                           enter ge[]
                           do
                              'Save changes to '->help[];
                              ge.fg.name->help.append;
                              '?'->help.append;
                              (THIS(ymerBrowserWindow)[],'Save group',help[])
                                -> (*UI.*) promptForBoolean
                                  (#
                                     done: @boolean;
                                     ok::< 
                                       (# 
                                       do
                                          (if not done then
                                              hide;
                                              true->done;
                                              (true,false)->ge.save
                                          if)
                                       #);
                                     notOK::< 
                                       (# 
                                       do
                                          (if not done then
                                              hide;
                                              true->done;
                                              ge.fg[]->onGroupNotSaved
                                          if)
                                       #);
                                     cancel::< 
                                       (# 
                                       do
                                          (if not done then
                                              hide; true->done; true->cancelled
                                          if)
                                       #);
                                     
                                  #)
                           exit cancelled
                           #);
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (# 
                           do
                           (*edenv.saveAll; temporarily placed here to be able 
                            to specify the fatherwindow for the dialog
                            in order to avoid "moving" dialogs *)
                              l: edenv.groupEditorList.scan
                                (# cancelled: @boolean
                                do
                                   (if (current.touched > 0) and not
                                   current.isAfrejaEditor then
                                       current[]->askSave->cancelled
                                    else
                                       doCommand
                                         (# 
                                         do
                                            (if current.needToUpdateTextFile and
                                            not current.isAFrejaEditor then
                                                current.fg[]
                                                  ->edenv.makeNewTextFile
                                            if)
                                         #)
                                   if);
                                   (if cancelled then leave l if)
                                #)
                           #)
                      #);
                    iSaveAbstract: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do browser.cge.saveabstract #)
                      #);
                    iRecover: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  browser.cge.autoSaveFileExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do browser.cge[]->recoverGroup #)
                      #);
                    iRevert: @item
                      (#
                         f: @file;
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                              (* '~'->((browser.cge.fg.diskFileName).copy).Append->f.name;
                               f.entry.exists->value*)
                                  browser.cge.touched > 0->value
                               else
                                  false->value
                              if);
                              (* temporarily disabled *)
                              false->value
                           #);
                         onSelect::  (#  do browser.cge[]->revertGroup #)
                      #);
                    iSetIsReadOnly: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupeditorExists and not checkOngoingTextediting
                                ->value;
                              (if groupeditorExists then
                                  browser.cge.isReadOnly->checked;
                                  (if browser.cge.isReadOnly then
                                      'Write protected'->writeInfoView
                                   else
                                      ''->writeInfoView
                                  if)
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              (if browser.cge.isReadOnly then
                                  (if browser.cge.groupFileWriteable then
                                      false->browser.cge.setIsReadOnly;
                                      ''->writeInfoView
                                  if)
                               else
                                  true->browser.cge.setIsReadOnly;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    iSetGlobalWriteProtection: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              edenv.globalWriteProtection->checked;
                              (if edenv.globalWriteProtection then
                                  'Write protected'->writeInfoView
                               else
                                  ''->writeInfoView
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              (if edenv.globalWriteProtection then
                                  false->edenv.setGlobalWriteProtection;
                                  ''->writeInfoView
                               else
                                  true->edenv.setGlobalWriteProtection;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    
                 #);
               GroupEdit: @simplemenu
                 (#
                    iUndo: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowPerform then
                                  browser.cge.askUndoPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do browser.cge.undo;  #)
                      #);
                    iCut: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              browser.cfe.frag[]->browser.cge.cutForm;
                              browser.cfe.frag[]->browser.currentGroup.close;
                              browser.cge.fg[]->findGroup;
                              
                           #)
                      #);
                    iCopy: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do browser.cfe.frag[]->browser.cge.copyForm
                           #)
                      #);
                    iPaste: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  (if formEditorExists then
                                      browser.cfe.frag[]
                                        ->browser.cge.askPastePermission->value
                                   else
                                      false->value
                                  if)
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              browser.cfe.frag[]->browser.cge.pasteForm->ff[]
                                ->findForm;
                              
                           #)
                      #);
                    iAppend: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowPerform then
                                  browser.cge.askPastePermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do none ->browser.cge.pasteForm->ff[]->findForm; 
                           #)
                      #);
                    iInsertDoPartFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],31)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iInsertDescriptorFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],2)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iInsertAttributesFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],3)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iEditProperties: @item
                      (#
                         onStatus::  (#  do allowPerform->value #);
                         onSelect::  (#  do browser.cge.editProperties;  #)
                      #);
                    iEditFormName: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              browser.cfe.frag[]->browser.cge.editFormName;
                              (browser.cge.fg[],browser.cfe.frag[])
                           #)
                      #);
                    iSearch: @item
                      (#
                         onStatus:: 
                           (#  do (*groupEditorExists*) false->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              node: ^astInterface.ast;
                              
                           do
                              (if formEditorExists then
                                  browser.cfe.frag[]->ff[];
                                  browser.cfe.cs.node[]->node[]
                              if);
                              (browser.cge.fg[],ff[],node[])
                                ->edenv.searchTextDialog
                                  (#
                                     found:: 
                                       (# 
                                       do
                                          'found'->putLine;
                                          foundNode[]->findandselect
                                       #);
                                     notfound:: 
                                       (# help: ^text
                                       do
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          help[]->putLine;
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    iReplace: @item
                      (#
                         onStatus:: 
                           (#  do (*groupEditorExists*) false->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              node: ^astInterface.ast;
                              
                           do
                              (if formEditorExists then
                                  browser.cfe.frag[]->ff[];
                                  browser.cfe.cs.node[]->node[]
                              if);
                              (browser.cge.fg[],ff[],node[])
                                ->edenv.searchTextDialog
                                  (#
                                     found:: 
                                       (# 
                                       do
                                          'found'->putLine;
                                          foundNode[]->findandselect
                                       #);
                                     notfound:: 
                                       (# help: ^text
                                       do
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          help[]->putLine;
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         'Undo'->iUndo.new;
                         newSeparator;
                         'Cut'->iCut.new;
                         'Copy'->iCopy.new;
                         'Paste Before'->iPaste.new;
                         'Paste At End'->iAppend.new;
                         newSeparator;
                         'New DoPart Fragment Form...'
                           ->iInsertDoPartFragmentForm.new;
                         'New Descriptor Fragment form...'
                           ->iInsertDescriptorFragmentForm.new;
                         'New Attributes Fragment Form...'
                           ->iInsertAttributesFragmentForm.new;
                         newSeparator;
                         'Edit Properties...'->iEditProperties.new;
                         'Edit Name...'->iEditFormName.new;
                         newSeparator;
                         'Search...'->iSearch.new;
                         'Replace...'->iReplace.new
                      #)
                 #);
               GroupPerform: @groupPerformMenuType;
               groupPerformMenuType:< simplemenu
                 (#
                    iCheck: @item
                      (#
                         onStatus::  (#  do allowPerform->value #);
                         onSelect:: 
                           (# 
                           do
                              browser.cge.fg[]->lastCompiledFg[];
                              browser.cge.fg[]->check
                           #)
                      #);
                    iReCheck: @item
                      (#
                         onStatus:: 
                           (#  do (lastCompiledFg[] <> none )->value #);
                         onSelect::  (#  do lastCompiledFg[]->check #)
                      #);
                    iCompile: @item
                      (#
                         onStatus::  (#  do allowPerform->value #);
                         onSelect:: 
                           (# 
                           do
                              browser.cge.fg[]->lastCompiledFg[];
                              browser.cge.fg[]->compile
                           #)
                      #);
                    iRecompile: @item
                      (#
                         onStatus:: 
                           (#  do (lastCompiledFg[] <> none )->value #);
                         onSelect::  (#  do lastCompiledFg[]->compile #)
                      #);
                    iExecute: @item
                      (#
                         onStatus:: 
                           (#  do false-> (*allowCommand*) value #);
                         onSelect::  (#  do  #)
                      #);
                    iReExecute: @item
                      (#
                         onStatus:: 
                           (#  do false-> (*allowCommand*) value #);
                         onSelect::  (#  do  #)
                      #);
                    iCompileAndExecute: @item
                      (#
                         onStatus:: 
                           (#  do false-> (*allowCommand*) value #);
                         onSelect:: 
                           (# 
                           do
                              browser.cge.fg[]->lastCompiledFg[];
                              browser.cge.fg[]->compile
                           #)
                      #);
                    iRecompileAndExecute: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if false (*allowCommand*) then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->compile #)
                      #);
                    iDebug: @item
                      (#
                         onStatus:: 
                           (#  do false (*groupEditorExists*) ->value #);
                         onSelect::  (#  do  #)
                      #);
                    iSemanticErrors: @item
                      (#
                         onStatus::  (#  do allowPerform->value #);
                         onSelect:: 
                           (#  do browser.cge.fg[]->showSemanticErrors #)
                      #);
                    iShowDiagram: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              (if formEditorExists and not
                              browser.cfe.textEditMode then
                                  (edenv.frejaInterface[] <> none )->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              doCommand
                                (# 
                                do
                                   (browser.cfe.frag[],browser.cfe.cs.node[])
                                     ->edenv.frejaInterface.openForm
                                #)
                           #)
                      #);
                    
                 #);
               Test: @TestMenuType;
               testMenuType:< simplemenu
                 (#
                    iuseExternalTextEditor: @item
                      (#
                         onStatus:: 
                           (# 
                           do true->value; edenv.useExternalTextEditor->checked
                           #);
                         onSelect:: 
                           (# 
                           do
                              not edenv.useExternalTextEditor
                                ->edenv.useExternalTextEditor
                           #)
                      #);
                    iEventTrace: @item
                      (#
                         onStatus:: 
                           (#  do true->value; edenv.trace[7]->checked #);
                         onSelect:: 
                           (# 
                           do
                              not edenv.switch[7]->edenv.switch[7];
                              not edenv.trace[7]->edenv.trace[7]
                           #)
                      #);
                    iTraceStart: @item
                      (#
                         onStatus:: 
                           (#  do true->value; edenv.trace[2]->checked #);
                         onSelect:: 
                           (#  do not edenv.trace[2]->edenv.trace[2] #)
                      #);
                    iTraceGetNextComment: @item
                      (#
                         isSet: @boolean;
                         onStatus::  (#  do true->value; isSet->checked #);
                         onSelect:: 
                           (# 
                           do
                              (mps.ast.trace.getNextComment,true)
                                ->mps.ast.trace.set;
                              true->isSet
                           #)
                      #);
                    open::< 
                      (# 
                      do
                         'Use emacs'->iUseExternalTextEditor.new;
                         newSeparator;
                         'Trace start'->iTraceStart.new;
                         'Event trace'->iEventTrace.new;
                         'Trace getnextComments'->iTraceGetNextComment.new;
                         INNER
                      #);
                    
                 #);
               open::< 
                 (# 
                 do
                    fileMenu.setup;
                    projectMenu.setup;
                    'Group'->GroupFile.new;
                    useExternalTexteditor->Edit.useExternalTextEditor;
                    Edit.new;
                    Edit[]->append;
                    isBETAsif->View.BETAsif;
                    View.new;
                    View[]->append;
                    INNER ;
                    historyMenu.setup;
                    windowsMenu.setup
                 #)
            #)
       #);
     inithere: @boolean;
     isBETAsif:< booleanValue;
     useExternalTextEditor:< booleanValue;
     editorVersion: (#  exit '5.1(1)' #);
     compilerVersion:
       (# version: ^text
       <<SLOT sifCompilerVersion:DoPart>>
       exit version[]
       #);
     betaenvVersion:<
       (# t: ^text
       do
          '~beta/basiclib/v1.5/betaenv'->t[];
          INNER
       exit t[]
       #);
     frejaInclude:<
       (# t: ^text
       do
          '~beta/freja/v2.2/associations'
            ->t[];
          INNER
       exit t[]
       #);
     onAboutToCloseGroup:<
       (#
          fg: ^astInterface.fragmentGroup;
          OKtoClose,askSave: @boolean
          (* if OKtoClose the groupEditor is closed.
           * if askSave the user is prompted whether to save the group
           * else it is just saved (if necessary)
           *)
       enter fg[]
       do true->OKtoClose->askSave; INNER
       exit (OKtoClose,askSave)
       #);
     onGroupClosed:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupSaved:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupNotSaved:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupCompiled:<
       (#
          fg: ^astInterface.fragmentGroup;
          OK: @boolean (* OK means no errors *)
       enter (fg[],OK)
       do INNER
       #);
     onGroupChecked:<
       (#
          fg: ^astInterface.fragmentGroup;
          OK: @boolean (* OK means no errors *)
       enter (fg[],OK)
       do INNER
       #);
     init::< 
       (# 
       do
          ymerBrowser.open;
          ymerBrowser.loadSettings;
          ymerBrowser.browser[]->ymerBrowser.ymerCallBack.theBrowser[];
          ymerBrowser.ymerCallback[]->ymerBrowser.edenv.ymerCallback[];
          INNER
       exit (mps.ast[],mps.betaCFL[])
       #);
     doTest,useExtTextEd: @boolean;
     
  do init; INNER fragmentBrowser
  #)  

