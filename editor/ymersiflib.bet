ORIGIN '~beta/sourcebrowser/v0.1/ymer';
INCLUDE '~beta/guienv/v1.3.1/utils/simplemenu'
        '~beta/sourcebrowser/v0.1/private/parseEditor'
        '~beta/sourcebrowser/v0.1/private/semanticEditor'
        '~beta/sourcebrowser/v0.1/private/ymerBody'
        'ymercall'
        'private/editorenvbody';
BODY 'private/globalfrejacommbody'
     'private/compiler'
     'private/globalexternalinterfacebody';
-- lib: Attributes --
fragmentBrowser: ymerApplication
  (#
     ymerBrowserWindow:: 
       (#
          eventhandler::  (#  #);
          open::  (#  #);
          theSifTextEditor: ^sifTextEditor;
          cbg: (* current browser group *) ^browser.projects.group;
          cff:
          (* current fragment form, not used anymore only in oldfromeditorExists *)
          ^astInterface.fragmentform;
          ymerCallback: @ymerCall
            (#
               refreshGroup::  (#  do (cb[],fg[],none )->updateBrowser #);
               findAndSelect::  (#  do node[]->findAndSelect #)
            #);
          ongoingTextediting: @boolean;
          checkOngoingTextEditing:
            (# value: @boolean
            do
               browser.getFragmentFormEditor->theSifTextEditor[];
               (if theSifTextEditor[] <> none then
                   (if theSifTextEditor.sifViewer[] <> none then
                       theSifTextEditor.textediting->value
                   if)
               if)
            exit value
            #);
          oldformEditorExists:
            (# exists: @boolean
            do
               browser.getFragmentFormEditor->theSifTextEditor[];
               (if theSifTextEditor[] <> none then
                   (if theSifTextEditor.sifViewer[] <> none then
                       theSifTextEditor.sifViewer[]->ymerBrowserPrivate.cfe[];
                       (if not theSifTextEditor.textediting then
                           true->exists; INNER oldformEditorExists
                        else
                           true->ongoingTextediting
                       if)
                   if)
               if)
            exit exists
            #);
          formEditorExists:
            (# exists: @boolean
            do
               (if ymerBrowserPrivate.cfe[] <> none then
                   (if ymerBrowserPrivate.cfe.theSifTextEditor[] <> none then
                       (if not
                       ymerBrowserPrivate.cfe.theSifTextEditor.textediting then
                           true->exists; 
                        else
                           true->ongoingTextediting
                       if)
                   if)
               if)
            exit exists
            #);
          groupEditorExists:
            (# exists: @boolean
            do
               (if cge[] <> none then
                   (if not checkOngoingTextediting then true->exists if)
               if)
            exit exists
            #);
          updateBrowser:
            (#
               fg: ^astInterface.fragmentGroup;
               ff: ^astInterface.fragmentForm;
               cb: ^object;
               browser: ^sourceBrowser;
               cbg: (* current browser group *) ^browser.projects.group
            enter (cb[],fg[],ff[])
            do
               (if cb[] <> none then
                   cb[]->browser[];
                   browser.currentgroup[]->cbg[];
                   (if (cbg[] <> none ) and (cbg.fg[] = fg[]) then
                       (browser.currentProject[],none )->browser.setGroup;
                       (browser.currentProject[],cbg[])->browser.setGroup;
                       (if ff[] <> none then
                           (browser.currentProject[],cbg[],ff[])
                             ->browser.setFragmentForm
                       if)
                   if)
               if)
            #);
          oldFindAndSelect:
            (# node: ^astInterface.ast
            enter node[]
            do
               (if node[] <> none then
                   (if ymerBrowserPrivate.cfe[] <> none then
                       (if node.frag[] <> ymerBrowserPrivate.cfe.frag[] then
                           ((node.frag.father).fullName,node.frag.name)
                             ->browser.selectFragmentForm;
                           (if oldformEditorExists then
                               (node[],1,0,0)->ymerBrowserPrivate.cfe.setFocus
                            else
                               'formeditor not found'->putLine
                           if)
                        else
                           (node[],1,0,0)->ymerBrowserPrivate.cfe.setFocus
                       if)
                    else
                       ((node.frag.father).fullName,node.frag.name)
                         ->browser.selectFragmentForm;
                       (if oldformEditorExists then
                           (node[],1,0,0)->ymerBrowserPrivate.cfe.setFocus
                        else
                           'formEditor not found'->putLine
                       if)
                   if)
                else
                   'findAndSelect: node is none!'->putLine
               if)
            #);
          findAndSelect:
            (# node: ^astInterface.ast
            enter node[]
            do
               (if node[] <> none then
                   (if ymerBrowserPrivate.cfe[] <> none then
                       (if node.frag[] <> ymerBrowserPrivate.cfe.frag[] then
                           ((node.frag.father).fullName,node.frag.name)
                             ->browser.selectFragmentForm;
                           (node[],1,0,0)->ymerBrowserPrivate.cfe.setFocus
                        else
                           (node[],1,0,0)->ymerBrowserPrivate.cfe.setFocus
                       if)
                    else
                       ((node.frag.father).fullName,node.frag.name)
                         ->browser.selectFragmentForm;
                       (node[],1,0,0)->ymerBrowserPrivate.cfe.setFocus
                   if)
                else
                   'findAndSelect: node is none!'->putLine
               if)
            #);
          findForm:
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do
               (if ff[] <> none then
                   ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                else
                   'findForm: ff is none!'->putLine
               if)
            #);
          findGroup:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            do
               (if fg[] <> none then
                   fg.fullName->browser.selectFragmentGroup
                else
                   'findGroup: fg is none!'->putLine
               if)
            #);
          externComm: @<<SLOT externprivate:Descriptor>>;
          onExternCommand:
            (# command,status: ^text; 
            enter command[]
            do
               <<SLOT onGlobalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externCommand:
            (# command,status: ^text; 
            enter command[]
            do <<SLOT globalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externalInterface:
          (* Global commands *) @
            (#
               externalInterfaceException: exception
                 (# m: ^text
                 enter m[]
                 <<SLOT externalInterfaceException:DoPart>>
                 #);
               selectEditor:
                 (#
                    fe:
                      ^ymerBrowser.
                         codeeditor
                 enter fe[]
                 <<SLOT externalInterfaceSelectEditor:DoPart>>
                 #);
               openForm:
                 (#
                    name: ^text;
                    node:
                      ^edenv.mps.
                         ast;
                    fe: ^ymerBrowser.codeEditor;
                    NoEditorForForm:< externalInterfaceException;
                    couldNotOpenFragmentform:< externalInterfaceException
                 enter (name[],node[])
                 <<SLOT externalInterfaceOpenForm:DoPart>>
                 exit fe[]
                 #);
               getEditorForForm:
                 (#
                    ff: ^edenv.mps.fragmentForm;
                    node: ^edenv.mps.ast;
                    fe: ^ymerBrowser.codeEditor;
                    NoEditorForForm:< externalInterfaceException
                 enter (ff[],node[])
                 <<SLOT externalInterfaceGetEditorForForm:DoPart>>
                 exit fe[]
                 #);
               newBETAProgram:
                 (# name: ^text; NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETAProgram:DoPart>>
                 #);
               newBETALibrary:
                 (#
                    name: ^text;
                    NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETALibrary:DoPart>>
                 #);
               saveAndQuit:
                 (# 
                 <<SLOT externalInterfaceSaveAndQuit:DoPart>>
                 #);
               save: (#  <<SLOT externalInterfaceSave:DoPart>> #);
               autoSave:
                 (# 
                 <<SLOT externalInterfaceAutoSave:DoPart>>
                 #);
               check:
                 (# fg: ^edenv.mps.fragmentGroup
                 enter fg[]
                 <<SLOT externalInterfaceCheck:DoPart>>
                 #);
               toggleDebug:
                 (# 
                 <<SLOT externalInterfaceToggleDebug:DoPart>>
                 #);
               onInput: @
                 (# t: ^text enter t[] <<SLOT commFrejaOnInput:DoPart>> #);
               putline: @
                 (# t: ^text
                 enter t[]
                 <<SLOT commFrejaPutline:DoPart>>
                 #);
               
            #);
          handleFrejaCommand:
            (# t: @text
            enter t
            do <<SLOT commFrejaHandleFrejaCommand:Descriptor>>; 
            #);
          lastCompiledFg:
            ^astInterface.
               fragmentGroup;
          compilerPrivate: @<<SLOT compilerPrivate:Descriptor>>;
          activateCompiler:
            (#
               fragmentFile: ^text;
               generateCode,
                 generateDebugInfo: @boolean
            enter
            (fragmentFile[],generateCode,
             generateDebugInfo)
            do <<SLOT activateCompilerBody:Descriptor>>
            #);
          initCompiler:
           <<SLOT initCompilerBody:Descriptor>>;
          formsWithSemanticErrors: @list
            (# element::< mps.ast.fragmentForm #);
          savedGroups: @set (# element::< mps.ast.fragmentGroup;  #);
          handleSavedGroups:
            (# done:< object; 
            do <<SLOT commFrejaHandleSavedGroups:Descriptor>>; 
            #);
          callCompiler:
            (#
               exp:
                 ^astInterface.
                    expanded;
               node: ^astInterface.ast
            do
               (if theSemanticErrorViewer[] <> none then
                   (if theSemanticErrorViewer.opened then
                       theSemanticErrorViewer.close
                   if);
                   none ->theSemanticErrorViewer[]
               if);
               ''->writeInfoView;
               formsWithSemanticErrors.clear;
               'Calling BETA compiler'->putLine;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               INNER ;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               (if formsWithSemanticErrors.size = 0 then
                   handleSavedGroups
                else
                   true->edenv.frejaInterface.groupChecked;
                   l: formsWithSemanticErrors.scan
                     (# 
                     do
                        none ->node[];
                        'Semantic Error(s)'->writeInfoView;
                        (if current.root.kind = mps.ast.kinds.interior then
                            current.root[]->exp[];
                            loop: exp.suffixWalk
                              (# 
                              do
                                 (if current.hasSemanticError then
                                 (*current.semanticError->semanticErrorText->t[];*)
                                     current[]->node[]; leave loop
                                 if)
                              #);
                            (if node[] <> none then node[]->findAndSelect if)
                        if);
                        current.father->openSemanticErrorEditor;
                        leave l
                     #);
                   savedGroups.clear
               if);
               
            #);
          check:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               edenv.nonterminalsExist->(nontExist,nont[]);
               (if nontExist then
                   nont[]->findAndSelect;
                   (none ,
                    'This fragment group cannot be checked\nbefore all nonterminals have been removed',
                    'Compiler message')->alertUser;
                   
                else
                   callCompiler
                     (#  do (fg.name,false,false)->activateCompiler;  #);
                   (formsWithSemanticErrors.size = 0)->status;
                   
               if)
            exit status
            #);
          compile:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               edenv.nonterminalsExist->(nontExist,nont[]);
               (if nontExist then
                   nont[]->findAndSelect;
                   (none ,
                    'This fragment group cannot be compiled\nbefore all nonterminals have been removed',
                    'Compiler message')->alertUser
                else
                   callCompiler
                     (#  do (fg.name,true,true)->activateCompiler;  #);
                   (formsWithSemanticErrors.size = 0)->status;
                   
               if)
            exit status
            #);
          writeInfoView:
            (# t: ^text
            enter t[]
            do
            (* newLine;
             '<<<'->putText;
             t[]->putText;
             '>>>'->putLine;
             ''->infoView.contents.label;*) t[]->infoView.msg; 
            #);
          openParseEditor:
            (#
               pe: @browser.parseEditor
                 (# open::  (#  do (600,800)->size; fn[]->loadFiles #)
                 #);
               fn: ^text
            enter fn[]
            do pe.open
            #);
          theSemanticErrorViewer: ^browser.semanticViewer;
          semanticErrorViewer: browser.semanticViewer
            (#
               open:: 
                 (# 
                 do
                    theGroupWithSemanticErrors[]->fg[];
                    theGroupWithSemanticErrors.name->fgName[];
                    
                 #)
            #);
          theGroupWithSemanticErrors: ^astInterface.fragmentGroup;
          openSemanticErrorEditor:
            (# 
            enter theGroupWithSemanticErrors[]
            do
               &semanticErrorViewer[]->theSemanticErrorViewer[];
               theSemanticErrorViewer.open
            #);
          recoverGroup:
            (#
               ge: ^editorEnv.groupEditor;
               g: ^astInterface.fragmentGroup;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->g.restoreBackup;
                   ge.groupTouched;
                   (false,true)->ge.save;
                   ge.resetGroupTouch;
                   browser.currentGroup.close;
                   true->edenv.onGoingRecovery;
                   groupName[]->browser.selectFragmentGroup;
                   (if cge[] = none then
                       'no groupEditor, should not happen'->putLine
                    else
                       cge.groupTouched
                   if);
                   false->edenv.onGoingRecovery;
                   
               if);
               
            #);
          revertGroup:
            (#
               g: ^astInterface.fragmentGroup;
               ge: ^editorEnv.groupEditor;
               f: @file;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->(g.diskFileName).copyAppend->f.name;
                   (if f.entry.exists then f.delete if);
                   ge.resetGroupTouch;
                   browser.currentGroup.close;
                   groupName[]->browser.selectFragmentGroup;
                   
               if);
               
            #);
          Edit: @codeEditorEditMenu
            (#
               formEditorExists:: 
                 (#  do ymerBrowserPrivate.cfe[]->cfe[] #);
               writeStatus::  (#  do t[]->writeInfoView #)
            #);
          View: @codeEditorViewMenu
            (#
               formEditorExists::  (#  do ymerBrowserPrivate.cfe[]->cfe[] #)
            #);
          SLOTs: @codeEditorSLOTsMenu
            (#
               formEditorExists::  (#  do ymerBrowserPrivate.cfe[]->cfe[] #)
            #);
          menubarType:: 
            (#
               GroupFile: @simplemenu
                 (#
                    iNew: @item
                      (#
                         onStatus::  (#  do false->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              ge: ^editorEnv.groupEditor
                           do
                              edenv.new->ge[];
                              ge.fg.fragmentlist.scan
                                (# 
                                do
                                   (if current.type = mps.ast.formType then
                                       current.f[]->ff[]
                                   if)
                                #);
                              ff[]->findForm
                           #)
                      #);
                    iNewBetaProgram: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextediting->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              edenv.showEditors;
                              (browser[],none ,false)->edenv.newBetaProgram
                                ->fg[];
                              edenv.showEditors;
                              (if fg[] <> none then
                                  fg.fullName->addproject; edenv.showEditors
                              if)
                           #)
                      #);
                    iNewBetaLibrary: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextEditing->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              edenv.showEditors;
                              (browser[],none ,false)->edenv.newBetaLibrary
                                ->fg[];
                              edenv.showEditors;
                              (if fg[] <> none then
                                  fg.fullName->addproject; edenv.showEditors
                              if)
                           #)
                      #);
                    iClose: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value;  #);
                         onSelect:: 
                           (# g: ^astInterface.fragmentGroup
                           do
                              cge.fg[]->g[];
                              (* g.close; 'NB! g.close'->putLine;*)
                              browser.currentGroup.close
                           #)
                      #);
                    iSave: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do (true,false)->cge.save #)
                      #);
                    iSaveAs: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect:: 
                           (# newName: ^text
                           do
                              cge.saveAs->newName[];
                              (if newName[] <> none then
                                  browser.currentGroup.close;
                                  newName[]->browser.selectFragmentGroup
                              if)
                           #)
                      #);
                    iSaveAll: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect::  (#  do edenv.saveAll #)
                      #);
                    iSaveAbstract: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do cge.saveabstract #)
                      #);
                    iRecover: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  cge.autoSaveFileExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do cge[]->recoverGroup #)
                      #);
                    iRevert: @item
                      (#
                         f: @file;
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  '~'->(cge.fg.diskFileName).copyAppend->f.name;
                                  f.entry.exists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do cge[]->revertGroup #)
                      #);
                    open:: 
                      (# 
                      do
                         'New...'->iNew.new;
                         'New BETA Program'->iNewBetaProgram.new;
                         'New BETA Library'->iNewBetaLibrary.new;
                         newSeparator;
                         'Close'->iClose.new;
                         'Save'->iSave.new;
                         'Save As...'->iSaveAs.new;
                         'Save All...'->iSaveAll.new;
                         'Save Abstract...'->iSaveAbstract.new;
                         newSeparator;
                         'Recover'->iRecover.new;
                         'Revert'->iRevert.new
                      #)
                 #);
               GroupEdit: @simplemenu
                 (#
                    iUndo: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  cge.askUndoPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do cge.undo;  #)
                      #);
                    iCut: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do ymerBrowserPrivate.cfe.frag[]->cge.cutForm; 
                           #)
                      #);
                    iCopy: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do ymerBrowserPrivate.cfe.frag[]->cge.copyForm
                           #)
                      #);
                    iPaste: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  (if formEditorExists then
                                      ymerBrowserPrivate.cfe.frag[]
                                        ->cge.askPastePermission->value
                                   else
                                      false->value
                                  if)
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              ymerBrowserPrivate.cfe.frag[]->cge.pasteForm
                                ->ff[];
                              
                           #)
                      #);
                    iAppend: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  (if formEditorExists then
                                      ymerBrowserPrivate.cfe.frag[]
                                        ->cge.askPastePermission->value
                                   else
                                      false->value
                                  if)
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do none ->cge.pasteForm->ff[]; 
                           #)
                      #);
                    iInsertDescriptorFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (ymerBrowserPrivate.cfe.frag[],2)->cge.insertForm
                                ->ff[];
                              
                           #)
                      #);
                    iInsertAttributesFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (ymerBrowserPrivate.cfe.frag[],3)->cge.insertForm
                                ->ff[];
                              
                           #)
                      #);
                    iInsertDoPartFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (ymerBrowserPrivate.cfe.frag[],31)->cge.insertForm
                                ->ff[];
                              
                           #)
                      #);
                    iEditProperties: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do cge.editProperties;  #)
                      #);
                    iEditFormName: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              ymerBrowserPrivate.cfe.frag[]->cge.editFormName;
                              (cge.fg[],ymerBrowserPrivate.cfe.frag[])
                           #)
                      #);
                    iSearchSLOT: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# node: ^astInterface.ast
                           do
                              ymerBrowserPrivate.cfe.frag[]->cge.searchSLOT
                                ->node[];
                              (if node[] <> none then
                                  node[]->findandselect
                              if);
                              
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         'Undo'->iUndo.new;
                         newSeparator;
                         'Cut'->iCut.new;
                         'Copy'->iCopy.new;
                         'Paste Before'->iPaste.new;
                         'Paste Append'->iAppend.new;
                         newSeparator;
                         'Insert Descriptor Fragment form...'
                           ->iInsertDescriptorFragmentForm.new;
                         'Insert Attributes Fragment Form...'
                           ->iInsertAttributesFragmentForm.new;
                         'Insert DoPart Fragment Form...'
                           ->iInsertDoPartFragmentForm.new;
                         newSeparator;
                         'Edit Properties... '->iEditProperties.new;
                         'Edit Fragment name...'->iEditFormName.new;
                         newSeparator;
                         'Search SLOT'->iSearchSLOT.new
                      #)
                 #);
               GroupPerform: @simplemenu
                 (#
                    iCheck: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->check
                           #)
                      #);
                    iReCheck: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->check #)
                      #);
                    iCompile: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->compile
                           #)
                      #);
                    iRecompile: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->compile #)
                      #);
                    iExecute: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do  #)
                      #);
                    iReExecute: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do  #)
                      #);
                    iCompileAndExecute: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->compile
                           #)
                      #);
                    iRecompileAndExecute: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->compile #)
                      #);
                    iDebug: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do  #)
                      #);
                    iSemanticErrors: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect:: 
                           (#  do cge.fg[]->openSemanticErrorEditor #)
                      #);
                    iSemanticLinks: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              (if ymerBrowserPrivate.cfe[] <> none then
                                  ymerBrowserPrivate.cfe.cs.hasSemanticLink
                                    ->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              ymerBrowserPrivate.cfe.cs.node[]
                                ->ymerBrowserPrivate.cfe.followSemanticLink
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         'Check Current'->iCheck.new;
                         'Recheck'->iReCheck.new;
                         'Compile Current'->iCompile.new;
                         ('Recompile','r')->iRecompile.newKey;
                         'Execute Current'->iExecute.new;
                         ('Reexecute','e')->iReExecute.newKey;
                         newSeparator;
                         'Debug'->iDebug.new;
                         newSeparator;
                         'Semantic Errors...'->iSemanticErrors.new;
                         'Semantic Links'->iSemanticLinks.new
                      #)
                 #);
               open:: 
                 (# 
                 do
                    'Group'->GroupFile.new;
                    Edit.new;
                    Edit[]->append;
                    View.new;
                    View[]->append;
                    SLOTs.new;
                    SLOTs[]->append;
                    'Fragment'->GroupEdit.new;
                    'Tools'->GroupPerform.new
                 #)
            #)
       #);
     inithere: @boolean;
     init:: 
       (# 
       do
          ymerBrowser.browser[]->ymerBrowser.ymerCallBack.theBrowser[];
          ymerBrowser.ymerCallback[]->ymerBrowser.edenv.ymerCallback[];
          ymerBrowser.initCompiler
       exit (mps.ast[],mps.betaCFL[])
       #);
     
  do init; INNER fragmentBrowser
  #)  

