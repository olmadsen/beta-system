ORIGIN '~beta/sourcebrowser/v1.0.2/ymer';
INCLUDE '~beta/guienv/v1.3.1/utils/simplemenu'
        '~beta/sourcebrowser/v1.0.2/private/parseEditor'
        '~beta/sourcebrowser/v1.0.2/private/semanticEditor'
        'ymercall'
        'createdialog';
BODY 'private/globalfrejacommbody'
     'private/globalexternalinterfacebody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-96
 *   All rights reserved.
 *)
-- lib: Attributes --
fragmentBrowser: ymerApplication
  (#
     ymerBrowser: @ymerBrowserWindow;
     ymerBrowserWindow::<
       (#
          browserTitle::< (#  do ' and Editor'->title.append; INNER #);
          theSifTextEditor: ^sifTextEditor;
          cbg: (* current browser group *) ^browser.projects.group;
          cff:
          (* current fragment form, not used anymore only in oldfromeditorExists *)
          ^astInterface.fragmentform;
          ymerCallback: @ymerCall
            (#
               refreshGroup:: (#  do (cb[],fg[],none )->updateBrowser #);
               findAndSelect::
                 (#  do node[]->THIS(ymerBrowserWindow).findAndSelect #);
               check::
                 (#  do fg[]->THIS(ymerBrowserWindow).check->status #);
               betaenvVersion::
                 (#  do THIS(fragmentBrowser).betaenvVersion->t[] #);
               frejaInclude::
                 (#  do THIS(fragmentBrowser).frejaInclude->t[] #);
               machineType::
                 (#  do THIS(fragmentBrowser).machineType[]->t[] #);
               infoView:: (#  do t[]->writeInfoView #);
               infoViewDone:: (#  do writeInfoViewDone #);
               onAboutToCloseGroup::
                 (# 
                 do
                    fg[]->THIS(fragmentBrowser).onAboutToCloseGroup
                      ->(OKtoClose,askSave)
                 #);
               onGroupClosed::
                 (#  do fg[]->THIS(fragmentBrowser).onGroupClosed #);
               onGroupSaved::
                 (#  do fg[]->THIS(fragmentBrowser).onGroupSaved #)
            #);
          doCommand:
            (# 
            do edenv.setWaitCursor; INNER doCommand; edenv.setStructureCursor
            #);
          checkOngoingTextEditing:
            (# value: @boolean
            do (if cfe[] <> none then cfe.texteditMode->value if)
            exit value
            #);
          formEditorExists:
            (# value: @boolean
            do
               (if cfe[] <> none then
                   (if not cfe.texteditMode then true->value;  if)
               if)
            exit value
            #);
          groupEditorExists:
            (# value: @boolean do (cge[] <> none )->value exit value #);
          allowCommand:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and not cge.isReadOnly and
                   not cge.isAfrejaEditor then
                       true->value
                   if)
               if)
            exit value
            #);
          allowPerform:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and not cge.isReadOnly then
                       true->value
                   if)
               if)
            exit value
            #);
          allowOnForm:
            (# value: @boolean
            do
               (if groupEditorExists and not browser.propertySelected then
                   (if not checkOngoingTextediting and not cge.isReadOnly then
                       true->value
                   if)
               if)
            exit value
            #);
          updateBrowser:
            (#
               fg: ^astInterface.fragmentGroup;
               ff: ^astInterface.fragmentForm;
               cb: ^object;
               browser: ^sourceBrowser;
               cbg: (* current browser group *) ^browser.projects.group
            enter (cb[],fg[],ff[])
            do
               (if cb[] <> none then
                   cb[]->browser[];
                   browser.currentgroup[]->cbg[];
                   (if (cbg[] <> none ) and (cbg.fg[] = fg[]) then
                       (browser.currentProject[],none )->browser.setGroup;
                       (browser.currentProject[],cbg[])->browser.setGroup;
                       (if ff[] <> none then
                           (browser.currentProject[],cbg[],ff[])
                             ->browser.setFragmentForm
                       if)
                   if)
               if)
            #);
          findAndSelect:
            (# node: ^astInterface.ast
            enter node[]
            do
               (if node[] <> none then
                   (if cfe[] <> none then
                       (if node.frag[] <> cfe.frag[] then
                           ((node.frag.father).fullName,node.frag.name)
                             ->browser.selectFragmentForm;
                           (node[],1,0,0)->cfe.setFocus
                        else
                           (node[],1,0,0)->cfe.setFocus
                       if)
                    else
                       ((node.frag.father).fullName,node.frag.name)
                         ->browser.selectFragmentForm;
                       (node[],1,0,0)->cfe.setFocus
                   if)
                else
                   'findAndSelect: node is none!'->putLine
               if)
            #);
          findForm:
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do
               (if ff[] <> none then
                   ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                else
                   'findForm: ff is none!'->putLine
               if)
            #);
          findGroup:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            do
               (if fg[] <> none then
                   fg.fullName->browser.selectFragmentGroup
                else
                   'findGroup: fg is none!'->putLine
               if)
            #);
          externComm: @<<SLOT externprivate:Descriptor>>;
          onExternCommand:
            (# command,status: ^text; 
            enter command[]
            do
               <<SLOT onGlobalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externCommand:
            (# command,status: ^text; 
            enter command[]
            do <<SLOT globalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externalInterface:
          (* Global commands *) @
            (#
               externalInterfaceException: exception
                 (# m: ^text
                 enter m[]
                 <<SLOT externalInterfaceException:DoPart>>
                 #);
               selectEditor:
                 (#
                    fe:
                      ^ymerBrowserWindow.codeeditor
                 enter fe[]
                 <<SLOT externalInterfaceSelectEditor:DoPart>>
                 #);
               openForm:
                 (#
                    name: ^text;
                    node:
                      ^edenv.mps.
                         ast;
                    fe: ^ymerBrowserWindow.codeEditor;
                    NoEditorForForm:< externalInterfaceException;
                    couldNotOpenFragmentform:< externalInterfaceException
                 enter (name[],node[])
                 <<SLOT externalInterfaceOpenForm:DoPart>>
                 exit fe[]
                 #);
               getEditorForForm:
                 (#
                    ff: ^edenv.mps.fragmentForm;
                    node: ^edenv.mps.ast;
                    fe: ^ymerBrowserWindow.codeEditor;
                    NoEditorForForm:< externalInterfaceException
                 enter (ff[],node[])
                 <<SLOT externalInterfaceGetEditorForForm:DoPart>>
                 exit fe[]
                 #);
               newBETAProgram:
                 (# name: ^text; NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETAProgram:DoPart>>
                 #);
               newBETALibrary:
                 (#
                    name: ^text;
                    NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETALibrary:DoPart>>
                 #);
               saveAndQuit:
                 (# 
                 <<SLOT externalInterfaceSaveAndQuit:DoPart>>
                 #);
               save: (#  <<SLOT externalInterfaceSave:DoPart>> #);
               autoSave:
                 (# 
                 <<SLOT externalInterfaceAutoSave:DoPart>>
                 #);
               check:
                 (# fg: ^edenv.mps.fragmentGroup
                 enter fg[]
                 <<SLOT externalInterfaceCheck:DoPart>>
                 #);
               toggleDebug:
                 (# 
                 <<SLOT externalInterfaceToggleDebug:DoPart>>
                 #);
               onInput: @
                 (# t: ^text enter t[] <<SLOT commFrejaOnInput:DoPart>> #);
               putline: @
                 (# t: ^text
                 enter t[]
                 <<SLOT commFrejaPutline:DoPart>>
                 #);
               
            #);
          handleFrejaCommand:
            (# t: @text
            enter t
            do <<SLOT commFrejaHandleFrejaCommand:Descriptor>>; 
            #);
          lastCompiledFg:
            ^astInterface.
               fragmentGroup;
          compilerPrivate: @<<SLOT compilerPrivate:Descriptor>>;
          activateCompiler:
            (#
               fragmentFile: ^text;
               generateCode,
                 generateDebugInfo,OK: @boolean
            enter (fragmentFile[],generateCode,generateDebugInfo)
            do <<SLOT activateCompilerBody:Descriptor>>
            exit OK
            #);
          initCompiler:
            (# version: ^text
            do
               <<SLOT initCompilerBody:Descriptor>>
            exit version[]
            #);
          formsWithSemanticErrors: @list
            (# element::< mps.ast.fragmentForm #);
          savedGroups: @set (# element::< mps.ast.fragmentGroup;  #);
          callCompiler: doCommand
            (# exp: ^astInterface.expanded; node: ^astInterface.ast
            do
               (if theSemanticErrorViewer[] <> none then
                   (if theSemanticErrorViewer.opened then
                       none ->theSemanticErrorViewer.registerFragmentGroup
                   if)
               if);
               ''->writeInfoView;
               formsWithSemanticErrors.clear;
               'Calling BETA compiler'->putLine;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               INNER ;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               (if formsWithSemanticErrors.size > 0 then
                   l: formsWithSemanticErrors.scan
                     (# 
                     do
                        none ->node[];
                        'Semantic Error(s)'->writeInfoView;
                        (if current.root.kind = mps.ast.kinds.interior then
                            current.root[]->exp[];
                            loop: exp.suffixWalk
                              (# 
                              do
                                 (if current.hasSemanticError then
                                 (*current.semanticError->semanticErrorText->t[];*)
                                     current[]->node[]; leave loop
                                 if)
                              #);
                            (if node[] <> none then node[]->findAndSelect if)
                        if);
                        current.father->showSemanticErrors;
                        leave l
                     #)
               if);
               
            #);
          check:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               edenv.nonterminalsExist->(nontExist,nont[]);
               (if nontExist then
                   nont[]->findAndSelect;
                   (none ,
                    'This fragment group cannot be checked\nbefore all nonterminals have been removed',
                    'Compiler message')->alertUser;
                   
                else
                   callCompiler
                     (# 
                     do (fg.name,false,false)->activateCompiler->status; 
                     #);
                   (if edenv.frejaInterface[] <> none then
                       not status->edenv.frejaInterface.groupChecked
                   if)
               if);
               (fg[],status)->onGroupChecked
            exit status
            #);
          compile:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               edenv.nonterminalsExist->(nontExist,nont[]);
               (if nontExist then
                   nont[]->findAndSelect;
                   (none ,
                    'This fragment group cannot be compiled\nbefore all nonterminals have been removed',
                    'Compiler message')->alertUser
                else
                   callCompiler
                     (# 
                     do (fg.name,true,true)->activateCompiler->status; 
                     #);
                   (* not used anymore? (formsWithSemanticErrors.size = 0)->status;*)
                   
               if);
               (fg[],status)->onGroupCompiled
            exit status
            #);
          writeInfoView:
            (#
               t: ^text;
               myMsgPop:
                 (# 
                 do
                    (if infoView.msgList.head <> none then
                        (infoView.msgList.head).elm[]->infoView.done;
                        infoView.msgList.head->infoView.msgList.delete
                    if)
                 #)
            enter t[]
            do
               (if t.length > 0 then
                   t[]->infoView.msg; infoView.msgPush
                else
                   ''->infoView.msg
               if)
            #);
          writeInfoViewDone:
            (#
               myMsgPop:
                 (# 
                 do
                    (if infoView.msgList.head <> none then
                        (infoView.msgList.head).elm[]->infoView.done;
                        infoView.msgList.head->infoView.msgList.delete
                    if)
                 #)
            do myMsgPop
            #);
          openParseEditor:
            (#
               pe: @parseEditor
                 (# open:: (#  do (600,800)->size; fn[]->loadFiles #)
                 #);
               fn: ^text
            enter fn[]
            do pe.open
            #);
          theSemanticErrorViewer: ^browser.semanticViewer;
          semanticErrorViewer: browser.semanticViewer
            (#
               open::
                 (# 
                 do
                    theGroupWithSemanticErrors[]->fg[];
                    theGroupWithSemanticErrors.name->fgName[];
                    
                 #)
            #);
          theGroupWithSemanticErrors: ^astInterface.fragmentGroup;
          showSemanticErrors:
            (# 
            enter theGroupWithSemanticErrors[]
            do
               (if theSemanticErrorViewer[] = none then
                   &semanticErrorViewer[]->theSemanticErrorViewer[];
                   theSemanticErrorViewer.open
                else
                   (if not theSemanticErrorViewer.opened then
                       &semanticErrorViewer[]->theSemanticErrorViewer[];
                       theSemanticErrorViewer.open
                    else
                       theGroupWithSemanticErrors[]
                         ->theSemanticErrorViewer.registerFragmentGroup;
                       theSemanticErrorViewer.show
                   if)
               if)
            #);
          recoverGroup: doCommand
            (#
               ge: ^editorEnv.groupEditor;
               g: ^astInterface.fragmentGroup;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->g.restoreBackup;
                   ge.groupTouched;
                   (false,true)->ge.save;
                   ge.resetGroupTouch;
                   browser.currentGroup.close;
                   true->edenv.onGoingRecovery;
                   groupName[]->browser.selectFragmentGroup;
                   (if cge[] = none then
                       'no groupEditor, should not happen'->putLine
                    else
                       cge.groupTouched; false->cge.autoSaveFileExists
                   if);
                   false->edenv.onGoingRecovery;
                   
               if);
               
            #);
          revertGroup: doCommand
            (#
               g: ^astInterface.fragmentGroup;
               ge: ^editorEnv.groupEditor;
               f: @file;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->(g.diskFileName).copyAppend->f.name;
                   (if f.entry.exists then f.delete if);
                   ge.resetGroupTouch;
                   edenv.showEditors;
                   browser.currentGroup.close;
                   edenv.showEditors;
                   groupName[]->browser.selectFragmentGroup;
                   edenv.showEditors;
                   
               if);
               
            #);
          Edit: @codeEditorEditMenu
            (#
               formEditorExists::
                 (#  do THIS(ymerBrowserWindow).cfe[]->cfe[] #);
               writeStatus:: (#  do t[]->writeInfoView #)
            #);
          View: @codeEditorViewMenu
            (#
               formEditorExists::
                 (#  do THIS(ymerBrowserWindow).cfe[]->cfe[] #)
            #);
          SLOTs: @codeEditorSLOTsMenu
            (#
               formEditorExists::
                 (#  do THIS(ymerBrowserWindow).cfe[]->cfe[] #)
            #);
          workspace::
            (#
               menubarType::
                 (#
                    editMenu: @codeEditorEditMenu
                      (#
                         formEditorExists::
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           do open; THIS(editMenu)[]->THIS(menubarType).append
                           #)
                      #);
                    viewMenu: @codeEditorViewMenu
                      (#
                         formEditorExists::
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do open; THIS(viewMenu)[]->THIS(menubarType).append
                           #)
                      #);
                    open::
                      (# 
                      do
                         fileMenu.setup;
                         editMenu.setup;
                         isBETAsif->viewMenu.setup;
                         importMenu.setup;
                         scrollMenu.setup;
                         closeMenu.setup;
                         
                      #)
                 #)
            #);
          codeViewWindow::
            (#
               menubarType::
                 (#
                    editMenu: @codeEditorEditMenu
                      (#
                         formEditorExists:: (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           do open; THIS(editMenu)[]->THIS(menubarType).append
                           #)
                      #);
                    viewMenu: @codeEditorViewMenu
                      (#
                         formEditorExists:: (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do open; THIS(viewMenu)[]->THIS(menubarType).append
                           #)
                      #);
                    open::
                      (# 
                      do
                         fileMenu.setup;
                         editMenu.setup;
                         isBETAsif->viewMenu.setup;
                         buffersMenu.setup
                      #)
                 #)
            #);
          menubarType::<
            (#
               GroupFile: @groupFileMenuType;
               groupFileMenuType:< simplemenu
                 (#
                    iNew: @item
                      (#
                         onStatus:: (#  do true->value #);
                         onSelect::
                           (#
                              fg: ^astInterface.fragmentGroup;
                              created: @createDialog
                                (#
                                   create::
                                     (# 
                                     do
                                        (browser[],grammar[],groupName[],
                                         formName[])->edenv.new->fg[];
                                        (if fg[] <> none then
                                            fg.fullName->addproject;
                                            (*(browser[],fg[],none )->updateBrowser*)
                                            
                                        if);
                                        
                                     #);
                                   cancel:: (#  do  #);
                                   
                                #)
                           do
                              created.open;
                              (none ,'Create new group','untitled','untitled')
                                ->created.popup
                           #)
                      #);
                    iNewBetaProgram: @item
                      (#
                         onStatus::
                           (#  do not checkOngoingTextediting->value #);
                         onSelect::
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaProgram
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iNewBetaLibrary: @item
                      (#
                         onStatus::
                           (#  do not checkOngoingTextEditing->value #);
                         onSelect::
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaLibrary
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iClose: @item
                      (#
                         onStatus::
                           (# 
                           do
                              groupEditorExists and not checkOngoingTextediting
                              and not cge.isAfrejaEditor->value;
                              
                           #);
                         onSelect::
                           (# g: ^astInterface.fragmentGroup
                           do
                              cge.fg[]->g[];
                              (* g.close; 'NB! g.close'->putLine;*)
                              browser.currentGroup.close
                           #)
                      #);
                    iSave: @item
                      (#
                         onStatus:: (#  do allowCommand->value #);
                         onSelect:: (#  do (true,false)->cge.save #)
                      #);
                    iSaveAs: @item
                      (#
                         onStatus::
                           (# 
                           do
                              groupEditorExists and not cge.isAfrejaEditor
                                ->value
                           #);
                         onSelect::
                           (# newName: ^text
                           do
                              cge.saveAs->newName[];
                              (if newName[] <> none then
                                  browser.currentGroup.close;
                                  newName[]->browser.selectFragmentGroup
                              if)
                           #)
                      #);
                    iSaveAll: @item
                      (#
                         onStatus:: (#  do true->value #);
                         onSelect::
                           (# 
                           do
                           (*edenv.saveAll; temporarily placed here to be able 
                            to specify the fatherwindow for the dialog
                            in order to avoid "moving" dialogs *)
                              l: edenv.groupEditorList.scan
                                (# help: ^text; done: @boolean
                                do
                                   (if (current.touched > 0) and not
                                   current.isAfrejaEditor then
                                       'Save changes to '->help[];
                                       current.fg.name->help.append;
                                       '?'->help.append;
                                       (THIS(ymerBrowserWindow)[],'Save group',
                                        help[])
                                         -> (*UI.*) promptForBoolean
                                           (#
                                              ok::<
                                                (# 
                                                do (true,false)->current.save
                                                #);
                                              notOK::< (#  do  #);
                                              cancel::<
                                                (#  do true->done #);
                                              
                                           #)
                                    else
                                       doCommand
                                         (# 
                                         do
                                            (if current.needToUpdateTextFile and
                                            not current.isAFrejaEditor then
                                                current.fg[]
                                                  ->edenv.makeNewTextFile
                                            if)
                                         #)
                                   if);
                                   (if done then leave l if)
                                #)
                           #)
                      #);
                    iSaveAbstract: @item
                      (#
                         onStatus:: (#  do groupEditorExists->value #);
                         onSelect:: (#  do cge.saveabstract #)
                      #);
                    iRecover: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowCommand then
                                  cge.autoSaveFileExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: (#  do cge[]->recoverGroup #)
                      #);
                    iRevert: @item
                      (#
                         f: @file;
                         onStatus::
                           (# 
                           do
                              (if allowCommand then
                              (* '~'->(cge.fg.diskFileName).copyAppend->f.name;
                               f.entry.exists->value*)
                                  cge.touched > 0->value
                               else
                                  false->value
                              if);
                              (* temporarily disabled *)
                              false->value
                           #);
                         onSelect:: (#  do cge[]->revertGroup #)
                      #);
                    iSetIsReadOnly: @item
                      (#
                         onStatus::
                           (# 
                           do
                              groupeditorExists and not checkOngoingTextediting
                                ->value;
                              (if groupeditorExists then
                                  cge.isReadOnly->checked;
                                  (if cge.isReadOnly then
                                      'Write protected'->writeInfoView
                                   else
                                      ''->writeInfoView
                                  if)
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              (if cge.isReadOnly then
                                  (if cge.groupFileWriteable then
                                      false->cge.setIsReadOnly;
                                      ''->writeInfoView
                                  if)
                               else
                                  true->cge.setIsReadOnly;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    iSetGlobalWriteProtection: @item
                      (#
                         onStatus::
                           (# 
                           do
                              edenv.globalWriteProtection->checked;
                              (if edenv.globalWriteProtection then
                                  'Write protected'->writeInfoView
                               else
                                  ''->writeInfoView
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              (if edenv.globalWriteProtection then
                                  false->edenv.setGlobalWriteProtection;
                                  ''->writeInfoView
                               else
                                  true->edenv.setGlobalWriteProtection;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    
                 #);
               GroupEdit: @simplemenu
                 (#
                    iUndo: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowPerform then
                                  cge.askUndoPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: (#  do cge.undo;  #)
                      #);
                    iCut: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              cfe.frag[]->cge.cutForm;
                              cfe.frag[]->browser.currentGroup.close;
                              cge.fg[]->findGroup;
                              
                           #)
                      #);
                    iCopy: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: (#  do cfe.frag[]->cge.copyForm #)
                      #);
                    iPaste: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  (if formEditorExists then
                                      cfe.frag[]->cge.askPastePermission->value
                                   else
                                      false->value
                                  if)
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do cfe.frag[]->cge.pasteForm->ff[]->findForm; 
                           #)
                      #);
                    iAppend: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowPerform then
                                  cge.askPastePermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do none ->cge.pasteForm->ff[]->findForm; 
                           #)
                      #);
                    iInsertDoPartFragmentForm: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if cfe[] <> none then cfe.frag[]->ff[] if);
                              (ff[],31)->cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iInsertDescriptorFragmentForm: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if cfe[] <> none then cfe.frag[]->ff[] if);
                              (ff[],2)->cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iInsertAttributesFragmentForm: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if cfe[] <> none then cfe.frag[]->ff[] if);
                              (ff[],3)->cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iEditProperties: @item
                      (#
                         onStatus:: (#  do allowPerform->value #);
                         onSelect:: (#  do cge.editProperties;  #)
                      #);
                    iEditFormName: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::
                           (# ff: ^astInterface.fragmentForm
                           do
                              cfe.frag[]->cge.editFormName;
                              (cge.fg[],cfe.frag[])
                           #)
                      #);
                    iSearch: @item
                      (#
                         onStatus::
                           (#  do (*groupEditorExists*) false->value #);
                         onSelect::
                           (#
                              ff: ^astInterface.fragmentForm;
                              node: ^astInterface.ast;
                              
                           do
                              (if formEditorExists then
                                  cfe.frag[]->ff[]; cfe.cs.node[]->node[]
                              if);
                              (cge.fg[],ff[],node[])
                                ->edenv.searchTextDialog
                                  (#
                                     found::
                                       (# 
                                       do
                                          'found'->putLine;
                                          foundNode[]->findandselect
                                       #);
                                     notfound::
                                       (# help: ^text
                                       do
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          help[]->putLine;
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    iReplace: @item
                      (#
                         onStatus::
                           (#  do (*groupEditorExists*) false->value #);
                         onSelect::
                           (#
                              ff: ^astInterface.fragmentForm;
                              node: ^astInterface.ast;
                              
                           do
                              (if formEditorExists then
                                  cfe.frag[]->ff[]; cfe.cs.node[]->node[]
                              if);
                              (cge.fg[],ff[],node[])
                                ->edenv.searchTextDialog
                                  (#
                                     found::
                                       (# 
                                       do
                                          'found'->putLine;
                                          foundNode[]->findandselect
                                       #);
                                     notfound::
                                       (# help: ^text
                                       do
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          help[]->putLine;
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    open::
                      (# 
                      do
                         'Undo'->iUndo.new;
                         newSeparator;
                         'Cut'->iCut.new;
                         'Copy'->iCopy.new;
                         'Paste Before'->iPaste.new;
                         'Paste At End'->iAppend.new;
                         newSeparator;
                         'New DoPart Fragment Form...'
                           ->iInsertDoPartFragmentForm.new;
                         'New Descriptor Fragment form...'
                           ->iInsertDescriptorFragmentForm.new;
                         'New Attributes Fragment Form...'
                           ->iInsertAttributesFragmentForm.new;
                         newSeparator;
                         'Edit Properties...'->iEditProperties.new;
                         'Edit Name...'->iEditFormName.new;
                         newSeparator;
                         'Search...'->iSearch.new;
                         'Replace...'->iReplace.new
                      #)
                 #);
               GroupPerform: @groupPerformMenuType;
               groupPerformMenuType:< simplemenu
                 (#
                    iCheck: @item
                      (#
                         onStatus:: (#  do allowPerform->value #);
                         onSelect::
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->check
                           #)
                      #);
                    iReCheck: @item
                      (#
                         onStatus::
                           (#  do (lastCompiledFg[] <> none )->value #);
                         onSelect:: (#  do lastCompiledFg[]->check #)
                      #);
                    iCompile: @item
                      (#
                         onStatus:: (#  do allowPerform->value #);
                         onSelect::
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->compile
                           #)
                      #);
                    iRecompile: @item
                      (#
                         onStatus::
                           (#  do (lastCompiledFg[] <> none )->value #);
                         onSelect:: (#  do lastCompiledFg[]->compile #)
                      #);
                    iExecute: @item
                      (#
                         onStatus::
                           (#  do false-> (*allowCommand*) value #);
                         onSelect:: (#  do  #)
                      #);
                    iReExecute: @item
                      (#
                         onStatus::
                           (#  do false-> (*allowCommand*) value #);
                         onSelect:: (#  do  #)
                      #);
                    iCompileAndExecute: @item
                      (#
                         onStatus::
                           (#  do false-> (*allowCommand*) value #);
                         onSelect::
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->compile
                           #)
                      #);
                    iRecompileAndExecute: @item
                      (#
                         onStatus::
                           (# 
                           do
                              (if false (*allowCommand*) then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: (#  do lastCompiledFg[]->compile #)
                      #);
                    iDebug: @item
                      (#
                         onStatus::
                           (#  do false (*groupEditorExists*) ->value #);
                         onSelect:: (#  do  #)
                      #);
                    iSemanticErrors: @item
                      (#
                         onStatus:: (#  do allowPerform->value #);
                         onSelect:: (#  do cge.fg[]->showSemanticErrors #)
                      #);
                    iShowDiagram: @item
                      (#
                         onStatus::
                           (# 
                           do
                              false->value;
                              (if formEditorExists and not cfe.textEditMode then
                                  (edenv.frejaInterface[] <> none )->value
                              if)
                           #);
                         onSelect::
                           (# 
                           do
                              doCommand
                                (# 
                                do
                                   (cfe.frag[],cfe.cs.node[])
                                     ->edenv.frejaInterface.openForm
                                #)
                           #)
                      #);
                    
                 #);
               open::<
                 (# 
                 do
                    fileMenu.setup;
                    projectMenu.setup;
                    'Group'->GroupFile.new;
                    Edit.new;
                    Edit[]->append;
                    isBETAsif->View.BETAsif;
                    View.new;
                    View[]->append;
                    INNER ;
                    historyMenu.setup;
                    windowsMenu.setup
                 #)
            #)
       #);
     inithere: @boolean;
     isBETAsif:< booleanValue;
     editorVersion: (#  exit '5.0.2(2) Pre-release' #);
     compilerVersion:
       (# version: ^text
       <<SLOT sifCompilerVersion:DoPart>>
       exit version[]
       #);
     betaenvVersion:<
       (# t: ^text
       do
          '~beta/basiclib/v1.4/betaenv'->t[];
          INNER
       exit t[]
       #);
     frejaInclude:<
       (# t: ^text
       do
          '~beta/freja/v2.1.1/associations'
            ->t[];
          INNER
       exit t[]
       #);
     onAboutToCloseGroup:<
       (#
          fg: ^astInterface.fragmentGroup;
          OKtoClose,askSave: @boolean
          (* if OKtoClose the groupEditor is closed.
           * if askSave the user is prompted whether to save the group
           * else it is just saved (if necessary)
           *)
       enter fg[]
       do true->OKtoClose->askSave; INNER
       exit (OKtoClose,askSave)
       #);
     onGroupClosed:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupSaved:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupCompiled:<
       (#
          fg: ^astInterface.fragmentGroup;
          OK: @boolean (* OK means no errors *)
       enter (fg[],OK)
       do INNER
       #);
     onGroupChecked:<
       (#
          fg: ^astInterface.fragmentGroup;
          OK: @boolean (* OK means no errors *)
       enter (fg[],OK)
       do INNER
       #);
     init::<
       (# 
       do
          ymerBrowser.open;
          ymerBrowser.loadSettings;
          ymerBrowser.browser[]->ymerBrowser.ymerCallBack.theBrowser[];
          ymerBrowser.ymerCallback[]->ymerBrowser.edenv.ymerCallback[];
          INNER
       exit (mps.ast[],mps.betaCFL[])
       #);
     
  do init; INNER fragmentBrowser
  #)  

