ORIGIN '~beta/sourcebrowser/v1.0/ymer';
INCLUDE '~beta/guienv/v1.3.1/utils/simplemenu'
        '~beta/sourcebrowser/v1.0/private/parseEditor'
        '~beta/sourcebrowser/v1.0/private/semanticEditor'
        'ymercall'
        'createdialog';
BODY 'private/globalfrejacommbody'
     'private/compiler'
     'private/globalexternalinterfacebody';
-- lib: Attributes --
fragmentBrowser: ymerApplication
  (#
     ymerBrowserWindow::< 
       (#
          theSifTextEditor: ^sifTextEditor;
          cbg: (* current browser group *) ^browser.projects.group;
          cff:
          (* current fragment form, not used anymore only in oldfromeditorExists *)
          ^astInterface.fragmentform;
          ymerCallback: @ymerCall
            (#
               refreshGroup::  (#  do (cb[],fg[],none )->updateBrowser #);
               findAndSelect::  (#  do node[]->findAndSelect #)
            #);
          checkOngoingTextEditing:
            (# value: @boolean
            do (if cfe[] <> none then cfe.texteditMode->value if)
            exit value
            #);
          formEditorExists:
            (# value: @boolean
            do
               (if cfe[] <> none then
                   (if not cfe.texteditMode then true->value;  if)
               if)
            exit value
            #);
          groupEditorExists:
            (# value: @boolean do (cge[] <> none )->value exit value #);
          allowCommand:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and cge.writeAccess and not
                   cge.isAfrejaEditor then
                       true->value
                   if)
               if)
            exit value
            #);
          allowPerform:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and cge.writeAccess then
                       true->value
                   if)
               if)
            exit value
            #);
          updateBrowser:
            (#
               fg: ^astInterface.fragmentGroup;
               ff: ^astInterface.fragmentForm;
               cb: ^object;
               browser: ^sourceBrowser;
               cbg: (* current browser group *) ^browser.projects.group
            enter (cb[],fg[],ff[])
            do
               (if cb[] <> none then
                   cb[]->browser[];
                   browser.currentgroup[]->cbg[];
                   (if (cbg[] <> none ) and (cbg.fg[] = fg[]) then
                       (browser.currentProject[],none )->browser.setGroup;
                       (browser.currentProject[],cbg[])->browser.setGroup;
                       (if ff[] <> none then
                           (browser.currentProject[],cbg[],ff[])
                             ->browser.setFragmentForm
                       if)
                   if)
               if)
            #);
          findAndSelect:
            (# node: ^astInterface.ast
            enter node[]
            do
               (if node[] <> none then
                   (if cfe[] <> none then
                       (if node.frag[] <> cfe.frag[] then
                           ((node.frag.father).fullName,node.frag.name)
                             ->browser.selectFragmentForm;
                           (node[],1,0,0)->cfe.setFocus
                        else
                           (node[],1,0,0)->cfe.setFocus
                       if)
                    else
                       ((node.frag.father).fullName,node.frag.name)
                         ->browser.selectFragmentForm;
                       (node[],1,0,0)->cfe.setFocus
                   if)
                else
                   'findAndSelect: node is none!'->putLine
               if)
            #);
          findForm:
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do
               (if ff[] <> none then
                   ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                else
                   'findForm: ff is none!'->putLine
               if)
            #);
          findGroup:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            do
               (if fg[] <> none then
                   fg.fullName->browser.selectFragmentGroup
                else
                   'findGroup: fg is none!'->putLine
               if)
            #);
          externComm: @<<SLOT externprivate:Descriptor>>;
          onExternCommand:
            (# command,status: ^text; 
            enter command[]
            do
               <<SLOT onGlobalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externCommand:
            (# command,status: ^text; 
            enter command[]
            do <<SLOT globalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externalInterface:
          (* Global commands *) @
            (#
               externalInterfaceException: exception
                 (# m: ^text
                 enter m[]
                 <<SLOT externalInterfaceException:DoPart>>
                 #);
               selectEditor:
                 (#
                    fe:
                      ^ymerBrowser.
                         codeeditor
                 enter fe[]
                 <<SLOT externalInterfaceSelectEditor:DoPart>>
                 #);
               openForm:
                 (#
                    name: ^text;
                    node:
                      ^edenv.mps.
                         ast;
                    fe: ^ymerBrowser.codeEditor;
                    NoEditorForForm:< externalInterfaceException;
                    couldNotOpenFragmentform:< externalInterfaceException
                 enter (name[],node[])
                 <<SLOT externalInterfaceOpenForm:DoPart>>
                 exit fe[]
                 #);
               getEditorForForm:
                 (#
                    ff: ^edenv.mps.fragmentForm;
                    node: ^edenv.mps.ast;
                    fe: ^ymerBrowser.codeEditor;
                    NoEditorForForm:< externalInterfaceException
                 enter (ff[],node[])
                 <<SLOT externalInterfaceGetEditorForForm:DoPart>>
                 exit fe[]
                 #);
               newBETAProgram:
                 (# name: ^text; NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETAProgram:DoPart>>
                 #);
               newBETALibrary:
                 (#
                    name: ^text;
                    NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETALibrary:DoPart>>
                 #);
               saveAndQuit:
                 (# 
                 <<SLOT externalInterfaceSaveAndQuit:DoPart>>
                 #);
               save: (#  <<SLOT externalInterfaceSave:DoPart>> #);
               autoSave:
                 (# 
                 <<SLOT externalInterfaceAutoSave:DoPart>>
                 #);
               check:
                 (# fg: ^edenv.mps.fragmentGroup
                 enter fg[]
                 <<SLOT externalInterfaceCheck:DoPart>>
                 #);
               toggleDebug:
                 (# 
                 <<SLOT externalInterfaceToggleDebug:DoPart>>
                 #);
               onInput: @
                 (# t: ^text enter t[] <<SLOT commFrejaOnInput:DoPart>> #);
               putline: @
                 (# t: ^text
                 enter t[]
                 <<SLOT commFrejaPutline:DoPart>>
                 #);
               
            #);
          handleFrejaCommand:
            (# t: @text
            enter t
            do <<SLOT commFrejaHandleFrejaCommand:Descriptor>>; 
            #);
          lastCompiledFg:
            ^astInterface.
               fragmentGroup;
          compilerPrivate: @<<SLOT compilerPrivate:Descriptor>>;
          activateCompiler:
            (#
               fragmentFile: ^text;
               generateCode,
                 generateDebugInfo: @boolean
            enter
            (fragmentFile[],generateCode,
             generateDebugInfo)
            do <<SLOT activateCompilerBody:Descriptor>>
            #);
          initCompiler:
           <<SLOT initCompilerBody:Descriptor>>;
          formsWithSemanticErrors: @list
            (# element::< mps.ast.fragmentForm #);
          savedGroups: @set (# element::< mps.ast.fragmentGroup;  #);
          handleSavedGroups:
            (# done:< object; 
            do <<SLOT commFrejaHandleSavedGroups:Descriptor>>; 
            #);
          callCompiler:
            (#
               exp:
                 ^astInterface.
                    expanded;
               node: ^astInterface.ast
            do
               (if theSemanticErrorViewer[] <> none then
                   (if theSemanticErrorViewer.opened then
                   (*theSemanticErrorViewer.close*)
                       none ->theSemanticErrorViewer.registerFragmentGroup
                   if);
                   (*none ->theSemanticErrorViewer[]*)
                   
               if);
               ''->writeInfoView;
               formsWithSemanticErrors.clear;
               'Calling BETA compiler'->putLine;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               INNER ;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               (if formsWithSemanticErrors.size = 0 then
                   handleSavedGroups
                else
                   (if edenv.frejaInterface[] <> none then
                       true->edenv.frejaInterface.groupChecked;
                       (* Michael: skal dettte ikke goeres i begge tilfaelde? *)
                       
                   if);
                   l: formsWithSemanticErrors.scan
                     (# 
                     do
                        none ->node[];
                        'Semantic Error(s)'->writeInfoView;
                        (if current.root.kind = mps.ast.kinds.interior then
                            current.root[]->exp[];
                            loop: exp.suffixWalk
                              (# 
                              do
                                 (if current.hasSemanticError then
                                 (*current.semanticError->semanticErrorText->t[];*)
                                     current[]->node[]; leave loop
                                 if)
                              #);
                            (if node[] <> none then node[]->findAndSelect if)
                        if);
                        current.father->showSemanticErrors;
                        leave l
                     #);
                   savedGroups.clear
               if);
               
            #);
          check:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               edenv.nonterminalsExist->(nontExist,nont[]);
               (if nontExist then
                   nont[]->findAndSelect;
                   (none ,
                    'This fragment group cannot be checked\nbefore all nonterminals have been removed',
                    'Compiler message')->alertUser;
                   
                else
                   callCompiler
                     (#  do (fg.name,false,false)->activateCompiler;  #);
                   (formsWithSemanticErrors.size = 0)->status;
                   
               if)
            exit status
            #);
          compile:
            (#
               fg: ^astInterface.fragmentGroup;
               status: @boolean;
               nontExist: @boolean;
               nont: ^astInterface.unexpanded
            enter fg[]
            do
               edenv.nonterminalsExist->(nontExist,nont[]);
               (if nontExist then
                   nont[]->findAndSelect;
                   (none ,
                    'This fragment group cannot be compiled\nbefore all nonterminals have been removed',
                    'Compiler message')->alertUser
                else
                   callCompiler
                     (#  do (fg.name,true,true)->activateCompiler;  #);
                   (formsWithSemanticErrors.size = 0)->status;
                   
               if)
            exit status
            #);
          writeInfoView:
            (# t: ^text
            enter t[]
            do
            (* newLine;
             '<<<'->putText;
             t[]->putText;
             '>>>'->putLine;
             ''->infoView.contents.label;*) t[]->infoView.msg; 
            #);
          openParseEditor:
            (#
               pe: @browser.parseEditor
                 (# open::  (#  do (600,800)->size; fn[]->loadFiles #)
                 #);
               fn: ^text
            enter fn[]
            do pe.open
            #);
          theSemanticErrorViewer: ^browser.semanticViewer;
          semanticErrorViewer: browser.semanticViewer
            (#
               open:: 
                 (# 
                 do
                    theGroupWithSemanticErrors[]->fg[];
                    theGroupWithSemanticErrors.name->fgName[];
                    
                 #)
            #);
          theGroupWithSemanticErrors: ^astInterface.fragmentGroup;
          showSemanticErrors:
            (# 
            enter theGroupWithSemanticErrors[]
            do
               (if theSemanticErrorViewer[] = none then
                   &semanticErrorViewer[]->theSemanticErrorViewer[];
                   theSemanticErrorViewer.open
                else
                   theGroupWithSemanticErrors[]
                     ->theSemanticErrorViewer.registerFragmentGroup;
                   theSemanticErrorViewer.show
               if)
            #);
          recoverGroup:
            (#
               ge: ^editorEnv.groupEditor;
               g: ^astInterface.fragmentGroup;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->g.restoreBackup;
                   ge.groupTouched;
                   (false,true)->ge.save;
                   ge.resetGroupTouch;
                   browser.currentGroup.close;
                   true->edenv.onGoingRecovery;
                   groupName[]->browser.selectFragmentGroup;
                   (if cge[] = none then
                       'no groupEditor, should not happen'->putLine
                    else
                       cge.groupTouched
                   if);
                   false->edenv.onGoingRecovery;
                   
               if);
               
            #);
          revertGroup:
            (#
               g: ^astInterface.fragmentGroup;
               ge: ^editorEnv.groupEditor;
               f: @file;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->(g.diskFileName).copyAppend->f.name;
                   (if f.entry.exists then f.delete if);
                   ge.resetGroupTouch;
                   edenv.showEditors;
                   browser.currentGroup.close;
                   edenv.showEditors;
                   groupName[]->browser.selectFragmentGroup;
                   edenv.showEditors;
                   
               if);
               
            #);
          Edit: @codeEditorEditMenu
            (#
               formEditorExists:: 
                 (#  do THIS(ymerBrowserWindow).cfe[]->cfe[] #);
               writeStatus::  (#  do t[]->writeInfoView #)
            #);
          View: @codeEditorViewMenu
            (#
               formEditorExists:: 
                 (#  do THIS(ymerBrowserWindow).cfe[]->cfe[] #)
            #);
          SLOTs: @codeEditorSLOTsMenu
            (#
               formEditorExists:: 
                 (#  do THIS(ymerBrowserWindow).cfe[]->cfe[] #)
            #);
          menubarType::< 
            (#
               GroupFile: @simplemenu
                 (#
                    iNew: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (#
                              fg: ^astInterface.fragmentGroup;
                              created: @createDialog
                                (#
                                   create:: 
                                     (# 
                                     do
                                        (browser[],grammar[],groupName[],
                                         formName[])->edenv.new->fg[];
                                        (if fg[] <> none then
                                            fg.fullName->addproject;
                                            (*(browser[],fg[],none )->updateBrowser*)
                                            
                                        if);
                                        
                                     #);
                                   cancel::  (#  do  #);
                                   
                                #)
                           do
                              created.open;
                              (none ,'Create new group','untitled','untitled')
                                ->created.popup
                           #)
                      #);
                    iNewBetaProgram: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextediting->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaProgram
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iNewBetaLibrary: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextEditing->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaLibrary
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iClose: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupEditorExists and not checkOngoingTextediting
                              and not cge.isAfrejaEditor->value;
                              
                           #);
                         onSelect:: 
                           (# g: ^astInterface.fragmentGroup
                           do
                              cge.fg[]->g[];
                              (* g.close; 'NB! g.close'->putLine;*)
                              browser.currentGroup.close
                           #)
                      #);
                    iSave: @item
                      (#
                         onStatus::  (#  do allowCommand->value #);
                         onSelect::  (#  do (true,false)->cge.save #)
                      #);
                    iSaveAs: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupEditorExists and not cge.isAfrejaEditor
                                ->value
                           #);
                         onSelect:: 
                           (# newName: ^text
                           do
                              cge.saveAs->newName[];
                              (if newName[] <> none then
                                  browser.currentGroup.close;
                                  newName[]->browser.selectFragmentGroup
                              if)
                           #)
                      #);
                    iSaveAll: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect::  (#  do edenv.saveAll #)
                      #);
                    iSaveAbstract: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do cge.saveabstract #)
                      #);
                    iRecover: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  cge.autoSaveFileExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do cge[]->recoverGroup #)
                      #);
                    iRevert: @item
                      (#
                         f: @file;
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                              (* '~'->(cge.fg.diskFileName).copyAppend->f.name;
                               f.entry.exists->value*)
                                  cge.touched > 0->value
                               else
                                  false->value
                              if);
                              (* temporarily disabled *)
                              false->value
                           #);
                         onSelect::  (#  do cge[]->revertGroup #)
                      #);
                    open:: 
                      (# 
                      do
                         'New...'->iNew.new;
                         'New BETA Program'->iNewBetaProgram.new;
                         'New BETA Library'->iNewBetaLibrary.new;
                         newSeparator;
                         'Close'->iClose.new;
                         'Save'->iSave.new;
                         'Save As...'->iSaveAs.new;
                         'Save All...'->iSaveAll.new;
                         'Save Abstract...'->iSaveAbstract.new;
                         newSeparator;
                         'Recover'->iRecover.new;
                         'Revert'->iRevert.new
                      #)
                 #);
               GroupEdit: @simplemenu
                 (#
                    iUndo: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  cge.askUndoPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do cge.undo;  #)
                      #);
                    iCut: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              cfe.frag[]->cge.cutForm;
                              cfe.frag[]->browser.currentGroup.close;
                              
                           #)
                      #);
                    iCopy: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do cfe.frag[]->cge.copyForm #)
                      #);
                    iPaste: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  (if formEditorExists then
                                      cfe.frag[]->cge.askPastePermission->value
                                   else
                                      false->value
                                  if)
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do cfe.frag[]->cge.pasteForm->ff[]; 
                           #)
                      #);
                    iAppend: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  (if formEditorExists then
                                      cfe.frag[]->cge.askPastePermission->value
                                   else
                                      false->value
                                  if)
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do none ->cge.pasteForm->ff[]; 
                           #)
                      #);
                    iInsertDescriptorFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do (cfe.frag[],2)->cge.insertForm->ff[]; 
                           #)
                      #);
                    iInsertAttributesFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do (cfe.frag[],3)->cge.insertForm->ff[]; 
                           #)
                      #);
                    iInsertDoPartFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do (cfe.frag[],31)->cge.insertForm->ff[]; 
                           #)
                      #);
                    iEditProperties: @item
                      (#
                         onStatus::  (#  do allowCommand->value #);
                         onSelect::  (#  do cge.editProperties;  #)
                      #);
                    iEditFormName: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              cfe.frag[]->cge.editFormName;
                              (cge.fg[],cfe.frag[])
                           #)
                      #);
                    iSearch: @item
                      (#
                         onStatus:: 
                           (#  do false (*groupEditorExists*) ->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              node: ^astInterface.ast;
                              
                           do
                              (if formEditorExists then
                                  cfe.frag[]->ff[]; cfe.cs.node[]->node[]
                              if);
                              (cge.fg[],ff[],node[])
                                ->edenv.searchTextDialog
                                  (#
                                     found:: 
                                       (#  do foundNode[]->findandselect #);
                                     notfound:: 
                                       (# help: ^text
                                       do
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    iSearchSLOT: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if groupEditorExists then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# node: ^astInterface.ast
                           do
                              cfe.frag[]->cge.searchSLOT->node[];
                              (if node[] <> none then
                                  node[]->findandselect
                              if);
                              
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         'Undo'->iUndo.new;
                         newSeparator;
                         'Cut'->iCut.new;
                         'Copy'->iCopy.new;
                         'Paste Before'->iPaste.new;
                         'Paste At End'->iAppend.new;
                         newSeparator;
                         'Insert Descriptor Fragment form...'
                           ->iInsertDescriptorFragmentForm.new;
                         'Insert Attributes Fragment Form...'
                           ->iInsertAttributesFragmentForm.new;
                         'Insert DoPart Fragment Form...'
                           ->iInsertDoPartFragmentForm.new;
                         newSeparator;
                         'Edit Properties...'->iEditProperties.new;
                         'Edit Fragment name...'->iEditFormName.new;
                         newSeparator;
                         'Search...'->iSearch.new;
                         'Search SLOT'->iSearchSLOT.new
                      #)
                 #);
               GroupPerform: @simplemenu
                 (#
                    iCheck: @item
                      (#
                         onStatus::  (#  do allowPerform->value #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->check
                           #)
                      #);
                    iReCheck: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowPerform then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->check #)
                      #);
                    iCompile: @item
                      (#
                         onStatus::  (#  do allowPerform->value #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->compile
                           #)
                      #);
                    iRecompile: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowPerform then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->compile #)
                      #);
                    iExecute: @item
                      (#
                         onStatus:: 
                           (#  do false-> (*allowCommand*) value #);
                         onSelect::  (#  do  #)
                      #);
                    iReExecute: @item
                      (#
                         onStatus:: 
                           (#  do false-> (*allowCommand*) value #);
                         onSelect::  (#  do  #)
                      #);
                    iCompileAndExecute: @item
                      (#
                         onStatus:: 
                           (#  do false-> (*allowCommand*) value #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->compile
                           #)
                      #);
                    iRecompileAndExecute: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if false (*allowCommand*) then
                                  (lastCompiledFg[] <> none )->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->compile #)
                      #);
                    iDebug: @item
                      (#
                         onStatus:: 
                           (#  do false (*groupEditorExists*) ->value #);
                         onSelect::  (#  do  #)
                      #);
                    iSemanticErrors: @item
                      (#
                         onStatus::  (#  do allowPerform->value #);
                         onSelect::  (#  do cge.fg[]->showSemanticErrors #)
                      #);
                    iSemanticLinks: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              (if cfe[] <> none then
                                  cfe.cs.hasSemanticLink->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do (cfe.cs.node[],false)->cfe.followSemanticLink
                           #)
                      #);
                    iShowDiagram: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              (if formEditorExists and not cfe.textEditMode then
                                  (edenv.frejaInterface[] <> none )->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              (cfe.frag[],cfe.cs.node[])
                                ->edenv.frejaInterface.openForm
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         'Check Current'->iCheck.new;
                         'Recheck'->iReCheck.new;
                         'Compile Current'->iCompile.new;
                         ('Recompile','r')->iRecompile.newKey;
                         'Execute Current'->iExecute.new;
                         ('Reexecute','e')->iReExecute.newKey;
                         newSeparator;
                         'Debug'->iDebug.new;
                         newSeparator;
                         'Semantic Errors...'->iSemanticErrors.new;
                         'Semantic Links'->iSemanticLinks.new;
                         newSeparator;
                         'Show Diagram'->iShowDiagram.new
                      #)
                 #);
               open::< 
                 (# 
                 do
                    'Group'->GroupFile.new;
                    Edit.new;
                    Edit[]->append;
                    View.new;
                    View[]->append;
                    SLOTs.new;
                    SLOTs[]->append;
                    'Fragment'->GroupEdit.new;
                    'Tools'->GroupPerform.new;
                    INNER
                 #)
            #)
       #);
     inithere: @boolean;
     init::< 
       (# 
       do
          ymerBrowser.browser[]->ymerBrowser.ymerCallBack.theBrowser[];
          ymerBrowser.ymerCallback[]->ymerBrowser.edenv.ymerCallback[];
          ymerBrowser.initCompiler;
          ('Sif: Known Bugs and Inconveniences','~beta/editor/agenda/TODO')
            ->ymerBrowser.helpDocument;
          INNER
       exit (mps.ast[],mps.betaCFL[])
       #);
     
  do init; INNER fragmentBrowser
  #)  

