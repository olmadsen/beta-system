ORIGIN '~beta/sourcebrowser/v0.1/ymer';
INCLUDE '~beta/guienv/v1.3.1/utils/simplemenu'
        '~beta/sourcebrowser/v0.1/private/parseEditor'
        '~beta/sourcebrowser/v0.1/private/semanticEditor'
        'ymercall';
BODY 'private/globalfrejacommbody'
     'private/compiler';
-- lib: Attributes --
fragmentBrowser: ymerApplication
  (#
     edenv: ^ymerBrowser.editorEnv;
     ymerBrowserWindow:: 
       (#
          eventhandler::  (#  #);
          open::  (#  #);
          theSifTextEditor: ^sifTextEditor;
          cfe: (* current form editor *) ^theSifTextEditor.codeViewerType;
          cbg: (* current browser group *) ^browser.projects.group;
          cge: (* current group editor *) ^edenv.groupEditor;
          cff: (* current fragment form *) ^mps.ast.fragmentform;
          ymerCallback: @ymerCall
            (# refreshGroup::  (#  do (fg[],none )->updateBrowser #) #);
          ongoingTextediting: @boolean;
          checkOngoingTextEditing:
            (# value: @boolean
            do
               browser.getFragmentFormEditor->theSifTextEditor[];
               (if theSifTextEditor[] <> none then
                   (if theSifTextEditor.sifViewer[] <> none then
                       theSifTextEditor.textediting->value
                   if)
               if)
            exit value
            #);
          formEditorExists:
            (# exists: @boolean
            do
               browser.getFragmentFormEditor->theSifTextEditor[];
               (if theSifTextEditor[] <> none then
                   (if theSifTextEditor.sifViewer[] <> none then
                       theSifTextEditor.sifViewer[]->cfe[];
                       (if not theSifTextEditor.textediting then
                           true->exists; INNER formEditorExists
                        else
                           true->ongoingTextediting
                       if)
                   if)
               if)
            exit exists
            #);
          groupEditorExists:
            (# exists: @boolean
            do
               (if browser[] <> none then
                   browser.currentgroup[]->cbg[];
                   browser.currentForm[]->cff[];
                   (if cbg[] <> none then
                       (if edenv[] <> none then
                           (if edenv.groupEditorList[] <> none then
                               cbg.fg[]
                                 ->edenv.groupEditorList.findOrCreateGroupEditor
                                 ->cge[];
                               (if cge[] <> none then
                                   (if not checkOngoingTextediting then
                                       true->exists; INNER groupEditorExists
                                   if)
                               if)
                           if)
                       if)
                   if)
               if)
            exit exists
            #);
          updateBrowser:
            (# fg: ^mps.ast.fragmentGroup; ff: ^mps.ast.fragmentForm
            enter (fg[],ff[])
            do
               (browser.currentProject[],none )->browser.setGroup;
               (browser.currentProject[],cbg[])->browser.setGroup;
               (if ff[] <> none then
                   (browser.currentProject[],cbg[],ff[])
                     ->browser.setFragmentForm
               if)
            #);
          findAndSelect:
            (# node: ^mps.ast.ast
            enter node[]
            do
               (if node[] <> none then
                   (if cfe[] <> none then
                       (if node.frag[] <> cfe.frag[] then
                           ((node.frag.father).fullName,node.frag.name)
                             ->browser.selectFragmentForm;
                           (if formEditorExists then
                               (node[],1,0,0)->cfe.setFocus
                            else
                               'formeditor not found'->putLine
                           if)
                        else
                           (node[],1,0,0)->cfe.setFocus
                       if)
                    else
                       ((node.frag.father).fullName,node.frag.name)
                         ->browser.selectFragmentForm;
                       (if formEditorExists then
                           (node[],1,0,0)->cfe.setFocus
                        else
                           'formEditor not found'->putLine
                       if)
                   if)
                else
                   'findAndSelect: node is none!'->putLine
               if)
            #);
          findForm:
            (# ff: ^mps.ast.fragmentForm
            enter ff[]
            do
               (if ff[] <> none then
                   ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                else
                   'findForm: ff is none!'->putLine
               if)
            #);
          findGroup:
            (# fg: ^mps.ast.fragmentGroup
            enter fg[]
            do
               (if fg[] <> none then
                   fg.fullName->browser.selectFragmentGroup
                else
                   'findGroup: fg is none!'->putLine
               if)
            #);
          externComm: @<<SLOT externprivate:Descriptor>>;
          onExternCommand:
            (# command,status: ^text; 
            enter command[]
            do
               <<SLOT onGlobalExternCommandBody:Descriptor>>
            exit status[]
            #);
          externCommand:
            (# command,status: ^text; 
            enter command[]
            do <<SLOT globalExternCommandBody:Descriptor>>
            exit status[]
            #);
          frejaComm: @
            (#
               onInput: @
                 (# t: ^text
                 enter t[]
                 <<SLOT commFrejaOnInput:DoPart>>
                 #);
               putline: @
                 (# t: ^text enter t[] <<SLOT commFrejaPutline:DoPart>> #);
               
            #);
          handleFrejaCommand:
            (# t: @text
            enter t
            do
               <<SLOT commFrejaHandleFrejaCommand:Descriptor>>;
               
            #);
          lastCompiledFg: ^mps.ast.fragmentGroup;
          compilerPrivate: @<<SLOT compilerPrivate:Descriptor>>;
          activateCompiler:
            (#
               fragmentFile: ^text;
               generateCode,
                 generateDebugInfo: @boolean
            enter
            (fragmentFile[],generateCode,
             generateDebugInfo)
            do <<SLOT activateCompilerBody:Descriptor>>
            #);
          initCompiler:
           <<SLOT initCompilerBody:Descriptor>>;
          formsWithSemanticErrors: @list
            (# element::< mps.ast.fragmentForm #);
          savedGroups: @set (# element::< mps.ast.fragmentGroup;  #);
          handleSavedGroups:
            (# done:< object; 
            do <<SLOT commFrejaHandleSavedGroups:Descriptor>>; 
            #);
          callCompiler:
            (#
               exp:
                 ^mps.ast.
                    expanded;
               node: ^mps.ast.ast
            do
               ''->writeInfoView;
               formsWithSemanticErrors.clear;
               'Calling BETA compiler'->putLine;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               INNER ;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               (if formsWithSemanticErrors.size = 0 then
                   handleSavedGroups
                else
                   l: formsWithSemanticErrors.scan
                     (# 
                     do
                        none ->node[];
                        'Semantic Error(s)'->writeInfoView;
                        (if current.root.kind = mps.ast.kinds.interior then
                            current.root[]->exp[];
                            loop: exp.suffixWalk
                              (# 
                              do
                                 (if current.hasSemanticError then
                                 (*current.semanticError->semanticErrorText->t[];*)
                                     current[]->node[]; leave loop
                                 if)
                              #);
                            (if node[] <> none then node[]->findAndSelect if)
                        if);
                        current.father->openSemanticErrorEditor;
                        leave l
                     #);
                   savedGroups.clear
               if);
               
            #);
          check:
            (#
               fg: ^mps.ast.fragmentGroup;
               status: @boolean;
               nontExist: @boolean;
               nont: ^mps.ast.unexpanded
            enter fg[]
            do
               edenv.nonterminalsExist->(nontExist,nont[]);
               (if nontExist then
                   nont[]->findAndSelect;
                   (none ,
                    'This fragment group cannot be checked\nbefore all nonterminals have been removed',
                    'Compiler message')->alertUser
                else
                   callCompiler
                     (#  do (fg.name,false,false)->activateCompiler;  #);
                   (formsWithSemanticErrors.size = 0)->status;
                   
               if)
            exit status
            #);
          compile:
            (#
               fg: ^mps.ast.fragmentGroup;
               status: @boolean;
               nontExist: @boolean;
               nont: ^mps.ast.unexpanded
            enter fg[]
            do
               edenv.nonterminalsExist->(nontExist,nont[]);
               (if nontExist then
                   nont[]->findAndSelect;
                   (none ,
                    'This fragment group cannot be compiled\nbefore all nonterminals have been removed',
                    'Compiler message')->alertUser
                else
                   callCompiler
                     (#  do (fg.name,true,true)->activateCompiler;  #);
                   (formsWithSemanticErrors.size = 0)->status;
                   
               if)
            exit status
            #);
          writeInfoView:
            (# t: ^text
            enter t[]
            do
            (* newLine;
             '<<<'->putText;
             t[]->putText;
             '>>>'->putLine;
             ''->infoView.contents.label;*) t[]->infoView.msg; 
            #);
          openParseEditor:
            (#
               pe: @browser.parseEditor
                 (# open::  (#  do (600,800)->size; fn[]->loadFiles #)
                 #);
               fn: ^text
            enter fn[]
            do pe.open
            #);
          openSemanticErrorEditor:
            (#
               theGroup: ^mps.ast.fragmentGroup;
               sv: @browser.semanticViewer
                 (#
                    open:: 
                      (#  do theGroup[]->fg[]; theGroup.name->fgName[];  #)
                 #)
            enter theGroup[]
            do sv.open
            #);
          recoverGroup:
            (# g: ^mps.ast.fragmentGroup; ge: ^edenv.groupEditor; 
            enter ge[]
            do
            (*g.name->help[];
             help.copy->oldName[];
             '#'->oldName.put;
             (g[],oldName[])->groupEnvPrivate.changeGroupFromFile;
             * *)
               ge.fg[]->g[];
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   '#'->g.restoreBackup;
                   ge.groupTouched;
                   (true,true)->ge.save;
                   ge.resetGroupTouch;
                   ge.closeGroup;
                   true->edenv.onGoingRecovery;
                   g[]->findGroup;
                   g[]->edenv.groupEditorList.findOrCreateGroupEditor->ge[];
                   (if ge[] = none then
                       'no groupEditor, should not happen'->putLine
                    else
                       ge.groupTouched
                   if);
                   false->edenv.onGoingRecovery;
                   
               if);
               
            #);
          revertGroup:
            (# g: ^mps.ast.fragmentGroup; ge: ^edenv.groupEditor; f: @file; 
            enter ge[]
            do
            (*g.name->help[];
             help.copy->oldName[];
             '#'->oldName.put;
             (g[],oldName[])->groupEnvPrivate.changeGroupFromFile;
             * *)
               ge.fg[]->g[];
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   '~'->g.restoreBackup;
                   '#'->(g.diskFileName).copyAppend->f.name;
                   (if f.entry.exists then f.delete if);
                   ge.groupTouched;
                   (true,true)->ge.save;
                   ge.resetGroupTouch;
                   ge.closeGroup;
                   g[]->findGroup;
                   g[]->edenv.groupEditorList.findOrCreateGroupEditor->ge[];
                   (if ge[] = none then
                       'no groupEditor, should not happen'->putLine
                   if);
                   
               if);
               
            #);
          menubarType:: 
            (#
               GroupFile: @simplemenu
                 (#
                    iNew: @item
                      (#
                         onStatus::  (#  do false->value #);
                         onSelect:: 
                           (#
                              ff: ^mps.ast.fragmentForm;
                              ge: ^edenv.groupEditor
                           do
                              edenv.new->ge[];
                              ge.fg.fragmentlist.scan
                                (# 
                                do
                                   (if current.type = mps.ast.formType then
                                       current.f[]->ff[]
                                   if)
                                #);
                              ff[]->findForm
                           #)
                      #);
                    iNewBetaProgram: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextediting->value #);
                         onSelect:: 
                           (#
                              ff: ^mps.ast.fragmentForm;
                              ge: ^edenv.groupEditor
                           do
                              edenv.newBetaProgram->ge[];
                              (if ge[] <> none then
                                  ge.fg.fragmentlist.scan
                                    (# 
                                    do
                                       (if current.type = mps.ast.formType then
                                           current.f[]->ff[]
                                       if)
                                    #);
                                  (if ff[] = none then
                                      'ff is none!'->putLine
                                   else
                                      ff[]->findForm
                                  if)
                              if)
                           #)
                      #);
                    iNewBetaLibrary: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextEditing->value #);
                         onSelect:: 
                           (#
                              ff: ^mps.ast.fragmentForm;
                              ge: ^edenv.groupEditor
                           do
                              edenv.newBetaLibrary->ge[];
                              (if ge[] <> none then
                                  ge.fg.fragmentlist.scan
                                    (# 
                                    do
                                       (if current.type = mps.ast.formType then
                                           current.f[]->ff[]
                                       if)
                                    #);
                                  (if ff[] = none then
                                      'ff is none!'->putLine
                                   else
                                      ff[]->findForm
                                  if)
                              if)
                           #)
                      #);
                    iClose: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #)
                           #);
                         onSelect:: 
                           (# 
                           do cge.closeGroup; (*currentGroup.close *)
                           #)
                      #);
                    iSave: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #)
                           #);
                         onSelect::  (#  do (true,true)->cge.save #)
                      #);
                    iSaveAs: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #);
                              
                           #);
                         onSelect::  
                           (# newName: ^text 
                           do cge.saveAs->newName[];
                              (if newName[]<>none then 
                                  newName[]->browser.selectFragmentGroup
                              if)
                           #)
                      #);
                    iSaveAll: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect::  (#  do edenv.saveAll #)
                      #);
                    iSaveAbstract: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #);
                              
                           #);
                         onSelect::  (#  do cge.saveabstract #)
                      #);
                    iRecover: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do cge.autoSaveFileExists->value #)
                           #);
                         onSelect::  (#  do cge[]->recoverGroup #)
                      #);
                    iRevert: @item
                      (#
                         f: @file;
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (# 
                                do
                                   '~'->(cge.fg.diskFileName).copyAppend
                                     ->f.name;
                                   f.entry.exists->value
                                #)
                           #);
                         onSelect::  (#  do cge[]->revertGroup #)
                      #);
                    open:: 
                      (# 
                      do
                         'New...'->iNew.new;
                         'New BETA Program'->iNewBetaProgram.new;
                         'New BETA Library'->iNewBetaLibrary.new;
                         newSeparator;
                         'Close'->iClose.new;
                         'Save'->iSave.new;
                         'Save As...'->iSaveAs.new;
                         'Save All...'->iSaveAll.new;
                         'Save Abstract...'->iSaveAbstract.new;
                         newSeparator;
                         'Recover'->iRecover.new;
                         'Revert'->iRevert.new
                      #)
                 #);
               GroupEdit: @simplemenu
                 (#
                    iUndo: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do cge.askUndoPermission->value #)
                           #);
                         onSelect:: 
                           (# 
                           do cge.undo; (cge.fg[],none )->updateBrowser
                           #)
                      #);
                    iCut: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do (cff[] <> none )->value #)
                           #);
                         onSelect:: 
                           (# 
                           do
                              cff[]->cge.cutForm;
                              (cge.fg[],none )->updateBrowser
                           #)
                      #);
                    iCopy: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do (cff[] <> none )->value #)
                           #);
                         onSelect::  (#  do cff[]->cge.copyForm #)
                      #);
                    iPaste: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (# 
                                do
                                   (if cff[] <> none then
                                       cff[]->cge.askPastePermission->value
                                   if)
                                #)
                           #);
                         onSelect:: 
                           (# ff: ^mps.ast.fragmentForm
                           do
                              cff[]->cge.pasteForm->ff[];
                              (cge.fg[],ff[])->updateBrowser
                           #)
                      #);
                    iAppend: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (# 
                                do none ->cge.askPastePermission->value
                                #)
                           #);
                         onSelect:: 
                           (# ff: ^mps.ast.fragmentForm
                           do
                              none ->cge.pasteForm->ff[];
                              (cge.fg[],ff[])->updateBrowser
                           #)
                      #);
                    iInsertDescriptorFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do cge.askInsertPermission->value #)
                           #);
                         onSelect:: 
                           (# ff: ^mps.ast.fragmentForm
                           do
                              (cff[],2)->cge.insertForm->ff[];
                              (cge.fg[],ff[])->updateBrowser
                           #)
                      #);
                    iInsertAttributesFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do cge.askInsertPermission->value #)
                           #);
                         onSelect:: 
                           (# ff: ^mps.ast.fragmentForm
                           do
                              (cff[],3)->cge.insertForm->ff[];
                              (cge.fg[],ff[])->updateBrowser
                           #)
                      #);
                    iInsertDoPartFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do cge.askInsertPermission->value #)
                           #);
                         onSelect:: 
                           (# ff: ^mps.ast.fragmentForm
                           do
                              (cff[],31)->cge.insertForm->ff[];
                              (cge.fg[],ff[])->updateBrowser
                           #)
                      #);
                    iEditProperties: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #)
                           #);
                         onSelect:: 
                           (# 
                           do
                              cge.editProperties;
                              (cge.fg[],none )->updateBrowser
                           #)
                      #);
                    iEditFormName: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do (cff[] <> none )->value #)
                           #);
                         onSelect:: 
                           (# ff: ^mps.ast.fragmentForm
                           do
                              cff[]->cge.editFormName;
                              (cge.fg[],cff[])->updateBrowser
                           #)
                      #);
                    iSearchSLOT: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do (cff[] <> none )->value #)
                           #);
                         onSelect:: 
                           (# node: ^mps.ast.ast
                           do
                              cff[]->cge.searchSLOT->node[];
                              (if node[] <> none then
                                  node[]->findandselect
                              if);
                              
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         'Undo'->iUndo.new;
                         newSeparator;
                         'Cut'->iCut.new;
                         'Copy'->iCopy.new;
                         'Paste Before'->iPaste.new;
                         'Paste Append'->iAppend.new;
                         newSeparator;
                         'Insert Descriptor Fragment form...'
                           ->iInsertDescriptorFragmentForm.new;
                         'Insert Attributes Fragment Form...'
                           ->iInsertAttributesFragmentForm.new;
                         'Insert DoPart Fragment Form...'
                           ->iInsertDoPartFragmentForm.new;
                         newSeparator;
                         'Edit Properties... '->iEditProperties.new;
                         'Edit Fragment name...'->iEditFormName.new;
                         newSeparator;
                         'Search SLOT'->iSearchSLOT.new
                      #)
                 #);
               GroupPerform: @simplemenu
                 (#
                    iCheck: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #)
                           #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->check
                           #)
                      #);
                    iReCheck: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do (lastCompiledFg[] <> none )->value #)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->check #)
                      #);
                    iCompile: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #)
                           #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->compile
                           #)
                      #);
                    iRecompile: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do (lastCompiledFg[] <> none )->value #)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->compile #)
                      #);
                    iExecute: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #);
                              false->value
                           #);
                         onSelect::  (#  do  #)
                      #);
                    iReExecute: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #);
                              false->value
                           #);
                         onSelect::  (#  do  #)
                      #);
                    iCompileAndExecute: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #)
                           #);
                         onSelect:: 
                           (# 
                           do cge.fg[]->lastCompiledFg[]; cge.fg[]->compile
                           #)
                      #);
                    iRecompileAndExecute: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do (lastCompiledFg[] <> none )->value #)
                           #);
                         onSelect::  (#  do lastCompiledFg[]->compile #)
                      #);
                    iDebug: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #);
                              false->value
                           #);
                         onSelect::  (#  do  #)
                      #);
                    iSemanticErrors: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              groupEditorExists
                                (#  do true->value #)
                           #);
                         onSelect:: 
                           (#  do cge.fg[]->openSemanticErrorEditor #)
                      #);
                    iSemanticLinks: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.cs.hasSemanticLink->value #)
                           #);
                         onSelect:: 
                           (#  do cfe.cs.node[]->cfe.followSemanticLink #)
                      #);
                    open:: 
                      (# 
                      do
                         'Check Current'->iCheck.new;
                         'Recheck'->iReCheck.new;
                         'Compile Current'->iCompile.new;
                         ('Recompile','r')->iRecompile.newKey;
                         'Execute Current'->iExecute.new;
                         ('Reexecute','e')->iReExecute.newKey;
                         newSeparator;
                         'Debug'->iDebug.new;
                         newSeparator;
                         'Semantic Errors...'->iSemanticErrors.new;
                         'Semantic Links'->iSemanticLinks.new
                      #)
                 #);
               Edit: @simplemenu
                 (#
                    eventHandler::< 
                      (#
                         onSelect::< 
                           (# 
                           do
                           (* (if formEditorExists then
                            'menu enable does not work!!'->putLine; enable
                            else
                            'menu disable does not work!!'->putLine; disable
                            if)*) 
                           #)
                      #);
                    iUndo: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askUndoPermission->value #)
                           #);
                         onSelect::  (#  do cfe.doUndo #)
                      #);
                    iCut: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doCut;  #)
                      #);
                    iCopy: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doCopy #)
                      #);
                    iPaste: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askPastePermission->value #)
                           #);
                         onSelect::  (#  do cfe.doPaste #)
                      #);
                    iTextedit: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (# 
                                do
                                   (if cfe.askTextEditMode then
                                       'Parse Text'->THIS(item).name
                                    else
                                       'Edit Text'->THIS(item).name
                                   if);
                                   true->value;
                                   
                                #);
                              (if ongoingTextediting then
                                  (if cfe.askTextEditMode then
                                      'Parse Text'->THIS(item).name
                                   else
                                      'Edit Text'->THIS(item).name
                                  if);
                                  true->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              (if cfe.askTextEditMode then
                                  cfe.doParseText; ''->writeInfoView
                               else
                                  cfe.doTextEdit;
                                  'Textediting Mode'->writeInfoView
                              if);
                              
                           #)
                      #);
                    iRevertTextEdit: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askTexteditMode->value #);
                              ongoingTextEditing->value
                           #);
                         onSelect:: 
                           (#  do cfe.doRevertTextEdit; ''->writeInfoView #)
                      #);
                    iShowTexteditCommands: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askTexteditMode->value #);
                              ongoingTextEditing->value
                           #);
                         onSelect::  (#  do cfe.doShowTexteditCommands;  #)
                      #);
                    iInsertBefore: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askInsertPermission->value #)
                           #);
                         onSelect::  (#  do cfe.doInsertBefore #)
                      #);
                    iInsertAfter: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askInsertPermission->value #)
                           #);
                         onSelect::  (#  do cfe.doInsertAfter #)
                      #);
                    iRemoveOptionals: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doRemoveOptionals #)
                      #);
                    iShowOptionals: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doShowOptionals #)
                      #);
                    iSearch: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doSearch #)
                      #);
                    iSubeditor: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.openSubeditor #)
                      #);
                    open:: 
                      (# 
                      do
                         ('Undo','z')->iUndo.newkey;
                         newSeparator;
                         ('Cut','x')->iCut.newkey;
                         ('Copy','c')->iCopy.newKey;
                         ('Paste','v')->iPaste.newKey;
                         newSeparator;
                         ('Textedit','t')->iTextEdit.newKey;
                         'Revert Textedit'->iRevertTextEdit.new;
                         'Show Textedit commands'->iShowTextEditCommands.new;
                         newSeparator;
                         ('Insert Before','u')->iInsertBefore.newKey;
                         ('Insert After','a')->iInsertAfter.newKey;
                         newSeparator;
                         ('Remove Optionals','n')->iRemoveOptionals.newKey;
                         ('Show Optionals','l')->iShowOptionals.newKey;
                         newSeparator;
                         ('Search...','s')->iSearch.newKey
                      #);
                    
                 #);
               View: @simplemenu
                 (#
                    eventHandler::< 
                      (#
                         onSelect::< 
                           (# 
                           do
                           (* (if formEditorExists then
                            'menu enable does not work!!'->putLine; enable
                            else
                            'menu disable does not work!!'->putLine; disable
                            if)*) 
                           #)
                      #);
                    iAbstract: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doAbstract #)
                      #);
                    iAbstractRecursively: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doAbstractRecursively;  #)
                      #);
                    iOverview: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doOverview;  #)
                      #);
                    iDetail: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doDetail #)
                      #);
                    iDetailRecursively: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doDetailRecursively;  #)
                      #);
                    iReprettyprint: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doReprettyprint;  #)
                      #);
                    iShowAstCS: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect:: 
                           (#  do cfe.cs.node[]->cfe.doShowAst;  #)
                      #);
                    iBack: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (# 
                                do
                                   cfe.cs.node[]->edenv.history.BackPossible
                                     ->value
                                #)
                           #);
                         onSelect:: 
                           (# 
                           do cfe.cs.node[]->edenv.history.back->findAndSelect
                           #)
                      #);
                    iForward: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (# 
                                do
                                   cfe.cs.node[]->edenv.history.ForwardPossible
                                     ->value
                                #)
                           #);
                         onSelect:: 
                           (# 
                           do
                              cfe.cs.node[]->edenv.history.forward
                                ->findAndSelect
                           #)
                      #);
                    open:: 
                      (# 
                      do
                         'Abstract'->iAbstract.new;
                         'Astract Recursively'->iAbstractRecursively.new;
                         ('Overview','o')->iOverview.newkey;
                         ('Detail','d')->iDetail.newkey;
                         'Detail Recursively'->iDetailRecursively.new;
                         newSeparator;
                         ('Reprettyprint','p')->iReprettyprint.newKey;
                         'Show AST Current Selection'->iShowAstCS.new;
                         newSeparator;
                         ('Back','b')->iBack.newKey;
                         ('Forward','f')->iForward.newKey;
                         
                      #);
                    
                 #);
               SLOTs: @simplemenu
                 (#
                    eventHandler::< 
                      (#
                         onSelect::< 
                           (# 
                           do
                           (* (if formEditorExists then
                            'menu enable does not work!!'->putLine; enable
                            else
                            'menu disable does not work!!'->putLine; disable
                            if)*) 
                           #)
                      #);
                    iSearchSlotBinding: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.cs.isSlot->value #)
                           #);
                         onSelect::  (#  do cfe.makeSearchSlotBinding #)
                      #);
                    iMakeDoPartSlot: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askMakeDoPartSlot->value #)
                           #);
                         onSelect::  (#  do cfe.doMakeDoPartSlot;  #)
                      #);
                    iMakeDescriptorSlot: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askMakeDescriptorSlot->value #)
                           #);
                         onSelect::  (#  do cfe.doMakeDescriptorSlot;  #)
                      #);
                    iMakeAttributesSlot: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askMakeAttributesSlot->value #)
                           #);
                         onSelect::  (#  do cfe.doMakeAttributesSlot;  #)
                      #);
                    iPasteFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.askPasteFragmentForm->value #)
                           #);
                         onSelect::  (#  do cfe.doPasteFragmentForm;  #)
                      #);
                    iHideImpl: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doHideImplementation;  #)
                      #);
                    iFillSlot: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              false->value;
                              formEditorExists
                                (#  do cfe.cs.isSlot->value #)
                           #);
                         onSelect::  (#  do cfe.doFillSlot;  #)
                      #);
                    iFillAllSlots: @item
                      (#
                         onStatus::  (#  do formEditorExists->value #);
                         onSelect::  (#  do cfe.doFillAllSlots #)
                      #);
                    open:: 
                      (# 
                      do
                         'Search SLOT Binding'->iSearchSlotBinding.new;
                         newSeparator;
                         'Make DoPart SLOT...'->iMakeDoPartSlot.new;
                         'Make Descriptor SLOT...'->iMakeDescriptorSlot.new;
                         'Make Attribute SLOT...'->iMakeAttributesSlot.new;
                         'Hide Implementation...'->iHideImpl.new;
                         newSeparator;
                         'Paste Fragment Form'->iPasteFragmentForm.new;
                         'Fill SLOT...'->iFillSlot.new;
                         'Fill All SLOTs'->iFillAllSlots.new;
                         
                      #);
                    
                 #);
               open:: 
                 (# 
                 do
                    GroupFile.new;
                    GroupEdit.new;
                    'Tools'->GroupPerform.new;
                    Edit.new;
                    View.new;
                    SLOTs.new
                 #)
            #)
       #);
     inithere: @boolean;
     init:: 
       (# 
       do
          objectPool.get
            (#
               type:: ymerBrowser.editorEnv;
               init:: 
                 (# 
                 do
                    (if true then 'init editorEnv from Ymer'->putLine;  if);
                    true->inithere;
                    (mps.ast[],mps.betaCFL[],THIS(fragmentBrowser)[])->obj.init
                 #)
            #)->edenv[];
          (if inithere then
              edenv.initiator[]->putLine;
              'ymersiflib'->edenv.initiator[]->putLine
          if);
          ymerBrowser.ymerCallback[]->edenv.ymerCallback[];
          ymerBrowser.initCompiler
       exit (mps.ast[],mps.betaCFL[])
       #);
     
  do init; INNER fragmentBrowser
  #)  

