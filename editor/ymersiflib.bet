ORIGIN '~beta/basiclib/basicsystemenv';
INCLUDE '~beta/sourcebrowser/ymer'
        '~beta/guienv/utils/simplemenu'
        '~beta/sourcebrowser/private/parseEditor'
        '~beta/sourcebrowser/private/semanticEditor'
        '~beta/dependency/dependency'
        'search'
        'ymercall'
        'createdialog'
        'frejasifcomm';
BODY 'private/globalexternalintbody'
     'private/ymersiflibslotbody'
     'private/ymersiflibperformbody';
(*
 * COPYRIGHT
 *   Copyright Mjolner Informatics, 1995-97
 *   All rights reserved.
 *)
-- systemlib: Attributes --
fragmentBrowser: ymerApplication
  (#
     onQuit::<  (#  do THIS(FragmentBrowser).askKillPrograms; INNER #);
     ymerBrowser: @ymerBrowserWindow;
     ymerBrowserWindow::< 
       (#
          browserTitle::<  (#  do ' and Editor'->title.append; INNER #);
          isEditor:< BooleanValue;
          theSifTextEditor: ^sifTextEditor;
          cbg: (* current browser group *) ^browser.projects.group;
          cff:
          (* current fragment form, not used anymore only in oldfromeditorExists *)
          ^astInterface.fragmentform;
          ymerCallback: @ymerCall
            (#
               refreshGroup::  (#  do (cb[],fg[],none )->updateBrowser #);
               findAndSelect:: 
                 (#  do node[]->THIS(ymerBrowserWindow).findAndSelect #);
               selectFragmentForm:: 
                 (# 
                 do ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                 #);
               getFragmentGroup:: 
                 (# 
                 do name[]->mps.fragmentGroupTable.getFragmentGroup->fg[]
                 #);
               getCurrentProject:: 
                 (# fg: ^astInterface.fragmentGroup
                 do
                    &fragmentGroupList[]->fgList[];
                    (if (browser.currentProject[] <> none ) then
                        browser.currentProject.groups.scan
                          (# 
                          do
                             current.getfg->fg[];
                             (if fg[] <> none then fg.name->fgList.append if)
                          #)
                    if)
                 #);
               getCurrentFragmentGroup:: 
                 (# 
                 do
                    (if browser.cge[] <> none then browser.cge.fg[]->fg[] if)
                 #);
               getCurrentFragmentForm:: 
                 (# 
                 do
                    (if browser.cfe[] <> none then
                        browser.cfe.frag[]->ff[]
                    if)
                 #);
               getCurrentNode:: 
                 (# 
                 do
                    (if browser.cfe[] <> none then
                        browser.cfe.cs.node[]->node[]
                    if)
                 #);
               check:: 
                 (#  do fg[]->THIS(ymerBrowserWindow).check->status #);
               betaenvVersion:: 
                 (#  do THIS(fragmentBrowser).betaenvVersion->t[] #);
               machineType::  (#  do machine_type->t[] #);
               infoView::  (#  do t[]->writeInfoView #);
               infoViewDone::  (#  do writeInfoViewDone #);
               onAboutToCloseGroup:: 
                 (# 
                 do
                    fg[]->THIS(fragmentBrowser).onAboutToCloseGroup
                      ->(OKtoClose,askSave)
                 #);
               onGroupSaved:: 
                 (# t: ^text
                 do
                    (if (browser.cge[] <> none ) and (browser.cge.fg[] = fg[])
                     then
                        browser.locationView.label->t[];
                        (' ',10)->t.inxPut;
                        t[]->browser.locationView.label
                    if)
                 #);
               onGroupNotSaved:: 
                 (#  do fg[]->THIS(fragmentBrowser).onGroupNotSaved #);
               onGroupTouched:: 
                 (# t: ^text
                 do
                    (if (browser.cge[] <> none ) and (browser.cge.fg[] = fg[])
                     then
                        false->browser.locationView.checked;
                        true->browser.locationView.changed
                    if)
                 #);
               onGroupDetouched:: 
                 (# t: ^text
                 do
                    (if (browser.cge[] <> none ) and (browser.cge.fg[] = fg[])
                     then
                        (if browser.cge.touched = 0 then
                            false->browser.locationView.changed
                        if)
                    if)
                 #);
               externalTextEdit:: 
                 (# 
                 do
                    textBefore[]->THIS(ymerBrowserWindow).externalTextEdit
                      ->textAfter[]
                 #);
               frejaInclude:: 
                 (#  do THIS(fragmentBrowser).frejaInclude->t[] #);
               setFrejaMark:: 
                 (#  do (frejaMark,fg[])->externalInterface.setFrejaMark #);
               getFrejaMark:: 
                 (#  do fg[]->externalInterface.getFrejaMark #);
               isEditor:: 
                 (# 
                 do true->value; THIS(ymerbrowserwindow).iseditor->value
                 #);
               (* futherbind in valhalla *)
               callSearch::  (#  do THIS(ymerBrowserWindow).callSearch #);
               callReplace::  (#  do THIS(ymerBrowserWindow).callReplace #);
               showSearchHits:: 
                 (# 
                 do
                    (fg[],ff[],node[],t[],caseSensitive,wholeWord,searchArea,
                     showAllHits)->THIS(ymerBrowserWindow).showSearchHits
                 #);
               isActive::  (#  do value->THIS(fragmentBrowser).isActive #);
               openSubeditor:: 
                 (# 
                 do
                    (editorRoot[],node[])->THIS(ymerBrowserWindow).openSubeditor
                 #);
               askShowDiagram::  (#  do true->value #);
               showDiagram:: 
                 (#  do node[]->externalInterface.showDiagram #);
               askEditUserinterface:: 
                 (# 
                 do node[]->THIS(fragmentBrowser).askEditUserInterface->value
                 #);
               editUserInterface:: 
                 (#  do node[]->THIS(fragmentBrowser).editUserInterface #);
               createImplementationFragment:: 
                 (# 
                 do fg[]->THIS(ymerBrowserWindow).createImplementationFragment
                 #);
               selectImplementationFragment:: 
                 (# 
                 do fg[]->THIS(ymerBrowserWindow).selectImplementationFragment
                 #)
            #);
          doCommand:
            (# 
            do
               (if (edenv[] <> none ) then
                   edenv.setWaitCursor;
                   INNER doCommand;
                   edenv.setStructureCursor
               if);
               
            #);
          checkOngoingTextEditing:
            (# value: @boolean
            do
               (if browser.cfe[] <> none then
                   browser.cfe.texteditMode->value
               if)
            exit value
            #);
          formEditorExists:
            (# value: @boolean
            do
               (if browser.cfe[] <> none then
                   (if not browser.cfe.texteditMode then true->value;  if)
               if)
            exit value
            #);
          groupEditorExists:
            (# value: @boolean
            do (browser.cge[] <> none )->value
            exit value
            #);
          allowCommand:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting and not
                   browser.cge.isReadOnly
                   (* and not browser.cge.isAfrejaEditor *) then
                       true->value
                   if)
               if)
            exit value
            #);
          allowPerform:
            (# value: @boolean
            do
               (if groupEditorExists then
                   (if not checkOngoingTextediting
                   (* and not
                    browser.cge.isReadOnly*) then
                       true->value
                   if)
               if)
            exit value
            #);
          allowDebug:< trueObject;
          allowOnForm:
            (# value: @boolean
            do
               (if groupEditorExists and not browser.propertySelected then
                   (if not checkOngoingTextediting and not
                   browser.cge.isReadOnly then
                       true->value
                   if)
               if)
            exit value
            #);
          updateBrowser:
            (#
               fg: ^astInterface.fragmentGroup;
               ff: ^astInterface.fragmentForm;
               cb: ^object;
               browser: ^sourceBrowser;
               theYmerBrowserWindow: ^ymerBrowserWindow;
               cbg: (* current browser group *) ^browser.projects.group
            enter (cb[],fg[],ff[])
            do
               (if (cb[] <> none ) and (cb## <= ymerBrowserWindow##) then
                   cb[]->theYmerBrowserWindow[];
                   theYmerBrowserWindow.browser[]->browser[];
                   browser.currentgroup[]->cbg[];
                   (if (cbg[] <> none ) and (cbg.fg[] = fg[]) then
                       (browser.currentProject[],none )->browser.setGroup;
                       (browser.currentProject[],cbg[])->browser.setGroup;
                       (if ff[] <> none then
                           (browser.currentProject[],cbg[],ff[])
                             ->browser.setFragmentForm
                       if)
                   if)
               if)
            #);
          findAndSelect:
            (# node: ^astInterface.ast
            enter node[]
            do
               (if node[] <> none then
                   (if browser.cfe[] <> none then
                       (if node.frag[] <> browser.cfe.frag[] then
                           ((node.frag.father).fullName,node.frag.name)
                             ->browser.selectFragmentForm;
                           (node[],1,0,0)->browser.cfe.setFocus
                        else
                           (node[],1,0,0)->browser.cfe.setFocus
                       if)
                    else
                       ((node.frag.father).fullName,node.frag.name)
                         ->browser.selectFragmentForm;
                       (node[],1,0,0)->browser.cfe.setFocus
                   if)
                else
                   'findAndSelect: node is none!'->putLine
               if)
            #);
          findForm:
            (# ff: ^astInterface.fragmentForm
            enter ff[]
            do
               (if ff[] <> none then
                   ((ff.father).fullName,ff.name)->browser.selectFragmentForm
                else
                   'findForm: ff is none!'->putLine
               if)
            #);
          findGroup:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            do
               (if fg[] <> none then
                   fg.fullName->browser.selectFragmentGroup
                else
                   'findGroup: fg is none!'->putLine
               if)
            #);
          externalInterface: (* Global commands *) @
            (#
               externalInterfaceException: exception
                 (# m: ^text
                 enter m[]
                 <<SLOT externalInterfaceException:DoPart>>
                 #);
               selectEditor:
                 (#
                    fe:
                      ^ymerBrowserWindow.codeeditor
                 enter fe[]
                 <<SLOT externalInterfaceSelectEditor:DoPart>>
                 #);
               openForm:
                 (#
                    name: ^text;
                    node:
                      ^edenv.mps.
                         ast;
                    fe: ^ymerBrowserWindow.codeEditor;
                    NoEditorForForm:< externalInterfaceException;
                    couldNotOpenFragmentform:< externalInterfaceException
                 enter (name[],node[])
                 <<SLOT externalInterfaceOpenForm:DoPart>>
                 exit fe[]
                 #);
               showDiagram:
                 (# node: ^astInterface.ast
                 enter node[]
                 do
                    (if node[] = none then
                        doCommand
                          (# 
                          do
                             (if (browser.cfe[] <> none ) then
                                 ''->writeInfoView;
                                 (browser.cfe.frag[],browser.cfe.cs.node[])
                                   ->edenv.frejaInterface.openForm
                              else
                                 'No fragment group selected'->writeInfoView
                             if)
                          #)
                     else
                        doCommand
                          (# 
                          do
                             ''->writeInfoView;
                             (node.frag[],node[])->edenv.frejaInterface.openForm
                          #)
                    if)
                 #);
               getEditorForForm:
                 (#
                    ff: ^edenv.mps.fragmentForm;
                    node: ^edenv.mps.ast;
                    fe: ^ymerBrowserWindow.codeEditor;
                    NoEditorForForm:< externalInterfaceException
                 enter (ff[],node[])
                 <<SLOT externalInterfaceGetEditorForForm:DoPart>>
                 exit fe[]
                 #);
               newBETAProgram:
                 (# name: ^text; NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETAProgram:DoPart>>
                 #);
               newBETALibrary:
                 (#
                    name: ^text;
                    NoEditorForForm:< externalInterfaceException
                 enter name[]
                 <<SLOT externalInterfaceNewBETALibrary:DoPart>>
                 #);
               saveAndQuit:
                 (# 
                 <<SLOT externalInterfaceSaveAndQuit:DoPart>>
                 #);
               save: (#  <<SLOT externalInterfaceSave:DoPart>> #);
               autoSave:
                 (# 
                 <<SLOT externalInterfaceAutoSave:DoPart>>
                 #);
               checkAutoSaveFile:
                 (# fg: ^astInterface.fragmentGroup; didRecover: @boolean
                 enter fg[]
                 <<SLOT externalInterfaceCheckAutoSaveFile:DoPart>>
                 exit
                 didRecover
                 #);
               check:
                 (# fg: ^edenv.mps.fragmentGroup
                 enter fg[]
                 <<SLOT externalInterfaceCheck:DoPart>>
                 #);
               toggleDebug:
                 (# 
                 <<SLOT externalInterfaceToggleDebug:DoPart>>
                 #);
               addProp:
                 (#
                    FG: ^astInterface.fragmentGroup;
                    propName,propString: ^text;
                    ifExists:<
                      (# delete: @boolean
                      do true->delete; INNER
                      exit delete
                      #)
                 enter (FG[],propName[],propString[])
                 <<SLOT externalInterfaceAddProp:DoPart>>
                 #);
               deleteProp:
                 (#
                    FG: ^astInterface.fragmentGroup;
                    propName,propString: ^text;
                    notFound:< object
                 enter (FG[],propName[],propString[])
                 <<SLOT externalInterfaceDeleteProp:DoPart>>
                 #);
               setFrejaMark:
                 (#
                    frejamark: @boolean;
                    FG: ^astInterface.fragmentGroup;
                    propvalue: @integer;
                    
                 enter (frejamark,FG[])
                 do
                    (if frejamark then 1->propValue else 0->propValue if);
                    'frejamark'
                      ->FG.prop.addprop (#  do propValue->addConst #);
                    
                 #);
               getFrejaMark:
                 (#
                    FG: ^astInterface.fragmentGroup;
                    GetProp:
                      (#
                         FG: ^astInterface.fragmentGroup;
                         P: @Text;
                         res: @integer;
                         Txt: ^Text;
                         
                      enter (P,FG[])
                      do
                         &Text[]->Txt[];
                         L: FG.prop.scanProp
                           (#
                              doProp:: 
                                (# 
                                do
                                   (if (P[]->Prop.equal) then
                                       scanParameters
                                         (#
                                            doConst:: 
                                              (#  do C->res; leave L #);
                                            doString:: 
                                              (#  do S[]->Txt[]; leave L #)
                                         #);
                                       
                                   if);
                                   
                                #);
                              
                           #);
                         INNER (* ugly *)
                      exit res
                      #);
                    
                 enter FG[]
                 exit (1 = (('frejamark',FG[])->getProp))
                 #);
               
            #);
          externalTextEdit:<
            (# textBefore,textAfter: ^text
            enter textBefore[]
            do INNER
            exit textAfter[]
            #);
          lastCompiledFg,lastCheckedFg,lastExecutedFg:
            ^astInterface.fragmentGroup;
          lastCompiledName,lastCheckedName,lastExecutedName: ^text;
          formsWithSemanticErrors: @list
            (# element:: mps.ast.fragmentForm #);
          savedGroups: @set (# element:: mps.ast.fragmentGroup;  #);
          callCompiler: doCommand
            (# exp: ^astInterface.expanded; node: ^astInterface.ast
            do
               (if theSemanticErrorViewer[] <> none then
                   (if theSemanticErrorViewer.opened then
                       theSemanticErrorViewer.clearBrowser
                   if)
               if);
               ''->writeInfoView;
               formsWithSemanticErrors.clear;
               'Calling BETA compiler'->putLine;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               INNER ;
               '-------------------------------------------------------------------------------'
                 ->putLine;
               (if formsWithSemanticErrors.size > 0 then
                   l: formsWithSemanticErrors.scan
                     (# 
                     do
                        none ->node[];
                        'Semantic Error(s)'->writeInfoView;
                        (if current.root.kind = mps.ast.kinds.interior then
                            current.root[]->exp[];
                            loop: exp.suffixWalk
                              (# 
                              do
                                 (if current.hasSemanticError then
                                 (*current.semanticError->semanticErrorText->t[];*)
                                     current[]->node[]; leave loop
                                 if)
                              #);
                            (if node[] <> none then node[]->findAndSelect if)
                        if);
                        current.father->showSemanticErrors;
                        leave l
                     #)
               if);
               
            #);
          fileChangedOutside: booleanValue
            (#
               fg: ^astInterface.fragmentGroup;
               diskfile,textfile: @file;
               t: @text
            enter fg[]
            do
               fg.diskfilename->diskfile.name;
               fg.textfilename->textfile.name;
               (if textfile.entry.exists and
               (textfile.entry.modTime > fg.modTime) then
               (* 'Source file: '->t.puttext;
                * fg.textfilename->t.putline;
                * 'changed since last access - reload?'->t.puttext;
                * (none ,'Notification',t[])
                *  ->promptForBoolean
                *      (# ok::
                *      (# do true->allowReload #);
                *    notok::
                *      (# do false->allowReload #);
                *    cancel::
                *       (# do false->allowReload #)
                *  #)
                *)
                   t.clear;
                   'Source file: '->t.puttext;
                   fg.textfilename->t.putline;
                   'has been changed since last access!'->t.putline;
                   t.newLine;
                   'The file must have been changed by some other application.'
                     ->t.putline;
                   'Quit immediately (and restart) to avoid further problems'
                     ->t.puttext;
                   (none ,t[],'Warning')->alertUser;
                   true->value
                else
                   (if diskfile.entry.exists then
                   (* 'Group file: '->t.puttext;
                    * fg.diskfilename->t.putline;
                    * 'changed since last access - reload?'->t.puttext;
                    * (none ,'Notification',t[])
                    *   ->promptForBoolean
                    * (# ok:: (# do true->allowReload #);
                    *    notok:: (# do false->allowReload #);
                    *    cancel:: (# do false->allowReload #)
                    * #)
                    *)
                       (if (diskfile.entry.modTime > fg.modTime) then
                           t.clear;
                           'Group file: '->t.puttext;
                           fg.diskfilename->t.putline;
                           'has been changed since last access!'->t.putline;
                           t.newLine;
                           'The file must have been changed by some other application.'
                             ->t.putline;
                           'Quit immediately (and restart) to avoid further problems'
                             ->t.puttext;
                           (none ,t[],'Warning')->alertUser;
                           true->value
                       if)
                    else
                       (if false then
                       (* outcommented in order to be able to check readonly files *)
                           checkIfOpen: edenv.groupEditorlist.scan
                             (# 
                             do
                                (if current.fg[] = fg[] then
                                    current.groupTouched; 
                                if)
                             #);
                           t.clear;
                           'Group file: '->t.puttext;
                           fg.diskfilename->t.putline;
                           'has been deleted!'->t.putline;
                           t.newLine;
                           'Use the Save command to reestablish it.'->t.putline;
                           'Quit immediately (and restart) to avoid further problems'
                             ->t.puttext;
                           (none ,t[],'Warning')->alertUser;
                           true->value
                       if)
                   if)
               if)
            #);
          nonterminalAllowed:< booleanValue
            (# nont: ^astinterface.unexpanded enter nont[] do INNER #);
          DGchangedOutside:
            (#
               changedOutside,otherError,nontExist: @boolean;
               fg,currentFG: ^astInterface.fragmentGroup;
               onFragmentGroupChanged:< object;
               nont: ^astInterface.unexpanded;
               isOpen,keepDoRealOpen: @boolean;
               textFile: ^text;
               f: @file
            enter fg[]
            do
               scanningDG:
               fg.name->mps.AST.expandToFullPath
                 ->mps.DG.scanExtent
                   (#
                      warn: ^text;
                      Report:
                        (# msg: ^text
                        enter msg[]
                        do
                           (none ,msg[],'Dependency graph error')->alertUser;
                           true->otherError;
                           leave scanningDG
                        #);
                      startingParsingNotification::  (#  do  #);
                      reloadingNotification::  (#  do false->doReload #);
                      DoubleFormException::  (#  do true->continue #);
                      transAccessException:: 
                        (# 
                        do
                           'No read access to: '->warn[];
                           FN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      circularDependencyException:: 
                        (# 
                        do
                           'Circular dependency: '->warn[];
                           fg.name->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      unknownPropertyException::  (#  do true->continue #);
                      emptyFragmentException::  (#  do true->continue #);
                      parseException:: 
                        (# 
                        do
                           fullFN[]->openParseEditor;
                           true->continue;
                           true->otherError;
                           leave scanningDG
                        #);
                      transCreateDirException:: 
                        (# 
                        do
                           'Unable to create directory: '->warn[];
                           FN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      MPSexception:: 
                        (# 
                        do
                           'MPS overflow - '->warn[];
                           T[]->warn.puttext;
                           ' too large)'->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      notExistingException:: 
                        (# 
                        do
                           'Not a fragment file: '->warn[];
                           fullFN[]->warn.putline;
                           true->continue;
                           warn[]->Report
                        #);
                      noSpaceException:: 
                        (# 
                        do
                           'No space on disk'->warn[];
                           true->continue;
                           warn[]->Report
                        #);
                      fragmentException::
                      (* Note that the group is closed here.
                       * Sif and other tools may NOT want to do this!
                       *) 
                        (# keepMsg: ^text
                        do (*CloseGroup;*)
                           msg.copy->keepMsg[];
                           msg.clear;
                           (if errNo
                            // 1 then
                               'Two or more slots have the name: '->msg.puttext;
                               slot.name->msg.puttext;
                               
                            // 2 then
                               'No free slot found for: '->msg.Puttext;
                               FF.name->msg.puttext;
                               
                            // 3 then
                               'Category of fragment: '->msg.puttext;
                               FF.name->msg.puttext;
                               ' does not correspond to category of slot'
                                 ->msg.puttext;
                               
                            // 4 then
                               'The slot: '->msg.Puttext;
                               FF.name->msg.puttext;
                               ' Is already bound'->msg.Puttext
                            // 5 then
                               'Category of: '->msg.Puttext;
                               FF.name->msg.Puttext;
                               ' must be either: '->msg.Putline;
                               'Descriptor (ObjectDescriptor, DescriptorForm) or '
                                 ->msg.puttext;
                               'Attributes (AttributeDecl, AttributesForm)'
                                 ->msg.putline;
                               
                            // 6 then
                               'Category of the following slot is not supported!\n\n\t'
                                 ->msg.putText;
                               slot.name->msg.putLine;
                               '\nCategory must be: descriptor, attributes or doPart'
                                 ->msg.putLine
                            // 7 then
                               'Warning: mainPart-slots are NOT fully supported!'
                                 ->msg.putLine
                           if);
                           (if slot[] <> none then
                               msg[]->Report
                            else
                               (if ff[] <> none then
                                   msg[]->Report
                                else
                                   (if fg[] <> none then
                                       msg[]->Report
                                    else
                                       'fragmentException: '->putText;
                                       errNo->putInt;
                                       ': '->putLine;
                                       keepMsg[]->putLine;
                                       keepMsg[]->report
                                   if)
                               if)
                           if)
                        #);
                      propertyException:: 
                        (# keepMsg: ^text
                        do
                           msg.copy->keepMsg[];
                           msg.clear;
                           true->continue;
                           (if n
                            // 1 then
                               'Two or more origins are specified:\n\t'
                                 ->msg.puttext;
                                 (# g: ^astInterface.fragment; 
                                 do FG.origin->g[]; g.name->msg.puttext; 
                                 #);
                               '\n\t'->msg.puttext;
                               t[]->msg.putline
                            // 2 then
                               'The reserved property name "'->msg.puttext;
                               t.makeUC;
                               t[]->msg.puttext;
                               '"\n\tappears in the property list for '
                                 ->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               
                            // 3 then
                               'Illegal "'->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               '" property: "'->msg.puttext;
                               t.makeUC;
                               t[]->msg.puttext;
                               '"\n'->msg.puttext
                            // 4 then
                               'Empty filename in property "'->msg.puttext;
                               p.makeUC;
                               p[]->msg.puttext;
                               '"'->msg.put;
                               msg.newline;
                               
                            // 5 then
                               'Unrecognised property: '->Msg.Puttext;
                               p[]->Msg.Putline;
                               true->warning;
                               true->continue
                            // 6 then
                               'Property "'->msg.puttext;
                               p[]->msg.puttext;
                               '" has no value for "'->msg.putText;
                               t[]->msg.putText;
                               '"\n'->msg.putLine;
                               true->warning
                           if);
                           (if not warning then
                               '\n\n\tA ";" may be missing before '
                                 ->msg.puttext;
                               t[]->msg.putline;
                               (if fg[] <> none then
                                   'in '->msg.append;
                                   fg.name->msg.append;
                                   msg[]->Report
                                else
                                   'propertyException: '->putText;
                                   n->putInt;
                                   ': '->putLine;
                                   keepMsg[]->putLine;
                                   keepMsg[]->report
                               if);
                               
                           if)
                        #);
                      
                   do
                      current[]->currentFG[];
                      checkIfOpen: edenv.groupEditorlist.scan
                        (# 
                        do
                           (if current.fg[] = currentFG[] then
                               (if current.touched > 0 then
                                   onFragmentgroupChanged
                               if);
                               current.nonterminalsExist->(nontExist,nont[]);
                               (if nontExist then
                                   (if nont[]->nonterminalAllowed then
                                   (* continue *)
                                       
                                    else
                                       nont[]->findAndSelect;
                                       (none ,
                                        'This fragment group cannot be checked\nbefore all nonterminals have been removed',
                                        'Compiler message')->alertUser;
                                       true->otherError;
                                       leave scanningDG
                                   if);
                                   
                                else
                                   (true,false,true)->current.save
                               if);
                               
                           if)
                        #);
                      (if currentFG[]->fileChangedOutside then
                          true->changedOutSide;
                          leave scanningDG;
                          false->isOpen;
                          groupEditorListScan: edenv.groupEditorlist.scan
                            (# 
                            do
                               (if current.fg[] = currentFG[] then
                                   true->isOpen; leave groupEditorListScan
                               if)
                            #);
                          newLine;
                          currentFG.name->putText;
                          (if isOpen then
                              ' is open'->putLine; leave scanningDG
                           else
                              ' is not Open'->putLine;
                              mps.ast.doRealOpen->keepDoRealOpen;
                              true->mps.ast.doRealOpen;
                              fg.textFileName->textFile[]->f.name;
                              (if f.entry.exists then
                                  'xyzzyx'->textFile.append
                              if);
                              keepDoRealOpen->mps.ast.doRealOpen;
                              false->changedOutSide;
                              
                          if)
                      if);
                      
                   #)
            exit (changedOutside,otherError)
            #);
          compilerPrivate: @<<SLOT compilerPrivate:Descriptor>>;
          initCompiler:
            (# version: ^text
            do
               <<SLOT initCompilerBody:Descriptor>>
            exit version[]
            #);
          setCompilerOptions:
            (# compiler:^object;
            do
               <<SLOT setCompilerOptions:descriptor>>;
               INNER setCompilerOptions;
            #);
          check:<
            (# fg: ^astInterface.fragmentGroup; status: @boolean; 
            enter fg[]
            do <<SLOT compilerCheck:Descriptor>>; INNER
            exit status
            #);
          compile:<
            (#
               fg:
                 ^astInterface.fragmentGroup;
               status: @boolean;
               
            enter fg[]
            do
               <<SLOT compilerCompile:Descriptor>>;
               INNER
            exit status
            #);
          jobFileRunning:< booleanValue
            (#  <<SLOT compilerJobFileRunning:DoPart>> #);
          waitOnJobfile:<
            (# 
            <<SLOT compilerWaitOnJobfile:DoPart>>
            #);
          writeInfoView:
            (#
               t: ^text;
               myMsgPop:
                 (# 
                 do
                    (if browser.infoView.msgList.head <> none then
                        (browser.infoView.msgList.head).elm[]
                          ->browser.infoView.done;
                        browser.infoView.msgList.head
                          ->browser.infoView.msgList.delete
                    if)
                 #)
            enter t[]
            do
               (if t.length > 0 then
                   t[]->browser.infoView.msg; browser.infoView.msgPush
                else
                   ''->browser.infoView.msg
               if)
            #);
          writeInfoViewDone:
            (#
               myMsgPop:
                 (# 
                 do
                    (if browser.infoView.msgList.head <> none then
                        (browser.infoView.msgList.head).elm[]
                          ->browser.infoView.done;
                        browser.infoView.msgList.head
                          ->browser.infoView.msgList.delete
                    if)
                 #)
            do myMsgPop
            #);
          openParseEditor:
            (#
               pe: @parseEditor
                 (# open::  (#  do (600,800)->size; fn[]->loadFiles #)
                 #);
               fn: ^text
            enter fn[]
            do pe.open
            #);
          theSemanticErrorViewer: ^browser.mySemanticViewer;
          semanticErrorViewer: browser.mySemanticViewer
            (#
               checkNode:: 
                 (# 
                 do
                    (if node.hasSemanticError then
                        (if (node.semanticError->isWarning) then
                            (if includeWarnings then
                                true->value;
                                'Warning: '->t[];
                                node.semanticError->semanticErrorText->t.append
                            if)
                         else
                            true->value;
                            (if includeWarnings then
                                'Error: '->t[]
                             else
                                &text[]->t[]
                            if);
                            node.semanticError->semanticErrorText->t.append
                        if)
                    if)
                 #);
               setTitle:: 
                 (# 
                 do
                    (if noHits then
                        'No semantic errors'->t[]
                     else
                        'Semantic errors'->t[]
                    if);
                    (if includeWarnings then ' or warnings'->t.append if);
                    ' in: '->t.append;
                    fgName[]->t.append
                 #);
               setNodesListLabel:: 
                 (# 
                 do
                    (if includeWarnings then
                        'Semantic Errors and Warnings'->t[]
                     else
                        'Semantic Errors'->t[]
                    if)
                 #);
               showSourceLine:: trueObject;
               open:: 
                 (# 
                 do
                    theGroupWithSemanticErrors[]->fg[];
                    theGroupWithSemanticErrors.name->fgName[];
                    
                 #)
            #);
          theGroupWithSemanticErrors: ^astInterface.fragmentGroup;
          showSemanticErrors:
            (# 
            enter theGroupWithSemanticErrors[]
            do
               (if theSemanticErrorViewer[] = none then
                   &semanticErrorViewer[]->theSemanticErrorViewer[];
                   theSemanticErrorViewer.open
                else
                   (if not theSemanticErrorViewer.opened then
                       &semanticErrorViewer[]->theSemanticErrorViewer[];
                       theSemanticErrorViewer.open
                    else
                       (theGroupWithSemanticErrors[],none ,none )
                         ->theSemanticErrorViewer.registerFragmentGroup;
                       theSemanticErrorViewer.show;
                       theSemanticErrorViewer.bringToFront
                   if)
               if)
            #);
          searchFor: @ (# t: ^text; caseSensitive,wholeWord: @boolean #);
          searchfg: ^astInterface.fragmentGroup;
          searchff: ^astInterface.fragmentForm;
          searchNode: ^astInterface.ast;
          theSearchViewer: ^browser.searchViewer;
          searchViewer: browser.searchViewer
            (#
               checkNode:: 
                 (# lexText: ^astInterface.lexemText; help: ^text
                 do
                    (if
                    (searchFor.t[],node[],searchFor.caseSensitive,
                     searchFor.wholeWord)->edenv.inLexem then
                        true->value;
                        node[]->lexText[];
                        '...'->t[];
                        lexText.getText->t.append;
                        '...'->t.append
                    if)
                 #);
               setTitle:: 
                 (# 
                 do
                    (if noHits then
                        'No search hits in: '->t[]
                     else
                        'Search hits in: '->t[]
                    if);
                    fgName[]->t.append
                 #);
               setNodesListLabel::  (#  do 'Nodes'->t[] #);
               showSourceLine:: trueObject;
               open:: 
                 (# 
                 do
                    searchfg[]->fg[];
                    (if fg[] <> none then fg.name->fgName[] if);
                    
                 #)
            #);
          searchArea_cs: (#  exit 1 #);
          searchArea_ff: (#  exit 2 #);
          searchArea_fg: (#  exit 3 #);
          searchArea_project: (#  exit 4 #);
          showSearchHits:
            (#
               fg: ^astInterface.fragmentGroup;
               ff: ^astInterface.fragmentForm;
               node: ^astInterface.ast;
               t: ^text;
               caseSensitive,wholeWord,showAllHits: @boolean;
               searchArea: @integer
            enter
            (fg[],ff[],node[],t[],caseSensitive,wholeWord,searchArea,
             showAllHits)
            do
               t[]->searchFor.t[];
               caseSensitive->searchFor.caseSensitive;
               wholeWord->searchFor.wholeword;
               (if searchArea
                // searchArea_cs then
                   node[]->searchNode[]; ff[]->searchff[]; fg[]->searchfg[]
                // searchArea_ff then
                   none ->searchNode[]; ff[]->searchff[]; fg[]->searchfg[]
                // searchArea_fg then
                   none ->searchNode[]; none ->searchff[]; fg[]->searchfg[]
                // searchArea_project then
                   none ->searchNode[]; none ->searchff[]; none ->searchfg[]
               if);
               (if theSearchViewer[] = none then
                   &searchViewer[]->thesearchViewer[];
                   showAllHits->theSearchViewer.showAllHits;
                   thesearchViewer.open
                else
                   (if not thesearchViewer.opened then
                       &searchViewer[]->thesearchViewer[];
                       showAllHits->theSearchViewer.showAllHits;
                       thesearchViewer.open;
                       (searchfg[],searchff[],searchNode[])
                         ->thesearchViewer.registerFragmentGroup
                    else
                       showAllHits->theSearchViewer.showAllHits;
                       (searchfg[],searchff[],searchNode[])
                         ->thesearchViewer.registerFragmentGroup;
                       thesearchViewer.show;
                       thesearchViewer.bringToFront
                   if)
               if)
            #);
          recoverGroup: doCommand
            (#
               ge: ^editorEnv.groupEditor;
               g: ^astInterface.fragmentGroup;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->g.restoreBackup;
                   ge.groupTouched;
                   (false,true,true)->ge.save;
                   ge.resetGroupTouch;
                   browser.currentGroup.close;
                   true->edenv.onGoingRecovery;
                   groupName[]->browser.selectFragmentGroup;
                   (if browser.cge[] = none then
                       'no groupEditor, should not happen'->putLine
                    else
                       browser.cge.groupTouched;
                       false->browser.cge.autoSaveFileExists
                   if);
                   false->edenv.onGoingRecovery;
                   
               if);
               
            #);
          revertGroup: doCommand
            (#
               g: ^astInterface.fragmentGroup;
               ge: ^editorEnv.groupEditor;
               f: @file;
               groupName: ^text;
               
            enter ge[]
            do
               (if ge[]
                // none then 'no groupEditor, should not happen'->putLine; 
                else
                   ge.fg[]->g[];
                   g.fullName->groupName[];
                   '#'->((g.diskFileName).copy).Append->f.name;
                   (if f.entry.exists then f.delete if);
                   ge.resetGroupTouch;
                   edenv.showEditors;
                   browser.currentGroup.close;
                   edenv.showEditors;
                   groupName[]->browser.selectFragmentGroup;
                   edenv.showEditors;
                   
               if);
               
            #);
          Edit: @codeEditorEditMenu
            (#
               formEditorExists::  (#  do browser.cfe[]->cfe[] #);
               writeStatus::  (#  do t[]->writeInfoView #);
               open::  (#  do  #)
            #);
          View: @codeEditorViewMenu
            (#
               formEditorExists::  (#  do browser.cfe[]->cfe[] #);
               open::  (#  do  #)
            #);
          SLOTs: @codeEditorSLOTsMenu
            (#
               formEditorExists::  (#  do browser.cfe[]->cfe[] #);
               open::  (#  do  #)
            #);
          popupMenu: @codeEditorPopUpMenu
            (#
               formEditorExists::  (#  do browser.cfe[]->cfe[] #);
               open::  (#  do  #)
            #);
          onCodeViewOpen::< (* FGC, til valhalla *) 
            (# 
            do
               INNER
               (* this operation is called before the sifviewer is created *)
            #);
          workspace::< 
            (#
               menubarType::< 
                 (#
                    editMenu: @codeEditorEditMenu
                      (#
                         formEditorExists:: 
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter useExternalTextEditor
                           do
                              open;
                              THIS(editMenu)[]->THIS(menubarType).append;
                              (*THIS(codeEditorEditMenu)[]
                               ->THIS(workspace).cfe.theEditMenu[]*)
                              
                           #)
                      #);
                    viewMenu: @codeEditorViewMenu
                      (#
                         formEditorExists:: 
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do
                              open;
                              THIS(viewMenu)[]->THIS(menubarType).append;
                              (* THIS(codeEditorViewMenu)[]
                               ->THIS(workspace).cfe.theViewMenu[]*)
                              
                           #)
                      #);
                    popUpMenu: @codeEditorPopUpMenu
                      (#
                         formEditorExists:: 
                           (#  do THIS(workspace).cfe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do
                              open;
                              THIS(codeEditorPopUpMenu)[]
                                ->THIS(menubarType).append;
                              
                           #)
                      #);
                    open::< 
                      (# 
                      do
                         fileMenu.setup;
                         useExternalTexteditor->editMenu.setup;
                         isBETAsif->viewMenu.setup;
                         importMenu.setup;
                         scrollMenu.setup;
                         closeMenu.setup;
                         INNER ;
                         
                      #)
                 #)
            #);
          codeViewWindow::< 
            (#
               menubarType::< 
                 (#
                    editMenu: @codeEditorEditMenu
                      (#
                         formEditorExists::  (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter useExternalTextEditor
                           do
                              open;
                              THIS(editMenu)[]->THIS(menubarType).append;
                              (*THIS(codeEditorEditMenu)[]->fe.theEditMenu[]*)
                              
                           #)
                      #);
                    viewMenu: @codeEditorViewMenu
                      (#
                         formEditorExists::  (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do
                              open;
                              THIS(viewMenu)[]->THIS(menubarType).append;
                              (*THIS(codeEditorViewMenu)[]->fe.theViewMenu[]*)
                              
                           #)
                      #);
                    popUpMenu: @codeEditorPopUpMenu
                      (#
                         formEditorExists::  (#  do fe[]->cfe[] #);
                         setup:
                           (# 
                           enter BETAsif
                           do
                              open;
                              THIS(codeEditorPopUpMenu)[]
                                ->THIS(menubarType).append;
                              
                           #)
                      #);
                    open::< 
                      (# 
                      do
                         fileMenu.setup;
                         useExternalTexteditor->editMenu.setup;
                         isBETAsif->viewMenu.setup;
                         buffersMenu.setup;
                         INNER
                      #)
                 #)
            #);
          subeditorWindow:< window
            (#
               eventhandler::< 
                 (#
                    onActivate::< 
                      (# 
                      do true->THIS(fragmentBrowser).isActive; INNER
                      #);
                    onDeactivate::< 
                      (# 
                      do false->THIS(fragmentBrowser).isActive; INNER
                      #);
                    
                 #);
               editor: @sifTextEditor (# codeViewerType::< codeeditor #);
               fe,sed: ^codeeditor;
               attach:
                 (# fe: ^codeEditor
                 enter fe[]
                 do
                    fe[]->Edit.attach;
                    fe[]->view.attach;
                    (*                  (fe[],edenv[])->slots.attach*)
                    
                 #);
               File: @mySimpleMenu
                 (#
                    iClose: @item
                      (# onSelect::  (#  do THIS(subEditorWindow).close #)
                      #);
                    open::  (#  do ('Close','w')->iClose.newKey #)
                 #);
               Edit: @codeEditorEditMenu;
               View: @codeEditorViewMenu;
               menubarType::< 
                 (#
                    open::< 
                      (# 
                      do
                         File.new;
                         File[]->append;
                         Edit.new;
                         Edit[]->append;
                         View.new;
                         View[]->append;
                         INNER ;
                         
                      #)
                 #);
               open::< 
                 (#
                    ff: ^astInterface.fragmentForm;
                    theEditorRoot,node: ^astInterface.ast;
                    help: ^text
                 enter (theEditorRoot[],node[])
                 do
                    (if theEditorRoot[] <> none then
                        ((theEditorRoot.frag.father).name).copy
                          ->edenv.fragmentDefaultName->help[];
                        help.length->help.pos;
                        '-'->help.put;
                        theEditorRoot.frag.name->help.putText;
                        help[]->title;
                        THIS(subeditorWindow).contents->editor.open;
                        (mps.ast[],fe.theGrammar[],theEditorRoot[],node[])
                          ->editor.newfragment->sed[];
                        (if sed[] <> none then
                            true->sed.isAsubEditor;
                            fe[]->sed.fatherEditor[];
                            fe.isReadOnly->sed.isReadOnly
                              ->sed.theSifTexteditor.isReadOnly;
                            sed[]->fe.subeditors.append;
                            sed[]->THIS(subeditorWindow).attach;
                            
                         else
                            'no subeditor created!'->putLine
                        if);
                        editor.size->size;
                        true->editor.bindBottom->editor.bindRight
                    if);
                    INNER ;
                    
                 #);
               
            #);
          openSubeditor:
            (#
               theSubwindow: ^subeditorWindow;
               fe: ^codeeditor;
               editorRoot,node: ^astInterface.ast
            enter (editorRoot[],node[])
            do
               (editorRoot.frag[],node[])->edenv.findEditorForForm->fe[];
               (if fe[] <> none then
                   &subeditorWindow[]->theSubwindow[];
                   fe[]->theSubWindow.fe[];
                   (editorRoot[],node[])->theSubwindow.open
                else
                   'openSubeditor: node codeeditor found'->putLine;
                   (if browser.cfe[] <> none then
                       (editorRoot[],editorRoot[],true)
                         ->browser.cfe.selectNodeInFormEditor
                   if)
               if)
            #);
          callSearch:
            (# 
            do
               (browser.cge[],browser.cfe[],none ,none ,none )
                 ->edenv.searchTextDialog
                   (#
                      found:: 
                        (# 
                        do
                           (if edenv.trace[12] then 'found'->putLine if);
                           foundNode[]->findandselect
                        #);
                      notfound:: 
                        (# help: ^text
                        do
                           ascii.bel->put;
                           searchText[]->help[];
                           ' not found'->help.append;
                           (if edenv.trace[12] then help[]->putLine if);
                           help[]->writeInfoView
                        #)
                   #)
            #);
          callReplace:
            (# 
            do
               (browser.cge[],browser.cfe[],none ,none ,none )
                 ->edenv.replaceTextDialog
                   (#
                      found:: 
                        (# 
                        do
                           (if edenv.trace[12] then 'found'->putLine if);
                           foundNode[]->findandselect
                        #);
                      notfound:: 
                        (# help: ^text
                        do
                           ascii.bel->put;
                           searchText[]->help[];
                           ' not found'->help.append;
                           (if edenv.trace[12] then help[]->putLine if);
                           help[]->writeInfoView
                        #)
                   #)
            #);
          createImplementationFragment:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            <<SLOT createImplementationFragment:DoPart>>
            #);
          selectImplementationFragment:
            (# fg: ^astInterface.fragmentGroup
            enter fg[]
            <<SLOT selectImplementationFragment:DoPart>>
            #);
          calculateBodyAndOriginName:
            (# (* calculates the body name for origin fg and vice versa *)
               originfg,bodyfg: ^astInterface.fragmentGroup;
               bodyName,originName: ^text
            enter (originfg[],bodyfg[])
            <<SLOT calculateBodyAndOriginName:DoPart>>
            exit (bodyName[],originName[])
            #);
          insertBodyAndOrigin:
            (# originfg,bodyfg: ^astInterface.fragmentGroup
            enter (originfg[],bodyfg[])
            <<SLOT insertBodyAndOrigin:DoPart>>
            #);
          menubarType::< 
            (#
               fileMenuType::< 
                 (#
                    item: menuitem
                      (#
                         enabled: @boolean
                         (* Set to true or false to enable or disable THIS(item) *)
                         ;
                         onSelect:< object
                         (* Shortcut for eventhandler.onSelect *) ;
                         onStatus:< booleanValue
                         (* Shortcut for eventhandler.onStatus *)
                           (#  do enabled->value; INNER #);
                         new:
                         (* Open and append THIS(Item) with label itemtext *)
                           (# itemtext: ^text; 
                           enter itemtext[]
                           do
                              true->enabled;
                              open;
                              (if itemtext[] <> none then
                                  itemtext[]->name
                              if);
                              THIS(item)[]->append;
                              INNER ;
                              
                           #);
                         newkey: new (* Also add key quivalent *)
                           (# equiv: @char enter equiv do equiv->key #);
                         eventhandler::< 
                           (#
                              onSelect::< 
                                (#  do THIS(item).onSelect; INNER #);
                              onStatus::< 
                                (# 
                                do THIS(item).onStatus->value; INNER
                                #);
                              
                           #);
                         
                      #);
                    newseparator:
                    (* Open and append a separator to THIS(simplemenu) *)
                      (# s: ^separator
                      do &separator[]->s[]; s.open; s[]->append; 
                      #);
                    iOpen: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (# 
                           do
                              'Open file'
                                ->projectMenu.iLoad.theEventHandler.onSelect
                           #)
                      #);
                    iNew: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (#
                              fg: ^astInterface.fragmentGroup;
                              created: @createDialog
                                (#
                                   create:: 
                                     (# 
                                     do
                                        (browser[],grammar[],groupName[],
                                         formName[])->edenv.new->fg[];
                                        (if fg[] <> none then
                                            fg.fullName->addproject;
                                            (*(browser[],fg[],none )->updateBrowser*)
                                            
                                        if);
                                        
                                     #);
                                   cancel::  (#  do  #);
                                   
                                #)
                           do
                              created.open;
                              (none ,'Create new file','untitled','untitled')
                                ->created.popup
                           #)
                      #);
                    iNewBetaProgram: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextediting->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaProgram
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iNewBetaLibrary: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextEditing->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaLibrary
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iSave: @item
                      (#
                         onStatus::  (#  do allowCommand->value #);
                         onSelect:: 
                           (#  do (true,false,true)->browser.cge.save #)
                      #);
                    iSaveAs: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupEditorExists and not
                              browser.cge.isAfrejaEditor->value
                           #);
                         onSelect:: 
                           (# newName: ^text
                           do
                              browser.cge.saveAs->newName[];
                              (if newName[] <> none then
                                  browser.currentGroup.close;
                                  newName[]->browser.selectFragmentGroup;
                                  browser.cge.groupTouched
                              if)
                           #)
                      #);
                    iSaveAll: @item
                      (#
                         askSave:
                           (#
                              ge: ^editorenv.groupEditor;
                              help: ^text;
                              cancelled,done: @boolean
                           enter ge[]
                           do
                              'Save changes to '->help[];
                              ge.fg.name->help.append;
                              '?'->help.append;
                              (THIS(ymerBrowserWindow)[],'Save group',help[])
                                -> (*UI.*) promptForBoolean
                                  (#
                                     done: @boolean;
                                     ok:: 
                                       (# 
                                       do
                                          (if not done then
                                              hide;
                                              true->done;
                                              (true,false,true)->ge.save
                                          if)
                                       #);
                                     notOK:: 
                                       (# 
                                       do
                                          (if not done then
                                              hide;
                                              true->done;
                                              ge.fg[]->onGroupNotSaved
                                          if)
                                       #);
                                     cancel:: 
                                       (# 
                                       do
                                          (if not done then
                                              hide; true->done; true->cancelled
                                          if)
                                       #);
                                     
                                  #)
                           exit cancelled
                           #);
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (# 
                           do
                           (*edenv.saveAll; temporarily placed here to be able
                            to specify the fatherwindow for the dialog
                            in order to avoid "moving" dialogs *)
                              l: edenv.groupEditorList.scan
                                (# cancelled: @boolean
                                do
                                   (if (current.touched > 0) and not
                                   current.isAfrejaEditor then
                                       current[]->askSave->cancelled
                                    else
                                       doCommand
                                         (# 
                                         do
                                            (if current.needToUpdateTextFile and
                                            not current.isAFrejaEditor then
                                                current.fg[]
                                                  ->edenv.makeNewTextFile
                                            if)
                                         #)
                                   if);
                                   (if cancelled then leave l if)
                                #)
                           #)
                      #);
                    iSaveAbstract: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do browser.cge.saveabstract #)
                      #);
                    iRecover: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  browser.cge.autoSaveFileExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              (THIS(ymerBrowserWindow)[],'Recover group',
                               'This command will also save the recovered fragment group\nDo you want to recover?')
                                ->promptForBoolean
                                  (#
                                     ok:: 
                                       (# 
                                       do browser.cge[]->recoverGroup
                                       #);
                                     
                                  #)
                           #)
                      #);
                    iRevert: @item
                      (#
                         f: @file;
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                              (* '~'->((browser.cge.fg.diskFileName).copy).Append->f.name;
                               f.entry.exists->value*)
                                  browser.cge.touched > 0->value
                               else
                                  false->value
                              if);
                              (* temporarily disabled *)
                              false->value
                           #);
                         onSelect::  (#  do browser.cge[]->revertGroup #)
                      #);
                    iPrint: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do browser.cfe.theSifTexteditor.contents.print
                           #)
                      #);
                    iSetIsReadOnly: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupeditorExists and not checkOngoingTextediting
                                ->value;
                              (if groupeditorExists then
                                  browser.cge.isReadOnly->checked; 
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              (if browser.cge.isReadOnly then
                                  (if browser.cge.groupFileWriteable then
                                      false->browser.cge.setIsReadOnly;
                                      ''->writeInfoView
                                  if)
                               else
                                  true->browser.cge.setIsReadOnly;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    iSetGlobalWriteProtection: @item
                      (#
                         onStatus:: 
                           (#  do edenv.globalWriteProtection->checked;  #);
                         onSelect:: 
                           (# 
                           do
                              (if edenv.globalWriteProtection then
                                  false->edenv.setGlobalWriteProtection;
                                  ''->writeInfoView
                               else
                                  true->edenv.setGlobalWriteProtection;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    open::< 
                      (# 
                      do
                         'New...'->iNew.new;
                         (if isBETAsif then
                             'New BETA Program...'->iNewBetaProgram.new;
                             'New BETA Library...'->iNewBetaLibrary.new
                         if);
                         'Open...'->iOpen.new;
                         iClose.open;
                         iClose[]->append;
                         newSeparator;
                         'Save'->iSave.new;
                         'Save As...'->iSaveAs.new;
                         'Save All...'->iSaveAll.new;
                         'Save Abstract...'->iSaveAbstract.new;
                         newSeparator;
                         'Recover...'->iRecover.new;
                         'Revert'->iRevert.new;
                         newSeparator;
                         'Print...'->iPrint.new;
                         newSeparator;
                         'Group File Write Protection'->iSetIsReadOnly.new;
                         'Global Write Protection'
                           ->iSetGlobalWriteProtection.new;
                         newSeparator;
                         INNER ;
                         
                      #)
                 #);
               GroupFile: @groupFileMenuType;
               groupFileMenuType:< simplemenu
                 (#
                    iNew: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (#
                              fg: ^astInterface.fragmentGroup;
                              created: @createDialog
                                (#
                                   create:: 
                                     (# 
                                     do
                                        (browser[],grammar[],groupName[],
                                         formName[])->edenv.new->fg[];
                                        (if fg[] <> none then
                                            fg.fullName->addproject;
                                            (*(browser[],fg[],none )->updateBrowser*)
                                            
                                        if);
                                        
                                     #);
                                   cancel::  (#  do  #);
                                   
                                #)
                           do
                              created.open;
                              (none ,'Create new file','untitled','untitled')
                                ->created.popup
                           #)
                      #);
                    iNewBetaProgram: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextediting->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaProgram
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iNewBetaLibrary: @item
                      (#
                         onStatus:: 
                           (#  do not checkOngoingTextEditing->value #);
                         onSelect:: 
                           (#
                              ff: ^astInterface.fragmentForm;
                              fg: ^astInterface.fragmentGroup
                           do
                              (browser[],none ,false)->edenv.newBetaLibrary
                                ->fg[];
                              (if fg[] <> none then
                                  fg.fullName->addproject
                              if)
                           #)
                      #);
                    iClose: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupEditorExists and not checkOngoingTextediting
                              and not browser.cge.isAfrejaEditor->value;
                              
                           #);
                         onSelect:: 
                           (# g: ^astInterface.fragmentGroup
                           do
                              browser.cge.fg[]->g[];
                              (*g.close;
                               'NB! g.close'->putLine;*)
                              browser.currentGroup.close
                           #)
                      #);
                    iSave: @item
                      (#
                         onStatus::  (#  do allowCommand->value #);
                         onSelect:: 
                           (#  do (true,false,true)->browser.cge.save #)
                      #);
                    iSaveAs: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupEditorExists and not
                              browser.cge.isAfrejaEditor->value
                           #);
                         onSelect:: 
                           (# newName: ^text
                           do
                              browser.cge.saveAs->newName[];
                              (if newName[] <> none then
                                  browser.currentGroup.close;
                                  newName[]->browser.selectFragmentGroup
                              if)
                           #)
                      #);
                    iSaveAll: @item
                      (#
                         askSave:
                           (#
                              ge: ^editorenv.groupEditor;
                              help: ^text;
                              cancelled,done: @boolean
                           enter ge[]
                           do
                              'Save changes to '->help[];
                              ge.fg.name->help.append;
                              '?'->help.append;
                              (THIS(ymerBrowserWindow)[],'Save group',help[])
                                -> (*UI.*) promptForBoolean
                                  (#
                                     done: @boolean;
                                     ok:: 
                                       (# 
                                       do
                                          (if not done then
                                              hide;
                                              true->done;
                                              (true,false,true)->ge.save
                                          if)
                                       #);
                                     notOK:: 
                                       (# 
                                       do
                                          (if not done then
                                              hide;
                                              true->done;
                                              ge.fg[]->onGroupNotSaved
                                          if)
                                       #);
                                     cancel:: 
                                       (# 
                                       do
                                          (if not done then
                                              hide; true->done; true->cancelled
                                          if)
                                       #);
                                     
                                  #)
                           exit cancelled
                           #);
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (# 
                           do
                           (*edenv.saveAll; temporarily placed here to be able
                            to specify the fatherwindow for the dialog
                            in order to avoid "moving" dialogs *)
                              l: edenv.groupEditorList.scan
                                (# cancelled: @boolean
                                do
                                   (if (current.touched > 0) and not
                                   current.isAfrejaEditor then
                                       current[]->askSave->cancelled
                                    else
                                       doCommand
                                         (# 
                                         do
                                            (if current.needToUpdateTextFile and
                                            not current.isAFrejaEditor then
                                                current.fg[]
                                                  ->edenv.makeNewTextFile
                                            if)
                                         #)
                                   if);
                                   (if cancelled then leave l if)
                                #)
                           #)
                      #);
                    iSaveAbstract: @item
                      (#
                         onStatus::  (#  do groupEditorExists->value #);
                         onSelect::  (#  do browser.cge.saveabstract #)
                      #);
                    iRecover: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                                  browser.cge.autoSaveFileExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              (THIS(ymerBrowserWindow)[],'Recover group',
                               'This command will also save the recovered fragment group\nDo you want to recover?')
                                ->promptForBoolean
                                  (#
                                     ok:: 
                                       (# 
                                       do browser.cge[]->recoverGroup
                                       #);
                                     
                                  #)
                           #)
                      #);
                    iRevert: @item
                      (#
                         f: @file;
                         onStatus:: 
                           (# 
                           do
                              (if allowCommand then
                              (* '~'->((browser.cge.fg.diskFileName).copy).Append->f.name;
                               f.entry.exists->value*)
                                  browser.cge.touched > 0->value
                               else
                                  false->value
                              if);
                              (* temporarily disabled *)
                              false->value
                           #);
                         onSelect::  (#  do browser.cge[]->revertGroup #)
                      #);
                    iSetIsReadOnly: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              groupeditorExists and not checkOngoingTextediting
                                ->value;
                              (if groupeditorExists then
                                  browser.cge.isReadOnly->checked; 
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              (if browser.cge.isReadOnly then
                                  (if browser.cge.groupFileWriteable then
                                      false->browser.cge.setIsReadOnly;
                                      ''->writeInfoView
                                  if)
                               else
                                  true->browser.cge.setIsReadOnly;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    iSetGlobalWriteProtection: @item
                      (#
                         onStatus:: 
                           (#  do edenv.globalWriteProtection->checked;  #);
                         onSelect:: 
                           (# 
                           do
                              (if edenv.globalWriteProtection then
                                  false->edenv.setGlobalWriteProtection;
                                  ''->writeInfoView
                               else
                                  true->edenv.setGlobalWriteProtection;
                                  'Write protected'->writeInfoView
                              if)
                           #)
                      #);
                    open::<  (#  do  #)
                 #);
               GroupEdit: @simplemenu
                 (#
                    iUndo: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowPerform then
                                  browser.cge.askUndoPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect::  (#  do browser.cge.undo;  #)
                      #);
                    iCut: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# brotherff: ^astInterface.fragmentForm
                           do
                              browser.cfe.frag[]->browser.cge.cutForm
                                ->brotherff[];
                              browser.cfe.frag[]->browser.currentGroup.close;
                              (if brotherff[] <> none then
                                  ((brotherff.father).fullName,brotherff.name)
                                    ->browser.selectFragmentForm
                              if);
                              
                           #)
                      #);
                    iCopy: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do browser.cfe.frag[]->browser.cge.copyForm
                           #)
                      #);
                    iPaste: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  (if formEditorExists then
                                      browser.cfe.frag[]
                                        ->browser.cge.askPastePermission->value
                                   else
                                      false->value
                                  if)
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              browser.cfe.frag[]->browser.cge.pasteForm
                                ->findForm;
                              
                           #)
                      #);
                    iAppend: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowPerform then
                                  browser.cge.askPastePermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do none ->browser.cge.pasteForm->ff[]->findForm; 
                           #)
                      #);
                    iInsertDoPartFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],31)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iInsertDescriptorFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],2)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iInsertAttributesFragmentForm: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  browser.cge.askInsertPermission->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# ff: ^astInterface.fragmentForm
                           do
                              (if browser.cfe[] <> none then
                                  browser.cfe.frag[]->ff[]
                              if);
                              (ff[],3)->browser.cge.insertForm->ff[];
                              (if ff[] <> none then ff[]->findForm if);
                              
                           #)
                      #);
                    iEditProperties: @item
                      (#
                         onStatus::  (#  do allowPerform->value #);
                         onSelect::  (#  do browser.cge.editProperties;  #)
                      #);
                    iEditFormName: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (if allowOnForm then
                                  formEditorExists->value
                               else
                                  false->value
                              if)
                           #);
                         onSelect:: 
                           (# 
                           do
                              browser.cfe.frag[]->browser.cge.editFormName;
                              (ymerBrowser[],browser.cge.fg[],
                               browser.cfe.frag[])->updateBrowser
                           #)
                      #);
                    iSearch: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (edenv[] <> none ) and not edenv.textediting
                                ->value
                           #);
                         onSelect::  (#  do callSearch #)
                      #);
                    iReplace: @item
                      (#
                         onStatus:: 
                           (# 
                           do
                              (edenv[] <> none ) and not edenv.textediting
                                ->value
                           #);
                         onSelect::  (#  do callReplace #)
                      #);
                    open::< 
                      (# 
                      do
                         'Undo Fragment Form Editing'->iUndo.new;
                         newSeparator;
                         'Cut Fragment Form'->iCut.new;
                         'Copy Fragment Form'->iCopy.new;
                         'Paste Fragment Form Before'->iPaste.new;
                         'Paste Fragment Form At End'->iAppend.new;
                         newSeparator;
                         'Insert DoPart Fragment Form...'
                           ->iInsertDoPartFragmentForm.new;
                         'Insert Descriptor Fragment Form...'
                           ->iInsertDescriptorFragmentForm.new;
                         'Insert Attributes Fragment Form...'
                           ->iInsertAttributesFragmentForm.new;
                         newSeparator;
                         ('Edit ORIGIN, INCLUDE etc ...','m')
                           ->iEditProperties.newKey;
                         'Edit Fragment Form Name...'->iEditFormName.new;
                         newSeparator;
                         ('Search...','s')->iSearch.newKey;
                         ('Replace...','r')->iReplace.newKey
                      #)
                 #);
               GroupPerform: @groupPerformMenuType;
               groupPerformMenuType:< simplemenu
                 (#
                    isProgram: booleanValue
                      (# fg: ^astInterface.fragmentGroup
                      enter fg[]
                      <<SLOT performMenuIsProgram:DoPart>>
                      #);
                    jobFileTimer: @timer
                      (#
                         action:: 
                           (# 
                           <<SLOT performMenuAction:DoPart>>
                           #);
                         
                      #);
                    iCheck: @item
                      (#
                         newName: ^text;
                         onStatus:: 
                           (#  <<SLOT performMenuCheckOnStatus:DoPart>> #);
                         onSelect:: 
                           (# 
                           <<SLOT performMenuCheckOnSelect:DoPart>>
                           #)
                      #);
                    iReCheck: @item
                      (#
                         newName: ^text;
                         onStatus:: 
                           (#  <<SLOT performMenuReCheckOnStatus:DoPart>> #);
                         onSelect:: 
                           (#  <<SLOT performMenuReCheckOnSelect:DoPart>> #)
                      #);
                    iCompile: @item
                      (#
                         newName:
                           ^text;
                         onStatus:: 
                           (#  <<SLOT performMenuCompileOnStatus:DoPart>> #);
                         onSelect:: 
                           (# newName: ^text
                           <<SLOT performMenuCompileOnSelect:DoPart>>
                           #)
                      #);
                    iRecompile: @item
                      (#
                         newName:
                           ^text;
                         onStatus:: 
                           (# 
                           <<SLOT performMenuRecompileOnStatus:DoPart>>
                           #);
                         onSelect:: 
                           (# 
                           <<SLOT performMenuRecompileOnSelect:DoPart>>
                           #)
                      #);
                    executableExists: booleanValue
                      (#
                         fg: ^astInterface.fragmentGroup;
                         executable: ^text;
                         f: @file
                      enter fg[]
                      <<SLOT performMenuExecutableExists:DoPart>>
                      #);
                    execute:
                     executableExists
                      (# msg: ^text <<SLOT performMenuExecute:DoPart>> #);
                    isRunning:
                     executableExists
                      (#  <<SLOT performMenuIsRunning:DoPart>> #);
                    kill: isRunning
                      (# 
                      <<SLOT performMenuKill:DoPart>>
                      #);
                    debuggerExists: booleanValue
                      (# betalib,debugger: ^text; f: @file
                      <<SLOT performMenuDebuggerExists:DoPart>>
                      #);
                    debug:
                    (* debuggerExists  *)
                      (# msg: ^text; fg: ^astInterface.fragmentGroup
                      enter fg[]
                      do
                         fg[]
                           ->executableExists
                             (# 
                             do
                                'Starting debugger on '->msg[];
                                executable[]->msg.append;
                                msg[]->writeInfoView;
                                ('' (*debugger[]*) ,executable[])->debugProgram;
                                ' done'->msg.append;
                                msg[]->writeInfoView
                             #)
                      #);
                    iExecute: @item
                      (#
                         newName: ^text;
                         onStatus:: 
                           (#  <<SLOT performMenuExecuteOnStatus:DoPart>> #);
                         onSelect:: 
                           (#  <<SLOT performMenuExecuteOnSelect:DoPart>> #)
                      #);
                    iReExecute: @item
                      (#
                         newName:
                           ^text;
                         onStatus:: 
                           (# 
                           <<SLOT performMenuReExecuteOnStatus:DoPart>>
                           #);
                         onSelect:: 
                           (# msg: ^text
                           <<SLOT performMenuReExecuteOnSelect:DoPart>>
                           #)
                      #);
                    iCompileAndExecute: @item
                      (#
                         newName: ^text;
                         onStatus:: 
                           (# 
                           <<SLOT performMenuCompileAndExecuteOnStatus:DoPart>>
                           #);
                         onSelect:: 
                           (# newName: ^text
                           <<SLOT performMenuCompileAndExecuteOnSelect:DoPart>>
                           #)
                      #);
                    iRecompileAndExecute: @item
                      (#
                         newName: ^text;
                         onStatus:: 
                           (# 
                           <<SLOT performMenuRecompileAndExecuteOnStatus:DoPart>>
                           #);
                         onSelect:: 
                           (# 
                           <<SLOT performMenuRecompileAndExecuteOnSelect:DoPart>>
                           #)
                      #);
                    iKill: @item
                      (#
                         newName: ^text;
                         onStatus:: 
                           (#  <<SLOT performMenuKillOnStatus:DoPart>> #);
                         onSelect:: 
                           (# 
                           <<SLOT performMenuKillOnSelect:DoPart>>
                           #)
                      #);
                    iSemanticErrors: @item
                      (#
                         onStatus:: 
                           (# 
                           <<SLOT performMenuSemanticErrorsOnStatus:DoPart>>
                           #);
                         onSelect:: 
                           (# 
                           <<SLOT performMenuSemanticErrorsOnSelect:DoPart>>
                           #)
                      #);
                    iDebug:
                      @item
                      (#
                         newName: ^text;
                         onStatus:: 
                           (#  <<SLOT performMenuDebugOnStatus:DoPart>> #);
                         onSelect:: 
                           (# 
                           <<SLOT performMenuDebugOnSelect:DoPart>>
                           #)
                      #);
                    open::< 
                      (# 
                      do <<SLOT compilerPerformMenu:Descriptor>>; INNER
                      #)
                 #);
               Test: @TestMenuType;
               testMenuType:< simplemenu
                 (#
                    iuseExternalTextEditor: @item
                      (#
                         onStatus:: 
                           (# 
                           do true->value; edenv.useExternalTextEditor->checked
                           #);
                         onSelect:: 
                           (# 
                           do
                              not edenv.useExternalTextEditor
                                ->edenv.useExternalTextEditor
                           #)
                      #);
                    iEventTrace: @item
                      (#
                         onStatus:: 
                           (#  do true->value; edenv.trace[7]->checked #);
                         onSelect:: 
                           (# 
                           do
                              not edenv.switch[7]->edenv.switch[7];
                              not edenv.trace[7]->edenv.trace[7]
                           #)
                      #);
                    iTraceStart: @item
                      (#
                         onStatus:: 
                           (#  do true->value; edenv.trace[2]->checked #);
                         onSelect:: 
                           (#  do not edenv.trace[2]->edenv.trace[2] #)
                      #);
                    iUndoTrace: @item
                      (#
                         onStatus:: 
                           (#  do true->value; edenv.trace[13]->checked #);
                         onSelect:: 
                           (#  do not edenv.trace[13]->edenv.trace[13] #)
                      #);
                    iTraceGetNextComment: @item
                      (#
                         isSet: @boolean;
                         onStatus::  (#  do true->value; isSet->checked #);
                         onSelect:: 
                           (# 
                           do
                              (mps.ast.trace.getNextComment,true)
                                ->mps.ast.trace.set;
                              true->isSet
                           #)
                      #);
                    iTraceSearch: @item
                      (#
                         onStatus:: 
                           (#  do true->value; edenv.trace[12]->checked #);
                         onSelect:: 
                           (#  do not edenv.trace[12]->edenv.trace[12] #)
                      #);
                    iReplace: @item
                      (#
                         onStatus::  (#  do true->value #);
                         onSelect:: 
                           (# 
                           do
                              (browser.cge[],browser.cfe[],none ,none ,none )
                                ->edenv.replaceTextDialog
                                  (#
                                     found:: 
                                       (# 
                                       do
                                          (if edenv.trace[12] then
                                              'found'->putLine
                                          if);
                                          foundNode[]->findandselect
                                       #);
                                     notfound:: 
                                       (# help: ^text
                                       do
                                          ascii.bel->put;
                                          searchText[]->help[];
                                          ' not found'->help.append;
                                          (if edenv.trace[12] then
                                              help[]->putLine
                                          if);
                                          help[]->writeInfoView
                                       #)
                                  #)
                           #)
                      #);
                    iTest: @item
                      (#
                         onStatus:: 
                           (#  do true (*groupEditorExists*) ->value #);
                         onSelect:: 
                           (# 
                           do
                              (if browser.cge[] <> none then
                                  (browser.cge.fg[],'INCLUDE','foolib')
                                    ->externalInterface.addprop
                              if)
                           #)
                      #);
                    iTest2: @item
                      (#
                         onStatus:: 
                           (#  do true (*groupEditorExists*) ->value #);
                         onSelect:: 
                           (# 
                           do
                              (if browser.cge[] <> none then
                                  (browser.cge.fg[],'INCLUDE','foolib')
                                    ->externalInterface.deleteprop
                              if)
                           #)
                      #);
                    iFreja: @item
                      (#
                         onStatus:: 
                           (#  do true (*groupEditorExists*) ->value #);
                         onSelect:: 
                           (# 
                           do
                              (if browser.cge[] <> none then
                                  (if browser.cfe[] <> none then
                                      true->browser.cfe.isAfrejaEditor;
                                      &frejaExternalInterface
                                      (*(# askBeforeTextediting:: trueObject
                                       #)*) []->browser.edenv.frejaInterface[]
                                  if)
                              if)
                           #)
                      #);
                    open::< 
                      (# 
                      do
                         'Use emacs'->iUseExternalTextEditor.new;
                         newSeparator;
                         'Trace start'->iTraceStart.new;
                         'Event trace'->iEventTrace.new;
                         'Undotrace'->iUndoTrace.new;
                         'Trace getnextComments'->iTraceGetNextComment.new;
                         newSeparator;
                         ('Replace','5')->iReplace.newKey;
                         'Trace search'->iTraceSearch.new;
                         newSeparator;
                         'Add prop'->iTest.new;
                         'Delete prop'->iTest2.new;
                         newSeparator;
                         'Freja mode'->iFreja.new;
                         INNER
                      #);
                    
                 #);
               open::< 
                 (# 
                 do
                    fileMenu.setup;
                    (*'Group'->GroupFile.new;*)
                    useExternalTexteditor->Edit.useExternalTextEditor;
                    Edit.new;
                    Edit[]->append;
                    isBETAsif->View.BETAsif;
                    View.new;
                    View[]->append;
                    (if isBETAsif then
                        SLOTs.new; SLOTs[]->append; 'Fragments'->GroupEdit.new
                    if);
                    <<SLOT compilerMenuOpen:Descriptor>>;
                    INNER ;
                    projectMenu.setup;
                    historyMenu.setup;
                    windowsMenu.setup
                 #)
            #);
          open::< 
            (# 
            do initCompiler; INNER
            #)
       #);
     inithere: @boolean;
     isBETAsif:< booleanValue;
     useExternalTextEditor:< booleanValue;
     executeProgram:<
       (# name: ^text
       enter name[]
       do INNER
       #);
     isProgramRunning:< booleanValue
       (# name: ^text
       enter name[]
       do INNER
       #);
     killProgram:<
       (# name: ^text
       enter name[]
       do INNER
       #);
     askKillPrograms:< object;
     debugProgram:<
       (# debugger,program: ^text
       enter (debugger[],program[])
       do INNER
       #);
     editorVersion:
       (#  exit '5.2(0)' #);
     compilerVersion:<
       (# version: ^text
       do
          <<SLOT sifCompilerVersion:Descriptor>>;
          INNER
       exit version[]
       #);
     betaenvVersion:<
       (# t: ^text do '~beta/basiclib/betaenv'->t[]; INNER exit t[] #);
     frejaInclude:<
       (# t: ^text do '~beta/freja/associations'->t[]; INNER exit t[] #);
     onAboutToCloseGroup:<
       (#
          fg: ^astInterface.fragmentGroup;
          OKtoClose,askSave: @boolean
          (* if OKtoClose the groupEditor is closed.
           * if askSave the user is prompted whether to save the group
           * else it is just saved (if necessary)
           *)
       enter fg[]
       do true->OKtoClose->askSave; INNER
       exit (OKtoClose,askSave)
       #);
     onGroupSaved:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupNotSaved:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     onGroupClosed:<
       (# fg: ^astInterface.fragmentGroup enter fg[] do INNER #);
     askEditUserinterface:< booleanValue
       (# node: ^astInterface.ast enter node[] do true->value; INNER #);
     editUserinterface:<
       (# node: ^astInterface.ast enter node[] do INNER #);
     init::< 
       (# 
       do
          ymerBrowser.open;
          ymerBrowser[]->ymerBrowser.ymerCallBack.theBrowser[];
          ymerBrowser.ymerCallback[]->ymerBrowser.edenv.ymerCallback[];
          INNER
       exit (mps.ast[],mps.betaCFL[])
       #);
     doTest,useExtTextEd,hiddenSif: @boolean;
     analyzeArg:
       (#
          arg,fgname,ffname: ^text;
          dashPos,dashPos2,slashPos: @integer;
          isAlphaNumeric: booleanValue
            (# t,s: ^text
            enter t[]
            do
               true->value;
               t.copy->s[];
               s.makeLC;
               loop: s.scanAll
                 (# 
                 do
                    (if not
                    (((ch >= 'a') and (ch <= 'z')) or
                     ((ch >= '0') and (ch <= '9'))) then
                        false->value; leave loop
                    if)
                 #)
            #)
       enter arg[]
       do
          0->dashPos->slashPos;
          ''->fgName[]->ffName[];
          '-'->arg.findAll (#  do inx->dashPos #);
          (if dashPos > 0 then
              (1,dashPos-1)->arg.sub->fgname[];
              '-'->fgname.findAll (#  do inx->dashPos2 #);
              (if dashPos2 > 0 then
                  ''->fgname[]
               else
                  (dashPos+1,arg.length)->arg.sub->ffName[];
                  (if not (ffname[]->isAlphaNumeric) then ''->ffname[] if)
              if)
           else
              arg[]->fgname[]
          if)
       exit (fgname[],ffname[])
       #);
     
  do init; INNER fragmentBrowser
  #)  

