ORIGIN '~beta/basiclib/betaenv';
INCLUDE '~beta/containers/dictionary';
INCLUDE '~beta/containers/list';



-- TextLib: Atttributes --

split:
  (# ch: @char;
     parts: ^TextList;
  enter ch
  do &TextList[] -> parts[];
     (# previousIndex: @integer;
     do 0 -> previousIndex;
        reset;
        ch -> find
          (#
          do (if (previousIndex + 1) = inx then
                &Text[] -> parts.append;
                inx -> previousIndex;
             else
                (previousIndex + 1, inx - 1) -> sub -> parts.append;
                inx -> previousIndex;
             if);
          #);
        (if previousIndex = length then
          &Text[] -> parts.append;
        else
          (previousIndex + 1, length) -> sub -> parts.append;
        if);
     #);
     INNER;
  exit parts[]
  #);


-- LIB: Attributes --


TextList: List
  (# element:: Text;
     get:
       (# index: @integer;
          elm: ^element;
       enter index
       do loop: scan
            (# i: @integer;
            do i + 1 -> i;
               (if i = index then
                  current[] -> elm[];
                  leave loop;
               if);
            #);
       exit elm[]
       #);
  #);
TextDictionary: Dictionary
  (# key:: Text;
     hashFunction::
       (# 
       do k.scanAll
          (#
          do 131 * value + ch -> value;
          #);
          (if value < 0 then
              -value -> value;
          if);
       #);
     keyEqual::
       (#
       do left[] -> right.equal -> value;
       #);
  #);
