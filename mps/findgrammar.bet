ORIGIN 'astlevel';
LIB_DEF 'mpsfindgram' '../lib';
BODY 'private/findGrammarBody'
(*
 * COPYRIGHT
 *       Copyright (C) Aarhus University
 *       All rights reserved.
 *)
--- astInterfaceLib: attributes ---
(* idx: 2 *)
findGrammar: grammarFinder
  (# registerGrammars::<
       (# do <<SLOT findGrammarRegisterGrammarsBody:descriptor>>; INNER #);
     registeredGrammars::<
       (# <<SLOT findGrammarRegisteredGrammarsBody:dopart>> #);
     grammarsPATH:<
       (* the name of the file in which the valid grammars are specified.
	* Used by registerGrammars 
	*)
       (# grammars: ^text
       <<SLOT findGrammarGrammarsPATHBody:dopart>>
       exit grammars[]
       #);
     metaGrammarFile:<
       (* the name of the file in which the  meta-grammar is specified *)
       (# metaGrammar: ^text
       <<SLOT findGrammarMetaGrammarFileBody:dopart>>
       exit metaGrammar[]
       #);
     loadingError:< exception
       (#
	  nameError:< error
	    (#
	    do 'Grammar extension or path found in wrong position'
		 ->msg.puttext;
	       INNER
	    #);
	  extensionPathError:< exception
	    (#
	    do 'Grammar extension or path found in wrong position'->msg.puttext;
	       INNER
	    #);
	  constError:< exception
	    (#
	    do 'Const values does not make sense in grammar specifications'
		 ->msg.puttext; INNER
	    #);
	  formatError:< exception
	    (#
	    do 'Something wrong with the number of values in grammar specification'
		 ->msg.puttext; INNER
	    #);
	  MPSerror:< exception
	    (# t: ^text enter t[] do t[]->msg.puttext; INNER #);
       #);
     private: @ <<SLOT findGrammarPrivate: descriptor>>;
  do <<SLOT findGrammarDoBody: descriptor>>;
  #);
