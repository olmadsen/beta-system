ORIGIN '../property';
ON 19
[[
(*
 * COPYRIGHT
 *       Copyright (C) Mjolner Informatics, 1986-93
 *       All rights reserved.
 *)
--- propertyPropertyListPropElementRepresentationPrivate: attributes ---
repSave: (* Private *) 
  (# f: ^repetitionStream 
  enter f[]
  do prop[] -> f.putText;
     par.size -> f.putInt;
     par.scan(# do f[] -> current.repSave #);
  #);
repRestore: (* Private *) 
  (# ch: @char;
     s: ^repetitionStream; t: ^text; no: @integer; e: ^par.element
  enter s[]
  do s.getText -> t[]; 
     t -> prop; 
     s.getInt -> no;
     (for no repeat &par.element[] -> e[]; s[] -> e.repRestore; e[] -> par.append for)
  #);

--- propertyPropertyListPropListRepresentationPrivate: attributes ---
repSave: (* Private *) 
  (# s: ^repetitionStream
  enter s[]
  do size -> s.putInt;
     scan(# do s[] -> current.repSave #)
  #);
repRestore: (* Private *) 
  (# s: ^repetitionStream; no: @integer; e: ^element
  enter s[]
  do s.getInt -> no;
     (for no repeat &element[] -> e[]; s[] -> e.repRestore; e[] -> append for)
  #);

--- propertyPropertyListRepresentationPrivate: attributes ---
repSave: (* Private *) (# enter propList.repsave #);
repRestore: (* Private *) (# enter propList.represtore #);

--- propertyPropertyListAddPropBody: dopart ---
do (if (propName[] -> findProp -> newPropElement[])
    //none then
       &propElement[] -> newPropElement[];
       newPropElement.par.init;
    else 
       newPropElement[] -> propList.at -> propList.delete;
       (if ifPropExist
        //true then newPropElement.par.clear
       if)
   if);
   propName -> newPropElement.prop;
   INNER addProp;
   newPropElement[] -> propList.append

--- propertyPropertyListFindPropBody: dopart ---
do theScan: propList.scan
     (# do
        (if (current.prop[] -> name.equalNCS)
         //true then current[] -> l[]; leave theScan
        if)
     #)

--- propertyPropertyListDeletePropBody: descriptor ---
(# l: ^propList.element;
do prop[] -> findProp ->l[];
   (if (l[]=none)//false then l[] -> propList.at -> propList.delete if)
#)

--- propertyPropertyListScanPropDoPropGetNameBody: descriptor ---
(# position: ^currentParList.theCellType
do (if currentParList.size
    //1 then
       (# par: ^parElement;
          n: ^nameElement
       do currentParList.head->position[];
          position.elm[]->par[];
          (if par.type
           //nameType then par.val[] -> n[]; n.n.copy -> name[]
           else notAName
          if)
       #);
    else notAName
   if);
#)

--- propertyPropertyListScanPropDoPropGetConstBody: descriptor ---
(# position: ^currentParList.theCellType
do (if currentParList.size
    //1 then
       (# par: ^parElement;
          c: ^constElement
       do currentParList.head->position[];
          position.elm[]->par[];
          (if par.type
           //constType then par.val[] -> c[]; c.c -> const
           else notAConst
          if)
       #);
    else notAConst
   if);
#)

--- propertyPropertyListScanPropDoPropGetStringBody: descriptor ---
(# position: ^currentParList.theCellType
do (if currentParList.size
    //1 then
       (# par: ^parElement;
          s: ^stringElement
       do currentParList.head->position[];
          position.elm[]->par[];
          (if par.type
           //stringType then par.val[] -> s[]; s.s.copy -> string[]
           else notAString
          if)
       #);
    else notAString
   if);
#)

--- propertyPropertyListScanPropDoPropScanParametersBody: dopart ---
do currentParList.scan
   (# thisParElm: ^parElement; 
      (* bug in virtuals???. has to requalify *)
   do current[] -> thisParElm[];
      (if thisParElm.type
       //constType then 
          (# c: ^constElement do thisParElm.val[] -> c[]; c.c -> doConst #)
       //stringType then 
          (# s: ^stringElement do thisParElm.val[] -> s[]; s.s[] -> doString#)
       //nameType then 
          (# n: ^nameElement do thisParElm.val[] -> n[]; n.n[] -> doName #)
      if)
   #);

--- propertyPropertyListScanPropBody: dopart ---
do propList.scan
   (# do
      current.par[] -> currentParList[];
      current.prop[] -> doProp;
   #);

--- propertyParElementLocalsPrivate: attributes ---
repSave: (* Private *)
  (# s: ^repetitionStream
  enter s[]
  do type ->s.putInt; s[] -> val.repsave
  #);
repRestore: (* Private *)
  (# s: ^repetitionStream;
     t: ^text;
  enter s[]
  do s.getInt -> type; 
     (if type
      //constType then
         (# c: ^constElement 
         do &constElement[] -> c[] -> val[]; s.getInt -> c.c 
         #);
      //stringType then
         (# str: ^stringElement;
            length: @integer; 
         do &stringElement[] -> str[] -> val[]; 
            s.getText -> t[]; t -> str.s
         #);
      //nameType then
         (# n: ^nameElement
         do &nameElement[] -> n[] -> val[]; s.getText -> t[]; t -> n.n
         #);
     if);
  #);
---]]
