ORIGIN '../notifications';
INCLUDE 'astPrivate';
LIB_ITEM 'mpsastlevel';
-- handleGetHandle: DoPart --
do &handleType[]->h[]; h[]->private.handles.append  

-- handleIgnoreHandle: DoPart --
do h[]->private.handles.at->private.handles.delete  

-- handleActivateHandle: DoPart --
do (if not (h[]->private.handles.has) then h[]->private.handles.append if)  

-- handleSignal: DoPart --
do
   start;
   private.handles.scan (* abstract superpattern *)
     (#
        where:: 
          (# 
          do
             THIS(signal).where and not ((fg[],ff[],node[])->current.ignore)
               ->value
          #);
        
     do current[]->THIS(signal).current[]; INNER signal
     #)  

-- fgPrivate: Descriptor --
(#
   groupSubject: @subject (# observerType:: fragmentGroupObserver #);
   no: @integer;
   wasRecovered:
   (* must be moved to the interface level and set by restoreBackup *)
     @boolean;
   
#)  

-- notificationsFragmentGroupAttachObserver: DoPart --
do
   theObserver[]->private.groupSubject.attach;
   THIS(FragmentGroup)[]->theObserver.group[];
   private.no+1->private.no->theObserver.no;
     

-- notificationsFragmentGroupDetachObserver: DoPart --
do theObserver[]->private.groupSubject.detach  

-- fragmentGroupSubjectNotify: DoPart --
do
   before;
   private.groupSubject.notify
     (#  do current[]->THIS(notify).current[]; INNER notify #);
   after  

-- fragmentGroupSubjectNotifyAs: DoPart --
do
   before;
   private.groupSubject.observers.scan
     (# 
     do
        (if current## <= type## then
            current[]->THIS(notifyAs).current[]; INNER notifyAs; 
        if);
        
     #);
   after;
     

-- ffPrivate: Descriptor --
(#
   fragSubject: @subject (# observerType:: fragmentFormObserver #);
   no: @integer;
   locked: (* used for example when entering textediting *) @boolean;    
   readOnly: @boolean;    
   touched: @integer;    
#)  

-- notificationsFragmentFormAttachObserver: DoPart --
do
   theObserver[]->private.fragSubject.attach;
   THIS(FragmentForm)[]->theObserver.frag[];
   private.no+1->private.no->theObserver.no;
     

-- notificationsFragmentFormDetachObserver: DoPart --
do theObserver[]->private.fragSubject.detach  

-- fragmentFormSubjectNotify: DoPart --
do
   before;
   private.fragSubject.notify
     (#  do current[]->THIS(notify).current[]; INNER notify #);
   after  

-- fragmentFormSubjectNotifyAs: DoPart --
do
   before;
   private.fragSubject.notify (* observers.scan *)
     (# 
     do
        (if current## <= type## then
            current[]->THIS(notifyAs).current[]; INNER notifyAs; 
        if);
        
     #);
   after;
     
-- fragmentFormLocked: DoPart --
do private.locked->value

-- fragmentFormLock: DoPart --
do true->private.locked 

-- fragmentFormUnLock: DoPart --
do false->private.locked 

-- fragmentFormReadOnly: DoPart --
do private.readOnly->value

-- fragmentFormSetReadOnly: DoPart --
do true->private.readOnly 

-- fragmentFormUnsetReadOnly: DoPart --
do false->private.readOnly

-- fragmentFormTouched: DoPart --
do (private.touched>0)->value

-- fragmentFormTouch: DoPart --
do private.touched+1->private.touched

-- fragmentFormDetouch: DoPart --
do private.touched-1->private.touched

