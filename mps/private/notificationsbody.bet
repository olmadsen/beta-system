ORIGIN '../notifications';
LIB_ITEM 'mps';
INCLUDE 'astPrivate';
-- handleGetHandle: DoPart --
do &handleType[]->h[]; h[]->private.handles.append  

-- handleIgnoreHandle: DoPart --
do h[]->private.handles.at->private.handles.delete  

-- handleActivateHandle: DoPart --
do (if not (h[]->private.handles.has) then h[]->private.handles.append if)  

-- handleSignal: DoPart --
do
   start;
   private.handles.scan (* abstract superpattern *)
     (#
        where:: 
          (# 
          do
             THIS(signal).where and not ((fg[],ff[],node[])->current.ignore)
               ->value
          #);
        
     do current[]->THIS(signal).current[]; INNER signal
     #)  

-- fgPrivate: Descriptor --
(#
   subjects: @containerList (# element:: fragmentGroupSubject #);
   no: @integer
#)  

-- notificationsFragmentGroupAttachObserver: DoPart --
do
     (# theSubject: ^subjectType
     do
        (if private.subjects.size = 0 then
            &subjectType[]->theSubject[]; theSubject[]->private.subjects.append
         else
            search: private.subjects.scan
              (# 
              do
                 (if subjectType## = current## then
                     current[]->theSubject[]; leave search
                 if)
              #);
            (if theSubject[] = none then
                &subjectType[]->theSubject[];
                theSubject[]->private.subjects.append
            if)
        if);
        &observerType[]->o[];
        theSubject[]->o.subject[];
        o[]->theSubject.observers.append;
        private.no+1->private.no->o.no;
        (if false then
            'attach fgSubject: '->putText;
            name->putText;
            ' '->put;
            o.no->putInt;
            newline;
            'observers: '->putText;
            theSubject.observers.scan
              (#  do current.no->putInt; ' '->put #);
            newLine
        if)
     #)  

-- notificationsFragmentGroupDetachObserver: DoPart --
do o[]->o.subject.observers.at->o.subject.observers.delete  

-- notificationsFragmentGroupReattachObserver: DoPart --
do
   (if not (o[]->o.subject.observers.has) then
       o[]->o.subject.observers.append
   if)  

-- fragmentGroupSubjectNotify: DoPart --
do
   before;
   (if false then
       'observers: '->putText;
       observers.scan
         (#  do current.no->putInt; ' '->put #);
       newLine
   if);
   observers.scan
     (#
        where:: 
          (#  do THIS(notify).where and not current.ignore->value #);
        
     do current[]->THIS(notify).current[]; INNER notify
     #);
   after  

-- ffPrivate: Descriptor --
(#
   subjects: @containerList (# element:: fragmentFormSubject #);
   no: @integer
#)  

-- notificationsFragmentFormAttachObserver: DoPart --
do
     (# theSubject: ^subjectType
     do
        (if private.subjects.size = 0 then
            &subjectType[]->theSubject[]; theSubject[]->private.subjects.append
         else
            search: private.subjects.scan
              (# 
              do
                 (if subjectType## = current## then
                     current[]->theSubject[]; leave search
                 if)
              #);
            (if theSubject[] = none then
                &subjectType[]->theSubject[];
                theSubject[]->private.subjects.append
            if)
        if);
        &observerType[]->o[];
        theSubject[]->o.subject[];
        o[]->theSubject.observers.append;
        private.no+1->private.no->o.no;
        (if false then
            'attach ffSubject: '->putText;
            (father).name->putText;
            '-'->put;
            name->putText;
            ' '->put;
            o.no->putInt;
            newline;
            'observers: '->putText;
            theSubject.observers.scan
              (#  do current.no->putInt; ' '->put #);
            newLine
        if)
     #)  

-- notificationsFragmentFormDetachObserver: DoPart --
do o[]->o.subject.observers.at->o.subject.observers.delete  

-- notificationsFragmentFormReattachObserver: DoPart --
do
   (if not (o[]->o.subject.observers.has) then
       o[]->o.subject.observers.append
   if)  

-- fragmentFormSubjectNotify: DoPart --
do
   before;
   (if false then
       'observers: '->putText;
       observers.scan
         (#  do current.no->putInt; ' '->put #);
       newLine
   if);
   observers.scan
     (#
        where:: 
          (#  do THIS(notify).where and not current.ignore->value #);
        
     do current[]->THIS(notify).current[]; INNER notify
     #);
   after  

