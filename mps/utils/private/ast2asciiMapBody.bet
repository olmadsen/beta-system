ORIGIN '../ast2asciiMap';
include '~beta/basiclib/numberio'
---ast2asciiMap_openRead:doPart---
do (*'\nOpenRead a2sHandle:' -> puttext;*)
   open:
     (# a2sFile: @a2sBinfile
          (# a2sError::
               (#
               do msg[] -> warning; leave open
               #)
          #);
        getB: @a2sFile.getBytes;
        firstPos: @integer;
        Swap:
          (#
          do (for i: top repeat
                  astIndex[i].%byteSwapLong;
                  start[i].%byteSwapLong;
                  end[i].%byteSwapLong;
             for)
          #);
        endianSwap: @boolean;
        errorMsg:
          (# msg: ^text; 
          enter msg[]
          do '\nhead[1]='-> msg.puttext;
             head[1] -> msg.putint;
             ', head[2]='-> msg.puttext;
             head[2] -> msg.putint;
             ', range='->msg.puttext;
             head.range -> msg.putint;
             ', head[3]='-> msg.puttext;
             head[3] -> msg.puthex
          exit msg[]
          #);
             
     do (if a2sFile.mkFileName then
            a2sFile.openRead; (* check existence and exceptions *)
            (if true then
                (head.range,0) -> a2sFile.getInt32Rep -> head[]
             else
                (@@head[1],head.range*4) -> getB;
            if);
            (if true 
             // isBigEndian then
                (if head[3] 
                 // littleEndianValue then 
                    true -> endianSwap
                 // bigEndianValue then 
                    (* ok *)                    
                 else
                    (failureTrace,'Illegal endian mark in a2s-file'->errorMsg) 
                      -> stop
                if)
             // isLittleEndian then
                (if head[3] 
                 // bigEndianValue then 
                    true -> endianSwap
                 // littleEndianValue then 
                    (* ok *)
                 else
                    (failureTrace,'Illegal endian mark in a2s-file'->errorMsg) 
                      -> stop
                if)
            if);
            (if endianSwap then
                head[1].%byteSwapLong;
                head[2].%byteSwapLong;
            if);
            
            (if head[1] <= 0 then 
                (failureTrace,'Negative range in a2s-file') -> stop
            if);
            
            (if head[2] <= 0 then 
                (failureTrace,'Negative top in a2s-file') -> stop
            if);
            
            (if head[1] <> head.range then
                '*** Invalid a2s-file' -> errorMSg -> warning
            if); 
            head[2] -> top;                   
            top -> astIndex.new;
            top -> start.new;
            top -> end.new;
            (if true then
                (top,0) -> a2sFile.getInt32Rep -> astIndex[];
                (top,0) -> a2sFile.getInt32Rep -> start[];
                (top,0) -> a2sFile.getInt32Rep -> end[];
             else
                (@@astIndex[1],top*4) -> getB;
                (@@start[1],top*4) -> getB;
                (@@end[1],top*4) -> getB;
            if);
            (if endianSwap then swap if);

            (*dump;*)
            1 -> top;
            L:
              (if top < astIndex.range then
                  top + 1 -> top -> firstPos;;
                  (if (formsTop + 1 -> formsTop) > forms.range then
                      forms.range -> forms.extend
                  if);
                  L2: (if astIndex[top+1->top] <> -1 then 
                             restart L2
                      if);
                  (firstPos,top-1) -> map -> forms[formsTop][];
                  (* astIndex[top] = -1 *)
                  restart L
              if);
            true -> openForRead;
            a2sFile.close;
         else
            '\nNo a2s-file: ' -> (a2sfile.name).prePend -> warning
        if)
     #)
   
---ast2asciiMap_close:doPart---
do doClose:
     (if openForWrite then
         (# a2sFile: @a2sBinfile
              (# a2sError::
                   (#do msg[] -> warning; leave doClose #)
              #);
            putB: @a2sFile.putBytes
         do newForm;
            head.range -> head[1];
            top -> head[2];
            (if isBigEndian then
                bigEndianValue -> head[3]
             else
                littleEndianValue -> head[3]
            if);
            (*dump;*)
            a2sFile.mkFileName;
            a2sFile.openWrite; (* check existence and exceptions *)
            
            (if true then
                (head[],head.range) -> a2sFile.putInt32Rep;
                (astIndex[],top) -> a2sFile.putInt32Rep;
                (start[],top) -> a2sFile.putInt32Rep;
                (end[],top) -> a2sFile.putInt32Rep;
             else
                (@@head[1],head.range*4) -> putB;
                (@@astIndex[1],top*4) -> putB;
                (@@start[1],top*4) -> putB;
                (@@end[1],top*4) -> putB;              
            if);
            a2sFile.close;
         #)
     if);
   false -> openForWrite -> openForRead

