ORIGIN '../getslotbinding'
---getSlotBinding_scan:doPart---
do head[]->R[];
   L:
     (if (R[] <> none ) then
         R.F[]->ThisGroup[]; 
         INNER ; 
         R.succ[]->R[]; 
         restart L
     if)
--getSlotBinding_domain:doPart--
do Insert:
     (# R: ^Elm
     do false->inserted;
        head[]->R[];
        L:
          (if (R[] <> none ) then
              (if R.F[] = F[] then leave insert if);
              R.succ[]->R[];
              restart L
          if);
        &Elm[]->R[];
        F[]->R.F[];
        head[]->R.succ[];
        R[]->Head[];
        True->inserted;
        trace.GetBindingMark
          ->tracer
        (#
        do
           'marked: '->screen.putText;
           f.fullname->screen.putText;

        #);

     #)
--getSlotBindingDoPart:dopart---
do (# Find:
        (# F: ^Fragment; G: ^FragmentGroup
        enter F[]
        do (if (F[] <> none ) then
               (if F.FragType = groupType then
                   (if (F[]->G[]->domain) then
                       G.scanIncludes
                       (# F1: ^Fragment; fn: ^text
                       do current.fullname->fn[];
                          (if (fn[]->top.groupTable.find->F1[])=NONE then
                              (fn[],screen[])->top.open->F1[]
                          if);
                          F1[] -> &Find
                       #);
                          (*(# do (name[],screen[])->top.open->&Find #);*)
                       G.origin->&Find
                   if)
               if)
           if)
        #);
      sl: ^slotDesc
   do trace.GetBinding
        ->tracer
      (#
      do
         'GetBinding of '->screen.putText;
         (*        ((index,frag[]),screen[],3,false)->prettyPrinter;*)
         ' for '->screen.putText;
         g.fullName->screen.putText;

      #);
      G[]->&Find;
      this(unExpanded).theSlot -> sl[];
      domain.Scan (* scan visible groups for forms *)
      (#
      do thisGroup.Scan
         (# F: ^FragmentForm; B: ^SlotDesc
         do
            (if current.FragType = FormType then
                current[]->f[];
                f.binding[]->b[];
                (if (b[] <> none ) then
                    (if (b[]->sl.equal) then current[]->&found if)
                if)
            if)
         #)
      #)

   #)
