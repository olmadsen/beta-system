ORIGIN '~beta/process/activate';
INCLUDE '~beta/mps/findgrammar';
INCLUDE '~beta/sysutils/pathhandler';
INCLUDE '~beta/basiclib/file';
--PROGRAM: descriptor--
systemenv
(# usage:
     (# 
     do 'Usage: dogram [-b] <grammar-name>' -> putline;
        stop;
     #);
   debug: (# exit true #);
   boot: @boolean;
   grammarname: ^text;
   machine_type: external (# T: [1]@ char  exit T #);
   objdir: @text;
   PH: @pathhandler;
   betalib: ^text;
   cmd, args, bobsfile, parserlst, output: ^text;
   execute:
     (# args: ^text;
        command: ^text;
        cmd: ^text;
        boot: @boolean;
     enter (command[], args[], boot)
     do betalib.copy -> cmd[];
        (if boot then
            'boot/' -> cmd.append;
        if);
        'bin/' -> cmd.append;
        objdir[] -> cmd.append;
        '/' -> cmd.append;
        command[] -> cmd.append;
        (if debug then
            'Executing ' -> puttext;
            cmd[] -> puttext;
            ' '->put;
            args[] -> putline;
        if);
        (if false then
            (* execReadtext fails with more than one argument *)
            (cmd[], args[]) -> execReadText -> puttext;
         else
            (# system: external
                 (# commandAndArgs: [0]@char;
                 enter commandAndArgs
                 #);
            do cmd.copy -> cmd[];
               ' ' -> cmd.append;
               args[] -> cmd.append;
               cmd -> system;
            #);
        if)
     #);
   entry: @diskentry;
do (if (NoOfArguments<2) or (NoOfArguments>3) then
       usage;
   if);
   (if '-b' -> (2->Arguments).equal then
       true -> boot;
       3 -> Arguments -> grammarname[];
    else
       2 -> Arguments -> grammarname[];
   if);
   machine_type -> objdir;
   PH.init;
   PH.betalib -> betalib[];
   
   (* generator *)
   'generator version 5.3' -> putline;
   grammarname.copy -> args[];
   ('gen', args[], boot) -> execute;
   
   (* bobsit *)
   'bobsit ' -> puttext; grammarname[]->puttext; '-parser.bobs'->putline;
   (if boot and ('metagrammar' -> grammarname.equal) then
       betalib.copy -> bobsfile[];
       'boot/grammars/metagrammar/metagrammar-parser.bobs' -> bobsfile.append;
    else
       grammarname.copy -> bobsfile[];
       '-parser.bobs' -> bobsfile.append;
   if);
   betalib.copy -> cmd[];
   (if $boot then
       'boot/' -> cmd.append;
   if);
   'bin/' -> cmd.append;
   objdir[] -> cmd.append;
   '/exbobs' -> cmd.append;
   (if debug then
       'Executing ' -> puttext;
       cmd[] -> puttext;
       ' '->put;
       args[] -> puttext;
       ' < ' -> puttext;
       bobsfile[] -> putline;
   if);
   (cmd[], args[], bobsfile[]) -> execReadTextAndPipeFileIn -> output[];
   output[] -> puttext;
   
   (* mv -f bobslist $grammarname-parser.lst *)
   grammarname.copy -> parserlst[];
   '-parser.lst' -> parserlst.append;
   'bobslist' -> entry.path;
   parserlst[] -> entry.rename;
   
   'tables' -> entry.path;
   (if (not entry.exists) or (entry.size = 0) then
       'ERRORS IN GRAMMAR' -> putline;
       'Look at ' -> puttext;
       parserlst[] -> puttext;
    else
       (* tabc *)
       'Creating tables' -> putline;
       'tables ' -> args[];
       grammarname[] -> args.append;
       '-parser ' -> args.append;
       grammarname[] -> args.append;
       ('tabc', args[], boot) -> execute;
   if);
   
   (if not boot then
       (* makepretty *)
       'makepretty version 5.3' -> putline;
       ('makepretty', grammarname.copy, false) -> execute;
       
       (* morepretty *)
       'morepretty version 5.3' -> putline;
       ('morepretty', grammarname.copy, false) -> execute;
   if);
#)
