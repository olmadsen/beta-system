ORIGIN '~beta/mps/astlevel';
INCLUDE '~beta/mps/findgrammar';
INCLUDE '~beta/mps/grammarinit';
--- program: descriptor ---
(# (* this program demonstrates how to open a fragmentfile.  If the text
    * representation of the file is the newest, the file will be parsed up
    *)
   ast: @astInterface(# defaultGrammarFinder:: findGrammar #);
   fg: ^ast.fragmentGroup;
   betagrammar: @ast.treelevel;
   fragmentName: ^text;
   exec:
     (#
     do ll:
          (fragmentName[],screen[])
	  ->ast.top.open
	      (# startingParsing::<
		   (# do 'starting parsing!'->screen.putline #);
		 parseErrors::<
		   (# do 'parse errors!'->screen.putline; leave ll #);
		 fatalParseError::<
		   (#
		   do 'fatal parse error! Error no: '->msg.puttext;
                      errNo->msg.putint;
                      msg[]->putline;
		      leave ll
		   #);
		 writeAccessError::<
		   (# do 'write access error'->screen.putline; leave ll #);
		 writeAccessOnLstFileError::<
		   (# do 'write access error on .lst file'->screen.putline; leave ll #);
		 ReadAccessError::<
		   (# do 'ReadAccessError'->putline; leave ll #);
		 EOSError::<
		   (# do 'EOSerror'->putline; leave ll #);
		 noSuchFileError::<
		   (# do 'noSuchFileError'->putline; leave ll #);
		 fileExistsError::<
		   (# do 'fileExistsError'->putline; leave ll #);
		 noSpaceLeftError::<
		   (# do 'noSpaceLeft'->putline; leave ll #);
		 otherFileError::<
		   (# do 'otherFileError'->putline; leave ll #);
		 FragmentNotExisting::<
		   (# do 'fragmentNotExisting'->putline; leave ll #);
		 grammarNotFound::<
		   (# do 'grammarNotFound'->putline; leave ll #);
		 BadFormat::<
		   (# do 'badFormat'->putline; leave ll #);
		 DoubleFormDeclaration::<
		   (# do 'doubleFormDeclaration'->putline; leave ll #);
	      #)->fg[];
        NONE->ast.theCatcher[]; (* cleanup after possible 'leave ll' *)
	fragmentName[]->puttext;
	(if fg[]=NONE then
	    ' not opened!'->putline
	 else ' opened!'->putline
	if)
     #)
do (* (ast.trace.topOpen,true)->ast.trace.set;
    * (ast.trace.fragmentOpen,true)->ast.trace.set;
    * (ast.trace.grammars,true)->ast.trace.set;
    * (ast.trace.compactOpen,true)->ast.trace.set;
    * (ast.trace.onParse,true)->ast.trace.set;
    * (ast.trace.parser,true)->ast.trace.set;
    * (ast.trace.getBinding,true)->ast.trace.set;
    * (ast.trace.getBindingMark,true)->ast.trace.set;
    *)
   ast.astLevelInit; (* initialize astlevel *)
   false->ast.doRealOpen;
   ('~beta/grammars/beta/beta','beta',screen[])
     ->betagrammar.betagrammarInit
   (* to enable beta fragment-files with SLOTs to be parsed too *)
	 (# MPSerror::
	      (# do (failure,msg[])->stop #);
	    noParserAvailable::
	      (# do (failure,msg[])->stop #)
	 #);
   (if NoOfArguments>1 then
       (for i: NoOfArguments-1 repeat
            i+1->arguments->fragmentName[];
            exec
       for)
    else
       loop:
         (#
         do 'Parse what file ? '->screen.puttext;
            keyBoard.getLine->fragmentName[]->putline;
            exec;
            restart loop
         #)
   if);
#)
