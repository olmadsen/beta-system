ORIGIN '~beta/mps/v5.2/astlevel';
INCLUDE '~beta/mps/v5.2/findgrammar';
--- program: descriptor ---
(# (* this program demonstrates how to open a fragmentfile.  If the text
    * representation of the file is the newest, the file will be parsed up
    *)
   ast: @astInterface
     (# (* such that we can find the grammars mentioned in
	 * ~beta/configuration/r<x.y>/MBSgrammars.text etc.
	 *)
	defaultGrammarFinder:: findGrammar
     #);
   fg: ^ast.fragmentGroup;
   fragmentName: ^text;
do (ast.trace.topOpen,true) -> ast.trace.set;
   (ast.trace.fragmentOpen,true) -> ast.trace.set;
   (ast.trace.grammars,true) -> ast.trace.set;
   (ast.trace.compactOpen,true) -> ast.trace.set;
   (ast.trace.onParse,true) -> ast.trace.set;
   (ast.trace.parser,true) -> ast.trace.set;
   (ast.trace.getBinding,true) -> ast.trace.set;
   (ast.trace.getBindingMark,true) -> ast.trace.set;
   ast.astLevelInit; (* initialize astlevel *)
   false->ast.doRealOpen;
   loop: (# do
	    'Parse what file ? ' -> screen.putText;
	    keyBoard.getLine -> fragmentName[];
	    (fragmentName[],screen[]) -> ast.top.open
	    (# parseErrors::<
		 (# do 'parse errors!' -> screen.putLine; restart loop #);
	       fatalParseError::<
		 (# do
		    'fatal parse error!' -> msg.putText;
		    (if errNo
		     // 1 then 'Error no: 1'->msg.putLine
		     // 2 then 'Error no: 2'->msg.putLine
		     // 3 then 'Error no: 3'->msg.putLine
		     // 4 then 'Error no: 4'->msg.putLine
		     // 5 then 'Error no: 5'->msg.putLine
		     // 6 then 'Error no: 6'->msg.putLine
		     // 7 then 'Error no: 7'->msg.putLine
		     // 8 then 'Error no: 8'->msg.putLine
		     // 9 then 'Error no: 9'->msg.putLine
		     // 10 then 'Error no: 10' ->msg.putLine
		     else
			'Error no: '->msg.puttext;
			errNo->msg.putint;
			msg.newline
		    if);
		    msg[] -> puttext;
		    restart loop
		 #);
	       writeAccessError::<
		 (# do 'write access error'->screen.putLine; restart loop #);
	       writeAccessOnLstFileError::<
		 (# do 'write access error on .lst file'->screen.putLine; restart loop #);
	       startingParsing::<
		 (# do 'starting parsing!' -> screen.putLine #);
	       ReadAccessError::<
		 (# do 'ReadAccessError'->putline;restart loop; #);
	       EOSError::<
		 (# do 'EOSerror'->putline;restart loop; #);
	       noSuchFileError::<
		 (# do 'noSuchFileError'->putline;restart loop; #);
	       fileExistsError::<
		 (# do 'fileExistsError'->putline;restart loop; #);
	       noSpaceLeftError::<
		 (# do 'noSpaceLeft'->putline;restart loop; #);
	       otherFileError::<
		 (# do 'otherFileError'->putline;restart loop; #);
	       FragmentNotExisting::<
		 (# do 'fragmentNotExisting'->putline;restart loop; #);
	       grammarNotFound::<
		 (# do 'grammarNotFound'->putline;restart loop; #);
	       BadFormat::<
		 (# do 'badFormat'->putline;restart loop; #);
	       DoubleFormDeclaration::<
		 (# do 'doubleFormDeclaration'->putline;restart loop; #);
	    #) -> fg[];
	    fragmentName[] -> putText;
	    (if fg[]=NONE then
		' not found!' -> putLine
	     else ' opened!' -> putLine
	    if);
	    restart loop
	 #)
#)
