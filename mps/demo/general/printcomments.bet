ORIGIN '~beta/mps/astlevel';
INCLUDE '~beta/mps/findgrammar';
INCLUDE '~beta/mps/private/indexPrivate';
INCLUDE '~beta/mps/grammarinit';
--- program: descriptor:beta ---
(# ast: @astInterface
     (# defaultGrammarFinder:: findGrammar #);
   betagrammar: @ast.treelevel;
   fg: ^ast.fragmentGroup;
   i: @integer;
   fragmentName: ^text;
   exec:
     (#
     do (fragmentName[]->ast.expandToFullPath,screen[])
	  ->ast.top.open(# startingParsing:: (# do '####Parsing...'->putline #) #)
	  ->fg[];
        (if fg[]=NONE then 'Not found'->screen.putText
         else
            fg.scan
	      (# ff: ^ast.fragmentForm;
		 exp: ^ast.expanded;
	      do (if current.type
		  //ast.formType then
		     current[]->ff[];
		     ff.root[]->exp[];
		     exp.suffixWalk
		       (# com: ^ast.comment; lt: ^ast.lexemtext; t:^text;
			  ue: ^ast.unexpanded; sd: ^ast.slotdesc;
		       do (if true
			   // current.hasComment then
			      current.getComment->com[];
			      com.lexeminx->screen.putint;
			      ' comment['->screen.putText;
			      i+1->i->screen.putInt;
			      '] = '->screen.putText;
			      com.getText ->screen.putLine;
			   // (current.kind=ast.kinds.unexpanded) then
			      current[]->ue[];
			      (if ue.isslot then
				  ue.theslot->sd[];
				  'SLOT: "'->screen.puttext;
                                  sd.name->screen.puttext;
                                  '"'->screen.putline;
			      if)
			   // (current.kind=ast.kinds.nameAppl)
			   // (current.kind=ast.kinds.nameDecl)
			   // (current.kind=ast.kinds.string)
			   // (current.kind=ast.kinds.comment)
			   // (current.kind=ast.kinds.const) then
			      current[]->lt[];
			      lt.lexeminx->screen.putint;
			      ' lexemtext(length='->screen.puttext;
			      lt.curlength->screen.putint; '): '->puttext;
			      lt.gettext->screen.putline;
			  if)
		       #);
		     'Scanning slots:'->putline;
		     ff.scanslots
		       (#
		       do '\t'->puttext;current.name->screen.putline;
		       #);
		 if)
	      #)
        if)
     #)
do (* (ast.trace.topOpen,true)->ast.trace.set;
    * (ast.trace.fragmentOpen,true)->ast.trace.set;
    * (ast.trace.grammars,true)->ast.trace.set;
    * (ast.trace.compactOpen,true)->ast.trace.set;
    * (ast.trace.onParse,true)->ast.trace.set;
    * (ast.trace.parser,true)->ast.trace.set;
    * (ast.trace.getBinding,true)->ast.trace.set;
    * (ast.trace.getBindingMark,true)->ast.trace.set;
    *)
   ast.astLevelInit; (* initialize astlevel *)
   ('~beta/grammars/beta/beta','beta',screen[])
     ->betagrammar.betagrammarInit
   (* to enable beta fragment-files with SLOTs to be parsed too *)
	 (# MPSerror::
	      (# do (failure,msg[])->stop #);
	    noParserAvailable::
	      (# do (failure,msg[])->stop #)
	 #);
   (if NoOfArguments>1 then
       (for i: NoOfArguments-1 repeat
            i+1->arguments->fragmentName[];
            exec
       for)
    else
       loop:
         (#
         do 'Print comments in what file ? '->screen.putText;
            keyBoard.getLine->fragmentName[]->putline;
            exec;
            restart loop
         #)
   if)
#)
