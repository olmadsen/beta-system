ORIGIN '~beta/mps/v5.2/astlevel';
INCLUDE '~beta/mps/v5.2/findgrammar';
INCLUDE '~beta/mps/v5.2/metagrammarcfl';
--- program: descriptor ---
(# ast: @astInterface;
   fg: ^ast.fragmentGroup;
   meta: @ast.metagrammar;
   i: @integer;
   fragmentName: ^text
do (*(ast.trace.topOpen,true) -> ast.trace.set;
    *(ast.trace.fragmentOpen,true) -> ast.trace.set;
    *(ast.trace.grammars,true) -> ast.trace.set;
    *(ast.trace.compactOpen,true) -> ast.trace.set;
    *(ast.trace.onParse,true) -> ast.trace.set;
    *(ast.trace.parser,true) -> ast.trace.set;
    *(ast.trace.getBinding,true) -> ast.trace.set;
    *(ast.trace.getBindingMark,true) -> ast.trace.set;
    *)
   ast.astLevelInit; (* initialize astlevel *)
   &ast.findGrammar[] -> ast.grammarMissing[];
   ast.grammarMissing.registerGrammars;
   (* so we can find the grammars mentioned in
    * ~beta/configuration/r<x.y>/MBSgrammars.text etc.
    *)
   (if NoOfArguments=2 then
       2 -> arguments -> fragmentName[]
    else
       'Print comments in what file ? ' -> screen.putText;
       keyBoard.getLine -> fragmentName[]
   if);
   (fragmentName[] -> ast.expandToFullPath,screen[])
     -> ast.top.open(# startingParsing:: (# do '#######Parsing...'->putline #) #)
     -> fg[];
   (if fg[]=NONE then 'Not found' -> screen.putText
    else
       fg.scan
       (# ff: ^ast.fragmentForm;
          exp: ^ast.expanded;
       do (if current.type
           //ast.formType then 
              current[] -> ff[]; 
              ff.root[] -> exp[];
              exp.suffixWalk
              (# com: ^ast.comment
              do (if current.hasComment then
                     current.getComment -> com[];
                     'Comment[' -> screen.putText;
                     i+1 -> i -> screen.putInt;
                     '] = ' -> screen.putText;
                     com.getText  -> screen.putLine;
          if)#)if)
       #)
   if)
#)
