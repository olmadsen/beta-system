ORIGIN 'exprcfl'
[[
--- INCLUDE 'exprsematt'
--- program: descriptor ---
(# (* This is a small demo-program of how to use the MetaProgrammingSystem.
    * The program implements a small desc-calculator a la dc in unix.  The
    * grammar for expressions is on the file expr-meta.gram.  The generated
    * context free level is on exprCfl.  Exprsematt contains semantic
    * attributes for expr.
    *)
   ast: @astinterface;
   expr: @ast.expr; (* the cfl of the grammar *)
   exprFragment: ^ast.fragmentForm;
   evalString: ^text;
   stat: ^expr.stat;
   ok: @boolean;
   btabFile: ^text;
do ast.astLevelInit; (* initialize astlevel *)
   expr.init;        (* and the context free level of the generated grammar *)
   'expr-parser' -> btabFile[];
   ast.parserFileExtension->btabFile.puttext;
   btabFile[] -> expr.parser.initialize; (* and the parser *)
   expr[] -> ast.newFragmentForm -> exprFragment[]
   (* create a fragmentform which can contain the asts *);
   cycle
   (# do
      'Eval? ' -> screen.putText;
      keyBoard.getLine -> evalString[]; (* read a string from keyboard *)
      evalString.newLine; (* add a newline to the string *)
      0 -> evalString.setPos;  (* reset evalString to start *)
      (1,evalString[],screen[],exprFragment[]) -> expr.parser -> ok;
      (* 1: goalSymbol,
       * evalString: input,
       * exprFragment: the fragmentform to contains the asts 
       *)
      (if ok 
       //false then 
          'PARSE-ERRORS' -> screen.putLine;
          0 -> evalString.setPos; (* reset evalString to start *)
          (evalString[],screen[]) -> expr.parser.ErrorReport;
       else  (* there was no parse-errors *)
          exprFragment.root[] -> stat[];
          (* the parser returns the root of the parsed ast in fragment.root *)
          stat.run; 
      if);
   #)
#)
---]]
