(************************************************************)
(** Collision patternet udregner om en given position      **)
(** kolliderer med et fast objekt. Til dette formål bruges **)
(** en kollision-map, et kort i få farver, hvor hver farve **)
(** angiver hvilken type objekt, punktet bilen gerne vil   **)
(** hen på, er. Kollisionsobjektet bruges af car-patternet **)
(** til at fortælle om der er sket en kollision og af      **)
(** control til at opdatere alle bilers positioner         **)
(************************************************************)

ORIGIN '~beta/guienv/guienv';
INCLUDE 'util';
INCLUDE 'map';
INCLUDE 'pixmapadds';

--- guienvLib: attributes ---

Collision:
(#
	theMap:^Map;                            (* En reference til kortet *)
	tiles:[MAX_NUMBER_TILES]@pixmap;        (* En repetition af kollision-tiles*)
	carRepPos:[MAX_NR_PLAYERS]@newPosition; (* En repetition af bilers positioner *)
	nrPlayers:@integer;                     (* Antal af spillere *)


	(* init : Initialisere collision-patternet og indlæser kollision-tiles i *)
	(* tiles-repetition. Kaldes fra Control ved start af nyt spil            *)
	(* Skal have en reference til det aktuelle kort og antallet af spillere  *)
	(* med i enter-delen *)
	init:
	(#
		filename:^text;
		enter (theMap[],nrPlayers)
	do	
		(for i:theMap.nrTiles repeat
			'tilec' -> fileName[];
			i -> fileName.putint;
			fileName[] -> tiles[i].Read;
		for);
	#);	
	
	(* Sætter de forskellige bilers position i car-Rep repetition *)
	(* Kaldes for hver af spillerne i control efter at bilen er   *)
	(* flyttet til den nye position                               *)
	setCarPos:
	(#
		nr:@integer;
		pos:@newPosition;
		enter (nr,pos)
	do
		pos -> carRepPos[nr]; 
	#);
	
	
	(* Hovedpatternet i collision - tjekker om en given position kollidere med *)
	(* kortet eller med en af de andre biler. Der skal angives hvilket nr bil  *)
	(* der chekkes således, at bilen ikke kolliderer med sig selv              *)
	(* returnerer farven på punktet i kollision-kortet                         *)
	isCollision:
	(#
		carPos,testPos:@newPosition;
		tileVert, tileHor,carNr:@integer;		
		c,c2:@color;
		enter (carNr,carPos)
	do
		(carPos.x div TILE_WIDTH) + 1 -> tileHor; 
		(carPos.y div TILE_HEIGHT) + 1 -> tileVert;
		(carPos.x mod TILE_WIDTH) -> testPos.x;
		(carPos.y mod TILE_HEIGHT) -> testPos.y;
		(if (tileHor>0) and (tileVert>0) then
			(testPos.x,testPos.y)->tiles[(tileHor,tileVert)->theMap.matrice].getpixelcolor->c;
		if);
		(for i:nrPlayers repeat
			(carPos.x-carRepPos[i].x)->testPos.x;
			(carPos.y-carRepPos[i].y)->testPos.y;			
			(if not(i=carNr) and (testPos.x>0) and (testPos.y>0) and (testPos.x<CAR_SIZE_WIDTH) and (testPos.y<CAR_SIZE_HEIGHT) then
				OBSTACLE->c;
			if);
		for);
		exit c
	#);
#);