(************************************************************)
(** Viewport er et pattern som indeholder en model af et   **)
(** vindue eller det udsnit af banen en bil kan se.        **)
(** Viewport bruges som sagt af car-patternet og der findes**)
(** det samme antal viewport-objekter som car-objekter     **)
(************************************************************)

ORIGIN '~beta/guienv/guienv';
INCLUDE 'util';
--- guienvLib: attributes ---

Viewport:
(#
	fromTop, fromSide:@integer; (* Afstanden bilen skal være fra kanten før der scrolles *)
	maxSize:@newPosition; (* Kortets størrelse - stopper scrollen når man kommer ud i kanten af kortet *)
	viewRect:@newRect; (* Det udsnit af kortet som vises *)
	update:@boolean; (* Er der sket ændringer i viewporten - skal billedet gentegnes *)
	
	(* Init opretter viewporten, sætter startrektanglet og størrelsen på kortet *)
	(* Kaldes fra Cars init *)
	init:
	(#
		enter (viewRect, maxSize)
	do
		viewRect.w/3 -> fromSide;
		viewRect.h/3 -> fromTop;
	#);
	
	(* checkIfUpdate: Chekker om bilen er kommet ud i kortets kant og om der derfor er behov for at *)
	(* flytte viewporten. Kaldes fra cars move *)
	
	checkIfUpdate:
	(#
		carPos,relPos:@newPosition;
		enter carPos
	do
		false->update;
		(carPos.x - viewRect.x, carPos.y - viewRect.y) -> relPos; (* Udregner den relative position *)
		(* Kommer bilen ud i venstre side *)
		(if relPos.x < fromSide then 
			viewRect.x - ((fromSide - relPos.x)) -> viewRect.x; 
			true->update; 
			(if viewRect.x<0 then 0->viewRect.x if);			
		if);
		(* Kommer bilen ud i højre side *)		
		(if relPos.x > (viewRect.w - fromSide) then 
			viewRect.x + ((relPos.x - (viewRect.w- fromSide)))-> viewRect.x; true->update; 
			(if viewRect.x>maxSize.x - viewRect.w then maxSize.x-viewRect.w->viewRect.x; if);		
		if);
		(* Kommer bilen op i toppen *)
		(if relPos.y < fromTop then
			 viewRect.y - ((fromTop - relPos.y)) -> viewRect.y; true->update; 
			(if viewRect.y<0 then 0->viewRect.y if);				 
		if);
		(* Kommer bilen ned i bunden *)		
		(if relPos.y > (viewRect.h - fromTop) then
			viewRect.y + ((relPos.y - (viewRect.h- fromTop))) -> viewRect.y; true->update; 
			(if viewRect.y>maxSize.y - viewRect.h then maxSize.y-viewRect.h->viewRect.y if);			
		if);
	#);
#);
	