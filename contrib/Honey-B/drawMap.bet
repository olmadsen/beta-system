(***************************************************************)
(** DrawMap omregner en position og rektangel via map-matricen**)
(** til et udsnit af det store kort. Bruges af draw i control **)
(***************************************************************)

ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/basiclib/math';
INCLUDE 'util';
INCLUDE 'map';
--- guienvLib: attributes ---

DrawMap:
(#
	tiles:[MAX_NUMBER_TILES]@pixmap;
	theMap:^Map;
	tilesWidth, tilesHeight:@integer;
	
	
	(* Sætter reference til map-matricen og loader tilesne ind *)
	setMap:
	(#
		filename:^text;			
		enter theMap[]
	do
		(for i:theMap.nrTiles repeat
			'tile' -> fileName[];
			i -> fileName.putint;
			fileName[] -> tiles[i].Read;
		for);				
	#);	
	
	
	(* getPart er en af de mest komplekse algoritmer i spillet. Tager en rektangel som  *)
	(* parameter og kopiere de aktuelle tiles ind i bufferen på position To. *)
	getPart:
	(#
		buffer:^pixmap;
		viewRect, copyRect:@newRect;
		progressPos,to:@newPosition;
		startTileVert, startTileHor,startX,startW,row,column:@integer;
		enter (buffer[], viewRect,to)
	do
		(viewRect.x div TILE_WIDTH) + 1 -> startTileHor->column; 
		(viewRect.y div TILE_HEIGHT) + 1 -> startTileVert->row;
		(viewRect.x mod TILE_WIDTH) -> copyRect.x->startX;
		(viewRect.y mod TILE_HEIGHT) -> copyRect.y;
		(viewRect.w,TILE_WIDTH-copyRect.x)->fmin->copyRect.w->startW;
		(viewRect.h,TILE_HEIGHT-copyRect.y)->fmin->copyRect.h;
		L:(if true then
			M:(if true then			
				(tiles[(column,row)->theMap.matrice][],(copyRect.x,copyRect.y),(to.x+progressPos.x,to.y+progressPos.y),copyRect.w, copyRect.h)->buffer.drawPixmap;
				progressPos.x+copyRect.w->progressPos.x;
				(if progressPos.x>=viewRect.w then leave M; if);
				column+1->column;
				0->copyRect.x;
				(viewRect.w-progressPos.x,TILE_WIDTH)->fmin->copyRect.w;
				restart M;					
			if);		
			progressPos.y+copyRect.h->progressPos.y;				
			(if progressPos.y>=viewRect.h then leave L; if);				
			0->progressPos.x;
			startX->copyRect.x;
			startTileHor->column;
			row+1->row;
			0->copyRect.y;
			startW->copyRect.w;
			(viewRect.h-progressPos.y,TILE_HEIGHT)->fmin->copyRect.h;	
			restart L;
		if);
	#);	
#);