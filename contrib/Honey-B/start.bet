(************************************************************)
(** Følgende pattern opretter vores startvindue, hvori der **)
(** er mulighed for at vælge navn, bil, antal spillere,    **)
(** omgange, hastighed og kort. Kaldes fra Control når et  **)
(** nyt spil oprettes. Alle informationer gemmes i         **)
(** information-patternet i slutningen af filen.           **)
(************************************************************)
ORIGIN '~beta/guienv/guienv';
INCLUDE '~beta/guienv/stddialogs'
        '~beta/guienv/graphics'
        '~beta/guienv/controls'
        '~beta/interfacebuilder/support/groupbox'
        '~beta/guienv/fields';
-- guienvLib: Attributes --
StartWindow: window
  (#
     open::<
       (# 
       do
          'Start nyt spil'->title;
          (150,150)->position;
          (655,410)->size;
          Playerfield1.open;
          Playerfield3.open;
          Playerfield2.open;
          Playerfield4.open;
          separator1.open;
          separator2.open;
          Spiller1.open;
          Spiller11.open;
          Spiller12.open;
          Spiller13.open;
          Carbutton4.open;
          Carbutton2.open;
          Carbutton1.open;
          Carbutton3.open;
          Quitbutton.open;
          Startbutton.open;
          Creditsbutton.open;
          speedbutton.open;
          Mapbutton.open;
          mapText.open;
          Carpic4.open;
          Carpic3.open;
          Carpic1.open;
          Carpic2.open;
          Lapbutton.open;
          Nrbutton1.open;
          Nrbutton2.open;
          Nrbutton3.open;
          Nrbutton4.open;
          Nrbutton1[]->RadioGroup[1][];
          Nrbutton2[]->RadioGroup[2][];
          Nrbutton3[]->RadioGroup[3][];
          Nrbutton4[]->RadioGroup[4][];
          showModal;
          INNER ;
          
       #);
     Playerfield1: @editText
       (#
          open::<
            (# 
            do 'Thomas'->contents; (100,25)->size; (85,30)->position; INNER
            #);
          
       #);
     Playerfield2: @editText
       (#
          open::<
            (# 
            do 'Peter'->contents; (105,25)->size; (455,35)->position; INNER
            #);
          
       #);
     Playerfield3: @editText
       (#
          open::<
            (# 
            do 'Pelle'->contents; (100,25)->size; (85,140)->position; INNER
            #)
       #);
     Playerfield4: @editText
       (#
          open::<
            (# 
            do
               'Schumacher'->contents;
               (100,25)->size;
               (455,140)->position;
               INNER
            #)
       #);
     separator1: @separator
       (#
          open::< (#  do (4,220)->size; (305,15)->position; INNER #);
          vertical:: (#  do true->value #);
          
       #);
     separator2: @separator
       (# open::< (#  do (615,4)->size; (20,245)->position; INNER #); 
       #);
     Spiller1: @statictext
       (#
          open::<
            (# 
            do 'Spiller 1:'->label; (60,16)->size; (25,30)->position; INNER
            #)
       #);
     Spiller11: @statictext
       (#
          open::<
            (# 
            do 'Spiller 2:'->label; (60,16)->size; (390,35)->position; INNER
            #);
          
       #);
     Spiller12: @statictext
       (#
          open::<
            (# 
            do 'Spiller 3:'->label; (60,16)->size; (25,145)->position; INNER
            #);
          
       #);
     Spiller13: @statictext
       (#
          open::<
            (# 
            do 'Spiller 4:'->label; (60,16)->size; (390,140)->position; INNER
            #)
       #);
     superCarButton: optionbutton
       (#
          carMenu: @menu
            (#
               open::
                 (# temp: ^menuItem; 
                 do
                    &menuItem[]->temp[];
                    temp.open;
                    'Honey-B'->temp.name;
                    temp[]->append;
                    &menuItem[]->temp[];
                    temp.open;
                    '2CV'->temp.name;
                    temp[]->append;
                    &menuItem[]->temp[];
                    temp.open;
                    'Corvette'->temp.name;
                    temp[]->append;
                    
                 #);
               
            #);
          open::<
            (# 
            do
               'Vælg bil:'->label;
               carMenu.open;
               carMenu[]->popupMenu;
               (150,40)->size;
               INNER
            #);
          
       #);
     Carbutton1: @superCarButton
       (#
          open::< (#  do (35,70)->position; INNER #);
          eventHandler::
            (# onCurrentItemChanged:: (#  do carPic1.update;  #);  #);
          
       #);
     Carbutton2: @superCarButton
       (#
          open::< (#  do (405,70)->position; INNER #);
          eventHandler::
            (# onCurrentItemChanged:: (#  do carPic2.update;  #);  #);
          
       #);
     Carbutton3: @superCarButton
       (#
          open::< (#  do (35,180)->position; INNER #);
          eventHandler::
            (# onCurrentItemChanged:: (#  do carPic3.update;  #);  #);
          
       #);
     Carbutton4: @superCarButton
       (#
          open::< (#  do (405,175)->position; INNER #);
          eventHandler::
            (# onCurrentItemChanged:: (#  do carPic4.update;  #);  #);
          
       #);
     Quitbutton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::<
            (# 
            do
               'quit'->thePixmap.read;
               (50,50)->size;
               (565,340)->position;
               INNER ;
               thePixmap[]->icon
            #);
          eventHandler::< (# onMouseUp::< (#  do terminate;  #);  #);
          
       #);
     Startbutton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::<
            (# 
            do
               'start'->thePixmap.read;
               (50,50)->size;
               (475,340)->position;
               INNER ;
               thePixmap[]->icon
            #);
          eventHandler::<
            (# onMouseUp::< (#  do information;  #);  #);
          
       #);
     Creditsbutton: @iconbutton
       (#
          thePixmap: @pixmap;
          open::<
            (# 
            do
               'about'->thePixmap.read;
               (40,40)->size;
               (565,260)->position;
               INNER ;
               thePixmap[]->icon
            #);
          eventHandler::<
            (#
               onMouseUp::<
                 (# theCredit: ^credit; 
                 do &credit[]->theCredit[]; theCredit.open; 
                 #);
               
            #);
          
       #);
     speedbutton: @optionbutton
       (#
          speedMenu: @menu
            (#
               open::
                 (# temp: ^menuItem; aText: @Text; 
                 do
                    (for i: 40 repeat
                      &menuItem[]->temp[];
                      temp.open;
                      aText.reset;
                      i->aText.putInt;
                      aText[]->temp.name;
                      temp[]->append;
                      
                    for);
                    
                 #);
               
            #);
          open::<
            (# 
            do
               'Hastighed:'->label;
               speedMenu.open;
               speedMenu[]->popupMenu;
               (125,40)->size;
               (50,280)->position;
               INNER
            #)
       #);
     Mapbutton: @iconbutton
       (#
          thePixmap: @pixmap;
          fileName: @text;
          open::<
            (# 
            do
               'Valley.map'->fileName;
               'map'->thePixmap.read;
               (40,40)->size;
               (240,275)->position;
               INNER ;
               thePixmap[]->icon
            #);
          eventHandler::<
            (#
               onMouseUp::<
                 (# aDialog: @FileSelectionDialog; filter,startText: @Text; 
                 do
                    'Maps|*.map|'->filter;
                    filter[]->aDialog.filter[];
                    filename->startText;
                    startText[]->aDialog.fileName[];
                    filename.reset;
                    none ->aDialog->filename.putText;
                    
                 #);
               
            #);
          
       #);
     mapText: @statictext
       (#
          open::<
            (# 
            do 'Kort:'->label; (35,25)->size; (195,285)->position; INNER
            #);
          
       #);
     SuperCarpic: canvas
       (#
          thePixmap: @pixmap;
          carButton: ^superCarButton;
          open::< (#  do (50,50)->size; INNER ;  #);
          eventHandler::
            (#
               onRefresh::
                 (# fileName: @text; 
                 do
                    'carPic'->fileName;
                    carButton.currentItem->fileName.putint;
                    fileName[]->thePixmap.read;
                    (0,0,0)->thePixmap.transparentColor;
                    Graphics
                      (# 
                      do (thePixmap[],(0,0),(0,0),50,50)->drawRaster; 
                      #);
                    
                 #);
               
            #);
          
       #);
     Carpic1: @superCarPic
       (#
          open:: (#  do (210,45)->position; carButton1[]->carButton[];  #);
          
       #);
     Carpic2: @superCarPic
       (#
          open:: (#  do (575,45)->position; carButton2[]->carButton[];  #);
          
       #);
     Carpic3: @superCarPic
       (#
          open::
            (#  do (210,155)->position; carButton3[]->carButton[];  #);
          
       #);
     Carpic4: @superCarPic
       (#
          open::
            (#  do (575,150)->position; carButton4[]->carButton[];  #);
          
       #);
     Lapbutton: @optionbutton
       (#
          lapMenu: @menu
            (#
               open::
                 (# temp: ^menuItem; aText: @Text; 
                 do
                    (for i: 5 repeat
                      &menuItem[]->temp[];
                      temp.open;
                      aText.reset;
                      i->aText.putInt;
                      aText[]->temp.name;
                      temp[]->append;
                      
                    for);
                    
                 #);
               
            #);
          open::<
            (# 
            do
               'Antal omgange:'->label;
               lapMenu.open;
               lapMenu[]->popupMenu;
               (160,40)->size;
               (305,275)->position;
               INNER
            #);
          
       #);
     radioGroup: [4] ^GroupRB;
     HandleGroup:
       (# rb: ^GroupRB; 
       enter rb[]
       do
          (for i: RadioGroup.Range repeat
            (if RadioGroup[i][] <> rb[] then false->RadioGroup[i].state;  if);
            
          for);
          
       #);
     GroupRB: RadioButton
       (#
          open::< (#  do (80,18)->size; INNER ;  #);
          eventhandler::
            (# onMouseUp:: (#  do THIS(GroupRB)[]->HandleGroup;  #); 
            #);
          
       #);
     Nrbutton1: @GroupRB
       (#
          open::<
            (# 
            do '1 spiller'->label; (60,350)->position; true->state; INNER
            #);
          
       #);
     Nrbutton2: @GroupRB
       (#
          open::<
            (#  do '2 spillere'->label; (150,350)->position; INNER #);
          
       #);
     Nrbutton3: @GroupRB
       (#
          open::<
            (#  do '3 spillere'->label; (240,350)->position; INNER #);
          
       #);
     Nrbutton4: @GroupRB
       (#
          open::<
            (#  do '4 spillere'->label; (330,350)->position; INNER #);
          
       #);
     (* Indeholder informationer om de valgte options *)
     information: @
       (#
          nrPlayers,speed,laps: @integer;
          playername: [4] ^Text;
          carType: [4] @integer;
          mapFileName: ^Text;
          
       do
          (if nrButton1.state then 1->nrPlayers;  if);
          (if nrButton2.state then 2->nrPlayers;  if);
          (if nrButton3.state then 3->nrPlayers;  if);
          (if nrButton4.state then 4->nrPlayers;  if);
          Playerfield1.Contents->playername[1][];
          Playerfield2.Contents->playername[2][];
          Playerfield3.Contents->playername[3][];
          Playerfield4.Contents->playername[4][];
          Carbutton1.currentItem-1->carType[1];
          Carbutton2.currentItem-1->carType[2];
          Carbutton3.currentItem-1->carType[3];
          Carbutton4.currentItem-1->carType[4];
          mapButton.filename[]->mapFileName[];
          Lapbutton.currentItem->laps;
          speedbutton.currentItem->speed;
          hide;
          'Loading'->putText;
          
       #);
     
  #);
(* Opretter et credit/about-vindue *)
credit: window
  (#
     open::<
       (# 
       do
       	(250,250)->position;
        'Credits'->title;
        (465,200)->size;
        statictext1.open;
        okButton.open;
        statictext2.open;
        statictext3.open;
        statictext4.open;
        statictext5.open;
        INNER ;
          
       #);
     statictext1: @statictext
       (#
          open::<
            (# 
            do
               borderStyles.shadowIn->border.style;
               true->border.visible;
               'Honey-B er produceret af Peter, Pelle og Thomas, Gruppe7, hold2'
                 ->label;
               (420,160)->size;
               (30,25)->position;
               INNER
            #);
          
       #);
     okButton: @pushbutton
       (#
          open::<
            (# 
            do 'OK'->label; (60,20)->size; (210,155)->position; INNER
            #);
          eventHandler:: (# onMouseUp:: (#  do closeWindow;  #);  #);
          
       #);
     closeWindow: (#  do close;  #);
     ;
     statictext2: @statictext
       (#
          open::<
            (# 
            do
               'En speciel tak til følgende personer:'->label;
               (225,20)->size;
               (35,45)->position;
               INNER
            #)
       #);
     statictext3: @statictext
       (#
          open::<
            (# 
            do
               'Rasmus Jerichau for animation af intro-demo'->label;
               (400,20)->size;
               (40,65)->position;
               INNER
            #)
       #);
     statictext4: @statictext
       (#
          open::<
            (# 
            do
               'Flemming Grahnung fra Mjölner support for teknisk assistance i forbindelse med implementation af lyd og grafik.'
                 ->label;
               (385,45)->size;
               (40,85)->position;
               INNER
            #)
       #);
     statictext5: @statictext
       (#
          open::<
            (# 
            do
               'og sidst men ikke mindst en stor tak til Honey-B...'->label;
               (400,20)->size;
               (40,120)->position;
               INNER
            #)
       #)
  #);
  

