ORIGIN '../pixmapaddsbody';
INCLUDE '~beta/guienv/private/winnt/guienv_ntiprivate';
--getpixelcolor:dopart--
do
   (# 
      CreateCompatibleDC:external
        (# hdcorig:@integer;
           hdccomp:@integer;
        enter hdcorig
        do callStd;
        exit hdccomp
        #);

      GetPixel:external
        (# hdc,x,y:@integer;
           colorref:@integer
        enter (hdc,x,y)
        do CallStd
        exit colorref
        #);
       
      DeleteDC:external
        (# hdc:@integer;
           result:@integer;
        enter hdc
        do CallStd;
        exit result
        #);

      extractColor:
        (# theColor: @color;
           theColorRef: @integer;
        enter theColorRef
        do 0 -> theColorRef.%getByte -> theColor.red;
           1 -> theColorRef.%getByte -> theColor.green;
           2 -> theColorRef.%getByte -> theColor.blue;
        exit theColor
        #);      
      cref:@integer
   do  0->createCompatibleDC->private.hdc;
      (private.hdc, private.handletopixmap)-> SelectObject;
      (private.hdc,x,y)->GetPixel->cref;
      cref->extractcolor->c;
      private.hdc->deleteDC;
   #);
