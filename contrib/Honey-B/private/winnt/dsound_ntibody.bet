ORIGIN '../../dsound';
INCLUDE '~beta/guienv/private/winnt/guienv_ntibody';
BUILD default '$$/dsnd.obj' 'external/dsnd.c' 'betacc $0 $1';
LINKOPT nti_ms  'winmm.lib dsound.lib'

(*
 * COPYRIGHT
 *       Copyright (C) Aarhus University
 *       All rights reserved.
 * 
 * DirectSound Intermediate Support Library.
 * Flemming Gram, gram@mjolner.dk
 * Thomas Gjoerup, thomasg@mjolner.dk
 *)
--lib:attributes--
sndinit:external
  (# hwnd:@integer;
     res:@integer
  enter hwnd
  do 'SND_Init'->callC
  exit res
  #);

sndexit:external
  (# 
  do 'SND_Exit'->callC
  #);

bufloadwavfile:external
  (# name:[0]@char;
     buffer:@integer;
  enter name
  do 'SND_Load'->callC
  exit buffer
  #);

SoundBufPlay:external
  (# buf,loop,res:@integer;
  enter (buf,loop)
  exit res
  #);

SoundBufStop:external
  (# buf:@integer;
  enter buf
  #);

SoundBufVol:external
  (# buf,vol:@integer;
  enter (buf,vol)
  #);      

SoundBufGetVol:external
  (# buf,vol:@integer;
  enter buf
  exit vol
  #);      

SoundBufIsPlaying:external
  (# buf:@integer;
     result:@boolean;
  enter buf
  exit result
  #);

SoundBufIsLooping:external
  (# buf:@integer;
     result:@boolean;
  enter buf
  exit result
  #);

SoundBufPan:external
  (# buf,pan:@integer;
  enter (buf,pan)
  #);

SoundBufGetPan:external
  (# buf,pan:@integer;
  enter buf
  exit pan
  #);

SoundBufSetPos:external
  (# buf,pos:@integer
  enter (buf,pos)
  #);

SoundBufGetPos:external
  (# buf,play,wrt:@integer
  enter (buf,play,wrt)
  #);

SoundBufSetFrq:external
  (# buf,frq:@integer
  enter (buf,frq)
  #);

SoundBufGetFrq:external
  (# buf,frq:@integer
  enter buf
  exit frq
  #);

--DirectSoundBufferLoadWavFile:dopart--
do (if dsndprivate.initialized then
       filename->bufloadwavfile->DSndBufprivate.directsoundbufferptr
    else
       (-1,'DirectSound not initialized!')->onError
   if)
   
--DirectSoundBufferPlay:dopart--
do (if looping then
       (DSndBufprivate.directsoundbufferptr,1)->SoundBufPlay;
    else
       (DSndBufprivate.directsoundbufferptr,0)->SoundBufPlay;
   if)
   
--DirectSoundBufferStop:dopart--
do DSndBufprivate.directsoundbufferptr->SoundBufStop
   
--DirectSoundBufferSetVolume:dopart--
do (DSndBufprivate.directsoundbufferptr,vol)->SoundBufVol;
   
--DirectSoundBufferGetVolume:dopart--
do DSndBufprivate.directsoundbufferptr->SoundBufGetVol->vol;

--DirectSoundBufferSetPanning:dopart--
do (if DSndBufprivate.directsoundbufferptr>0 then
       (DSndBufprivate.directsoundbufferptr,pan)->SoundBufPan
    else
       (-1,'DirectSound not initialized!')->onError
   if)
   
--DirectSoundBufferGetPanning:dopart--
do (if DSndBufprivate.directsoundbufferptr>0 then
       DSndBufprivate.directsoundbufferptr->SoundBufGetPan->pan
    else
       (-1,'DirectSound not initialized!')->onError
   if)

--DirectSoundBufferIsPlaying:dopart--
do (if DSndBufprivate.directsoundbufferptr>0 then
       DSndBufprivate.directsoundbufferptr->SoundBufIsPlaying->value
    else
       (-1,'DirectSound not initialized!')->onError
   if)
   
--DirectSoundBufferIsLooping:dopart--
do (if DSndBufprivate.directsoundbufferptr>0 then
       DSndBufprivate.directsoundbufferptr->SoundBufIsLooping->value
    else
       (-1,'DirectSound not initialized!')->onError
   if)
   
--DirectSoundBufferSetPos:dopart--
do (if DSndBufprivate.directsoundbufferptr>0 then
       (DSndBufprivate.directsoundbufferptr,newPosition)
         ->SoundBufSetPos
    else
       (-1,'DirectSound not initialized!')->onError
   if)
   
--DirectSoundBufferGetPos:dopart--
do (if DSndBufprivate.directsoundbufferptr>0 then
       (DSndBufprivate.directsoundbufferptr,@@playPosition,@@writePosition)
         ->SoundBufGetPos
    else
       (-1,'DirectSound not initialized!')->onError
   if) 
   
--DirectSoundBufferSetFrq:dopart--
do (if DSndBufprivate.directsoundbufferptr>0 then
       (DSndBufprivate.directsoundbufferptr,frq)
         ->SoundBufSetFrq
    else
       (-1,'DirectSound not initialized!')->onError
   if)
   
--DirectSoundBufferGetFrq:dopart--
do (if DSndBufprivate.directsoundbufferptr>0 then
       DSndBufprivate.directsoundbufferptr
         ->SoundBufGetFrq->frq
    else
       (-1,'DirectSound not initialized!')->onError
   if)

   
--DirectSoundBufferPrivate:descriptor--
(# 
   directsoundbufferptr:@integer;
#)

--DirectSoundInit:dopart--
do interfaceObjectID->sndinit;
   true->DSndPrivate.initialized;
   
--DirectSoundClose:dopart--
do sndexit

--DirectSoundPrivate:descriptor--
(# 
   initialized:@boolean
#)
