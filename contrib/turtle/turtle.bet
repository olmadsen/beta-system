ORIGIN '~beta/guienv/guienvsystemenv';
INCLUDE '~beta/guienv/guienv';
INCLUDE '~beta/guienv/utils/colorTable';
INCLUDE '~beta/containers/sets';
INCLUDE '~beta/utils/AgentSystem/WorldWindow';
INCLUDE '~beta/utils/AgentSystem/colors'
---lib:attributes--
Point:
  (# x,y: @integer;
     equal: BooleanValue
       (# P: ^Point
       enter P[]
       do (P.x = x) and (P.y = y) -> value
       #);
     print:
       (#
       do 'Point('->puttext; x -> putint; ','->put; y ->putint; ')'->put
       #)
  enter(x,y)
  exit(x,y)
  #);
north: (#exit 0 #);
east: (#exit 1 #);
south: (#exit 2 #);
west: (#exit 3 #);

World: guienv
  (#
     Turtle:
       (# location: @Point;
          direction: @ integer;
          pen: @
            (# col: @color;
               width: @integer;
               isDown: @boolean;
               plot:
                 (# start,end: @Point;
                    l:  @theWorldW.turtleLine
                 enter(start,end)
                 do (if isDown then
                        'down:'->puttext;
                        width -> theWorldW.pensize;
                        (none,start.x,start.y,end.x,end.y,col) -> l.open
                     else
                        'up:'->puttext;
                    if);
                    (start.x,start.y)->out; 
                    (end.x,end.y)->out; 
                    width -> putint; 
                    ' direction: ' -> puttext; direction ->  putint;
                    newline;
                 #);
               
               out:
                 (# x,y: @integer
                 enter(x,y)
                 do '(' -> put; x -> putint; ','->put; y->putint; ')'->put
                 #)
            #);
          move:
            (# spaces: @integer; turn: @integer; newLoc: @Point
            enter(spaces,turn)
            do (if direction
                // north then
                   (location.x,location.y - spaces) -> newLoc;
                // east then 
                   (location.x + spaces,location.y) -> newLoc;
                // south then
                   (location.x,location.y + spaces) -> newLoc;
                // west then
                   (location.x - spaces,location.y) -> newLoc;
                else
                   '????' -> putline
               if);
               (location,newLoc) -> pen.plot;
               newLoc -> location;
               (direction + turn) mod 4 -> direction;
            #)
       #);
     world_width: @ (# w: @integer enter w exit w #);
     world_height: @ (# h: @integer enter h exit h #);
     theWorldW: @WorldWindow;
     
  do 50 -> world_width;
     50 -> world_height;
     (world_width,world_height,7) -> theWorldW.open;
     
     inner World
  exit this(World)[]
  #)
---program:descriptor---
World
(# T: @Turtle
do 
   (40,100) -> T.location;
   east -> T.direction;
   green -> T.pen.col;
   2 -> T.pen.width;
   true -> T.pen.isDown;
   (10,1) -> T.move;
   (80,1) -> T.move;
   false -> T.pen.isDown;
   (20,1) -> T.move;
   true -> T.pen.isDown;
   red -> T.pen.col;
   (50,1) -> T.move;
   5 -> guienvwait
#)
